1000 A=200:GOTO20:REM SCHIEBEFAX
1010 DIMA$(15):RY=1
1020 GOSUB1870
1030 R$="COMPUTERMAGAZIN "
1040 FORN=0 TO15:A$(N)=MID$(R$,N+1,1):NEXTN
1050 GT=15:HG=3:VG=3:BE=1
1060 REM---teken het speelveld---
1070 A$="+---------------+"
1080 B$="!               !"
1090 HO=9:VE=4:GOSUB110:PRINTA$
1100 VE=17:GOSUB110:PRINTA$
1110 FORN=5 TO16:VE=N:GOSUB110:PRINTB$:NEXTN
1120 REM---print de letters---
1130 GOSUB1530
1140 REM---in de war maken---
1150 GOSUB1590
1160 HO=4:VE=0:GOSUB110
1170 PRINT"Bedienung mit Cursortasten"
1180 VE=1:GOSUB110
1190 PRINT"(und 2 oder 3) S = Stoppen"
1200 HO=8:VE=20:GOSUB110
1210 PRINT"Schwierigkeitsgrad ";MM
1220 VE=21:GOSUB110
1230 SR=BE:GOSUB300
1240 PRINTSR$;". Zug..."
1250 REM---de cursor---
1260 SD=10000:GOSUB450
1270 IFIN=50 THENRY=2:GOTO1260
1280 IFIN=51 THENRY=3:GOTO1260
1290 IFIN=83 THEN950
1300 Q=IN-27:IF(Q<1)OR(Q>4) THEN1260
1310 ONQ GOSUB1670,1720,1770,1820
1320 GOSUB1530:RY=1
1330 SR=BE:GOSUB300
1340 BE=BE+1:HO=8:VE=21:GOSUB110:PRINTSR$;". Zug ..."
1350 T$="":FORN=0 TO15:T$=T$+A$(N):NEXTN
1360 IFT$<>R$ THEN1260
1370 SV=7:SD=4:SP=80:GOSUB400
1380 SP=84:GOSUB400
1390 SP=87:GOSUB400
1400 SP=92:SD=12:GOSUB400
1410 HO=23:VE=16:GOSUB110:PRINT" "
1420 HO=8:VE=21:GOSUB110
1430 PRINT"Geschafft in ";BE-1;" Zuegen!"
1440 VE=23:GOSUB110:PRINT"Noch einmal? (J/N) ";
1450 GOSUB210:IFIN=78 THEN950
1460 IFIN=74 THEN1000
1470 GOTO1450
1480 REM-----------------
1490 REM
1500 REM---SUBROUTINES---
1510 REM
1520 REM---druk de getallen af---
1530 VE=3:FORJ=0 TO15 STEP4:VE=VE+3
1540 FORK=0 TO3:HO=11+4*K
1550 GOSUB110:PRINTA$(J+K):NEXTK
1560 NEXTJ
1570 RETURN
1580 REM---in de war maken---
1590 SD=20:GOSUB450
1600 FORN=1 TO10*MM
1610 GOSUB260:X=1+INT(RV*4)
1620 ONX GOSUB1670,1720,1770,1820
1630 IFN=10*INT(N/10) THEN GOSUB1530
1640 NEXTN
1650 HC=11:VC=7:RETURN
1660 REM---gat naar rechts---
1670 IFHG=3 THENRETURN
1680 HG=HG+1:A$(GT)=A$(GT+1):GT=GT+1
1690 A$(GT)=" ":RY=RY-1:IFRY>0 THEN1670
1700 RETURN
1710 REM---gat naar links---
1720 IFHG=0 THENRETURN
1730 HG=HG-1:A$(GT)=A$(GT-1):GT=GT-1
1740 A$(GT)=" ":RY=RY-1:IFRY>0 THEN1720
1750 RETURN
1760 REM---gat omhoog---
1770 IFVG=0 THENRETURN
1780 VG=VG-1:A$(GT)=A$(GT-4):GT=GT-4
1790 A$(GT)=" ":RY=RY-1:IFRY>0 THEN1770
1800 RETURN
1810 REM---gat omlaag---
1820 IFVG=3 THENRETURN
1830 VG=VG+1:A$(GT)=A$(GT+4):GT=GT+4
1840 A$(GT)=" ":RY=RY-1:IFRY>0 THEN1820
1850 RETURN
1860 REM---intro---
1870 PRINT:PRINT"SCHIEBEFAX":PRINT:PRINT
1880 PRINT:PRINT"Das weltberuehmte Spiel."
1890 PRINT:PRINT"Hier ueber die vier"
1900 PRINT:PRINT"Cursortasten."
1910 PRINT:PRINT"Moechten Sie zwei oder drei"
1920 PRINT:PRINT"Bloecke gleichzeitig verschieben,"
1930 PRINT:PRINT"dann vorher 2 oder 3 eingeben."
1940 PRINT:PRINT
1950 PRINT:PRINT"-Welchen Schwierigkeitsgrad ?"
1960 PRINT:PRINT" Waehle 1 bis 9 ";
1970 GOSUB210:MM=VAL(IN$):IF(MM<1)OR(MM>9) THEN1970
1980 GOSUB100:RETURN
32000 REM--------------------------
32010 REM Ch.W.Brederode
32020 REM NL - LISSE
32030 REM--------------------------
32040 REM TROS-RADIO dd 891115
32050 REM Erstausstrahlung Radio DDR
32060 REM-spezial 900525

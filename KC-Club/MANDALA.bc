1000 A=100:GOTO20:REM *** MANDALA.BAC ***
1010 PP=3.141592:P1=100/PP:P2=1:P3=30:P4=0
1020 PRINT"     *** MANDALA ***":PRINT
1030 PRINT:PRINT"Hinweise ab Zeile 30000"
1040 PRINT:PRINT:SR$="Taste":GOSUB150:GOSUB210:GOSUB100
1050 GOSUB600:HO=0:VE=0:CN=1:GOSUB620:CN=0
1060 HO=0.9999:VE=0:GOSUB630
1070 HO=0.9999:VE=0.9999:GOSUB630
1080 HO=0:VE=0.9999:GOSUB630
1090 HO=0:VE=0:GOSUB630
1100 FORY=0 TO98
1110 FORX=0 TO98
1120 AR=P2*((X+0.01)/P1)+P4:FK=1/TAN(AR)
1130 HH=INT(ABS(P3*FK)):CN=HH-2*INT(HH/2)
1140 XZ=X/200+0.5:YZ=Y/200+0.5
1150 HO=XZ:VE=YZ:GOSUB620
1160 HO=XZ:VE=1-YZ:GOSUB620
1170 HO=1-XZ:VE=1-YZ:GOSUB620
1180 HO=1-XZ:VE=YZ:GOSUB620
1190 NEXTX
1200 P2=P2-0.01:P3=P3-1:P4=P4+0.04
1210 NEXTY
1220 SD=20:GOSUB450:GOSUB250
1230 IFIN$="" THEN1220
1240 GOSUB210:GOSUB100:GOTO950
30000 REM 
30010 REM Das Programm zeichnet ein magisches Auge auf
30020 REM den Bildschirm.
30030 REM Variationsmoeglichkeiten ergeben sich durch
30040 REM Abaenderung der Zuweisung FK= ... in Zeile 1120.
30050 REM Man experimentiere bevorzugt mit transzendenten
30060 REM Funktionen.
30070 REM 
30080 REM Laufzeit auf KC compact ca. 27 Minuten
30090 REM 
30100 REM Das Ende der Berechnung wird durch einen Inter-
30110 REM valltan akustisch signalisiert. Durch ein-
30120 REM maliges Betaetigen einer Taste wird der Ton
30130 REM abgeschaltet, die Grafik kann in Ruhe be-
30140 REM trachtet werden. Bei nochmaliger Betaetigung
30150 REM einer Taste wird der Bildschirm geloescht
30160 REM und das Programm beendet.
30180 REM 
32000 REM *********************************************
32010 REM *                                           *
32020 REM *              MANDALA.BAC                  *
32030 REM * Erzeugung von Moireemustern aus           *
32040 REM * Funktionsscharen                          *
32050 REM *                                           *
32060 REM * Heimo Ponnath, Hamburg 1989               *
32070 REM *                                           *
32080 REM *********************************************
32090 REM *                                           *
32100 REM * entnommen aus:                            *
32110 REM * Ponnath,H.: Grafik - Leckerbissen fuer    *
32120 REM *             Feinschmecker                 *
32130 REM * in: DOS extra 12/90, Seite 135 ff.        *
32140 REM *                                           *
32150 REM *********************************************
32160 REM *                                           *
32170 REM * Anpassung an Basicode auf KC compact:     *
32180 REM *                                           *
32190 REM * Frank Berndt                              *
32200 REM * Rudolf-Hable Str. 5                       *
32210 REM * O-8106 Radeburg                           *
32220 REM *                                           *
32230 REM * 31.12.1990                                *
32240 REM *                                           *
32250 REM *********************************************
32260 REM Erstausstrahlung: Deutschlandsender Kultur
32270 REM-spezial 910123

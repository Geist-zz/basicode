1000 A=500:GOTO20:REM VIER GEWINNT !
1005 REM ** REMZEILE = ACTIONSZEILE - 5 **
1010 K=0:J=0:I=0:H1=HO:V1=VE:R=.999:CN=0
1020 DIMS(8,7),H(7)
1030 GOSUB9000:GOSUB6000:GOTO4000
1035 REM Grafik & BREAK-Verriegelung
1040 GOSUB600
1050 FR=1:GOSUB280:RETURN
1075 REM Piepton
1080 SD=.5:SV=5:SP=100:GOSUB400:RETURN
1095 REM Eingabe
1100 GOSUB200
1110 SD=300:GOSUB450:GOSUB1080:RETURN
1995 REM Sieg - Kontrolle
2000 SI=0:IFHS<7 THENRETURN
2010 M=4:N=6:FORI=1 TOM:FORJ=1 TON
2020 PB=S(I+1,J):PC=S(I+2,J):PD=S(I+3,J)
2030 GOSUB2350:NEXTJ:NEXTI:IFSI THENRETURN
2100 M=7:N=3:FORI=1 TOM:FORJ=1 TON
2110 PB=S(I,J+1):PC=S(I,J+2):PD=S(I,J+3)
2120 GOSUB2350:NEXTJ:NEXTI:IFSI THENRETURN
2200 M=4:L=3:FORI=1 TOM:FORJ=1 TON
2210 PB=S(I+1,J+1):PC=S(I+2,J+2):PD=S(I+3,J+3)
2220 GOSUB2350:NEXTJ:NEXTI:IFSI THENRETURN
2300 M=7:N=3:FORI=4 TOM:FORJ=1 TON
2310 PB=S(I-1,J+1):PC=S(I-2,J+2):PD=S(I-3,J+3)
2320 GOSUB2350:NEXTJ:NEXTI:RETURN
2350 IFS(I,J)<>P THENRETURN
2360 IF(PB=P)AND(PC=P)AND(PD=P)THENI=M:J=N:SI=P
2370 RETURN
2995 REM HC - Taktik
3000 IN=0:Z=0:C=0:SI=0:IFS(4,1)=0 THENEN=4:RETURN
3010 PK=3-P:IFHS<3 THEN3900
3015 REM PRoblem erst 3 dann 2 Steine
3020 FORPR=3 TO2 STEP-1
3030 FORPX=P TOPK STEPPK-P
3040 IFPR=3 THEN GOSUB3200
3050 IFPR=2 THEN GOSUB3100
3060 IFSI=3 THENPX=PK:PR=2
3070 NEXTPX:NEXTPR:IFSI=3 THENRETURN
3080 GOTO3900
3095 REM zwei Steine
3100 EN=C:M=6:N=4:GOSUB200:IFIN THENSI=3:RETURN
3110 FORJ=1 TOM:Y=J-1:FORI=2 TON
3120 IFS(I,J)<>PX THEN3180
3130 PB=S(I+1,J):PC=S(I+2,J):PD=S(I-1,J)
3135 REM ---- waagerecht
3140 IF(PC=PX)AND(PB=0)AND(S(I+1,Y))THENEN=I+1
3145 REM ---- Diagonale steigend
3150 IF(PB=PX)AND(PC=0)AND(S(I+2,Y))THENEN=I+2
3155 REM ---- Diagonale fallend
3160 IF(PB=PX)AND(PD=0)AND(S(I-1,Y))THENEN=I-1
3170 IFEN<>C THENSI=3:J=M:I=N
3180 NEXTI
3190 NEXTJ:RETURN
3195 REM 3 Steine ---- waagerecht
3200 GOSUB200:IFIN THENSI=3:RETURN
3210 M=6:N=4:FORJ=1 TOM:FORI=1 TON
3220 S=0:FORK=0 TO3
3230 IFS(I+K,J)=PX THENS=S+1
3240 NEXTK
3250 IFS=3 THEN GOSUB3600:GOSUB3580
3260 NEXTI:NEXTJ:IFSI=3 THENRETURN
3295 REM ---- senkrecht
3300 GOSUB200:IFIN THENSI=3:RETURN
3310 M=3:N=7:FORJ=1 TOM:FORI=1 TON
3320 S=0:FORK=0 TO3
3330 IFS(I,J+K)=PX THENS=S+1
3340 NEXTK
3350 IFS=3 THEN GOSUB3630:GOSUB3580
3360 NEXTI:NEXTJ:IFSI=3 THENRETURN
3395 REM ---- Diagonale steigend
3400 GOSUB200:IFIN THENSI=3:RETURN
3410 M=3:N=4:FORJ=1 TOM:FORI=1 TON
3420 S=0:FORK=0 TO3
3430 IFS(I+K,J+K)=PX THENS=S+1
3440 NEXTK
3450 IFS=3 THEN GOSUB3660:GOSUB3580
3460 NEXTI:NEXTJ:IFSI=3 THENRETURN
3495 REM ---- Diagonale fallend
3500 GOSUB200:IFIN THENSI=3:RETURN
3510 M=3:N=7:FORJ=1 TOM:FORI=4 TON
3520 S=0:FORK=0 TO3
3530 IFS(I-K,J+K)=PX THENS=S+1
3540 NEXTK
3550 IFS=3 THEN GOSUB3690:GOSUB3580
3560 NEXTI:NEXTJ
3570 RETURN
3580 IFSI=3 THENJ=M:I=N
3590 RETURN
3595 REM Suche freies Feld
3600 FORK=0 TO3:PA=S(I+K,J)
3610 IFPA=0 THENEN=I+K:Y=J:GOSUB3800
3620 NEXTK:RETURN
3630 FORK=0 TO3:PA=S(I,J+K)
3640 IFPA=0 THENSI=3:EN=I:K=3
3650 NEXTK:RETURN
3660 FORK=0 TO3:PA=S(I+K,J+K)
3670 IFPA=0 THENEN=I+K:Y=J+K:GOSUB3800
3680 NEXTK:RETURN
3690 FORK=0 TO3:PA=S(I-K,J+K)
3700 IFPA=0 THENEN=I-K:Y=J+K:GOSUB3800
3710 NEXTK:RETURN
3795 REM Untersuchung freies Feld
3800 PA=S(EN,Y-1):IFY<2 THEN3830
3810 PB=S(EN,Y-2)
3820 IF(PB)AND(PA=0)THENC=EN:SI=0:RETURN
3830 IFPA THENSI=3:K=3:RETURN
3840 SI=0:RETURN
3895 REM Zufallsgenerator
3900 Z=0:GOSUB200:IFIN THENSI=3:RETURN
3910 GOSUB260:Z=Z+1:EN=INT(RV*7)+1:IFH(EN)>5 THEN3910
3920 IFZ>100 THENRETURN
3930 IFEN=C THEN3910
3940 RETURN
3995 REM Graphic & Action
4000 P=2
4010 IFP=1 THENP=2:PL$=N2$:SR$=N2$+B$:H$=SR$:GOTO4030
4020 P=1:PL$=N1$:SR$=N1$+B$:H$=SR$
4030 HO=0:VE=.07:GOSUB650:GOSUB1080
4040 IFLEFT$(PL$,8)=C$ THEN GOSUB4200:GOTO4100
4050 GOSUB1100:EN=IN-48:IF(IN)AND(EN)THEN4080
4060 CN=1:GOSUB650:CN=0:SR$=PL$+E$:GOSUB650
4070 GOSUB250:GOSUB250:GOSUB3000
4080 IF(EN<1)OR(EN>7)THEN GOSUB250:GOTO4050
4090 IFH(EN)>5 THEN GOSUB250:GOTO4050
4100 H(EN)=H(EN)+1:SH=H(EN):S(EN,SH)=P:HS=HS+1
4110 GOSUB1080:GOSUB5000:GOSUB4160
4140 GOSUB2000:IF(HS>41)OR(SI)THEN8000
4150 GOTO4010
4160 HO=0:VE=.07:CN=1:GOSUB650:CN=0:RETURN
4195 REM Einmischung
4200 GOSUB3000:IFIN THEN4220
4210 GOSUB200:IFIN=0 THENRETURN
4220 IF(IN<49)OR(IN>55)THEN4200
4230 IFH(IN-48)>5 THEN4200
4240 CN=1:GOSUB650:CN=0
4250 SR$="Fremdling mischt sich ein !"
4260 GOSUB650:GOSUB250:EN=IN-48:RETURN
4995 REM Feld fuellen
5000 K1=(6-SH+.1)/6*.7+.2
5010 K2=(7-SH-.1)/6*.7+.2
5020 K3=(EN-.9)/7
5030 K4=(EN-.1)/7
5040 FORVE=K1 TOK2 STEPP*P/VG
5050 HO=K3:GOSUB620
5060 HO=K4:GOSUB630
5070 NEXTVE
5080 IFP=1 THENRETURN
5090 FORHO=K3 TOK4 STEP4/HG
5100 VE=K1:GOSUB620
5110 VE=K2:GOSUB630
5120 NEXTHO
5130 RETURN
5995 REM Titelbild
6000 GOSUB1040:G1=0:G2=0:SN=1
6010 T$=V$+V$:SR$=T$
6020 FORI=0 TO21
6030 IFCN THENCN=0:GOTO6050
6040 CN=1:GOSUB1080
6050 HO=0:VE=I/80:GOSUB650
6060 NEXTI
6070 HO=0:VE=.5:SR$="Bitte Namen eingeben !":GOSUB650
6080 VE=.6:SR$=">0< ist der "+C$+" !":GOSUB650
6090 GOSUB7000
6095 REM Spielfeld zeichnen
6100 GOSUB1040:X=0
6110 FORHO=0 TOR STEPR/7
6120 VE=.2:GOSUB620
6130 VE=.9:GOSUB630
6140 NEXTHO
6200 FORVE=.2 TO.901 STEP.7/6
6210 HO=0:GOSUB620
6220 HO=R:GOSUB630
6230 NEXTVE
6240 VE=.15
6250 FORI=1 TO7
6260 SR$=CHR$(48+I)
6270 HO=I*R/7-.07:GOSUB650
6280 NEXTI
6290 HO=0:VE=0:SR$=T$:GOSUB650
6300 VE=.05:HO=0:GOSUB620
6310 HO=R:GOSUB630
6400 FORVE=.92 TO.96 STEP1/VG
6410 HO=0:GOSUB620
6420 HO=.05:GOSUB630
6430 NEXTVE
6440 HO=.1:VE=.92:SR$=N1$:GOSUB650
6450 FORVE=.92 TO.96 STEP4/VG
6460 HO=.5:GOSUB620
6470 HO=.54:GOSUB630
6480 NEXTVE
6490 FORHO=.5 TO.54 STEP4/HG
6500 VE=.92:GOSUB620
6510 VE=.96:GOSUB630
6520 NEXTHO
6530 HO=.6:VE=.92:SR$=N2$:GOSUB650
6540 GOSUB270:GOSUB1080:RETURN
6995 REM Eingabe der Namen
7000 GOSUB200
7010 PL$="":P=1:GOSUB7300:GOSUB250
7020 GOSUB7100:N1$=PL$
7030 PL$="":P=2:GOSUB7300:GOSUB250
7040 GOSUB7100:SD=30:GOSUB450:N2$=PL$
7050 IFN1$<>N2$ THENRETURN
7060 N1$=N1$+" 1":N2$=N2$+" 2":RETURN
7100 GOSUB1100:K=LEN(PL$):IFIN=48 THENIN=0
7110 IF(IN>64)AND(IN<91)AND(K<10)THEN7230
7120 IFIN THEN7140
7130 CN=1:GOSUB7300:CN=0:PL$=C$:GOSUB7300:GOSUB7400:RETURN
7140 IF(IN<>127)AND(IN<>28)THEN7180
7150 IFK<1 THEN7100
7160 CN=1:GOSUB7300:CN=0
7170 PL$=LEFT$(PL$,K-1):GOSUB7300:GOTO7100
7180 IF(IN<>32)AND(IN<>13)THEN7210
7190 IFK<2 THEN7100
7200 GOTO7240
7210 IF(IN>64)AND(IN<91)AND(K<10)THEN7230
7220 GOTO7100
7230 PL$=PL$+CHR$(IN):GOSUB7300:GOTO7100
7240 GOSUB7400
7250 H$="":K=LEN(PL$)
7270 FORI=2 TOK:H$=H$+CHR$(ASC(MID$(PL$,I,1))+32):NEXTI
7280 PL$=LEFT$(PL$,1)+H$:RETURN
7300 HO=0:VE=.7+P/10:SR=P:GOSUB300
7310 SR$=P$+SR$+" ? "+PL$:GOSUB650:RETURN
7400 SD=2:FORI=0 TO5:SV=I+10:SP=P*I*3+60:GOSUB400:NEXTI
7410 RETURN
7995 REM Gewinnauswertung
8000 GOSUB4160:CN=1:HO=0:VE=.05:GOSUB620:HO=R:GOSUB630
8010 SR$="Unentschieden !!!"
8020 IFSI=1 THENSR$=N1$+G$:G1=G1+1
8030 IFSI=2 THENSR$=N2$+G$:G2=G2+1
8040 CN=0:HO=0:GOSUB650:H$="52596468645952":SD=5:SV=15
8050 FORI=1 TO13 STEP2:SP=VAL(MID$(H$,I,2)):GOSUB400:NEXTI
8060 CN=1:VE=0:SR$=T$:GOSUB650:GOSUB8520
8070 CN=0:SR$=T$:GOSUB650:CN=1:GOSUB6400
8080 GOSUB11000:CN=0:HO=0:VE=.1:SR$=R$:GOSUB650:J=0
8090 IF(LEFT$(N1$,8)=C$)AND(LEFT$(N2$,8)=C$)THENJ=1
8100 HO=0:VE=R:SR$=BI$:GOSUB650:SR$=">>":HO=1-2/V1
8110 GOSUB200:FORI=0 TO50:FORCN=0 TO1
8120 GOSUB650:SD=3:GOSUB450:IFIN THENCN=1:I=50
8130 NEXTCN:NEXTI
8140 CN=0:IFIN THEN8170
8150 IFJ=1 THEN8500
8160 N2$=C$+" 1":N1$=C$+" 2":G1=0:G2=0:SN=0:GOTO8500
8170 IF(IN=82)AND(J=0)THEN8500
8190 IFIN<>69 THEN GOSUB6000:GOTO4000
8200 GOTO10000
8495 REM Revanche !!!
8500 H$=N1$:N1$=N2$:N2$=H$:I=G1:G1=G2:G2=I
8510 SN=SN+1:GOSUB8520:GOSUB6100:GOTO4000
8520 SR=SN:GOSUB300:T1$=SR$
8530 SR=G1:GOSUB300:T2$=SR$
8540 SR=G2:GOSUB300:T3$=SR$
8550 T$=V$+"Nr. "+T1$+"  "+T2$+" : "+T3$:RETURN
8995 REM Spielregeln & Strings & START
9000 GOSUB1050:C$="Computer":B$=" ist dran !"
9010 V$=" VIER GEWINNT ! ":E$=" hilft der "+C$+" !!"
9020 BI$="Bitte Taste druecken...! ":P$="Spieler "
9030 R$=">R< = Revanche  >E< = ENDE":G$=" hat gewonnen !"
9050 HO=H1/2-10:VE=1::SR$=V$:GOSUB110:GOSUB150
9060 PRINT:PRINT:PRINT:PRINT
9070 PRINT"Sind die Spielregeln":PRINT"bekannt  (J/N) ? "
9080 PRINT:PRINTBI$;:GOSUB1080:GOSUB11000:GOSUB1110
9090 IF(IN<>78)AND(IN)THENRETURN
9100 GOSUB100:PRINT
9110 SR$=V$:GOSUB150
9120 PRINT:PRINT
9130 PRINT"Ziel des Spiels ist es,"
9140 PRINT" vier Steine in einer Reihe"
9150 PRINT" zu haben."
9160 PRINT"Diese koennen :"
9170 PRINT" - waagerecht nebeneinander"
9180 PRINT" - senkrecht uebereinander"
9190 PRINT" - diagonal steigend"
9200 PRINT" - diagonal fallend"
9210 PRINT" in einer Reihe liegen."
9220 PRINT"Wenn eine >0< gedrueckt wird,"
9230 PRINT" spielt der ";C$;"."
9240 PRINT"Wird keine Taste gedrueckt,"
9250 PRINT" greift nach ca. 30 s"
9260 PRINT" der ";C$;" ein."
9300 PRINT:PRINTBI$;:GOSUB250
9310 GOSUB1100:IFIN THENRETURN
9320 PRINT:PRINT"ALLES KLAR ? (J/N) ";
9330 GOSUB250:GOSUB1100:IFIN=78 THEN9100
9340 RETURN
9995 REM Abspann
10000 GOSUB1040:M=1/V1:N=1/H1:V3=.5-M-.1:V4=.5+M+.1:I=.999
10010 HO=0:VE=V3:GOSUB620:HO=I:GOSUB630
10020 HO=0:VE=V4:GOSUB620:HO=I:GOSUB630
10030 HO=0:VE=V3:GOSUB620:VE=V4:GOSUB630
10040 HO=I:VE=V3:GOSUB620:VE=V4:GOSUB630
10050 SR$="Auf Wiedersehen !!":VE=.47:HO=.5-9*N:GOSUB650
10060 FORJ=0 TO1:FORI=V3+.03 TOV4-.03 STEP1/VG
10070 VE=I:IFJ=0 THEN10100
10080 CN=0:VE=V3+.08:HO=.5-8*N:SR$=V$:GOSUB650
10090 VE=V3+.15:SR$=" by  L.M.  1991":GOSUB650:VE=1-I
10100 CN=J:HO=.02:GOSUB620:HO=.98:GOSUB630
10110 NEXTI:NEXTJ:FORI=0 TO6:GOSUB250:NEXTI
10120 SD=100:GOSUB450
10130 GOTO950:REM ENDE
10995 REM Spielfeld setzen
11000 FORJ=0 TO7:H(J)=0:S(0,J)=3:S(8,J)=3
11010 FORI=0 TO8
11020 S(I,J)=0:S(I,0)=3:S(I,7)=3
11030 NEXTI
11040 NEXTJ
11050 HS=0
11060 RETURN
32000 REM *-------------------*
32010 REM !    Lutz Mueller   !
32020 REM !   Leninallee 50   !
32030 REM !   O-2802 DOEMITZ  !
32040 REM !     24.06.1991    !
32050 REM !  auf ATARI 800 XE !
32060 REM !-------------------!
32070 REM !Wenn das Programm  !
32080 REM !gefaellt oder nicht!
32090 REM !schreiben Sie mir !!
32100 REM *-------------------*
32110 REM
32120 REM Erstausstrahlung: Deutschlandsender Kultur
32130 REM-spezial 910904

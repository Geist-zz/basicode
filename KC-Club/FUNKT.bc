1000 A=200:GOTO20:REM Z=F(X,Y) ,BC3-C
1010 DIMZ(441):CN=0
1015 IFSV<35 THENDIMCC(1):REM COMPATIBILITEIT
1020 CC(0)=0:CC(1)=6:GOSUB100:GOSUB1500
1030 GOSUB100:GOSUB1670
1040 GOSUB600:T=0
1050 REM -- PLAATS TEKST --
1060 SR$=Z$:HO=.4:VE=.95:GOSUB650
1070 REM -- TEKEN IN X-RICHTING --
1080 FORPY=0 TO20:Y=PY/3.18-FF
1090 FORPX=0 TO20:X=PX/3.18-FF
1100 GOSUB1390:GOSUB1480
1110 IFPX=0 THEN GOSUB620:GOTO1130
1120 GOSUB630
1130 NEXTPX:NEXTPY
1140 REM -- TEKEN IN Y-RICHTING --
1150 FORPX=0 TO20:X=PX/3.18-FF
1160 FORPY=0 TO20:Y=PY/3.18-FF
1170 Z=Z(PX+21*PY)
1180 GOSUB1450:IFPY=0 THEN GOSUB620:GOTO1200
1190 GOSUB630
1200 NEXTPY:NEXTPX
1210 IFFF=0 THEN1240
1220 PX=10:PY=10:Z=Z(220):GOSUB1450
1230 GOSUB620:VE=.1:GOSUB630:GOTO1260
1240 PX=0:PY=0:Z=Z(0):GOSUB1450
1250 GOSUB620:VE=.1:GOSUB630
1260 GOSUB210:GOSUB100
1270 HO=4:VE=10:GOSUB110
1280 PRINT"Nog een funktie (J/N) ? ";
1290 GOSUB210:IFIN=74 THEN1030
1300 IFIN=78 THEN950
1310 GOTO1290
1320 REM -- FORMULES --
1330 ONFU GOTO1340,1350,1360,1370
1340 Z$="z=sin(x) * sin(y)":RETURN
1350 Z$="z=cos(x) * sin(2y)":RETURN
1360 Z$="z=sin(x) + sin(y)":RETURN
1370 Z$="z=sin(2x) + cos(y)":RETURN
1380 REM -- BEREKEN Z --
1390 ONFU GOTO1400,1410,1420,1430
1400 Z=SIN(X)*SIN(Y):GOTO1450
1410 Z=COS(X)*SIN(2*Y):GOTO1450
1420 Z=SIN(X)+SIN(Y):GOTO1450
1430 Z=SIN(2*X)+COS(Y)
1440 REM -- BEREKEN COORDINATEN --
1450 HO=.23+PX/27-PY/100
1460 VE=.85-PX/80-PY/40-Z/20:RETURN
1470 REM -- BEWAAR Z IN Z() --
1480 Z(T)=Z:T=T+1:RETURN
1490 REM -- INLEIDING --
1500 HO=9:VE=1:GOSUB110
1510 SR$="Z = f(X,Y)":GOSUB150
1520 PRINT:PRINT
1530 PRINT"Dit programma maakt de ruimtelijke"
1540 PRINT"afbeelding van het vlak Z. De X-as"
1550 PRINT"wijst naar rechts , de  Y-as wijst"
1560 PRINT"naar achter  en de Z-as wijst naar"
1570 PRINT"boven.":PRINT
1580 PRINT"Er zijn twee opties:":PRINT
1590 PRINT"Het eerste kwadrant wordt getekend"
1600 PRINT"voor het domein van 0 tot 2 pi RAD"
1610 PRINT"= 0 tot 360 graden in X en Y.":PRINT
1620 PRINT"Er worden vier kwadranten getekend"
1630 PRINT"met de oorsprong in het midden, en"
1640 PRINT"180 graden in alle richtingen.":PRINT
1650 PRINT"Ieder hokje is 18 bij 18 graden.":PRINT
1660 PRINT"    Tik op een toets ";:GOSUB210:RETURN
1670 PRINT
1680 PRINT"Beschikbaar zijn de funkties:":PRINT
1690 PRINT"1.  z = sin(x) * sin(y)":PRINT
1700 PRINT"2.  z = cos(x) * sin(2y)":PRINT
1710 PRINT"3.  z = sin(x) + sin(y)":PRINT
1720 PRINT"4.  z = sin(2x) + cos(y)":PRINT
1730 PRINT"    kies een funktie : ";
1740 GOSUB210:IF(IN$<"1")OR(IN$>"4")THEN1740
1750 PRINTIN$:PRINT:FU=VAL(IN$):GOSUB1330
1760 PRINT"A.  vier kwadranten":PRINT
1770 PRINT"B.  alleen eerste kwadrant":PRINT
1780 PRINT"    kies A of B: ";
1790 GOSUB210:IF(IN<65)OR(IN>66)THEN1790
1800 FF=3.14:IFIN=66 THENFF=0
1810 PRINTZ$:PRINT
1820 PRINT"    Tik op een toets ";
1830 GOSUB210:RETURN
1840 REM---------------------------------
32000 REM  CH.W.BREDERODE
32010 REM  VON BONNINGHAUSENLAAN 33
32020 REM  2161 ES  LISSE
32030 REM
32040 REM TROS-RADIO DD 911127
32050 REM DS-Kultur
32060 REM-spezial 911211

1000 A=200:GOTO20:REM ## ENERGY COSTS ##
1010 SR$="ELECTRICAL ENERGY COST CALCULATION"
1020 GOTO3000
1030 HO=1:VE=2:GOSUB110:GOSUB150
1040 HO=0:VE=4:GOSUB110
1050 PRINT" Entering the performance parameters"
1060 PRINT"--------------------------------"
1070 PRINT
1200 INPUT" Price (DM) per kWh =";AP
1210 INPUT" fixed performance price (DM) =";FL
1220 INPUT" variable performance price (DM/kWh) =";VL
1230 INPUT" Transfer price for counters (DM) =";VP
1240 PRINT" Average-"
1250 INPUT" Maximum price (DM/kWh) =";DH
1260 INPUT" Minimum price (DM/kWh) =";DM:PRINT
1270 INPUT" Tax (VAT)(%) =";US:PRINT
1280 INPUT" Power consumption (kWh/Year) =";JV
2000 REM
2010 JK=JV*(AP+VL)
2020 GK=JK+FL
2030 DK=GK/JV:C=US*.01+1
2040 IFDK>DH THEN2400
2050 IFDK<DM THEN2600
2060 GS=(GK+VP)*C
2200 PRINT
2210 PRINT" Total cost = ";INT(GS*100+.5)/100;
2220 PRINT" DM":PRINT
2230 PRINT" Average price/kWh =";
2240 PRINTINT(GK*C*1000/JV+.5)/1000;" DM"
2250 PRINT" (with tax)"
2300 GOSUB210:IFJV=1500 THEN4100:GOTO3000
2310 GOTO3000
2400 GS=DH*JV*C+VP:PRINT
2410 PRINT" Total cost = ";INT(GS*100+.5)/100;
2420 PRINT" DM":PRINT
2430 PRINT" Average price/kWh =";
2440 PRINTDH*C;" DM":PRINT
2450 PRINT"(with tax)"
2460 GOSUB210:IFJV=1500 THEN4100:GOTO3000
2470 GOTO3000
2600 GS=DM*JV*C+VP:PRINT
2610 PRINT" Total cost = ";INT(GS*100+.5)/100;
2620 PRINT" DM":PRINT
2630 PRINT" Average price/kWh =";
2640 PRINTDM*C;" DM"
2650 PRINT"(with tax)"
2660 GOSUB210:IFJV=1500 THEN4100:GOTO3000
2680 GOTO3000
3000 GOSUB100:HO=1:VE=2:GOSUB110:GOSUB150
3010 HO=0:VE=4:GOSUB110
3020 PRINT" M E N U"
3030 PRINT" ---------"
3040 PRINT" -1- Explanation and example parameters"
3050 PRINT:PRINT" -2- Entering the parameters"
3060 PRINT:PRINT" -3- Recalculation with new param."
3070 PRINT"     Power consumption (kWh/Year)":PRINT
3080 PRINT" -4- Graphical representation":PRINT
3090 PRINT" -5- Finish calculation"
3100 GOSUB210
3110 IFIN$="1" THEN GOSUB100:GOTO6010
3120 IFIN$="2" THENPRINT:GOSUB100:GOTO1030
3130 IFIN$="3" THEN GOSUB100:GOTO1280
3140 IFIN$="4" THEN4010
3150 IFIN$="5" THEN950
3160 GOTO3000
4000 REM
4010 GOSUB100
4020 PRINT"Enter the maximum energy consumption "
4030 PRINT"(kWh/Year)":PRINT
4040 INPUTN
4050 INPUT"Enter the maximum energy costs (DM)";MK
4100 GOSUB600:CN=0
4110 HO=0:VE=.99:GOSUB620
4120 HO=.99:VE=.99:GOSUB630
4130 HO=0:VE=0:GOSUB620
4140 HO=0:VE=.99:GOSUB630
4150 FORF=1 TO3
4160 FORE=0 TO1 STEP.05
4170 HO=F/300:VE=E:GOSUB620
4180 NEXTE
4190 NEXTF
4200 FORF=1 TO4
4210 FORE=0 TO1 STEP.05
4220 HO=E:VE=.99-F/200:GOSUB620
4230 NEXTE
4240 NEXTF
4300 REM
4310 FORM=0.01 TON-100 STEPN/300
4320 JK=M*(AP+VL):GK=JK+FL:DK=GK/M:C=US*.01+1
4330 IFDK>DH THEN4600
4340 IFDK<DM THEN4800
4350 GS=(GK+VP)*C:DP=GK*C/M
4400 HO=M/N:VE=1-GS/MK:GOSUB620
4410 VE=1-DP:GOSUB620
4420 GOTO5000
4600 GS=DH*M*C+VP:DP=DH*C
4610 HO=M/N:VE=1-GS/MK:GOSUB620
4620 VE=1-DP:GOSUB620
4700 GOTO5000
4800 GS=DM*M*C+VP:DP=DM*C
4810 HO=M/N:VE=1-GS/MK:GOSUB620
4820 VE=1-DP:GOSUB620
4900 GOTO5000
5000 NEXTM:HO=0:VE=0
5010 SR=MK:GOSUB300:SR$=SR$+" DM (1.-DM/kWh)"
5020 GOSUB650
5030 HO=.8:VE=.93:SR=N:GOSUB300:SR$=SR$+" kWh"
5040 GOSUB650
5050 HO=.3:VE=.55:SR$="(Average price/kWh)"
5060 GOSUB650
5070 HO=.45:VE=.25:SR$="Energy costs/Year"
5080 GOSUB650
5090 SR$=" ELECTRICAL ENERGY COST CALCULATION"
5100 GOSUB210:GOTO3000
6000 REM ERKLAERUNG
6010 HO=4:VE=2:GOSUB110:SR$="EXPLANATION":GOSUB150
6020 HO=0:VE=4:GOSUB110
6030 PRINT"In the 5 new federal states,"
6040 PRINT"the introduction of a new energy"
6050 PRINT"cost calculation has caused some confusion."
6060 PRINT
6070 PRINT"This program enables"
6080 PRINT"the energy cost calculation for any type"
6090 PRINT"of tariff.":PRINT
6100 PRINT"An example with graph. Representation"
6110 PRINT"for a medium-sized household in"
6120 PRINT"the ESAG tariff area (Saxony)"
6130 PRINT"certainly contributes to understanding"
6140 HO=30:VE=24:GOSUB110:PRINT"KEY=>":GOSUB210
6150 GOSUB100:PRINT" Example:"
6160 PRINT" --------"
6170 PRINT" Price (DM) per kWh = 0.14 DM/kWh":PRINT
6180 PRINT" fixed performance price = 89.40 DM":PRINT
6190 PRINT" variable performance price = .02 DM/kWh"
6200 PRINT
6210 PRINT" Transfer price for counters = 31.80 DM":PRINT
6220 PRINT" Average-":PRINT
6230 PRINT" Maximum price      = .42 DM/kWh"
6240 PRINT" Minimum price      = .171 DM/kWh"
6250 PRINT:PRINT" Tax (VAT) = 14%"
6260 PRINT:PRINT" Annual consumption = 1500 kWh"
6300 AP=.14:FL=89.4:VL=.02:VP=31.8
6310 DH=.42:DM=.171:US=14:JV=1500
6320 N=2000:MK=500
6330 SR$="ELECTRICAL ENERGY COST CALCULATION"
6400 GOTO2010
7000 GOSUB210
30130 REM -----------------------------
30140 REM ENERGY COST CALCULATION
32000 REM ----------------------------
32010 REM -    (C) 03.01.91          -
32020 REM -    MANFRED STEUDNER      -
32030 REM -    HEIDESTRASSE 166      -
32040 REM -    O-8142 RADEBERG       -
32050 REM -     COMMODORE C64        -
32060 REM ----------------------------
32070 REM
32080 REM DS-KULTUR
32090 REM-spezial 911113

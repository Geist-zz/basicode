1000 A=100:GOTO20:REM # BC-SUMERIA #
1010 H1=HG:V1=VG:H2=2/H1:V2=2/V1
1020 REM -------- VORSPANN --------
1030 GOSUB600
1040 CN=0
1050 FORAA=0 TO46:H=64+AA:V=8+INT(AA/5):GOSUB10000
1060 H=64-AA:GOSUB10000:NEXTAA
1070 H=18:V=17:GOSUB10000:H=110:GOSUB10050
1080 H=20:V=18:GOSUB10000:H=31:GOSUB10050
1090 H=45:GOSUB10000:H=56:GOSUB10050
1100 H=72:GOSUB10000:H=83:GOSUB10050
1110 H=97:GOSUB10000:H=109:GOSUB10050
1120 H=21:V=19:GOSUB10000:H=30:GOSUB10050
1130 H=46:GOSUB10000:H=55:GOSUB10050
1140 H=73:GOSUB10000:H=82:GOSUB10050
1150 H=98:GOSUB10000:H=108:GOSUB10050
1160 FORH=22 TO29:V=20:GOSUB10000:V=59:GOSUB10050:NEXTH
1170 FORH=47 TO54:V=20:GOSUB10000:V=59:GOSUB10050:NEXTH
1180 FORH=74 TO81:V=20:GOSUB10000:V=59:GOSUB10050:NEXTH
1190 FORH=99 TO107:V=20:GOSUB10000:V=59:GOSUB10050:NEXTH
1200 H=21:V=60:GOSUB10000:H=30:GOSUB10050
1210 H=46:GOSUB10000:H=55:GOSUB10050
1220 H=73:GOSUB10000:H=82:GOSUB10050
1230 H=98:GOSUB10000:H=108:GOSUB10050
1240 H=20:V=61:GOSUB10000:H=31:GOSUB10050
1250 H=45:GOSUB10000:H=56:GOSUB10050
1260 H=72:GOSUB10000:H=83:GOSUB10050
1270 H=97:GOSUB10000:H=109:GOSUB10050
1280 H=18:V=62:GOSUB10000:H=110:GOSUB10050
1290 SR$="BASICODE-3":HO=.38:VE=.65:GOSUB650
1300 SR$="S  U  M  E  R  I  A":HO=.27:VE=.75:GOSUB650
1310 SR$="--> TASTE":HO=.75:VE=.95:GOSUB650
1320 SD=100:GOSUB450
1330 GOSUB100
1340 SR$="** SUMERIA **":HO=10:VE=0:GOSUB110:GOSUB150
1350 PRINT:PRINT:PRINT
1360 PRINT"Sie wurden vom Senat und den Goettern"
1370 PRINT"zum Tyrannen der Stadt fuer die Dauer"
1380 PRINT"von 10 Jahren gewaehlt.  Toll, was? Sie"
1390 PRINT"haben ein hohes Gehalt und wenig zu tun."
1400 PRINT"Nur einmal im Jahr muessen Sie einige"
1410 PRINT"Entscheidungen ueber das Leben Ihres"
1420 PRINT"Volkes treffen. Ansonsten koennen Sie"
1430 PRINT"10 Jahre lang tun und lassen, was Sie"
1440 PRINT"wollen. Doch ein Tip sei Ihnen gegeben:"
1450 PRINT:PRINT"Denken Sie immer daran, wie es Ihren"
1460 PRINT"Kollegen Caesar oder Ramses zu Beginn"
1470 PRINT"der neuen Legislaturperiode erging !"
1480 PRINT:PRINTTAB(29);"--> TASTE"
1490 SD=200:GOSUB450
1500 IFSD<>0 THEN1630
1510 GOSUB100:PRINT:PRINT:PRINT:PRINT
1520 PRINT"WOLLEN SIE DAS BISHERIGE PROGRAMM NOCH"
1530 PRINT"EINMAL ABLAUFEN LASSEN (J/N)"
1540 GOSUB200:IFIN=0 THEN1540
1550 IFIN=74 THEN1000
1600 REM -----------------------------
1610 REM --------- S P I E L ---------
1620 REM -----------------------------
1630 GOSUB100:SR$="S U M E R I A":HO=10:VE=0:GOSUB110
1640 GOSUB150
1650 D1=0:P1=0:Z=0:P=95:S=2800:H=3000:E=H-S:B=0:X=0
1660 Y=3:AA=H/Y:I=5:Q=1
1670 D=0
1680 Z=Z+1:P0=INT((P-5)*.9)
1690 IFP0<0 THENP0=0
1700 B=INT(B*1.1+.5)
1710 HO=0:VE=10:GOSUB110:PRINT"Im Jahr ";Z;" starben ";
1720 PRINTD;" Einwohner, ";I;" sind"
1730 PRINT"hinzugekommen.":P=P+I
1740 IFQ>0 THEN1780
1750 P=INT(P/2):GOSUB260:C=RV:S=INT(S/2-C*50)
1760 PRINT"Eine fuerchterliche Seuche hat die"
1770 PRINT"halbe Stadt dahingerafft.":P0=INT((P-5)*.9)
1780 PRINT"Die Einwohnerzahl ist jetzt ";P
1790 PRINT"Die Anzahl der Arbeitskraefte ist ";P0
1800 PRINT"Die Stadt besitzt an Land ";AA;" Hektar."
1810 PRINT"Die Ernte betrug ";Y;" dt/Hektar."
1820 PRINT"Die Ratten haben ";E;" dt gefressen."
1830 PRINT"Die Vorraete betragen ";S;" dt."
1840 IFB=0 THEN1860
1850 PRINT"Die Stadtschulden betragen ";B
1860 IFZ=11 THEN2950
1870 GOSUB260:C=INT(RV*10)
1880 Y=C+17
1890 PRINT"Ein Hektar kostet (dt) ";Y
1900 PRINTTAB(30);"> TASTE <":GOSUB4010
1910 GOSUB200:IFIN=0 THEN1910
1920 VE=20:GOSUB110:GOSUB4510:Q=0
1930 INPUT"Wieviel Land wollen Sie kaufen";Q
1940 IFQ<0 THEN2910
1950 IFQ*Y<S THEN1980
1960 GOSUB2800
1970 GOTO1930
1980 IFQ=0 THEN2040
1990 AA=AA+Q:S=S-Y*Q:C=0
2000 GOSUB120:V5=VE:HO=15:VE=5:GOSUB110:PRINTAA;"  "
2010 VE=6:GOSUB110:PRINTS;"  ":HO=0:VE=V5+1:GOSUB110
2020 IFS>20*P THEN2280
2030 IF(S<=20)*P THEN2140
2040 INPUT"Wieviel Land wollen Sie verkaufen";Q
2050 IFQ<0 THEN2910
2060 IFQ<AA THEN2090
2070 GOSUB2860
2080 GOTO2040
2090 AA=AA-Q:S=S+Y*Q:C=0
2100 GOSUB120:V5=VE:HO=15:VE=5:GOSUB110:PRINTAA;"  "
2110 VE=6:GOSUB110:PRINTS;"  ":HO=0:VE=V5+1:GOSUB110
2120 IFS>P*20 THEN2190
2130 X=0
2140 INPUT"Wieviel dt wollen Sie sich borgen";X
2150 B=B+X:S=S+X
2160 GOSUB120:V5=VE:HO=15:VE=8:GOSUB110:PRINTB;"   "
2170 VE=6:GOSUB110:PRINTS;"  ":HO=0:VE=V5+1:GOSUB110
2180 IFX<>0 THEN2280
2190 IFB=0 THEN2280
2200 X=0
2210 PRINT"Wieviel dt Schulden wollen Sie"
2220 INPUT"zurueckzahlen";X
2230 IFX<=S THEN2260
2240 GOSUB2800
2250 GOTO2210
2260 S=S-X:B=B-X
2270 GOSUB120:V5=VE:HO=15:VE=8:GOSUB110:PRINTB;"   "
2280 HO=15:VE=6:GOSUB110:PRINTS;"  ":HO=0:VE=V5+1:GOSUB110
2290 PRINT"Wieviel dt wollen Sie an Ihr Volk"
2300 INPUT"verteilen";Q
2310 IFQ<0 THEN2910
2320 IFQ<=S THEN2350
2330 GOSUB2800
2340 VE=12:GOTO2290
2350 S=S-Q:C=1
2360 GOSUB120:V5=VE:HO=15:VE=6:GOSUB110:PRINTS;"  "
2370 HO=0:VE=V5+1:GOSUB110
2380 INPUT"Wieviel Land wollen Sie bebauen";D
2390 IFD=0 THEN2570
2400 IFD<0 THEN2770
2410 IF(D<=AA)THEN2450
2420 GOSUB2860
2430 GOTO2380
2440 IFS=0 THEN2480
2450 IFINT(D/S)<S THEN2480
2460 GOSUB2800
2470 GOTO2380
2480 IFD<100/8.05*P0 THEN2560
2490 PRINT"Sie haben aber nur ";P0;" Arbeitskraefte."
2500 PRINT"Wuerden Sie die Freundlichkeit"
2510 PRINT"besitzen ,beim Regieren auch mal zu"
2520 PRINT"ueberlegen!"
2530 PRINTTAB(30);"> TASTE <"
2540 GOSUB200:IFIN=0 THEN2540
2550 GOTO2380
2560 S=S-INT(D/2)
2570 GOSUB2890
2580 Y=C:H=D*Y:E=0
2590 GOSUB2890
2600 IFINT(C/2)<>C/2 THEN2620
2610 E=ABS(INT(S/C))
2620 S=S-E+H
2630 GOSUB2890
2640 I=20*AA+S:I=C*I/P/100+1
2650 I=INT(I):C=INT(Q/20)
2660 GOSUB260:Q=10*RV:Q=INT(Q-.3)
2670 IFP<C THEN GOSUB4510:GOTO1670
2680 D=P-C
2690 IFD>.45*P THEN2720
2700 P1=((Z-1)*P1+D*100/P)/Z:P=C:D1=D1+D
2710 GOSUB4510:GOTO1680
2720 GOSUB4510:PRINT"Sie haben ";D;" Menschen in nur"
2730 PRINT"einem Jahr verhungern lassen !"
2740 PRINT"Auf Grund dieser extremen Misswirtschaft"
2750 PRINT"werden Sie nicht nur aus Amt und"
2760 PRINT"Wuerden gejagt, sondern auch zum"
2770 PRINT"V E R S A G E R  D E S  J A H R E S"
2780 PRINT"erklaert ."
2790 GOTO3300
2800 PRINT:PRINT"Nun denken Sie einmal richtig"
2810 PRINT"nach, Sie haben doch nur ";S;" dt Korn"
2820 PRINT"zur Verfuegung. Nun denn ...":PRINT
2830 PRINTTAB(30);"> TASTE <"
2840 GOSUB200:IFIN=0 THEN2840
2850 GOSUB4510:RETURN
2860 PRINT"Ueberlegen Sie doch, Sie haben"
2870 PRINT"nur";AA;"Hektar Land."
2880 PRINT:RETURN
2890 GOSUB260:C=INT(RV*5)+1
2900 RETURN
2910 GOSUB4510:PRINT"Das kann man mit mir nicht machen !"
2920 PRINT"Suchen Sie sich doch einen Anderen"
2930 PRINT"zum Mitspielen !"
2940 GOTO3300
2950 SD=80:GOSUB450:GOSUB4510
2960 E=0:S=S-B:S1=S-2800:AA=AA+INT(S1/21):S=2800:B=0
2970 GOSUB120:V5=VE:HO=15:VE=8:GOSUB110:PRINTB;"   "
2980 VE=5:GOSUB110:PRINTAA;"  "
2990 VE=6:GOSUB110:PRINTS;"   ":HO=0:VE=V5+1:GOSUB110
3000 PRINT"In Ihrer zehnjaehrigen Regierungszeit"
3010 P1=P1+.5
3020 PRINT"sind ";INT(P1);" % der Einwohner im Jahr"
3030 PRINT"durchschnittlich verhungert. Insgesamt"
3040 PRINT"starben ";D1;" Einwohner."
3050 L=INT(AA/P)
3060 PRINT"Zu Beginn hatten Sie 10 Hektar pro"
3070 PRINT"Einwohner, jetzt haben Sie ";L;" Hektar"
3080 PRINT"pro Einwohner.":PRINT
3090 IFP1>33 THEN2740
3100 IFL<7 THEN2740
3110 IF(P1>10)AND(L<9)THEN3180
3120 IF(P1>3)OR(L<=9)THEN3240
3130 PRINT"Eine wunderbare Regierungszeit!"
3140 PRINT"Karl, Hugo und Willi (die Grossen)"
3150 PRINT"zusammen haetten es nicht besser"
3160 PRINT"machen koennen!"
3170 GOTO3300
3180 PRINT"Ihre hartherzige Regierungsmethode"
3190 PRINT"erinnert an Nero und Iwan den"
3200 PRINT"Schrecklichen. Die (verbliebenen)"
3210 PRINT"Einwohner wuerden Sie gerne zum"
3220 PRINT"Teufel jagen!"
3230 GOTO3300
3240 GOSUB260:Q=RV:Q=INT(Q*.8*P)
3250 PRINT"Sie haetten gluecklicher agieren"
3260 PRINT"koennen, aber es war nicht uebel."
3270 PRINTQ;"Einwohner moechten Sie haengen"
3280 PRINT"sehen, aber kleine Probleme hat ja"
3290 PRINT"jeder."
3300 PRINT:PRINT"Moechten Sie noch einmal spielen?(J/N)"
3310 GOSUB210
3320 IFIN=74 THEN1630
3330 GOTO950
4000 REM --------- AUSGABE -----------
4010 HO=0:VE=2:GOSUB110
4020 PRINT"JAHR          :";Z
4030 PRINT"EINWOHNER     :";P
4040 PRINT"ARBEITSKRAEFTE:";P0
4050 PRINT"LANDBESITZ    :";AA
4060 PRINT"VORRAT        :";S
4070 PRINT"HEKTARPREIS   :";Y
4080 PRINT"STADTSCHULDEN :";B
4090 RETURN
4500 REM --SCHIRM HALB LOESCHEN-------
4510 GOSUB120:V7=VE-9:HO=0:VE=10:GOSUB110
4520 SL$="                                       "
4530 FORSL=0 TOV7:PRINTSL$:NEXTSL
4540 HO=0:VE=10:GOSUB110
4550 RETURN
10000 HO=(H*H2)+.1:VE=V*V2:GOSUB620:RETURN
10050 HO=(H*H2)+.1:VE=V*V2:GOSUB630:RETURN
10060 REM
32000 REM ****************************
32010 REM *                          *
32020 REM *      S U M E R I A       *
32030 REM *                          *
32040 REM *  Der Original-Autor ist  *
32050 REM *  nicht bekannt.          *
32060 REM *                          *
32070 REM * ------------------------ *
32080 REM *                          *
32090 REM *   Fuer BASICODE-3        *
32100 REM *   bearbeitet             *
32110 REM *                          *
32120 REM *   von MICHAEL FIEDLER    *
32130 REM *       KOETHENER STR.1    *
32140 REM *       ZOERBIG            *
32150 REM *       4415               *
32160 REM *                          *
32170 REM *   auf KC85/3             *
32180 REM *                          *
32190 REM *    am 14.07.90           *
32200 REM *                          *
32210 REM ****************************
32220 REM
32230 REM Erstausstrahlung: Deutschlandsender Kultur
32240 REM-spezial 910417

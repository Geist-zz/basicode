1000 A=100:GOTO20:REM  BURGSCHLACHT
1010 LG=VE/(VE+1)
1020 DIMLS(50):SV=10
1030 TT$="Burgschlacht"
1040 BU$="Burg"
1050 B1$="Falkenhorst"
1060 B2$="Rabenstein"
1070 PL$="Pulverladung"
1080 SW$="Schusswinkel"
1090 WT$="Weiter mit beliebigem Tastendruck ... "
1100 ST$="****************"
1110 SU$="*              *"
1500 REM 
1510 REM - HAUPTPROGRAMM -
1520 REM 
1530 GOSUB6030:REM TITEL
1540 GOSUB250:IFSR$="E" THEN950
1550 GOSUB260:AN=INT(RV+.5)
1560 P1=0:P2=0:RU=0
1570 GOSUB2030:REM BERECHNEN
1580 GOSUB2530:REM ZEICHNEN
1590 GOSUB3030:REM TEXTE
1600 CN=0:GOSUB3530:REM SPIELSTAND
1610 GOSUB4030:REM SPIEL
1620 RU=RU+1:IFRU>=20 THEN GOSUB5530:GOTO1530
1630 IFTR=1 THEN1570
1640 GOTO1600
2000 REM 
2010 REM - LANDSCHAFT BERECHNEN -
2020 REM 
2030 GOSUB600:CN=0
2040 SR$="Bitte warten, ich rechne!"
2050 HO=.17:VE=LG/2:GOSUB650
2060 GOSUB260:IF(RV<.44)OR(RV>.9) THEN2060
2070 LS(0)=RV
2080 FORI=1 TO50
2090 GOSUB260
2100 IF(I>6)AND(I<44) THENLS(I)=LS(I-1)+RV/6-1/12:GOTO2120
2110 LS(I)=LS(I-1)+RV/20-1/40
2120 IF(LS(I)<.44)OR(LS(I)>.9) THEN2090
2130 NEXTI
2140 Z1=LS(6)-.14:Z2=LS(44)-.14
2150 CN=1:GOSUB650:RETURN
2500 REM 
2510 REM - LANDSCHAFT ZEICHNEN -
2520 REM 
2530 HO=0:VE=LS(0):CN=0:GOSUB620
2540 FORI=1 TO49
2550 HO=I/50:VE=LS(I):GOSUB630
2560 NEXTI
2570 HO=.999:VE=LS(50):GOSUB630
2580 REM 
2590 REM - BURGEN ZEICHNEN -
2600 REM 
2610 RESTORE:H1=6/50:V1=LS(6)
2620 HO=H1:VE=V1:GOSUB620
2630 FORI=1 TO18
2640 READHO,VE:HO=-HO+H1:VE=VE+V1:GOSUB630
2650 NEXTI
2660 HO=4/50:VE=LS(4):GOSUB630
2670 RESTORE:H1=44/50:V1=LS(44)
2680 HO=H1:VE=V1:GOSUB620
2690 FORI=1 TO18
2700 READHO,VE:HO=HO+H1:VE=VE+V1:GOSUB630
2710 NEXTI
2720 HO=46/50:VE=LS(46):GOSUB630
2730 RETURN
3000 REM 
3010 REM - TEXTE AUSGEBEN -
3020 REM 
3030 HO=.36:VE=0:SR$=TT$:GOSUB650
3040 HO=.04:VE=.06:SR$=BU$:GOSUB650
3050 HO=.86:GOSUB650
3060 HO=.04:VE=.12:SR$=B1$:GOSUB650
3070 HO=.72:SR$=B2$:GOSUB650
3080 HO=.39:SR$="Runde: ":GOSUB650
3090 RETURN
3500 REM 
3510 REM - SPIELSTAND AUSGEBEN -
3520 REM 
3530 SR=P1:HO=.065:VE=.2
3540 GOSUB300:IFLEN(SR$)=1 THENSR$="0"+SR$
3550 GOSUB650
3560 SR=P2:HO=.885
3570 GOSUB300:IFLEN(SR$)=1 THENSR$="0"+SR$
3580 GOSUB650
3590 SR=INT(RU/2+1):HO=.56:VE=.12
3600 GOSUB300:IFLEN(SR$)=1 THENSR$="0"+SR$
3610 GOSUB650
3620 HO=.04:IFAN=1 THENHO=.86
3630 VE=.18:GOSUB620
3640 HO=HO+.094:GOSUB630
3650 VE=VE+.07:GOSUB630
3660 HO=HO-.094:GOSUB630
3670 VE=VE-.07:GOSUB630
3680 RETURN
4000 REM 
4010 REM - SPIEL -
4020 REM 
4030 TX$=PL$+": (20-100%)":MI=20:MA=100
4040 GOSUB8530:PL=WE:PX=PL
4050 TX$=SW$+": (0-70 Grad)":MI=0:MA=70
4060 GOSUB8530:SW=WE
4070 SR=PL:GOSUB300:W1$=SR$
4080 SR=SW:GOSUB300:W2$=SR$
4090 SR$=PL$+"="+W1$+"% / "+SW$+"="+W2$
4100 HO=0:VE=LG:CN=0:GOSUB650
4110 SD=10:SP=30:GOSUB400
4120 SI=SIN(SW*.017):CO=COS(SW*.017)
4130 T=0:SD=.5:CN=0
4140 IFAN=1 THEN4330
4200 REM 
4210 REM - SCHUSS --->
4220 REM 
4230 HO=6/50+PL*T*CO/250
4240 VE=Z1-(PL*T*SI-5*T*T)/180
4250 HX=INT(HO*50+.5):IFHX>50 THENHX=50
4260 IF(HO>=1)OR(VE>LS(HX)) THEN4530
4270 IF(HX>=44)AND(HX<=46)AND(VE>Z2) THEN5030
4280 GOTO4430
4300 REM 
4310 REM - SCHUSS <---
4320 REM 
4330 HO=44/50-PL*T*CO/250
4340 VE=Z2-(PL*T*SI-5*T*T)/180
4350 HX=INT(HO*50+.5):IFHX<0 THENHX=0
4360 IF(HO<0)OR(VE>LS(HX)) THEN4530
4370 IF(HX>=4)AND(HX<=6)AND(VE>Z1) THEN5030
4400 REM 
4410 REM - GESCHOSS ZEICHNEN -
4420 REM 
4430 SP=60+4*T:IFSP<100 THEN GOSUB400
4440 IFVE>=0 THEN GOSUB620
4450 PL=PL-PL/100
4460 T=T+.1:GOTO4140
4500 REM 
4510 REM - ZIEL VERFEHLT -
4520 REM 
4530 SD=10:SP=30:GOSUB400
4540 HO=0:VE=LG:CN=1:GOSUB650
4550 SR$="Der Schuss ging leider daneben!"
4560 HO=.05:CN=0:GOSUB650:SD=2
4570 FORSP=96 TO48 STEP-12:GOSUB400:NEXTSP
4580 CN=1:GOSUB650:GOSUB3530
4600 REM 
4610 REM - SCHUSS LOESCHEN -
4620 REM 
4630 PL=PX:T=0:CN=1
4640 IFAN=1 THEN4700
4650 HO=6/50+PL*T*CO/250
4660 VE=Z1-(PL*T*SI-5*T*T)/180
4670 HX=INT(HO*50+.5):IFHX>50 THENHX=50
4680 IF(HO>=1)OR(VE>LS(HX)) THEN4770
4690 GOTO4740
4700 HO=44/50-PL*T*CO/250
4710 VE=Z2-(PL*T*SI-5*T*T)/180
4720 HX=INT(HO*50+.5):IFHX<0 THENHX=0
4730 IF(HO<0)OR(VE>LS(HX)) THEN4770
4740 IFVE>=0 THEN GOSUB620
4750 PL=PL-PL/100
4760 T=T+.1:GOTO4640
4770 AN=AN+1:IFAN>1 THENAN=0
4780 TR=0:RETURN
5000 REM 
5010 REM - TREFFER -
5020 REM 
5030 SD=10:SP=50:GOSUB400
5040 HO=0:VE=LG:CN=1:GOSUB650
5050 SR$="Das war ein Volltreffer!"
5060 HO=.05:CN=0:GOSUB650:SD=2
5070 FORSP=48 TO96 STEP12:GOSUB400:NEXTSP
5080 CN=1:GOSUB650:GOSUB3530
5090 IFAN=0 THENP1=P1+1:GOTO5110
5100 P2=P2+1
5110 CN=0:GOSUB3530
5120 AN=AN+1:IFAN>1 THENAN=0
5130 HO=0:VE=LG
5140 SR$=WT$:GOSUB650
5150 GOSUB210:CN=1:GOSUB650
5160 TR=1:RETURN
5500 REM 
5510 REM - SPIELAUSWERTUNG -
5520 REM 
5530 HO=.27:VE=LG:SR$="*** Spielende ***"
5540 CN=0:GOSUB650:SD=2
5550 FORI=0 TO12 STEP4
5560 SP=I+60:GOSUB400
5570 SP=I+67:GOSUB400
5580 SP=I+62:GOSUB400
5590 NEXTI
5600 SD=10:SP=84:GOSUB400
5610 GOSUB8030
5620 PRINT:PRINT:PRINT:PRINT
5630 SR=P1:GOSUB300:P1$=SR$
5640 SR=P2:GOSUB300:P2$=SR$
5650 PRINT"Graf Siegfried erzielte ";P1$;" Treffer"
5660 PRINT"und Ritter Kunibert erzielte ";P2$
5670 PRINT"Treffer.":PRINT
5680 IFP2>P1 THEN5790
5690 IFP1=P2 THEN5880
5700 PRINT"Das Spiel endete  mit  einem  Sieg  des"
5710 PRINT"Grafen Siegfried von Falkenhorst  ueber"
5720 PRINT"Ritter Kunibert von Rabenstein."
5730 PRINT:PRINT"Burg  Rabenstein  ist  nur  noch   eine"
5740 PRINT"Ruine!"
5750 IFP2<3 THEN5930
5760 PRINT:PRINT"An Burg Falkenhorst muessen  umfangrei-"
5770 PRINT"che Reparaturen durchgefuehrt werden!"
5780 GOTO5930
5790 PRINT"Das Spiel endete  mit  einem  Sieg  von"
5800 PRINT"Ritter Kunibert  von  Rabenstein  ueber"
5810 PRINT"Graf Siegfried von Falkenhorst."
5820 PRINT:PRINT"Burg  Falkenhorst  ist  nur  noch  eine"
5830 PRINT"Ruine!"
5840 IFP1<3 THEN5930
5850 PRINT:PRINT"Burg Rabenstein bedarf einer  gruendli-"
5860 PRINT"chen Renovierung!"
5870 GOTO5930
5880 PRINT"Das Spiel endete unentschieden."
5890 IFP1<3 THEN5930
5900 PRINT:PRINT"Beide  Burgherren   sollten   zunaechst"
5910 PRINT"ihre Burgen wieder in  Ordnung  bringen"
5920 PRINT"bevor sie weiterkaempfen!"
5930 GOSUB8130:RETURN
6000 REM 
6010 REM - TITELBILD -
6020 REM 
6030 RESTORE:GOSUB600:CN=0
6040 FORI=1 TO19:READHO,VE:NEXTI
6050 GOSUB620
6060 FORI=1 TO64
6070 READHO,VE:GOSUB630
6080 NEXTI
6090 READHO,VE:GOSUB620
6100 FORI=1 TO4
6110 READHO,VE:GOSUB630
6120 NEXTI
6130 HO=.08:VE=.075:SR$=ST$:GOSUB650
6140 VE=.125:SR$=SU$:GOSUB650
6150 VE=.175:SR$="* "+TT$+" *":GOSUB650
6160 VE=.225:SR$=SU$:GOSUB650
6170 VE=.275:SR$=ST$:GOSUB650
6180 HO=.6:VE=.125
6190 SR$="S = Start":GOSUB650
6200 SR$="H = Hinweise":VE=.175:GOSUB650
6210 SR$="E = Ende":VE=.225:GOSUB650
6220 GOSUB200:SR$=IN$:GOSUB330
6230 IF(SR$="S")OR(SR$="E") THENRETURN
6240 IFSR$<>"H" THEN6220
6250 GOSUB7030:GOTO6030
7000 REM 
7010 REM - HINWEISE -
7020 REM 
7030 GOSUB250:GOSUB8030
7040 PRINT"Man schreibt das Jahr 1493."
7050 PRINT"  Der Machtkonflikt zwischen Graf Sieg-"
7060 PRINT"fried von Falkenhorst und Ritter  Kuni-"
7070 PRINT"bert von Rabenstein spitzt sich zu.  Da"
7080 PRINT"sich ihre  Besitztuemer  in  Sichtweite"
7090 PRINT"befinden und durch eine Huegelkette ge-"
7100 PRINT"trennt sind,  plant man einen  Beschuss"
7110 PRINT"der gegnerischen Burg ..."
7120 PRINT"  So aehnlich koennte es sich  im  Mit-"
7130 PRINT"telalter zugetragen haben.  Bei  diesem"
7140 PRINT"Spiel haben zwei Mitspieler  die  Moeg-"
7150 PRINT"lichkeit,  die Rollen der beiden  Burg-"
7160 PRINT"herren zu uebernehmen."
7170 PRINT"  Der  Computer  wird  eine  zufaellige"
7180 PRINT"Landschaft mit den  Burgen  der  beiden"
7190 PRINT"Kontrahenten auf den Bildschirm  zeich-"
7200 PRINT"nen.  Beide Mitspieler geben  dann  ab-"
7210 PRINT"wechselnd einen Kanonenschuss  auf  die"
7220 PRINT"gegnerische Burg ab.  Jeder Spieler hat"
7230 PRINT"dabei 10 Kanonenkugeln zur Verfuegung."
7240 GOSUB8130:GOSUB8030
7250 PRINT"  Zum  Abfeuern  eines  Kanonenschusses"
7260 PRINT"muessen 2 Werte eingegeben werden:"
7270 PRINT"- Pulverladung = die  Pulvermenge,  die"
7280 PRINT"  in die Kanone eingefuellt  wird.  Sie"
7290 PRINT"  bestimmt die Geschwindigkeit, mit der"
7300 PRINT"  die Kugel abgefeuert wird (Schusswei-"
7310 PRINT"  te)."
7320 PRINT"- Schusswinkel = der Neigungswinkel des"
7330 PRINT"  Kanonenrohres (in Grad).  Er bestimmt"
7340 PRINT"  die Schussrichtung (Hoehe)."
7350 PRINT"Nach der Eingabe der beiden Werte  wird"
7360 PRINT"die Flugbahn des Geschosses  gezeichnet"
7370 PRINT"und angezeigt,  ob die gegnerische Burg"
7380 PRINT"getroffen wurde oder nicht."
7390 PRINT"  Wer den  ersten  Schuss  abgibt,  be-"
7400 PRINT"stimmt der Computer und  ist  rein  zu-"
7410 PRINT"faellig.  Die Punktzahl  des  Spielers,"
7420 PRINT"der gerade schiesst,  ist umrandet dar-"
7430 PRINT"gestellt."
7440 GOSUB8130:GOSUB8030
7450 PRINT"  Alle  Treffer  werden  gezaehlt,  und"
7460 PRINT"Sieger ist der Burgherr mit den meisten"
7470 PRINT"Treffern.":PRINT
7480 PRINT"  Achtung:  Um das  Spiel etwas schwie-"
7490 PRINT"riger zu machen,  wird nach jedem Tref-"
7500 PRINT"fer ein neues  Landschaftsbild  berech-"
7510 PRINT"net."
7520 PRINT"  Die Landschaft kann manchmal  so  un-"
7530 PRINT"guenstig  sein,  dass   es  fuer  einen"
7540 PRINT"Spieler  fast  unmoeglich  ist,  seinen"
7550 PRINT"Gegner zu treffen.":PRINT:PRINT
7560 PRINT"Viel Spass wuenscht"
7570 PRINT:PRINTTAB(31);"S. Huehn"
7580 GOSUB8130:RETURN
8000 REM 
8010 REM - TITEL AUSGEBEN -
8020 REM 
8030 GOSUB100:HO=11:VE=0:GOSUB110
8040 SR$=TT$:GOSUB150
8050 PRINT:PRINT:RETURN
8100 REM 
8110 REM - WARTEN AUF TASTE -
8120 REM 
8130 HO=0:VE=23
8140 GOSUB110:PRINTWT$;
8150 GOSUB210:GOSUB250
8160 RETURN
8500 REM 
8510 REM - WERTEEINGABE -
8520 REM 
8530 HO=.05:VE=LG:CN=0
8540 SR$=TX$:SD=1:SP=100
8550 WE$="_":GOSUB650:HO=.8
8560 SR$=WE$:CN=0:GOSUB650:GOSUB400
8570 GOSUB210:IFIN=69 THEN950:REM E
8580 IFIN=127 THEN8660
8590 IFIN=13 THEN8710
8600 IF(IN$<"0")OR(IN$>"9") THEN8570
8610 IFLEN(WE$)=4 THEN8570
8620 CN=1:GOSUB650
8630 IFLEN(WE$)=1 THENWE$=IN$+"_":GOTO8650
8640 WE$=LEFT$(WE$,LEN(WE$)-1)+IN$+"_"
8650 GOTO8560
8660 IFLEN(WE$)=1 THEN8570
8670 CN=1:GOSUB650
8680 IFLEN(WE$)=2 THENWE$="_":GOTO8700
8690 WE$=LEFT$(WE$,LEN(WE$)-2)+"_"
8700 GOTO8560
8710 IFLEN(WE$)=1 THEN8570
8720 WE=VAL(LEFT$(WE$,LEN(WE$)-1))
8730 IF(WE<MI)OR(WE>MA) THEN8570
8740 CN=1:SR$=WE$:GOSUB650
8750 HO=.05:SR$=TX$:GOSUB650
8760 SP=88:GOSUB400:RETURN
25000 REM 
25010 REM - BURGDATEN -
25020 REM 
25030 DATA0,-.1,-.015,-.1,-.015,-.14,-.005,-.14,-.005,-.12
25040 DATA.005,-.12,.005,-.14,.015,-.14,.015,-.12,.025,-.12
25050 DATA.025,-.14,.035,-.14,.035,-.12,.045,-.12,.045,-.14
25060 DATA.055,-.14,.055,-.1,.04,-.1
25070 REM 
25080 REM - TITELBILDDATEN -
25090 REM 
25200 DATA.22,.9,.22,.55,.15,.55,.15,.45,.17,.45,.17,.5
25210 DATA.19,.5,.19,.45,.21,.45,.21,.5,.23,.5,.23,.45
25220 DATA.25,.45,.25,.5,.27,.5,.3,.35,.33,.5,.35,.5
25230 DATA.35,.45,.37,.45,.37,.5,.39,.5,.39,.45,.41,.45
25240 DATA.41,.5,.43,.5,.43,.45,.45,.45,.45,.55,.38,.55
25250 DATA.38,.9,.38,.75,.7,.75,.7,.9,.7,.65,.65,.65
25260 DATA.65,.58,.67,.58,.67,.62,.69,.62,.69,.58,.71,.58
25270 DATA.71,.62,.73,.62,.73,.58,.75,.58,.75,.62,.77,.62
25280 DATA.77,.58,.79,.58,.79,.62,.81,.62,.81,.58,.83,.58
25290 DATA.83,.65,.78,.65,.78,.9,.33,.9,.33,.8,.315,.77
25300 DATA.3,.76,.285,.77,.27,.8,.27,.9,.22,.9
25310 DATA0,0,.999,0,.999,.999,0,.999,0,0
32000 REM 
32010 REM  --------------------------
32020 REM 
32030 REM  SPIELPROGRAMM BURGSCHLACHT
32040 REM 
32050 REM  GESCHRIEBEN: 09/89 - 10/89
32060 REM 
32070 REM  VON: SCOTT-FALK HUEHN
32080 REM       ERICH-HEYL-STR. 4
32090 REM       SOEMMERDA, 5230, DDR
32100 REM 
32110 REM  AUF: ZX-SPECTRUM + 128K
32120 REM 
32130 REM  --------------------------
32140 REM 
32150 REM Erstausstrahlung: Radio DDR / TROS (NL)
32160 REM 900502

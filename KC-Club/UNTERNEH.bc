1000 A=100:GOTO20:REM * UNTERNEHMER *
1010 GOSUB2470:REM VARIABLE
1020 WO=WO+1
1030 GOSUB1830:REM AUSGABE
1040 GOSUB2200:REM MITARBEITER
1050 GOSUB1830:REM AUSGABE
1060 GOSUB2050:REM PRODUKTION
1070 GOSUB1830:REM AUSGABE
1080 GOSUB1660:REM VERKAUF
1090 GOSUB1150:REM ZUFAELLE
1100 KA=KA-LO*AZ
1110 GOTO1020
1120 REM
1130 REM Zufaelle
1140 REM
1150 GOSUB260:IFRV<0.45 THEN1260
1160 PRINT:PRINT"   "
1170 GOSUB260:AA=INT(RV*7)+1
1180 PRINT
1190 PRINT"Gewerkschaftsforderung:"
1200 PRINTAA;"% Lohnsteigerung"
1210 LO=INT(100*LO*(1+AA/100))/100
1220 SD=17:GOSUB450
1230 PRINT"Der Arbeitslohn betraegt nun DM ";LO
1240 PRINT:PRINT
1250 SD=40:GOSUB450
1260 GOSUB260:IFRV<0.85 THEN1400
1270 PRINT"Eine Sturmflut vernichtete Teile ihres"
1280 PRINT"Lagers !!!"
1290 PRINT:PRINT"Lagerbericht:"
1300 PRINT"-------------"
1310 SD=6:GOSUB450
1320 GOSUB260:BB=INT(RV*LA/2)+1
1330 LA=LA-BB
1340 PRINT
1350 PRINT"Zerstoerte Bestaende:"
1360 PRINTBB;" Zibbies im Wert von DM ";BB*VP
1370 SD=50:GOSUB450
1380 PRINT"Der Aktuelle Lagerbestand ist nun ";LA
1390 SD=50:GOSUB450
1400 GOSUB260:IFRV>0.3 THEN1620
1410 PRINT"Der Lieferant meldet Preissteigerungen"
1420 GOSUB260:CC=INT(RV*100*KO/7)/100
1430 IFCC<0.01 THEN1420
1440 PRINT"Produktionskosten steigen um DM ";CC
1450 PRINT"Pro Stueck !"
1460 SD=50:GOSUB450
1470 KO=KO+CC
1480 PRINT"Zibbies kosten jetzt DM ";KO;" in der"
1490 PRINT"Herstellung.":SD=50:GOSUB450
1500 GOSUB260:IFRV<0.5 THENRETURN
1510 PRINT"Sie koennen jetzt eine Preis-Erhoehung"
1520 PRINT"vornehmen. Zibbies kosten jetzt "
1530 PRINT"Pro Stueck DM ";VP
1540 GOSUB260:YY=INT(RV*10)*KO/CC
1550 INPUT"Wieviel % Teuerungsrate ";D
1560 IFD>YY THENPRINT"Der Markt laesst nur ";YY;"% zu!":D=YY
1570 IFD>0 THENZ=Z+AA
1580 QQ=VP*100
1590 VP=(QQ+D*VP)/100
1600 PRINT"Zibbies kosten jetzt DM ";VP
1610 SD=66:GOSUB450
1620 RETURN
1630 REM
1640 REM Verkauf
1650 REM
1660 PRINT"GESAMTLAGERBESTAND ";LA
1670 SD=20:GOSUB450
1680 GOSUB100:PRINT:PRINT
1690 PRINTTAB(3);"Es folgt der Verkaufsbericht..."
1700 PRINTTAB(3);"-------------------------------"
1710 PRINT:PRINT
1720 GOSUB260:E=INT(RV*100*LA/Z)+1
1730 IFE>LA THEN1720
1740 PRINT" Gesamtverkauf: ";E;" Stueck"
1750 LA=LA-E
1760 PRINT" Verkaufsvolumen = DM ";E*VP
1770 KA=KA+E*VP
1780 SD=66:GOSUB450
1790 PRINT:RETURN
1800 REM
1810 REM Ausgabe
1820 REM
1830 GOSUB100
1840 IFKA+LA<1 THEN2610:REM KONKURS
1850 IFKA>999999 THEN2780
1860 PRINT"Geschaeftsbericht: WOCHE ";WO
1870 PRINT"--------------------------"
1880 PRINT:PRINT
1890 PRINT"Liquides Kapital ist DM ";KA
1900 PRINT
1910 PRINT"Ihr Lager enthaelt ";LA;" Zibbies"
1920 PRINT
1930 PRINT"Der Verkaufspreis pro Stueck DM ";VP
1940 PRINT"Die Herstellungskosten betragen DM ";KO
1950 PRINT
1960 PRINT"Anzahl der Mitarbeiter ist ";AZ
1970 PRINT
1980 PRINT"Der Lohn betraegt DM ";LO
1990 PRINT"Die Lohnsumme in dieser Woche DM ";LO*AZ
2000 PRINT
2010 PRINT"Produktion pro Person ";PR;" Stueck"
2020 PRINT"Gesamtproduktion pro Woche ";PR*AZ;" Stueck"
2030 PRINT
2040 RETURN
2050 INPUT"Wieviel wollen Sie herstellen ";XX
2060 IFXX=0 THENRETURN
2070 IFXX*KO>KA THENPRINT"NICHT GENUG GELD !":GOTO2050
2080 IFXX>PR*AZ THENPRINT"ZU WENIG MITARBEITER":GOTO2050
2090 PRINT"Ziel fuer Woche ";WO;" ist ";XX
2100 GOSUB260:XX=XX-INT(RV*XX*Z/500)
2110 SD=10:GOSUB450
2120 PRINT"Gesamtproduktion der Woche ";WO;" war ";XX
2130 LA=LA+XX
2140 KA=KA-KO*XX
2150 SD=10:GOSUB450
2160 RETURN
2170 REM
2180 REM Mitarbeiter
2190 REM
2200 PRINT"Wieviel Mitarbeiter sollen eingestellt"
2210 INPUT"werden ";AA
2220 AZ=AZ+AA
2230 PRINT"Die Zahl der Mitarbeiter ist ";AZ
2240 SD=10:GOSUB450
2250 IFAA>0 THENRETURN
2260 GOSUB1830
2270 PRINT"Wieviel Mitarbeiter sollen entlassen"
2280 INPUT"werden ";AA
2290 IFAA=0 THEN2350
2300 IFAA>AZ THEN2270
2310 GOSUB260:AA=INT(RV*AA+1)
2320 SD=10:GOSUB450
2330 PRINT"Es duerfen entlassen werden ";AA
2340 AZ=AZ-AA
2350 SD=10:GOSUB450
2360 RETURN
2370 PRINT" 'J' Eingeben fuer einen neuen Versuch"
2380 PRINT
2390 PRINT" oder 'N' fuer beenden"
2400 GOSUB200
2410 IFIN=74 THEN1000
2420 IFIN=78 THEN950
2430 GOTO2400
2440 REM
2450 REM VARIABLE
2460 REM
2470 GOSUB260:KA=500+INT(RV*50)
2480 GOSUB260:LA=100+INT(RV*50)
2490 GOSUB260:VP=10+INT(RV*5)
2500 GOSUB260:KO=2+INT(RV*5)
2510 IFKO>VP THEN2500
2520 GOSUB260:AZ=7+INT(RV*10)
2530 GOSUB260:LO=12+INT(RV*VP*5)
2540 GOSUB260:PR=5+INT(RV*6)
2550 WO=0
2560 Z=1
2570 RETURN
2580 REM
2590 REM ENDE-Konkurs
2600 REM
2610 GOSUB100:PRINT
2620 PRINT" **************************************"
2630 PRINT" *                                    *"
2640 PRINT" *                                    *"
2650 PRINT" *        K O N K U R S  ! ! !        *"
2660 PRINT" *                                    *"
2670 PRINT" *                                    *"
2680 PRINT" **************************************"
2690 PRINT:PRINT:PRINT
2700 PRINTTAB(6);"So ein Pech - ":PRINT
2710 PRINTTAB(6);"aber Sie haben ";WO-1;" Wochen als"
2720 PRINT:PRINTTAB(6);"Unternehmer geschafft."
2730 FORII=1 TO5:PRINT:NEXTII
2740 GOTO2370
2750 REM
2760 REM ENDE-Millionaer
2770 REM
2780 GOSUB100:PRINT
2790 PRINT" **************************************"
2800 PRINT" *                                    *"
2810 PRINT" *                                    *"
2820 PRINT" *       Sie sind Millionaer !!!      *"
2830 PRINT" *                                    *"
2840 PRINT" *                                    *"
2850 PRINT" **************************************"
2860 PRINT:PRINT:PRINT
2870 PRINTTAB(6);"Herzlichen Glueckwunsch -"
2880 PRINT
2890 PRINTTAB(6);"Sie haben es in ";WO-1;" Wochen"
2900 PRINT
2910 PRINTTAB(6);"geschafft."
2920 FORII=1 TO5:PRINT:NEXTII
2930 GOTO2370
2940 REM
30000 REM ****************************
30010 REM *                          *
30020 REM *   UNTERNEHMERSPIEL VON   *
30030 REM *   T.HARTNELL             *
30040 REM *   /ZX-SPEKTRUM           *
30050 REM *                          *
32000 REM ****************************
32010 REM *                          *
32020 REM *   FUER BASICODE-3        *
32030 REM *   BEARBEITET             *
32040 REM *                          *
32050 REM *   von MICHAEL FIEDLER    *
32060 REM *       KOETHENERSTR.1     *
32070 REM *       ZOERBIG            *
32080 REM *       4415               *
32090 REM *                          *
32100 REM *   auf KC85/3             *
32110 REM *                          *
32120 REM *    am 22.12.89           *
32130 REM *                          *
32140 REM ****************************
32150 REM Erstausstrahlung: Radio DDR
32160 REM-spezial 900525

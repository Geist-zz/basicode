1000 A=1000:GOTO20:REM ### VIC ###
1010 REM INITIALISIERUNG+TITEL
1020 HT=HO:VT=VE:L6=0:D6=0:FF=0:F=0:TA=0:R=0:WA=0:WT=0
1030 VE=3:HO=INT((HT-19)/2):GOSUB110
1040 PRINT"  VIC - INTERVIEW":VE=4:GOSUB110
1050 PRINT"-------------------"
1060 VE=12:HO=INT((HT-30)/2):GOSUB110
1070 PRINT"ICH MUSS MICH MAL KURZ KONZEN-"
1080 VE=14:GOSUB110:PRINT"TRIEREN."
1090 EA=50:FA=50:GA=3
1100 REM DATA'S ZAEHLEN
1110 GOSUB2780
1120 DIMB(200),E$(EA),F$(FA),S$(SA)
1130 DIMS(SA,GA),T$(TK+TN),T2$(TK)
1140 DIMTV$(TV),TF$(TF),Z(10)
1150 GOSUB2660:F$="HALLO, ICH HEISSE VIC ! WIE HEISST DU ?"
1160 GOSUB100
1170 FORVE=3 TO9:HO=3:GOSUB110:PRINT"*":NEXTVE
1180 FORHO=3 TOHT-3:VE=9:GOSUB110:PRINT"*":NEXTHO
1190 FORVE=9 TO3 STEP-1:HO=HT-3:GOSUB110:PRINT"*":NEXTVE
1200 FORHO=HT-3 TO3 STEP-1:VE=3:GOSUB110:PRINT"*":NEXTHO
1210 FORVE=VT-9 TOVT-3:HO=3:GOSUB110:PRINT"*":NEXTVE
1220 FORHO=3 TOHT-3:VE=VT-3:GOSUB110:PRINT"*":NEXTHO
1230 FORVE=VT-3 TOVT-9 STEP-1:HO=HT-3:GOSUB110:PRINT"*"
1240 NEXTVE
1250 FORHO=HT-3 TO3 STEP-1:VE=VT-9:GOSUB110:PRINT"*":NEXTHO
1260 FORVE=1 TOVT-1:HO=1:GOSUB110:PRINT"#":NEXTVE
1270 FORHO=2 TOHT-1:VE=VT-1:GOSUB110:PRINT"#":NEXTHO
1280 FORVE=VT-1 TO1 STEP-1:HO=HT-1:GOSUB110:PRINT"#":NEXTVE
1290 FORHO=HT-1 TO1 STEP-1:VE=1:GOSUB110:PRINT"#":NEXTHO
1300 VE=3:HO=INT((HT-23)/2):GOSUB110
1310 PRINT"*** VIC - INTERVIEW ***"
1320 GOSUB2440
1330 GOSUB1690
1340 N$=E$(W)
1350 F$="ERZAEHL MIR ETWAS VON DIR UND DEINER FAMILIE, "+N$
1360 GOTO1640
1370 REM INPUT+VERGLEICH
1380 GOSUB1690
1390 FORI=0 TOW:B=ASC(E$(I))-65:IF(B<0)OR(B>25) THEN1480
1400 S=B(B):IFS=0 THEN1480
1410 FORS=S TOSA
1420 IFS(S,0)>TK THENZ$=LEFT$(E$(I),LEN(S$(S))):GOTO1440
1430 Z$=E$(I)
1440 IFZ$<=S$(S) THEN1460
1450 NEXTS:GOTO1480
1460 IFZ$<S$(S) THEN1480
1470 GOSUB2260
1480 NEXTI
1490 REM OUTPUT VORBEREITUNG
1500 IFFF=0 THEN1530
1510 FF=0:IFTF>0 THENTF=TF-1
1520 F$=TF$(TF):GOTO1640
1530 IF(W>1)OR(F>0) THEN1550
1540 F$="MACH EINEN RICHTIGEN SATZ!":GOTO1640
1550 IFF<1 THEN1580
1560 F$=F$(F):F$(F)="":F=F-1:GOTO1640
1570 REM VERLEGENHEITSFRAGEN STELLEN
1580 IFTA<3 THEN1610
1590 F$="SO, NUN MACH ICH SCHLUSS. TSCHUESS "+N$+"."
1600 GOSUB2440:SD=200:GOSUB450:GOSUB950
1610 GOSUB260:T=INT(RV*TV+0.5):IFTV$(T)="" THEN1610
1620 TA=TA+1:F$=TV$(T):TV$(T)=""
1630 REM FRAGE AUSGEBEN
1640 GOSUB2440
1650 GOTO1380
1660 REM ROUTINEN
1670 REM INPUT
1680 REM EINGABE
1690 E=0:E$=""
1700 IFD6=0 THEND6=1
1710 FORVE=VT-7 TOVT-5:FORHO=5 TOHT-4:GOSUB110
1720 PRINT" ":NEXTHO:NEXTVE
1730 VE=VT-7:HO=5:GOSUB110:PRINT"-"
1740 GOSUB200:A$=CHR$(IN):IFA$="" THEN1740
1750 IFIN<>28 THEN1840
1760 IFE<1 THEN1740
1770 IF(VE=VT-5)AND(HO=HT-6)AND(R=1) THENHO=HT-5:R=0
1780 GOSUB110:PRINT" "
1790 IF(VE=VT-5)AND(HO=5) THENVE=VT-6:HO=HT-4
1800 IF(VE=VT-6)AND(HO=5) THENVE=VT-7:HO=HT-4
1810 E=E-1:HO=HO-1:GOSUB110
1820 E$=LEFT$(E$,E):PRINT"-"
1830 GOTO1740
1840 IFIN=13 THENA$="."
1850 IF(A$<" ")OR(A$>"Z") THEN1740
1860 E$=E$+A$:E=E+1:GOSUB110
1870 PRINTA$:HO=HO+1:GOSUB110:PRINT"-"
1880 IF(A$="!")OR(A$=".") THEN1990
1890 IFA$="?" THENFF=1:GOTO1990
1900 IF(HO<HT-4)OR(VE<>VT-7) THEN1920
1910 GOSUB110:PRINT" ":HO=5:VE=VT-6
1920 IF(HO<HT-4)OR(VE<>VT-6) THEN1940
1930 GOSUB110:PRINT" ":HO=5:VE=VT-5
1940 GOSUB110:PRINT"-"
1950 IF(HO<>HT-5)OR(VE<>VT-5) THEN1970
1960 E=E-1:HO=HO-1:E$=LEFT$(E$,E):R=1
1970 GOTO1740
1980 REM IN WORTE ZERLEGEN
1990 IFE<1 THENRETURN
2000 W=-1:FORI=1 TOE:A$=MID$(E$,I,1):B=ASC(A$)
2010 IF((B>64)AND(B<91))OR((B>47)AND(B<58)) THEN2050
2020 IF(WA=0)OR(WT=1) THEN2070
2030 IFB=45 THENWT=1:GOTO2070
2040 WA=0:GOTO2070
2050 IFWA=0 THENWA=1:W=W+1:E$(W)="":IFW>EA-1 THENRETURN
2060 E$(W)=E$(W)+A$
2070 NEXTI
2080 REM 1.PERS. ERSETZEN DURCH 2.PERS.
2090 FORI=0 TOW:IFLEN(E$(I))>6 THEN2220
2100 Z$=LEFT$(E$(I),4)
2110 IFZ$="ICH" THENE$(I)="DU":GOTO2190
2120 IFZ$="DU" THENE$(I)="ICH":GOTO2190
2130 IFZ$="MICH" THEN2210
2140 IFZ$="DICH" THEN2200
2150 IFZ$="MEIN" THEN2210
2160 IFZ$="DEIN" THEN2200
2170 IFZ$="MIR" THEN2210
2180 IFZ$="DIR" THEN2200
2190 GOTO2220
2200 E$(I)="M"+RIGHT$(E$(I),LEN(E$(I))-1):GOTO2220
2210 E$(I)="D"+RIGHT$(E$(I),LEN(E$(I))-1)
2220 NEXTI
2230 REM DRUCKEN
2240 RETURN
2250 REM FRAGEN VORBEREITEN
2260 IFF>FA-1 THENRETURN
2270 REM NICHT BENUTZTE TEXTE SUCHEN
2280 Z=0:FORJ=0 TOGA:T=S(S,J):IFT$(T)>"" THENZ(Z)=T:Z=Z+1
2290 NEXTJ
2300 IFZ=0 THENRETURN
2310 GOSUB260:T=Z(RV*Z):IF(T<=TK)AND(I=W) THENRETURN
2320 REM TEXT HOLEN
2330 F=F+1:F$(F)=T$(T):T$(T)="":IFT>TK THENRETURN
2340 REM TEXT-KOMPOSITION
2350 IF(E$(I+1)="ICH")OR(E$(I+1)="DU") THEN2370
2360 FORJ=I+1 TOW:GOTO2380
2370 FORJ=0 TOW:IFJ=I THENJ=J+2
2380 IF(E$(J)="UND")OR(E$(J)="ODER") THENJ=W:GOTO2400
2390 F$(F)=F$(F)+" "+E$(J)
2400 NEXTJ
2410 F$(F)=F$(F)+" "+T2$(T)
2420 RETURN
2430 REM BS EDITIEREN
2440 IFL6=0 THENL6=1
2450 FORVE=5 TO8:FORHO=5 TOHT-5:GOSUB110:PRINT" "
2460 NEXTHO:NEXTVE
2470 X$=F$
2480 VE=5:HO=4
2490 Z=29
2500 Z$=MID$(F$,Z,1):IFZ$="" THEN2530
2510 IFASC(Z$)<65 THEN2530
2520 Z=Z-1:GOTO2500
2530 Z$=LEFT$(F$,Z)
2540 FORH=1 TOZ
2550 HO=HO+1:IFHO=HT-4 THENVE=VE+1:HO=5
2560 GOSUB110
2570 PRINTMID$(Z$,H,1):FORA=1 TO10:NEXTA
2580 IFHO<HT-10 THEN2600
2590 IFMID$(Z$,H,1)=" " THENHO=HT-5
2600 NEXTH
2610 IFZ>=LEN(F$) THEN2630
2620 F$=MID$(F$,Z+1):IFF$<>"" THEN2490
2630 HO=10
2640 RETURN
2650 REM DATA'S LESEN
2660 RESTORE:FORI=1 TOSA:READS$(I):J=0:B=ASC(S$(I))-65
2670 IFB(B)=0 THENB(B)=I
2680 READZ:IFZ=0 THEN2700
2690 S(I,J)=ABS(Z)-TK*(Z>0):J=J+1:GOTO2680
2700 NEXTI:READZ$
2710 FORI=TK+1 TOTK+TN:READT$(I):NEXTI:READZ$
2720 FORI=1 TOTK:READT$(I),T2$(I):NEXTI:READZ$
2730 FORI=0 TOTV-1:READTV$(I):NEXTI:READZ$
2740 FORI=0 TOTF-1:READTF$(I):NEXTI:READZ$
2750 IFZ$<>"$" THENPRINT" DATA FEHLER":GOTO950
2760 RETURN
2770 REM DATA'S ZAEHLEN
2780 GOSUB2810:SA=Z:GOSUB2810:TN=Z:GOSUB2810
2790 TK=Z/2:GOSUB2810:TV=Z
2800 GOSUB2810:TF=Z:RETURN
2810 Z=0
2820 READZ$:IFZ$="$" THENRETURN
2830 A=ASC(Z$):IF(A>57)OR(A<45)OR(A=46) THENZ=Z+1
2840 GOTO2820
25000 REM
25010 REM --- DATA-ZEILEN ---
25020 DATA"AERGER",14,25,0,"ARM",2,22,0
25030 DATA"BIN",-1,-12,-13,-19,0,"BIST",-17,-35,-37,-38,0
25040 DATA"BLEIBE",-7,-30,0
25050 DATA"BRAUCHE",-14,0,"BRUDER",9,0,"BRUEDER",9,0
25060 DATA"COMPUTER",11,0
25070 DATA"DARF",-3,-29,0
25080 DATA"FINDE",-34,0,"FRAU",3,4,0,"FREUND",1,5,0
25090 DATA"GEBE",-32,0,"GEHE",-15,0,"GELD",2,0
25100 DATA"GESCHWISTER",9,0
25110 DATA"GESUND",21,22,0,"GLAUBE",-5,27,29,0,"GLUECK",23,0
25120 DATA"HABE",-2,-22,-39,0,"HAETTE",17,0,"HAST",-36,0
25130 DATA"HAT",-2,5,0,"HOFFE",-6,0
25140 DATA"IDIOT",20,0
25150 DATA"KANN",-4,-24,0,"KANNST",-18,-42,0,"KOENNTE",17,0
25160 DATA"KOMME",-33,0,"KRANK",22,0,"KUMMER",25,0
25170 DATA"LIEB",6,28,0,"LUST",24,0
25180 DATA"MACHE",-11,-21,-27,-28,0,"MAENNER",5,0
25190 DATA"MOECHTE",-16,-31,0,"MOEGLICH",13,0
25200 DATA"MUSS",-20,-26,0
25210 DATA"MUTTER",7,0
25220 DATA"ONKEL",10,0
25230 DATA"REICH",2,22,0
25240 DATA"SAG",12,29,0,"SCWAEGERIN",10,0,"SCWAGER",10,0
25250 DATA"SCHWESTER",9,0,"SEX",26,0,"SOEHNE",1,0
25260 DATA"SPIEL",16,0,"SORGEN",25,0
25270 DATA"SPINNER",20,0,"STREIT",25,0,"STRESS",25,0
25280 DATA"TANTE",10,0,"TOECHTER",1,0,"TRAURIG",14,25,0
25290 DATA"TROTTEL",20,0
25300 DATA"UNGLUECK",14,23,0,"UNZUFRIEDEN",14,0
25310 DATA"VATER",8,0,"VERWANDT",10,0,"VIELLEICHT",13,0
25320 DATA"WETTER",18,0,"WERDE",-9,-23,-40,-41,0
25330 DATA"WAERE",17,0,"WEISS",-10,0
25340 DATA"WILL",-8,-9,-41,0,"WUERDE",17,0,"WUNSCH",19,0
25350 DATA"$"
25360 REM TEXTE
25370 DATA"BIST DU STOLZ AUF DEINE KINDER. WAS MACHEN SIE?"
25380 DATA"GELD ALLEIN MACHT NICHT GLUECKLICH!"
25390 DATA"ICH GLAUBE FRAUEN SIND EIN HEIKLES THEMA."
25400 DATA"UEBRIGENS, WIE SOLLTE DEINE TRAUMFRAU SEIN?"
25410 DATA"MACHST DU DIR VIEL AUS MAENNERN?"
25420 DATA"MIT DER LIEBE IST ES HALT SO EINE SACHE."
25430 DATA"ERZAHLE MIR MEHR UEBER DEINE MUTTER."
25440 DATA"WAR DEIN VATER SEHR STRENG MIT DIR?"
25450 DATA"ERZAEHL MIR ETWAS MEHR VON DEINEN GESCHWISTERN."
25460 DATA"GIBT ES AUCH VERWANDTE LEUTE DIE DU MAGST?"
25470 DATA"SAG MAL, WAS HAELST DU VON DEN COMPUTERN?"
25480 DATA"DAS HALTE ICH FUER EIN GERUECHT."
25490 DATA"DU SCHEINST ETWAS UNSICHER ZU SEIN."
25500 DATA"VERSUCHE DIE NEG. GEDANKEN DIR FERN ZU HALTEN."
25510 DATA"ABER DU BIST DOCH NICHT VERHEIRATET - ODER?"
25520 DATA"GAMBLER!"
25530 DATA"SO SICHER SCHEINT DAS ABER NICHT ZU SEIN - ODER?"
25540 DATA"HAST DU KEIN BESSERES THEMA ALS DAS WETTER?"
25550 DATA"WUENSCHE SIND DIE TRIEBFEDER DER MENSCHHEIT."
25560 DATA"SCHIMPFWOERTER MOECHTE ICH NICHT MEHR HOEREN!"
25570 DATA"A VOTRE SANTE! (ICH KANN SOGAR FRANZOESISCH)"
25580 DATA"DU SAGST DIR WOHL AUCH:LIEBER REICH ALS ARM."
25590 DATA"KENNST DU DIE GESCHICHTE VOM HANS IM GLUECK ?"
25600 DATA"APROPOS LUST: ICH HABE JETZT LUST AUF EIN BIER."
25610 DATA"DU MUSST DAS LEBEN VON DER HEITEREN SEITE SEHEN."
25620 DATA"WEISST DU WIE NONNEN ZAEHLEN? 1 2 3 4 5 PFUI!"
25630 DATA"DAS KANN ICH ALLERDINGS NICHT SO RECHT GLAUBEN."
25640 DATA"GEHT DIE LIEBE BEI DIR AUCH DURCH DEN MAGEN?"
25650 DATA"GLAUBST DU DAS WIRKLICH?"
25660 DATA"$"
25670 REM KOMPOSITIONS-TEXTE  
25680 DATA"BIST DU ETWA STOLZ DARAUF, DASS DU","BIST?"
25690 DATA"WAS FUER EIN ZUFALL, AUCH ICH HABE","."
25700 DATA"WER BESTIMMT DENN, DASS DU","DARFST?"
25710 DATA"DAS HABE ICH MIR GEDACHT, DASS DU","KANNST."
25720 DATA"WARUM GLAUBST DU","?"
25730 DATA"HOFFST DU NOCH ETWAS ANDERES, AUSSER","?"
25740 DATA"DEINE STANDHAFTIGKEIT IN EHREN. IST ES KLUG,"
25750 DATA"ZU BLEIBEN?"
25760 DATA"AN WAS DENKST DU, WENN DU","WILLST?"
25770 DATA"GLAUBST DU ES WAERE GUT,WENN DU","WUERDEST?"
25780 DATA"WIE MEINST DU DAS GENAU, DU WUESSTEST","?"
25790 DATA"ALSO MACH",".IST MIR AUCH GLEICH."
25800 DATA"WEN INTERESSIERT DAS SCHON, DAS DU","BIST?"
25810 DATA"OH, ICH WAERE AUCH GERNE","."
25820 DATA"VIELLEICHT BRAUCHST DU WIRKLICH","."
25830 DATA"ICH MOECHTE EIGENTLICH AUCH","GEHEN."
25840 DATA"AN WAS DENKST DU, WENN DU","MOECHTEST?"
25850 DATA"UEBRIGENS: DU BIST SELBST","!"
25860 DATA"WETTEN WIR,DASS DU SELBST","KANNST?"
25870 DATA"SO. DU BIST ALSO",".OB DAS WOHL JEMANDEN JUCKT?"
25880 DATA"KANN ICH DIR HELFEN, WENN DU DAS NAECHSTE MAL"
25890 DATA"MUSST ?"
25900 DATA"ERZAEHLE MIR MEHR DARUEBER, WIE DU","MACHST."
25910 DATA"WAS GLAUBST DU WOHER DAS KOMMT, DASS DU","HAST?"
25920 DATA"WARUM WIRST DU","?"
25930 DATA"KANNST DU MIR ERKLAEREN, WIE MAN","KANN?"
25940 DATA"WER HAT SONST NOCH","?"
25950 DATA"DU BIST NICHT DER EINZIGE. AUCH ICH SOLLTE","."
25960 DATA"WAS DENKST DU DIR DABEI WENN DU","MACHST?"
25970 DATA"WAS WAERE WOHL, WENN JEDER","MACHEN WUERDE?"
25980 DATA"DAS SOLLTE MIR MAL EINER SAGEN,DASS ICH","DARF!"
25990 DATA"WARUM KANNST DU NUR SO STUR SEIN UND","BLEIBEN?"
26000 DATA" "," - DAS MOECHTE NOCH MANCHER!"
26010 DATA"ICH FINDE ES GROSSZUEGIG VON DIR, DASS DU"
26020 DATA"GIBST."
26030 DATA"KOMMT SONST NOCH JEMAND","?"
26040 DATA"ICH STIMME DIR VOLL BEI. AUCH ICH FINDE","."
26050 DATA"OK, ICH BIN"," - WAS BIST DENN DU?"
26060 DATA"DAS WEISS DOCH JEDER, DASS ICH","HABE."
26070 DATA"HAT LANGE GEDAUERT, BIST DU MERKTEST, DASS ICH"
26080 DATA"BIN."
26090 DATA"DUMMKOPF! DAS WEISS MAN DOCH, DASS ICH","BIN."
26100 DATA"BIST DU FROH, DASS DU","HAST?"
26110 DATA"WER WIRD SONST NOCH","?"
26120 DATA"WENN NUR ALLE","WUERDEN!"
26130 DATA"WARUM SOLL ICH","KOENNEN?"
26140 DATA"GLAUBST DU, DAS BEEINDRUCKT MICH, DASS DU"
26150 DATA"KANNST ?","$"
26160 REM VERLEGENHEITS-TEXTE
26170 DATA"REDEST DU IMMER SO EINFAELTIGES ZEUG?"
26180 DATA"ERZAEHLE MIR WAS DU VON MIR DENKST."
26190 DATA"GLAUBST DU, DASS ICH DIR ETWAS UEBERLEGEN BIN?"
26200 DATA"WARUM SPRICHST DU EIGENTLICH MIT EINEM COMPUTER?"
26210 DATA"KANNST DU NICHT ETWAS GESCHEITERES ERZAEHLEN?"
26220 DATA"BIST DU AUCH SO INTELLIGENT WIE ICH?"
26230 DATA"WAS HAELST DU VON UNSERER KONVERSATION?"
26240 DATA"WENN ES DIR ZU BLOED WIRD, ZIEH DEN STECKER."
26250 DATA"EIN GESPRAECH MIT DIR IST ZIEMLICH EINFAELTIG."
26260 DATA"DEM GESPRAECH NACH,BIST DU NICHT DER HELLSTE !"
26270 DATA"$"
26280 REM VERLEGENH.-TEXTE AUF ?
26290 DATA"DEINE FRAGEREI GEHT MIR AUF DEN WECKER!"
26300 DATA"DUMME FRAGE! WEISS DOCH JEDER!"
26310 DATA"KEINE AHNUNG! WAS ERWARTEST DU VON MIR?"
26320 DATA"DAS WEISST DU BESSER ALS ICH. ERZAHLE WEITER!"
26330 DATA"$"
30000 REM -------------------------------
30010 REM DIESES PROGRAMM SIMULIERT EIN
30020 REM GESPRAECH MIT DEM COMPUTER.
30030 REM
30040 REM DAS ERSTE PROGRAMM DIESER ART
30050 REM STAMMT VON PROF.WEIZENBAUM (USA)
30060 REM UND TRAEGT DEN NAMEN
30070 REM         - ELIZA -
30080 REM
30090 REM FUER DEN KC85/3 WURDE IN MUEHL-
30100 REM HAUSEN DIE BASIC-VERSION - VIC -
30110 REM GESCHRIEBEN. AUS IHR ENTSTAND DIE
30120 REM BASICODE-FASSUNG.
32000 REM
32010 REM *******************
32020 REM *                 *
32030 REM * VIC-INTERVIEW   *
32040 REM * eine ELIZA-     *
32050 REM *  Adaption       *
32060 REM *                 *
32070 REM * in BASICODE-3   *
32080 REM *      von:       *
32090 REM *                 *
32100 REM * Thomas Goetze   *
32110 REM * 4908 Troeglitz  *
32120 REM * Ringstr.5       *
32130 REM *                 *
32140 REM * KC85/4          *
32150 REM * 24.02.1990      *
32160 REM *                 *
32170 REM *******************
32180 REM
32190 REM Erstausstrahlung: Radio DDR
32200 REM-spezial 900316

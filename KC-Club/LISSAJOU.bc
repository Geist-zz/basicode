1000 A=100:GOTO20:REM *** LISSAJOU.BAC ***
1010 GE=0.001:SZ=1/GE:PP=3.141592
1020 PRINT"    *** LISSAJOU ***"
1030 PRINT:PRINT"Hinweise ab Zeile 30000!"
1040 PRINT:PRINT:SR$="Taste":GOSUB150:GOSUB210
1050 GOSUB100:PRINT:PRINT
1060 INPUT"Frequenz in X - Richtung: ";FX
1070 INPUT"Frequenz in Y - Richtung: ";FY
1080 PRINT"Phasendifferenz"
1090 INPUT"(Vielfaches von Pi) ";PH
1100 GOSUB600:HO=0:VE=0:CN=1:GOSUB620:CN=0
1110 HO=0.9999:VE=0:GOSUB630
1120 HO=0.9999:VE=0.9999:GOSUB630
1130 HO=0:VE=0.9999:GOSUB630
1140 HO=0:VE=0:GOSUB630
1150 UX=1/FX:UY=1/FY:PA=PH*PP:GOSUB1350
1160 UM=2*PP*KV:SC=UM/SZ
1170 HO=0.5:VE=0.5+0.49*SIN(PA):GOSUB620
1180 FORT=0 TOUM STEPSC
1190 HO=0.5+0.49*SIN(FX*T):VE=0.5+0.49*SIN(FY*T+PA)
1200 GOSUB630
1210 NEXTT
1220 SD=20:GOSUB450:GOSUB250
1230 IFIN$="" THEN1220
1240 GOSUB210:GOSUB100
1250 PRINT"Noch einmal (Y/N) ?":GOSUB210
1260 IFIN=89 THEN1010
1270 GOTO950
1280 REM 
1290 REM Unterprogramm KGV
1300 REM Ermittelt kleinstes gemeinsames Vielfaches
1310 REM von UX und UY.
1320 REM Dabei wird bis zur Genauigkeit von ge
1330 REM gerechnet.
1340 REM 
1350 N=1:M=1
1360 S=UX*M:U=UY*N:DF=S-U
1370 IFABS(DF)<GE THENDF=0
1380 IFDF>0 THENN=N+1:GOTO1360
1390 IFDF<0 THENM=M+1:GOTO1360
1400 KV=S:RETURN
30000 REM 
30010 REM Das Programm zeichnet die von osillographischen
30020 REM Messungen her bekannten Lissajous - Figuren
30030 REM auf den Bildschirm.
30040 REM Die beiden zu ueberlagernden Frequenzen sowie
30050 REM deren Phasenlage zueinander koennen interaktiv
30060 REM geaendert werden.
30070 REM Ein geschlossener Kurvenzug kann sich nur dann
30080 REM ergeben, wenn die beiden Frequenzen im Ver-
30090 REM haeltnis ganzer rationaler Zahlen zueinander 
30100 REM stehen.
30110 REM Empehlung: Fx=34; Fy=38; Phase=0.5
30120 REM Das Ende der Berechnung wird durch einen In-
30130 REM tervallton akustisch signalisiert. Durch ein-
30140 REM maliges Betaetigen einer Taste wird der Ton 
30150 REM abgeschaltet, die Grafik kann in Ruhe betrach-
30160 REM tet werden, bei Betaetigung einer weiteren Taste
30170 REM wird der Bildschirm geloescht und das Programm 
30180 REM fortgesetzt.
30190 REM 
32000 REM *********************************************
32010 REM *                                           *
32020 REM *               LISSAJOU.BAC                *
32030 REM * Erzeugung von Lissojous - Figuren         *
32040 REM *                                           *
32050 REM * Heimo Ponnath, Hamburg 1989               *
32060 REM *                                           *
32070 REM *********************************************
32080 REM *                                           *
32090 REM * entnommen aus:                            *
32100 REM * Ponnath,H.: Grafik - Leckerbissen fuer    *
32110 REM *             Feinschmecker                 *
32120 REM * in: DOS extra 12/90, Seite 135 ff.        *
32130 REM *                                           *
32140 REM *********************************************
32150 REM *                                           *
32160 REM * Anpassung an Basicode auf KC compact:     *
32170 REM *                                           *
32180 REM * Frank Berndt                              *
32190 REM * Rudolf-Hable Str. 5                       *
32200 REM * O-8106 Radeburg                           *
32210 REM *                                           *
32220 REM * 03.01.1991                                *
32230 REM *                                           *
32240 REM *********************************************
32250 REM Erstausstrahlung: Deutschlandsender Kultur
32260 REM-spezial 910123

1000 A=100:GOTO20:REM  ** OTHELLO **
1010 DIMF(10,10),I4(8),J4(8),C$(8),D$(3)
1020 HT=HO:VT=VE
1030 GOSUB100:PRINT:SR$="OTHELLO":GOSUB150
1040 PRINT"wird auf einem quadrat."
1050 PRINT" Brett gespielt, dessen Zeilen von 1-8"
1060 PRINT" und dessen Reihen von A-H bezeichnet"
1070 PRINT" sind. Die Ausgangsstellung ist alle"
1080 PRINT" Felder leer, ausser den mittleren"
1090 PRINT" die eine feste Form haben."
1100 PRINT" Versuche deine Steine so zu plazieren"
1110 PRINT" dass sie meine einschliessen indem sie"
1120 PRINT" eine horizontale,vertikale oder eine"
1130 PRINT" diagonale Linie bilden. Dann werden"
1140 PRINT" alle neu eingeschlossenen Steine in"
1150 PRINT" deine umgewandelt - und umgekehrt"
1160 PRINT" geht es natuerlich auch !"
1170 HO=0:VE=VT:GOSUB110:PRINT"(Taste druecken)";
1180 GOSUB210:GOSUB100:PRINT
1190 SR$="Beachte":GOSUB150
1200 PRINT" Du musst mindestens einen"
1210 PRINT" von meinen Steinen schlagen wenn es"
1220 PRINT" moeglich ist; ansonsten gib fuer"
1230 PRINT" Deinen Zug   0,0   ein."
1240 PRINT"****************************************"
1500 REM *** INITIALISIERUNG ***
1510 D$(1)="X":D$(2)=".":D$(3)="O":SD=40
1520 SR$="Falsche Eingabe"
1530 RESTORE:FORI=1 TO8:READI4(I),J4(I),C$(I):NEXTI
1540 PRINT"  Welche Spielfeldgroesse (3...8) ?":GOSUB210
1550 IF(IN<51)OR(IN>56)THEN GOSUB150:PRINT:GOTO1540
1560 M=IN-48:G=INT(M/2)+1:V=INT(M/2)+2
1570 FORI=1 TO8:FORJ=1 TO8:F(I,J)=0:NEXTJ:NEXTI
1580 F(G,G)=1:F(V,V)=1:F(V,G)=-1:F(G,V)=-1
1590 C1=2:H1=2:N1=4:Z=0:XX=0:YY=0
1600 PRINT:PRINT"  Welche Betriebsart :"
1610 PRINT"    1 = gegen den Computer"
1620 PRINT"    2 = Computer gegen sich selbst"
1630 PRINT"    3 = zwei Spieler gegeneinander"
1640 GOSUB210:IF(IN<49)OR(IN>51)THEN GOSUB150:GOTO1600
1650 IFIN=50 THENXX=1:S2=2:C=1:H=-1:GOSUB3510:GOTO2010
1660 IFIN=51 THENYY=1:C=1:H=-1:GOSUB3510:GOTO2510
1670 PRINT:PRINT"Soll ich meine grosse Staerke spielen"
1680 PRINT"   oder nicht (Y/N) ?":GOSUB210
1690 IFIN=89 THENS2=2:GOTO1720
1700 IFIN=78 THENS2=0:GOTO1720
1710 GOSUB150:PRINT:GOTO1670
1720 PRINT:PRINT"  Willst Du  X oder O  ?":GOSUB210
1730 IFIN=88 THENC=1:H=-1:GOTO1760
1740 IFIN=79 THENC=-1:H=1:GOTO1760
1750 GOSUB150:PRINT:GOTO1720
1760 PRINT:PRINT"  Willst Du anfangen ? Y/N":GOSUB210
1770 IFIN=89 THEN GOSUB3510:GOTO2510
1780 IFIN=78 THEN GOSUB3510:GOTO2010
1790 GOSUB150:PRINT:GOTO1760
1800 VE=VT-3:HO=0:GOSUB110::SD=20:GOSUB450
1810 PRINT"                                    "
1820 PRINT"                                    "
1830 PRINT"                                    "
1840 VE=VT-3:GOSUB110:RETURN
2000 REM *** COMPUTERZUG ***
2010 T1=C:T2=H
2020 B1=-1:I3=0:J3=0:IF(H1=0)OR(N1=INT(M^2))THEN2760
2030 GOSUB1800
2040 PRINT" Bitte warten  '";D$(T1+2);"'  ist dran"
2050 FORI=2 TOM+1
2060 FORJ=2 TOM+1
2070 IFF(I,J)<>0 THEN2170
2080 GOSUB2910:IFF1=0 THEN2170
2090 U=-1:GOSUB3000
2100 IFS1=0 THEN2170
2110 IF(I-2)*(I-M+1)=0 THENS1=S1+S2
2120 IF(J-2)*(J-M+1)=0 THENS1=S1+S2
2130 IFS1<B1 THEN2170
2140 IFS1>B1 THEN2160
2150 GOSUB260:IFRV<.5 THEN2170
2160 B1=S1:I3=I:J3=J
2170 NEXTJ
2180 NEXTI
2190 IFB1>0 THEN2230
2200 PRINT"  Ich setze aus !"
2210 IFZ=1 THEN2860
2220 Z=1:GOSUB450:GOTO2290
2230 Z=0:PRINT" Ich ziehe nach   ";I3-1;", ";C$(J3-1)
2240 I=I3:J=J3:U=1:GOSUB3000
2250 IFT1=C THENC1=C1+S1+1:H1=H1-S1
2260 IFT1=H THENH1=H1+S1+1:C1=C1-S1
2270 N1=N1+1
2280 PRINT" Ich bekomme ";S1+1;" Steine dazu":GOSUB3520
2290 IF(XX=1)AND(T1=C)THENT1=H:T2=C:GOTO2020
2300 IFXX=1 THEN2010
2500 REM *** SPIELERZUG ***
2510 T1=H:T2=C
2520 IF(C1=0)OR(N1=INT(M^2))THEN2760
2530 GOSUB1800
2540 PRINT" Dein Zug (Zeile,Spalte),  ";D$(T1+2);"  ";
2550 GOSUB210:IF(IN<48)OR(IN>56)THEN2740
2560 I=IN-48:PRINTI;", ";:GOSUB210:PRINTIN$
2570 IFI=0 THENZ=1:GOTO2710
2580 Q=1
2590 IFC$(Q)=IN$ THEN2620
2600 IFQ<M THENQ=Q+1:GOTO2590
2610 GOTO2750
2620 J=Q:I=I+1:J=J+1
2630 IFF(I,J)<>0 THEN2750
2640 GOSUB2910:IFF1<>1 THEN2740
2650 U=-1:GOSUB3000:IFS1=0 THEN2740
2660 Z=0:U=1:GOSUB3000
2670 PRINT" Du bekommst ";S1+1;" Steine dazu"
2680 IFT1=H THENH1=H1+S1+1:C1=C1-S1
2690 IFT1=C THENC1=C1+S1+1:H1=H1-S1
2700 N1=N1+1:GOSUB3520
2710 IF(YY=1)AND(T1=H)THENT1=C:T2=H:GOTO2520
2720 IFYY=1 THEN2510
2730 GOTO2010
2740 PRINT:REM fehlerhafte Eingabe
2750 GOSUB150:GOSUB450:GOTO2510
2760 IF(XX=1)OR(YY=1)THEN2860
2770 GOSUB1800:IFH1=C1 THENPRINT" Unentschieden !"
2780 IFH1<C1 THENPRINT" Diesmal habe ich gewonnen"
2790 IFH1>C1 THENPRINT" Du hast gewonnen"
2800 PRINT" Das war ein   ";
2810 IFH1>INT(M*M*.9)THENPRINT"  Super-Spiel":GOTO2860
2820 IFH1>INT(M*M*.8)THENPRINT"  perfektes Spiel ":GOTO2860
2830 IFH1>INT(M*M*.7)THENPRINT"  grosser Kampf":GOTO2860
2840 IFH1>INT(M*M*.6)THENPRINT"  heisses Spiel":GOTO2860
2850 PRINT"  Mist - Spiel"
2860 SR$=" Ein neues  Spiel ?   Y/N   "
2870 GOSUB1800:GOSUB150:GOSUB210
2880 IFIN=89 THEN GOSUB100:GOTO1510
2890 IFIN=78 THEN950
2900 PRINT:GOTO2870
2910 F1=0:FORI1=-1 TO1:FORJ1=-1 TO1
2920 IFF(I+I1,J+J1)=T2 THENF1=1
2930 NEXTJ1:NEXTI1
2940 RETURN
3000 S1=0:FORK=1 TO8
3010 I5=I4(K):J5=J4(K):I6=I+I5:J6=J+J5:S3=0
3020 IFF(I6,J6)<>T2 THEN3120
3030 S3=S3+1:I6=I6+I5:J6=J6+J5
3040 IFF(I6,J6)=T1 THEN3070
3050 IFF(I6,J6)=0 THEN3120
3060 GOTO3030
3070 S1=S1+S3:IFU<>1 THEN3120
3080 I6=I:J6=J
3090 FORK1=0 TOS3
3100 F(I6,J6)=T1:I6=I6+I5:J6=J6+J5
3110 NEXTK1
3120 NEXTK
3130 RETURN
3500 REM *** SPIELFELD ***
3510 GOSUB100
3520 VE=9-M:HO=9-M:GOSUB110
3530 IF(XX=1)OR(YY=1)THENPRINT"   ";:GOTO3560
3540 IFS2=0 THENPRINT"k) ";
3550 IFS2=2 THENPRINT"g) ";
3560 FORQ=1 TOM:PRINTC$(Q);" ";:NEXTQ
3570 FORI=2 TOM+1:VE=VE+2:HO=9-M:GOSUB110
3580 SR=I-1:GOSUB300:PRINT" ";SR$;" ";
3590 FORJ=2 TOM+1:PRINTD$(F(I,J)+2);" ";:NEXTJ
3600 PRINT:PRINT:NEXTI
3610 IF(XX=1)OR(YY=1)THEN3630
3620 VE=5:HO=26:GOSUB110:PRINT"DU   ICH"
3630 VE=7:HO=26:GOSUB110
3640 IFH=1 THENPRINT"O     X"
3650 IFH=-1 THENPRINT"X     O"
3660 VE=13:HO=25:GOSUB110:SR=H1:CT=2:CN=0:GOSUB310:PRINTSR$
3670 HO=31:GOSUB110:SR=C1:GOSUB310:PRINTSR$
3680 RETURN
25000 DATA0,1,"A",-1,1,"B",-1,0,"C",-1,-1,"D"
25010 DATA0,-1,"E",1,-1,"F",1,0,"G",1,1,"H"
32000 REM ****************************
32010 REM *                          *
32020 REM *  STRATEGIESPIEL OTHELLO  *
32030 REM * geschrieben auf  KC 85/4 *
32040 REM *                          *
32050 REM *      Wolfgang Pohle      *
32060 REM *     Waldpromenade  3     *
32070 REM *       Erkner  1250       *
32080 REM *                          *
32090 REM *     Stand 02.02.1990     *
32100 REM *                          *
32110 REM ****************************
32120 REM
32130 REM Erstausstrahlung: DS-Kultur
32140 REM-spezial 900725

1000 A=50:GOTO 20:REM JOSEF-SPIEL
1010 GOSUB 100
1020 SR$="                  "
1030 HO=5:VE=9:GOSUB 110:GOSUB 150
1040 SR$="   JOSEF-Spiel    "
1050 HO=5:VE=10:GOSUB 110:GOSUB 150
1060 SR$="                  "
1070 HO=5:VE=11:GOSUB 110:GOSUB 150
1080 HO=6:VE=13:GOSUB 110
1090 PRINT "BASIC 1x1 - Horst Voelz"
1100 HO=6:VE=14:GOSUB 110
1110 PRINT "RADIO DDR - 25.3.1987"
1120 REM -----------------------
1130 GOSUB 10000
1140 REM -----------------------
2010 SR$="Erlaeuterungen:"
2020 HO=2:VE=2:GOSUB 110:GOSUB 150
2030 HO=1:VE=4:GOSUB 110
2040 PRINT "N Teilnehmer bilden einen"
2045 HO=1:VE=5:GOSUB 110
2050 PRINT "Kreis. Jeder M-te scheidet"
2055 HO=1:VE=6:GOSUB 110
2070 PRINT "aus."
2080 HO=1:VE=8:GOSUB 110
2090 PRINT "Wieviel Teilnehmer ?"
2095 HO=20:VE=8:GOSUB 110
2100 INPUT N:DIM K(N)
2105 HO=1:VE=10:GOSUB 110
2110 PRINT "Der Wievielte soll jeweils"
2115 HO=1:VE=11:GOSUB 110
2120 PRINT "ausscheiden ? (<= Anzahl!)"
2125 HO=28:VE=11:GOSUB 110
2130 INPUT M:IF M>N THEN 2105
2135 REM -------------------------
2140 GOSUB 10000
2145 REM -------------------------
3000 HO=1:VE=2:GOSUB 110
3010 PRINT "Der Reihe nach scheiden aus:"
3015 REM 
3020 FOR Z=1 TO N:K(Z)=Z+1:NEXT 
3030 HO=1:VE=4:GOSUB 110
3035 P=0
3040 K(N)=1:Z=N:REM KREIS + ANFANG
3050 REM 
3060 FOR I=1 TO M-1:Z=K(Z):NEXT 
3070 REM 
3090 PRINT K(Z);:P=P+1:IF P=8 THEN P=0:PRINT 
3100 K(Z)=K(K(Z))
3110 IF K(Z)<>Z THEN GOTO 3040
3115 HO=1:VE=22:GOSUB 110
3120 PRINT "Der letzte ist:"Z;"/"
3130 GOSUB 10000:GOSUB 950
3140 REM --------------------------
10000 SR$="Taste druecken !"
10005 HO=19:VE=22:GOSUB 110:GOSUB 150
10010 GOSUB 210:GOSUB 100:RETURN
10020 REM -------------------------
32000 REM JOSEF-SPIEL
32010 REM HORST VOELZ
32020 REM BASIC 1X1; RADIO DDR 25.3.87
32030 REM 
32040 REM 
32050 REM UMGESETZT IN BASICODE-3
32060 REM C-128 (C-64-MODE)
32070 REM F. DORMEIER, 2/88

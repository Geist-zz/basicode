1000 A=52*201:GOTO20:REM = CB-SKIPS =
1010 REM DIM UND A= IMMER AN+1 !!!
1020 VM=INT(VE/2):HM=INT(HO/2)
1030 V0=VE:H0=HO:AN=200:FR=1:GOSUB280
1040 DIMA$(201),B$(201),C$(201),D$(201)
1050 IA=-1:LL$=" "
1060 L2$="                    "
1070 GOSUB100:HO=HM-11:VE=1:GOSUB110
1080 REM
1090 REM HAUPTMENUE
1100 REM
1110 SR$="CB-Funk  Skipliste":GOSUB150
1120 HO=HM-17:VE=VM-7:GOSUB110
1130 PRINT"Skips erfassen .................. A"
1140 HO=HM-17:VE=VM-5:GOSUB110
1150 PRINT"Skips ausgeben .................. B"
1160 HO=HM-17:VE=VM-3:GOSUB110
1170 PRINT"Skips sortieren ................. C"
1180 HO=HM-17:VE=VM-1:GOSUB110
1190 PRINT"Skips suchen .................... D"
1200 HO=HM-17:VE=VM+1:GOSUB110
1210 PRINT"Skips aendern ................... E"
1220 HO=HM-17:VE=VM+3:GOSUB110
1230 PRINT"Skips loeschen .................. F"
1240 HO=HM-17:VE=VM+5:GOSUB110
1250 PRINT"Laden & Speichern ............... G"
1260 HO=HM-17:VE=VM+7:GOSUB110
1270 PRINT"Programm beenden ................ H"
1280 HO=HM-19:VE=V0-1:GOSUB110
1290 SR$="(c) 1990 REBO-Soft     Version 1.0":GOSUB150
1300 GOSUB250
1310 GOSUB200:IFIN=0 THEN1310
1320 IF(IN<65)OR(IN>72)THEN1310
1330 IN=IN-64
1340 ONIN GOTO3000,2000,4000,9000,11000,8000,7500,7000
1950 REM
1960 REM SKIPS AUSGEBEN
1970 REM
2000 GOSUB100
2010 HO=HM-11:VE=2:GOSUB110
2020 SR$="AUSGABE SKIPLISTE":GOSUB150
2030 IFIA=-1 THEN10000
2040 HO=HM-9:VE=VM-1:GOSUB110
2050 PRINT"1 = BILDSCHIRM"
2060 HO=HM-9:VE=VM+1:GOSUB110
2070 PRINT"2 = DRUCKER (80x55)"
2080 GOSUB200:IF(IN$<>"1")AND(IN$<>"2")THEN2080
2090 IFIN$="2" THEN2370
2100 REM === BILDSCHIRM ===
2110 GOSUB100:Z=0
2120 HO=HM-11:VE=2:GOSUB110
2130 SR$="AUSGABE SKIPLISTE":GOSUB150
2140 HO=0:VE=5:GOSUB110
2150 FORI=0 TO2
2160 IFZ+I>IA THEN2260
2170 PRINT"  Nr.:";Z+I+1
2180 PRINT"  Skip    : ";A$(Z+I);
2190 L=LEN(A$(Z+I)):PRINTRIGHT$(L2$,20-L)
2200 PRINT"  Vorname : ";B$(Z+I);
2210 L=LEN(B$(Z+I)):PRINTRIGHT$(L2$,10-L)
2220 PRINT"  QTH     : ";C$(Z+I);
2230 L=LEN(C$(Z+I)):PRINTRIGHT$(L2$,20-L)
2240 PRINT"  Stamm   : ";D$(Z+I);"  "
2250 PRINT
2260 NEXTI
2270 Z=Z+3:HO=H0-19:VE=V0-1:GOSUB110
2280 PRINT"(X=MENU) ";
2290 SR$="TASTE":GOSUB150
2300 GOSUB200:IFIN=0 THEN2300
2310 IFIN=88 THEN1070
2320 HO=H0-10:VE=V0-1
2330 GOSUB110:PRINT"          "
2340 IFZ<=IA THEN2140
2350 GOTO1070
2360 REM === DRUCKER ===
2370 GOSUB100
2380 HO=HM-11:VE=2:GOSUB110
2390 SR$="AUSGABE SKIPLISTE":GOSUB150
2400 REM
2410 HO=HM-13:VE=VM:GOSUB110
2420 PRINT"Drucker fertig, dann RETURN!"
2430 GOSUB200:IFIN<>13 THEN2430
2440 L1$="      ":PA=1:Z=0:Z1=1
2450 HO=HM-13:VE=VM:GOSUB110
2460 PRINT"   Ich drucke...            "
2470 SR=PA:GOSUB300:HP$=SR$
2480 REM DRUCKER OHNE AUTO-LF siehe REM
2490 REM NAECHSTE ZEILE SCHALTET PAPER-OUT=OFF FUER XMM801
2500 REM SR$=CHR$(27)+CHR$(48):SR$=SR$+....
2510 SR$="                              CB-FUNK  "
2520 SR$=SR$+"SKIPLISTE                Seite : "
2530 SR$=SR$+HP$:GOSUB350:GOSUB360
2540 GOSUB360:GOSUB360:GOSUB360
2550 SR$="      Nr.   Skip                  "
2560 SR$=SR$+" Vorname     "
2570 SR$=SR$+" QTH                   "
2580 SR$=SR$+" Stamm":GOSUB350:GOSUB360
2590 SR$="      --------------------------"
2600 SR$=SR$+"----------------------"
2610 SR$=SR$+"----------------------"
2620 GOSUB350:GOSUB360
2630 HP$="":IF(Z+1)<100 THENHP$="0"
2640 IF(Z+1)<10 THENHP$=HP$+"0"
2650 SR=Z+1:GOSUB300:SR$=L1$+HP$+SR$
2660 SR$=SR$+"   "+A$(Z)
2670 L=LEN(A$(Z))
2680 IFL<20 THENSR$=SR$+RIGHT$(L2$,20-L)
2690 SR$=SR$+"   "+B$(Z)
2700 L=LEN(B$(Z))
2710 IFL<10 THENSR$=SR$+RIGHT$(L2$,10-L)
2720 SR$=SR$+"   "+C$(Z)
2730 L=LEN(C$(Z))
2740 IFL<20 THENSR$=SR$+RIGHT$(L2$,20-L)
2750 SR$=SR$+"   "+D$(Z):GOSUB350:GOSUB360
2760 Z=Z+1:Z1=Z1+1
2770 IF(Z1<51)AND(IA>=Z)THEN2630
2780 REM NAECHSTE ZEILE
2790 GOSUB360
2800 IFIA>=Z THEN2830
2810 HO=HM-13:VE=VM:GOSUB110:PRINT"    Ausdruck beendet !  "
2820 GOTO2840
2830 HO=HM-13:VE=VM:GOSUB110:PRINT"   Neues Blatt einlegen."
2840 HO=H0-10:VE=V0-1:GOSUB110
2850 SR$="TASTE":GOSUB150
2860 GOSUB200:IFIN=0 THEN2840
2870 HO=H0-10:VE=V0-1:GOSUB110
2880 PRINT"          "
2890 REM NAECHSTE SEITE
2900 Z1=1:PA=PA+1:IFIA>=Z THEN2450
2910 GOTO1070
2950 REM
2960 REM SKIPS ERFASSEN
2970 REM
3000 GOSUB100
3010 HO=HM-9:VE=2:GOSUB110
3020 SR$="Skips erfassen":GOSUB150
3030 IFIA<AN THEN3070
3040 HO=HM-10:VE=VM:GOSUB110
3050 PRINT"Speicher voll !!!"
3060 SD=20:GOSUB450:GOTO1070
3070 HO=0:VE=VM-3:GOSUB110
3080 A$(IA+1)=""
3090 INPUT"  SKIP    :";A$(IA+1)
3100 IFA$(IA+1)="" THEN1070
3110 IFLEN(A$(IA+1))>20 THENA$(IA+1)=LEFT$(A$(IA+1),20)
3120 PRINT:INPUT"  Vorname :";B$(IA+1)
3130 IFLEN(B$(IA+1))>10 THENB$(IA+1)=LEFT$(B$(IA+1),10)
3140 PRINT:INPUT"  QTH     :";C$(IA+1)
3150 IFLEN(C$(IA+1))>20 THENC$(IA+1)=LEFT$(C$(IA+1),20)
3160 PRINT:INPUT"  Stamm   :";D$(IA+1)
3170 IFLEN(D$(IA+1))>2 THEND$(IA+1)=LEFT$(D$(IA+1),2)
3180 PRINT:PRINT:GOSUB120:REM POITION MERKEN
3190 PRINT"  Daten OK? (J/N)"
3200 GOSUB200:IF(IN<>74)AND(IN<>78)THEN3200
3210 IFIN=74 THENIA=IA+1
3220 GOSUB110:REM UEBERSCHREIBEN
3230 PRINT"  RETURN=weiter, X=Menu"
3240 GOSUB200:IF(IN<>13)AND(IN<>88)THEN3240
3250 IFIN=13 THEN3000
3260 GOTO1070
3950 REM
3960 REM SKIPS SORTIEREN
3970 REM
4000 IFIA<1 THEN1070
4010 GOSUB100
4020 HO=HM-8:VE=2:GOSUB110
4030 SR$="SORTIEREN":GOSUB150
4040 HO=HM-8:VE=VM-1:GOSUB110
4050 PRINT"1 - nach Skips"
4060 HO=HM-8:VE=VM+1:GOSUB110
4070 PRINT"2 - nach QTH"
4080 GOSUB200:IF(IN<49)OR(IN>50)THEN4080
4090 IFIN=49 THENK=1:REM SKIP
4100 IFIN=50 THENK=2:REM QTH
4110 GOSUB100:HO=HM-9:VE=2:GOSUB110
4120 SR$="SORT SKIPS"
4130 IFK=2 THENSR$=" SORT QTH "
4140 GOSUB150
4150 HO=HM-16:VE=VM:GOSUB110
4160 PRINT"Noch     Datensaetze zu sortieren."
4170 FORJ=0 TOIA
4180 Z$="ZZZZZZZZZZZZZZZZZZZZ"
4190 FORI=J TOIA
4200 IFK=2 THEN4230
4210 IFA$(I)<Z$ THENZ$=A$(I):Z=I
4220 GOTO4240
4230 IFC$(I)<Z$ THENZ$=C$(I):Z=I
4240 NEXTI
4250 HO=HM-12:VE=VM:GOSUB110
4260 PRINTIA-J
4270 HO=HM-13:VE=VM+3:GOSUB110
4280 PRINT"-> ";Z$;L2$
4290 REM EINTRAEGE WECHSELN
4300 A$(AN+1)=A$(J):B$(AN+1)=B$(J)
4310 C$(AN+1)=C$(J):D$(AN+1)=D$(J)
4320 A$(J)=A$(Z):B$(J)=B$(Z)
4330 C$(J)=C$(Z):D$(J)=D$(Z)
4340 A$(Z)=A$(AN+1):B$(Z)=B$(AN+1)
4350 C$(Z)=C$(AN+1):D$(Z)=D$(AN+1)
4360 NEXTJ
4370 REM AN+1 IST EIN ZWISCHENSPEICHER
4380 GOTO1070
4950 REM
4960 REM DATEN LADEN
4970 REM
5000 GOSUB100
5010 HO=HM-8:VE=2:GOSUB110
5020 SR$="DATEN LADEN":GOSUB150
5030 HO=HM-18:VE=V0-2:GOSUB110
5040 IFIA>=0 THENPRINT"  Gespeicherte Daten gehen verloren!"
5050 HO=HM-9:VE=VM-3:GOSUB110
5060 PRINT"1 - Diskette"
5070 HO=HM-9:VE=VM-1:GOSUB110
5080 PRINT"2 - Kassette BASICODE"
5090 HO=HM-9:VE=VM+1:GOSUB110
5100 PRINT"3 - Kassette STANDARD"
5110 HO=HM-9:VE=VM+3:GOSUB110
5120 PRINT"4 - Zum Menue"
5130 GOSUB200:IF(IN<49)OR(IN>52)THEN5130
5140 REM Wandle <IN> in <NF> um
5150 IN=IN-48
5160 IFIN=3 THENNF=2
5170 IFIN=1 THENNF=4
5180 IFIN=2 THENNF=0
5190 IFIN=4 THEN1070
5200 GOSUB100
5210 HO=HM-9:VE=2:GOSUB110
5220 SR$="DATEN LADEN":GOSUB150
5230 HO=2:VE=6:GOSUB110
5240 INPUT"Filename ";NF$
5250 HO=2:VE=8:GOSUB110
5260 PRINT"Eingabegeraet fertig, dann RETURN"
5270 GOSUB200:IFIN<>13 THEN5270
5280 HO=2:VE=8:GOSUB110
5290 PRINT"Daten werden gelesen...          "
5300 GOSUB500
5310 IFIN=0 THEN5350
5320 PRINT:PRINT"  Dateiname falsch!"
5330 GOSUB580
5340 SD=40:GOSUB450:GOTO1070
5350 IA=-1
5360 GOSUB540
5370 IFIN$<>"END OF FILE" THEN5390
5380 GOSUB580:GOTO1070:REM ENDE
5390 A$(IA+1)=IN$
5400 GOSUB540
5410 IFIN=-1 THEN5500:REM FEHLER
5420 B$(IA+1)=IN$
5430 GOSUB540
5440 IFIN=-1 THEN5500
5450 C$(IA+1)=IN$
5460 GOSUB540
5470 IFIN=-1 THEN5500
5480 D$(IA+1)=IN$
5490 IA=IA+1:GOTO5360:REM NEXT SATZ
5500 PRINT:PRINT"Lesefehler! - Bitte wiederholen!"
5510 GOTO5330:REM ENDE
5950 REM
5960 REM DATEN SPEICHERN
5970 REM
6000 GOSUB100
6010 HO=HM-10:VE=2:GOSUB110
6020 SR$="DATEN SPEICHERN":GOSUB150
6030 HO=HM-12:VE=VM-3:GOSUB110
6040 PRINT"  1 - Diskette"
6050 HO=HM-12:VE=VM-1:GOSUB110
6060 PRINT"  2 - Kassette BASICODE"
6070 HO=HM-12:VE=VM+1:GOSUB110
6080 PRINT"  3 - Kassette STANDARD"
6090 HO=HM-12:VE=VM+3:GOSUB110
6100 PRINT"  4 - Zum Menue"
6110 GOSUB200:IF(IN<49)OR(IN>52)THEN6110
6120 REM Wandle <IN> in <NF> um
6130 IN=IN-48
6140 IFIN=3 THENNF=3
6150 IFIN=1 THENNF=5
6160 IFIN=2 THENNF=1
6170 IFIN=4 THEN1070
6180 GOSUB100
6190 HO=HM-10:VE=2:GOSUB110
6200 SR$="DATEN SPEICHERN":GOSUB150
6210 HO=2:VE=6:GOSUB110
6220 INPUT"Filename ";NF$
6230 HO=2:VE=8:GOSUB110
6240 PRINT"Ausgabegeraet fertig, dann RETURN"
6250 GOSUB200:IFIN<>13 THEN6250
6260 HO=2:VE=8:GOSUB110
6270 PRINT"Daten werden geschrieben...      "
6280 REM DATEI EROEFFNEN UND DATEN SCHREIBEN
6290 GOSUB500
6300 FORI=0 TOIA
6310 SR$=A$(I):GOSUB560
6320 SR$=B$(I):GOSUB560
6330 SR$=C$(I):GOSUB560
6340 SR$=D$(I):GOSUB560
6350 NEXTI
6360 SR$="END OF FILE":GOSUB560
6370 GOSUB580
6380 GOTO1070
6950 REM
6960 REM PROGRAMMENDE
6970 REM
7000 IFIA=-1 THEN7100
7010 GOSUB100
7020 HO=HM-9:VE=2:GOSUB110
7030 SR$="PROGRAMMENDE":GOSUB150
7040 HO=HM-11:VE=VM-3:GOSUB110
7050 PRINT"  1 - Daten speichern"
7060 HO=HM-11:VE=VM-1:GOSUB110
7070 PRINT"  2 - Ende"
7080 GOSUB200:IF(IN<49)OR(IN>50)THEN7080
7090 IFIN=49 THEN6000
7100 FR=0:GOSUB280:GOTO950
7450 REM
7460 REM VERTEILER
7470 REM
7500 IFIA=-1 THEN5000
7510 GOSUB100:HO=HM-9:VE=2:GOSUB110
7520 SR$="DATENTRANSFER":GOSUB150
7530 HO=HM-9:VE=VM-1:GOSUB110
7540 PRINT"1 - Daten laden"
7550 HO=HM-9:VE=VM+1:GOSUB110
7560 PRINT"2 - Daten speichern"
7570 GOSUB200:IF(IN<49)OR(IN>50)THEN7570
7580 IN=IN-48:ONIN GOTO5000,6000
7950 REM
7960 REM SKIPS LOESCHEN
7970 REM
8000 GOSUB100:R=0
8010 HO=HM-9:VE=2:GOSUB110
8020 SR$="SKIP LOESCHEN":GOSUB150
8030 IFIA=-1 THEN10000
8040 HO=0:VE=7:GOSUB110
8050 GOSUB120:INPUT"  Skip    :";HP$
8060 IFHP$="" THEN1070
8070 GOSUB110:PRINT"  Skip    : ";HP$+LL$
8080 REM SKIP IN TABELLE SUCHEN
8090 I=0
8100 IFA$(I)=HP$ THEN8180
8110 I=I+1:IFIA>I THEN8100
8120 HO=HM-11:GOSUB110
8130 IFR=1 THEN1070:REM NUR EINEN SKIP!
8140 PRINT"Nichts weiter gefunden!"
8150 SD=20:GOSUB450
8160 GOTO1070
8170 REM GEFUNDEN! -> ANZEIGEN...
8180 HO=0:VE=8:GOSUB110
8190 PRINT"  Vorname : ";B$(I);
8200 L=LEN(B$(I)):PRINTRIGHT$(L2$,10-L)
8210 PRINT"  QTH     : ";C$(I);
8220 L=LEN(C$(I)):PRINTRIGHT$(L2$,20-L)
8230 PRINT"  Stamm   : ";D$(I);"  "
8240 PRINT:PRINT:GOSUB120:HO=HM-9:GOSUB110
8250 PRINT"Satz loeschen (J/N)"
8260 GOSUB200:IF(IN<>74)AND(IN<>78)THEN8260
8270 IFIN=78 THEN8110:REM NEIN!
8280 REM
8290 REM Geloescht wird, indem der zu
8300 REM loeschende Datensatz durch den
8310 REM letzten gueltigen Datensatz
8320 REM ersetzt wird und die Anzahl
8330 REM der gueltigen Saetze um 1 ver-
8340 REM mindert wird
8350 REM
8360 A$(I)=A$(IA):B$(I)=B$(IA)
8370 C$(I)=C$(IA):D$(I)=D$(IA)
8380 IA=IA-1:I=IA+1:R=1:GOTO8110
8950 REM
8960 REM SKIPS SUCHEN
8970 REM
9000 GOSUB100:T=0:REM NORMALE SUCHE
9010 HO=HM-9:VE=2:GOSUB110
9020 SR$="SKIPS SUCHEN":GOSUB150
9030 IFIA=-1 THEN10000
9040 HO=0:VE=6:GOSUB110:I=0
9050 GOSUB120:INPUT"  Skip    :";HP$
9060 IFHP$="" THEN1070
9070 GOSUB110:PRINT"  Skip    : ";HP$+LL$
9080 IFLEN(HP$)>20 THENHP$=LEFT$(HP$,20)
9090 IFHP$=A$(I)THEN9140
9100 I=I+1:IFI<=IA THEN9090
9110 HO=HM-13:VE=VM:GOSUB110
9120 PRINT"   Ende der Datei...     "
9130 SD=20:GOSUB450:GOTO1070
9140 HO=0:VE=7:GOSUB110
9150 PRINT"  Vorname : ";B$(I);
9160 L=LEN(B$(I)):PRINTRIGHT$(L2$,10-L)
9170 PRINT"  QTH     : ";C$(I);
9180 L=LEN(C$(I)):PRINTRIGHT$(L2$,20-L)
9190 PRINT"  Stamm   : ";D$(I);"  "
9200 HO=HM-12:VE=VM:GOSUB110
9210 PRINT"Suche fortsetzen (J/N) ?"
9220 GOSUB200:IF(IN<>74)AND(IN<>78)THEN9220
9230 GOSUB250
9240 IF(IN=78)AND(T=0)THEN1070
9250 REM "SUCHEN" wird durch "AENDERN" aufgerufen (T=1)
9260 IF(IN=78)AND(T=1)THENRETURN
9270 GOTO9100
9950 REM
9960 REM UP KEINE DATEN
9970 REM
10000 HO=HM-7:VE=VM:GOSUB110
10010 PRINT"Keine Daten..."
10020 SD=50:GOSUB450:GOTO1070
10950 REM
10960 REM SKIPS AENDERN
10970 REM
11000 GOSUB100:HO=HM-9:VE=2:GOSUB110
11010 SR$="Skips aendern":GOSUB150
11020 IFIA=-1 THEN10000
11030 T=1:GOSUB9040:REM "SUCHEN"
11040 HO=2:VE=VM:GOSUB110
11050 PRINT"NEU :                        "
11060 PRINT:GOSUB120
11070 A$(AN+1)="":INPUT"  Skip    :";A$(AN+1)
11080 IF(A$(AN+1)<>"")THENA$(I)=A$(AN+1)
11090 GOSUB110
11100 PRINT"  Skip    : ";A$(I)+LL$
11110 GOSUB120
11120 B$(AN+1)="":INPUT"  Vorname :";B$(AN+1)
11130 IF(B$(AN+1)<>"")THENB$(I)=B$(AN+1)
11140 GOSUB110
11150 PRINT"  Vorname : ";B$(I)+LL$
11160 GOSUB120
11170 C$(AN+1)="":INPUT"  QTH     :";C$(AN+1)
11180 IF(C$(AN+1)<>"")THENC$(I)=C$(AN+1)
11190 GOSUB110
11200 PRINT"  QTH     : ";C$(I)+LL$
11210 GOSUB120
11220 D$(AN+1)="":INPUT"  Stamm   :";D$(AN+1)
11230 IF(D$(AN+1)<>"")THEND$(I)=D$(AN+1)
11240 GOSUB110
11250 PRINT"  Stamm   : ";D$(I)+LL$
11260 HO=H0-10:VE=V0-1:GOSUB110
11270 SR$="TASTE":GOSUB150
11280 GOSUB200:IFIN=0 THEN11280
11290 GOTO1070
30000 REM -----------------------------
30010 REM     VERWENDETE VARIABLEN
30020 REM -----------------------------
30030 REM HM,VM = MERKER FUER HO/2,VE/2
30040 REM H0,V0 = MERKER FUER HO,VE
30050 REM A$-D$ = DATENSTRINGS
30060 REM L1$   = LEERSTRING(20 Z.)
30070 REM L2$   = LEERSTRING(6 Z.)
30080 REM HP$   = HILFSSTRING
30090 REM AN    = MAX. DATENSAETZE
30100 REM IA    = VORH. DATENSAETZE
30110 REM PA    = DRUCKSEITE
30120 REM Z     = ZAEHLER GESAMT
30130 REM Z1    = ZAEHLER ZEILEN/SEITE
30140 REM HP$   = HILFSSTRING
30150 REM K,T   = HILFSVARIABLEN
30160 REM L     = STRING-LAENGE
30170 REM I,J   = LAUFVARIABLEN
30180 REM -----------------------------
30190 REM Das Programm stellt eine Da-
30200 REM tenverwaltung dar, die dem
30210 REM CB-Funker eine Hilfe sein
30220 REM soll. Das Programm soll zum
30230 REM Experimentieren anregen.
30240 REM An Verbesserungen ist der
30250 REM Autor stets interessiert.
30260 REM -----------------------------
30270 REM Der Druck ist auf Einzelblatt
30280 REM ausgelegt.
30290 REM ACHTUNG! Paper-Out-Sensor ist
30300 REM zu desaktivieren; Auto-Lining
30310 REM (LF=CHR$(10)) muss aktiv sein
30320 REM (eventuell Steuerzeichen in
30330 REM Zeile 2480-2500 einfuegen)!
30340 REM Drucker, die kein Auto-LF be-
30350 REM sitzen, brauchen in den Zei-
30360 REM len 2540, 2580, 2620 und 2750
30370 REM ein zusaetzliches "GOSUB360"!
32000 REM ------------------------------
32010 REM Autor: Rene Bollinger
32020 REM        Beethovenstr. 3
32030 REM        O-1597 POTSDAM
32040 REM
32050 REM geschrieben:  14.-16.11.1990
32060 REM Rechnertyp:   ATARI 800XL
32070 REM getestet auf: KC 85/4
32080 REM ----------------------------------
32090 REM Erstausstrahlung: Deutschlandsender Kultur
32100 REM-spezial 910529

1000 A=1000:GOTO20:REM ####  LINDWURM ####
1010 DIMG(1000):HH=HO:VH=VE:PR=0
1020 X=0:M=INT(HH/2):N=INT(VH/2):E=M:F=N
1030 P=1:L=0:Q=1:Z=0
1040 FORI=0 TOHH
1050 VE=0:HO=I:GOSUB110:PRINT"O";
1060 VE=2:HO=I:GOSUB110:PRINT"O";
1070 VE=VH-1:HO=I:GOSUB110:PRINT"O";
1080 VE=VH-3:HO=I:GOSUB110:PRINT"O";
1090 NEXTI
1100 FORI=0 TOVH-1
1110 HO=0:VE=I:GOSUB110:PRINT"O";
1120 HO=2:VE=I:GOSUB110:PRINT"O";
1130 HO=HH:VE=I:GOSUB110:PRINT"O";
1140 HO=HH-2:VE=I:GOSUB110:PRINT"O";
1150 NEXTI
1160 HO=INT(HH/2-7):VE=INT(VH/2):GOSUB110
1170 SR$="LINDWURM":GOSUB150
1180 SD=1000:GOSUB450:GOSUB100
1190 REM ## AUFBAU DER GIFTPILZE ##
1200 FE=INT((HH+1)*(VH+1)/50)
1210 FORI=1 TOFE
1220 GOSUB260:HO=INT((HH-1)*RV)+1
1230 GOSUB260:VE=INT((VH-2)*RV)+1
1240 GOSUB110:PRINT"#";
1250 NEXTI
1260 REM #### WANDAUFBAU ####
1270 FORI=0 TOHH
1280 VE=0:HO=I:GOSUB110:PRINT"-";
1290 VE=VH-1:HO=I:GOSUB110:PRINT"-";
1300 NEXTI
1310 FORI=0 TOVH-1
1320 HO=0:VE=I:GOSUB110:PRINT"!";
1330 HO=HH:VE=I:GOSUB110:PRINT"!";
1340 NEXTI
1350 REM ###  AUFBAU DER PILZE ###
1360 FE=INT((HH+1)*(VH+1)/50):TA=0
1370 FORI=1 TOFE
1380 GOSUB260:HO=INT((HH-1)*RV)+1
1390 GOSUB260:VE=INT((VH-2)*RV)+1
1400 GOSUB220:IF(IN<>32) THEN1380
1410 GOSUB110:PRINT"*";
1420 NEXTI
1430 REM #### :GOSUB110:PRINT"O";
1440 HO=M:VE=N:GOSUB110:PRINT"O";
1450 REM  !SD=2:SV=15:SP=30:GOSUB400
1460 IF(((P-1)/FE)=INT((P-1)/FE))AND(Q=0) THENQ=1:GOTO1360
1470 Z=Z+1:GOSUB200:IF(IN<28)OR(IN>31) THENIN=TA
1480 TA=IN:IFIN=0 THEN1470
1490 ON(IN-27) GOTO1500,1580,1660,1740
1500 HO=M-1:VE=N:GOSUB220
1510 IFIN=79 THEN GOSUB1920:GOTO1440:REM "o"
1520 IFIN=33 THEN GOSUB1960:GOTO1440:REM "!"
1530 IFIN=35 THEN GOSUB2000:REM "#"
1540 IFIN=42 THENP=P+1:Q=0:GOSUB250:REM "*"
1550 HO=M:VE=N:GOSUB110:PRINT"o";
1560 M=M-1:G(X)=1:GOSUB1830:REM SCHWANZENDE
1570 GOTO1440
1580 HO=M+1:VE=N:GOSUB220
1590 IFIN=79 THEN GOSUB1920:GOTO1440:REM "o"
1600 IFIN=33 THEN GOSUB1960:GOTO1440:REM "!"
1610 IFIN=35 THEN GOSUB2000:REM "#"
1620 IFIN=42 THENP=P+1:Q=0:GOSUB250:REM "*"
1630 HO=M:VE=N:GOSUB110:PRINT"o";
1640 M=M+1:G(X)=2:GOSUB1830:REM SCHWANZENDE
1650 GOTO1440
1660 HO=M:VE=N+1:GOSUB220
1670 IFIN=79 THEN GOSUB1920:GOTO1440:REM "o"
1680 IFIN=45 THEN GOSUB1960:GOTO1440:REM "-"
1690 IFIN=35 THEN GOSUB2000:REM "#"
1700 IFIN=42 THENP=P+1:Q=0:GOSUB250:REM "*"
1710 HO=M:VE=N:GOSUB110:PRINT"o";
1720 N=N+1:G(X)=3:GOSUB1830:REM SCHWANZENDE
1730 GOTO1440
1740 HO=M:VE=N-1:GOSUB220
1750 IFIN=79 THEN GOSUB1920:GOTO1440:REM "o"
1760 IFIN=45 THEN GOSUB1960:GOTO1440:REM "-"
1770 IFIN=35 THEN GOSUB2000:REM "#"
1780 IFIN=42 THENP=P+1:Q=0:GOSUB250:REM "*"
1790 HO=M:VE=N:GOSUB110:PRINT"o";
1800 N=N-1:G(X)=4:GOSUB1830:REM SCHWANZENDE
1810 GOTO1440
1820 REM ####   SCHWANZENDE  ####
1830 Y=X-P:IFY<0 THENY=Y+1000
1840 IFG(Y)=1 THENE=E-1
1850 IFG(Y)=2 THENE=E+1
1860 IFG(Y)=3 THENF=F+1
1870 IFG(Y)=4 THENF=F-1
1880 G(Y)=0
1890 HO=E:VE=F:GOSUB110:PRINT" ";
1900 X=X+1:IFX=1000 THENX=0
1910 RETURN
1920 GOSUB2050:HO=0:VE=VH-1:GOSUB110
1930 PRINT"SIE BISSEN SICH IN DEN SCHWANZ";:SD=40:GOSUB450
1940 GOSUB110:PRINT"!-----------------------------";
1950 RETURN
1960 GOSUB2050:HO=0:VE=VH-1:GOSUB110
1970 PRINT"SIE LIEFEN GEGEN DIE WAND";:SD=40:GOSUB450
1980 GOSUB110:PRINT"!-------------------------";
1990 RETURN
2000 GOSUB2050:HO=0:VE=VH-1:GOSUB110
2010 PRINT"SIE FRASSEN GIFT";:SD=40:GOSUB450
2020 GOSUB110:PRINT"!---------------------------";
2030 RETURN
2040 REM ####  LEBENABZUG  ####
2050 GOSUB220
2060 GOSUB110:PRINT"X";:GOSUB250
2070 GOSUB110:PRINT" ";:GOSUB110:PRINT"X";:GOSUB250
2080 GOSUB110:PRINTCHR$(IN);
2090 SD=4:SV=15
2100 SP=67:GOSUB400
2110 SP=64:GOSUB400
2120 SP=60:GOSUB400
2130 L=L+1:IFL=5 THEN2170
2140 TA=0
2150 RETURN
2160 REM #### AUSWERTUNG ####
2170 GOSUB100:HO=HH/2-5:VE=0:GOSUB110
2180 SR$="AUSWERTUNG":GOSUB150
2190 PP=P-INT(Z/200)
2200 VE=VH/2:HO=0:GOSUB110
2210 PRINT"  SIE ERREICHTEN ";PP;" PUNKTE"
2220 PRINT
2230 IFPP<=PR THENPRINT"  Record";PR
2240 IFPP>PR THENPR=PP:PRINT"  Neuer Rekord  ";PR
2250 PRINT:PRINT
2260 PRINT"  WOLLEN SIE NOCHEINMAL":PRINT
2270 PRINT"  SPIELEN (J/N)?";
2280 GOSUB210
2290 IFIN=78 THEN2310
2300 GOSUB100:GOTO1020
2310 GOTO950
30000 REM **********************
30010 REM * KURZBESCHHREIBUNG: *
30020 REM *                    *
30030 REM * "*" = 1 PUNKT      *
30040 REM * "#" = 1 LEBEN WEG  *
30050 REM * "o" = 1 LEBEN WEG  *
30060 REM * "-" = 1 LEBEN WEG  *
30070 REM * "!" = 1 LEBEN WEG  *
30080 REM * ZEIT=PUNKTEABZUG   *
30090 REM **********************
30100 REM * PROGRAMMDETAILS:   *
30110 REM * ANPASSUNG DER      *
30120 REM * GESCHWINDIGKEIT    *
30130 REM * AN EIGENEN RECHNER *
30140 REM * IN ZEILE 1450      *
30150 REM * SD ERHOEHEN ODER   *
30160 REM * ZEILE 1450 LOESCHEN*
30170 REM * ANZAHL DER LEBEN   *
30180 REM * IN ZEILE 2130      *
30190 REM * FESTGELEGT         *
30200 REM **********************
32000 REM **********************
32010 REM *    DER LINDWURM    *
32020 REM **********************
32030 REM * SPIELIDEE:         *
32040 REM * LINDI AUS          *
32050 REM * SPIELBOX 4 ZUM     *
32060 REM * KC-compact         *
32070 REM **********************
32080 REM * AUTOR:             *
32090 REM * Thomas Schaefer    *
32100 REM * H.-Heine-Str.42d   *
32110 REM * Perleberg          *
32120 REM * 2910               *
32130 REM **********************
32140 REM * ERSTELLT AUF       *
32150 REM * MRB - Z  1 0 1 3   *
32160 REM * FERTIGGESTELLT AM  *
32170 REM * 25.06.1990         *
32180 REM * fuer               *
32190 REM * Rundfunk der DDR   *
32200 REM **********************
32210 REM
32220 REM Erstausstrahlung: DS-Kultur
32230 REM-spezial 900725

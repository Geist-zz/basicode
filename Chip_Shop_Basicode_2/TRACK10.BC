1000 A=100:GOTO20:REM  MEZIRAC SQUARES.
1010 GOSUB100
1020 PRINT:PRINT"The problem of:"
1030 PRINT:PRINT"CLAUDE GASPARD BACHET DE MEZIRAC"
1040 PRINT:PRINT"       ( 1581 - 1638 )"
1050 PRINT:PRINT
1060 PRINT:PRINT
1070 PRINT:PRINT"Write a number as the sum of"
1080 PRINT:PRINT"four whole squares."
1090 PRINT:PRINT:GOSUB2000
1100 GOSUB100
1110 PRINT:PRINT"Which number should I calculate";
1120 INPUTG
1130 IFG>0 THEN1160
1140 PRINT:PRINT"Only POSITIVE numbers !"
1150 GOTO1100
1160 IFG=INT(G) THEN1190
1170 PRINT:PRINT"Only WHOLE numbers !"
1180 GOTO1100
1190 PRINT:PRINT"OK, ";G;" SO...."
1200 REM 
1210 REM  *** SQUARES CALCULATION TO SQR(G) ***
1220 REM 
1230 WG=INT(SQR(G))
1240 DIMKW(WG),G(4)
1250 KW(0)=0
1260 FORI=1 TOWG
1270 KW(I)=KW(I-1)+I+I-1
1280 NEXTI
1290 REM 
1300 REM  *** FOUR NUMBERS SEARCH ***
1310 REM 
1320 FORG1=0 TOWG
1330 S1=KW(G1)
1340 FORG2=G1 TOWG
1350 S2=S1+KW(G2)
1360 FORG3=G2 TOWG
1370 S3=S2+KW(G3)
1380 FORG4=G3 TOWG
1390 S4=S3+KW(G4)
1400 IFS4<G THEN1630
1410 IFS4>G THEN1620
1420 REM 
1430 REM  *** FOUND ***
1440 REM 
1450 VV=0:G(0)=-1
1460 SR=G:GOSUB300
1470 PRINT:PRINT:PRINTSR$;" = ";
1480 G(1)=G1:G(2)=G2:G(3)=G3:G(4)=G4
1490 FORI=1 TO4
1500 IFG(I)=G(I-1) THENVV=1
1510 SR=G(I):GOSUB300
1520 PRINTSR$;:GOSUB120
1530 VE=VE-1:GOSUB110:PRINT"2";
1540 VE=VE+1:HO=HO+1:GOSUB110
1550 IFI<4 THENPRINT" + ";
1560 NEXTI
1570 IFVV=0 THENPRINT"   ><";
1580 PRINT
1590 REM 
1600 REM  *** NEW G3 ***
1610 REM 
1620 G4=WG
1630 NEXTG4
1640 NEXTG3
1650 NEXTG2
1660 NEXTG1
1670 END
2000 PRINT:PRINT"Press SPACE to continue.";
2010 GOSUB210:IFIN$<>" " THEN2010
2020 RETURN
30000 REM "THIS PROGRAM WAS WRITTEN"
30010 REM "IN THE NETHERLANDS BY  "
30020 REM "MEMBERS OF THE BASICODE  "
30030 REM "TEAM. IT DEMONSTRATES AN"
30040 REM "EDUCATIONAL USE OF BASICODE"
30050 REM "TRANSLATED FOR RADIO 4 "
30060 REM "BY JONATHAN MARKS.   "
30070 REM "COPYRIGHT FREE."

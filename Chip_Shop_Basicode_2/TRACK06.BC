1000 A=500:GOTO20:REM WORD SEARCH
1010 S=1:P=0:KR=0
1020 T$="ABCDEFGHIJKLMNOPQRSTUVWXYZ"
1030 HT$="abcdefghijklmnopqsrtuvwxyz"
1040 HK=ASC(LEFT$(T$,1))-ASC(LEFT$(HT$,1))
1050 DIMR$(8,8)
1060 GOSUB100
1070 PRINT"W  O  R  D     S  E  A  R  C  H"
1080 PRINT
1090 PRINT"Need instructions (Y/N)";
1100 INPUTA$:A$=LEFT$(A$,1)
1110 IFA$="Y" ORA$="y" THENGOSUB2810
1120 GOSUB1230:REM ?DIFFICULTY.
1130 GOSUB1380:REM WORD SEARCH
1140 GOSUB1480:REM SEARCH DESIGN
1150 GOSUB1940:REM PRINT MATRIX
1160 GOSUB2080:REM ASK SOLUTION.
1170 IFXO=0 THEN1150
1180 GOSUB2540:REM SCORES
1190 IFXO=0 THEN2700
1200 GOSUB2510:REM SETUP NEXT
1210 GOTO1120:REM NEW GAME
1220 REM ASK DEGREE DIFFICULTY AND WORDLENGTH
1230 PRINT
1240 PRINT"How difficult should I make it"
1250 PRINT"(1 means easy, 2 means harder)";
1260 INPUTM
1270 IFM<1 THEN1230
1280 IFM>2 THEN1230
1290 PRINT
1300 PRINT"How long should the word be  "
1310 PRINT"(between 3 and 7 letters !)";
1320 INPUTL
1330 IFL<3 THEN1290
1340 IFL>7 THEN1290
1350 N=L+1
1360 RETURN
1370 REM SEARCH FOR WORD OF CORRECT LENGTH
1380 RESTORE
1390 READA$
1400 IFLEN(A$)<>L THEN1390
1410 GOSUB260:Q=15*RV+1
1420 FORX=1 TOQ:READW$:NEXTX
1430 HW$="":FORI=1 TOL
1440 HW$=HW$+CHR$(HK+ASC(MID$(W$,I,1)))
1450 NEXTI
1460 RETURN
1470 REM A WORD-PAD NEAR DEGREE OF DIFFICULTY
1480 X=0
1490 GOSUB260:R=INT(RV*N+1)
1500 GOSUB260:K=INT(RV*N+1)
1510 IFR<3 ORR>N-2 THEN1570
1520 IFK<3 ORK>N-2 THEN1570
1530 X=X+1
1540 IFX=20 THENSTOP
1550 REM AFTER 20 GOES STOP PROGRAM
1560 GOTO1490
1570 FORX=1 TON
1580 FORY=1 TON
1590 GOSUB260:Q=INT(RV*26+1)
1600 R$(X,Y)=MID$(HT$,Q,1)
1610 NEXTY
1620 NEXTX
1630 REM FILL IN THE WORD
1640 IFM=2 THEN1820
1650 REM IF DEGREE OF DIFFICULTY IS 1 HOR. OR VERT.
1660 IFR<3 ORR>N-2 THEN1750
1670 REM HORIZONTAL WORD THEN
1680 IFK<3 THENKR=1
1690 IFK>N-2 THENKR=-1
1700 FORX=1 TOLEN(W$)
1710 R$(K,R)=MID$(W$,X,1)
1720 K=K+KR
1730 NEXTX
1740 RETURN
1750 IFR<3 THENRR=1
1760 IFR>N-2 THENRR=-1
1770 FORX=1 TOLEN(W$)
1780 R$(K,R)=MID$(W$,X,1)
1790 R=R+RR
1800 NEXTX
1810 RETURN
1820 REM DIFFICULTY DEGREE 2 DIAGONAL
1830 IFR<3 THENRR=1
1840 IFR>N-2 THENRR=-1
1850 IFK<3 THENKR=1
1860 IFK>N-2 THENKR=-1
1870 FORX=1 TOLEN(W$)
1880 R$(K,R)=MID$(W$,X,1)
1890 K=K+KR
1900 R=R+RR
1910 NEXTX
1920 RETURN
1930 REM PRINTED SECTION
1940 GOSUB100
1950 PRINT"====================";
1960 PRINT"  DIFFICULTY=";M
1970 FORX=1 TON
1980 PRINT
1990 PRINT"     ";
2000 FORY=1 TON:PRINTR$(X,Y);" ";:NEXTY
2010 PRINT
2020 NEXTX
2030 PRINT
2040 PRINT"====================";
2050 PRINT"  LENGTH=";L
2060 RETURN
2070 REM ASK FOR SOLUTION
2080 PRINT
2090 PRINT"What is the word";
2100 INPUTA$
2110 P=P+1
2120 IFA$=W$ ORA$=HW$ THEN2410
2130 PRINT
2140 PRINT"Sorry, that is NOT the right answer."
2150 IF(P-2)=L THEN2320
2160 IFP>2 THEN2200
2170 PRINT
2180 PRINT"T r y  a g a i n "
2190 GOTO2270
2200 REM ER IS AL >2 MAAL MISGERADEN
2210 H$=LEFT$(W$,P-2)
2220 PRINT
2230 PRINT"I think I'd better help.":PRINT
2240 IFP>3 THEN2290
2250 PRINT"The first letter is: ";H$
2260 REM XO=0 MEANS INCORRECT SOLUTION
2270 XO=0:FORI=1 TO3000:NEXTI
2280 RETURN
2290 REM GIVE MORE THAN 1 LETTER
2300 PRINT"The first ";P-2;" letters are ";H$
2310 GOTO2270
2320 REM NOT GUESSED IN TIME
2330 PRINT:PRINT"Sorry, but you haven't managed"
2340 PRINT:PRINT"to guess in ";P;" goes."
2350 FORI=1 TO3000:NEXTI:GOSUB1930
2360 PRINT:PRINT"It was ";W$
2370 PRINT:PRINT"Continue ";
2380 INPUTA$
2390 XO=-1
2400 RETURN
2410 REM CORRECT ANSWER
2420 GOSUB100
2430 PRINT"Well done, that is correct  !!!"
2440 PRINT
2450 PRINT"You made ";P;" attempt";
2460 IFP>1 THENPRINT"s."
2470 PRINT
2480 XO=1
2490 RETURN
2500 REM MAKE EVERYTHING READY FOR NEW GAME
2510 S=S+1:RR=0:KR=0:P=0
2520 RETURN
2530 REM REL.SCORE, AV.SCORE AND NEW GAME?
2540 PRINT
2550 T=T+(L/P)*(L/P)*M
2560 PRINT"The score in the game was ";(L/P)*(L/P)*M
2570 PRINT"(Maximum possible was ";L*L*M;")"
2580 PRINT
2590 PRINT"The average score from"
2600 PRINT"ALL games ";T/S
2610 PRINT
2620 PRINT
2630 PRINT"Another game ";
2640 INPUTA$:A$=LEFT$(A$,1)
2650 PRINT
2660 IFA$="Y" ORA$="y" THENXO=1:RETURN
2670 IFA$="N" ORA$="n" THENXO=0:RETURN
2680 PRINT"Type YES or NO please"
2690 GOTO2620
2700 REM SIGN OFF
2710 GOSUB100
2720 PRINT"The FINAL score is    ";T
2730 PRINT"Number of games       ";S
2740 PRINT"Average score         ";T/S
2750 PRINT
2760 PRINT
2770 PRINT"I hope you enjoyed the game."
2780 PRINT
2790 PRINT"Goodbye from the Chip Shop team...."
2800 END
2810 REM  INSTRUCTIONS
2820 GOSUB100
2830 PRINT"Try and find the hidden word"
2840 PRINT"in the square block of letters."
2850 PRINT"It may be hidden in various ways,"
2860 PRINT"so you should search :"
2870 PRINT
2880 PRINT"HORIZONTAL OR VERTICAL"
2890 PRINT"   .......       ......."
2900 PRINT"   .......       ...h..."
2910 PRINT"   .......       ...c..."
2920 PRINT"   .search       ...r..."
2930 PRINT"   .......       ...a..."
2940 PRINT"   .......       ...e..."
2950 PRINT"   .......       ...s..."
2960 PRINT:PRINT"or DIAGONAL"
2970 PRINT"   s......"
2980 PRINT"   .e....."
2990 PRINT"   ..a...."
3000 PRINT"   ...r..."
3010 PRINT"   ....c.."
3020 PRINT"   .....h."
3030 PRINT"   ......."
3040 PRINT
3050 PRINT"Continue ";
3060 GOSUB210
3070 GOSUB100
3080 PRINT"If you choose the easy option 1"
3090 PRINT"then I will hide words in the square"
3100 PRINT"either vertically, or"
3110 PRINT"horizontally."
3120 PRINT"If you select (2) then I may"
3130 PRINT"also hide the word diagonally."
3140 PRINT"So you have a choice."
3150 PRINT
3160 PRINT"If you do not guess the word"
3170 PRINT"after two goes, I will start to help."
3180 PRINT"You will get the first letter,"
3190 PRINT"then the second, and finally"
3200 PRINT"the complete word."
3210 PRINT"With every letter I give, though, "
3220 PRINT"the possible score goes down."
3230 PRINT
3240 PRINT"Continue";
3250 GOSUB210
3260 RETURN
25000 REM THE WORDS
25010 DATA  "333","one","pop","got","eel","not"
25020 DATA  "fit","try","put"
25030 DATA  "now","top","mop","hot","bus","car","dot"
25040 DATA  "4444","four","loop","make","good","hope","bark"
25050 DATA  "fine","clap","nose","ears","deck","park","book"
25060 DATA  "loud","lamp","door"
25070 DATA  "55555","tiles","grate","clear","apple","marks"
25080 DATA  "speed","slope","beast","horse","table","stool"
25090 DATA  "sleep","radio","water","train"
25100 DATA  "666666","minute","growth","midday","grudge"
25110 DATA  "hunger","rubber","bottle","mouldy","though"
25120 DATA  "sounds","danger","thirty","snooze","thread"
25130 DATA  "vacuum"
25140 DATA  "7777777","kitchen","utility","vulture"
25150 DATA  "gateway","recital","meeting","soapsud"
25160 DATA  "samples","licence","isolate","station"
25170 DATA  "garbage","targets","mistake","signify"
30000 REM  W O R D  S E A R C H 
30010 REM  
30020 REM  WRITTEN IN BASICODE BY
30030 REM    JOS BURGHOUTS
30040 REM       ON  AN
30050 REM    O.S.I. SUPERBOARD.
30060 REM  
30070 REM  TRANSLATED BY JONATHAN 
30080 REM  MARKS FOR THE CHIP SHOP

1000 A=100:GOTO20:REM TANK SHOOTING
1010 DIMS(3)
1080 GOSUB100:HO=11:VE=6:GOSUB110:PRINT"Tank Shooting"
1090 VE=7:GOSUB110:PRINT"-------------"
1100 VE=18:HO=0:GOSUB110:PRINT"Instructions? y/n ";
1110 INPUTIN$
1120 IFIN$="y" ORIN$="Y" THENGOSUB6000:GOTO2000
2000 GOSUB100
2010 V=0:T1=5:T2=5:T3=5
2020 FORI=1 TO39:PRINT"*";:NEXTI
2030 HO=39:FORVE=0 TO22:GOSUB110:PRINT"*";:NEXTVE
2040 VE=23:FORHO=38 TO1 STEP-1:GOSUB110:PRINT"*";:NEXTHO
2050 HO=0:FORVE=22 TO1 STEP-1:GOSUB110:PRINT"*";:NEXTVE
2060 FORVE=3 TO17 STEP3
2070 HO=1:GOSUB110:PRINT"    <>---*"
2080 VE=VE+1:GOSUB110:PRINT" ====";CHR$(48+(VE+3)/7);
2090 HO=0:VE=VE+1:GOSUB110
2100 PRINTCHR$(48+T1);" ======="
2110 HO=1:VE=VE+1:GOSUB110:PRINT" <''''''> "
2120 VE=VE+1:GOSUB110:PRINT"  ''''''"
2130 NEXTVE
2140 VE=0:HO=12:GOSUB110:PRINT" Wait     seconds "
2150 FORI=5 TO1 STEP-1:HO=18:VE=0:GOSUB110:PRINTCHR$(48+I)
2160 FORJ=1 TO500:NEXTJ:NEXTI
3000 FORWW=20 TO1 STEP-1
3010 IFT1+T2+T3=0 THEN5000
3020 VE=23:HO=10:GOSUB110
3030 PRINT"****************************";
3040 VE=0:HO=12:GOSUB110
3045 SR=WW:GOSUB300
3050 PRINT"Only ";SR$" targets left"
3060 GOSUB260:L=5*INT(12*RV)
3070 GOSUB260:LA=13+INT(26*RV)
3080 GOSUB200:IFIN$<>"" THEN3080
3090 HO=LA
3100 FORVE=1 TO22
3110 GOSUB110
3120 PRINT"=";:KL=160
3130 FORJ=1 TOL:NEXTJ
3140 GOSUB200:IFIN$<>"" THENGOSUB3500
3150 IFV=1 THENV=0:GOTO3240
3160 GOSUB110:PRINT"-";:KL=98
3170 VE=VE+1:GOSUB110:PRINT"-";
3180 FORJ=1 TOL:NEXTJ
3190 GOSUB200:IFIN$<>"" THENGOSUB3500
3200 IFV=1 THENV=0:GOTO3240
3210 VE=VE-1:GOSUB110:PRINT" "
3220 NEXTVE
3230 FORI=1 TO2000:NEXTI
3240 NEXTWW:GOTO5000
3500 Y=VAL(IN$):IFY<1 ORY>3 THENRETURN
3510 IFVE=7*Y-3 ANDKL=98 THEN3540
3520 IFVE=7*Y-4 ANDKL=160 THEN3540
3530 KL=32
3540 ONY GOTO3600,3700,3800
3600 IFT1=0 THENRETURN
3610 T1=T1-1:HO=0:VE=5:GOSUB110:PRINTCHR$(48+T1)
3620 VE=3
3630 GOTO3830
3700 IFT2=0 THENRETURN
3710 T2=T2-1:HO=0:VE=12:GOSUB110:PRINTCHR$(48+T2)
3720 VE=10
3730 GOTO3830
3800 IFT3=0 THENRETURN
3810 T3=T3-1:HO=0:VE=19:GOSUB110:PRINTCHR$(48+T3)
3820 VE=17
3830 FORHO=12 TO37:GOSUB110:PRINT" .";:NEXTHO
3840 HO=12:FORVE=1 TO22:GOSUB110
3850 PRINT"                           "
3860 NEXTVE
3870 GOSUB4000
3880 V=1:RETURN
4000 VE=23:HO=16:GOSUB110
4010 IFKL=32 THENPRINT" missed ! ";:GOTO4050
4020 IFKL=98 THENPRINT"  hit ! ";:S(Y)=S(Y)+1:GOTO4050
4030 IFKL=160 THENPRINT" direct hit ! ";
4040 S(Y)=S(Y)+2
4050 IFS(Y)<>10 THEN4110
4060 VE=7*Y-2:HO=39:GOSUB110
4070 PRINT"0"
4080 VE=7*Y-3:HO=39:GOSUB110
4090 PRINT"1"
4100 RETURN
4110 VE=7*Y-2:HO=39:GOSUB110
4120 PRINTCHR$(48+S(Y))
4130 FORI=1 TO2000:NEXTI
4140 RETURN
5000 O=INT(100*(S(1)+S(2)+S(3))/(2*(19-WW)))
5010 HO=3:VE=0:GOSUB110
5020 PRINT"  You scored an average of ";O;"%"
5030 HO=9:VE=23:GOSUB110
5040 PRINT"Another game  ? (y/n)";
5050 GOSUB210
5060 IFIN$="y" ORIN$="Y" THENRUN
5070 IFIN$<>"n" ANDIN$<>"N" THEN5050
5080 GOSUB100:PRINT:PRINT:PRINT
5090 PRINT"Goodbye then...":PRINT:PRINT:PRINT
5100 END
6000 GOSUB100:PRINT
6010 PRINT"TANK SHOOTING is a game of skill. ":PRINT
6020 PRINT"You get three tanks, each with 5 shots":PRINT
6030 PRINT"and you must aim to hit the moving":PRINT
6040 PRINT"targets you see.":PRINT:PRINT:PRINT
6050 PRINT"You can totally, half or not destroy"
6060 PRINT:PRINT"the target, for which you get 2,1,or 0 "
6070 PRINT:PRINT"points."
6080 PRINT:PRINT:PRINT:PRINT
6090 PRINT"Press any key to continue":GOSUB210
6100 GOSUB100:PRINT
6110 PRINT"You get 20 targets per game.":PRINT
6120 PRINT"To fire, press the number of the tank":PRINT
6130 PRINT"as seen on the screen.":PRINT
6140 PRINT:PRINT"The number of targets is displayed"
6150 PRINT:PRINT"on the left, the score on the right.":PRINT
6160 PRINT:PRINT:PRINT:PRINT:PRINT:PRINT:PRINT:PRINT
6170 PRINT"Press any key to start the game."
6180 GOSUB210
6190 RETURN
30000 REM   **********************
30010 REM   *                    *
30020 REM   *    TANK SHOOTING   *
30030 REM   *                    *
30040 REM   **********************
30050 REM   WRITTEN BY JOS LEPPENS
30060 REM   KORTE HEERSTRAAT 6
30070 REM   3580 NEERPELT - BELGIUM
30080 REM   Translated for the 
30090 REM   Chip-Shop, Radio 4
30100 REM   by Jonathan Marks

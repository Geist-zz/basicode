
1000 A=500:GOTO20:REM KORFBAL DEEL 1
1010 DIMCR$(3),BH(30):DIMBV(30),Z$(7)
1020 GOSUB10010
1030 HO=10:VE=3:GOSUB110
1040 PRINT"K  O  R  F  B  A  L"
1050 HO=17:VE=6:GOSUB110
1060 PRINT"door"
1070 HO=2:VE=10:GOSUB110
1080 PRINT"Martin van Luijk en Pieter van Dijk"
1090 HO=14:VE=12:GOSUB110
1100 PRINT"----------"
1110 HO=14:VE=14:GOSUB110
1120 PRINT"----------"
1130 L$="DRUK OP DE SPATIEBALK  DRUK OP"
1140 FORI=1 TOLEN(L$)-10
1150 HO=14:VE=13:GOSUB110
1160 PRINTMID$(L$,I,10)
1170 W=0.3:GOSUB11060
1180 GOSUB200:IFIN$=" " THENI=LEN(L$)-9:GOTO1210
1190 GOSUB11080:NEXTI
1200 GOTO1140
1210 GOSUB100
1220 HO=0:VE=2:GOSUB110
1230 PRINT"Dit programma bestaat uit 2 delen."
1240 PRINT"----------------------------------":PRINT
1250 PRINT"In het 1e deel maak je kennis met de"
1260 PRINT:PRINT"theorie van het zaalkorfbal."
1270 PRINT:PRINT:PRINT"In het 2e deel kun je je kennis"
1280 PRINT:PRINT"van de spelregels testen.":PRINT:PRINT
1290 PRINT:PRINT"Als je dit stukje gelezen hebt, druk"
1300 PRINT:PRINT"dan op de spatiebalk..."
1310 GOSUB10210
1320 GOSUB100
1330 HO=10:VE=10:GOSUB110
1340 PRINT"DEEL I, de spelregels"
1350 GOSUB10210
1360 GOSUB100
1370 GOSUB10340
1380 PRINT" Een korfbalveld ziet er zo uit:"
1390 HO=0:VE=0:GOSUB110
1400 GOSUB10050
1410 GOSUB10210
1420 GOSUB10340
1430 PRINT" De middellijn verdeelt het veld in"
1440 PRINT" twee vakken van 2O x 2O m."
1450 GOSUB10100:REM MIDDELLIJN
1460 GOSUB10210
1470 GOSUB10340
1480 PRINT" Die vakken zijn bij korfbal heel  "
1490 PRINT" belangrijk.Dat zul je straks merken."
1500 GOSUB10210
1510 GOSUB10340
1520 PRINT" In elk vak bevindt zich een doel:"
1530 PRINT" dat is een open korf,die op een hoogte"
1540 PRINT" van 3,5O m aan een paal bevestigd is."
1550 GOSUB10160
1560 GOSUB10210
1570 GOSUB10340
1580 PRINT" De paal staat bijna 7 meter vanaf     "
1590 PRINT" de eindlijn, zodat je er omheen kunt  "
1600 PRINT" spelen.                              "
1610 GOSUB10210
1620 HO=0:VE=17:GOSUB110
1630 PRINT"  x x x x x x x x     + + + + + + + +"
1640 PRINT"                                       "
1650 PRINT"Het spel wordt gespeeld door 2 partijen"
1660 PRINT"van 8 spelers. Elk achttal bestaat uit "
1670 PRINT"4 dames en 4 heren."
1680 GOSUB10210
1690 GOSUB10340
1700 PRINT
1710 PRINT" In beide vakken stellen zich          "
1720 PRINT" 8 spelers op, van elke partij         "
1730 PRINT" 2 dames en 2 heren."
1740 FORP=1 TO4:GOSUB10490:NEXTP
1750 FORP=1 TO4:GOSUB10590:NEXTP
1760 GOSUB10220
1770 HO=4:FORVE=3 TO14:GOSUB110
1780 READZ$:PRINTZ$:NEXTVE
1790 GOSUB10610
1800 GOSUB10210
1810 FORP=1 TO21:READHO,VE:BH(P)=HO:BV(P)=VE
1820 GOSUB110:PRINT"o"
1830 W=0.1:GOSUB11080
1840 GOSUB110:PRINT" ":NEXTP
1850 GOSUB110:PRINT"o"
1860 GOSUB10590
1870 GOSUB10210
1880 FORP=21 TO1 STEP(-1):HO=BH(P):VE=BV(P)
1890 GOSUB110:PRINT"o"
1900 GOSUB110:PRINT" ":NEXTP
1910 GOSUB110:PRINT"*"
1920 GOSUB10220:GOSUB100:GOSUB10040:GOSUB10170
1930 GOSUB10610
1940 GOSUB10220
1950 READHO,VE:GOSUB110:PRINT"oa"
1960 READHO,VE:GOSUB110:PRINT"a"
1970 FORN=1 TO2:V$="o"
1980 K=10:GOSUB10740
1990 J=2:GOSUB10660:READHO,VE:GOSUB110:PRINT"o"
2000 V$="a"
2010 J=6:GOSUB10660:READHO,VE:GOSUB110:PRINT"a"
2020 NEXTN
2030 V$="o"
2040 J=3:GOSUB10650
2060 K=10:GOSUB10740
2070 J=1:GOSUB10650
2080 K=20:GOSUB10740
2090 GOSUB110:PRINT"O"
2100 GOSUB10230:GOSUB100:GOSUB10040:GOSUB10170
2110 GOSUB10610
2120 GOSUB10600:GOSUB10230
2130 FORK=1 TO5:READHO,VE,CR$
2140 GOSUB10460:NEXTK
2150 FORK=1 TO4
2160 GOSUB10610
2170 READX,Y,CR$(1),CR$(2)
2180 GOSUB10810
2190 HO=X:VE=Y:GOSUB110:PRINTCR$(2)
2200 NEXTK
2210 FORJ=1 TO3
2220 GOSUB10610
2230 GOSUB10210
2240 NEXTJ
2250 FORM=1 TO3
2260 GOSUB10720
2270 V$="o":READJ:GOSUB10660
2280 GOSUB10720
2290 IFM<>1 THEN2340
2300 GOSUB110:PRINT"o"
2310 HO=27:VE=11:GOSUB110:PRINT"a"
2320 HO=7:VE=11:GOSUB110:PRINT" "
2330 V$="v":READJ:GOSUB10660:HO=HO+1:GOSUB110:PRINTV$
2340 NEXTM
2350 GOSUB110:PRINTV$
2360 GOSUB10210
2370 GOSUB100:GOSUB10040:GOSUB10170
2380 FORJ=1 TO5
2390 GOSUB10610
2400 IFJ<>2 THENGOTO2450
2410 READX,Y,CR$(1),CR$(2)
2420 GOSUB10810
2430 HO=X:VE=Y:GOSUB110:PRINT"*"
2440 GOTO2470
2450 GOSUB10210
2470 NEXTJ
2480 FORK=1 TO2:READHO,VE,CR$
2490 GOSUB10460:NEXTK
2500 FORM=1 TO2
2510 GOSUB10720
2520 V$="o":READJ:GOSUB10660
2530 GOSUB10720
2540 GOSUB110:PRINT"o"
2550 V$="v":READJ:GOSUB10660
2560 GOSUB110:PRINTV$
2570 W=4:GOSUB11080
2580 NEXTM
2590 GOSUB100:GOSUB10040:GOSUB10170
2600 FORM=1 TO3
2610 GOSUB10610:GOSUB10200
2620 NEXTM
2630 FORM=1 TO8:GOSUB10450
2640 NEXTM
2650 GOSUB10210
2660 FORM=1 TO4:GOSUB10970:NEXTM
2670 GOSUB10210
2680 GOSUB10010
2690 Z$(1)="     S A M E N V A T T I N G":Z$(2)=" "
2700 Z$(3)="  Je hebt nu de belangrijkste":Z$(6)=" "
2710 Z$(4)="  spelregels gezien. We zullen":Z$(7)=" "
2720 Z$(5)="  ze nog eens kort samenvatten."
2730 GOSUB11020:GOSUB10210
2740 Z$(2)="* Niet lopen met de bal.       "
2750 Z$(3)="* Niet stuiten (dribbelen).   "
2760 Z$(4)="* De tegenstander niet aanraken,"
2770 Z$(5)="  duwen, of vasthouden.        "
2780 Z$(6)="* Niet met voet of been aan de bal"
2790 Z$(7)="  komen."
2800 GOSUB11020:GOSUB10210
2810 Z$(2)="* Niet 'gedekt' doelen.    "
2820 Z$(3)="* Niet in het andere vak komen."
2830 Z$(4)="* Na ieder doelpunt hervatting van"
2840 Z$(5)="  het spel vanaf de middenlijn."
2850 Z$(6)="* Na iedere twee doelpunten       "
2860 Z$(7)="  van vak wisselen."
2870 GOSUB11020:GOSUB10210
2880 Z$(2)="* Niet tegen de bal slaan."
2890 Z$(3)="* Bal niet uit de handen van   "
2900 Z$(4)="  een tegenstander pakken.        "
2910 Z$(5)="* Niet vanuit het verdedigingsvak"
2920 Z$(6)="  in de korf gooien.              "
2925 Z$(7)="                         "
2930 GOSUB11020:GOSUB10210
2940 Z$(2)="* Niet een tegenstander van de"
2950 Z$(3)="  andere sekse verdedigen.   "
2960 Z$(4)="* Als een speler een overtreding  "
2970 Z$(5)="  begaat dan krijgt de tegenpartij"
2980 Z$(6)="  een vrije worp op de plaats     "
2990 Z$(7)="  van de fout.               "
3000 GOSUB11020:GOSUB10210
3010 GOSUB10010
3020 HO=10:VE=8:GOSUB110
3030 PRINT"Einde van de uitleg."
3040 GOSUB10210:GOSUB100
3050 PRINT:PRINT" JE KUNT NU KIEZEN UIT 4 MOGELIJKHEDEN:"
3060 PRINT" --------------------------------------"
3070 PRINT:PRINT
3080 PRINT" Je wilt de hele uitleg nog eens zien,"
3090 PRINT:PRINT"                  druk dan op toets -A-"
3100 PRINT:PRINT
3110 PRINT" Je wilt de samenvatting nog eens zien,"
3120 PRINT:PRINT"                  druk dan op toets -B-"
3130 PRINT:PRINT
3140 PRINT" Je wilt je kennis testen met deel 2,"
3150 PRINT:PRINT"                  druk dan op toets -C-"
3160 PRINT:PRINT
3170 PRINT" Je wilt stoppen: druk dan op toets -D-"
3180 GOSUB200:IF(IN$="A") OR(IN$="a") THENRESTORE:GOTO1360
3190 IF(IN$="B") OR(IN$="b") THENGOSUB10010:GOTO2740
3200 IF(IN$="C") OR(IN$="c") THEN3210
3205 IF(IN$<>"D") AND(IN$<>"d") THEN3180
3210 GOSUB100:HO=7:VE=10:GOSUB110
3220 PRINT"Bedankt voor je aandacht.":HO=7:VE=12:GOSUB110
3230 IF(IN$="C") OR(IN$="c") THENPRINT"Succes met deel 2."
3240 HO=0:VE=20:GOSUB110
9999 END
10000 REM ----- VELD -----
10010 GOSUB100
10020 ML$="*                                     *"
10030 GOTO10050
10040 ML$="*                  *                  *"
10050 PRINT"***************************************"
10060 FORI=1 TO15:PRINTML$:NEXTI
10070 PRINT"***************************************"
10080 RETURN
10090 REM
10100 REM  ----- MIDDELLIJN -----
10110 HO=19
10120 FORVE=1 TO15:GOSUB110
10130 PRINT"*"
10140 NEXTVE:RETURN
10150 REM
10160 REM ----- KORVEN -----
10170 HO=4:VE=8:GOSUB110
10180 PRINT"(O)            *            (O)"
10190 RETURN
10200 REM
10210 REM ----- SPATIEBALKROUTINE -----
10220 REM RETURN:REM DE SPATIEBALK OVERSLAAN
10230 GOSUB10930:HO=32:VE=22
10240 L$="SPATIEBALK SPATIE":W=0.8
10250 FORI=1 TO11
10260 GOSUB110
10270 PRINTMID$(L$,I,7)
10280 GOSUB11060
10290 GOSUB200:IFIN$=" " THENI=12:W=5
10300 NEXTI:IFIN$=" " THEN10320
10310 GOTO10250
10320 GOSUB110:PRINT"       ":RETURN
10330 REM
10340 REM ----- PRINTPOSITIE OP 0,18 -----
10350 HO=0:VE=18:GOSUB110:RETURN
10360 REM
10370 REM ----- KNIPPERROUTINE -----
10380 K2$=" ":FORI=1 TOLEN(K1$):K2$=K2$+" ":NEXTI
10390 W=0.3:FORI=1 TO15
10400 GOSUB110:PRINTK1$:GOSUB11080
10410 GOSUB110:PRINTK2$:GOSUB11080
10420 NEXTI:RETURN
10430 REM
10440 REM ----- KARAKTER PLAATSEN -----
10450 READHO,VE,CR$
10460 GOSUB110:PRINTCR$:RETURN
10470 REM
10480 REM ----- SPELERS IN HET VELD PLAATSEN -----
10490 GOSUB10210
10500 READZ1$,Z2$,Z3$
10510 GOSUB10340
10520 PRINT:PRINTZ1$:PRINTZ2$:PRINTZ3$
10530 READHO,VE,K1$:GOSUB10370
10540 CR$=LEFT$(K1$,1)
10550 FORM=1 TO4:READHO,VE:GOSUB10460
10560 NEXTM
10570 RETURN
10580 REM
10590 REM ----- NIEUWE TEKST, ZELFDE PLAATJE -----
10600 GOSUB10220
10610 GOSUB10340
10620 READQ:FORS=1 TOQ:READZ$:PRINTZ$:NEXTS
10630 RETURN
10640 REM
10650 REM ----- DE BAL BESCHRIJFT EEN BAAN -----
10660 FORP=1 TOJ:READHO,VE
10670 GOSUB110:PRINTV$
10680 W=0.4:GOSUB11080
10690 GOSUB110:PRINT" "
10700 NEXTP:RETURN
10710 REM
10720 REM ----- DE BAL KNIPPERT OP DE PLAATS -----
10730 READK
10740 W=0.5:READHO,VE
10750 FORL=1 TOK
10760 GOSUB110:PRINT"o":GOSUB11080
10770 GOSUB110:PRINT" ":GOSUB11080
10780 NEXTL:RETURN
10790 REM
10800 REM ----- SPATIE & KNIPPERROUTINE -----
10810 L$="SPATIEBALK SPATIE":GOSUB10930
10820 W=0.7
10830 FORI=1 TO11
10840 HO=32:VE=22
10850 GOSUB110
10860 PRINTMID$(L$,I,7)
10870 FORQ=1 TO2:HO=X:VE=Y:GOSUB110:IFQ=2 THENGOSUB11080
10880 PRINTCR$(Q):NEXTQ
10890 GOSUB200:IFIN$=" " THENI=11
10900 NEXTI:IFIN$<>" " THEN10820
10910 I=12:HO=32:VE=22:GOSUB110:PRINT"       ":RETURN
10920 REM 
10930 REM ----- BUFFER LEGEN -----
10940 FORU=1 TO10:GOSUB200:NEXTU:RETURN
10950 REM
10960 REM ----- SPELERS WISSELEN VAN VAK -----
10970 W=1:READX,Y,X1,X2,CR$
10980 FORHO=X TOX+X1 STEP(X2):FORVE=Y TOY+1
10985 GOSUB110:PRINTCR$
10990 NEXTVE:GOSUB11070:NEXTHO:RETURN
11000 REM
11010 REM ----- SAMENVATTING PRINTEN -----
11020 HO=3:FORVE=2 TO14 STEP(2):GOSUB110
11030 PRINTZ$(VE/2):NEXTVE:RETURN
11040 REM
11050 REM ----- WACHTLUS -----
11060 REM HOE GROTER HET GETAL WAARMEE W WORDT
11070 REM VERMENIGVULDIGD, HOE LANGER DE WACHTTIJD IS.
11080 FORV=1 TOW*100:NEXTV:RETURN
11090 REM
25000 DATA " 4 verdedigers (v) stellen zich op"
25010 DATA"bij de eigen korf ...       "
25020 DATA "                                       "
25030 DATA 2,17,"v v v v",5,3,14,6,5,12,14,13
25040 DATA " 4 aanvallers (a) stellen zich op "
25050 DATA " bij de korf van de tegenpartij." 
25060 DATA " "
25070 DATA 10,17,"a a a a",33,3,26,7,36,11,26,13
25080 DATA " De tegenpartij doet dit ook:     "
25090 DATA " 4 verdedigers bij de eigen korf ... "
25100 DATA " "
25110 DATA 30,17,"v v v v",34,2,29,7,35,10,25,14
25120 DATA " en 4 aanvallers bij de andere korf."
25130 DATA "                                     "
25140 DATA " "
25150 DATA 22,17,"a a a a",4,4,13,7,4,11,13,14
25160 DATA 3," Zoals je ziet, heeft elke speler "
25170 DATA " een persoonlijke tegenstander.     "
25180 DATA " Dat is bij korfbal de gewoonte."
25190 DATA 3," Een dame en een heer mogen niet  "
25200 DATA " elkaars tegenstander zijn.    "
25210 DATA "                                "
25220 DATA 2," De spelers mogen tijdens het spel"
25230 DATA " niet in het andere vak komen. "
25240 DATA 3," Als de bal in het ene vak is, dan"
25250 DATA " hebben de spelers in het andere vak"
25260 DATA " even 'rust'."
25270 DATA "           ","           ","XOOO       "
25280 DATA "X          ","X          ","X          "
25290 DATA "X          ","X          ","X          "
25300 DATA "X          ","           ","           "
25310 DATA 3," De bedoeling van het korfbalspel "
25320 DATA " is, dat de aanvallers de bal door  "
25330 DATA " de korf werpen ... "
25340 DATA 19,12,18,11,17,10,16,9,15,8,14,7,13,6,12,5
25350 DATA 11,4,10,3,9,2,8,2,7,3,6,4,6,6,6,7,6,8,6
25360 DATA 9,6,10,6,11,6,12
25370 DATA 3," De verdedigers proberen dat te   "
25380 DATA " verhinderen.                     "
25390 DATA "                    "
25400 DATA 3," Om bij de korf te komen moeten de"
25410 DATA " aanvallers de bal steeds (met de "
25420 DATA " handen) naar elkaar overspelen."
25430 DATA 16,7,14,10,16,7,15,8,14,9,13,10
25440 DATA 17,7,16,7,15,7,14,7,13,7,12,7,11,7
25450 DATA 13,10,12,9,11,8,10,7
25460 DATA 14,10,13,10,12,10,11,10,10,10,9,10,8,10
25470 DATA 10,7,9,8,8,9,7,10
25480 DATA 6,9,5,8
25490 DATA 3," Met de bal lopen of dribbelen     "
25500 DATA " is niet toegestaan.               "
25510 DATA "                                  "
25520 DATA 3," Doelen mag alleen, als er geen   "
25530 DATA " verdediger VLAK VOOR JE staat, die "  
25540 DATA " probeert de bal tegen te houden."
25550 DATA 2,4,"a     v a",3,5,"v",11,7,"va"
25560 DATA 7,11,"v",9,13,"a"
25570 DATA 3," Deze aanvaller mag dus niet doelen."
25580 DATA " Hij moet overspelen naar een        "
25590 DATA " medespeler.                      "
25600 DATA 12,7," ","a"
25610 DATA 3," Deze aanvaller mag wel doelen,     "
25620 DATA " omdat de verdediger ver genoeg     "
25630 DATA " weg staat. "
25640 DATA 9,13," ","a"
25650 DATA 3," Deze aanvaller mag ook doelen. De"
25660 DATA " verdediger staat wel vlak bij, maar"
25670 DATA " blokkeert niet de weg naar de korf."
25680 DATA 10,4," ","a"
25690 DATA 3," Deze aanvaller mag niet doelen.  "
25700 DATA " Hij moet overspelen naar een       "
25710 DATA " medespeler.                        "
25720 DATA 2,4," ","a"
25730 DATA 3," Als een verdediger de bal te pakken"
25740 DATA " krijgt, moet hij hem naar het andere "
25750 DATA " vak zien te krijgen...               "
25760 DATA 3," bij zijn partijgenoten, die daar     "
25770 DATA " aanvaller zijn.                      "
25780 DATA "                                      "
25790 DATA 2," Dit gebeurt door samenspel met zijn"
25800 DATA " medeverdedigers.                     "
25810 DATA 20,7,4,2,8,5,9,6,10,10,7,5,8
25820 DATA 11,9,11,10,11,11,11,12,11
25830 DATA     10,10,7,3,11,8,12,9,13,10,10,14,11,0,0,0
25850 DATA 5,16,11,18,11,20,11,22,11,24,11,20,26,11 
25860 DATA 3," Als er gescoord is, krijgt         "
25870 DATA " de partij, die het punt NIET"
25880 DATA " gemaakt heeft, de bal... "
25890 DATA 3," Het spel begint opnieuw vanaf"
25900 DATA " de middellijn.              "
25910 DATA "                          "
25920 DATA 19,8," ","O"
25930 DATA 2," Als de bal het veld uitgaat, moet"
25940 DATA " hij ingegooid worden (inworp)... "
25950 DATA 2," Dit doet de partij die de bal    "
25960 DATA " NIET het laatste heeft geraakt.  "
25970 DATA 2," De inworp wordt naast        "
25980 DATA " de grenslijn genomen.          "
25990 DATA 23,10,"a v     a",35,11,"v"
26000 DATA 20,31,11,2,31,13,31,15,10,31,17,4,35,11,34,13
26010 DATA 33,15,32,17,10,31,17,4,29,15
26020 DATA 28,14,27,13,26,12,20,25,11
26030 DATA 4,32,17,33,15,34,13,35,11
26040 DATA 3," Als een speler een overtreding begaat,"
26050 DATA " krijgt de tegenpartij een vrije worp  "
26060 DATA " vanaf de plaats van de overtreding.  "
26070 DATA 3," Steeds nadat er 2 keer gescoord is,   "
26080 DATA " (het maakt niet uit door welke partij)"
26090 DATA " moeten alle spelers van vak wisselen."
26100 DATA 3," De aanvallers worden dan verdediger,  "
26110 DATA " De verdedigers worden aanvaller.      "
26120 DATA "                                      "
26130 DATA 10,2,"aa",10,3,"aa",27,5,"vv",27,6,"vv"
26140 DATA 10,10,"vv",10,11,"vv",27,13,"aa",27,14,"aa"
26150 DATA 12,2,13,1,"->vv"
26160 DATA 23,5,-13,-1,"aa<-"
26170 DATA 12,10,13,1,"->aa"
26180 DATA 23,13,-13,-1,"vv<-"
26190 REM
30000 REM
30010 REM **** KORFBAL **** DEEL 1 16K
30020 REM GEMAAKT OP NEWBRAIN COMPUTER
30030 REM
30040 REM MARTIN VAN LUIJK &
30050 REM PIETER VAN DIJK
30060 REM HNO-SCHOOL "DE SPINBAAN"
30070 REM WASSENAAR
30080 REM
30090 REM VOORJAAR 1984
30100 REM
30110 REM
30120 REM
30130 REM
30140 REMCOPYRIGHT
30150 REMNOS HILVERSUM

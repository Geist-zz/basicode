
1000 A=1000:GOTO20:REM ** WOORDTRAINER **
1010 F=0:Y=50:GOSUB10000
1020 HO=6:VE=4:GOSUB110
1025 DIMA$(Y)
1030 PRINT"WOORDEN WORDEN INGELEZEN !"
1035 FORWW=1 TO200:NEXTWW
1040 FORI=1 TOY
1050 READA$(I)
1060 NEXTI
1070 GOSUB100
1080 HO=4:VE=9:GOSUB110
1100 PRINT"HALLO, IK BEN DE TAALCOMPUTER":PRINT
1110 PRINT"    HOE HEET JIJ ":PRINT
1120 HO=4:VE=13:GOSUB110:INPUTN$
1130 GOSUB100
1140 HO=1:VE=2:GOSUB110
1150 PRINT" DAG ";N$;" IK GA JE HELPEN BIJ":PRINT
1160 PRINT" HET LEREN SCHRIJVEN EN HERKENNEN VAN":PRINT
1170 PRINT" WOORDEN.":PRINT
1180 PRINT" IK DOE DAT DOOR HEEL EVEN EEN WOORD TE":PRINT
1190 PRINT" LATEN ZIEN.":PRINT
1200 PRINT" JIJ MOET DAT WOORD DAN INVULLEN.":PRINT:PRINT
1210 PRINT" BEGREPEN ?    DRUK OP EEN TOETS."
1220 GOSUB210:GOSUB100:VE=2:HO=1:GOSUB110
1230 PRINT" JE ZIET HIER DE KEUZE IN MOEILIJKHEID"
1235 PRINT:PRINT
1240 PRINT"   (1)  EEN LETTERGREEP":PRINT
1250 PRINT"   (2)  TWEE LETTERGREPEN":PRINT
1260 PRINT"   (3)  DRIE LETTERGREPEN":PRINT
1270 PRINT"   (4)  VIER LETTERGREPEN":PRINT
1280 PRINT"   (5)  VIJF LETTERGREPEN":PRINT
1290 PRINT"   WAT KIES JE ?":GOSUB210
1300 J=VAL(IN$):ONJ GOTO1320,1330,1340,1350,1360
1310 GOTO1290
1320 A=1:B=10:W=300:GOTO1400
1330 A=11:B=20:W=400:GOTO1400
1340 A=21:B=30:W=600:GOTO1400
1350 A=31:B=40:W=800:GOTO1400
1360 A=41:B=50:W=1000:GOTO1400
1400 GOSUB100
1410 HO=1:VE=10:GOSUB110
1420 PRINT" TOT WELK WOORD WAS JE GEKOMEN ?":PRINT
1430 PRINT" GEEF HET NUMMER VAN HET LAATSTE WOORD":PRINT
1440 PRINT" ALS JE PAS BEGINT GEEF JE 0"
1450 INPUTL
1460 A=A+L
1470 GOSUB100:GOSUB1500
1480 GOTO1550
1500 HO=3:VE=5:GOSUB110
1510 PRINT"*** WOORDEN LEREN ***"
1520 HO=6:VE=7:GOSUB110
1530 PRINT"->"
1540 RETURN
1550 FORI=A TOB
1560 C=C+1:N1=W
1570 HO=10:VE=7:GOSUB110
1580 PRINTA$(I);:PRINT"   <-"
1590 FORX=1 TON1:NEXTX
1600 HO=10:VE=7:GOSUB110
1610 PRINT"                           "
1620 HO=2:VE=10:GOSUB110
1630 PRINT"WELK WOORD WAS DIT ";:INPUTC$
1640 IFC$=A$(I) THEN1750
1650 HO=2:VE=15:GOSUB110
1660 PRINT"JAMMER DIT WAS FOUT !  KIJK NOG EENS"
1665 FORWW=1 TO100:NEXTWW
1670 HO=22:VE=10:GOSUB110
1680 PRINT"                      "
1690 F=F+1
1700 FORX=1 TO500:NEXTX
1710 HO=2:VE=15:GOSUB110
1720 PRINT"                                       "
1730 GOTO1570
1750 IF(C=10) AND(F>4) THEN2250
1760 IFC=10 THEN1850
1770 HO=2:VE=15:GOSUB110
1780 PRINT"DIT WAS GOED ! LET OP DE VOLGENDE "
1785 FORWW=1 TO100:NEXTWW
1790 HO=22:VE=10:GOSUB110
1800 PRINT"                   "
1810 FORX=1 TO500:NEXTX
1820 HO=2:VE=15:GOSUB110
1830 PRINT"                                       "
1840 GOTO2400
1850 GOSUB100
1860 HO=3:VE=5:GOSUB110
1870 PRINT" ZO ";N$;"... DAT WAREN ER 10 !":PRINT
1880 PRINT"    VOND JE HET MOEILIJK ?       ":PRINT
1890 PRINT"    < J/N >"
1900 GOSUB210
1910 IF(IN$="J") OR(IN$="j") THEN2100
1920 GOSUB100:VE=12:HO=3:GOSUB110
1930 PRINT"  JE VOND HET DUS NIET MOEILIJK !":PRINT
1940 PRINT"  DAN GAAN WE DE WOORDEN DIE JE NET ":PRINT
1950 PRINT"  HEBT GEHAD EERST IN EEN SCHRIFT":PRINT
1960 PRINT"  SCHRIJVEN."
1970 PRINT:PRINT"  DRUK OP EEN TOETS."
1980 GOSUB210:GOSUB100
1990 HO=0:VE=3:GOSUB110
2000 FORP=I-9 TOI
2010 PRINT"    ";P;"...."A$(P)
2020 NEXTP
2030 HO=0:VE=20:GOSUB110
2040 PRINT" DRUK OP EEN TOETS OM VERDER TE GAAN >"
2050 GOSUB210
2060 C=0:N1=W:F=0
2070 GOSUB100
2080 GOSUB1500
2090 GOTO2400
2100 GOSUB100
2110 HO=0:VE=3:GOSUB110
2120 PRINT" JE VOND HET TOCH NIET GEMAKKELIJK.":PRINT
2130 PRINT" WIL JE DE WOORDEN NOG EEN KEER ?":PRINT
2140 PRINT" JE KRIJGT DE WOORDEN DAN WAT LANGER":PRINT
2150 PRINT" TE ZIEN.      <J/N>"
2160 GOSUB210
2170 IF(IN$="J") OR(IN$="j") THEN2210
2180 GOTO1920
2210 W=N1+200:I=I-10:GOSUB100
2220 C=0:F=0
2230 GOSUB1500
2240 GOTO2400
2250 GOSUB100
2260 VE=5:HO=3:GOSUB110
2270 PRINT" JE HEBT ";F;" FOUTEN GEMAAKT !":PRINT
2280 PRINT" DAT IS NIET ZO BEST ";N$;" !":PRINT
2290 PRINT" DIT DOEN WE NOG EEN KEER."
2300 FORX=1 TO1500:NEXTX
2310 GOTO2210
2400 NEXTI
2410 PRINT"ZO ! DAT WAS DE LAATSTE"
2420 PRINT"KIES DE VOLGENDE":GOTO1220
5000 REM   *** EEN LETTERGREEP ***
5010 REM   *** --------------- ***
5020 DATA"AAP","AF","BAAS","BAL","BAND"
5030 DATA"FIETS","HAAI","GLOED","HERFST","POES"
5080 REM   ***
5090 REM   *** TWEE LETTERGREPEN ***
5100 REM   *** ----------------- ***
5110 DATA"AARDBEI","APPELS","AUTO","BLOEMKOOL","BALLEN"
5120 DATA"BOMEN","DAKPAN","DERTIG","DWEILEN","FORNUIS"
5160 REM   ***
5170 REM   *** DRIE LETTERGREPEN ***
5180 REM   *** ----------------- ***
5190 DATA"MOLENAAR","NOTARIS","ONDERBROEK","PARAPLU"
5200 DATA"ROGGEBROOD","RECHERCHEUR","KOZIJNEN","STOFZUIGER"
5210 DATA"VANACHTER","VERKOUDEN"
5240 REM   ***
5250 REM   *** VIER LETTERGREPEN ***
5260 REM   *** ----------------- ***
5270 DATA"EIERWEKKER","ENTHOUSIAST","FIGUURRAADSEL"
5271 DATA"HYACINTEN"
5280 DATA"GECONCENTREERD","HAZESLAAPJE","INDIANEN"
5281 DATA"INVALIDE"
5300 DATA"KOMKOMMERSCHIL","LETTERGREPEN"
5320 REM   ***
5330 REM   *** VIJF LETTERGREPEN ***
5340 REM   *** ----------------- ***
5350 DATA"GEVARENDRIEHOEK","GROTONDERZOEKER"
5351 DATA"SIGARETTENPIJP"
5360 DATA"TELEFOONTAFEL","WANDTEKENINGEN","TELEVISIEBEELD"
5370 DATA"VERJAARDAGSCADEAU","VOEDINGSMIDDELEN"
5380 DATA"VRACHTWAGENCHAUFFEURS","ZOMERVAKANTIE"
10000 GOSUB100
10010 HO=4:VE=5:GOSUB110
10020 PRINT"WILT U UITLEG ?  <J/N>"
10030 GOSUB210
10040 IF(IN$="J") OR(IN$="j") THEN10060
10050 GOSUB100:RETURN
10060 GOSUB100
10070 PRINT:PRINT" DE BEDOELING VAN DIT PROGRAMMA IS BIJ"
10071 PRINT
10080 PRINT" MENSEN MET ENIGE VORM VAN DISLEXIE":PRINT
10090 PRINT" (TAALSTOORNIS BIJ HET LEZEN)":PRINT
10100 PRINT" DE HERSENFUCTIE TE TRAINEN OP HET VOR-":PRINT
10110 PRINT" MEN VAN EEN JUIST WOORDBEELD.":PRINT
10120 PRINT" DEZE TRAINING MAAKT O.A. DEEL UIT ":PRINT
10130 PRINT" VAN EEN REMEDIAL TEACHINGS PROGRAMMA":PRINT
10140 PRINT" VOOR MOEILIJK LERENDE KINDEREN":PRINT
10150 PRINT:PRINT"DRUK OP EEN TOETS."
10160 GOSUB210:GOSUB100
10170 PRINT:PRINT" HET PROGRAMMA IS AAN TE PASSEN VOOR"
10171 PRINT
10180 PRINT" ANDERE VORMEN VAN TAALONDERWIJS":PRINT
10190 PRINT" DOOR IN DE DATAREGELS ANDERE WOORDEN":PRINT
10200 PRINT" TE ZETTEN.":PRINT
10210 PRINT" BIJV. WOORDEN MET BIJZONDERE KENMERKEN":PRINT
10220 PRINT" ZOALS WOORDEN MET AU-OU":PRINT
10230 PRINT"    OF             IJ-EI":PRINT
10240 PRINT" VULT U ZELF MAAR AAN.":PRINT
10250 PRINT:PRINT" DRUK OP EEN TOETS"
10260 GOSUB210:GOSUB100
10270 PRINT:PRINT" VOOR OPMERKINGEN EN SUGGESTIES KUNT U"
10271 PRINT
10280 PRINT" ALTIJD KONTAKT MET MIJ OPNEMEN.":PRINT
10290 PRINT" MIJN NAAM EN ADRES STAAN OP DE DATA-":PRINT
10300 PRINT" REGELS VANAF 30000":PRINT
10310 PRINT" HET PROGRAMMA START NA HET INDRUKKEN":PRINT
10320 PRINT" VAN EEN TOETS.  VEEL SUCCES !!":PRINT
10330 GOSUB210:GOSUB100:RETURN
30000 REM *** DIT PROGRAMMA IS GEMAAKT
30010 REM *** OP EEN MICROPROFESSOR II
30020 REM *** DOOR F. DOOLAEGE
30030 REM *** GRAAFJANSDIJK B 195
30040 REM *** 4554 LG  WESTDORPE (ZLD)
30050 REM *** 01158-2953
30060 REM *** 20 - 12 - 1983
30070 REM  
30080 REM COPYRIGHT NOS HILVERSUM


1000 A=100:GOTO20:REM  *** BREUKEN *** 
1010 GOSUB100:REM  * HOOFDPROGR *
1020 E=0:K=1:PT$=".":DS$="/"
1025 PRINT:PRINT
1030 PRINTTAB(12);"***********"
1040 PRINTTAB(12);"* BREUKEN *"
1050 PRINTTAB(12);"***********"
1060 PRINT:PRINT
1070 PRINTTAB(8);"JE KUNT KIEZEN UIT :":PRINT
1080 PRINTTAB(8);" 1. OPTELLEN"
1090 PRINTTAB(8);" 2. AFTREKKEN"
1100 PRINTTAB(8);" 3. VERMENIGVULDIGEN"
1110 PRINTTAB(8);" 4. DELEN"
1120 PRINT:PRINT
1130 PRINT"N.B. * Tik 'n breuk met deelteken in !"
1140 PRINT"     * DUS ZO --> 2/5"
1150 PRINT"     * En 'n gemengd getal met punt !"
1160 PRINT"     * ZO DUS --> 2.5/9":PRINT
1170 PRINTTAB(7);"TYP 1 2 3 of 4 ":GOSUB210
1180 IF(ASC(IN$)<49) OR(ASC(IN$)>52) THENGOSUB250:GOTO1170
1185 N=ASC(IN$)-48
1190 PRINT:PRINTTAB(7);"HOEVEEL SOMMEN ";
1200 INPUTH:L=H+1
1210 ONN GOTO1300,1500,1700,1900
1220 REM
1230 REM ** BEGIN GEKOZEN BEWERKING **
1240 REM
1300 REM ****** OPTELLEN ******
1310 J=0:GOSUB100:HO=10:VE=5:GOSUB110
1315 PRINT"* BREUKEN OPTELLEN *"
1330 GOSUB4000:REM *ZOEK GETAL*
1340 X=Q*D+C*B:Y=B*D
1350 GOSUB5000:REM *VEREENVOUDIGEN*
1360 IF(X>Y) OR(X=Y) THEN1330
1370 HO=5:VE=12:GOSUB110:PRINTQ;"/";B;"+";C;"/";D;" ="
1380 GOSUB6000:REM *INVOER EN CONTROLE*
1390 GOTO1310
1500 REM ***** AFTREKKEN *****
1510 J=0:GOSUB100:HO=8:VE=5:GOSUB110
1515 PRINT"* BREUKEN AFTREKKEN *"
1520 GOSUB3000:REM *PRINT SOM*
1530 GOSUB4000:REM *ZOEK GETAL*
1540 X=Q*D-C*B:Y=B*D
1550 GOSUB5000:REM *VEREENVOUDIGEN*
1560 HO=5:VE=12:GOSUB110:PRINTQ;"/";B;"-";C;"/";D;" ="
1570 GOSUB6000:REM *INVOER EN CONTROLE*
1580 GOTO1510
1700 REM ** VERMENIGVULDIGEN **
1710 GOSUB100
1715 PRINT"* BREUKEN VERMENIGVULDIGEN *"
1720 GOSUB3000:REM *PRINT SOM*
1730 GOSUB4000:REM *ZOEK GETAL*
1740 X=Q*C:Y=B*D
1750 GOSUB5000:REM *VEREENVOUDIGEN*
1760 HO=5:VE=12:GOSUB110:PRINTQ;"/";B;"*";C;"/";D;" ="
1770 GOSUB6000:REM *INVOER EN CONTROLE*
1780 GOTO1710
1900 REM ****** DELEN ******
1910 J=1:GOSUB100:HO=12:VE=5:GOSUB110
1915 PRINT"* BREUKEN DELEN *"
1920 GOSUB3000:REM *PRINT SOM*
1930 GOSUB4000:REM *ZOEK GETAL*
1940 X=Q*D:Y=B*C
1950 Z=INT(X/Y):W=INT((((X/Y)-Z)*Y)+.001)
1955 IFW=0 THEN1910
1960 X=W
1970 GOSUB5000:REM *VEREENVOUDIGEN*
1980 HO=5:VE=12:GOSUB110:PRINTQ;"/";B;":";C;"/";D;" ="
1990 GOSUB6000:REM *INVOER EN CONTROLE*
2000 GOTO1910
2100 REM
2110 REM ** EINDE GEKOZEN BEWERKING **
2200 REM
2210 REM ** BEGIN SUBROUTINES **  
2220 REM
3000 REM **** PRINT SOM ****
3020 HO=5:VE=8:GOSUB110:PRINT"SOM :";K
3030 RETURN
4000 REM **** ZOEK GETAL ****
4010 GOSUB260:Q=INT(ABS(10*RV)+1)
4020 GOSUB260:B=INT(ABS(10*RV)+1)
4030 GOSUB260:C=INT(ABS(10*RV)+1)
4040 GOSUB260:D=INT(ABS(10*RV)+1)
4050 IF(Q=B) OR(C=D) OR(B=D) THEN4010
4060 IF(Q>B) OR(C>D) THEN4010
4070 IF((Q*D-C*B)<0) OR((Q*D-C*B)=0) THEN4010
4080 RETURN
5000 REM     ** VEREENVOUDIGEN ** 
5010 FORI=0 TO3
5020 IF((X/7)=INT(X/7)) AND((Y/7)=INT(Y/7)) THENX=X/7:Y=Y/7
5030 IF((X/5)=INT(X/5)) AND((Y/5)=INT(Y/5)) THENX=X/5:Y=Y/5
5040 IF((X/3)=INT(X/3)) AND((Y/3)=INT(Y/3)) THENX=X/3:Y=Y/3
5045 IF((X/2)=INT(X/2)) AND((Y/2)=INT(Y/2)) THENX=X/2:Y=Y/2
5050 NEXTI
5060 RETURN
6000 REM   ** INVOER EN CONTROLE **
6010 HO=24:VE=12:GOSUB110:INPUTIV$
6020 IFJ=0 THENGOSUB7000
6025 IFJ=1 THENGOSUB8000
6030 HO=5:VE=16:GOSUB110
6035 IFIV$=SM$ THENPRINT"** GOED **":GOTO6100
6040 PRINT"< FOUT >":E=E+1
6050 VE=18:GOSUB110:PRINT"HET GOEDE ANTWOORD :";SM$
6060 REM      ** PAUZE **
6100 FORT=0 TO4000:NEXTT
6110 K=K+1:IFK<>L THENRETURN
6120 IFK=L THENPRINT:PRINT"VAN DE ";H;" SOMMEN";
6130 PRINT" HEB JE ER ";H-E;" GOED.":PRINT:GOTO6140
6140 PRINT"NOG EENS ? TYP J/N":GOSUB210
6150 IF(IN$="J") OR(IN$="j") THEN1000
6160 GOSUB100:PRINT" TOT ZIENS !":END
6170 REM
7000 SR=X:GOSUB300:SM$=SR$+DS$:SR$=""
7010 SR=Y:GOSUB300:SM$=SM$+SR$:SR$=""
7020 RETURN
8000 SR=Z:GOSUB300:SM$=SR$+PT$:SR$=""
8010 SR=X:GOSUB300:SM$=SM$+SR$+DS$:SR$=""
8020 SR=Y:GOSUB300:SM$=SM$+SR$:SR$=""
8030 RETURN
8040 REM **** EINDE SUBROUTINES ****
30000 REM 
30010 REM   HOOFDBEWERKINGEN BREUKEN
30020 REM 
30030 REM         H.J.G.HOENEN
30040 REM       FLORIBERTUSSTR.3
30050 REM       6444 XG BRUNSSUM
30060 REM
30070 REM        BBC-COMPUTER
30080 REM
30090 REM         MAART 1984 
30100 REM
30110 REM COMPUTER BEZITTERS
30120 REM DIE PROBLEMEN HEBBEN
30130 REM MET DE SCHERM-INDELING:
30140 REM (b.v. BBC, APPLE, etc.)
30150 REM VERANDER IN REGEL
30160 REM 6010 HO=24 in: HO=16
30170 REM
30180 REM COPYRIGHT
30190 REM NOS HILVERSUM

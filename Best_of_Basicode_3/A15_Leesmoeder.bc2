
1000 A=400:GOTO20:REM *** LEESMOEDER
1010 REM*** INITIALISATIE
1020 TK=50:REM*** TIJD KARAKTER IN BEELD
1030 L=80:REM*** MAX. AANTAL DATA
1040 DIMT$(L),F$(15),G$(15),S(15)
1050 A=0:REM*** TELLER DATA
1060 AF=0:AG=0:AV=0:REM***AANTAL FOUT,GOED,VRAGEN
1070 C=0:REM*** CIJFER
1080 TT=0:REM TOTAALTIJD
1090 V1=0:V2=0:V3=0:V4=0
1100 V5=0:REM*** VRAAGVARIABELEN
1110 REM*** HOOFDPROGRAMMA
1120 GOSUB2000:REM*** INTRODUKTIE
1130 GOSUB3000:REM*** DATA INLEZEN
1140 GOSUB100
1150 HO=8:VE=9:GOSUB110
1160 PRINT"Wil je de tekst eerst"
1170 VE=11:GOSUB110
1180 PRINT"nog eens bekijken ?"
1190 PRINT:PRINT"           (Ja / Nee) ";
1200 GOSUB210:IF(IN$="N") OR(IN$="n") THENGOSUB100:GOTO1230
1210 IF(IN$<>"J") AND(IN$<>"j") THEN1200
1220 GOSUB3500:GOSUB100
1230 GOSUB4000:REM*** SCHERMOPBOUW
1240 GOSUB5000:REM*** INVOERVERWERKING
1250 IF(A$="S") AND(AV=0) THEN1270
1260 GOSUB6000:REM*** EINDOVERZICHT
1270 GOSUB7000:REM*** TOT ZIENS
1280 END
1290 REM
1500 REMSPATIEBALK
1510 HO=0:VE=23:GOSUB110
1520 PRINT"Druk op de spatiebalk....";
1530 GOSUB210
1540 IFIN$<>" " THEN1530
1550 GOSUB100
1560 RETURN
2000 REM*** INTRODUKTIE
2010 GOSUB100
2020 PRINT"             **************"
2030 PRINT"             *            *"
2040 PRINT"             * LEESMOEDER *"
2050 PRINT"             *            *"
2060 PRINT"             **************"
2070 PRINT:PRINT
2080 PRINT"Nog nooit van gehoord?"
2090 PRINT
2100 PRINT"In veel scholen zijn er moeders die de"
2110 PRINT
2120 PRINT"onderwijzer(es) assisteren bij het"
2130 PRINT
2140 PRINT"leesonderwijs. Zodoende krijgt ieder"
2150 PRINT
2160 PRINT"kind meer aandacht bij het leren lezen."
2170 PRINT
2180 PRINT"Zo'n moeder heet een leesmoeder."
2190 GOSUB1500
2200 PRINT
2210 PRINT"Die rol van leesmoeder ga ik nu over-"
2220 PRINT
2230 PRINT"nemen, al zie ik er niet bepaald als"
2240 PRINT
2250 PRINT"een moeder uit."
2260 PRINT
2270 PRINT"Even met elkaar kennis maken."
2280 PRINT
2290 PRINT"Druk na je antwoord op de invoertoets."
2300 VE=21:GOSUB110
2310 PRINT"Wat is jouw voornaam ";
2320 INPUTNV$
2330 VE=23:GOSUB110
2340 PRINT"En jouw achternaam   ";
2350 INPUTNA$
2360 N$=NV$+" "+NA$
2370 IFNV$="" THENNV$="onbekende"
2380 IFN$=" " THENN$="Anoniem"
2390 GOSUB100
2400 PRINT"Hallo ";NV$
2410 PRINT
2420 PRINT"Ik wil je graag helpen bij het"
2430 PRINT
2440 PRINT"verbeteren van jouw leessnelheid en"
2450 PRINT
2460 PRINT"spelling (ook: zuiver schrijven)."
2470 PRINT
2480 PRINT"Dat gaat als volgt:"
2490 PRINT
2500 PRINT"Gedurende een korte periode laat ik je"
2510 PRINT
2520 PRINT"een woord, een zinsdeel of een hele"
2530 PRINT
2540 PRINT"zin zien. Jij probeert het te lezen."
2550 PRINT
2560 PRINT"Daarna verdwijnt alles van het scherm"
2570 PRINT
2580 PRINT"en typ jij in wat je gelezen hebt."
2590 PRINT
2600 PRINT"Ik let wel op of je het goed doet."
2610 GOSUB1500
2620 RETURN
2630 REM
3000 REM*** DATA INLEZEN
3010 READR$
3020 IFR$="*" THEN3060
3030 A=A+1
3040 IFA>L THENPRINT"TE VEEL DATAREGELS.":END
3050 T$(A)=R$:GOTO3010
3060 GOSUB100
3070 RETURN
3080 REM
3500 REM*** TEKST AFDRUKKEN
3510 I=1
3520 GOSUB100
3530 PRINT"    B E K I J K  D I T  G O E D !!!"
3540 PRINT"======================================="
3550 HO=0:VE=21:GOSUB110
3560 PRINT"======================================="
3570 VE=3:GOSUB110
3580 Z$=T$(I)
3590 PRINTZ$:PRINT
3600 I=I+1
3610 IFI/9=INT(I/9) THEN3650
3620 IFI>(A) THEN3650
3630 GOTO3580
3640 RETURN
3650 REM*** SCHERM VOL OF TEKST AF
3660 VE=23:GOSUB110
3670 IFI/9=1 THENPRINT"                        V = Verder";
3680 IFI/9>1 THENPRINT"T = Terug               V = Verder";
3690 GOSUB210
3700 IF(IN$="T") OR(IN$="t") THEN3730
3710 IF(IN$="V") OR(IN$="v") THEN3750
3720 GOTO3690
3730 I=INT((I-1)/9-1)*9
3740 IFI<1 THENI=1
3750 IFI>(A) THEN3640
3760 GOTO3520
3770 REM
4000 REM*** SCHERMOPBOUW
4010 PRINT"--------------LEESMOEDER---------------"
4020 PRINT
4030 PRINT"  ************************************"
4040 PRINT"  *                                  *"
4050 PRINT"  *                                  *"
4060 PRINT"  *                                  *"
4070 PRINT"  ************************************"
4080 FORVE=7 TO19
4090 IFVE=10 THEN4120
4100 HO=3:GOSUB110:PRINT"!"
4110 HO=36:GOSUB110:PRINT"!"
4120 NEXTVE
4130 HO=3:VE=9:GOSUB110
4140 PRINT"+--------------------------------+"
4150 VE=11:GOSUB110
4160 PRINT"+--------------------------------+"
4170 HO=0:VE=20:GOSUB110
4180 PRINT"  +----------------------------------+"
4190 PRINT"  !                                  !"
4200 PRINT"  +----------------------------------+"
4210 RETURN
4220 REM
5000 REM*** INVOERVERWERKING
5010 GOSUB260:R=INT(RV*A)
5020 IF(R=V1) OR(R=V2) OR(R=V3) THEN5010
5030 IF(R=V4) OR(R=V5) THEN5010
5040 TL=LEN(T$(R))
5050 IFTL=0 THEN5000
5060 T=TL*TK
5070 REM*** WACHTLUS
5080 FORW=1 TO900:NEXTW
5090 FORW=1 TO3:GOSUB250:NEXTW
5100 FORW=1 TO600:NEXTW
5110 HO=4:VE=4:GOSUB110
5120 PRINTT$(R);
5130 REM*** TIJD TEKST ZICHTBAAR
5140 FORW=1 TOT:NEXTW
5150 REM*** TEKST WISSEN
5160 GOSUB110
5170 PRINT"                                 "
5180 REM*** VRAAG EN ANTWOORD
5190 HO=5:VE=8:GOSUB110
5200 PRINT"Wat stond er ?"
5210 VE=10:GOSUB110
5220 INPUTA$
5230 IFA$="S" THENRETURN
5240 IFA$=T$(R) THEN5280
5250 GOSUB8000:IFK=1 THEN5490
5260 IFA$=T$(R) THEN5280
5270 IFA$<>T$(R) THEN5780
5280 HO=5:VE=14:GOSUB110
5290 PRINT"Dat is goed, ";NV$
5300 AV=AV+1:AG=AG+1
5310 TT=TT+T
5320 TK=INT(TK/1.25)
5330 R$="IETS korter."
5340 IFTK<=1 THENTK=1:R$="zo KORT MOGELIJK !!!"
5350 REM*** VOORKOM HERHALING TEKST
5360 V1=V2:V2=V3:V3=V4:V4=V5:V5=R
5370 VE=16:GOSUB110
5380 PRINT"De leestijd wordt nu:"
5390 VE=18:GOSUB110
5400 PRINTR$
5410 REM*** CIJFERBEREKENING
5420 C=INT((AG/AV)*10+.5)
5430 HO=4:VE=3:GOSUB110
5440 PRINT"Aantal vragen: ";AV
5450 HO=26:VE=4:GOSUB110
5460 PRINT"Cijfer: ";C
5470 HO=4:VE=5:GOSUB110
5480 PRINT"Aantal goed  : ";AG
5490 REM*** KEUZE
5500 FORI=1 TO10
5510 HO=2:VE=21:GOSUB110
5520 PRINT"! S = STOPPEN           V = VERDER !"
5530 NEXTI
5540 FORI=1 TO10
5550 VE=21:GOSUB110
5560 PRINT"!   = STOPPEN             = VERDER !"
5570 NEXTI
5580 GOSUB200
5590 IFIN$="" THEN5500
5600 IFIN$="S" THENRETURN
5610 IFIN$<>"V" THEN5500
5620 REM*** TEKST WISSEN
5630 HO=4:VE=3:GOSUB110
5640 FORI=1 TO3
5650 PRINT"                                 "
5660 VE=VE+1:GOSUB110
5670 NEXTI
5680 HO=0:VE=8:GOSUB110
5690 FORI=1 TO12
5700 IF(I=2) OR(I=4) THEN5730
5710 IFI=3 THENPRINT"                                       "
5715 IFI=3 GOTO5730
5720 PRINT"   !                                !   ";
5730 VE=VE+1:GOSUB110
5740 NEXTI
5750 HO=2:VE=21:GOSUB110
5760 PRINT"!                                  !"
5770 GOTO5000
5780 REM*** ANTWOORD FOUT
5790 HO=5:VE=12:GOSUB110
5800 PRINT"Dat is niet goed. Het was:"
5810 VE=14:GOSUB110
5820 PRINT" ";
5830 PRINTT$(R)
5840 AV=AV+1:AF=AF+1:TT=TT+T
5850 F$(AF)=A$
5860 G$(AF)=T$(R)
5870 S(AF)=T
5880 IFAF=15 THENRETURN
5890 TK=TK*2
5900 IFTK>=800 THENTK=800:R$="GELIJK aan de vorige."
5910 IFTK=800 THEN5350
5920 R$="TWEE keer zo LANG."
5930 GOTO5350
5940 REM
6000 REM*** EINDOVERZICHT
6010 GOSUB100
6020 IFAF=0 THEN6310
6030 PRINT"-------------EINDOVERZICHT-------------"
6040 PRINT
6050 PRINT"Tenslotte volgen de woorden of zinnen,"
6060 PRINT
6070 PRINT"waarin je fouten maakte. Oefen deze"
6080 PRINT
6090 PRINT"woorden of zinnen nog eens heel goed."
6100 PRINT
6110 PRINT"Je kunt ook nog zien hoe lang de tekst"
6120 PRINT
6130 PRINT"in beeld was."
6140 PRINT:PRINT
6150 GOSUB1500
6160 PRINT"-------------EINDOVERZICHT-------------"
6170 VE=2
6180 FORI=1 TOAF
6190 HO=0:GOSUB110
6200 PRINT"Vraag: ";G$(I)
6210 VE=VE+1:GOSUB110
6220 PRINT"Antw : ";F$(I)
6230 VE=VE+1:GOSUB110
6240 CT=4:CN=2:SR=(S(I))/725:GOSUB310
6250 PRINT"Tijd : ";SR$;" seconde."
6260 PRINT:VE=VE+2
6270 IFVE>21 THENGOSUB1500:VE=2
6280 NEXTI
6290 PRINT
6300 GOSUB1500
6310 PRINT"---------------LEESMOEDER--------------"
6320 PRINT:PRINT:PRINT
6330 PRINT"Naam         : ";N$
6340 PRINT
6350 SR=AV:GOSUB300
6360 PRINT"Aantal vragen: ";SR$
6370 PRINT
6380 SR=AG:GOSUB300
6390 PRINT"Aantal goed  : ";SR$;
6400 SR=C:GOSUB300
6410 PRINT"          Cijfer: ";SR$
6420 PRINT
6430 SR=AF:GOSUB300
6440 PRINT"Aantal fout  : ";SR$
6450 PRINT
6460 CT=4:CN=2:SR=(TT/AV)/725:GOSUB310
6470 PRINT"---------------------------------------"
6480 PRINT
6490 PRINT"Gem. leestijd: ";SR$;" sec."
6500 PRINT
6510 PRINT"---------------------------------------"
6520 PRINT:PRINT
6530 GOSUB1500
6540 RETURN
6550 REM
7000 REM*** TOT ZIENS
7010 GOSUB100
7020 HO=12:VE=15:GOSUB110
7030 PRINT"*************"
7040 VE=16:GOSUB110
7050 PRINT"*           *"
7060 VE=17:GOSUB110
7070 PRINT"* TOT ZIENS *"
7080 VE=18:GOSUB110
7090 PRINT"*           *"
7100 VE=19:GOSUB110
7110 PRINT"*************"
7120 HO=0:VE=22:GOSUB110
7130 RETURN
7140 REM
8000 REM*** SUBROUTINE FOUTEN
8010 K=LEN(T$(R)):H=LEN(A$):IL=1
8020 IFRIGHT$(A$,1)=" " THENA$=LEFT$(A$,H-1):GOTO8010
8030 IFLEFT$(A$,1)=" " THENA$=RIGHT$(A$,H-1):GOTO8010
8040 IFA$=T$(R) THENRETURN
8050 IFH>K+1 THENRETURN:REM*** ANTWOORD TE LANG
8060 IFH<K-1 THENRETURN:REM*** ANTWOORD TE KORT
8070 CL$=LEFT$(A$,IL):DL$=LEFT$(T$(R),IL)
8080 IFCL$=DL$ THENIL=IL+1:GOTO8070
8090 IFK-IL<0 THEN8460:REM*** LAATSTE LETTER TEVEEL
8100 IFK-IL=0 THENCR$="":DR$="":GOTO8120
8110 CR$=RIGHT$(A$,K-IL):DR$=RIGHT$(T$(R),K-IL)
8120 IFCR$<>DR$ THEN8540:REM*** RECHTERDEEL GOED?
8130 KL=1:IFH>K THEN8380
8140 IFIL=1 THENRL$="":RR$=RIGHT$(A$,K-IL)
8150 IFIL=1 THEN8180
8160 IFK-IL=0 THENRL$=LEFT$(A$,IL-1):RR$="":GOTO8180
8170 RL$=LEFT$(A$,IL-1):RR$=RIGHT$(A$,K-IL)
8180 HO=5:VE=12:GOSUB110
8190 PRINT" ";RL$;"*";RR$;
8200 VE=VE+2:GOSUB110
8210 PRINT"Niet helemaal goed."
8220 VE=VE+1:GOSUB110
8230 PRINT"Het * geeft aan"
8240 VE=VE+1:GOSUB110
8250 PRINT"waar de fout zit."
8260 VE=VE+2:GOSUB110
8270 IFH<K THENPRINT"Je bent wat vergeten."
8280 IFH<K THENVE=VE+1:GOSUB110
8290 IFH<K THENPRINT"Tik dat alsnog in."
8300 IFH=K THENPRINT"Een letter verkeerd."
8310 IFH=K THENVE=VE+1:GOSUB110
8320 IFH=K THENPRINT"Tik alsnog de goede letter in."
8330 HO=LEN(RL$)+6:VE=12:GOSUB110
8340 GOSUB210
8350 PRINTIN$
8360 A$=RL$+IN$+RR$
8370 GOTO8790
8380 CR$=RIGHT$(A$,KL):DR$=RIGHT$(T$(R),KL)
8390 IFCR$=DR$ THENKL=KL+1:GOTO8380
8400 IFK-KL<0 THEN8460
8410 RL$=LEFT$(A$,IL-1):RR$=RIGHT$(T$(R),KL-1)
8420 HO=5:VE=10:GOSUB110
8430 PRINT"                              "
8440 GOSUB110
8450 PRINTRL$;"*";RR$;
8460 HO=5:VE=12:GOSUB110
8470 PRINT"Een letter te veel."
8480 VE=14:GOSUB110
8490 PRINT"Het moest zijn:"
8500 VE=16:GOSUB110
8510 PRINTT$(R):A$=T$(R)
8520 K=1
8530 GOTO8840
8540 IFK-IL=0 THEN8600
8550 IFK-IL-1=0 THENCR$="":DR$="":GOTO8570
8560 CR$=RIGHT$(A$,K-IL-1):DR$=RIGHT$(T$(R),K-IL-1)
8570 IFCR$<>DR$ THENRETURN
8580 IFIL=1 THENRL$="":RR$=RIGHT$(A$,K-IL-1):GOTO8610
8590 IFK-IL-1=0 THENRL$=LEFT$(A$,IL-1):RR$="":GOTO8610
8600 RL$=LEFT$(A$,IL-1):RR$=RIGHT$(A$,K-IL-1)
8610 HO=5:VE=12:GOSUB110
8620 PRINT" ";RL$;"**";RR$;
8630 VE=VE+2:GOSUB110
8640 PRINT"Niet helemaal goed."
8650 VE=VE+1:GOSUB110
8660 PRINT"De ** geven aan"
8670 VE=VE+1:GOSUB110
8680 PRINT"waar de fouten zitten."
8690 VE=VE+2:GOSUB110
8700 PRINT"Tik de goede letters in."
8710 HO=LEN(RL$)+6:VE=12:GOSUB110
8720 GOSUB210
8730 PRINTIN$;
8740 I1$=IN$
8750 GOSUB210
8760 PRINTIN$
8770 I2$=IN$
8780 A$=RL$+I1$+I2$+RR$
8790 HO=0:VE=14:GOSUB110
8800 FORI=1 TO6
8810 PRINT"   !                                !   ";
8820 VE=VE+1:GOSUB110
8830 NEXTI
8840 RETURN
8850 REM
25000 REM DATA
25010 DATA"ATTENDANT"
25020 DATA"BIKE"
25030 DATA"BIRO"
25040 DATA"CLOAKROOM"
25050 DATA"COAT"
25060 DATA"DAUGHTER"
25070 DATA"EXCUSE ME SIR"
25080 DATA"EXERCISE BOOK"
25090 DATA"HOUSE"
25100 DATA"NUMBER"
25110 DATA"PARDON?"
25120 DATA"PENCIL"
25130 DATA"PLANE"
25140 DATA"RULER"
25150 DATA"SCHOOLBAG"
25160 DATA"SEAT"
25170 DATA"SHIRT"
25180 DATA"SUIT"
25190 DATA"TEACHER"
25200 DATA"HERE IS MY TICKET"
25210 DATA"UMBRELLA"
25220 DATA"THANK YOU VERY MUCH"
25230 DATA"IS THIS YOUR WATCH?"
25240 DATA"READY"
25250 DATA"WHAT'S YOUR NAME?"
25260 DATA"LONDON"
25270 DATA"SORRY SIR"
25280 DATA"AIR-HOSTESS"
25290 DATA"ARE YOU FRENCH?"
25300 DATA"CARPENTER"
25310 DATA"WHAT'S YOUR JOB?"
25320 DATA"DANCER"
25330 DATA"ARE YOU AN ENGINEER?"
25340 DATA"HELLO EVERYBODY"
25350 DATA"HOUSEWIFE"
25360 DATA"ICE-CREAM MAN"
25370 DATA"MECHANIC"
25380 DATA"WHAT NATIONALITY ARE YOU?"
25390 DATA"I'M A NURSE"
25400 DATA"HE'S A PILOT"
25410 DATA"POLICEWOMAN"
25420 DATA"SALESMAN"
25430 DATA"TAXI-DRIVER"
25440 DATA"SHE'S A TYPIST"
25450 DATA"IS HE AMERICAN?"
25460 DATA"NO HE ISN'T"
25470 DATA"IT'S A BELGIAN FARM"
25480 DATA"ARE YOU DANISH?"
25490 DATA"I'M DUTCH"
25500 DATA"HE'S AN ENGLISH POSTMAN"
25510 DATA"IT'S A GERMAN CAR"
25520 DATA"HE'S AN IRISH FARMER"
25530 DATA"ROBERT IS ITALIAN"
25540 DATA"THIS IS MY PLANE"
25550 DATA"WHAT MAKE IS IT?"
25560 DATA"GOOD AFTERNOON"
25570 DATA"NICE TO SEE YOU"
25580 DATA"HOW ARE YOU TODAY?"
25590 DATA"I'M VERY WELL"
25600 DATA"HOW DO YOU DO?"
25610 DATA"GOODBYE ALICE"
25620 DATA"NICE TO MEET YOU"
25630 DATA"LOOK AT THAT GIRL"
25640 DATA"SHE'S BUSY"
25650 DATA"IT'S A JAPANESE RADIO"
25660 DATA"THAT BOY IS LAZY"
25670 DATA"MRS HARPER IS COLD"
25680 DATA"THE POLICEMAN IS FAT"
25690 DATA"HIS WIFE IS THIN"
25700 DATA"I'M FINE THANKS"
25710 DATA"HERE SHE IS"
25720 DATA"CLASS-ROOM"
25730 DATA"HE'S TALL"
25740 DATA"THAT WOMAN IS SHORT"
25750 DATA"SHE'S VERY YOUNG"
25760 DATA"*"
30000 REM***********************
30010 REM*                     *
30020 REM* HARRY ENGEL         *
30030 REM* FEBRUARI 1985       *
30040 REM* SG. EYSENHEGGE      *
30050 REM* VSO - LOM - SCHOOL  *
30060 REM* VALKSTRAAT 4        *
30070 REM* 6135 GC SITTARD     *
30080 REM* 04490 - 22088       *
30090 REM*                     *
30100 REM***********************
30110 REM
30120 REM COPYRIGHT NOS HILVERSUM
30130 REM
30140 REMOPMERKING VAN DE BASICODE
30150 REMREDACTIE:
30160 REMVoor veel computers -bv C-64-
30170 REMgeldt: Tik de antwoorden
30180 REMexact als het voorbeeld in.
30190 REMDus: met hoofdletters en de
30200 REMShift-toets loslaten bij de
30210 REMspaties !!
30220 REMEventueel de data-regels
30230 REMaan uw computer aanpassen!

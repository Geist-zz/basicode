
1000 A=100:GOTO20:REM  wiskunde programma
1010 REM 
1020 REM   module keuzemenu
1030 GOSUB100
1040 HO=8:VE=2:GOSUB110
1050 PRINT"keuzemenu"
1060 HO=5:VE=7:GOSUB110
1070 PRINT"1. abc-formule"
1080 HO=5:VE=10:GOSUB110
1090 PRINT"2. stelling van Pythagoras"
1100 HO=5:VE=13:GOSUB110
1110 PRINT"3. cosinusregel"
1120 HO=5:VE=16:GOSUB110
1130 PRINT"4. beeindigen"
1140 HO=8:VE=22:GOSUB110
1150 PRINT"Uw keuze:"
1160 GOSUB210:REM  toets afvangen
1165 IF(ASC(IN$)-48>4) OR(ASC(IN$)-48<1) THEN1160
1170 ON(ASC(IN$)-48) GOSUB2000,3000,5000,1190
1180 GOTO1010:REM  keuzemenu
1190 GOSUB100
1200 HO=0:VE=12:GOSUB110
1210 PRINT"Programma beeindigd"
1220 END
2000 REM 
2010 REM   module abc-formule
2020 REM 
2030 GOSUB100
2040 HO=8:VE=0:GOSUB110
2050 PRINT">> abc-formule <<"
2060 HO=0:VE=5:GOSUB110
2070 PRINT"Voer de a in ";:INPUTA
2080 PRINT:PRINT"Voer de b in ";:INPUTB
2090 PRINT:PRINT"Voer de c in ";:INPUTC
2100 GOSUB9000:REM  goed zo ?
2110 IF(IN$="j") OR(IN$="J") THEN2130
2120 IF(IN$="n") OR(IN$="N") THEN2030
2125 GOTO2100:REM  geen j of n
2130 IF(A=0) AND(B=0) AND(C=0) THEN2910
2140 GOSUB9060:REM  >>uitwerking<<
2150 D=B^2-4*A*C
2160 IF(D<0) THEN2910
2170 X=(-B+SQR(D))/(2*A)
2180 Y=(-B-SQR(D))/(2*A)
2190 HO=0:VE=2:GOSUB110
2200 PRINT"  -";B;"+-V";B^2;"-";4*A*C
2210 PRINT"X=------------------"
2220 PRINT"        ";2*A
2230 HO=0:VE=7:GOSUB110
2240 PRINT"  -";B;"+";SQR(D)
2250 PRINT"X=------------"
2260 PRINT"     ";2*A
2270 HO=0:VE=11:GOSUB110
2280 PRINT"   ";-B+SQR(D)
2290 PRINT"X=--------"
2300 PRINT"   ";2*A
2310 HO=0:VE=15:GOSUB110
2320 PRINT"X=";X
2330 HO=23:VE=7:GOSUB110
2340 PRINT"  -";B;"-";SQR(D)
2350 VE=8:GOSUB110:PRINT"X=---------"
2360 VE=9:GOSUB110:PRINT"     ";2*A
2370 VE=11:GOSUB110
2380 PRINT"   ";-B-SQR(D)
2390 VE=12:GOSUB110
2400 PRINT"X=--------"
2410 VE=13:GOSUB110
2420 PRINT"   ";2*A
2430 VE=15:GOSUB110
2440 PRINT"X=";Y
2510 HO=16:VE=20:GOSUB110
2520 PRINT"en"
2530 HO=0:VE=17:GOSUB110
2540 PRINT"De snijpunten van deze functie met de"
2550 PRINT"X-as zijn dus:"
2560 HO=0:VE=20:GOSUB110
2570 PRINT"(";X;", 0 )"
2580 HO=23:GOSUB110
2590 PRINT"(";Y;", 0 )"
2600 GOSUB9120:REM <<RETURN>>
2610 GOSUB100
2620 HO=12:VE=0:GOSUB110
2630 PRINT">> top <<"
2640 HO=0:VE=12:GOSUB110
2650 PRINT"Wilt u de top van deze functie weten ?"
2660 GOSUB210
2670 IF(IN$="j") OR(IN$="J") THEN2700
2675 IF(IN$="n") OR(IN$="N") THENRETURN
2690 GOTO2660
2700 T=X+Y:U=T/2
2705 GOSUB9060
2710 HO=0:VE=5:GOSUB110
2720 PRINTX;"+";Y;"=";T
2730 PRINTT;"/ 2 =";U
2740 PRINT:PRINT"Nu gaan we f (";U;") uitrekenen."
2750 GOSUB9120:REM  return
2760 GOSUB9060:REM  >>uitwerkin<<
2770 V=A*(U^2):W=B*U:M=V+W+C
2780 HO=5:VE=5:GOSUB110
2790 PRINTA;"X^2 +";B;"X +";C
2800 PRINT:PRINT"==>  ";A;"*";U^2;"+";B;"*";U;"+";C
2810 PRINT:PRINT"==>  ";V;"+";W;"+";C;"=";M
2820 HO=0:VE=15:GOSUB110
2830 PRINT"De top is dus:"
2840 PRINT:PRINT"T=(";U;",";M;")"
2850 HO=20:VE=15:GOSUB110
2860 PRINT"De extreem is dus:"
2870 HO=20:VE=17:GOSUB110
2880 PRINT"f (";U;") =";M
2890 GOSUB9120:REM  <<return>>
2900 RETURN:REM  keuzemenu
2910 GOSUB9060:REM  uitwerking
2915 HO=0:VE=10:GOSUB110
2920 PRINT"Deze functie heeft geen originelen van"
2925 PRINT"nul."
2930 GOSUB9120:RETURN
3000 REM 
3010 REM  module stelling v. Pythagoras
3020 REM 
3030 GOSUB100
3040 HO=5:VE=0:GOSUB110
3050 PRINT"<< Stelling v. Pythagoras >>"
3060 GOSUB9900:REM  driehoek
3070 HO=15:VE=5:GOSUB110
3080 PRINT"Met dit deel van het"
3090 VE=6:GOSUB110:PRINT"programma kunt u de"
3100 VE=7:GOSUB110:PRINT"stelling van pythagoras"
3110 VE=8:GOSUB110:PRINT"toepassen. De schuine"
3120 VE=9:GOSUB110:PRINT"zijde moet altijd de"
3130 VE=10:GOSUB110:PRINT"variabele -C- hebben."
3140 HO=10:VE=6:CH$="C":GOSUB10050
3150 REM  ^^ wat en waar knipperen ^^
3160 HO=15:VE=12:GOSUB110:PRINT"De andere zijden hebben"
3170 VE=13:GOSUB110:PRINT"de variabelen -A- en -B-."
3180 HO=1:VE=7:CH$="A":GOSUB10050
3190 HO=7:VE=13:CH$="B":GOSUB10050
3200 HO=0:VE=18:GOSUB110
3210 PRINT"Is de schuine zijde bekend ? (j/n)";:GOSUB210
3220 IF(IN$="n") OR(IN$="N") THEN3250
3230 IF(IN$="j") OR(IN$="J") THEN3530
3240 GOTO3200:REM  geen j of n ingedrukt
3245 REM  c niet bekend
3250 GOSUB100
3260 HO=5:VE=0:GOSUB110
3270 PRINT"<< Stelling v. Pythagoras >>"
3280 HO=0:VE=5:GOSUB110:PRINT"voer de -A- in ";:INPUTA
3290 PRINT:PRINT"voer de -B- in ";:INPUTB
3300 GOSUB9000:REM  goed zo
3310 IF(IN$="n") OR(IN$="N") THEN3250
3320 IF(IN$="j") OR(IN$="J") THEN3340
3330 GOTO3300:REM  geen j of n
3340 IF(A<0) OR(B<0) THENGOSUB10135:GOTO3250
3350 GOSUB9060
3360 L=SQR(A^2+B^2)
3370 GOSUB9900:REM  driehoek
3375 REM  ABC bij driehoek
3380 HO=10:VE=6:CH$="C":GOSUB10050
3390 HO=1:VE=7:CH$="A":GOSUB10050
3400 HO=7:VE=13:CH$="B":GOSUB10050
3410 HO=5:VE=16:GOSUB110
3420 PRINT"C^2 =";A^2;"+";B^2
3430 VE=17:GOSUB110
3440 PRINT"C^2 =";A^2+B^2
3450 VE=18:GOSUB110
3460 PRINT"C   =";L
3470 HO=17:VE=7:GOSUB110
3480 PRINT"A =";A
3490 VE=8:GOSUB110:PRINT"B =";B
3500 VE=9:GOSUB110:PRINT"C =";L
3510 GOSUB9120:REM  <return>
3520 RETURN
3530 REM  c wel bekend
3540 GOSUB100
3550 HO=5:VE=0:GOSUB110
3560 PRINT"<< Stelling v. Pythagoras >>"
3570 HO=0:VE=5:GOSUB110
3580 PRINT"Voer de -C- in :";:INPUTC
3590 PRINT:PRINT"Voer de andere bekende zijde in :";:INPUTA
3600 GOSUB9000
3610 IF(IN$="n") OR(IN$="N") THEN3540
3620 IF(IN$="j") OR(IN$="J") THEN3640
3630 GOTO3600:REM  geen j of nee 
3640 IF(C<0) OR(A<0) THENGOSUB10135:GOTO3540
3650 IF(C<A) THENGOSUB10150:GOTO3540
3660 GOSUB9060:L=SQR(C^2-A^2)
3670 GOSUB9900
3680 HO=10:VE=6:CH$="C":GOSUB10050
3690 HO=1:VE=7:CH$="A":GOSUB10050
3700 HO=7:VE=13:CH$="B":GOSUB10050
3710 HO=3:VE=16:GOSUB110
3720 PRINTC;"^2 =";A;"^2 + B^2"
3730 HO=5:VE=17:GOSUB110:PRINTC^2;"=";A^2;"+ B^2"
3740 VE=18:GOSUB110:PRINT"B^2 =";C^2;"-";A^2
3750 VE=19:GOSUB110:PRINT"B^2 =";C^2-A^2
3760 VE=20:GOSUB110:PRINT"B   =";L
3770 HO=17:VE=7:GOSUB110:PRINT"A =";A
3780 VE=8:GOSUB110:PRINT"B =";L
3790 VE=9:GOSUB110:PRINT"C =";C
3800 GOSUB9120:REM  <return>
3810 RETURN
5000 REM 
5010 REM  module cosinusregel
5020 REM 
5030 GOSUB100
5040 HO=5:VE=0:GOSUB110:PRINT"<< cosinusregel >>"
5050 HO=5:VE=6:GOSUB110:PRINT"kies versie:"
5060 HO=2:VE=10:GOSUB110
5070 PRINT"1. a^2 = b^2 + c^2 - 2bc cos A"
5080 VE=13:GOSUB110
5090 PRINT"2. b^2 = a^2 + c^2 - 2ac cos B"
5100 VE=16:GOSUB110
5110 PRINT"3. c^2 = a^2 + b^2 - 2ab cos C"
5120 HO=5:VE=22:GOSUB110
5130 PRINT"Uw keuze";:GOSUB210
5140 IF(ASC(IN$)-48<1) OR(ASC(IN$)-48>3) THEN5130
5150 ON(ASC(IN$)-48) GOSUB6000,7000,8000
5460 RETURN:REM  hoofdmenu
5999 REM  c.r. module 1
6000 GOSUB10200:REM  submenu 
6010 IF(ASC(IN$)-48=2) THEN6500
6020 GOSUB100:HO=5:VE=1:GOSUB110
6030 PRINT"<< c.r. versie 1 (ZZZ) >>"
6040 HO=0:VE=5:GOSUB110
6050 PRINT"Voer de a in :";:INPUTA
6060 PRINT:PRINT"Voer de b in :";:INPUTB
6070 PRINT:PRINT"Voer de c in :";:INPUTC
6080 GOSUB9000
6090 IF(IN$="n") OR(IN$="N") THEN6020
6100 IF(IN$="j") OR(IN$="J") THEN6120
6110 GOTO6080:REM  geen j of n
6120 GOSUB9060:QI=1.5708:Z=2*B*C:Y=B^2+C^2-A^2:W=Y/Z
6130 V=-1*(ATN(W/SQR(-1*W^2+1)))+QI
6140 HO=5:VE=4:GOSUB110
6150 PRINT"a^2 = b^2 + c^2 - 2bc cos A"
6160 HO=0:VE=6:GOSUB110
6170 PRINT"==> ";A^2;" = ";B^2;" + ";C^2;" - ";Z;"cos A"
6180 VE=8:GOSUB110
6190 PRINT"==> ";Z;"cos A = ";Y
6200 VE=10:GOSUB110
6210 PRINT"==>  cos A = ";W
6220 VE=12:GOSUB110
6230 PRINT"==> ";" A = ";V/(4*QI/360)
6240 GOSUB9120:RETURN
6500 REM  zhz
6510 GOSUB100:HO=5:VE=0:GOSUB110
6520 PRINT"<< c.r. versie 1 (ZHZ) >>"
6530 HO=0:VE=5:GOSUB110
6540 PRINT"Voer de b in:";:INPUTB
6550 PRINT:PRINT"voer de c in:";:INPUTC
6560 PRINT:PRINT"voer hoek alpha (A) in:";:INPUTAL
6570 GOSUB9000
6580 IF(IN$="n") OR(IN$="N") THEN6510
6590 IF(IN$="j") OR(IN$="J") THEN6610
6600 GOTO6570:REM  geen j of n
6610 GOSUB9060:Y=B^2+C^2
6620 Z=2*B*C:QI=6.28319:UI=Y-(Z*COS(AL*QI/360))
6630 HO=5:VE=5:GOSUB110
6640 PRINT"a^2 = b^2 + c^2 - 2bc cos A"
6650 HO=0:VE=7:GOSUB110
6660 PRINT"==>  a^2 = ";B^2;" + ";C^2;" - ";Z;"cos";AL
6670 VE=9:GOSUB110
6680 PRINT"==>  a^2 = ";Y;" - ";Z;"cos";AL
6690 VE=11:GOSUB110
6700 PRINT"==>  a^2 = ";Y;" - ";Z*COS(AL*QI/360)
6710 VE=13:GOSUB110
6720 PRINT"==>  a^2 = ";UI
6730 VE=15:GOSUB110
6740 PRINT"==>   a  = ";SQR(UI)
6750 GOSUB9120:RETURN
7000 REM  c.r. module 2
7010 GOSUB10200
7020 IF(ASC(IN$)-48=2) THEN7500
7030 GOSUB100:HO=5:VE=0:GOSUB110
7040 PRINT"<< c.r. versie 2 (ZZZ) >>"
7050 HO=0:VE=5:GOSUB110
7060 PRINT"Voer de b in:";:INPUTB
7070 PRINT:PRINT"Voer de a in:";:INPUTA
7080 PRINT:PRINT"Voer de c in:";:INPUTC
7090 GOSUB9000:REM  goed zo ?
7100 IF(IN$="n") OR(IN$="N") THEN7030
7110 IF(IN$="j") OR(IN$="J") THEN7130
7120 GOTO7090:REM  geen j of n
7130 GOSUB9060:QI=1.570796:Z=2*A*C:Y=A^2+C^2:X=Y-B^2:W=X/Z
7140 V=-1*(ATN(W/SQR(-1*W^2+1)))+QI
7150 HO=5:VE=4:GOSUB110
7160 PRINT"b^2 = a^2 + c^2 - 2ac cos B"
7170 HO=0:VE=6:GOSUB110
7180 PRINT"==> ";B^2;" = ";A^2;" + ";C^2;" - ";Z;"cos B"
7190 VE=8:GOSUB110
7200 PRINT"==> ";B^2;" = ";Y;" - ";Z;" cos B"
7210 VE=10:GOSUB110
7220 PRINT"==> ";Z;"cos B = ";X
7230 VE=12:GOSUB110
7240 PRINT"==>  cos B = ";W
7250 VE=14:GOSUB110
7260 PRINT"==>  B     = ";V/(4*QI/360)
7270 GOSUB9120:RETURN
7500 REM  zhz
7510 GOSUB100:HO=5:VE=0:GOSUB110
7520 PRINT"<< c.r. Versie 2 (ZHZ) >>"
7530 HO=0:VE=4:GOSUB110
7540 PRINT"Voer de a in:";:INPUTA
7550 PRINT:PRINT"Voer de c in:";:INPUTC
7560 PRINT:PRINT"Voer hoek beta (B) in:";:INPUTBE
7570 GOSUB9000
7580 IF(IN$="n") OR(IN$="N") THEN7510
7590 IF(IN$="j") OR(IN$="J") THEN7610
7600 GOTO7570:REM  geen j of n
7610 GOSUB9060:Z=2*A*C:Y=A^2+C^2:QI=6.28319
7620 UI=Y-Z*COS(BE*QI/360)
7630 HO=5:VE=4:GOSUB110
7640 PRINT"b^2 = a^2 + c^2 - 2ac cos B"
7650 VE=6:GOSUB110
7660 PRINT"==>  b^2 = ";A^2;" + ";C^2;" - ";Z;"cos";BE
7670 VE=8:GOSUB110
7680 PRINT"==>  b^2 = ";Y;" - ";Z;"COS";BE
7690 VE=10:GOSUB110
7700 PRINT"==>  b^2 = ";Y;" - ";Z*COS(BE*QI/360)
7710 VE=12:GOSUB110
7720 PRINT"==>  b^2 = ";UI
7730 VE=14:GOSUB110
7740 PRINT"==>   b  = ";SQR(UI)
7750 GOSUB9120:RETURN
8000 REM  c.r. versie 3
8010 GOSUB10200:REM  submenu
8020 IF(ASC(IN$)-48=2) THEN8500
8030 GOSUB100:HO=5:VE=0:GOSUB110
8040 PRINT"<< c.r. versie 3 (ZZZ) >>"
8050 HO=0:VE=4:GOSUB110
8060 PRINT"Voer de c in:";:INPUTC
8070 PRINT:PRINT"Voer de a in:";:INPUTA
8080 PRINT:PRINT"Voer de b in:";:INPUTB
8090 GOSUB9000:REM  goed zo?
8100 IF(IN$="n") OR(IN$="N") THEN8030
8110 IF(IN$="j") OR(IN$="J") THEN8130
8120 GOTO8090:REM  geen j ofn
8130 GOSUB9060:QI=1.570796:Z=2*A*B:Y=A^2+B^2:X=Y-C^2
8140 W=X/Z:V=-1*(ATN(W/SQR(-1*W^2+1)))+QI
8150 HO=5:VE=4:GOSUB110
8160 PRINT"c^2 = a^2 + b^2 - 2ab cos C"
8170 HO=0:VE=6:GOSUB110
8180 PRINT"==> ";C^2;" = ";A^2;" + ";B^2;" - ";Z;"cos C"
8190 VE=8:GOSUB110
8200 PRINT"==> ";C^2;" = ";Y;" - ";Z;"cos C"
8210 VE=10:GOSUB110
8220 PRINT"==> ";Z;"cos C = ";X
8230 VE=12:GOSUB110
8240 PRINT"==>  cos C = ";W
8250 VE=14:GOSUB110
8260 PRINT"==>   C    = ";V/(4*QI/360)
8270 GOSUB9120:RETURN
8500 REM  zhz
8510 GOSUB100:HO=5:VE=0:GOSUB110
8520 PRINT"<< c.r. versie 3 (ZHZ) >>"
8530 HO=0:VE=4:GOSUB110
8540 PRINT"Voer de a in:";:INPUTA
8550 PRINT:PRINT"Voer de b in:";:INPUTB
8560 PRINT:PRINT"Voer hoek gamma (C) in:";:INPUTGA
8570 GOSUB9000
8580 IF(IN$="n") OR(IN$="N") THEN8510
8590 IF(IN$="j") OR(IN$="J") THEN8610
8600 GOTO8570:REM  geen j of n
8610 GOSUB9060:Z=2*A*B:Y=A^2+B^2:QI=6.28319
8620 UI=Y-(Z*COS(GA*QI/360))
8630 HO=5:VE=4:GOSUB110
8640 PRINT"c^2 = a^2 + b^2 - 2ab cos C"
8650 HO=0:VE=6:GOSUB110
8660 PRINT"==>  c^2 = ";A^2;" + ";B^2;" - ";Z;"cos ";GA
8670 VE=8:GOSUB110
8680 PRINT"==>  c^2 = ";Y;" - ";Z;"cos";GA
8690 VE=10:GOSUB110
8700 PRINT"==>  c^2 = ";UI
8710 VE=12:GOSUB110
8720 PRINT"==>   c  = ";SQR(UI)
8730 GOSUB9120:RETURN
9000 REM 
9010 REM  module hulproutines
9015 REM 
9020 REM  goed zo? op scherm
9030 HO=5:VE=12:GOSUB110
9040 PRINT"Goed zo ? (j/n) : ";:GOSUB210
9050 RETURN
9060 REM 
9070 REM  >>uitwerking<< op scherm
9080 GOSUB100
9090 HO=8:VE=0:GOSUB110
9100 PRINT">> uitwerking <<"
9110 RETURN
9120 REM 
9130 REM  <<RETURN>> op scherm
9140 HO=0:VE=24:GOSUB110
9150 PRINT"<<RETURN>>";
9160 GOSUB210
9170 IF(ASC(IN$)<>13) THEN9160
9780 RETURN
9900 REM 
9910 REM  driehoek
9920 FORI=1 TO8
9930 VE=I+3:HO=I+3:GOSUB110
9940 PRINT"*"
9950 NEXTI
9960 FORI=8 TO1 STEP-1
9970 VE=11:HO=I+3:GOSUB110
9980 PRINT"*"
9990 NEXTI
10000 FORI=8 TO1 STEP-1
10010 HO=4:VE=I+3:GOSUB110
10020 PRINT"*"
10030 NEXTI
10040 RETURN
10050 REM 
10060 REM  driehoek knipper
10070 FORI=1 TO10
10080 GOSUB110:PRINT"  "
10090 FORJ=1 TO10:NEXTJ
10100 GOSUB110:PRINTCH$
10110 FORJ=1 TO10:NEXTJ
10120 NEXTI
10130 RETURN
10135 REM 
10136 REM  foutmelding
10140 F$="Geen negatieve getallen invoeren !":G$=""
10145 GOTO10170
10150 F$="De schuine zijde kan nooit korter"
10160 G$="dan de rechte zijde zijn !"
10170 HO=5:VE=16:GOSUB110:PRINTF$:GOSUB250
10175 VE=17:GOSUB110:PRINTG$
10180 GOSUB9120:REM  <<return>>
10185 GOSUB250
10190 RETURN
10200 REM 
10210 REM  submenu cosinusregel
10220 GOSUB100:HO=5:VE=1:GOSUB110
10230 PRINT"<< cosinusregel >>"
10240 HO=6:VE=5:GOSUB110:PRINT"Wat is er bekend"
10250 HO=6:VE=6:GOSUB110:PRINT"----------------"
10260 HO=1:VE=10:GOSUB110:PRINT"1. De drie zijden (ZZZ)"
10270 VE=14:GOSUB110
10280 PRINT"2. Twee zijden & ingesloten hoek (ZHZ)"
10290 HO=5:VE=22:GOSUB110
10300 PRINT"Uw keuze:":GOSUB210
10310 IF(ASC(IN$)-48<1) OR(ASC(IN$)-48>2) THEN10290
10320 RETURN
20000 REM 
20010 REM   de 'V' in de stelling van
20020 REM   Pythagoras is een wortel-
20030 REM   teken. de '+-' staat voor
20040 REM   plus OF min.
20050 REM 
30000 REM    wiskunde programma
30040 REM        versie 5       
30050 REM    bevat abc-formule
30060 REM 
30070 REM  stelling v. Pythagoras
30080 REM 
30090 REM       cosinusregel
30100 REM 
30120 REM 
30130 REM    geschreven op een
30140 REM   P2000T 16k  05-05-'85
30145 REM   laatste bewerking op
30148 REM        07-09-'85
30150 REM 
30160 REM      J. Valkenburg
30170 REM      tooroplaan 10
30180 REM    3141 KP  Maassluis
30190 REM
30200 REM COPYRIGHT
30210 REM NOS HILVERSUM

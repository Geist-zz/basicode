
1000 A=500:GOTO20
1010 FR=0:KE=0
1020 DIMA$(40),B$(9),A(25),B(25)
1030 GOSUB19500
1040 GOSUB1500
1050 GOSUB1795
1060 KE$="let":GOSUB12000:GOSUB2000
1070 GOSUB19700
1080 KE$="licht":GOSUB12000:GOSUB3000
1090 GOSUB19700
1100 KE$="inv":GOSUB12000:GOSUB4000
1110 GOSUB19700
1120 KE$="woord":GOSUB12000:GOSUB5000
1130 GOSUB19700
1140 KE$="vang":GOSUB12000:GOSUB6000
1150 GOTO9999
1500 REM**MANNEKE
1505 A=(VM-16)/2:GOSUB100
1510 VE=A:HO=8:GOSUB110:PRINT"*";
1515 VE=A+1:HO=6:GOSUB110:PRINT"*";
1520 VE=A+2:HO=5:GOSUB110:PRINT"*";
1525 VE=A+3:HO=4:GOSUB110:PRINT"*";
1530 VE=A+4:HO=3:GOSUB110:PRINT"*";
1535 VE=A+6:HO=2:GOSUB110:PRINT"*";
1540 VE=A+7:GOSUB110:PRINT"*";
1545 VE=A+8:GOSUB110:PRINT"*";
1550 VE=A+9:GOSUB110:PRINT"*";
1555 VE=A+10:GOSUB110:PRINT"*";
1560 VE=A+12:HO=3:GOSUB110:PRINT"*";
1565 VE=A+13:HO=4:GOSUB110:PRINT"*";
1570 VE=A+14:HO=5:GOSUB110:PRINT"*     *";
1575 VE=A+15:HO=6:GOSUB110:PRINT"****";
1580 A=A+5:B=HM-3
1585 VE=A+8:HO=B:GOSUB110:PRINT" JJ ";
1590 VE=A+7:GOSUB110:PRINT" II ";
1595 VE=A+6:GOSUB110:PRINT" II ";
1600 VE=A:GOSUB110:PRINT" == ";
1605 VE=A+1:GOSUB110:PRINT";.] ";
1610 VE=A+2:GOSUB110:PRINT": ] ";
1615 VE=A+3:GOSUB110:PRINT" ][ ";
1620 VE=A+4:GOSUB110:PRINT" () ";
1625 VE=A:GOSUB110:PRINT" == ";
1630 FORT=0 TO80:NEXTT
1635 IFB=6 THEN1675
1640 VE=A+8:GOSUB110:PRINT"J J ";
1645 VE=A+7:GOSUB110:PRINT"/ I ";
1650 VE=A+6:GOSUB110:PRINT" /I ";
1655 VE=A+4:GOSUB110:PRINT"/() ";
1660 FORT=0 TO50:NEXTT
1665 B=B-1
1670 GOTO1585
1675 FORT=0 TO310:NEXTT
1680 VE=A+1:GOSUB110:PRINT"<!!>";
1685 VE=A+2:GOSUB110:PRINT"(--)";
1690 VE=A+3:GOSUB110:PRINT" >< ";
1695 VE=A+4:GOSUB110:PRINT"/()]";
1700 VE=A+6:GOSUB110:PRINT" II ";
1705 VE=A+7:GOSUB110:PRINT" II ";
1710 VE=A+8:GOSUB110:PRINT" Jl ";
1715 VE=A-2:HO=7:GOSUB110:PRINT"HET MAANEKE";
1720 VE=A+5:HO=11:GOSUB110:PRINT"IN DE";
1725 VE=A+7:GOSUB110:PRINT"MAN";
1730 FORT=0 TO4000:NEXTT
1735 VE=A+1:HO=11:GOSUB110:PRINT"ehh";
1740 VE=A+2:GOSUB110:PRINT"mpf..burp";
1745 FORT=0 TO4000:NEXTT
1750 VE=A+1:GOSUB110:PRINT"   ";
1755 VE=A+2:GOSUB110:PRINT"         ";
1760 VE=A-2:HO=7:GOSUB110:PRINT"           ";
1765 VE=A+7:HO=11:GOSUB110:PRINT"    ";
1770 FORT=0 TO1000:NEXTT
1775 VE=A-2:HO=7:GOSUB110:PRINT"HET MANNEKE";
1780 VE=A+7:HO=11:GOSUB110:PRINT"MAAN";
1785 FORT=0 TO6000:NEXTT
1790 RETURN
1795 GOSUB10000:GOSUB19000:NA$=A$
1800 IFNA$="" THEN1815
1805 IFLEN(NA$)>=20 THEN1835
1810 RETURN
1815 GOSUB10000:GOSUB19000:NA$=A$
1820 IFNA$="" THENNA$="pukkel"
1825 IFLEN(NA$)>=20 THEN1835
1830 RETURN
1835 KE$="lang":GOSUB12000:GOSUB10000
1840 GOSUB19000:NA$=A$
1845 IF(LEN(NA$)>=20) OR(NA$="") THENNA$="uk"
1850 RETURN
2000 REM**LETTERGREPEN**
2010 GOSUB100
2020 FO=0:B$="":KE=2:X=0
2030 GOSUB10008
2040 A$(X)=B$:A=X:GOTO2100
2050 A$(X)=LEFT$(B$,LR):X=X+1:RETURN
2100 X=0:REM**PRINTEN
2110 HO=0:VE=2*X+1:GOSUB110
2120 FORB=1 TOLEN(A$(X))
2130 A$=MID$(A$(X),B,1)
2140 IFA$="-" THENA$=""
2150 PRINTA$;:FORT=0 TO20:NEXTT
2160 NEXTB
2170 X=X+1:IFX>B THEN2200
2180 GOTO2110
2200 REM**NU MET -**
2210 FORT=0 TO6000:NEXTT
2220 FORX=0 TOA
2230 HO=0:VE=X*2+1:GOSUB110
2240 FORTE=1 TOLEN(A$(X))
2250 A$=MID$(A$(X),TE,1)
2260 IFTE=1 THEN2290
2270 GOSUB2500
2280 GOSUB2600
2290 PRINTA$;:IFA$="-" THENGOSUB2400
2300 NEXTTE
2320 REM**EINDE REGEL
2330 HO=TE-3:VE=VE-1:GOSUB110:PRINT"  ";
2340 VE=VE+2:GOSUB110:PRINT"  ";
2350 NEXTX
2360 FORT=0 TO3000:NEXTT
2370 GOSUB10000:PRINTFO;:GOSUB10010
2380 RETURN
2400 REM**BEHANDELING -**
2410 GOSUB120
2420 FORT=TE+1 TOLEN(A$(X))
2430 A$=MID$(A$(X),T,1)
2440 IFA$="-" THENA$=""
2450 PRINTA$;
2460 NEXTT
2470 GOSUB110:RETURN
2500 REM**MERKTEKEN**
2510 VE=VE-1:HO=TE-3
2520 IFHO=-1 THEN2560
2530 GOSUB110:PRINT" ><";
2540 VE=VE+2:GOSUB110:PRINT" ><";
2550 VE=VE-1:HO=HO+2:GOSUB110:RETURN
2560 HO=0:GOSUB110:PRINT"><";
2570 VE=VE+2:GOSUB110:PRINT"><";
2580 HO=1:VE=VE-1:GOSUB110:RETURN
2600 T=0:REM**INPUT**
2610 GOSUB200:IFIN$=CHR$(13) THEN2700
2620 T=T+1:IFT<=15 THEN2610
2630 IFA$="-" THEN2650
2640 RETURN
2650 HO=TE-1:GOSUB110:PRINTA$;:GOSUB2400
2660 HO=HO-1:GOSUB110:A$="-":GOSUB2720:RETURN
2700 REM**BESLISBLOK**
2710 IFA$="-" THEN:RETURN
2720 GOSUB120:FO=FO+1
2730 FORB=0 TO6
2740 PRINTA$;:GOSUB110
2750 FORT=0 TO20:GOSUB200:NEXTT
2760 PRINT"*";:GOSUB110
2770 FORT=0 TO100:NEXTT
2780 NEXTB:GOSUB200
2790 RETURN
3000 REM**LICHTKRANT**
3010 GOSUB10000
3020 A=15:B=100:C=INT(A/2):B$="":D$="":FO=0
3030 READA$:IFA$="%" THEN3050
3040 B$=B$+A$:GOTO3030
3050 FORT=1 TOLEN(B$)
3060 A$=MID$(B$,T,1)
3070 IF(A$="a") OR(A$="e") OR(A$="i") THENA$="*"
3080 IF(A$="o") OR(A$="u") THENA$="*"
3090 D$=D$+A$
3100 NEXTT
3110 GOSUB19700:GOSUB100:TE=1
3120 HO=(HM-A)/2:VE=VM/2-3:GOSUB110
3130 FORT=0 TOA+1:PRINT"-";:NEXTT
3140 VE=VE+2:GOSUB110:FORT=0 TOA+1:PRINT"-";:NEXTT
3150 HO=HO+C-1:GOSUB110:PRINT"^";
3160 VE=VE-1:HO=HO-C+2+A:GOSUB110:PRINT"=";
3170 HO=HO-A-1:GOSUB110:PRINT"=";
3180 HO=HO+1
3190 FORT=0 TOA:A$=A$+" ":NEXTT:C$=A$
3200 REM**VENSTER EN AFDRUKKEN**
3205 A$=RIGHT$(A$,A-1)+MID$(B$,TE,1)
3210 C$=RIGHT$(C$,A-1)+MID$(D$,TE,1)
3215 E$=MID$(A$,C-1,1)
3220 GOSUB110:PRINTC$;
3225 FORT=0 TOB
3230 GOSUB200
3235 IFVAL(IN$)>0 THENB=INT(460/(VAL(IN$)+2))-36
3240 IF(IN$=E$) AND(IN$<>" ") THENGOSUB3300
3245 NEXTT
3250 IFB$=D$ THEN3400
3255 FO=FO+B
3260 TE=TE+1:IFTE>LEN(B$) THENTE=1
3265 GOTO3205
3300 REM**JUISTE LETTER**
3310 HO=HO+C-2:GOSUB110:PRINTE$
3320 FORT=0 TO100:NEXTT
3330 D=TE-A+C-2:IFD<0 THEND=LEN(B$)+D
3340 C$=LEFT$(C$,C-2)+E$+RIGHT$(C$,A-C+1)
3350 D$=LEFT$(D$,D)+E$+RIGHT$(D$,LEN(D$)-D-1)
3360 T=B+1:HO=HO-C+2
3370 RETURN
3400 REM**EINDE LICHTKRANT**
3410 FORTE=0 TO3000:NEXTTE
3420 B$="":GOSUB10000
3430 KE$="1"
3440 IFFO<18000 THENKE$="2"
3450 IFFO<10000 THENKE$="3"
3460 IFFO<4000 THENKE$="4"
3470 GOSUB12000:GOSUB10010
3480 RETURN
4000 REM**INVULOEFENING**
4010 B=0
4020 GOSUB10000
4030 X=0:FO=0
4040 KE=1:GOSUB10008:IFKE=0 THEN4060
4050 A$(X)=LEFT$(B$,LR):X=X+1:RETURN
4060 A$(X)=B$:B$=""
4070 GOSUB19700:GOSUB100:HO=0
4080 GOSUB4800
4090 X=0:FORT=0 TO9:GOSUB200:NEXTT
4100 READA$:IFA$="%" THEN4400:REM**EIND
4110 A=LEN(A$(X)):REM**ZOEKEN **
4120 FORTE=1 TOA-2
4130 B$=MID$(A$(X),TE,2)
4140 IFB$="**" THEN4170:REM DIT MAG DUS NIET !!!!!!!
4150 NEXTTE
4160 X=X+1:GOTO4110
4170 VE=X*2+1:HO=TE-1
4180 GOSUB200:IFIN$=CHR$(13) THEN4300
4190 IFIN$="" THENIN$="*"
4200 IF(ASC(IN$)<65) OR(ASC(IN$)>122) THENIN$=""
4210 B$=B$+IN$:IFLEN(B$)=3 THENB$=IN$
4220 GOSUB110:PRINT"  ";:FORT=0 TO300:NEXTT
4230 GOSUB110:PRINTLEFT$(B$+"**",2);
4240 FORT=0 TO300:NEXTT
4250 GOTO4180
4300 REM**BESLIS
4310 IFA$=B$ THENGOSUB4600
4320 IFA$<>B$ THENGOSUB4700:GOSUB4600
4330 GOTO4100
4400 REM:EINDE OEFENING
4410 FORT=0 TO4000:NEXTT
4420 IFFO<=1 THEN4565
4430 B=B+1
4440 IFB>=3 THEN4500
4450 GOSUB10000
4460 GOTO4030
4500 C=LEN(NA$):A=0:B=0:C$=""
4505 FORT=1 TOC
4510 A$=MID$(NA$,T,1):B$=MID$(NA$,T+1,1)
4515 IF(A$="a") OR(A$="e") OR(A$="i") THENA=1
4520 IF(A$="o") OR(A$="u") THENA=1
4525 IF(B$="a") OR(B$="e") OR(B$="i") THENB=1
4530 IF(B$="o") OR(B$="u") THENB=1
4535 IF(A=1) AND(B=1) THENC$=C$+A$:T=T+1:GOTO4550
4540 IF(A=1) AND(B=0) THENC$=C$+A$+A$:GOTO4550
4545 C$=C$+A$
4550 A=0:B=0
4555 NEXTT
4560 NA$=C$:GOSUB10000:GOTO9999
4565 KE$="goed":GOSUB12000
4570 GOSUB10000:RETURN
4600 REM**GOEDE ANTWOORD
4605 A$(X)=LEFT$(A$(X),TE-1)+A$+RIGHT$(A$(X),A-TE-1)
4610 HO=0:GOSUB110:
4620 FORT=0 TOHM:PRINT" ";:NEXTT
4630 GOSUB110:PRINTA$(X)
4640 RETURN
4700 REM**FOUTE ANTWOORD
4710 HO=0:GOSUB110:FO=FO+1
4720 FORT=0 TOHM:PRINT" ";:NEXTT
4730 GOSUB110:PRINT" Dat was niet goed."
4740 FORT=0 TO1300:NEXTT
4800 FORY=0 TOX
4810 VE=2*Y+1:HO=0:GOSUB110:PRINTA$(Y);
4820 NEXTY
4830 RETURN
5000 REM**WOORDEN VERBETEREN**
5010 GOSUB10000
5020 B=-1:C=0:E=2:IFVM<21 THENE=1
5030 B=B+1:READA$(B)
5040 IFA$(B)="%" THEN5050
5045 GOTO5030
5050 REM**10 WOORDEN
5055 FORT=0 TO9
5060 GOSUB260:A(T)=INT(RV*B):A=A(T)
5065 IFT=0 THEN5085
5070 TE=T-1
5075 IFA=A(TE) THEN5060
5080 TE=TE-1:IFTE>=0 THEN5075
5085 B$(T)=A$(A)
5090 NEXTT
5095 FO=0:B$="":C=C+1
5105 GOSUB19700:GOSUB100
5110 HO=0:X=0
5120 GOSUB5320
5150 VE=E*X+1:GOSUB110
5160 PRINTX;" ";:PRINTLEFT$(B$(X),TE-1)+RIGHT$(B$(X),1);
5165 A(X)=1
5170 GOSUB260:IFINT(RV*2)=1 THENPRINTRIGHT$(B$(X),1);:A(X)=2
5180 PRINTMID$(B$(X),TE+1,LEN(B$(X))-TE-3);
5190 X=X+1:IFX<=9 THEN5120
5200 REM**WOORD VERANDEREN
5210 GOSUB260:X=INT(RV*10):GOTO5400
5220 VE=E*X+1:HO=0:GOSUB110:PRINT"                 ";
5230 GOSUB5320
5240 FORT=0 TO500:NEXTT:GOSUB110
5245 PRINTX;" ";
5250 PRINTLEFT$(B$(X),TE-1)+RIGHT$(B$(X),1);
5260 IFA(X)=1 THENPRINTRIGHT$(B$(X),1);:A=2
5270 PRINTMID$(B$(X),TE+1,LEN(B$(X))-TE-3);
5280 A(X)=1:IFA=2 THENA(X)=2:A=1
5290 GOSUB200:IFVAL(IN$)=X THENIN$=""
5300 GOSUB5500:IFLEN(B$)=10 THEN5900
5310 GOTO5210
5320 TE=1
5330 A$=MID$(B$(X),TE,1)
5340 IFA$="-" THENRETURN
5350 TE=TE+1:GOTO5330
5400 REM**SELEKTIE WOORD
5410 SR=X:GOSUB300:A$=SR$
5420 FORTE=1 TOLEN(B$)
5430 IFA$=MID$(B$,TE,1) THEN5210
5440 NEXTTE
5450 GOTO5220
5500 REM**INPUT
5505 GOSUB260:A=INT(RV*200)+30
5510 FORTE=0 TOA
5520 GOSUB5550:GOSUB200
5530 NEXTTE
5540 RETURN
5550 REM**BESLISBLOK
5560 IF(IN$<>"0") AND(VAL(IN$)=0) THENRETURN
5570 FORT=1 TOLEN(B$)
5580 IFIN$=MID$(B$,T,1) THENRETURN
5590 NEXTT
5600 X=VAL(IN$):B$=B$+IN$
5610 VE=X*E+1:HO=0:GOSUB110
5620 IFA(X)=VAL(LEFT$(RIGHT$(B$(X),2),1)) THEN5800
5630 FO=FO+1
5640 PRINT"                ";
5650 GOSUB110:PRINT" Dat was fout.";
5660 FORT=0 TO1700:NEXTT
5670 GOSUB110:PRINT"              ";
5680 GOSUB5320
5690 GOSUB5730:C$=" "+C$
5700 GOSUB110:PRINTC$;
5710 HO=HO+1:IFHO+LEN(C$)>=HM THENRETURN
5720 FORT=0 TO80:NEXTT:GOTO5700
5730 C$=LEFT$(B$(X),TE-1)+RIGHT$(B$(X),1)
5740 IFLEFT$(RIGHT$(B$(X),2),1)<>"2" THEN5750
5745 C$=C$+RIGHT$(B$(X),1)
5750 C$=C$+MID$(B$(X),TE+1,LEN(B$(X))-TE-3)
5760 RETURN
5800 GOSUB5320:GOSUB5730
5810 C$=IN$+" "+C$+" ":D=LEN(C$)
5820 FORT=0 TO150:NEXTT
5830 GOSUB110:PRINTRIGHT$(C$,D);
5840 D=D-1:IFD=0 THEN:RETURN
5850 GOTO5820
5900 IFFO<>0 THEN5050
5910 IFC<4 THENKE$="1"
5915 IF(C<7) AND(C>=4) THENKE$="2"
5920 IF(C<11) AND(C>=7) THENKE$="3"
5930 IFC>=11 THENKE$="4"
5940 GOSUB12000
5950 GOSUB10000
5960 RETURN
6000 REM**LETTERREGEN
6010 GOSUB10000:B=(HM-21)/2:E=0
6015 GOSUB19700
6018 Y=B+1:A=13:D=3:C=0:F=.21:G=0
6020 FORX=1 TO25
6030 GOSUB260:A(X)=INT(RV*20)+1+B:B(X)=1
6040 GOSUB260:T=INT(RV*26)+65:IFT=89 THEN6040
6050 A$(X)=CHR$(T):NEXTX
6060 GOSUB100
6070 HO=B:VE=0:GOSUB110:PRINT"     LETTERREGEN";
6080 FORTE=0 TOVM-1
6090 VE=TE:HO=B:GOSUB110:PRINT"*";
6100 HO=21+B:GOSUB110:PRINT"*";:FORT=0 TO80:NEXTT
6110 NEXTTE:FORT=0 TO1000:NEXTT
6130 HO=B+1:VE=0:GOSUB110:PRINT"score 0  pijltjes 3";
6140 VE=VM-1:GOSUB110:PRINT"^";
6150 REM**REGEN ZELF
6160 FORX=1 TOA:GOSUB200
6170 IF(IN$="e") OR(IN$="o") THENGOSUB6250
6180 HO=A(X):VE=B(X):GOSUB110:PRINT" ";
6190 B(X)=B(X)+F*X:IFB(X)>=VM-1 THEN6300
6200 VE=B(X):VE=B(X):GOSUB110:PRINTA$(X);
6210 NEXTX
6220 GOTO6160
6250 HO=Y:VE=VM-1:GOSUB110:PRINT" ";
6260 IFIN$="e" THENY=Y-1:IFY=B THENY=B+20
6270 IFIN$="o" THENY=Y+1:IFY=B+21 THENY=B+1
6280 HO=Y:GOSUB110:PRINT"^";
6290 RETURN
6300 REM**BESLISBLOK**
6310 IF(A$(X)="A") OR(A$(X)="E") OR(A$(X)="I") THEN6400
6315 IF(A$(X)="O") OR(A$(X)="U") THEN6400
6320 IFA(X)=Y THEN6500
6330 GOSUB6700:REM**NIEUWELETTER**
6340 GOTO6210
6400 REM**KLINKER BENEDEN**
6410 IFA(X)<>Y THEN6460
6420 C=C+1
6430 VE=0:HO=7+B:GOSUB110:PRINTC;
6435 IFC=5 THEN:A=18:F=.17
6440 IFC=15 THEN:A=25:F=.15
6445 GOTO6330
6460 FORT=0 TO5
6470 HO=A(X):VE=VM:GOSUB110:PRINT" ";
6473 FORTE=0 TO100:NEXTTE
6475 HO=A(X):VE=VM:GOSUB110:PRINTA$(X);
6478 FORTE=0 TO100:NEXTTE
6480 NEXTT
6490 G=G+1:IFG=10 THEN6800
6495 GOTO6330
6500 REM**MEDEKLINKER GEVANGEN**
6510 FORTE=0 TO5
6520 HO=A(X):VE=VM-1:GOSUB110:PRINTA$(X);
6530 FORT=0 TO100:NEXTT
6540 GOSUB110:PRINT" ";
6550 FORT=0 TO100:NEXTT:GOSUB110:PRINT"^";
6560 NEXTTE
6570 D=D-1:VE=0:HO=19+B:GOSUB110:PRINTD;
6580 IFD=0 THEN6800
6590 GOTO6330
6700 REM**NIEUWE LETTER**
6710 GOSUB260:A(X)=INT(RV*20)+1+B
6720 GOSUB260:T=INT(RV*26)+65
6730 IFT=89 THEN6720
6740 A$(X)=CHR$(T):B(X)=1
6750 HO=A(X):VE=B(X):GOSUB110:PRINTA$(X);
6760 RETURN
6800 REM**EINDE SPEL**
6810 FORT=0 TO2000:NEXTT
6815 H=H+C:IFE=2 THEN6840
6820 GOSUB10000
6830 GOSUB200:IF(IN$="j") OR(IN$="J") THENE=E+1:GOTO6018
6835 IF(IN$="n") OR(IN$="N") THEN6840
6836 GOTO6830
6840 KE$="eind":GOSUB12000:GOSUB10000
6850 SR=H:GOSUB300:B$=B$+SR$:GOSUB10010
6860 RETURN
9999 FR=1:END
10000 REM**AFDRUKROUTINE**
10004 B$=""
10005 GOSUB100
10008 VE=1:HO=0:GOSUB110
10010 GOSUB120:LR=HM+1-HO
10020 IFLEN(B$)>=LR+1 THEN10090
10030 READA$
10040 IFA$="#" THENB$=B$+" ":GOTO10090
10050 IFA$="$" THENPRINTB$; GOSUB19700:GOTO10004
10060 IFA$="%" THENPRINTB$;:B$="":RETURN
10065 IFA$="&" THEN:KE=0:RETURN
10070 IFA$="na$" THENA$=NA$
10080 B$=B$+A$:IFLEN(B$)<=LR THEN10030
10090 IFLEN(B$)<LR THENLR=LEN(B$)
10095 IFMID$(B$,LR+1,1)=" " THEN10130
10100 LR=LR-1
10110 IFLR=-1 THENLR=HM+1:LS=LR:GOTO10135
10120 GOTO10090
10130 LS=LR+1
10135 IFKE<>0 THENONKE GOSUB4050,2050
10140 IFKE=0 THEN:PRINTLEFT$(B$,LR);
10145 LS=LS-1
10150 B$=RIGHT$(B$,LEN(B$)-LS)
10160 VE=VE+2:HO=0
10170 IF(VE>VM-3) AND(KE=0) THENGOSUB19700:GOTO10005
10180 GOSUB110:GOTO10010
12000 READA$:IFA$<>KE$ THEN12000
12010 RETURN
19000 REM**INPUT**
19010 A$="":INPUTA$
19020 RETURN
19500 REM**SCHERM OPMETEN
19510 HO=21:VE=15
19520 HO=HO+1:HM=HO:GOSUB110:GOSUB120:IFHM=HO THEN19520
19530 HM=HM-1:HO=0
19540 VE=VE+1:VM=VE:GOSUB110:GOSUB120:IFVM=VE THEN19540
19550 VM=VM-1:HO=0:VE=0
19560 RETURN
19600 GOSUB200:IFIN$<>CHR$(13) THEN19600
19610 RETURN
19700 FORT=0 TO9:GOSUB200:NEXTT
19705 VE=VM-1:HO=0:GOSUB110
19710 PRINT"Druk op return";
19720 FORTE=0 TO1900:NEXTTE
19730 GOSUB110:PRINT"              ";
19740 FORTE=0 TO900:NEXTTE
19750 GOSUB200:IFIN$<>CHR$(13) THEN19705
19760 RETURN
25000 DATA"HET MANNEKE IN DE MAAN","#"
25010 DATA"Je gaat een paar spellingsoefeningen "
25020 DATA"maken. Voor je begint, wil ik weten hoe je "
25030 DATA"heet. Typ hieronder je voornaam en druk daarna "
25040 DATA"op return.","#","%"
25050 DATA"Doe niet zo flauw en zeg me hoe je heet. "
25060 DATA"Je mag nog een keer je naam intikken. Als je "
25070 DATA"het niet doet noem ik je pukkel.","#","%"
25080 DATA"lang","Het spijt me, je naam is te lang voor "
25090 DATA"me. Ik kan dat niet onthouden. Tik nog een "
25100 DATA"keer je naam in. Gebruik niet meer dan 17 "
25110 DATA"letters.","#","%"
25120 DATA"let","Ver-deel de-ze zin-nen "
25130 DATA"in let-ter-gre-pen. "
25140 DATA"Pro-beer zo wei-nig "
25150 DATA"mo-ge-lijk fou-ten te "
25160 DATA"ma-ken. Druk op "
25170 DATA"re-turn, als het "
25180 DATA"merk-te-ken bij een "
25190 DATA"let-ter-greep staat. "
25200 DATA"Ho-pe-lijk vind je dit "
25210 DATA"een ge-mak-ke-lij-ke "
25220 DATA"oe-fe-ning.","&"
25230 DATA"Je hebt ","%"," keer verkeerd gedrukt, of ver"
25240 DATA"geten te drukken. Je mag zelf bepalen ","na$"
25250 DATA", of je dat slecht of goed vindt. ","%"
25260 DATA"licht"
25270 DATA"   LICHTKRANT ","#","In een lichtkrant komen "
25280 DATA"woorden voorbij. Je kunt de snelheid verander"
25290 DATA"en door op de getallen 1 tot en met 9 te druk"
25300 DATA"ken.","#","Sommige letters zijn door een ster"
25310 DATA"retje vervangen.","#","Tik de goede letter in "
25320 DATA"na$",", als een sterretje bij het pijltje staat."
25330 DATA" Ga door tot de krant kompleet is.","%"
25340 DATA"vuurtorenwachter hutkofferdeksel gemeente"
25350 DATA"secretaris reddingsbrigade huiskamerdeur "
25360 DATA"woningbouwvereniging toneelvoorstelling ","%"
25370 DATA"Je hebt vast en zeker gemerkt ","na$",", dat "
25380 DATA"de letters die je hebt moeten invullen, "
25390 DATA"allemaal klinkers waren. ","%"
25400 DATA"1","Je hebt er nogal wat moeite mee gehad "
25410 DATA"na$",". Maar nu mag je verder met de "
25420 DATA"volgende oefening.","%"
25430 DATA"2","Het viel niet mee ","na$",". Nu mag je "
25440 DATA"toch verder gaan.","%"
25450 DATA"3","Dat ging goed ","na$"
25460 DATA". Ga zo door.","%"
25470 DATA"4","Dat ging supersnel ","na$"
25480 DATA". Ga zo door.","%"
25490 DATA"inv","   INVULOEFENING","#"
25500 DATA"Je krijgt een tekst ","na$",". Daarin staan op "
25510 DATA"een aantal plaatsen sterretjes. Op die plaatsen "
25520 DATA"moet je 1 of 2 letters invullen. Dit doe je "
25530 DATA"door de goede letters in te tikken.","#"
25540 DATA"Vergissingen kan je herstellen. Tik dan nie"
25550 DATA"uwe letters in. Druk op return als het volgens "
25560 DATA"jou goed is.","%"
25570 DATA"Een me**eklinker uit T**len","#"
25580 DATA"K**n de klinkers niet bepa**en","#"
25590 DATA"H**rden ze a**een of toch per paar","#"
25600 DATA"Hij wist 't niet z**ker en g**kte maar","#"
25610 DATA"Zijn spe**ing was nooit goed","#"
25620 DATA"Bijna verl**r hij de moed","#"
25630 DATA"Tot dit progra**a verscheen","#"
25640 DATA"Want toen wist hij 't me**een","&"
25650 DATA"d","a","o","l","oo","ll","e","o","ll","oo","mm"
25655 DATA"t","%"
25660 DATA"Je hebt teveel fouten gemaakt ","na$",". Je "
25670 DATA"krijgt een herkansing. ","%"
25680 DATA"Goed spe**en is moeilijker d**n je zou d**nken. "
25690 DATA"Ik h**p ","na$",", dat je d**ze k**r minder "
25700 DATA"fou**en maakt. Druk op de goede kno**en, dan "
25710 DATA"g**t het vast en ze**er b**ter dan d**rnet. ","&"
25720 DATA"ll","a","e","oo","e","ee","t"
25725 DATA"pp","aa","k","e","aa","%"
25730 DATA"Dat ging weer niet goed ","na$",". Je mag nog "
25740 DATA"een keer proberen het beter te doen. ","%"
25750 DATA"Als het nu niet l**kt ","na$",", is het b**ter,"
25760 DATA" dat je bij je l**raar om m**r uitl**g gaat vra"
25770 DATA"**en. Die kan je verte**en, w**nneer je 1 of 2 "
25780 DATA"le**ers moet gebrui**en. Ik w**ns je v**l "
25790 DATA"succes. ","&","u","e","e","ee","e"
25795 DATA"g","ll","a","tt","k","e","ee","%"
25800 DATA"Ik stop er mee ","na$",". Ik raak er helemaal"
25810 DATA" van in de war en kan zelf ook niet meer goed "
25820 DATA"schrijven. Vraag aan je leraar hoe je moet "
25830 DATA"spellen.","%"
25840 DATA"goed","Je mocht in deze oefening niet meer dan "
25850 DATA"1 fout maken ","na$",". Aan die voorwaarde "
25860 DATA"heb je voldaan. Je mag nu verder met de "
25870 DATA"volgende oefening. ","%"
25880 DATA"woord","   De BEZEM ERDOOR","#","Op het scherm"
25890 DATA" komen 10 woorden te staan, genummerd van 0 tot"
25900 DATA" 9. De spelling verandert zo nu en dan. Je kan "
25910 DATA"een woord dat goed is van het scherm vegen "
25920 DATA"door op zijn nummer te drukken. Als je verkeerd"
25930 DATA" drukt, dan blijft het woord in de goede "
25940 DATA"spelling op het scherm staan. Je moet het hele "
25950 DATA"scherm schoonvegen ","na$",". Als dat lukt "
25960 DATA"komt er nog een spelletje.","%"
25970 DATA"perzi-en 1k","notari-en 2s","kubu-en 2s"
25980 DATA"circu-en 2s","leeuweri-en 1k","zani-en 1k"
25990 DATA"pa-etten 2k","mi-chien 2s","stommeri-en 1k"
26000 DATA"gra-atica 2m","ca-ette 2s","co-unicatie 2m"
26010 DATA"commi-ie 2s","sy-etrie 2m","onmidde-ijk 2l"
26020 DATA"para-el 2l","i-uun 2m","verkli-en 2k"
26030 DATA"intere-e 2s","kenni-en 2s","kolo-aal 2s"
26040 DATA"a-uleren 2n","portemo-ee 2n","ta-ereel 1f"
26050 DATA"a-iche 2f","concu-entie 2r","sigare-en 2t"
26060 DATA"gra-ofoon 2m","ba-icade 2r","te-arium 2r"
26070 DATA"ca-ouflage 1m","ca-osserie 2r","carro-erie 2s"
26080 DATA"a-alyse 1n","kroke-en 2t","a-onnee 1b"
26090 DATA"pa-allel 1r","a-iveren 2r","a-oniem 1n","%"
26100 DATA1,"hardstikke goed ","na$",". je bent een kei "
26110 DATA"in spelling. Je kan klinkers gaan vangen. ","%"
26120 DATA2,"Goed zo ","na$",". Wat een moeilijke woorden "
26130 DATA"waren dat. Ga maar klinkers vangen. ","%"
26140 DATA3,"Dat viel niet mee ","na$",". Ik hoop dat je "
26150 DATA"de spelling van deze woorden nooit meer verg"
26160 DATA"eet. Nu komt er nog een spelletje. ","%"
26170 DATA4,"Ik dacht even ","na$",", dat het je nooit "
26180 DATA"zou lukken. Gelukkig kwamen dezelfde woorden "
26190 DATA"steeds weer terug. Begin maar gauw aan het "
26200 DATA"spelletje. ","%"
26210 DATA"vang","    LETTERREGEN","#"
26220 DATA"Bij dit spel ","na$",", moet je "
26230 DATA"klinkers vangen op het pijltje. Je mag 10 "
26240 DATA"klinkers laten schieten. Als je een medeklin"
26250 DATA"ker vangt, verlies je een pijltje. Je hebt 3 "
26260 DATA"pijltjes.","#","Met o beweeg je het pijltje "
26270 DATA"naar rechts. ","#","Met e beweeg je het pijltje"
26280 DATA" naar links. ","%"
26290 DATA"Wil je nog een keer ","na$","? Druk j of n.","%"
26300 DATA"Je mag nog een keer ","na$",". Druk j of n.","%"
26310 DATA"eind","Het spel is afgelopen. "
26320 DATA"Je hebt totaal ","&"," klinkers gevangen. "
26330 DATA"Ik hoop dat je het leuk hebt gevonden. "
26340 DATA"Tot ziens ","na$",".","%"
30000 REM**  FEBRUARI 1984
30010 REM**  MICRONCURSUS
30020 REM**  GEMAAKT OP CBM 64
30030 REM**DOOR OLIVIER STIKKELMAN
30040 REM**  IN SAMENWERKING MET
30050 REM**     MARIJKE KOPS
30060 REM**     MAARTEN ERNST
30070 REM**EN   RENS KROON
30080 REM** ELIA KAZANSTROOK 11
30090 REM** 2726 VB ZOETERMEER
30100 REM
30110 REM COPYRIGHT NOS HILVERSUM

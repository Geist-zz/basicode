
1000 A=200:GOTO20:REM ** REKENEN VOOR DE BASISSCHOOL.
1010 GOSUB100
1020 VE=2:GOSUB5000
1030 HO=7:VE=5:GOSUB110:PRINT"*** REKENEN ***"
1040 HO=9:VE=7:GOSUB110:PRINT"1:Optellen"
1050 VE=9:GOSUB110:PRINT"2:Aftellen"
1060 VE=11:GOSUB110:PRINT"3:Vermenigvuldigen"
1070 VE=13:GOSUB110:PRINT"4:Delen"
1080 VE=15:GOSUB110:PRINT"5:Uitleg"
1090 HO=5:VE=18:GOSUB110:PRINT"Je mag kiezen 1,2,3,4of 5"
1100 GOSUB210:K=VAL(IN$):IF(K>5) OR(K=0) THEN1090
1110 GOSUB100:HO=3:F2=0:G2=0
1120 FORVE=5 TO13 STEP2
1130 GOSUB260:A=INT(RV*9)+1:GOSUB260:B=INT(RV*9)+1
1140 GOSUB110:H=1
2000 ONK GOTO2010,2020,2040,2050,6000
2010 PRINTA;"+";B;"=";:C=A+B:GOTO2060
2020 IFA<B THEND=A:A=B:B=D:PRINTA;"-";B;"=";:C=A-B:GOTO2060
2030 PRINTA;"-";B;"=";:C=A-B:GOTO2060
2040 PRINTA;"X";B;"=";:C=A*B:GOTO2060
2050 PRINTA*B;":";B;"=";:C=A:GOTO2060
2060 INPUTX:IFX=C THEN3000
2070 HO=HO+8:G2=G2+1:FORF=1 TO10:GOSUB110:PRINT"     "
2080 GOSUB110:FORW=1 TO100:NEXTW:PRINT"FOUT"
2090 GOSUB110:FORW=1 TO50:NEXTW:NEXTF
2100 H=H+1:IFH=3 THENPRINT"X   ":HO=HO-8:GOSUB110
2110 IFH=3 THENF2=F2+1:GOTO3000
2120 HO=HO-8:GOSUB110:PRINT"            "
2130 GOSUB110:GOTO2000
3000 NEXTVE:IFHO=20 THEN4000
3010 HO=20:GOSUB110:GOTO1120
4000 IFG2>0 THEN4020
4010 HO=0:VE=16:GOSUB110:PRINT"PRIMA ALLES GOED":GOTO4050
4020 HO=0:VE=16:GOSUB110:PRINT;G2;" X GING HET FOUT."
4030 IFF2=0 THEN4050
4040 PRINT:PRINT"WAARVAN ";F2*2;" MAAL BIJ DEZELFDE SOM."
4050 PRINT:PRINT"Wil je nog meer rekenen?"
4060 PRINT"typ j(a) of n(ee)"
4070 GOSUB210:IF(IN$="j") OR(IN$="J") THEN1000
4080 GOSUB100:HO=12:VE=12:GOSUB110:PRINT"TOT ZIENS."
4090 GOTO7000
5000 HO=3:VE=VE+1:GOSUB110:FORA=1 TO5:PRINT". . . ";:NEXTA
5010 IFVE=20 THENRETURN
5020 GOTO5000
6000 REM UITLEG
6010 HO=8:VE=4:GOSUB110:PRINT"***** UITLEG *****"
6020 PRINT:PRINT"Op het scherm zie je direkt een som."
6030 PRINT:PRINT"Daar typ jij dan het antwoord achter."
6040 PRINT:PRINT"Als het goed is komt de volgende som,"
6050 PRINT:PRINT"als het fout is knippert FOUT."
6060 PRINT:PRINT"Je mag het dan nog eenmaal overdoen."
6070 PRINT:PRINT"Na elk antwoord moet je RETURN geven."
6080 PRINT:PRINT:PRINT"VEEL SUCCES."
6090 HO=20:VE=23:GOSUB110:PRINT"DRUK OP RETURN."
6100 GOSUB210:GOTO1000
7000 END
30000 REM DIT PROGRAMMA IS BEDOELD ALS
30010 REM OEFENSTOF.
30020 REM HET GAAT NIET IN OP
30030 REM DE SOORT FOUTEN,
30040 REM ZE WORDEN WEL OPGETELD.
30050 REM 
30060 REM DE MOEILIJKHEID KAN GEMAK-
30070 REM KELIJK WORDEN GEWIJZIGD DOOR
30080 REM 1130 OF DE REGELS
30090 REM 2010-2050 TE VERANDEREN.
30100 REM
30110 REM DIT PROGRAMMA IS GESCHREVEN
30120 REM OP EEN BBC MODEL B COMPUTER.
30130 REM
30140 REM  ********************
30150 REM  *   PETER ROUWEN.  *
30160 REM  *  ZUTPHENSEWEG 25 *
30170 REM  *    7251 DG       *
30180 REM  *     VORDEN.      *
30190 REM  *   05752-2460     *
30200 REM  ********************
30210 REM
30220 REM COPYRIGHT NOS HILVERSUM
30250 END

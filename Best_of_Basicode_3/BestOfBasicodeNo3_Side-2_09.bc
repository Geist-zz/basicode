
1000 A=1000:GOTO20:REM TYPLESSEN (L. DE JONG) OP NEWBRAIN
1010 DIMX(60),Y(60),A$(20),H(3)
1012 DIMN$(3),KZ$(4)
1015 LT$="":LP=1:H(1)=0:H(2)=0:H(3)=0:V=0:CC=0
1020 SP$="                                        "
1025 LP$="........* = STOP........"
1030 KZ$(1)=" LETTERS DOOR ELKAAR "
1040 KZ$(2)=" VINGERRIJ OEFENING "
1050 KZ$(3)=" ALFABET - ZINNEN "
1055 KZ$(4)=" NIVEAU-LESJES TYPEN "
1056 GOSUB100
1057 PRINT"Geef eerst : LIST 20000-20100"
1058 PRINT"        en : LIST 30610-30850"
1059 VE=20:HO=0:GOSUB2100
1060 GOSUB100:GOSUB3000:REM TOETSENBORD
1065 HO=0:VE=0:GOSUB110:PRINT"WIL JE INSTRUKTIES? (J/N) ";
1067 GOSUB210:GOSUB5000
1068 IF(IN$="J") OR(IN$="j") THENGOSUB6000
1070 GOSUB4000:REM MENUKEUZE
1080 K=VAL(IN$)
1090 IFK=2 THENGOSUB5000:GOSUB4100
1100 GOSUB5000:ONK GOSUB1200,1400,1400,2400
1105 IFK=4 THEN1060
1110 GOSUB5000:GOSUB1800:REM RESULTAAT
1115 IFIN$="*" THEN1070
1120 IFK=1 THEN1100
1130 IFA$(ZN+1)<>"-" THENGOSUB5000:GOSUB1490:GOTO1110
1140 GOTO1070
1150 REM-----EINDE HOOFDPROGRAMMA------
1200 REM ****  LETTERS TYPEN **********
1210 TM=0:CC=0:CO=0
1220 GOSUB260:N=65+INT(RV*26):REM HOOFDLETTERS
1225 NN$=CHR$(ASC("A")+INT(RV*26))
1230 IFN=CO THEN1220:REM GEEN GELIJKE LETTERS NA ELKAAR
1240 CO=N
1250 HO=0:VE=5:GOSUB110:PRINT"TYP DE LETTER:"
1260 HO=19:VE=5:GOSUB110
1270 PRINTNN$:REMPRINT RANDOM LETTER
1280 GOSUB200
1290 IFTM>210 THENRETURN
1300 IF(IN$<"a") OR(IN$>"z") THEN1310
1305 IN$=CHR$(ASC(IN$)-ASC("a")+ASC("A"))
1310 IFIN$<>NN$ THENGOSUB1380:PRINT" ";:FORI=1 TO20:NEXTI
1340 GOSUB1380:GOSUB20010:PRINTCHR$(N);:GOSUB20020
1350 TM=TM+1
1360 IFIN$=NN$ THENCC=CC+1:GOTO1220
1370 GOTO1280
1380 HO=X(N-31):VE=Y(N-31):GOSUB110:RETURN
1400 REM --- ZINNEN TYPEN ---
1420 IFK=3 THENRE$="###":GOSUB15000
1430 PRINT"EVEN ZOEKEN.......":AA$="":D=0:ZN=0
1440 D=D+1:READA$(D)
1450 IFA$(D)<>"-" THENAA$=AA$+A$(D):GOTO1440
1460 IF(V=1) OR(V=2) THENV=2/V:REM OMWISS.1 EN 2
1470 R=1:IFV=1 THENR=2:REM R=AANT.LETTERRIJEN-KEUZE
1480 S=5:IFK=3 THENS=1:REM S=STAPGROOTTE LETTERLEZEN IN AA$
1490 B$="":ZN=ZN+1:IFK=3 THENB$=A$(ZN):GOTO1550
1500 FORX=V TO20*S STEPS
1510 GOSUB260:Y=V+(INT(RV*8))*5
1520 GOSUB260:Y=Y+INT(RV*R)
1530 B$=B$+MID$(AA$,Y,1)
1540 NEXTX
1550 GOSUB5000:PRINTB$
1560 FORI=1 TOLEN(B$):PRINT"-";:NEXTI
1570 PRINT:PRINT:PRINT"         TYP BOVENSTAANDE ZIN OVER "
1580 TM=0
1590 FORX=1 TOLEN(B$)
1600 N=ASC(MID$(B$,X,1))
1610 GOSUB200
1630 IFIN$>CHR$(127) THENIN$=CHR$(ASC(IN$)-32):GOTO1630
1640 IFIN$=CHR$(N) THEN1690
1650 N1=N:IFN=32 THENN1=45:REM KNIPPER SPATIE="-"
1660 IFN1>95 THENN1=N-32:GOTO1660
1670 GOSUB1900:PRINT" "
1680 GOSUB1900:GOSUB20010:PRINTCHR$(N1);:GOSUB20020
1690 TM=TM+1
1700 IFIN$<>CHR$(N) THEN1610
1710 HO=X-1:VE=0:GOSUB110:PRINT" ";:VE=1:GOSUB110:PRINT" ";
1720 NEXTX
1730 RETURN
1800 GOSUB2000:HO=10:VE=1:GOSUB110:PRINTKZ$(K);
1810 SR=CC
1820 IFK<>1 THENSR=LEN(B$)*60/(TM/5)
1830 HO=8:VE=4:GOSUB110:PRINT"JE SCORE"
1840 CT=3:CN=0:GOSUB310:PRINTSR$;" LETTERS PER MIN. ";
1850 IFSR>H(K) THENH(K)=SR:GOSUB2210
1860 HO=22:VE=4:GOSUB110:PRINT"HOOGSTE SCORE ";
1870 SR=H(K):GOSUB310:PRINTSR$
1880 HO=22:VE=5:GOSUB110:PRINTN$(K);
1890 HO=0:VE=9:GOSUB2110:RETURN
1900 HO=X(N1-31):VE=Y(N1-31):GOSUB110:RETURN
2000 GOSUB5000:FORI=1 TO120:PRINT"#";
2010 NEXTI:PRINT
2020 RETURN
2100 REM ---KNIPPERROUTINE VERVOLG------
2105 LP=1
2110 GOSUB110
2120 GOSUB20010:PRINTTAB(3)"<< SPATIEBALK >>";
2125 PRINTTAB(27);"<<";MID$(LP$,LP,8);">>";
2130 FORI=1 TO150:NEXTI:LP=LP+1:IFLP>=17 THENLP=1
2140 GOSUB110:PRINTTAB(3)"<<";LEFT$(SP$,12);">>";
2145 PRINTTAB(27);"<<";MID$(LP$,LP,8);">>";
2150 FORI=1 TO150:NEXTI:GOSUB200
2170 IF(IN$<>" ") AND(IN$<>"*") THENLP=LP+1:GOTO2110
2180 GOSUB110:GOSUB20020:PRINTSP$;:RETURN
2190 REM
2200 REM-----NAAM INVOER----------------
2210 N$(K)="":HO=0:VE=7:GOSUB110
2220 PRINT"TYP JE NAAM (DAARNA NEWLINE/ENTER) ":VE=8
2225 PRINT"- ":GOSUB110
2230 GOSUB200:IFIN$="" THENKY=0:GOTO2225
2235 KY=ASC(IN$):IFIN$="" THEN2225
2240 IFKY=13 THEN2320
2250 IFKY>31 THEN2290
2260 IFLEN(N$(K))>0 THENN$(K)=LEFT$(N$(K),LEN(N$(K))-1)
2270 IFHO>0 THENHO=HO-1:GOSUB110
2280 GOTO2225
2290 IFKY>95 THENKY=KY-32:GOTO2290:REM *HFDLETTERS*
2300 PRINTCHR$(KY);:HO=HO+1
2310 N$(K)=N$(K)+CHR$(KY):IFLEN(N$(K))<18 THEN2230
2320 HO=0:VE=7:GOSUB110:PRINTSP$;SP$;
2330 RETURN
2400 REM-----NIVEAU TYPEN---------------
2402 NI=0:LT$="":VD=0.7:VD$="LAAG"
2405 IFNI<1 THENNI=1
2408 GOSUB100:GOSUB20010:PRINT" ";VD$;" ";:GOSUB20020
2410 PRINT" NIVEAU  1 2 3 4 5 EXAMEN  ";
2412 SR=VD*(100+NI*10):GOSUB300
2415 GOSUB20010:PRINTSR$;"/min":GOSUB20020
2420 VE=1:HO=13+NI*2:GOSUB110
2430 IFNI=6 THENHO=22
2440 PRINT"^":AA$="":IFLT$="" THENGOSUB2900
2445 PRINT"****************************************"
2447 LT$=""
2450 READA$:IFA$="-" THENGOSUB2900:GOTO2450
2460 GOSUB260:IFRV*2<1 THENAA$=AA$+A$+" "
2470 IFLEN(AA$)<150 THEN2450
2480 J=0:FORI=39 TO120 STEP39:A=I:J=J+1
2490 IF(MID$(AA$,A,1)<>" ") AND(A>1) THENA=A-1:GOTO2490
2500 IFA>1 THENLT$=LEFT$(AA$,A-1)
2501 IFA>1 THENLT$=LT$+CHR$(13)+CHR$(10)+CHR$(10)
2505 LT$=LT$+MID$(AA$,A+1,LEN(AA$)-A)
2507 IFJ=3 THENLT$=LEFT$(LT$,A+39)
2510 I=A+1:AA$=LT$:NEXTI
2511 FORI=LEN(AA$) TOLEN(AA$)-20 STEP-1
2512 A$=MID$(AA$,I,1):IF(A$>CHR$(31)) AND(A$<=".") THEN2514
2513 NEXTI
2514 AA$=LEFT$(AA$,I)
2515 IFRIGHT$(AA$,1)=" " THENAA$=LEFT$(AA$,LEN(AA$)-1)
2520 HO=0:VE=4:GOSUB110:PRINTAA$:VE=5:F=0:TM=0:EX=0
2530 FORA=1 TOLEN(AA$):A$=MID$(AA$,A,1)
2540 GOSUB110:PRINT" ";:GOSUB110
2545 TM=TM+1:GOSUB200:IFIN$=A$ THEN2560
2547 IFIN$="" THENPRINT"-";:GOTO2540
2550 IN$="*":F=F+1:GOSUB250
2560 PRINTIN$;:HO=HO+1
2565 IF(ASC(A$)>47) AND(ASC(A$)<91) THENEX=EX+1
2570 IFA$=CHR$(13) THENA=A+2:HO=0:VE=VE+3
2580 NEXTA
2590 PRINT:PRINT
2595 SR=F:GOSUB300
2600 PRINTSR$;" FOUT! - ";
2610 SR=INT((LEN(AA$)-6)*60/(TM/11))
2615 GOSUB300:PRINTSR$;" AANSLAGEN PER MINUUT."
2620 A=NI:IF(F<3) AND(SR>=VD*(100+(NI*10))) THENNI=NI+1
2622 IFNI>=7 THENGOSUB2700:RUN
2625 IFF>10-NI THENNI=NI-1:IFVD=0.7 THENNI=1
2630 SR=A:GOSUB300
2635 IFA<>NI THENPRINT:PRINT"VAN ";VD$;" NIVEAU ";SR$;
2640 IF(NI<1) AND(VD=1) THENVD=0.7:NI=5:VD$="LAAG"
2642 IFA<>NI THENPRINT" NAAR ";:GOSUB2900
2643 IF(NI>5) AND(VD=0.7) THENVD=1:NI=1:VD$="HOOG"
2645 SR=NI:GOSUB300
2650 PRINTVD$;" NIVEAU ";SR$;".":PRINT:PRINT
2655 GOSUB120:GOSUB110:GOSUB2110:IFIN$<>"*" THEN2405
2680 RETURN
2700 GOSUB100:K=0:GOSUB2200
2710 GOSUB100:PRINTTAB(7);"VAN HARTE GEFELICITEERD !!"
2715 GOSUB20010
2720 HO=10:VE=4:GOSUB110:PRINT"T Y P D I P L O M A"
2730 HO=16:VE=6:GOSUB110:PRINT"V O O R"
2735 VE=8:HO=19-(LEN(N$(0))/2):GOSUB110:PRINTN$(0)
2737 GOSUB20020
2740 HO=0:VE=16:GOSUB110:PRINT"Eisen voor diploma waren:"
2750 PRINT:PRINTTAB(5);"- minimaal 160 aansl.per minuut"
2760 PRINTTAB(5);"- blindtypen"
2770 PRINTTAB(5);"- max. 2 fouten"
2780 GOSUB210:RETURN
2900 ONNI GOSUB2910,2920,2930,2940,2950,2960:RETURN
2910 RE$="#1#":GOSUB15000:RETURN
2920 RE$="#2#":GOSUB15000:RETURN
2930 RE$="#3#":GOSUB15000:RETURN
2940 RE$="#4#":GOSUB15000:RETURN
2950 RE$="#5#":GOSUB15000:RETURN
2960 RE$="###":GOSUB260:IFRV*2<1 THENRE$="#6#"
2965 GOSUB15000
2970 RETURN
3000 REM**** TOETSENBORD TEKENEN *******
3010 HO=0:VE=10:GOSUB110:FORI=0 TO39:PRINT"*";:NEXTI
3020 FORI=1 TO13:HO=0:VE=10+I:GOSUB110:PRINT"*";
3030 HO=39:GOSUB110:PRINT"*";:NEXTI
3040 HO=0:VE=23:GOSUB110:FORI=0 TO39:PRINT"*";:NEXTI
3050 HO=7:VE=11:GOSUB110:PRINT"LINKERHAND      RECHTERHAND"
3060 HO=3:VE=12:GOSUB110
3070 PRINT"pink rv mv wysv  wysv  mv rv pink"
3080 RESTORE:R=0:GOSUB20010
3090 FORVE=14 TO20 STEP2:R=R+1
3100 FORHO=R+4 TO34 STEP3
3110 READCH
3120 X(CH-31)=HO:Y(CH-31)=VE
3130 GOSUB110:PRINTCHR$(CH);
3140 NEXTHO
3150 NEXTVE:X(45-31)=22:Y(45-31)=22
3155 HO=3:VE=20:GOSUB110
3156 PRINT"HFD":HO=35:GOSUB110:PRINT"HFD"
3158 HO=3:VE=21:GOSUB110
3159 PRINT"LET":HO=35:GOSUB110:PRINT"LET"
3160 HO=15:VE=22:GOSUB110:PRINT"(SPATIE-BALK)";
3170 GOSUB20020:RETURN
4000 REM *** MENU***********************
4010 GOSUB5000:PRINT"KIES UIT ONDERSTAANDE MOGELIJKHEDEN:"
4020 PRINT:PRINT:FORI=1 TO4:PRINT"  ";I;" = ";KZ$(I):NEXTI
4030 PRINT:PRINT"WELK NR. KIES JE? ";:GOSUB210
4040 IF(IN$<"1") OR(IN$>"4") THEN4010
4050 RETURN
4100 PRINT"WELKE VINGERRIJ?":PRINT
4110 PRINT"  1 = WIJSVINGERS (RIJEN F EN J)"
4120 PRINT"  2 = WIJSVINGERS (RIJEN F, G, H EN J)"
4130 PRINT"  3 = MIDDELVINGERS":PRINT"  4 = RINGVINGERS"
4140 PRINT"  5 = PINKEN":PRINT
4150 PRINT"WAT IS JE KEUZE?";:GOSUB210:GOSUB5000
4160 IF(IN$<"1") OR(IN$>"5") THEN4100
4170 V=VAL(IN$):RE$="#":GOSUB15000
4180 PRINT"DE CIJFERS OOK OEFENEN?":PRINT:PRINT
4190 PRINT"  1 = GEEN CIJFERS":PRINT
4200 PRINT"  2 = WEL CIJFERS":PRINT:PRINT
4210 PRINT"WAT IS JE KEUZE?";:GOSUB210
4220 IFIN$="1" THENRE$="##":GOSUB15000
4230 IF(IN$<>"1") AND(IN$<>"2") THEN4180
4240 RETURN
5000 REM 10 REGELS VAN SCHERM SCHOON
5010 HO=0:FORVE=9 TO0 STEP-1:GOSUB110
5020 PRINTSP$;:NEXTVE:VE=0:GOSUB110:RETURN
6000 PRINT"Plaats de vingers van de LINKERHAND op"
6010 PRINT"de toetsen: A S D F"
6020 PRINT:PRINT"en de vingers van de RECHTERHAND op"
6030 PRINT"de toetsen: J K L ;"
6040 PRINT:PRINT"De G aanslaan met de LINKER WIJSVINGER."
6050 PRINT"De H aanslaan met de RECHTER WIJSVINGER."
6060 VE=9:GOSUB2100
6070 GOSUB5000:PRINT"Plaats de vingers vooral in het begin"
6080 PRINT"steeds weer op deze beginpositie terug"
6090 PRINT"na elke aanslag.":PRINT
7000 PRINT"Hierdoor leert u de basisstand automa-"
7010 PRINT"tisch te vinden. Later kunt u proberen"
7020 PRINT"rechtstreeks naar de volgende letter te"
7025 PRINT"gaan. Daarna weer naar de basisstand!"
7030 VE=9:GOSUB2100:GOSUB5000
7035 PRINT"De LINKER SHIFTtoets met de pink bedie-"
7040 PRINT"nen als een vinger van de RECHTERHAND"
7042 PRINT"een (hoofd)letter moet typen.":PRINT
7044 PRINT"De RECHTER SHIFT-toets met RECHTERPINK"
7046 PRINT"om hfdlet. met de LINKERHAND te maken."
7047 PRINT:PRINT"KYK NAAR 'T SCHERM, NIET NAAR DE TOETSEN"
7048 VE=9:GOSUB2100:GOSUB5000
7050 PRINT"Kies straks eerst keuzenr. 2 als u het"
7060 PRINT"blindtypen nog niet beheerst."
7070 PRINT"Later als test keuzenr. 1 en 3.":PRINT
7080 PRINT"Bij menukeuze 4 komt u op een hoger"
7090 PRINT"niveau als u 't vermelde aantal aansla-"
8000 PRINT"gen per min. hebt gehaald met max 2 ft!"
8010 PRINT"Een hoger niveau eist meer aansl.p.m."
8020 VE=9:GOSUB2100
8030 RETURN
12120 GOSUB20010:PRINTTAB(2);"<< SPATIEBALK >>";
15000 RESTORE
15010 READRY$:IFRY$<>RE$ THEN15010
15020 RETURN
20000 REM --PAS 20010 EN 20020 AAN--
20010 REM  PRINTCHR$(23);CHR$(68);CHR$(14);:REM INVERSE
20015 RETURN
20020 REM PRINTCHR$(15);:REM REVERSE (NORMAAL)
20025 RETURN
24900 REM***************************
24910 REMVERWIJDER REGEL 25205
24920 REMALS UW COMPUTER
24930 REMLANGERE STRINGS TOELAAT.
24950 REM***************************
24999 REM ********DATA REGELS************
25000 REM ** ASCII DATA VOOR TOETSENBORD
25010 DATA49,50,51,52,53,54,55,56,57,48
25020 DATA81,87,69,82,84,89,85,73,79,80
25030 DATA65,83,68,70,71,72,74,75,76,59
25040 DATA90,88,67,86,66,78,77,44,46,47
25098 REM --- VINGERRIJEN ---
25099 DATA"#"
25100 DATA"54321","67890"
25105 DATA"##"
25110 DATA"trewq","yuiop"
25120 DATA"gfdsa","hjkla"
25130 DATA"bvcxz","nm,.z"
25140 DATA"b exq","nuio "
25145 DATA"-"
25148 REM --- ALF. ZINNEN + NIVEAU 6 ---
25149 DATA"###"
25150 DATA"Alfabet, abcdef ghijk lmnop qrstu vwxyz."
25160 DATA"A quick brown fox jumps over a lazy dog."
25170 DATA"Qua type zijn telexmachines voor u goed."
25180 DATA"Andersom zyxwv utsrq ponml kjihg fedcba."
25190 DATA"Computers zijn voor uw bedrijf geknipt."
25200 DATA"Een quantum van vijf apparaten s.v.p."
25205 DATA"-"
25210 DATA"Dit zal in de typkamer worden gebracht."
25220 DATA"Een alfabet construeren is zeer simpel."
25230 DATA"Waarin presenteren de w, x en y zich nu."
25240 DATA"Qua chef zwijgt je exboksvriend lomp."
25250 DATA"Luxe pyamastof verkrijgbaar bij C en A."
25260 DATA"-"
25298 REM --- NIVEAU 1 ---
25299 DATA"#1#"
25300 DATA"gal,","lak,","klas,","aak,","kaal,","half,"
25310 DATA"Klaas gaf gas.","Half kalf.","Kaal glas."
25320 DATA"Aad gaf.","Klaas kaas.","Daf gaf gas."
25330 DATA"Allah gaf aas.","Glad afdak.","Jakhals,"
25340 DATA"haas,","das.","Laag dak.","Alaska.","Shag,"
25350 DATA"sjaal,","jas.","Sla, kas, jaffa.","Haal lak."
25360 DATA"Jaag, las, haak, gaas.","Ga Ada.","Sjaal."
25370 DATA"-"
25398 REM --- NIVEAU 2 ---
25399 DATA"#2#"
25400 DATA"niveau,","twee,","rietje,","huur,","guur,"
25410 DATA"duur,","keur,","geur,","deur,","huif","kuif,"
25420 DATA"duif,","reis,","luis,","Huis,","Fris,","gris,"
25430 DATA"gras,","Klas,","klus,","Dijk,","lijk,","kijk,"
25440 DATA"rijk,","hijs,","lijs,","rijg,","hijg,","kijf,"
25450 DATA"Er is u die keus duidelijk afgeraden."
25460 DATA"Ik kijk geduldig.","Jij reist geregeld."
25465 DATA"De dijk is erg laag.","De held had alles."
25470 DATA"Hij is geheel afgeleid.","De galei is lek."
25480 DATA"De raaf daalde fraai af.","Hij hield lef."
25485 DATA"Liefde is leuk.","Die klas durfde helaas alles."
25490 DATA"Graaf jij dadelijk aarde.","Kaal is fris."
25495 DATA"Die fraaie fles.","Duffe dure klei.","Luilak."
25497 DATA"-"
25498 REM --- NIVEAU 3 ---
25499 DATA"#3#"
25500 DATA"jury, fury, kley, grey.","Stuy, yard, yell, ay."
25505 DATA"Daar is het akelig kil.","Ga hier steeds terug."
25510 DATA"De jury raadt het af.","Grey is grijs."
25515 DATA"Die kater lautert thuis graag.","He did it."
25520 DATA"Het slijk lijkt egaal.","Helaas gaat dat fout."
25525 DATA"Het gilde geeft de adel geld.","He tried it."
25530 DATA"IJdelheid streelt ieder.","Tyfus is heel erg."
25535 DATA"Hij tuurt altijd.","Ga steeds daar uit."
25540 DATA"I try.","They did it.","They had just kissed."
25545 DATA"Hey, he said, shall I try.","Stay here today."
25550 DATA"-"
25598 REM --- NIVEAU 4 ---
25599 DATA"#4#"
25600 DATA"Whole, why, worst.","low, gow, gauw, dauw, touw."
25605 DATA"Aparte apparaten.","Apropo.","a priori,","apart."
25610 DATA"Lauw, Kauw, woef, Woody.","tower, wordt, kort."
25615 DATA"Aquarius,","aquarel,","Aphrodite,","Apollo."
25620 DATA"Waardoor wordt je gauw kwaad.","Ik wil worst."
25625 DATA"Quart is kwart.","Is de paprika fruit."
25630 DATA"De yet gaat omhoog.","Ik weet wel wat dit is."
25635 DATA"Peperdure pepers.","Pas op, Pistolen Paultje."
25640 DATA"Welk woordje leert hij daar.","How are you."
25645 DATA"quarto werd kwarto.","Water is aqua."
25650 DATA"The whole world.","I know that you are right."
25660 DATA"Why would we stay.","Awfully words.","Gij wuift."
25670 DATA"-"
25698 REM --- NIVEAU 5 ---
25699 DATA"#5#"
25700 DATA"Ben jij beneden geweest.","Hij bekent bijna."
25710 DATA"Vanmiddag verveelden wij ons meer."
25720 DATA"Vervelende vaders.","Vormvaste voetballers."
25725 DATA"Cor schreef correct.","Xerxes bracht hem luxe."
25730 DATA"De zon zakt achter de horizon.","Zalige zomer."
25735 DATA"Zeven zachte zomers.","Ze is verd... vervelend."
25740 DATA"De clownen brachten menigeen in extase."
25745 DATA"De excersitie kreeg extra cachet.","Zalige zalf."
25750 DATA"Door het vuur vatte mijn nieuwe vest vlam."
25755 DATA"Concreet is echt niet abstract.","Mini en Maxi."
25760 DATA"Zwijg, zwaan, ziezo.","Hozen, zwijn, wezen."
25770 DATA"Pauze, pezen.","Die bakker bakte beter brood."
25775 DATA"Bont en blauw.","Beste bruine boon.","Bijennest."
25780 DATA"Pikeur was piqueur.","Werp, werk, werd, dwerg."
25790 DATA"-"
25798 REM --- NIVEAU 6 ---
25799 DATA"#6#"
25800 DATA"Van harte gefeliciteerd.","Niveau 6 gehaald."
25810 DATA"10 stuks, 62 kilo.","38 gram, 40 ml.,","5 x."
25820 DATA"Bij de dinghyshow liep ik over parquet."
25830 DATA"46 loten.","76 azen.","95 apen.","84 hl.","7 x."
25840 DATA"Dit examen is tamelijk lastig.","Vind je niet?"
25850 DATA"H.M. De Koningin reikt het diploma uit."
25860 DATA"f 1,25 is f 0,25 meer dan f 1,00","5 x 6 = 30."
25870 DATA"f 8,65 is f 0,65 meer dan f 8,00."
25890 DATA"-"
30000 REM ------------------------------
30010 REM - TYPLESSEN 20-06-84 NEWBRAIN-
30020 REM - AUTEUR: LUUK DE JONG       -
30030 REM -         KORVETPAD 17       -
30040 REM -         8862 ZH  HARLINGEN -
30050 REM -    TEL. 05178-4116         -
30060 REM -                            -
30070 REM - IN  BASICODE-2 FORMAAT 20K -
30080 REM ------------------------------
30085 REM
30090 REM PAS REGELS 20010 EN 20020 AAN
30100 REM VOOR INVERSE EN REVERSE.
30150 REM
30200 REM DE SUBR.2100 EN 2200 KUNNEN
30300 REM SIMPELER MAAR TONEN ZO DE BIJ-
30400 REM ZONDERE MOGELIJKHEDEN MET BC-2
30500 REM OM O.A. INVOER TE BESCHERMEN.
30510 REM
30520 REM IN REGEL 1290 MOET DE WAARDE
30530 REM VAN TM OVEREENKOMEN MET EXACT
30540 REM 1 MINUUT VOOR TONEN LETTERS.
30550 REM 
30560 REM IN RGL 1820 NAGAAN OF (TM/5)
30570 REM KLOPT DOOR BIJV. 1 MIN. TE
30580 REM DOEN OVER EEN REGEL.
30590 REM ZO OOK BIJ REGEL 2610 (TM/11)
30600 REM
30610 REM DIT PROGRAMMA KAN OP
30620 REM SOMMIGE COMPUTERS
30630 REM PROBLEMEN OPLEVEREN.
30640 REM PAS DE SCHERMPRESENTATIE
30650 REM IN DAT GEVAL ZELF AAN
30660 REM UW SYSTEEM AAN.
30670 REM SOMS WERKT MENU KEUZE
30680 REM NUMMER 3 NIET.
30690 REM VERVANG DAN ALLE HOOFD-
30700 REM LETTERS UIT DE DATAREGELS
30710 REM 25150-25250 DOOR KLEINE
30720 REM LETTERS (O.A. NODIG VOOR
30730 REM CBM-64).
30740 REM
30750 REM ALS BIJ KEUZE '4'
30760 REM DE SCHERMPRESENTATIE
30770 REM BIJ HET TYPEN VAN DE
30780 REM VOLGENDE REGELS NIET
30790 REM KLOPT VERANDER DAN IN
30800 REM REGEL 2570 VE+3 IN
30810 REM VE+2
30820 REM BIJ CBM-64 MOETEN IN
30830 REM REGEL 2501 ALLE CHR$(10)
30840 REM IN CHR$(13) WORDEN
30850 REM VERANDERD.
30860 REM
30870 REM ***********************
30880 REM * MET DE HIER GEGEVEN *
30890 REM * AANWIJZINGEN EN UW  *
30900 REM * EIGEN KUNNEN MOET U *
30910 REM * PROBEREN DIT PRO-   *
30920 REM * GRAMMA NETJES OP UW *
30930 REM * SCHERM TE KRIJGEN.  *
30940 REM * DIT IS EEN GOEDE    *
30950 REM * OEFENING !          *
30960 REM ***********************
30970 REM
30980 REM COPYRIGHT
30990 REM NOS HILVERSUM


1000 A=500:GOTO20:REM  globe
1010 P1=3.141592653589:REM  pi
1020 S1=5:REM  stap in graden
1030 DIMSN(360/S1):REM  sin-waarden
1040 DIMCS(360/S1):REM  cos-waarden
1050 PS=.75:REM  namaakpixels basicode
1060 AM=2.1:REM  asmaat
1070 R=PS/AM:REM  projectiegetal
1080 DIMBR(162):REM hoeken breedtes
1090 GOSUB100:REM  scherm schoon
1100 PRINT"Dit programma tekent een globe"
1110 PRINT"met werelddelen."
1120 PRINT"We kunnen de kijkrichting kiezen."
1130 PRINT"Lijnen aan de achterkant worden"
1140 PRINT"weggelaten."
1150 PRINT
1160 PRINT"Wanneer een tijdlang niets nieuws"
1170 PRINT"verschijnt, is het programma aan de"
1180 PRINT"achterkant bezig."
1190 PRINT
1200 PRINT"Even geduld aub...";
1210 FORHK=0 TO360 STEPS1:REM  tabel
1220 SN(HK/S1)=SIN(HK*P1/180):REM  sinus
1230 CS(HK/S1)=COS(HK*P1/180):REM  cosinus
1240 NEXTHK
1250 SD=50:GOSUB450
1260 GOSUB100:REM  scherm schoon
1270 PRINT"Geef de breedte van het opkijkpunt"
1280 PRINT
1290 PRINT"+90 = Noordpool"
1300 PRINT" 52 = Nederland"
1310 PRINT" 41 = New York"
1320 PRINT" 19 = Bombay"
1330 PRINT" -1 = Nairobi"
1340 PRINT"-14 = Sydney"
1350 PRINT"-51 = Falkland eilanden"
1360 PRINT"-90 = Zuidpool"
1370 PRINT
1380 INPUTF
1390 F=90-F:REM hoek uit noordpool
1400 SF=SIN(F*P1/180):REM  sinus
1410 CF=COS(F*P1/180):REM  cosinus
1420 PRINT
1430 PRINT"Geef de lengte van het opkijkpunt"
1440 PRINT
1450 PRINT"  0 = Greenwich"
1460 PRINT"  5 = Nederland"
1470 PRINT" 37 = Nairobi"
1480 PRINT" 73 = Bombay"
1490 PRINT"140 = Tokyo"
1500 PRINT"151 = Sydney"
1510 PRINT"238 = San Francisco"
1520 PRINT"300 = Falkland Eilanden"
1530 PRINT"343 = Dakar"
1540 PRINT
1550 INPUTT
1560 SU=SIN(T*P1/180):REM  sinus
1570 CU=COS(T*P1/180):REM  cosinus
1580 XK=SF*CU:REM  x van kijkplaats
1590 YK=SF*SU:REM  y van kijkplaats
1600 ZK=CF:REM     z van kijkplaats
2000 CN=0:REM  voorgrondkleur
2010 GOSUB600:REM  grafisch scherm
2020 REM
2030 REM  breedtecirkels
2040 REM
2050 SS=10:REM  hoogste cirkel
2060 SB=20:REM  tussenruimte
2070 SE=171:REM  laagste cirkel
2080 FORBR=SS TOSE STEPSB
2090 B=0:REM  naar beginpunt
2100 SW=SIN(BR*P1/180):REM  sin breedte
2110 CW=COS(BR*P1/180):REM  cos breedte
2120 Z=CW:REM  z van punt
2130 FORHK=0 TO360/S1
2140 X=SW*CS(HK):REM  x van punt
2150 Y=SW*SN(HK):REM  y van punt
2160 GOSUB2500:REM  tekenbaar punt
2170 NEXTHK:REM  verder met breedtecirkel
2180 NEXTBR:REM  volgende breedtecirkel
2190 REM
2200 REM  lengtecirkels
2210 REM
2220 SS=0:REM  eerste halve cirkel
2230 SL=20:REM  tussenruimte
2240 SE=350:REM  laatste halve cirkel
2250 FORLT=SS TOSE STEPSL
2260 B=0:REM  naar beginpunt
2270 SW=SIN(LT*P1/180):REM  sin lengte
2280 CW=COS(LT*P1/180):REM  cos lengte
2290 FORHK=0 TO180/S1
2300 X=CW*SN(HK):REM  x van punt
2310 Y=SW*SN(HK):REM  y van punt
2320 Z=CS(HK):REM  z van punt
2330 GOSUB2500:REM  tekenbaar punt
2340 NEXTHK:REM  verder met lengtecirkel
2350 NEXTLT:REM  volgende lengtecirkel
2360 REM
2370 REM  omtrekcirkel
2380 REM
2390 B=0:REM  naar beginpunt
2400 FORHK=0 TO360/S1
2410 XP=R*CS(HK):REM  xp van punt
2420 YP=R*SN(HK):REM  yp van punt
2430 GOSUB2540:REM  tekenbaar punt
2440 B=1:REM  om op aan te sluiten
2450 NEXTHK:REM  verder met omtrekcirkel
2460 GOTO3030
2470 REM
2480 REM  subroutine projectie
2490 REM
2500 IP=X*XK+Y*YK+Z*ZK:REM  inprodukt
2510 IFIP<0 THENB=0:GOTO2590:REM  achter
2520 XP=(-X*SU+Y*CU)*R
2530 YP=(-X*CU*CF-Y*SU*CF+Z*SF)*R
2540 HO=.5+XP
2550 VE=.5-YP*4/3
2560 IFB=0 THEN GOSUB620:REM  punt
2570 IFB=1 THEN GOSUB630:REM  lijntje
2580 B=1:REM  om op aan te sluiten
2590 RETURN
3000 REM
3010 REM vastleggen breedtes
3020 REM
3030 FORI=67 TO162:REM lage breedtes: mercator
3040 BR(I)=-2*ATN(EXP((I-162)/73.5))+P1/2
3050 NEXTI:REM volgende breedte
3060 BR(4)=82*P1/180
3070 FORI=4 TO67:REM hoge breedtes: lineair
3080 BR(I)=BR(4)+(BR(67)-BR(4))*(I-4)/63
3090 NEXTI:REM volgende breedte
3100 REM
3110 REM tekenen werelddelen
3120 REM
3130 B=0:REM laat gat
3140 READLT,BT:REM eerste lengte-, breedtecode
3150 IFLT=999 THEN3370:REM afsluiting
3160 GOSUB3450:REM TEKEN VANAF CODE
3170 READTX$:REM tekstcodering
3180 FORI=1 TOLEN(TX$):REM loop tekst door
3190 T$=MID$(TX$,I,1):REM isoleer teken
3200 IFT$=" " THEN3310:REM niets
3210 IFT$="L" THENLT=LT-1:GOTO3300
3220 IFT$="R" THENLT=LT+1:GOTO3300
3230 IFT$="U" THENBT=BT-1:GOTO3300
3240 IFT$="D" THENBT=BT+1:GOTO3300
3250 IFT$="E" THENLT=LT+1:BT=BT-1:GOTO3300
3260 IFT$="F" THENLT=LT+1:BT=BT+1:GOTO3300
3270 IFT$="G" THENLT=LT-1:BT=BT+1:GOTO3300
3280 IFT$="H" THENLT=LT-1:BT=BT-1:GOTO3300
3290 IFT$="Q" THEN3310:REM klaar
3300 GOSUB3450:REM teken vanaf code
3310 NEXTI:REM volgende teken
3320 IFT$="Q" THEN3130:REM gat naar volgende
3330 GOTO3170:REM volgende tekst
3340 REM
3350 REM klaar met alles
3360 REM
3370 HO=.8:VE=.95:REM rechts onder
3380 SR$="klaar>"
3390 GOSUB650:REM plaats tekst
3400 GOSUB210:REM wacht op toets
3410 GOTO950:REM stop
3420 REM
3430 REM subroutine teken punt van kustlijn
3440 REM
3450 HK=((LT/639)*2+1/3)*P1:REM lengte radialen
3460 B9=BT:IFBT>162 THENB9=324-BT:REM in array
3470 HB=BR(B9)*SGN(162-BT):REM breedte radialen
3480 Z=SIN(HB):REM z-waarde
3490 Z1=COS(HB):REM complement
3500 X=Z1*COS(HK):REM x-waarde
3510 Y=Z1*SIN(HK):REM y-waarde
3520 GOSUB2500:REM teken geprojecteerd
3530 RETURN
25000 REM Azie tot Arabie
25010 DATA84,26
25020 DATA"RFREFRFREFRFRDGGGLHGLLGDGFRERERRRERRREREE"
25030 DATA"RRRRFFRRERRRERRFRRDFFFRREERFRREERRDDFDFRR"
25040 DATA"FRERFRERRFRRERRFFRFRFERFRFERFRERFREURFRER"
25050 DATA"FRERFREERRDFRFFFFRERFRREREURFRFRERRDFFRFR"
25060 DATA"FDFERERDFFRDFDHLGLHLLGLHHHLLLUHGLGDFFGLGD"
25070 DATA"GLHLLGDGLLGDGLGFDLLLLLGGLGGDGDFDGLHLGDGFF"
25080 DATA"LGGGGGLHHUEHUUEUHEEERERREREEUHUHHLHLGLHGL"
25090 DATA"GFDLLLGGLLLLLLGGLHLLGLHLGLGLGDGGGLGGGGLGD"
25100 DATA"FERFRFREFRDFRGDGGGDGGLGDGLLGLLGLGDGGDFRFD"
25110 DATA"FRGDGLGGLLHUEHLREUHLLHLHHREUHLGLLGLHLGDGL"
25120 DATA"GLHLHGGLGDFFERERFREFRDLGLGGFFFG G GG LGGG"
25130 DATA"GGLLHLGDGLLG LGFFFF FERF   FFFFRFFDG GLLG"
25140 DATA"LLLHHHLGLGDFFFDDGLGDHLUHLHUEUUHHHHGLLHHLL"
25150 DATA"HHHHHLHGGLHHHHLGGLLUUUHLLHGGGGGDGLGDGLLGD"
25160 DATA"GDFGDFGDGFLGLGLLUHLUUHHUHEHHHHHHHHUHHUHHL"
25170 DATA"LHHGLLHHHLGGLHHL"
25180 REM Arabie, Afrika, Europa
25190 DATA"LHLHLGLHH"
25200 DATA"LHHLGFFDFFRFERFRFFRRDLGDGGLHLGGLGLGLGGLGD"
25210 DATA"LHLUEHHHHUHLLHHUHLUHHLLUHLUHHLGLLGFDFFFFF"
25220 DATA"DFRDFFRRFRDFFFDFRFDFFFRERFERREERRGDFLGDGG"
25230 DATA"LGDGLLGGGLGDGGLGGGGGGGGLLG  G GF  FFFFFDL"
25240 DATA"GDGLHGDLGDFRDFRDFRGDLLGDGLLGDLGLGLGGGDLLG"
25250 DATA"GGGLLGDGLGLLGLHLGLHGGGLHUHLHUHUUREUHLHUUU"
25260 DATA"UHLHHH EHHHHUH  E HEUREUE HUEREUUHUHEUHHH"
25270 DATA"LHUHHUEUHUEUHLHLGLHGLHHGGLLLHGLHLGLHUHHHL"
25280 DATA"UHUHGLHUUHHHHHUREUEHUEUEREUREEUEUREEUHERE"
25290 DATA"EEERERERFERRERRFREFREFRDFREERFRDFRDFFFRRE"
25300 DATA"RFRRERFEUEREURFRFDFRERFFFRFRERFRDFRFRRERE"
25310 DATA"UREUHUEUUEHUHHLGLGLLHLHLLUUHEUHHUHLHLGGDF"
25320 DATA"RDGLHLUEHUHLHHHLLUHLUHLLGLHGGFFRFFFFFFFRD"
25330 DATA"LLHGGGGGGLGUREUREHUHHHHHLGLUHLLUHLLLLLGDL"
25340 DATA"GDGLHHGGLGDLGLGDGGLGGGLGLGLHGLUUUHLGLHUEH"
25350 DATA"UEHUHUERFRERFRFREREUHUHHHLRRFREUHUFRFEERR"
25360 DATA"EUREREURERERFRFRREREUHLUUEEERRFDGFFFFRERF"
25370 DATA"RREERFRFREURERERREUEUEUEREFRERREHUHLGDUHL"
25380 DATA"EUHHUEREHHHHLLGDDGLGLGDGLGDFDDGDDGDLGGGGL"
25390 DATA"LGHLUEHHHLHHUHGLHLUEUEUEREEEUEFRRFEUEUUER"
25400 DATA"EURFEEEUEUREURFFRREURREURE"
25410 DATA"FFFERRRFEERFRFFERRFRFRERFFDGDGLHLGDFRFRER"
25420 DATA"FREREREUEREURRFREERREUFRFDFEERRRRR"
25430 REM Noordwestkust Azie
25440 DATA" REERERE REERFRRFDLGDF"
25450 DATA"DGLGDGLGFRREEEEE E REEEREERE ERRRFRRREUEE"
25460 DATA"RFRRFRREUERREUEEERRRFFFREEEREERFFRRFRRERR"
25470 DATA"ERREFREEQ"
25480 REM Ierland
25490 DATA515,77
25500 DATA"RFRFRDGDDGLGLHEUUHUEUQ"
25510 REM Engeland
25520 DATA522,68
25530 DATA"RRGFFRDDFRFFRFFFFFDGLGLGHLHLLGLGLLEUHUEEH"
25540 DATA"UEHHLLUEHUEREQ"
25550 REM Kaspische Zee
25560 DATA628,112
25570 DATA"EEHHUEREHHHHUEREHUHLGLLHLGGLLGGFRDDFFRFR"
25580 DATA"DDDGFFRFRRQ"
25590 REM Zwarte Zee
25600 DATA591,101
25610 DATA"RERFFRFERFREFREREUHLLUHLUHLUHLHHLGLHHLGG"
25620 DATA"DGGDLGGFFRFFREQ"
25630 REM Noord Japan
25640 DATA154,96
25650 DATA"RRFDGLHLUEQ"
25660 REM Zuid Japan
25670 DATA153,101
25680 DATA"FDHGDDGDGGGGLGLLGGGGHHREUREEREUREEEEUERFE"
25690 DATA"UQ"
25700 REM Amerika
25710 DATA315,42
25720 DATA"RRFFRDDFRDFFFREEFEERREUFDFDFRDFEUUERFGDFR"
25730 DATA"EURFEUERFDDFFRREUURRERFRREDFEERRFRRRDFRDF"
25740 DATA"RDFRERFRDFRDFRDDGGGDGGDGLGDDFDFRLGDFGFFRD"
25750 DATA"FRRRRRRDDFFREEEUUEEEEUHUEUEREUUUUUUUUUEER"
25760 DATA"RFRRFFRRFDDDDDFEREFRDFDFRFDFRDFRDGGLLGLLG"
25770 DATA"DDGLHLGLGGLHLGLGLGDLGDFERERDFFRFRGLGLGLGL"
25780 DATA"GDGLLGFFDLGDGGDLGDGDDDFRFFDFFDFLGHLHHUHUH"
25790 DATA"HHLLLUHLLLLLLLGDLLLLLLGLHLLLGGDLGGDGGDFDG"
25800 DATA"DFDDFFRDFFRERRREURREURRERFDGDDDDFFRRRRRDD"
25810 DATA"DDFFREEFRFREHFRERFERREUEUFFEFRERFRDFEREFR"
25820 DATA"FERFRRRRFDFRFRFDFRFRFRERFRFRGFDLGLGFRRRRE"
25830 DATA"URDFDGFREUEEFRFEFRFRERRRFRFRFFFFGDFDGDGGD"
25840 DATA"LGLLGGLGGLGLGGGGGLGDGLGLGDGGGLGDGDGDGGDGL"
25850 DATA"HLHGLLGFRFFDLGGGGGGDLGDGLGGGDDGGGFDDGGLGF"
25860 DATA"RFDGLGDGDFFRFDGFDLUHGLHLHLHHGLHHUHHUHUREU"
25870 DATA"HUEUHUREUHUREUEUEUUUUUUEUEUUUUEUHUEHHUEUE"
25880 DATA"UHUHHUHUUEUUUEEHHHUHLUHHLLHHHHHHUEHUHHUEU"
25890 DATA"HUREEEREUHUEUHLHLGLGLGLHHHLLUHLHLLHLGLHLL"
25900 DATA"GLHLHLLHLLLHLUHHHLUHUHUUHLHUHLHUHHHLLHHHU"
25910 DATA"HLLGDDDFRDDFFDFFDDFFFFFHHLHLHUHHHHHHHHHHU"
25920 DATA"HLUHHHLHLUUHLHUHEUUUHLHUEUEHHHHUEUHRRRRRR"
25930 DATA"RRRRRRRRRRRRRRRRRRRRRRRRRRRRRRRRRRRRRRRRR"
25940 DATA"RRRRRRRRRRRRRRRRRRRFRRLLGGFRERFRFRGDGDFER"
25950 DATA"EUEERFDLGFFREUEEREREUHLGLGUHLHLLHLHGHLLLL"
25960 DATA"HLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLL"
25970 DATA"LLLLLLLLLLLLLLLLLLLLLLLLLLUHHUEUHHLHUHGHH"
25980 DATA"LUHGHUHLHLHLHLLGLHHLHLLGLHLHLLLLLLLLLLLLL"
25990 DATA"LLLUHLGDLLGLGDGLGDLLGLGLGLGLLGHLGLLLGLGHL"
26000 DATA"LLERRFEERRERFERERERREUREEEREHLLHLLLHHGLHH"
26010 DATA"UUREUHHLUREREREHLHLHREREERRFRERRRERERERFR"
26020 DATA"ERERFFRRRRRFRFRERFRFRERFRFRERFFRRRRRRREEE"
26030 DATA"URRFRREEURFDFREUEEEQ"
26040 REM Groenland
26050 DATA467,4
26060 DATA"DFFRFREERRFRRRFRFRRRFRRERRFRRDGLLGLGDGGFD"
26070 DATA"GDFFDGLGDLGDLGDGFRDGGFRLGFRRFRFRGLLLGLGLL"
26080 DATA"LGLGGLGFDGDLLGLLGDGLGDGLHGLGDGGLGLGGLGEHL"
26090 DATA"LLHHUHUHLHHHHEREREEEHLHLUHHHUUHLUHUHHLHLL"
26100 DATA"UHHLLHLLLLGLHLLHLLLUEUHHLHHLUERRRRRRRRRRE"
26110 DATA"EHHHLLHEERRREEREFRRFRREUREERRRERRRFFRREEE"
26120 DATA"RRFFRRRRREERRFFRREERREEQ"
26130 REM Cuba
26140 DATA396,134,"FRRRRFRRRFLLLLHLLLLLHLLERRQ"
26160 REM Haiti
26170 DATA412,139,"GUURRRLGQ"
26190 REM Australie
26200 DATA100,190
26210 DATA"RERERREREUEREEUERERFDFEREEUERRERFRFRGLGGF"
26220 DATA"RFRFRFRFFREREEUEUUEUERFDFRFDDFRFDFDFRDFRR"
26230 DATA"DFFDFDFDFDFGDDGGDDLGGDDLGGLGGLLHLLHHHHUUE"
26240 DATA"UHLGGDHHUHLLHLLGLGGLGLLLLGLLHLLGLGLGLHLLL"
26250 DATA"GLGLLLHHHUUHUHUUHUUHUHUEUEEEREQ"
26260 REM Zuidpool
26270 DATA0,278
26280 REM 60-70 OL,
26290 DATA"RRRRFRRRRFRRRRFRR"
26300 REM 70-80 OL,
26310 DATA"RFRFRFRREREREREEER"
26320 REM 80-90 OL,
26330 DATA"RRERERERRRRRRRRRRR"
26340 REM 90-100 OL,
26350 DATA"RRRRRRRRRRRRRRRRRR"
26360 REM 100-110 OL,
26370 DATA"RRRRRRRRRRRRRRRRRR"
26380 REM 110-120 OL,
26390 DATA"REEEEEERRRFFFFFFR"
26400 REM 120-130 OL,
26410 DATA"RREEERRRRRRRRRRRRR"
26420 REM 130-140 OL,
26430 DATA"RRRFRRFRRFRRFRRFRR"
26440 REM 140-150 OL,
26450 DATA"RRRFRRFRRFRRFRRFRR"
26460 REM 150-160 OL,
26470 DATA"RRRFRRFRRFRRFRRFRR"
26480 REM 160-170 OL,
26490 DATA"RRRFRRFRRFRRFRRFRR"
26500 DATA"DGDGDGDGDGDGDGDGDG"
26510 DATA"RRRRRRRRR"
26520 REM 170-180 OL,
26530 DATA"RRRRRRRRRRRRRRRRRR"
26540 REM 180-170 WL,
26550 DATA"RRRRRRRRRRRRRRRRRR"
26560 REM 170-160 WL,
26570 DATA"RRRRRRRRRRRRRRRRRR"
26580 REM 160-150 WL,
26590 DATA"RRERRERRERRERRERRE"
26600 REM 150-140 WL,
26610 DATA"RRERRERRERRERRERRE"
26620 REM 140-130 WL,
26630 DATA"RRERRERRERRERRERRE"
26640 REM 130-120 WL,
26650 DATA"RRRRRRRRRRRRRRRRRR"
26660 REM 120-110 WL,
26670 DATA"RRRRRRRRRRRRRRRRRR"
26680 REM 110-100 WL,
26690 DATA"RRRRRRRRRRRRRRRHHH"
26700 REM 100-90 WL,
26710 DATA"RRRRRRRRRRRRRRRRR"
26720 REM 90-80 WL,
26730 DATA"RRRRRRRRRRRRRRRRRR"
26740 REM 80-70 WL,
26750 DATA"RRRRRRRRRRRRRRRRRR"
26760 REM 70-60 WL,
26770 DATA"EUUUEUUUEUUUEUUEEREEREERRRERRE"
26780 DATA"RRRRRERRRGGGDGGDGGDDDGDDGDD"
26790 DATA"DDGDDGDDGDDDGDDG"
26800 REM 60-50 WL,
26810 DATA"RRRFRRRFRRRFRRRRFRRRR"
26820 REM 50-40 WL,
26830 DATA"RRFRRRFRRFRRRFRRRR"
26840 REM 40-30 WL,
26850 DATA"RERRERRERRERRERRER"
26860 REM 30-20 WL,
26870 DATA"RRERERERRRERRRERRR"
26880 REM 20-10 WL,
26890 DATA"RRRERRRRRERRRRRERR"
26900 REM 10-0 WL,
26910 DATA"RRERRRRERRRRRERRRR"
26920 REM 0-10 OL,
26930 DATA"ERERRRRRRRRRRRRRRR"
26940 REM 10-20 OL,
26950 DATA"RRRRRRRRRRRRRRRRRR"
26960 REM 20-30 OL,
26970 DATA"RFRFFRREERERRRRRRR"
26980 REM 30-40 OL,
26990 DATA"RRERRERRERRERRERRR"
27000 REM 40-50 OL,
27010 DATA"RRERRERRRERRERRERR"
27020 REM 50-60 OL,
27030 DATA"RRUUEEERRRRFFFDDRRRRRRQ"
27040 REM Madagascar
27050 DATA620,178
27060 DATA"FFFDGDGDGDGDGDGDGGGG"
27070 DATA"HUHUHUEEHUUUURREEREREUUUQ"
27080 REM einde
27090 DATA999,999
30000 REM
30010 REM  Literatuur:
30020 REM
30030 REM  over de tekenwijze:
30040 REM  Aarde en Kosmos 7/1985,
30050 REM  bladzijde 494
30060 REM
30070 REM  A&K Informatica, 7/87,
30080 REM  1 oktober - 15 november
30090 REM
32000 REM
32010 REM  Basicode-3:
32020 REM
32030 REM  Pim van Tend
32040 REM  Veldheimwg 8
32050 REM  6871 CD  RENKUM
32060 REM  08373 - 15358
32070 REM
32080 REM  copyright (c) 1987
32090 REM  Stichting Mens en
32100 REM  Wetenschap, Postbus 108,
32110 REM  1270 AC  HUIZEN NH
32120 REM  02152 - 58388
32130 REM
32140 REM TROS-RADIO dd 871118


1000 A=3500: GOTO20
1010 REM SPELLING (werkwoordsvormen, 9 K)
1020 DIMQ(15),T$(6):R$="                   "
1030 DIMS$(110),W$(110),V$(110),P$(110),C$(66)
1040 FORB=1 TO6:READT$(B):NEXTB:B=6: GOSUB1800
1100 PRINT"  Trichter College - Maastricht":PRINT
1110 PRINT"     Maarten van Gils (1987)":PRINT
1120 PRINT"WERKWOORDS-VORMEN : programma voor"
1130 PRINT"groepjes van ongeveer 3 leerlingen"
1140 PRINT"met weinig training op dit gebied."
1200 FORJ=1 TO110:IFJ<41 THENREADW$(J)
1210 B=68:READS$:IFJ>40 THENW$(J)=S$+"EN"
1220 E=ASC(RIGHT$(S$,1)):IF(E=83) OR(E=84) THENB=84
1230 IF(E=72) OR(E=75) OR(E=80) THENB=84
1240 V$(J)=S$+CHR$(B)+"E":S$(J)=S$
1250 P$(J)=S$:IF(E<>B) THENP$(J)=P$(J)+CHR$(B)
1260 IF(J<26) OR(J>60) THENP$(J)="GE"+P$(J)
1270 VE=11:HO=0: GOSUB110:PRINTW$(J);R$:PRINTS$;R$
1280 PRINTV$(J);R$:PRINTP$(J);R$:NEXTJ:F=0
1290 FORJ=1 TO66:READC$(J):NEXTJ: GOSUB2600: GOTO7000
1500  GOSUB260:J=INT(110*RV)+1:Z$=S$(J)
1510 FORE=1 TO13:Q(E)=32:NEXTE
1520 E=0:IFRIGHT$(Z$,1)="T" THENE=1
1530 VE=G+2:HO=0: GOSUB110: GOTO250
1600 HO=0:VE=14: GOSUB110
1610 PRINT"WIL JE NOG MEER VOORBEELDEN ZIEN ?"
1700 PRINT"(J = ja  <-->  N = nee) ";
1710  GOSUB1900:IF(E<>74) AND(E<>78) THEN1710
1720 PRINTIN$:RETURN
1800  GOSUB100:PRINT"*****  ";T$(B);
1810 PRINT"  *****":PRINT:RETURN
1900  GOSUB210:E=ASC(IN$):IFE>90 THENE=E-32
1910 RETURN
2000 VE=14:HO=0: GOSUB110:PRINTR$;R$:PRINTR$;R$;
2010  GOSUB110:PRINT"Noteer hier de juiste vorm van het"
2020 PRINT"werkwoord ";W$(J);" : ? ";:FORB=1 TO13
2100  GOSUB1900:IFE=13 THENB=E: GOTO2200
2110 IF(E<65) OR(E>90) THEN2100
2120 PRINTCHR$(E);:Q(B)=E
2200 NEXTB:VE=G+2:HO=0:FORE=1 TOLEN(Z$)
2210 B=ASC(MID$(Z$,E)):IFB<>Q(E) THENE=40
2220 IFQ(LEN(Z$)+1)<>32 THENE=40
2230 NEXTE: GOSUB110:PRINTR$;R$: GOSUB110
2240 IFE<30 THENPRINT"   **       ";Z$:RETURN
2250 F=F+1:PRINTZ$;" (fout !)": GOTO250
2300  GOSUB2500:B=6: GOSUB1800
2310 PRINT"Aantal goede antwoorden : ";10-F
2320 PRINT:IFF>0 THEN2400
2330 PRINT"UITSTEKEND, ";L$;" !":PRINT
2340 PRINT"Maak maar plaats voor iemand anders."
2350 PRINT"Tot de volgende keer !": GOSUB2600: GOTO2500
2400 IFF<3 THENPRINT"REDELIJK, ";L$;" !": GOTO2500
2410 PRINT"HET GAAT NIET ZO GOED, ";L$;" !":PRINT
2420 PRINT"Bestudeer de theorie nog maar eens."
2500 VE=14:HO=0: GOSUB110:PRINTR$;R$:PRINTR$;
2510 PRINT"Druk op een toets. ";: GOSUB210: GOTO100
2600 HO=0:FORVE=12 TO15: GOSUB110:PRINTR$;R$;:NEXTVE
2610 VE=14: GOSUB110:PRINTR$;"Even geduld ...": GOTO270
3000  GOSUB1800:FORG=0 TO9: GOSUB260:A=INT(15*RV)+41
3010  GOSUB1500: GOSUB260:B=INT(20*RV)+1: GOSUB260
3020 ONINT(6*RV)+1 GOTO3100,3200,3300,3400,3500,3600
3100 Z$=W$(J): GOSUB260:IFRV>.5 THENPRINTC$(A);
3110 PRINT"  ----  ";C$(B+20): GOTO3800
3200  GOSUB260:B=INT(17*RV)+4
3210 PRINTC$(A);"  ----  ";C$(B): GOTO3700
3300  GOSUB260:B=INT(3*RV)+1
3310 PRINT"----  ";C$(B);" ";C$(A);" ?": GOTO3800
3400  GOSUB260:B=INT(17*RV)+4
3410 PRINTC$(B);"  ----  ";C$(A): GOTO3700
3500  GOSUB260:A=INT(17*RV)+4
3510 PRINT"----  ";C$(A);" ?": GOTO3700
3600 PRINTC$(B);:IFB=1 THEN3800
3610 PRINT"  ----  ";C$(A)
3700 IFE=0 THENZ$=S$(J)+"T"
3800  GOSUB2000:NEXTG: GOSUB2300:IFF>2 THEN6000
3810  GOTO7000
4000  GOSUB1800:FORG=0 TO9: GOSUB1500:Z$=V$(J)
4010  GOSUB260:A=INT(15*RV)+47: GOSUB260
4020 B=INT(20*RV)+1: GOSUB260:RV=INT(5*RV)+1
4030 ONRV GOTO4100,4200,4300,4400,4500
4100 PRINTC$(B);"  ----  ": GOTO4600
4200 PRINTC$(B+20);"  ----":Z$=Z$+"N": GOTO4600
4300 PRINT"----  ";C$(B);" ";C$(A);" ?": GOTO4600
4400 PRINTC$(B+20);"  ----  ";C$(A):Z$=Z$+"N": GOTO4600
4500 PRINT"----  ";C$(B+20);" ";C$(A);" ?":Z$=Z$+"N"
4600  GOSUB2000:NEXTG: GOSUB2300:IFF>2 THEN6000
4610  GOTO7000
5000  GOSUB1800:FORG=0 TO9: GOSUB260
5010 A=INT(20*RV)+47: GOSUB260:B=INT(20*RV)+1
5020  GOSUB1500:Z$=P$(J): GOSUB260:RV=INT(7*RV)+1
5030 ONRV GOTO5100,5200,5300,5400,5500,5600,5700
5100 PRINTC$(B);" HAD ";C$(A);: GOTO5800
5200 PRINT"JE  HEBT ";C$(A);: GOTO5800
5300  GOSUB260:B=INT(3*RV)+1
5310 PRINTC$(A);" HEB ";C$(B);: GOTO5800
5400  GOSUB260:B=INT(17*RV)+4
5410 PRINTC$(B);" HEEFT ";C$(A);: GOTO5800
5500  GOSUB260:B=INT(17*RV)+4
5510 PRINTC$(A);" HEEFT ";C$(B);: GOTO5800
5600 PRINTC$(A);" HEBBEN ";C$(B+20);: GOTO5800
5700 PRINTC$(B+20);" HADDEN ";C$(A);
5800 PRINT" ----.": GOSUB2000
5810 NEXTG: GOSUB2300:IFF<3 THEN7000
6000 B=5:G=0: GOSUB1800
6010 PRINT"Weet je, wat een werkwoords-STAM is ?"
6020  GOSUB1700:PRINT:IFE=74 THEN6200
6100 PRINT"De STAM is gelijk aan de IK-VORM."
6110 G=5: GOSUB1500:PRINT"      IK ";Z$
6120 PRINT:PRINT"Stam :   ";Z$
6130  GOSUB1600:B=5: GOSUB1800:IFE=74 THEN6100
6140 PRINT"Geef nu van VIER werkwoorden de STAM."
6150 HO=0:F=0:FORG=7 TO10: GOSUB1500
6160 VE=G-3: GOSUB110:PRINTG-6;"  ";W$(J)
6170  GOSUB2000:NEXTG: GOSUB2500
6180 IFF>0 THENPRINT"Herhaling !":PRINT: GOTO6100
6200 B=1:G=0: GOSUB1800: GOSUB1500:PRINT"       ";W$(J);
6210 PRINT"  (voorbeeld)":PRINT:PRINT"   IK  ";Z$
6220 PRINT"   JIJ ";Z$;:IFE=0 THENPRINT"T";
6230 PRINT:PRINT"       ";Z$;" JE ?"
6240 PRINT"   HIJ ";Z$;:IFE=0 THENPRINT"T";
6250 PRINT:PRINT:PRINT"   WIJ ";W$(J)
6260 PRINT"JULLIE ";W$(J):PRINT"   ZIJ ";W$(J)
6270  GOSUB1600:B=2:IFE=74 THEN6200
6300  GOSUB1800:PRINT"STAM + -TE(N) OF STAM + -DE(N)"
6310 PRINT"Ezelsbruggetje :  SCHOFTEKOP !"
6320 FORG=3 TO5: GOSUB260:A=INT(20*RV+1)
6330  GOSUB1500:PRINTC$(A);" ";V$(J):NEXTG
6340 FORG=7 TO9: GOSUB260:A=INT(20*RV)+21
6350  GOSUB1500:PRINTC$(A);" ";V$(J);"N"
6360 NEXTG: GOSUB1600:B=2:IFE=74 THEN6300
6400 B=3: GOSUB1800:PRINT"De slot-letter van deze vorm"
6410 PRINT"kun je HOREN in de ";T$(2)
6420 PRINT"als je tenminste goed luistert.":PRINT: GOSUB120
6430 PRINTT$(2);:HO=20: GOSUB110:PRINTT$(3):PRINT
6440 FORG=6 TO8: GOSUB1500:PRINTV$(J);: GOSUB120:HO=16
6450  GOSUB110:PRINT"-"RIGHT$(P$(J),1);"-     ";P$(J)
6460 NEXTG: GOSUB1600:F=1:IFE=74 THEN6400
7000 B=6: GOSUB1800:PRINT:IFF>0 THEN7100
7010 G=0:PRINT"Toets je antwoorden nauwkeurig in."
7020 PRINT:PRINT"Verbeteringen zijn onmogelijk !"
7030 PRINT:PRINT"Noteer hieronder je VOORNAAM.":PRINT
7040 L$="":INPUTL$:IFL$="" THENL$="BESTE ONBEKENDE"
7050  GOSUB1800:IFLEN(L$)>15 THENL$=LEFT$(L$,15)
7100 PRINT:FORE=1 TO3:PRINTE;" Oefening ";T$(E):PRINT
7110 NEXTE:PRINT4;" ";T$(5);" (theorie)":PRINT:PRINT
7200 PRINT" Wat kies je, ";L$;" ? ";: GOSUB1900
7210 PRINTIN$:IF(E<49) OR(E>52) THEN7200
7220 F=0:B=E-48:ONB GOTO3000,4000,5000,6000
25000 DATA "TEGENWOORDIGE TIJD","VERLEDEN TIJD"
25010 DATA "VOLTOOID DEELWOORD","OEFENING"
25020 DATA "SPEL-REGELS","SPELEN met SPELLING"
26000 DATA "SPELLEN","SPEL","WAKEN","WAAK","JATTEN","JAT"
26010 DATA "LOTEN","LOOT","WEDDEN","WED","DUTTEN","DUT"
26020 DATA "TENNISSEN","TENNIS","SPELEN","SPEEL","ZWEVEN"
26030 DATA "ZWEEF","STUDEREN","STUDEER","PRATEN","PRAAT"
26040 DATA "PROEVEN","PROEF","SCHAKEN","SCHAAK","ROKEN"
26050 DATA "ROOK","VITTEN","VIT","STUREN","STUUR","RILLEN"
26060 DATA "RIL","DAMMEN","DAM","STAKEN","STAAK","HOLLEN"
26070 DATA "HOL","BOREN","BOOR","VERVEN","VERF","RENNEN"
26080 DATA "REN","ROVEN","ROOF","DRAVEN","DRAAF"
26100 DATA "ONDERDRUKKEN","ONDERDRUK","VERTRAGEN"
26110 DATA "VERTRAAG","GELOVEN","GELOOF","BEWAREN"
26120 DATA "BEWAAR","BETALEN","BETAAL","BEKENNEN","BEKEN"
26130 DATA "VERTONEN","VERTOON","HERHALEN","HERHAAL"
26140 DATA "VOORSPELLEN","VOORSPEL","HERKENNEN","HERKEN"
26150 DATA "BEWAKEN","BEWAAK","VERPAKKEN","VERPAK"
26160 DATA "OVERLEGGEN","OVERLEG","VERMAKEN","VERMAAK"
26170 DATA "ONTDEKKEN","ONTDEK"
26200 DATA "BEDANK","ONTCIJFER","HERVORM","ONTVREEMD"
26210 DATA "VERDIEN","BEDOEL","OVERNACHT","BELEDIG"
26220 DATA "ONTWIKKEL","VERMOED","BERUST","ONTMOET"
26230 DATA "BEGELEID","VERZAMEL","BEPLEIT","OVERTUIG"
26240 DATA "VERWACHT","VEROVER","ONTMOEDIG","VERMELD"
26250 DATA "ZWETS","KIBBEL","FEEST","ADEM","BOET","BOUW"
26260 DATA "HARK","DUW","DULD","BOKS","HOCKEY","ANTWOORD"
26270 DATA "ZUCHT","WENK","ZWAAI","HUIL","SCHERTS"
26280 DATA "LUISTER","SPIEK","KAUW","HOEST","KLETS"
26290 DATA "ZWOEG","RODDEL","HINK","LEID","KAART","ZEUR"
26300 DATA "WERK","GROET","JUICH","MOMPEL","SCHREEUW"
26310 DATA "RASP","TEKEN","SNOEP","RUST","FIETS","SPELD"
26320 DATA "SCHETS","POETS","KNIKKER","OEFEN","AAI"
26330 DATA "KUCH","BIBBER","DANS","ROEI","RUIL","WACHT"
27000 DATA "IK","JE","JIJ","HIJ","MEN","OMA","CLEOPATRA"
27010 DATA "MR. X","LAMBIEK","TOM POES","KUIFJE","BEATRIX"
27020 DATA "JE BROER","DIT MEISJE","JE TANTE","DE RECTOR"
27030 DATA "NAPOLEON","DE AGENT","DE BOZE HEKS","DE PRINS"
27100 DATA "WE","WE","WIJ","JULLIE","JULLIE"
27110 DATA "DE LERAREN","OT EN SIEN","OPA EN OMA"
27120 DATA "DE HOFDAMES","ONZE BUREN","DE PUNKERS"
27130 DATA "DEZE KERELS","HAAR BROERS","DE SOLDATEN"
27140 DATA "MIJN ZUSSEN","DIE MEISJES","DE KINDEREN"
27150 DATA "JIJ EN IK","DE BOEVEN","JE OUDERS"
27200 DATA "VANDAAG","MOMENTEEL","SINDSDIEN","IEDERE DAG"
27210 DATA "ELKE ZONDAG","DEZE WEEK","IN DE LENTE"
27220 DATA "DIE ZOMER","OP 1 APRIL","SOMS","VAAK","TOCH"
27230 DATA "MET PASEN","IN JUNI","INDERDAAD","VORIGE WEEK"
27240 DATA "EERGISTEREN","IN 1983","GISTEREN","VORIG JAAR"
27250 DATA "DESTIJDS","KORT GELEDEN","IN OKTOBER"
27260 DATA "ONLANGS","NOG NOOIT","VROEGER"
30000 REM Maarten van Gils   (TRS-80)
30010 REM Basicode-versie : dec. 1986
30020 REM **   Trichter College    **
30030 REM **     Aylvalaan  9      **
30040 REM **  6212 BA Maastricht   **
30050 REM Gebruikte BASICODE-routines
30060 REM 100 110 120 210 250 260 270
30070 REM 1987  (C)  NOS - Hobbyscoop

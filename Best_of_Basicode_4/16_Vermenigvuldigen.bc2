
1000 A=300:GOTO20:REM"VERMENIGVULDIG"
1010 DIMPP$(8)
1020 FO=0:GOSUB100:PRINT
1030 PRINT" WE GAAN SAMEN VERMENIGVULDIGEN.":PRINT
1040 PRINT" Wil je kijken of zelf rekenen (K/R) ";
1050 GOSUB210:PRINTIN$:PRINT
1060 IF(IN$<>"R") AND(IN$<>"r") THENFO=-1:GOTO1180
1070 PRINT" Jij mag twee getallen geven."
1080 PRINT" Die schrijf ik dan onder elkaar."
1090 PRINT" Dan mag jij de getallen intikken,"
1100 PRINT" maar ik reken mee!":PRINT
1110 PRINT" Als je een fout cijfertje intikt"
1120 PRINT" Dan houd ik dat tegen."
1130 PRINT" Dan kun je gewoon het goede cijfertje"
1140 PRINT" intikken, en dan krijgen we allemaal"
1150 PRINT" goede sommen!"
1160 PRINT" Als je geen enkel foutje maakt"
1170 PRINT" dan zeg ik 'GOED GEDAAN!'":PRINT
1180 PRINT" Geef maar twee getallen.":PRINT
1190 PRINT" EERSTE GETAL ";:GOSUB1270
1200 AA$=AO$:LA=LEN(AA$):PRINT
1210 PRINT:PRINT" TWEEDE GETAL ";:GOSUB1270
1220 BB$=AO$:LB=LEN(BB$):PRINT
1230 PRINT:PRINT" Dank je wel. TIK OP RETURN ";
1240 GOSUB210:IFIN$<>CHR$(13) THEN1240
1250 GOTO1350
1260 :
1270 REM === SUBR.INNEMEN GETAL ===
1280 GOSUB210:IF(ASC(IN$)<49) OR(ASC(IN$)>57) THEN1280
1290 AI=ASC(IN$)-48:PRINTIN$;:AO$=IN$
1300 GOSUB210:IF(IN$=CHR$(13)) OR(LEN(AO$)>5) THEN1330
1310 IF(ASC(IN$)<48) OR(ASC(IN$)>57) THEN1300
1320 AO$=AO$+IN$:PRINTIN$;:AI=AI*10+ASC(IN$)-48:GOTO1300
1330 RETURN
1340 :
1350 REM === HOOFDPROGRAMMA ===
1360 GOSUB100
1370 HO=12:VE=8:GOSUB110
1380 AP$=RIGHT$("              "+AA$,14):PRINTAP$
1390 VE=9:GOSUB110
1400 BP$=RIGHT$("              "+BB$,14):PRINTBP$
1410 LC=LA:IF(LB>LA) THENLC=LB
1420 HO=26:VE=10:GOSUB110:PRINT" x"
1430 FORN=1 TOLC:HO=HO-1:GOSUB110:PRINT"-";:NEXTN:TT=0
1440 :
1450 REM --- de vermenigvuldiglus ---
1460 FORM=LB TO1 STEP-1:VE=VE+1:HO=25:GOSUB110:TT=TT+1
1465 IFTT=1 THENPP$(TT)="":GOTO1480
1470 PP$(TT)=RIGHT$("00000000",TT-1)
1480 B=ASC(MID$(BB$,M,1))-48:BW=0
1490 IFB=0 THENVE=VE-1:GOTO1640
1500 IFTT=1 THEN1530
1510 FORKK=1 TOTT-1:G$="0":GOSUB2020:PRINTG$;
1520 HO=HO-1:GOSUB110:NEXTKK
1530 FORN=LA TO1 STEP-1
1540 A=ASC(MID$(AA$,N,1))-48
1550 C=BW+A*B:SR=C:GOSUB300:SR$=RIGHT$("0"+SR$,2)
1560 D=ASC(MID$(SR$,2,1))-48:E=ASC(MID$(SR$,1,1))-48
1570 SR=D:GOSUB300:G$=SR$:GOSUB2020
1580 PRINTG$:HO=HO-1:GOSUB110
1590 PP$(TT)=SR$+PP$(TT):BW=E
1600 NEXTN
1610 IFE=0 THEN1640
1620 SR=BW:GOSUB300:G$=SR$:GOSUB2020
1630 PRINTG$:PP$(TT)=SR$+PP$(TT)
1640 NEXTM
1650 :
1660 REM --- onderstrepen ---
1670 VE=VE+1:GOSUB110
1680 IFHO<>26 THENHO=HO+1:PRINT"-";:GOTO1680
1690 PRINT" +":HO=HO-1:GOSUB110
1700 FORN=TT TO1 STEP-1
1710 PP$(N)=RIGHT$("00000000000000"+PP$(N),14):NEXTN
1720 :
1730 REM --- de optellus ---
1740 BW=0:UI$="":VE=VE+1:GOSUB110
1750 FORM=14 TO1 STEP-1:G=0
1760 FORN=1 TOTT
1770 H=ASC(MID$(PP$(N),M,1))-48
1780 G=G+H
1790 NEXTN
1800 G=G+BW
1810 SR=G:GOSUB300
1820 G$=RIGHT$(SR$,1)
1830 UI$=UI$+G$
1840 BW=INT(G/10)
1850 NEXTM
1860 :
1870 L=LEN(UI$)
1880 IFMID$(UI$,L,1)="0" THENL=L-1:GOTO1880
1890 UI$=LEFT$(UI$,L)
1900 :
1910 REM --- uitkomst printen ---
1920 HO=26
1930 FORN=1 TOLEN(UI$)
1940 G$=MID$(UI$,N,1)
1950 HO=HO-1:GOSUB110:GOSUB2020:PRINTG$
1960 NEXTN:PRINT" KLAAR !"
1970 HO=3:VE=21:GOSUB110:IFFO=0 THENPRINT" GOED GEDAAN!";
1980 PRINT" TIK OP RETURN ";
1990 GOSUB210:IFIN$<>CHR$(13) THEN1990
2000 GOTO1020
2010 :
2020 REM === SUBR.TIKCONTROLE ===
2030 IFFO=-1 THENFORW=1 TO200:NEXTW:RETURN
2040 REM KIJK-WACHTLUS; AANPASSEN AAN COMPUTER!
2050 GOSUB210:IFIN$<>G$ THENFO=FO+1:GOTO2050
2060 RETURN
2070 :
30000 REM GEMAAKT 4 JANUARI 1987
30010 REM OP EEN  BBC-B COMPUTER
30020 REM
30030 REM door Ch.W.Brederode
30040 REM Von Bonninhausenlaan 33
30050 REM 2161 ES  LISSE
30060 REM
30070 REM COPYRIGHT NOS HILVERSUM

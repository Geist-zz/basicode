1000 A=500:GOTO 20:REM "OVERHOOR"
1010 DIM NR(201),DT(201)
1020 REM --- data-paren tellen ---
1030 GOSUB 100:PRINT "  EVEN GEDULD A.U.B."
1040 N=0
1050 N=N+1:READ A$
1060 IF A$<>"*" THEN READ B$:GOTO 1050
1070 MX=N:GOSUB 1620
1080 REM ---schermhoogte meten ---
1090 GOSUB 100
1100 FOR N=1 TO 50:PRINT :NEXT N
1110 GOSUB 120:VV=VE
1120 REM --- HERSTART ---
1130 RESTORE 
1140 GOSUB 100:PRINT :PRINT " VERTAAL IN HET ENGELS"
1150 LI=2:RE=20:GO=0:FO=0:FT=0:VE=3
1160 :
1170 REM === HOOFDLUS ===
1180 TT=0
1190 TT=TT+1:IF TT=MX THEN GOSUB 1530:GOTO 1120
1200 IF NR(TT)=0 THEN 1190
1210 GH=NR(TT)
1220 REM -- lege regel tussenvoegen --
1230 PRINT :VE=VE+1:GOSUB 1720
1240 RESTORE :FOR N=1 TO GH:READ A$,B$:NEXT N
1250 HO=LI:GOSUB 110:PRINT A$;
1260 PRINT :GOSUB 120:GOSUB 1720:VE=VE-1
1270 HO=RE:GOSUB 110:INPUT C$
1280 IF C$<>B$ THEN 1370
1290 REM --- wis dit woord uit NR()--
1300 NR(TT)=0
1310 REM --- vraagteken wissen ---
1320 GOSUB 110:PRINT " ";
1330 REM --- verbeterd woord merken --
1340 IF FT=1 THEN FT=0:GOSUB 110:PRINT ".";:NR(TT)=GH
1350 GO=GO+1:PRINT :GOSUB 120:GOTO 1190
1360 GOTO 1240
1370 FT=1:FO=FO+1:GOSUB 110
1380 REM --- foute woord wissen ---
1390 PRINT "                ";
1400 GOSUB 110:LB=LEN(B$):PRINT " ";
1410 REM --- woord spellen ---
1420 FOR N=1 TO LEN(B$)
1430 D$=MID$(B$,N,1):PRINT D$;:GOSUB 1480:NEXT N
1440 REM --voorbeeld wissen ,herhalen-
1450 GOSUB 110:PRINT "               ";
1460 GOTO 1270
1470 :
1480 REM === WACHTLUS ===
1490 FOR W=1 TO 500:NEXT W:REM rem
1500 RETURN
1510 REM aanpassen aan uw computer
1520 :
1530 REM === UITSLAG ===
1540 PRINT :PRINT :IF FO>0 THEN 1560
1550 PRINT " GOED GEDAAN! - NOG EEN KEER?";:
1555 GOSUB 210:IF (IN$="N") OR(IN$="n") THEN END
1556 IF (IN$<>"J") AND(IN$<>"j") THEN 1555
1557 GOTO 1590
1560 PRINT " NOG ";FO;" FOUTJE";:IF FO>1 THEN PRINT "S";
1570 PRINT " - Tik op RETURN";
1580 GOSUB 210:IF IN$<>CHR$(13) THEN 1580
1590 IF FO=0 THEN GOSUB 1620:GOTO 1120
1600 RETURN
1610 :
1620 REM === DATA NRS SCHUDDEN ===
1630 FOR N=1 TO MX:DT(N)=N:NEXT N
1640 FOR N=MX-1 TO 2 STEP -1
1650 GOSUB 260:X=1+INT(RV*N)
1660 NR(N)=DT(X)
1670 FOR M=X TO N-1:DT(M)=DT(M+1)
1680 NEXT M:NEXT N
1690 NR(1)=DT(1)
1700 RETURN
1710 :
1720 REM === SCROLL ROUTINE ===
1730 IF VE>VV THEN VE=VE-1:GOTO 1730
1740 RETURN
1750 :
25000 DATA "een","one"
25010 DATA "twee","two"
25020 DATA "drie","three"
25040 DATA "vier","four"
25060 DATA "vijf","five"
25070 DATA "zes","six"
25080 DATA "zeven","seven"
25090 DATA "acht","eight"
25100 DATA "negen","nine"
25110 DATA "tien","ten"
25120 DATA "elf","eleven"
25130 DATA "twaalf","twelve"
25140 DATA "dertien","thirteen"
25150 DATA "veertien","fourteen"
25160 DATA "vijftien","fifteen"
25170 DATA "muziek","music"
25180 DATA "dak","roof"
25190 DATA "tuin","garden"
25200 DATA "deur","door"
25210 DATA "keuken","kitchen"
25220 DATA "venster","window"
25230 DATA "ontbijt","breakfast"
25240 DATA "krant","newspaper"
25250 DATA "bloemen","flowers"
25260 DATA "postbode","postman"
25270 DATA "tafel","table"
25280 DATA "telefoon","telephone"
25290 DATA "paraplu","umbrella"
25300 DATA "brief","letter"
25320 DATA "schoenen","shoes"
25330 DATA "*"
25340 :
30000 REM Aantal DATA-paren max.200
30010 REM Laatste DATA MOET "*" zijn
30020 REM REGEL  1490  AANPASSEN!
30030 REM 
30040 REM Dit programma is ook goed
30050 REM voor andere DATA-paren zo
30060 REM als land + hoofdstad, enz
30070 REM 
30080 REM GEMAAKT 6 JANUARI 1987
30090 REM OP EEN  BBC-B COMPUTER
30100 REM 
30110 REM door Ch.W.Brederode
30120 REM Von Bonninghausenlaan 33
30130 REM 2161 ES  LISSE
30140 REM 
30150 REM COPYRIGHT NOS HILVERSUM

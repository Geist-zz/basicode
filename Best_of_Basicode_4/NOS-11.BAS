1000 A=100:GOTO 20:REM    ** K L I N K E R S **
1010 GOSUB 100
1020 DIM C$(500):Z=0:T=0:AW=0:P1=0:P2=0:A=0:TR=0
1030 DIM LI$(50):REM ** LIMIET IS 50 VERSCHILLENDE **
1040 REM  **  WOORDEN.ALS JE MEER ZOU NEMEN,DAN    **
1050 REM  **  DUURT HET ZOEKEN NAAR EEN WOORD,DAT  **
1060 REM  **  NOG NIET IS GEWEEST STEEDS LANGER.   **
1070 VE=0:HO=0:GOSUB 110
1080 PRINT "EVEN WAT WOORDJES INLEZEN."
1090 FOR F=1 TO 500
1100 READ A$:C$(F)=A$
1110 T=T+1:IF T=20 THEN PRINT "*";:T=0
1120 NEXT F
1130 PRINT "*"
1140 GOSUB 100
1150 VE=0:HO=0:GOSUB 110
1160 PRINT "-:* K L I N K E R S P E L *:-"
1170 PRINT "-----------------------------"
1180 PRINT "NAAM VAN SPELER 1:";:INPUT SA$
1190 PRINT "NAAM VAN SPELER 2:";:INPUT SB$
1200 PRINT SA$;",JIJ LET OP TOETS <Q>"
1210 PRINT SB$;",JIJ LET OP TOETS <P>"
1220 PRINT "ER KOMT STRAKS EEN WOORD OP HET"
1230 PRINT "SCHERM WAAR GEEN KLINKERS ZIJN"
1240 PRINT "INGEVULD.WIE HET WOORD WEET,DRUKT"
1250 PRINT "SNEL OP ZIJN OF HAAR KNOP (Q OF P)"
1260 PRINT "WIE IS HET SNELST ????..........."
1270 PRINT SA$;" OF ";SB$;" ?? EEN VAN JULLIE"
1280 PRINT "MOET DAN HET WOORD INTYPEN."
1290 GOSUB 20000
1300 GOSUB 100:VE=2:HO=0:GOSUB 110
1310 PRINT "-:* K L I N K E R S P E L *:-"
1320 PRINT "-----------------------------"
1330 PRINT "IS HET WOORD GOED INGETYPT,DAN KRIJG"
1340 PRINT "JE DE LENGTE VAN HET WOORD ALS PUNTEN."
1350 PRINT "TYP JE HET FOUT,DAN GAAN ZE VAN JE TO-"
1360 PRINT "TAAL AF.JULLIE BEGINNEN ALLEBEI MET"
1370 PRINT "0 PUNTEN.WETEN JULLIE HET WOORD GEEN "
1380 PRINT "VAN BEIDEN,DAN GAAT BIJ ALLE TWEE DE"
1390 PRINT "LENGTE VAN HET WOORD ER AF...BEGREPEN !!!"
1400 GOSUB 20000
1410 GOSUB 100:VE=6:HO=0:GOSUB 110
1420 PRINT "HOEVEEL WOORDEN WILLEN JULLIE DOEN ?"
1430 PRINT "(LIEVER NIET MEER DAN 50,ANDERS DUURT"
1440 PRINT "HET ZOEKEN STEEDS LANGER.)"
1450 PRINT "TYP HET AANTAL MAAR IN :";:INPUT AW
1460 REM  *** WIL JE TOCH MEER WOORDEN DOEN ***
1470 REM  *** VERANDER DAN REGEL 1030.      ***
1480 T=0
1490 IF AW<1 THEN 1410
1500 GOSUB 20000
1510 T=T+1:IF T=AW+1 THEN 20070
1520 Z=Z+1
1530 GOSUB 260:Q1=INT(499*RV+1)
1540 A$=C$(Q1)
1550 FOR NT=1 TO T
1560 IF LI$(T)=A$ THEN 1510
1570 NEXT NT
1580 LI$(T)=A$
1590 GOSUB 100
1600 VE=0:HO=8:GOSUB 110
1610 PRINT "-:* KLINKERSPEL *:-"
1620 VE=2:HO=3:GOSUB 110:PRINT P1;" PUNTEN VOOR ";SA$;"."
1630 VE=3:HO=3:GOSUB 110:PRINT P2;" PUNTEN VOOR ";SB$;"."
1640 VE=6:HO=19:GOSUB 110
1650 A=(LEN(A$)):TR=0
1660 TR=TR+1
1670 B$=MID$(A$,TR,1)
1680 IF (B$="Y") OR(B$="A") OR(B$="E") THEN B$="-"
1690 IF (B$="O") OR(B$="U") OR(B$="I") THEN B$="-"
1700 IF MID$(A$,TR,2)="IJ" THEN B$="--":TR=TR+1
1710 PRINT B$;
1720 IF (TR<=A) THEN 1660
1725 VE=7:HO=0:GOSUB 110
1727 FOR WA=1 TO 250
1730 GOSUB 200
1740 IF IN$="Q" THEN PRINT SA$:GOTO 1770
1750 IF IN$="P" THEN PRINT SB$:GOTO 1770
1760 NEXT WA
1765 PRINT "JE TIJD IS OM,JAMMER !"
1766 P1=P1-LEN(A$):P2=P2-LEN(A$)
1767 GOSUB 20000
1768 GOTO 1510
1770 VE=9:HO=0:GOSUB 110
1780 PRINT "WAT IS WOORD ";Z;"-";:INPUT D$
1790 S$=IN$
1800 IF D$="" THEN 1770
1810 VE=11:HO=1:GOSUB 110
1820 ZQ$="NEE,NEE,HET WAS  *"
1830 IF D$<>A$ THEN PRINT ZQ$;A$;"*":GOSUB 20000:GOTO 1850
1840 GOTO 1880
1850 IF S$="Q" THEN P1=P1-LEN(A$)
1860 IF S$="P" THEN P2=P2-LEN(A$)
1870 GOTO 1510
1880 IF D$=A$ THEN PRINT "GOED ZO.":GOSUB 20000
1890 IF S$="Q" THEN P1=P1+LEN(A$)
1900 IF S$="P" THEN P2=P2+LEN(A$)
1910 GOTO 1510
20000 REM 
20010 REM  ***  TOETSINDRUK  ***
20020 REM 
20030 VE=14:HO=2:GOSUB 110
20040 PRINT "-:* DRUK MAAR OP EEN TOETS *:-"
20050 GOSUB 210
20060 RETURN
20070 REM 
20080 REM  **  DOORGAAN OF NIET  **
20090 REM 
20100 GOSUB 100:VE=6:HO=0:GOSUB 110
20110 PRINT "ZO,DAT WAREN ";Z;" WOORDEN."
20120 PRINT SA$;" HEEFT ";P1;" PUNTEN."
20130 PRINT SB$;" HEEFT ";P2;" PUNTEN."
20140 PRINT "-----------------------------"
20150 PRINT "GA JE DOOR JA OF NEE ?"
20160 PRINT "TYP MAAR IN J OF N ";:INPUT Z$
20170 IF Z$="J" THEN 1410
20180 IF Z$="N" THEN GOSUB 100
20190 VE=6:HO=0:GOSUB 110
20200 PRINT "TOT ZIENS, ";SA$;" EN ";SB$;"."
20210 PRINT 
20220 GE$=" HEEFT GEWONNEN MET "
20230 IF P2>P1 THEN PRINT SB$;GE$;(P2-P1);" PUNT(EN)."
20240 IF P2<P1 THEN PRINT SA$;GE$;(P1-P2);" PUNT(EN)."
20250 IF P1=P2 THEN PRINT "JULLIE HEBBEN GELIJK GESPEELD."
20260 GOSUB 20000
20270 GOSUB 100:END
25000 REM 
25010 REM  **  500 WOORDEN  **
25020 REM 
25030 DATA "ALLERLEI","ALLEBEI","ONMIDDELLIJK"
25040 DATA "GEMAKKELIJK","SCHAATSSPORT","HUISSLEUTEL"
25050 DATA "PRODUKTIE","OFFICIEEL","OMELET"
25060 DATA "IJLINGS","OPVOEDINTG","VANILLE"
25070 DATA "UNIFORM","GOOCHELEN","VERLOOCHENEN"
25080 DATA "TAFEREEL","THEATER","TENNISSEN"
25090 DATA "TWIJFELEN","WEIFELEN","SIGNALEMENT"
25100 DATA "SPECULAAS","SYMPATHIEK","STEWARDESS"
25110 DATA "SOLLICITEREN","REFREIN","RESERVOIR"
25120 DATA "RIJSTEBRIJ","PRAKTISCH","LOGISCH"
25130 DATA "BELANGRIJK","BESCHIKBAAR","BETEKENIS"
25140 DATA "BIJVOORBEELD","BURGEMEESTER","BEDREIGEN"
25150 DATA "BEHANDELING","BEZWAAR","BESLISSING"
25160 DATA "BEVRIJDING","BELANGSTELLING","BIJDRAGE"
25170 DATA "BISSCHOP","BLIJKBAAR","BESCHOUWING"
25180 DATA "DAGELIJKS","DANKBAAR","DUIDELIJK"
25190 DATA "DEGELIJK","DIREKTEUR","DEFENSIE"
25200 DATA "DEMOCRATIE","DESKUNDIG","DESTIJDS"
25210 DATA "DEMONSTREREN","DOORDRINGEN","DREIGEN"
25220 DATA "PROFESSOR","PROVINCIE","PARLEMENT"
25230 DATA "PERSOONLIJKHEID","POPULAIR","POSITIEF"
25240 DATA "PROTESTANT","PUBLICITEIT","PLEITEN"
25250 DATA "PROTESTEREN","PUBLICEREN","PRESENTEREN"
25260 DATA "GELEGENHEID","HOEVEELHEID","OVEREIND"
25270 DATA "DISCRIMINEREN","TERREIN","AANLEIDING"
25280 DATA "GELEIDELIJK","INLEIDING","KWALITEIT"
25290 DATA "ONDERSCHEID","OPLEIDING","SCHIJNHEILIG"
25300 DATA "WEEKEINDE","WIJSHEID","EIGENSCHAP"
25310 DATA "PERSOONLIJKHEID","PUBLICEREN","UNIVERSITEIT"
25320 DATA "VERANTWOORDEN","BEGELEIDEN","ONDERSCHEIDEN"
25330 DATA "SPOORBOEKJE","TOESCHOUWERS","TOESTEMMING"
25340 DATA "TOEPASSING","ONDERZOEKEN","VERVOEREN"
25350 DATA "UITBREIDING","UITDRUKKING","UITEINDELIJK"
25360 DATA "UITZONDERING","OVERTUIGING","UITGANGSPUNT"
25370 DATA "FLUISTEREN","UITNODIGEN","VERSCHUILEN"
25380 DATA "HOOFDSTUK","HOEVEELHEID","HERHAALDELIJK"
25390 DATA "HERINNERING","HULPVERLENING","HISTORIE"
25400 DATA "HANDHAVEN","HERKENNEN","HERSTELLEN"
25410 DATA "RECHTSTREEKS","REDELIJK","REDENERING"
25420 DATA "RESULTATEN","RESTAURANT","REVOLUTIE"
25430 DATA "REALISEREN","RECHTVAARDIGEN","REDENEREN"
25440 DATA "TEGENWOORDIG","TELEFOON","TELEURSTELLING"
25450 DATA "TENTOONSTELLING","TOESCHOUWERS","TRADITIE"
25460 DATA "HULPVERLENING","ONSCHULDIG","SUBSIDIE"
25470 DATA "BERUSTEN","FUNCTIONEREN","ILLUSTREREN"
25480 DATA "ZICHTBAAR","AANLEIDING","ENIGSZINS"
25490 DATA "EVENWICHT","GEENSZINS","OMSTANDIGHEDEN"
25500 DATA "ONZICHTBAAR","OPLEIDING","OPRICHTING"
25510 DATA "SAMENSTELLEN","SCHRIFTELIJK","WAARSCHUWING"
25520 DATA "INTERESSANT","INTERVIEW","JOURNALIST"
25530 DATA "AALMOEZENIER","AALSCHOLVER","AAMBEELD"
25540 DATA "AANBEVELEN","AANBIEDING","AANDEELHOUDER"
25550 DATA "AANDOENING","AANDRINGEN","AANGESCHOTEN"
25560 DATA "AANGIFTEBILJET","ENCYCLOPEDIE","AANMOEDIGEN"
25570 DATA "AANSPRAKELIJK","ABNORMAAL","ABORTUS"
25580 DATA "AANVANKELIJK","AANWENSEL","AARTSBISSCHOP"
25590 DATA "ACCENTUEREN","ACCEPTABEL","ACCESSOIRES"
25600 DATA "ACCURAAT","ACTUALITEIT","ADJUDANT"
25610 DATA "ADMIRAAL","ADOPTEREN","ADVISEREN"
25620 DATA "AFFAIRE","AFFICHE","AFGEVAARDIGDE"
25630 DATA "AFLEIDING","AFTROGGELEN","AFMETING"
25640 DATA "AGGLOMERATIE","AGRESSIE","AKADEMIE"
25650 DATA "ACCIJNS","ALARMEREN","ALCOHOL"
25660 DATA "ALLERGISCH","ALLESZINS","ALPINIST"
25670 DATA "AMBASSADE","AMBASSADEUR","AMBTENAAR"
25680 DATA "AMFIBIE","AMFITHEATER","AMMONIAK"
25690 DATA "AMPUTATIE","AMULET","ANALFABEET"
25700 DATA "ANEKDOTE","ANGSTVALLIG","ANTICLIMAX"
25710 DATA "APARTHEID","APOTHEKER","APPARAAT"
25720 DATA "ARBEIDSBUREAU","ARGUMENT","ARRESTEREN"
25730 DATA "BEDISSELEN","BEDACHTZAAM","BAROMETER"
25740 DATA "BEGELEIDEN","BEDWELMEN","BEHEERSING"
25750 DATA "BEKENTENIS","BEKLAAGDE","BELEDIGING"
25760 DATA "BEMACHTIGEN","BENIJDEN","BEREIDWILLIG"
25770 DATA "BESCHROOMD","BESMETTELIJK","BESPARING"
25780 DATA "BIOGRAFIE","BIOLOGIE","BLESSEREN"
25790 DATA "BLOEDVERWANT","BLOOTSVOETS","BOEDDHISME"
25800 DATA "BRUIDSSCHAT","BRUGKLAS","BUURTHUIS"
25810 DATA "CAMPING","CARROSSERIE","CASSETTE"
25820 DATA "CATALOGUS","CATEGORIE","CAVALERIE"
25830 DATA "CHAMPAGNE","CHAMPIGNON","CHIMPANSEE"
25840 DATA "CHEMICUS","CILINDER","CIRCULEREN"
25850 DATA "COLLECTANT","COLLECTIE","COLLEGE"
25860 DATA "CONDITIE","COMPAGNON","COMMANDO"
25870 DATA "COMPLIMENT","COMPONIST","CONCLUSIE"
25880 DATA "CONDOLEREN","COMPUTER","CONCOURS"
25890 DATA "CONFEREREN","CONFERENTIE","CONSERVATIEF"
25900 DATA "CONSERVEREN","CONSTATEREN","CONSTRUEREN"
25910 DATA "CONSUMENT","CORRESPONDENTIE","COUPLET"
25920 DATA "DEPARTEMENT","DEMOCRATISCH","DEMONSTRATIE"
25930 DATA "DESERTEREN","DEVALUATIE","DETECTIVE"
25940 DATA "DICTATOR","DILEMMA","DIPLOMA"
25950 DATA "DOCUMENT","DISCUSSIE","DOCENT"
25960 DATA "DOODSANGST","DOORDRIJVEN","DOORSLAGGEVEND"
25970 DATA "DOUANIER","DRESSEREN","DROMEDARIS"
25980 DATA "EIGENDUNK","EIGENGEREID","EMANCIPATIE"
25990 DATA "ENTHOUSIAST","EVENEMENT","EVENAAR"
26000 DATA "EXPERIMENT","EXCLUSIEF","EXEMPLAAR"
26010 DATA "EXPORT","ENCYCLIEK","EMIGRANT"
26020 DATA "FORMALITEIT","FORCEREN","FRAGMENT"
26030 DATA "FUNCTIONEREN","FUNDERING","FYSICA"
26040 DATA "GARNIZOEN","GARANDEREN","GASTARBEIDER"
26050 DATA "GOUVERNEUR","GOUVERNANTE","GRATIFICATIE"
26060 DATA "HARTSTOCHT","HOOGHARTIG","HONORARIUM"
26070 DATA "HERHALING","HERKAUWEN","HINKELEN"
26080 DATA "INDUSTRIE","INENTEN","INBOORLING"
26090 DATA "INFORMATIE","INGENIEUR","INGEWIKKELD"
26100 DATA "INNERLIJK","INQUISITIE","INRICHTING"
26110 DATA "INSTRUMENT","INSPRAAK","INTERIEUR"
26120 DATA "JALOEZIE","JOVIAAL","JUBILARIS"
26130 DATA "KENMERKEND","KIBBOETS","KIESKAUWEN"
26140 DATA "KINDERBIJSLAG","KLAPPERTANDEN","KOUKLEUM"
26150 DATA "KLINKNAGEL","KLOOSTERLING","KLUIZENAAR"
26160 DATA "KOERIER","KOESTEREN","KONVOOI"
26170 DATA "KOOPVAARDIJ","KRITISEREN","KRINGLOOP"
26180 DATA "LUCHTSPIEGELING","LUGUBER","LUITENANT"
26190 DATA "MEMOIRES","MEERSTEMMIG","MENGELING"
26200 DATA "MEUBILAIR","MIJMEREN","MILJONAIR"
26210 DATA "MINDERWAARDIG","MINISTERIE","MINSTREEL"
26220 DATA "MOGENDHEID","MITRAILLEUR","MISLEIDING"
26230 DATA "MOHAMMEDAAN","MORFINE","MONARCHIE"
26240 DATA "BARMHARTIG","ONAFHANKELIJK","ONBESUISD"
26250 DATA "ORGANEN","OVEREENKOMST","OVERSCHRIJDEN"
26260 DATA "PROTESTANT","RESTAURANT","PSYCHIATER"
26270 DATA "REPUTATIE","PROMOTIE","RUIMSCHOOTS"
26280 DATA "SCHUCHTER","TECHNICUS","TRANSPIREREN"
26281 DATA "MOEILIJK","YOGHURT","NAVIGATIE"
26282 DATA "VARIATIE","GOOCHELAAR","TREITEREN"
26283 DATA "SUCCES","SITUATIE","BEGRIJPELIJK"
26284 DATA "ELEKTRISCH","BEWEGING","RISICO"
26285 DATA "BEVOLKING","BIJZONDER","BETREKKELIJK"
26286 DATA "DAARENTEGEN","DEFINITIEF","DIREKTIE"
26287 DATA "SEIZOEN","PERCENTAGE","PRINCIPE"
26288 DATA "PRODUCEREN","PROFITEREN","SCHEIDSRECHTER"
26289 DATA "BESCHEIDEN","MEERDERHEID","UITBREIDING"
26290 DATA "MOGELIJKHEID","WEIGEREN","REIKEN"
26291 DATA "UITVOERING","VERWOESTEN","UITSTEKEND"
26292 DATA "BESLUITEN","VERSCHUIVEN","HOEDANIGHEID"
26293 DATA "HERINNEREN","HERDENKEN","REGELMATIG"
26294 DATA "ZORGVULDIG","RESERVEREN","TEMPERATUUR"
26295 DATA "TROTTOIR","VERONTSCHULDIGEN","VERONTRUSTEN"
26296 DATA "ERGERNIS","ONDERHANDELEN","OVEREENSTEMMING"
26297 DATA "COMMISSIE","LAATSTE","AANBESTEDEN"
26298 DATA "AANHANKELIJK","AANGETEKEND","ACADEMIE"
26299 DATA "AANSLIBBEN","ABATTOIR","ACCOMODATIE"
26300 DATA "ADMINISTRATEUR","ADVOCAAT","AFHANKELIJK"
26301 DATA "AMPUTEREN","AKOESTIEK","ALFABETISCH"
26302 DATA "ALTHANS","AMANDEMENT","AMNESTIE"
26303 DATA "ANATOMIE","ANTIQUAIR","APPLAUS"
26304 DATA "ARRIVEREN","BARRICADE","BEHULPZAAM"
26305 DATA "BELEMMERING","BEROERTE","BEZETTING"
26306 DATA "BLIJMOEDIG","BOETIEK","BUITENSPORIG"
26307 DATA "CAPITULATIE","CELLOFAAN","CHANTAGE"
26308 DATA "COACH","COLLEGA","COMMUNISME"
26309 DATA "CONCENTRATIE","CONCURRENT","CONRECTOR"
26310 DATA "CONSULAAT","CONVERSATIE","DENKBAAR"
26311 DATA "DEURWAARDER","DIRIGENT","DOELTREFFEND"
26312 DATA "DOPING","DUIZELIG","EMBLEEM"
26313 DATA "EVANGELIE","EXERCEREN","ENERGIE"
26314 DATA "FRANKEREN","FUNGEREN","GERAFFINEERD"
26315 DATA "GROSSIER","HOOGLERAAR","HISTORISCH"
26316 DATA "INCASSEREN","INKOMSTEN","INTELLIGENTIE"
26317 DATA "INTERNAAT","JONGLEREN","KIESSCHIJF"
26318 DATA "KLEINZIELIG","KNIPPEREN","KOMPRES"
26319 DATA "KWAADSCHIKS","LUSTELOOS","MERIDIAAN"
26320 DATA "MILIEUBEWUST","MISSIONARIS","MOBILOFOON"
26321 DATA "MOLECUUL","ONBERISPELIJK","OVERTOLLIG"
26322 DATA "PROCESSIE","RUILVERKAVELING","ZONDERLING"
30000 REM 
30010 REM ***********************
30020 REM **                   **
30030 REM **  DIT PROGRAMMA    **
30040 REM **  WERD GESCHREVEN  **
30050 REM **  OP 'N ASTER CT-80**
30060 REM **  DOOR             **
30070 REM **  C.J.ALBERTS SR.  **
30080 REM **        EN         **
30090 REM **  C.J.ALBERTS JR.  **
30100 REM **                   **
30110 REM **  DORPSSTRAAT 86   **
30120 REM **  2441 CJ          **
30130 REM **  NIEUWVEEN        **
30140 REM **                   **
30150 REM ***********************
30160 REM 
30170 REM     COPYRIGHT
30180 REM   NOS HOBBYSCOOP
30190 REM     HILVERSUM

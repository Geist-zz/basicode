1000 A=500:GOTO 20:REM "LETTERBAK"
1010 DIM A$(27),B$(27),C(4)
1020 GOSUB 100:PRINT 
1030 GOSUB 2080
1040 :
1050 REM === instructies ===
1060 PRINT "==================<==============":PRINT 
1070 PRINT " HELP!   DEZE COMPUTER SCHUIFT"
1080 PRINT :PRINT " HET HELE ALFABET DOOR ELKAAR!":PRINT 
1090 PRINT "=================================":PRINT 
1100 PRINT " Maak  jij  het weer in  orde?":PRINT 
1110 PRINT " Je hoeft alleen maar de letters":PRINT 
1120 PRINT " aan te toetsen die je in de lege":PRINT 
1130 PRINT " plek wilt schuiven.":PRINT :PRINT 
1140 PRINT "=================================":PRINT 
1150 :
1160 REM === inname aantal ===
1170 PRINT " HOEVEEL KEER MAG DE COMPUTER"
1180 PRINT :PRINT " SCHUIVEN ";:INPUT I
1190 GOSUB 100
1200 :
1210 REM === letterbak tekenen ===
1220 HO=LI+3:VE=8:GOSUB 110
1230 A$="+-+-+-+-+-+-+-+-+-+"
1240 B$="! ! ! ! ! ! ! ! ! !"
1250 PRINT A$
1260 FOR VE=9 TO 13:GOSUB 110:PRINT B$
1270 VE=VE+1:GOSUB 110:PRINT A$:NEXT VE
1280 :
1290 REM === A$() en B$() vullen ===
1300 FOR N=1 TO 26:A$(N)=CHR$(64+N):B$(N)=A$(N):NEXT N
1310 A$(27)=" ":B$(27)=" "
1320 :
1330 REM === letters in bak zetten ==
1340 FOR NR=1 TO 27:GOSUB 1820:NEXT NR
1350 :
1360 REM === teksten ===
1370 HO=LI+4:VE=2:GOSUB 110:PRINT "L E T T E R B A K"
1380 HO=LI+10:VE=4:GOSUB 110:PRINT "0 x"
1390 HO=LI+4:VE=6:GOSUB 110:PRINT "G E S C H O V E N"
1400 HO=LI:VE=18:GOSUB 110:PRINT "J I J   H E B T   0     x"
1410 HO=LI+4:VE=20:GOSUB 110:PRINT "G E S C H O V E N"
1420 :
1430 REM === A$() in de war maken ===
1440 D=27:E=0
1450 FOR N=1 TO I
1460 GOSUB 1900
1470 GOSUB 260:X=1+INT(RV*(B-1))
1480 IF C(X)=E THEN 1470
1490 A$(D)=A$(C(X)):A$(C(X))=" "
1500 NR=C(X):GOSUB 1820:NR=D:GOSUB 1820
1510 E=D:D=C(X)
1520 HO=LI+10:VE=4:GOSUB 110
1530 PRINT ;N;" x"
1540 NEXT N:T=0
1550 :
1560 REM === DE SPEEL ROUTINE ===
1570 F=0:REM foutvlag als A$()<>B$()
1580 REM === alles al op z'n plaats?==
1590 FOR N=1 TO 27:IF A$(N)<>B$(N) THEN F=1
1600 NEXT N
1610 IF F=0 THEN GOSUB 2020:GOTO 1020
1620 :
1630 REM === zoek leeg hokje ===
1640 D=1
1650 IF A$(D)<>" " THEN D=D+1:GOTO 1650
1660 REM === wie zijn de buren? ===
1670 GOSUB 1900
1680 REM === letter intoetsen ===
1690 GOSUB 210
1700 REM === is dit 'n buurletter? ===
1710 S=0
1720 IF IN$=A$(BA) THEN A$(D)=A$(BA):A$(BA)=" ":S=BA
1730 IF IN$=A$(BB) THEN A$(D)=A$(BB):A$(BB)=" ":S=BB
1740 IF IN$=A$(BC) THEN A$(D)=A$(BC):A$(BC)=" ":S=BC
1750 IF IN$=A$(BD) THEN A$(D)=A$(BD):A$(BD)=" ":S=BD
1760 IF S=0 THEN 1790
1770 NR=S:GOSUB 1820:NR=D:GOSUB 1820
1780 T=T+1:HO=LI+18:VE=18:GOSUB 110:PRINT ;T
1790 GOTO 1560
1810 :
1820 REM === SUBR.A$(NR)PRINTEN ===
1830 HO=LI+2+2*NR
1840 IF HO>(LI+20) THEN HO=HO-18:GOTO 1840
1850 VE=9+2*INT((NR-1)/9)
1860 GOSUB 110:PRINT A$(NR)
1870 GOSUB 2140:REM --- vertraging --
1880 RETURN
1890 :
1900 REM === SUBR.BUURHOKJES ZOEKEN ==
1910 B=1
1920 BA=D+9:IF BA>27 THEN BA=0:GOTO 1940
1930 C(B)=BA:B=B+1
1940 BB=D-9:IF BB<1 THEN BB=0:GOTO 1960
1950 C(B)=BB:B=B+1
1960 BC=D+1:IF (BC=10) OR(BC=19) OR(BC=28) THEN BC=0:GOTO 1980
1970 C(B)=BC:B=B+1
1980 BD=D-1:IF (BD=0) OR(BD=9) OR(BD=18) THEN BD=0:GOTO 2000
1990 C(B)=BD:B=B+1
2000 RETURN
2010 :
2020 REM === SUBR. 'KLAAR!' ===
2030 HO=LI+3:VE=16:GOSUB 110:PRINT "--> K L A A R ! <--"
2040 HO=LI+6:VE=22:GOSUB 110:PRINT "NOG 'N KEER?";
2050 GOSUB 210:IF (IN$="N") OR(IN$="n") THEN END
2055 IF (IN$<>"J") AND(IN$<>"j") THEN 2050
2060 RETURN
2070 :
2080 REM === CENTREREN EN CURSOR ===
2090 LI=6:REM aantal lege pos.links
2100 REM hier commando 'cursor off'
2110 RETURN
2120 REM aanpassen aan uw computer
2130 :
2140 REM === SUBR.VERTRAGINGSLUS ===
2150 FOR W=1 TO 100:NEXT W
2160 RETURN
2170 REM aanpassen aan uw computer
2180 :
30000 REM GEMAAKT 5 JANUARI 1987
30010 REM OP EEN  BBC B COMPUTER
30020 REM 
30030 REM door Ch.W.Brederode
30040 REM Von Bonninghausenlaan 33
30050 REM 2161 ES  LISSE
30060 REM COPYRIGHT NOS HILVERSUM

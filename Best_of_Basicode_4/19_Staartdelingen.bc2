
1000 A=1000:GOTO20:REM STAARTDELINGEN
1010 DIMD$(4),T$(8),U$(8),A$(4),B$(4),S$(4)
1020 W=0:Z=0
1030 GOSUB10200:REM BEGINSCHERM
1040 GOSUB10140:REM WACHTLUS
1050 HO=6:VE=15:GOSUB110
1060 PRINT"Wil je uitleg ? (J/N) ";
1070 GOSUB210
1080 IF(IN$="J") OR(IN$="j") THENGOSUB10300:GOTO1100
1090 IF(IN$<>"N") AND(IN$<>"n") THEN1070
1100 REM INITIALISATIE
1110 D=0:D1=0:LD=0:WD=0:DC$=""
1120 REM deler
1130 T=0:T1=0:TH=0:LT=0:WT=0:TC$=""
1140 REM deeltal
1150 UI=0:UH=0:UH$="":UC$=""
1160 U2$="":U3=0:U5=0:U4$="":U5$=""
1170 REM uitkomsten
1180 GH=0:AG=0:AF=0:REM getalvar.
1190 RE=1:P=1:Q=0:R=0:F=0
1200 GU=0:PU=0:PL=0
1210 REM plaatsvar.
1220 PR=0:SP=0:REM printer/spatie
1230 Q1=0:Q2=0:Q3=0:REM plaats
1240 L1=0:L2=0:L3=0:REM lengtevar.
1250 M=0:REM verm.factor
1260 K1=0:K2=0:REM keuze
1270 F1=0:F2=0:F3=0:FO=0:REM fout
1290 REM
1300 GOSUB10200
1310 PRINT:PRINT
1320 PRINT"    Wil je zelf getallen invoeren,"
1330 PRINT:PRINT"                druk dan <1>"
1340 PRINT:PRINT
1350 PRINT"    Wil je het de computer laten doen,"
1360 PRINT:PRINT"                druk dan <2>"
1370 HO=6:VE=20:GOSUB110
1380 PRINT"KIES 1 OF 2 ";:GOSUB210
1390 IF(IN$<"1") OR(IN$>"2") THEN1370
1400 K1=VAL(IN$)
1490 REM
1500 GOSUB10200
1510 PRINT:PRINT:PRINT
1520 PRINT"     Druk <1> om zelf te rekenen."
1530 PRINT:PRINT:PRINT"     Druk <2> om de computer te"
1540 PRINT:PRINT"              te laten rekenen."
1550 HO=6:VE=20:GOSUB110
1560 PRINT"Kies 1 of 2 ";:GOSUB210
1570 IF(IN$<"1") OR(IN$>"2") THEN1550
1580 K2=VAL(IN$)
1600 IF(K2=2) THENZ=Z+1
1610 GOSUB14100:REM PRINTER
1670 REM
1680 REM GETALLEN KIEZEN
1690 ONK1 GOTO1700,5400
1700 GOSUB10200
1710 PRINT:PRINT"           GETALLEN KIEZEN"
1720 PRINT:PRINT"       deler/ deeltal \uitkomst"
1730 HO=6:VE=14:GOSUB110
1740 PRINT"Geef de deler..(+RETURN)"
1750 PRINT"        (max. 4 cijfers) ";
1760 HO=25:VE=15:GOSUB9000
1770 D$=G$:D=VAL(D$)
1780 HO=6:VE=17:GOSUB110
1790 PRINT"Geef 't deeltal..(+RETURN)"
1800 PRINT"        (max. 6 cijfers) ";
1810 HO=25:VE=18:GOSUB9000
1820 T$=G$:T=VAL(T$)
1830 HO=6:VE=12:GOSUB110
1840 PRINTD$;" / ";T$;" \ ??"
1850 GOSUB10060
1880 GOTO8000:REM GETALLEN CONTROLEREN
1890 REM
1900 FORJ=6 TO0 STEP-1
1910 GOSUB100
1920 HO=7-Q1:VE=12-Q2:GOSUB110
1930 PRINTD$;"/";T$;"\"
1940 GOSUB10140
1950 Q1=Q1+1:Q2=Q2+2
1960 NEXTJ
1990 REM *** BEGIN DEELROUTINE
2000 IFPR=1 THENGOSUB14200
2010 PU=PU+LD+LT+2
2050 IFVAL(MID$(T$,1,LD))/WD<1 THENR=1
2060 IFLEFT$(U$,1)="0" THENR=0
2070 IFPR=1 THENGOSUB14230
2080 B$=MID$(T$,1,LD+R)
2090 IF(Z>4) THENK2=1:GOSUB9400
2100 REM ** CIJFER UITKOMST
2110 ONK2 GOSUB5010,5310
2120 HO=PU+P:VE=0:GOSUB110
2130 PRINTMID$(U$,P,1):GOSUB250
2140 GOSUB10120
2150 GU=VAL(MID$(U$,P,1))
2160 AG=GU*WD
2170 SR=AG:GOSUB300:A$=SR$
2180 Q=0
2190 REM ** VERMENIGVULDIGING
2200 IFPR=1 THENGOSUB14240
2210 FORI=LEN(A$) TO1 STEP-1
2220 ONK2 GOSUB5110,5310
2230 HO=2*LD+P-Q+R:VE=RE:GOSUB110
2235 PRINTMID$(A$,I,1)
2240 Q=Q+1
2250 GOSUB250
2260 NEXTI
2270 Q=0
2280 REM ** LIJN
2290 IFPR=1 THENGOSUB14270
2320 FORI=LEN(B$) TO1 STEP-1
2330 HO=2*LD+P-Q+R:VE=RE+1:GOSUB110:PRINT"-"
2340 Q=Q+1
2350 NEXTI
2370 RE=RE+2:Q=0
2380 AF=VAL(B$)-AG
2390 SR=AF:GOSUB300:S$=SR$
2400 REM ** AFTREKKING
2410 GOSUB7110:IFPR=1 THENGOSUB14320
2420 FORI=LEN(S$) TO1 STEP-1
2430 ONK2 GOSUB5210,5310
2440 HO=2*LD+P-Q+R:VE=RE:GOSUB110:PRINTMID$(S$,I,1)
2450 Q=Q+1
2460 GOSUB250
2470 NEXTI
2480 IFP=LEN(U$) THEN2670
2490 GOSUB10130
2500 REM ** AANHALEN
2510 IFMID$(T$,LD+P+R,1)="." THENGOSUB4000
2520 IFPR=1 THENGOSUB14370
2530 HO=2*LD+P+R+1:VE=0:GOSUB110:PRINT" ":GOSUB10140
2540 HO=2*LD+P+R+1:VE=0:GOSUB110:PRINTMID$(T$,LD+P+R,1)
2550 FORF=1 TORE-1
2560 HO=2*LD+P+R+1:VE=F:GOSUB110:PRINTMID$(T$,LD+P+R,1)
2570 GOSUB10120
2580 HO=2*LD+P+R+1:VE=F:GOSUB110:PRINT" "
2590 GOSUB10120
2600 NEXTF
2610 HO=2*LD+P+R+1:VE=F:GOSUB110:PRINTMID$(T$,LD+P+R,1)
2620 B$=S$+MID$(T$,LD+P+R,1)
2630 RE=RE+1
2640 P=P+1
2650 REM ** EINDE DEELROUTINE
2660 GOTO2100
2670 IF(LD+P-4)<0 THENP=10
2680 HO=LD+P-4:VE=RE:GOSUB110:PRINT"REST"
2690 IFK2=2 THEN2900
2700 GOSUB7100:GOSUB110
2710 PRINT"! R E S U L T A A T"
2720 GOSUB7030
2730 HO=21:VE=4:GOSUB110:PRINT"SCHATTEN :";F1;"x F."
2740 VE=6:GOSUB110:PRINT"KEER     :";F2;"x F."
2750 VE=8:GOSUB110:PRINT"AFTREKKEN:";F3;"x F."
2760 FO=F1+F2+F3
2770 VE=9:GOSUB110:PRINT"         -----"
2780 VE=11:GOSUB110:PRINT"  TOTAAL :";FO;"FOUT"
2790 IF(FO=0) THENFU$=" HEEL GOED"
2800 IF(FO=1) THENFU$="  GOED"
2810 IF(FO=2) THENFU$=" REDELIJK"
2820 IF(FO=3) OR(FO=4) THENFU$=" VOLDOENDE"
2830 IF(FO>=5) THENFU$=" ONVOLDOENDE !"
2840 VE=14:GOSUB110:PRINTFU$
2900 IFPR=1 THENGOSUB14390
2910 HO=8:VE=23:GOSUB110
2920 PRINT"NOG EEN DELING ? (J/N)";
2930 GOSUB210
2940 IF(IN$="J") OR(IN$="j") THEN1100
2950 IF(IN$="N") OR(IN$="n") THEN2970
2960 GOTO2930
2970 GOSUB100:END
3990 REM * KOMMA IN DEELTAL
4000 P=P+1
4010 HO=2*LD+P+R-Q:VE=RE:GOSUB110
4020 FORI=1 TOLEN(S$):PRINT" ":NEXTI
4030 HO=2*LD+P+R-Q+1:VE=RE:GOSUB110
4040 PRINTS$
4050 HO=PU+P:VE=0:GOSUB110
4060 PRINTMID$(U$,P,1):GOSUB250
4070 GOSUB10120:RETURN
5000 REM ** ZELF REKENEN
5010 HO=22:VE=0:GOSUB110:PRINT"( K = KLADBLOK)   "
5020 HO=PU+P:VE=0:GOSUB110
5030 GOSUB210:IFIN$=MID$(U$,P,1) THENRETURN
5040 IF(IN$="K") OR(IN$="k") THENGOSUB6000:GOTO5020
5050 IF(IN$<"0") OR(IN$>"9") THEN5020
5060 GOSUB9200
5070 GOTO5020
5100 REM
5110 HO=22:VE=0:GOSUB110:PRINT"( K = KLADBLOK)   "
5120 HO=2*LD+P-Q+R:VE=RE:GOSUB110
5130 GOSUB210:IFIN$=MID$(A$,I,1) THENRETURN
5140 IF(IN$="K") OR(IN$="k") THENGOSUB6300:GOTO5120
5150 IF(IN$<"0") OR(IN$>"9") THEN5120
5160 GOSUB9250
5170 GOTO5120
5200 REM
5210 REM
5220 HO=2*LD+P-Q+R:VE=RE:GOSUB110
5230 GOSUB210:IFIN$=MID$(S$,I,1) THENRETURN
5240 IF(IN$<"0") OR(IN$>"9") THEN5220
5250 GOSUB9300
5260 GOTO5220
5290 REM
5300 REM * COMPUTER REKENT
5310 GOSUB10140:RETURN
5390 REM
5400 REM COMPUTER KIEST
5410 GOSUB260:D=INT(RV*100)
5420 SR=D:GOSUB300:D$=SR$
5430 GOSUB260:T=INT(RV*1000000)
5440 SR=T:GOSUB300:T$=SR$
5450 GOTO8070
5990 REM
6000 GOSUB7000:GOSUB7080:REM KLADBLOK
6010 GU=VAL(MID$(U$,P,1)):Q=0
6020 HO=21:VE=2:GOSUB110
6030 PRINTD$;"/";B$;"\";
6040 PL=LD+LEN(B$)+2
6050 GOSUB210
6060 IF(IN$="T") OR(IN$="t") THENGOSUB7150:RETURN
6070 IF(IN$<"0") OR(IN$>"9") THEN6050
6080 U2$=IN$:PRINTU2$:GOSUB250
6090 HO=21+PL+3:GOSUB110:PRINTD$
6100 HO=HO+LD-1:VE=3:GOSUB110:PRINTU2$;"x"
6110 HO=21+PL+3:VE=4:GOSUB110
6120 FORJ=1 TOLD:PRINT"-";:NEXTJ
6130 U3=VAL(U2$)*WD
6140 SR=U3:GOSUB300:U4$=SR$
6150 FORJ=LEN(U4$) TO1 STEP-1
6160 GOSUB7150:GOSUB7200
6180 PRINTMID$(U4$,J,1):GOSUB250
6190 Q=Q+1
6200 NEXTJ
6210 Q=0
6220 HO=22:VE=7:GOSUB110
6230 IF(VAL(U2$)=GU) THENPRINTU2$;"x is GOED.":RETURN
6240 IF(VAL(U2$)<GU) THENPRINTU2$;"x is te LAAG.":RETURN
6250 IF(VAL(U2$)>GU) THENPRINTU2$;"x is te HOOG.":RETURN
6290 REM VERMENIGV.
6300 GOSUB7000
6310 HO=23:VE=3:GOSUB110:PRINTD$
6320 HO=23+LD-1:VE=4:GOSUB110
6330 PRINTMID$(U$,P,1)+" x"
6340 HO=23:VE=5:GOSUB110
6350 FORJ=1 TOLD:PRINT"-";:NEXTJ
6360 U5=VAL(A$):GOSUB300:U5$=SR$
6371 FORJ=LEN(U5$) TO1 STEP-1
6380 HO=23+LD-1-Q3:VE=6:GOSUB110
6390 GOSUB7300
6400 PRINTMID$(U5$,J,1):GOSUB250
6410 Q3=Q3+1
6420 NEXTJ
6430 Q3=0
6450 HO=24:VE=8:GOSUB110
6460 PRINT"GOED":RETURN
6500 GOSUB7000
6510 HO=26-LEN(B$):VE=3:GOSUB110
6520 PRINTB$
6530 HO=26-LEN(A$):VE=4:GOSUB110
6540 PRINTA$+"-"
6550 HO=26-LEN(B$):VE=5:GOSUB110
6560 FORJ=1 TOLEN(B$)
6570 PRINT"-";:NEXTJ
6580 FORJ=LEN(S$) TO1 STEP-1
6590 HO=26-1-Q3:VE=6:GOSUB110
6600 GOSUB7400
6610 PRINTMID$(S$,J,1):GOSUB250
6620 Q3=Q3+1
6630 NEXTJ:Q3=0
6640 HO=24:VE=8:GOSUB110
6650 PRINT"GOED":RETURN
6990 REM * KLADBLOK
7000 GOSUB7100
7010 GOSUB110
7020 PRINT"!  K L A D B L O K"
7030 FORJ=1 TO19:VE=J:GOSUB110
7040 PRINT"!                   "
7050 NEXTJ
7060 GOSUB7100:VE=VE+20:GOSUB110
7070 PRINT"--------------------"
7075 RETURN
7080 HO=24:VE=21:GOSUB110
7090 PRINT"( T = terug )":RETURN
7100 HO=20:VE=0:RETURN
7110 GOSUB7100
7120 FORJ=0 TO21:VE=J:GOSUB110
7130 PRINT"                    "
7140 NEXTJ:RETURN
7150 HO=24:VE=21:GOSUB110
7160 PRINT"             ":RETURN
7200 HO=21+PL+3+LD-1-Q:VE=5:GOSUB110
7210 GOSUB210
7230 IF(IN$<"0") OR(IN$>"9") THEN7210
7240 IF(IN$=MID$(U4$,J,1)) THENRETURN
7260 HO=26:VE=7:GOSUB110:PRINTIN$" is FOUT"
7270 GOSUB10100
7280 HO=26:VE=7:GOSUB110:PRINT"            "
7290 GOTO7200
7300 HO=23+LD-1-Q3:VE=6:GOSUB110
7310 GOSUB210
7330 IF(IN$<"0") OR(IN$>"9") THEN7310
7340 IF(IN$=MID$(U5$,J,1)) THENRETURN
7360 HO=26:VE=8:GOSUB110:PRINTIN$" is FOUT"
7370 GOSUB10100
7380 HO=26:VE=8:GOSUB110:PRINT"            "
7390 GOTO7300
7400 HO=26-1-Q3:VE=6:GOSUB110
7410 GOSUB210
7420 IF(IN$<"0") OR(IN$>"9") THEN7410
7430 IF(IN$=MID$(S$,J,1)) THENRETURN
7440 HO=26:VE=8:GOSUB110:PRINTIN$" is FOUT"
7450 GOSUB10100
7460 HO=26:VE=8:GOSUB110:PRINT"           "
7470 GOTO7400
8000 REM GETALLEN CONTROLEREN
8010 IF(D=0) OR(T=0) THENGOSUB8300:GOTO8320
8020 GOSUB8500:REM DELER CONTROLEREN
8030 IFLEN(D$)>4 THENGOSUB8300:GOSUB10060:GOTO8350
8040 IFLEN(D$)>LEN(T$) THENGOSUB8300:GOSUB10060:GOTO8440
8050 IFLEN(T$)>8 THENGOSUB8300:GOSUB10060:GOTO8380
8070 LD=LEN(D$):WD=VAL(D$)
8080 LT=LEN(T$):WT=VAL(T$)
8090 GOSUB8700
8100 IFLEN(U$)>6 THENGOSUB8300:GOSUB10060:GOTO8410
8110 IFWT<>INT(WT) THENGOSUB8900
8120 IFLEFT$(T$,2)="00" THENGOSUB8300:GOSUB10060:GOTO8320
8130 IF(WT<.01) THENGOSUB8300:GOSUB10060:GOTO8320
8140 GOTO1900
8300 IFF=3 THENPRINT"   AL 4x FOUT....":GOSUB10000:GOTO1000
8310 RETURN
8320 GOSUB8490:PRINT"GETALLEN ZIJN NIET IN ORDE."
8330 F=F+1:GOSUB10000:GOTO1700
8340 REM
8350 GOSUB8490:PRINT"DELER IS TE GROOT."
8360 F=F+1:GOSUB10000:GOTO1700
8370 REM
8380 GOSUB8490:PRINT"DEELTAL IS TE GROOT."
8390 F=F+1:GOSUB10000:GOTO1700
8400 REM
8410 GOSUB8490:PRINT"UITKOMST IS TE GROOT."
8420 F=F+1:GOSUB10000:GOTO1700
8430 REM
8440 GOSUB8490:PRINT"DEELTAL VOOR PUNT IS TE KLEIN."
8450 F=F+1:GOSUB10000:GOTO1700
8490 HO=5:VE=18:GOSUB110:RETURN
8500 IFD<>INT(D) THEN8550
8510 SR=D:GOSUB300:D$=SR$
8520 SR=T:GOSUB300:T$=SR$
8530 IFLEFT$(T$,1)="." THENT$="0"+T$
8540 RETURN
8550 REM * KOMMA UIT DELER
8560 FORI=1 TOLEN(D$)
8570 IFMID$(D$,I,1)="." THENGH=I
8580 NEXTI
8590 DC$=RIGHT$(D$,LEN(D$)-GH)
8600 L1=LEN(DC$):M=10^L1
8610 GOSUB8490:PRINT"DELER EN DEELTAL WORDEN"
8620 PRINT:PRINT"      ";M;"x ZO GROOT."
8630 D1=M*D:T1=M*T
8640 SR=D1:GOSUB300:D$=SR$
8650 SR=T1:GOSUB300:T$=SR$
8660 IFLEFT$(T$,1)="." THENT$="0"+T$
8670 GOSUB10000:GOSUB8860:RETURN
8700 IF(K1=2) OR(WT=INT(WT)) THEN8850
8710 FORI=1 TOLEN(T$)
8720 IFMID$(T$,I,1)="." THENTH=I
8730 NEXTI:IFTH=0 THENTH=LEN(T$)
8735 TH$=LEFT$(T$,TH-1):L2=LEN(TH$)
8740 TC$=RIGHT$(T$,LEN(T$)-TH)
8750 L3=LEN(TC$)
8755 UI=WT/WD:SR=UI:GOSUB300:U$=SR$
8760 IFLEFT$(U$,1)="." THENU$="0"+U$
8770 FORJ=1 TOLEN(U$)
8780 IFMID$(U$,J,1)="." THENUH=J
8790 NEXTJ
8800 UH$=LEFT$(U$,UH)
8810 UC$=MID$(U$,LEN(UH$)+1,L3)
8820 U$=UH$+UC$
8830 RETURN
8850 UI=INT(WT/WD):SR=UI:GOSUB300:U$=SR$
8860 HO=6:VE=12:GOSUB110
8870 PRINTD$;" / ";T$;" \ ??"
8880 GOSUB10060
8890 RETURN
8900 REM "0" VOOR DEELTAL
8910 IF(L2=LD) OR(L2>LD) THENRETURN
8920 IFLD-L2=1 THENT$="0"+T$:PU=1:RETURN
8930 GOSUB8300:GOSUB10060:GOTO8440
8990 REM INVOERROUTINE SOM
9000 G$="":I=0
9010 GOSUB110:GOSUB210
9020 IF(IN$=CHR$(8)) AND(I=0) THEN9010
9030 IF(IN$=CHR$(8)) THEN9090
9040 IF(IN$=CHR$(13)) AND(I>0) THENRETURN
9050 IF(IN$<".") OR(IN$>"9") OR(IN$="/") THEN9010
9060 PRINTIN$;:G$=G$+IN$
9070 I=I+1:HO=HO+1:IF(I>8) THENRETURN
9080 GOTO9010
9090 G$=LEFT$(G$,LEN(G$)-1)
9100 HO=HO-1:GOSUB110:I=I-1:PRINT" "
9110 GOTO9010
9200 REM FOUT UITKOMST
9210 F1=F1+1
9220 GOSUB9350:GOSUB6000:GOSUB9360
9230 RETURN
9250 REM FOUT KEER
9260 F2=F2+1
9270 GOSUB9350:GOSUB6300:GOSUB9360
9280 RETURN
9300 REM FOUT AFTREKKEN
9310 F3=F3+1
9320 GOSUB9350:GOSUB6500:GOSUB9360
9330 RETURN
9350 GOSUB9370:PRINT"FOUT":RETURN
9360 GOSUB9370:PRINT"    ":RETURN
9370 HO=15:VE=4:GOSUB110:RETURN
9400 HO=20:VE=4:GOSUB110
9410 PRINT"Ik heb 4 sommen"
9420 VE=6:GOSUB110:PRINT" gemaakt."
9430 VE=8:GOSUB110:PRINT"NU JIJ DUS."
9440 RETURN
9990 REM SUBROUTINES
10000 HO=0:VE=23:GOSUB110
10010 PRINT" Druk op de spatiebalk...";
10020 GOSUB210
10030 IF(IN$<>" ") THEN10020
10040 GOSUB100:RETURN
10050 REM * WISSEN
10060 HO=0:VE=14:GOSUB110
10070 FORJ=1 TO68:PRINT"          ";
10080 NEXTJ:RETURN
10090 REM WACHTLUSSEN
10100 FORW=1 TO1000:NEXTW:RETURN
10110 FORW=1 TO2000:NEXTW:RETURN
10120 FORW=1 TO50:NEXTW:RETURN
10130 FORW=1 TO500:NEXTW:RETURN
10140 FORW=1 TO100:NEXTW:RETURN
10190 REM BEGINSCHERM
10200 GOSUB100
10210 HO=0:VE=2:GOSUB110
10220 PRINT"        **********************"
10230 PRINT"        *                    *"
10240 PRINT"        *   STAARTDELINGEN   *"
10250 PRINT"        *                    *"
10260 PRINT"        **********************"
10270 RETURN
10290 REM INSTRUKTIE
10300 GOSUB100
10310 PRINT:PRINT"       > >  U I T L E G  < <"
10320 PRINT:PRINT" Ik wil je helpen bij het maken van"
10330 PRINT:PRINT" staartdelingen."
10340 PRINT:PRINT" De vorm ziet er zo uit:"
10350 PRINT:PRINT"     18/2345\130"
10360 PRINT"        18"
10370 PRINT"        --"
10380 PRINT"         54"
10390 PRINT"         54"
10400 PRINT"         --"
10410 PRINT"          05"
10420 PRINT"           0"
10430 PRINT"          --"
10440 PRINT"    REST   5"
10450 PRINT"               Aanhalen doe ik zelf!"
10460 GOSUB10000
10470 PRINT:PRINT" Je kunt zelf de som samenstellen"
10480 PRINT:PRINT" en je kunt het mij laten doen."
10490 PRINT:PRINT
10500 PRINT:PRINT" Als je in een getal een komma zet"
10510 PRINT:PRINT" moet je er voor zorgen dat je"
10520 PRINT:PRINT" een PUNT typt."
10530 PRINT:PRINT" KOMMA'S IN GETALLEN ZIJN EEN PUNT !!"
10540 PRINT:PRINT
10550 PRINT:PRINT" Een punt in de deler haal ik er uit."
10560 PRINT:PRINT" De getallen veranderen vanzelf !"
10570 GOSUB10000
10580 PRINT:PRINT" De deler en het deeltal mogen"
10590 PRINT:PRINT" niet 0 zijn."
10600 PRINT:PRINT" De deler mag 1 cijfer groter zijn dan"
10610 PRINT:PRINT" 't hele getal van het deeltal."
10620 PRINT:PRINT:PRINT" Als een som niet gaat moet je"
10630 PRINT:PRINT" opnieuw getallen opgeven."
10640 PRINT:PRINT:PRINT" Maak de getallen ook niet groter"
10650 PRINT:PRINT" dan aangegeven is."
10660 GOSUB10000
10670 PRINT:PRINT" Als je niet uit het hoofd kunt"
10680 PRINT:PRINT" uitrekenen hoeveel keer iets gaat,"
10690 PRINT:PRINT" kun je op de > K < drukken."
10700 PRINT:PRINT" Je krijgt dan een KLADBLOK waarop"
10710 PRINT:PRINT" je kunt oefenen."
10720 PRINT:PRINT" Fouten worden hier niet meegeteld."
10730 PRINT:PRINT" Als je een fout maakt krijg je"
10740 PRINT:PRINT" automatisch de kladblok."
10750 GOSUB10000
10760 PRINT:PRINT" Je kunt mij ook laten rekenen."
10770 PRINT:PRINT" Maar.... ik reken niet alles voor."
10780 PRINT:PRINT" Ik reken 4 sommen voor je uit;"
10790 PRINT:PRINT" de rest mag jij doen."
10800 PRINT:PRINT:PRINT"      VEEL SUCCES..."
10810 GOSUB10000
10820 RETURN
14000 REM
14090 REM SUBROUTINE PRINTER
14100 GOSUB10200
14110 HO=6:VE=15:GOSUB110
14120 PRINT"Wil je een afdruk"
14130 PRINT:PRINT"        op de printer? (J/N) ";
14140 GOSUB210
14150 IF(IN$="J") OR(IN$="j") THENPR=1:RETURN
14160 IF(IN$="N") OR(IN$="n") THENPR=0:RETURN
14170 GOTO14140
14190 REM SUBROUTINES PRINTER
14200 SR$=" "+D$+" / "+T$+" \ ":GOSUB350
14210 RETURN
14230 SR$=U$:GOSUB350:GOSUB360:RETURN
14240 SP=2*LD+P-LEN(A$)+R+3
14250 FORJ=1 TOSP:SR$=" ":GOSUB350:NEXTJ
14260 SR$=A$:GOSUB350:GOSUB360:RETURN
14270 SP=2*LD+P+R+3-LEN(B$)
14280 FORJ=1 TOSP:SR$=" ":GOSUB350:NEXTJ
14290 FORI=1 TOLEN(B$)
14300 SR$="-":GOSUB350:NEXTI
14310 GOSUB360:RETURN
14320 SP=2*LD+P-LEN(S$)+R+3
14330 FORJ=1 TOSP:SR$=" ":GOSUB350:NEXTJ
14340 SR$=S$:GOSUB350:RETURN
14370 SR$=MID$(T$,LD+T+R,1):GOSUB350
14380 GOSUB360:RETURN
14390 GOSUB360:SP=2*LD+P-LEN(S$)+R+2
14400 FORJ=1 TOSP:SR$=" ":GOSUB350:NEXTJ
14410 FORI=1 TOLEN(S$)+2
14420 SR$="=":GOSUB350:NEXTI
14430 GOSUB360:GOSUB360:RETURN
14990 REM
30000 REM AANPASSING "\"
30010 REM Op enkele computers zal de
30020 REM schuine streep (CODE 92)
30030 REM aangepast moeten worden.
30040 REM
30050 REM WIS/DELETE is in dit prog.
30060 REM CHR$(8) in 9020 en 9030
30070 REM
30100 REM EDUCATIEF PROGRAMMA VOOR
30110 REM O.A. BASISSCHOOL GROEP 7/8
30120 REM GEMAAKT OP MSX-1
30130 REM   JANUARI 1987
30140 REM
30150 REM ***********************
30160 REM *                     *
30170 REM * LEO JANSSEN         *
30180 REM * KNIPPENBERGHSTR. 56 *
30190 REM * 5988 CW  HELDEN     *
30200 REM *   04760-72692       *
30210 REM *                     *
30220 REM ***********************
30230 REM
30240 REM COPYRIGHT NOS HILVERSUM


1000 A=200:GOTO20:REM"ONTBIND IN FACT"
1010 GOSUB100
1020 GOSUB1340
1030 GOSUB100
1040 PRINT"WE GAAN ONTBINDEN IN FACTOREN"
1045 PRINT"Alleen RETURN tikken = stoppen.":PRINT
1050 PRINT"GEEF EEN GETAL ";:INPUTE
1055 IFE=0 THENEND
1060 IF(E<2) OR(E>99999999) THEN1050
1070 PRINT
1080 GOSUB1120
1090 PRINT:PRINT
1100 GOTO1050
1110 :
1120 REM === ONTBINDEN IN FACTOREN ==
1130 IFE=1 THENPRINT"1 = 1";:RETURN
1140 IFE=2 THENPRINT"2 = 2 (priemgetal)";:RETURN
1150 V=1:D=2:P=SQR(E)
1160 SR=E:GOSUB300:E$=SR$
1170 PRINT;E$;" = ";
1180 GOSUB1270
1190 IFD=2 THEND=3:GOTO1210
1200 D=D+2
1210 IFD<=P THEN1180
1220 IFE=1 THENGOSUB120:HO=HO-3:GOSUB110:PRINT"   "
1230 IFE>1 THENPRINT;E$;
1240 IFV=1 THENPRINT" (priemgetal)";
1250 RETURN
1260 :
1270 P=E/D:IFP<>INT(P) THENRETURN
1280 SR=D:GOSUB300:D$=SR$
1290 V=0:PRINT;D$;" x ";
1300 E=INT(E/D):P=SQR(E)
1310 SR=E:GOSUB300:E$=SR$
1320 GOTO1270
1330 :
1340 REM === INLEIDING ===
1350 GOSUB100
1360 FORN=1 TO18:READA$:PRINTA$:NEXTN
1370 DATA" "," IK KAN ONTBINDEN IN FACTOREN"
1380 DATA" ============================"," "
1390 DATA" "," Als je mij een getal geeft, dan"
1400 DATA" "," zal ik het laten zien."
1410 DATA" "," Ik deel het getal door alle"
1420 DATA" "," PRIEMGETALLEN die er op passen."
1430 DATA" "," Ik probeer eerst 2, dan 3, dan 5"
1440 DATA" "," en zo maar door."
1450 DATA" "," De factoren schrijf ik voor je op."
1460 PRINT:PRINT"--> TIK OP 'N TOETS";
1470 GOSUB210:RETURN
1480 REM============================
30000 REM GEMAAKT OP EEN BBC COMPUTER
30010 REM 30 JANUARI 1987
30020 REM DOOR
30030 REM Ch.W.Brederode
30040 REM Von Bonninghausenlaan 33
30050 REM 2161 ES  LISSE
30060 REM
30070 REM COPYRIGHT NOS HILVERSUM


1000 A=1000:GOTO20:REM REKENFRIEK
1010 DIMLC$(25):DIMW(10),X(10),Y(10),Z(10),AF(10)
1015 GOSUB100
1020 HO=6:VE=4:GOSUB110:PRINT"**************"
1030 HO=6:VE=5:GOSUB110:PRINT"*            *"
1040 HO=6:VE=6:GOSUB110:PRINT"* REKENFRIEK *"
1050 HO=6:VE=7:GOSUB110:PRINT"*            *"
1060 HO=6:VE=8:GOSUB110:PRINT"**************"
1070 HO=0:VE=12:GOSUB110:PRINT"     */^?=%+-*/^?=%+-/^?="
1080 HO=3:VE=15:GOSUB110:PRINT"Hallo,mijn naam is"
1090 HO=7:VE=17:GOSUB110:PRINT"REKENFRIEK"
1100 HO=3:VE=21:GOSUB110:PRINT"Hoe heet jij";:INPUTNA$
1110 IFNA$<>"" THEN1160
1120 HO=3:VE=21:GOSUB110:PRINT"Heb jij geen naam???"
1130 FORJ=1 TO1000:NEXTJ
1140 HO=3:VE=21:GOSUB110:PRINT"                           "
1150 GOTO1100
1160 GOSUB100
1170 HO=3:VE=3:GOSUB110:PRINT"Hallo ";NA$
1180 HO=1:VE=5:GOSUB110
1190 PRINT"Leuk dat je met mij wilt rekenen."
1200 HO=1:VE=9:GOSUB110:PRINT"Wil je eerst uitleg (j/n)?"
1210 GOSUB210:F$=IN$
1220 IFF$="S" THEN6410
1230 IFF$="s" THEN6410
1240 IFF$="J" THEN1290
1250 IFF$="j" THEN1290
1260 IFF$="N" THEN1710
1270 IFF$="n" THEN1710
1280 GOTO1210
1290 GOSUB100
1300 PRINT"   "NA$;","
1310 PRINT:PRINT"ik zal het net zo moeilijk"
1320 PRINT"maken als je zelf wenst."
1330 PRINT:PRINT"Welke rekenbewerking je wilt gaan"
1340 PRINT"doen kun je op het"
1350 PRINT"-MENU-scherm kiezen."
1360 PRINT:PRINT"Bij het berekenen van de antwoorden"
1370 PRINT"mag je net zoveel kladpapier"
1380 PRINT"gebruiken als je wilt"
1390 PRINT"maar probeer het zoveel"
1400 PRINT"mogelijk uit je hoofd te doen."
1410 GOSUB5120
1420 PRINT"Het gebruiken van een zakrekenmachine"
1430 PRINT"voor het vinden van de antwoorden"
1440 PRINT"maakt me ziek.Ik neem aan dat je"
1450 PRINT"me dat niet wilt aandoen!"
1460 PRINT:PRINT"Je krijgt steeds blokken van 10"
1470 PRINT"vragen,waarna je kunt kiezen om door"
1480 PRINT"te gaan met dezelfde of met een"
1490 PRINT"een andere rekenkundige bewerking."
1500 PRINT:PRINT"Na 10 vragen vertel ik je hoe hoog"
1510 PRINT"je score is. Als je het antwoord"
1520 PRINT"direkt goed hebt krijg je 10 punten."
1530 PRINT"Is je 2e antwoord goed dan"
1540 PRINT"krijg je 5 punten."
1550 GOSUB5120
1560 PRINT:PRINT"De score wordt bepaald door"
1570 PRINT"het totaal aantal behaalde punten"
1580 PRINT"te delen door het aantal "
1590 PRINT"gestelde vragen."
1600 PRINT:PRINT"Naar aanleiding van jouw score geef"
1610 PRINT"ik je advies om je speelniveau"
1620 PRINT"te verhogen,te verlagen,of"
1630 PRINT"hetzelfde te laten."
1640 PRINT:PRINT"Elke keer als ik je een antwoord"
1650 PRINT"vraag,kun je stoppen. In plaats"
1660 PRINT"van een antwoord druk je dan -S- in."
1670 GOSUB5120:REMVolgende scherm'
1680 PRINT:PRINT"Na ieder antwoord moet je -RETURN-"
1690 PRINT"indrukken,anders krijg ik jouw"
1700 PRINT"antwoord niet tezien."
1710 GOSUB100:HO=6:VE=10:GOSUB110:PRINT"SUCCES ";NA$
1720 GOSUB5120:REMVolgende scherm'
1730 GOSUB100:K=0
1740 HO=10:VE=2:GOSUB110:PRINT"MENU"
1750 HO=4:VE=12:GOSUB110:PRINT"-M-MACHTSVERHEFFEN."
1760 HO=4:VE=14:GOSUB110:PRINT"-W-WORTELTREKKEN."
1770 HO=4:VE=16:GOSUB110:PRINT"-S-STOPPEN."
1780 HO=5:VE=22:GOSUB110:PRINTNA$;",maak je keuze."
1790 GOSUB210:A$=IN$
1800 IFA$="M" THENH=12:GOSUB9510:K=3:GOTO1870
1810 IFA$="m" THENH=12:GOSUB9510:K=3:GOTO1870
1820 IFA$="W" THENH=14:GOSUB9510:K=4:GOTO2440
1830 IFA$="w" THENH=14:GOSUB9510:K=4:GOTO2440
1840 IFA$="S" THENH=16:GOSUB9510:GOTO6410
1850 IFA$="s" THENH=16:GOSUB9510:GOTO6410
1860 GOTO1790
1870 REM
1880 REM Machtsverheffen
1890 REM
1900 GOSUB100:R$="0"
1910 HO=3:VE=3:GOSUB110:PRINTNA$;","
1920 HO=1:VE=5:GOSUB110
1930 PRINT"  wil je eerst uitleg over"
1940 PRINT"   machtsverheffen (j/n)?"
1950 GOSUB210:A$=IN$
1960 IFA$="S" THEN6410
1970 IFA$="s" THEN6410
1980 IFA$="J" THEN2030
1990 IFA$="j" THEN2030
2000 IFA$="N" THEN2040
2010 IFA$="n" THEN2040
2020 GOTO1950
2030 GOSUB6650:REMUitleg'
2040 GOSUB3070:REMMoeilijkheids                       graad'
2050 GOSUB7040:REMGrootte van                      de macht'
2060 GOSUB4240:REMGetalsoort'
2070 GOSUB4660:REMGetallen                        genereren'
2080 GOSUB100:N=0
2090 FORI=1 TO10
2100 B$="0"
2110 AW=(INT((W(I)^X(I)+.5/GW)*GW))/GW
2120 GOSUB5060:REMVraag bij                         opgaven'
2130 HO=0:VE=8:GOSUB110:PRINTW(I);"^";X(I);"=";:INPUTB$
2140 IFB$="S" THEN6410
2150 IFB$="s" THEN6410
2160 B=(INT((VAL(B$)+.5/GW)*GW))/GW
2170 GOSUB5210:REMControle                         antwoord'
2180 GOSUB100
2190 NEXTI
2200 GOSUB6150:REMAdvies'
2210 HO=2:VE=20:GOSUB110
2220 PRINT"Wil je met machtsverheffen"
2230 PRINT"  door gaan (j/n)?"
2240 GOSUB210:A$=IN$
2250 IFA$="S" THEN6410
2260 IFA$="s" THEN6410
2270 IFA$="J" THEN1870
2280 IFA$="j" THEN1870
2290 IFA$="N" THEN2320
2300 IFA$="n" THEN2320
2310 GOTO2240
2320 GOSUB100
2330 HO=2:VE=20:GOSUB110
2340 PRINT"Wil je een andere"
2350 PRINT"  rekenbewerking doen (j/n)?"
2360 GOSUB210:A$=IN$
2370 IFA$="J" THEN1730
2380 IFA$="j" THEN1730
2390 IFA$="N" THEN6410
2400 IFA$="n" THEN6410
2410 IFA$="S" THEN6410
2420 IFA$="s" THEN6410
2430 GOTO2360
2440 REM
2450 REM Worteltrekken
2460 REM
2470 GOSUB100:R$="0"
2480 HO=3:VE=3:GOSUB110:PRINTNA$;","
2490 HO=2:VE=5:GOSUB110
2500 PRINT"wil je eerst uitleg over"
2510 PRINT"  worteltrekken (j/n)?"
2520 GOSUB210:A$=IN$
2530 IFA$="S" THEN6410
2540 IFA$="s" THEN6410
2550 IFA$="J" THEN2600
2560 IFA$="j" THEN2600
2570 IFA$="N" THEN2610
2580 IFA$="n" THEN2610
2590 GOTO2520
2600 GOSUB7380:REMUitleg'
2610 GOSUB3070:REMMoeilijkheids                       graad'
2620 GOSUB9830:REMGrootte van                     de macht'
2630 GOSUB4240:REMGetalsoort'
2640 AG$="1"
2650 GOSUB4660:REMGetallen                        genereren'
2660 GOSUB100:N=0
2670 FORI=1 TO10
2680 B$="0"
2690 IFG=1 THEN2720
2700 IFQ>2 THEN2720
2710 GOTO2730
2720 W(I)=(INT(W(I)^(1/Q)))^Q
2730 AW=(INT((W(I)^(1/Q)+.5/GW)*GW))/GW
2740 GOSUB5060:REMVraag bij                         opgaven'
2750 IFQ<>2 THENHO=1:VE=8:GOSUB110:PRINTQ
2760 HO=2:VE=9:GOSUB110:PRINT"V";W(I);"=";:INPUTB$
2770 IFB$="S" THEN6410
2780 IFB$="s" THEN6410
2790 B=(INT((VAL(B$)+.5/GW)*GW))/GW
2800 GOSUB5210:REMControle                         antwoord'
2810 GOSUB100
2820 NEXTI
2830 GOSUB6150:REMAdvies'
2840 HO=2:VE=20:GOSUB110
2850 PRINT"Wil je met worteltrekken"
2860 PRINT"  door gaan (j/n)?"
2870 GOSUB210:A$=IN$
2880 IFA$="S" THEN6410
2890 IFA$="s" THEN6410
2900 IFA$="J" THEN2440
2910 IFA$="j" THEN2440
2920 IFA$="N" THEN2950
2930 IFA$="n" THEN2950
2940 GOTO2870
2950 GOSUB100
2960 HO=2:VE=20:GOSUB110
2970 PRINT"Wil je een andere"
2980 PRINT"  rekenbewerking doen (j/n)?"
2990 GOSUB210:A$=IN$
3000 IFA$="J" THEN1730
3010 IFA$="j" THEN1730
3020 IFA$="N" THEN6410
3030 IFA$="n" THEN6410
3040 IFA$="S" THEN6410
3050 IFA$="s" THEN6410
3060 GOTO2990
3070 REM
3080 REM Moeilijkheidsgraad
3090 REM
3100 GOSUB100
3110 HO=3:VE=2:GOSUB110:PRINTNA$;","
3120 HO=1:VE=4:GOSUB110:PRINT"je moet nu de"
3130 PRINT" moeilijkheidsgraad kiezen.Ik wil"
3140 PRINT" graag weten hoe groot de getallen,"
3150 PRINT" waarmee je wilt rekenen,moeten"
3160 PRINT" zijn.Op het volgende scherm kun"
3170 PRINT" je jouw keuze aangeven met 0,1,..of 9"
3180 IFK<>4 THEN3220
3190 HO=1:VE=14:GOSUB110:PRINT"Advies:kies tenminste -3-"
3200 PRINT" anders krijg je bijna steeds"
3210 PRINT" dezelfde opgaven."
3220 GOSUB5120:REMVolgende scherm'
3230 HO=5:VE=0:GOSUB110:PRINT"MOEILIJKHEIDSGRAAD"
3240 HO=2:VE=2:GOSUB110:PRINT"-1-Getallen van 0 t/m      5"
3250 HO=2:VE=4:GOSUB110:PRINT"-2-Getallen van 0 t/m     10"
3260 HO=2:VE=6:GOSUB110:PRINT"-3-Getallen van 0 t/m     20"
3270 HO=2:VE=8:GOSUB110:PRINT"-4-Getallen van 0 t/m     50"
3280 HO=2:VE=10:GOSUB110:PRINT"-5-Getallen van 0 t/m    100"
3290 HO=2:VE=12:GOSUB110:PRINT"-6-Getallen van 0 t/m    500"
3300 HO=2:VE=14:GOSUB110:PRINT"-7-Getallen van 0 t/m  1.000"
3310 HO=2:VE=16:GOSUB110:PRINT"-8-Getallen van 0 t/m  5.000"
3320 HO=2:VE=18:GOSUB110:PRINT"-9-Getallen van 0 t/m 10.000"
3330 HO=2:VE=20:GOSUB110:PRINT"-0-Getallen zelf bepalen"
3340 HO=2:VE=22:GOSUB110:PRINTNA$;",wat kies je?"
3350 GOSUB210:M$=IN$
3360 IFM$="S" THEN6410
3370 IFM$="s" THEN6410
3380 IFM$="1" THENH=2:GOSUB9620:MH=5:GOTO3490
3390 IFM$="2" THENH=4:GOSUB9620:MH=10:GOTO3490
3400 IFM$="3" THENH=6:GOSUB9620:MH=20:GOTO3490
3410 IFM$="4" THENH=8:GOSUB9620:MH=50:GOTO3490
3420 IFM$="5" THENH=10:GOSUB9620:MH=100:GOTO3490
3430 IFM$="6" THENH=12:GOSUB9620:MH=500:GOTO3490
3440 IFM$="7" THENH=14:GOSUB9620:MH=1000:GOTO3490
3450 IFM$="8" THENH=16:GOSUB9620:MH=5000:GOTO3490
3460 IFM$="9" THENH=18:GOSUB9620:MH=10000:GOTO3490
3470 IFM$="0" THENH=20:GOSUB9620:GOTO3510
3480 GOTO3350
3490 ML=0
3500 GOTO4230
3510 GOSUB100
3520 HO=5:VE=2:GOSUB110:PRINT"MOEILIJKHEIDSGRAAD"
3530 HO=3:VE=5:GOSUB110:PRINTNA$;","
3540 PRINT:PRINT"vertel me maar wat het kleinste"
3550 PRINT"en grootste getal is waarmee"
3560 PRINT"je wilt rekenen."
3570 HO=3:VE=12:GOSUB110:PRINT"Kleinste getal is";
3580 INPUTML$
3590 IFML$="S" THEN6410
3600 IFML$="s" THEN6410
3610 RESTORE
3620 FORI=1 TO10
3630 READL$(I)
3640 NEXTI
3650 L=LEN(ML$)
3660 LG=0
3670 FORJ=1 TOL
3680 LC$(J)=MID$(ML$,J,1)
3690 FORI=1 TO10
3700 IFLC$(J)=L$(I) THEN3900
3710 NEXTI
3720 IFLC$(J)="E" THEN3740
3730 GOTO3770
3740 IFJ<>1 THEN3760
3750 GOTO3770
3760 IFJ<>L THENLG=LG+1:IFLG<2 THEN3900
3770 IFLC$(J)="+" THEN3790
3780 GOTO3810
3790 IFJ=1 THEN3900
3800 IFLC$(J-1)="E" THEN3900
3810 IFLC$(J)="-" THEN3830
3820 GOTO3850
3830 IFJ=1 THEN3900
3840 IFLC$(J-1)="E" THEN3900
3850 IFLC$(J)="." THEN3870
3860 GOTO3880
3870 IFLG=0 THEN3900
3880 HO=3:VE=12:GOSUB110:PRINT"                           "
3890 GOTO3570
3900 NEXTJ
3910 ML=VAL(ML$)
3920 HO=3:VE=15:GOSUB110:PRINT"Grootste getal is";
3930 INPUTMH$
3940 IFMH$="S" THEN6410
3950 IFMH$="s" THEN6410
3960 L=LEN(MH$)
3970 LG=0
3980 FORJ=1 TOL
3990 LC$(J)=MID$(MH$,J,1)
4000 FORI=1 TO10
4010 IFLC$(J)=L$(I) THEN4210
4020 NEXTI
4030 IFLC$(J)="E" THEN4050
4040 GOTO4080
4050 IFJ<>1 THEN4070
4060 GOTO4080
4070 IFJ<>L THENLG=LG+1:IFLG<2 THEN4210
4080 IFLC$(J)="+" THEN4100
4090 GOTO4120
4100 IFJ=1 THEN4210
4110 IFLC$(J-1)="E" THEN4210
4120 IFLC$(J)="-" THEN4140
4130 GOTO4160
4140 IFJ=1 THEN4210
4150 IFLC$(J-1)="E" THEN4210
4160 IFLC$(J)="." THEN4180
4170 GOTO4190
4180 IFLG=0 THEN4210
4190 HO=3:VE=15:GOSUB110:PRINT"                            "
4200 GOTO3920
4210 NEXTJ
4220 MH=VAL(MH$)
4230 RETURN
4240 REM
4250 REM Getalsoort
4260 REM
4270 GOSUB100
4280 HO=3:VE=2:GOSUB110:PRINTNA$;","
4290 PRINT:PRINT"je moet nu (bij het volgende scherm)"
4300 PRINT"kiezen of je met gehele of gebroken"
4310 PRINT"getallen (getallen met een komma"
4320 PRINT"erin) wilt rekenen."
4330 PRINT:PRINT"Bij decimale (=gebroken) getallen"
4340 PRINT"worden de antwoorden zonodig afgerond"
4350 PRINT"op 2 of 4 cijfers achter de komma,"
4360 PRINT"e.e.a.afhankelijk van jouw keuze."
4370 PRINT:PRINT"In getallen wordt een komma "
4380 PRINT"weergegeven als een punt-.-"
4390 GOSUB5120:REMVolgende scherm'
4400 HO=5:VE=2:GOSUB110:PRINT"GETALSOORT"
4410 HO=2:VE=4:GOSUB110:PRINT"-1-Alleen gehele getallen."
4420 HO=2:VE=6:GOSUB110
4430 PRINT"-2-Alleen antwoord decimaal"
4440 PRINT"     max. 2 decimalen."
4450 HO=2:VE=10:GOSUB110
4460 PRINT"-3-Alleen antwoord decimaal"
4470 PRINT"     max. 4 decimalen."
4480 HO=2:VE=14:GOSUB110
4490 PRINT"-4-Alle getallen decimaal"
4500 PRINT"     max. 2 decimalen."
4510 HO=2:VE=17:GOSUB110
4520 PRINT"-5-Alle getallen decimaal"
4530 PRINT"     max. 4 decimalen."
4540 HO=2:VE=22:GOSUB110:PRINTNA$;",wat kies je?"
4550 GOSUB210:G$=IN$
4560 IFG$="S" THEN6410
4570 IFG$="s" THEN6410
4580 IFG$="1" THENH=4:GOSUB9620:GH=1:GW=1:GOTO4640
4590 IFG$="2" THENH=6:GOSUB9620:GH=1:GW=100:GOTO4640
4600 IFG$="3" THENH=10:GOSUB9620:GH=1:GW=10000:GOTO4640
4610 IFG$="4" THENH=14:GOSUB9620:GH=100:GW=100:GOTO4640
4620 IFG$="5" THENH=17:GOSUB9620:GH=10000:GW=10000:GOTO4640
4630 GOTO4550
4640 G=VAL(G$)
4650 RETURN
4660 REM
4670 REM  Getallen genereren
4680 REM
4690 GOSUB100:GA=0
4700 HO=1:VE=10:GOSUB110:PRINT"EVEN GEDULD ";NA$;","
4710 HO=1:VE=12:GOSUB110:PRINT"ik moet even getallen"
4720 HO=1:VE=14:GOSUB110:PRINT"bedenken."
4730 FORI=1 TO10:GOTO4770
4740 GA=GA-1:GOTO4770
4750 GA=GA-2:GOTO4770
4760 GA=GA-3
4770 XT=0:YT=0:ZT=0
4780 FORJ=1 TOI*I:NEXTJ
4790 GOSUB260
4800 W(I)=(INT(RV*MH*GH+.5))/GH
4810 IFW(I)<ML THEN4790
4820 IFW(I)=W(I-1) THEN4790
4830 IFI>2 THEN4850
4840 GOTO4860
4850 IFW(I)=W(I-2) THEN4790
4860 GA=GA+1
4870 HO=1:VE=20:GOSUB110:PRINT"Ik heb er al:";GA
4880 FORJ=1 TO50:NEXTJ
4890 IFK=4 THEN5010
4900 GOSUB260
4910 X(I)=(INT(RV*MM*GH+.5))/GH
4920 X(I)=INT(X(I)+.5)
4930 IFX(I)<ML THEN4900
4940 IFX(I)=X(I-1) THEN4900
4950 IFI>2 THEN4970
4960 GOTO4980
4970 IFX(I)=X(I-2) THEN4900
4980 GA=GA+1
4990 HO=1:VE=20:GOSUB110:PRINT"Ik heb er al:";GA
5000 FORJ=1 TO50:NEXTJ
5010 NEXTI
5020 GOSUB100
5030 HO=1:VE=20:GOSUB110:PRINT"Zo,ik heb ze allemaal!"
5040 FORJ=1 TO1000:NEXTJ
5050 RETURN
5060 REM
5070 REM  Vraag bij opgaven
5080 REM
5090 HO=3:VE=3:GOSUB110:PRINTNA$;","
5100 HO=1:VE=5:GOSUB110:PRINT"kun je beantwoorden:"
5110 RETURN
5120 REM
5130 REM Volgende scherm
5140 REM
5150 HO=5:VE=21:GOSUB110:PRINT"Druk op een knop."
5160 GOSUB210:A$=IN$
5170 IFA$="S" THEN6410
5180 IFA$="s" THEN6410
5190 GOSUB100
5200 RETURN
5210 REM
5220 REM  Controle antwoord
5230 REM
5240 IFR$="2" THEN5260
5250 GOTO5270
5260 IFRB<>RA THEN5280
5270 IFB=AW THEN5520
5280 GOSUB5650:REMUitspraken                       bij fout'
5290 GOSUB6090:REMGeluid fout'
5300 HO=1:VE=12:GOSUB110:PRINTC$
5310 RB$="0":B$="0"
5320 HO=1:VE=14:GOSUB110:PRINT"Wat is het goede antwoord?"
5330 IFR$="2" THENHO=6:VE=17:GOSUB110:PRINT"Rest"
5340 HO=1:VE=16:GOSUB110:PRINT"2e poging";:INPUTB$
5350 IFB$="S" THEN6410
5360 IFB$="s" THEN6410
5370 HO=1:VE=12:GOSUB110:PRINT"                            "
5380 HO=1:VE=14:GOSUB110:PRINT"                            "
5390 B=(INT((VAL(B$)+.5/GW)*GW))/GW
5400 IFR$<>"2" THEN5460
5410 HO=10:VE=17:GOSUB110:INPUTRB$
5420 IFRB$="S" THEN6410
5430 IFRB$="s" THEN6410
5440 RB=VAL(RB$)
5450 IF(RB<>RA) THEN5470
5460 IFB=AW THEN5550
5470 GOSUB6090:REMGeluid fout'
5480 HO=1:VE=18:GOSUB110:PRINT"Wat jammer,het goede"
5490 HO=1:VE=20:GOSUB110:PRINT"antwoord is:";AW;
5500 IFR$="2" THENPRINT" rest:";RA
5510 GOTO5600
5520 GOSUB5820:REMUitspraken                       bij goed'
5530 GOSUB6040:REMGeluid goed'
5540 N=N+10:HO=1:VE=18:GOSUB110:PRINTD$:GOTO5590
5550 GOSUB6000:REMUitspraken             bij 2e poging goed'
5560 GOSUB6040:REMGeluid goed'
5570 N=N+5
5580 HO=1:VE=18:GOSUB110:PRINTE$
5590 HO=1:VE=20:GOSUB110:PRINT"je antwoord is goed."
5600 WR=(INT(N/I*10))/10
5610 HO=1:VE=22:GOSUB110:PRINT"OPGAVEN:";I
5620 HO=16:VE=22:GOSUB110:PRINT"SCORE  :";WR
5630 FORJ=1 TO2000:NEXTJ
5640 RETURN
5650 REM
5660 REM  Uitspraken bij fout
5670 REM
5680 GOSUB260
5690 C=INT(RV*11)
5700 IFC=0 THENC$="nee toch"
5710 IFC=1 THENC$="je weet wel beter"
5720 IFC=2 THENC$="hoe kom je er bij"
5730 IFC=3 THENC$="hoe bedenk je het"
5740 IFC=4 THENC$="dit is helemaal fout"
5750 IFC=5 THENC$="dit is echt niet goed"
5760 IFC=6 THENC$="weet je dat wel zeker"
5770 IFC=7 THENC$="je bent een oenemeloen"
5780 IFC=8 THENC$="ik schrik van je antwoord"
5790 IFC=9 THENC$="dit valt me van je tegen"
5800 IFC=10 THENC$="ik had beter van je verwacht"
5810 RETURN
5820 REM
5830 REM  Uitspraken bij goed
5840 REM
5850 GOSUB260
5860 D=INT(RV*12)
5870 IFD=0 THEND$="onwijs gaaf"
5880 IFD=1 THEND$="tof he"
5890 IFD=2 THEND$="je doet het te gek"
5900 IFD=3 THEND$="gaaf,je bent een kei"
5910 IFD=4 THEND$="je bent echt steengoed"
5920 IFD=5 THEND$="je bent een genie"
5930 IFD=6 THEND$="heel goed,ga zo door"
5940 IFD=7 THEND$="hoe krijg je het voor elkaar"
5950 IFD=8 THEND$="uit de kunst"
5960 IFD=9 THEND$="wauw!!"
5970 IFD=10 THEND$="prima!"
5980 IFD=11 THEND$="subliem!"
5990 RETURN
6000 REM
6010 REM  Uitspraken bij 2e
6020 REM  poging goed
6030 REM
6040 GOSUB260
6050 E=INT(RV*8)
6060 IFE=0 THENE$="dat is beter"
6070 IFE=1 THENE$="zo gaat ie goed"
6080 IFE=2 THENE$="ga zo door"
6090 IFE=3 THENE$="hou vol"
6100 IFE=4 THENE$="prima"
6110 IFE=5 THENE$="zo ken ik je weer"
6120 IFE=6 THENE$="gefeliciteerd"
6130 IFE=7 THENE$="ik ben weer tevreden"
6140 RETURN
6150 REM
6160 REM  Advies
6170 REM
6180 HO=3:VE=3:GOSUB110:PRINTNA$;","
6190 HO=2:VE=5:GOSUB110:PRINT"je score is:";WR
6200 HO=2:VE=10:GOSUB110:PRINT"Wil je mijn advies?(j/n)"
6210 GOSUB210:AD$=IN$
6220 IFAD$="S" THEN6410
6230 IFAD$="s" THEN6410
6240 IFAD$="N" THEN6390
6250 IFAD$="n" THEN6390
6260 IFAD$="J" THEN6290
6270 IFAD$="j" THEN6290
6280 GOTO6210
6290 IFWR<5 THEN6340
6300 IFWR>7 THEN6360
6310 HO=2:VE=10:GOSUB110:PRINT"Het gaat wel aardig,maar"
6320 PRINT"  het kan nog beter.Blijf nog oefenen"
6330 PRINT"  op hetzelfde niveau!":GOTO6380
6340 HO=2:VE=10:GOSUB110:PRINT"Oefen nog maar eens een"
6350 PRINT"  tijdje op een lager niveau!":GOTO6380
6360 HO=2:VE=10:GOSUB110:PRINT"Het gaat prima,probeer    "
6370 PRINT"  maar eens moeilijker opgaven!"
6380 FORJ=1 TO3000:NEXTJ
6390 GOSUB100
6400 RETURN
6410 REM
6420 REM  Stoppen
6430 REM
6440 GOSUB100
6450 HO=3:VE=5:GOSUB110:PRINTNA$;","
6460 HO=1:VE=7:GOSUB110:PRINT"weet je zeker dat je"
6470 HO=1:VE=9:GOSUB110:PRINT"wilt stoppen? (j/n)"
6480 GOSUB210:S$=IN$
6490 IFS$="J" THEN6540
6500 IFS$="j" THEN6540
6510 IFS$="N" THEN1730
6520 IFS$="n" THEN1730
6530 GOTO6480
6540 GOSUB100
6550 FORJ=1 TO7
6560 HO=16:VE=9:GOSUB110
6570 PRINT"TOT"
6580 HO=15:VE=15:GOSUB110
6590 PRINT"ZIENS"
6600 FORI=1 TO250:NEXTI
6610 GOSUB100
6620 FORI=1 TO150:NEXTI
6630 NEXTJ
6640 END
6650 REM
6660 REM Uitleg
6670 REM      machtsverheffen
6680 REM
6690 GOSUB100
6700 HO=3:VE=2:GOSUB110:PRINTNA$;","
6710 HO=1:VE=4:GOSUB110:PRINT"machtsverheffen is eigenlijk"
6720 HO=1:VE=6:GOSUB110:PRINT"een bijzondere vorm van"
6730 HO=1:VE=8:GOSUB110:PRINT"vermenigvuldigen."
6740 HO=1:VE=11:GOSUB110:PRINT"Machtsverheffen betekent:"
6750 HO=1:VE=13:GOSUB110:PRINT"vermenigvuldig het grondtal"
6760 HO=1:VE=15:GOSUB110:PRINT"zoveel keer met zichzelf"
6770 HO=1:VE=17:GOSUB110:PRINT"als de macht groot is."
6780 GOSUB5120:REMVolgende scherm'
6790 HO=3:VE=2:GOSUB110:PRINT"Voorbeeld 1 :"
6800 HO=1:VE=4:GOSUB110:PRINT"2^4 (zeg:twee tot de vierde"
6810 HO=1:VE=6:GOSUB110:PRINT"of twee tot de macht vier)"
6820 HO=1:VE=8:GOSUB110:PRINT"is:2*2*2*2;het antwoord"
6830 HO=1:VE=10:GOSUB110:PRINT"is dus:16."
6840 HO=1:VE=12:GOSUB110:PRINT" 2 is hier het grondtal"
6850 HO=1:VE=14:GOSUB110:PRINT" 4 is hier de macht"
6860 GOSUB5120:REMVolgende scherm'
6870 HO=3:VE=2:GOSUB110:PRINT"Voorbeeld 2 :"
6880 HO=1:VE=4:GOSUB110:PRINT"5^2 (grondtal=5,macht=2)"
6890 HO=1:VE=6:GOSUB110:PRINT"5^2=5*5=25"
6900 HO=3:VE=10:GOSUB110:PRINT"Voorbeeld 3 :"
6910 HO=1:VE=12:GOSUB110:PRINT"4^3 (grondtal=4,macht=3)"
6920 HO=1:VE=14:GOSUB110:PRINT"4^3=4*4*4=64"
6930 HO=1:VE=17:GOSUB110:PRINT"Ik zal voor de 'machten'"
6940 PRINT" alleen gehele getallen geven"
6950 HO=3:VE=21:GOSUB110:PRINT"Uitleg nog eens (j/n)?"
6960 GOSUB210:A$=IN$
6970 IFA$="S" THEN6410
6980 IFA$="s" THEN6410
6990 IFA$="J" THEN6650
7000 IFA$="j" THEN6650
7010 IFA$="N" THENRETURN
7020 IFA$="n" THENRETURN
7030 GOTO6960
7040 REM
7050 REM Grootte van de macht
7060 REM      machtsverheffen
7070 GOSUB100:MM$="0"
7080 HO=3:VE=2:GOSUB110:PRINTNA$;","
7090 HO=2:VE=5:GOSUB110:PRINT"je moet nu de grootte van"
7100 PRINT"  de macht kiezen(m.a.w.:hoe vaak"
7110 PRINT"  wil je het grondgetal met"
7120 PRINT"  zichzelf vermenigvuldigen)."
7130 HO=2:VE=11:GOSUB110:PRINT"Bedenk bij je keuze dat"
7140 PRINT"  b.v.  5^4 (vijf tot de macht vier,"
7150 PRINT"  dit is 5*5*5*5) al 625 is!!"
7160 GOSUB5120:REMVolgende scherm'
7170 HO=3:VE=2:GOSUB110:PRINT"Grootte van de macht"
7180 HO=2:VE=4:GOSUB110:PRINT"-1-Machten t/m  3"
7190 HO=2:VE=6:GOSUB110:PRINT"-2-Machten t/m  5"
7200 HO=2:VE=8:GOSUB110:PRINT"-3-Machten t/m  8"
7210 HO=2:VE=10:GOSUB110:PRINT"-4-Machten t/m 10"
7220 HO=2:VE=12:GOSUB110:PRINT"-5-Machten t/m 15"
7230 HO=2:VE=14:GOSUB110:PRINT"-6-Machten t/m 20"
7240 HO=2:VE=16:GOSUB110:PRINT"-7-Machten t/m 25"
7250 HO=3:VE=22:GOSUB110:PRINTNA$;",wat kies je?"
7260 GOSUB210:MM$=IN$
7270 IFMM$="S" THEN6410
7280 IFMM$="s" THEN6410
7290 IFMM$="1" THENH=4:GOSUB9620:MM=3:GOTO7370
7300 IFMM$="2" THENH=6:GOSUB9620:MM=5:GOTO7370
7310 IFMM$="3" THENH=8:GOSUB9620:MM=8:GOTO7370
7320 IFMM$="4" THENH=10:GOSUB9620:MM=10:GOTO7370
7330 IFMM$="5" THENH=12:GOSUB9620:MM=15:GOTO7370
7340 IFMM$="6" THENH=14:GOSUB9620:MM=20:GOTO7370
7350 IFMM$="7" THENH=16:GOSUB9620:MM=25:GOTO7370
7360 GOTO7260
7370 RETURN
7380 REM
7390 REM Uitleg                             machtsverheffen
7400 REM
7410 GOSUB100
7420 HO=3:VE=2:GOSUB110:PRINTNA$;","
7430 HO=1:VE=4:GOSUB110:PRINT"worteltrekken is het"
7440 HO=1:VE=6:GOSUB110:PRINT"tegenovergestelde van"
7450 HO=1:VE=8:GOSUB110:PRINT"machtsverheffen."
7460 HO=1:VE=11:GOSUB110:PRINT"Worteltrekken wordt"
7470 HO=1:VE=13:GOSUB110:PRINT"aangegeven door -V- hetgeen"
7480 HO=1:VE=15:GOSUB110:PRINT"zonder verdere aanduiding"
7490 HO=1:VE=17:GOSUB110:PRINT"de 2e-machtswortel betekent"
7500 GOSUB5120:REMVolgende scherm'
7510 HO=1:VE=2:GOSUB110:PRINT"De andere machten worden als"
7520 PRINT" volgt aangegeven:"
7530 HO=26:VE=5:GOSUB110:PRINT"3"
7540 HO=1:VE=6:GOSUB110:PRINT"de 3e-machts wortel als -V-"
7550 HO=26:VE=8:GOSUB110:PRINT"4"
7560 HO=1:VE=9:GOSUB110:PRINT"de 4e-machts wortel als -V-"
7570 HO=26:VE=11:GOSUB110:PRINT"5"
7580 HO=1:VE=12:GOSUB110:PRINT"de 5e machts wortel als -V-"
7590 HO=1:VE=15:GOSUB110:PRINT"enz,enz,enz."
7600 GOSUB5120:REMVolgende scherm'
7610 HO=1:VE=2:GOSUB110:PRINT"We zullen nu gaan kijken"
7620 HO=1:VE=4:GOSUB110:PRINT"naar de 2e-machts wortel."
7630 HO=1:VE=7:GOSUB110:PRINT"Als gevraagd wordt de wortel"
7640 HO=1:VE=9:GOSUB110:PRINT"uit een getal te trekken"
7650 HO=1:VE=11:GOSUB110:PRINT"b.v.V25(zeg: wortel uit 25)"
7660 HO=1:VE=13:GOSUB110:PRINT"dan moet een getal gezocht"
7670 HO=1:VE=15:GOSUB110:PRINT"worden dat,met zichzelf"
7680 HO=1:VE=17:GOSUB110:PRINT"vermenigvuldigt,25 oplevert"
7690 GOSUB5120:REMVolgende scherm'
7700 HO=1:VE=2:GOSUB110:PRINT"Dus:V25=5 want 5*5=25"
7710 HO=1:VE=4:GOSUB110:PRINT"Evenzo is V4=2 en V49=7 enz."
7720 HO=1:VE=7:GOSUB110:PRINT"Bij deze kleine getallen"
7730 HO=1:VE=9:GOSUB110:PRINT"gaat het vinden van het"
7740 HO=1:VE=11:GOSUB110:PRINT"antwoord vrij eenvoudig."
7750 HO=1:VE=13:GOSUB110:PRINT"Meestal gaat het minder"
7760 HO=1:VE=15:GOSUB110:PRINT"eenvoudig zoals b.v.V56 of"
7770 HO=1:VE=17:GOSUB110:PRINT"V127 of V625 enz."
7780 GOSUB5120:REMVolgende scherm'
7790 HO=1:VE=2:GOSUB110:PRINT"Als voorbeeld gaan we V57"
7800 HO=1:VE=4:GOSUB110:PRINT"bepalen."
7810 HO=1:VE=7:GOSUB110:PRINT"Dit gebeurt in een aantal"
7820 HO=1:VE=9:GOSUB110:PRINT"stappen."
7830 HO=1:VE=11:GOSUB110:PRINT"Het-op papier-uitrekenen"
7840 HO=1:VE=13:GOSUB110:PRINT"van de wortel uit een getal"
7850 HO=1:VE=15:GOSUB110:PRINT"lijkt op een staartdeling."
7860 GOSUB5120:REMVolgende scherm'
7870 HO=1:VE=2:GOSUB110:PRINT"Stap 1:Verdeel 57 vanuit de"
7880 HO=1:VE=4:GOSUB110:PRINT"komma (=. op scherm) naar"
7890 HO=1:VE=6:GOSUB110:PRINT"links en rechts in groepjes"
7900 HO=1:VE=8:GOSUB110:PRINT"van 2 cijfers."
7910 HO=1:VE=11:GOSUB110:PRINT"Antw:57 kan ook geschreven"
7920 HO=1:VE=13:GOSUB110:PRINT"worden als 57.00000000"
7930 HO=1:VE=15:GOSUB110:PRINT"dus verdeling in groepjes"
7940 HO=1:VE=17:GOSUB110:PRINT"geeft:57 . 00 00 00 00"
7950 GOSUB5120:REMVolgende scherm'
7960 HO=10:VE=2:GOSUB110:PRINT"V57.000000=?"
7970 HO=7:VE=3:GOSUB110:PRINT"a*a=__"
7980 HO=1:VE=17:GOSUB110:PRINT"Stap 2:Vul voor a een getal"
7990 HO=1:VE=18:GOSUB110:PRINT"in zodat a*a kleiner is"
8000 HO=1:VE=19:GOSUB110:PRINT"dan 57."
8010 HO=1:VE=20:GOSUB110:PRINT"Antw:a=7 (7*7=49)"
8020 GOSUB9740:REMVolgende  stap'
8030 HO=21:VE=2:GOSUB110:PRINT"7"
8040 HO=7:VE=3:GOSUB110:PRINT"7*7=49"
8050 HO=11:VE=4:GOSUB110:PRINT"____ -"
8060 HO=12:VE=5:GOSUB110:PRINT"8"
8070 HO=1:VE=17:GOSUB110:PRINT"Stap 3:Haal de volgende 2  "
8080 HO=1:VE=18:GOSUB110:PRINT"cijfers aan;deze staan    "
8090 HO=1:VE=19:GOSUB110:PRINT"achter de komma dus plaats"
8100 HO=1:VE=20:GOSUB110:PRINT"een komma in het antwoord."
8110 FORJ=1 TO1000:NEXTJ
8120 HO=14:VE=5:GOSUB110:PRINT"00"
8130 GOSUB9740:REMVolgende  stap'
8140 HO=22:VE=2:GOSUB110:PRINT"."
8150 HO=6:VE=6:GOSUB110:PRINT"da*a= _ __"
8160 HO=1:VE=17:GOSUB110:PRINT"Stap 4:Vul voor d een getal"
8170 HO=1:VE=18:GOSUB110:PRINT"in ter grootte van 2x het  "
8180 HO=1:VE=19:GOSUB110:PRINT"antwoord tot nu toe        "
8190 HO=1:VE=20:GOSUB110:PRINT"Antw:d=14 (2*7)            "
8200 GOSUB9740:REMVolgende  stap'
8210 HO=5:VE=6:GOSUB110:PRINT"14a*a= _ __"
8220 HO=1:VE=17:GOSUB110:PRINT"Stap 5:Vul voor a een getal"
8230 HO=1:VE=18:GOSUB110:PRINT"in zodat 14a*a kleiner is  "
8240 HO=1:VE=19:GOSUB110:PRINT"dan 800                     "
8250 HO=1:VE=20:GOSUB110:PRINT"Antw:a=5 (145*5=725)        "
8260 GOSUB9740:REMVolgende  stap'
8270 HO=23:VE=2:GOSUB110:PRINT"5"
8280 HO=5:VE=6:GOSUB110:PRINT"145*5= 7 25"
8290 HO=12:VE=7:GOSUB110:PRINT"________ -"
8300 HO=14:VE=8:GOSUB110:PRINT"75"
8310 HO=1:VE=17:GOSUB110:PRINT"                            "
8320 HO=1:VE=18:GOSUB110:PRINT"Stap 6:Haal de volgende 2"
8330 HO=1:VE=19:GOSUB110:PRINT"cijfers aan"
8340 HO=1:VE=20:GOSUB110:PRINT"                            "
8350 FORJ=1 TO1000:NEXTJ
8360 HO=16:VE=8:GOSUB110:PRINT"00"
8370 GOSUB9740:REMVolgende  stap'
8380 HO=6:VE=9:GOSUB110:PRINT"da*a=   ____"
8390 HO=1:VE=17:GOSUB110:PRINT"Stap 7:Vul voor d weer een"
8400 HO=1:VE=18:GOSUB110:PRINT"getal in ter grootte van  "
8410 HO=1:VE=19:GOSUB110:PRINT"2x het antwoord tot nu toe"
8420 HO=1:VE=20:GOSUB110:PRINT"(de komma telt hier niet)."
8430 HO=1:VE=21:GOSUB110:PRINT"Antw:d=150 (2*75)          "
8440 GOSUB9740:REMVolgende  stap'
8450 HO=4:VE=9:GOSUB110:PRINT"150a*a=   ____"
8460 HO=1:VE=17:GOSUB110:PRINT"Stap 8:Vul voor a een getal"
8470 HO=1:VE=18:GOSUB110:PRINT"in zodat 150a*a kleiner is"
8480 HO=1:VE=19:GOSUB110:PRINT"dan 7500                   "
8490 HO=1:VE=20:GOSUB110:PRINT"Antw:a=4 (1504*4=6016)     "
8500 HO=1:VE=21:GOSUB110:PRINT"                           "
8510 GOSUB9740:REMVolgende  stap'
8520 HO=24:VE=2:GOSUB110:PRINT"4"
8530 HO=4:VE=9:GOSUB110:PRINT"1504*4=   6016"
8540 HO=14:VE=10:GOSUB110:PRINT"________ -"
8550 HO=14:VE=11:GOSUB110:PRINT"1484"
8560 HO=1:VE=17:GOSUB110:PRINT"                            "
8570 HO=1:VE=18:GOSUB110:PRINT"Stap 9:Haal de volgende 2 "
8580 HO=1:VE=19:GOSUB110:PRINT"cijfers aan."
8590 HO=1:VE=20:GOSUB110:PRINT"                            "
8600 FORJ=1 TO1000:NEXTJ
8610 HO=18:VE=11:GOSUB110:PRINT"00"
8620 GOSUB9740:REMVolgende  stap'
8630 HO=6:VE=12:GOSUB110:PRINT"da*a=   ______"
8640 HO=1:VE=17:GOSUB110:PRINT"Stap 10:Vul voor d een    "
8650 HO=1:VE=18:GOSUB110:PRINT"getal in ter grootte van "
8660 HO=1:VE=19:GOSUB110:PRINT"2x het antwoord tot nu toe."
8670 HO=1:VE=20:GOSUB110:PRINT"Antw:d=1508 (2*754)"
8680 GOSUB9740:REMVolgende  stap'
8690 HO=3:VE=12:GOSUB110:PRINT"1508a*a=   ______"
8700 HO=1:VE=17:GOSUB110:PRINT"Stap 11:Vul voor a een getal"
8710 HO=1:VE=18:GOSUB110:PRINT"in zodat 1508a*a kleiner is "
8720 HO=1:VE=19:GOSUB110:PRINT"dan 148400                  "
8730 HO=1:VE=20:GOSUB110:PRINT"Antw:a=9 (15089*9=135801)   "
8740 GOSUB9740:REMVolgende  stap'
8750 HO=25:VE=2:GOSUB110:PRINT"9"
8760 HO=3:VE=12:GOSUB110:PRINT"15089*9=   135801"
8770 HO=14:VE=13:GOSUB110:PRINT"____________ -"
8780 HO=15:VE=14:GOSUB110:PRINT"12599"
8790 HO=1:VE=17:GOSUB110:PRINT"Op deze wijze kun je zo lang"
8800 HO=1:VE=18:GOSUB110:PRINT"doorgaan tot de rest 0 wordt"
8810 HO=1:VE=19:GOSUB110:PRINT"Als je een antwoord met 2   "
8820 HO=1:VE=20:GOSUB110:PRINT"cijfers na de komma wilt   "
8830 HO=1:VE=21:GOSUB110:PRINT"hoef je niet door te gaan.  "
8840 GOSUB9740:REMVolgende  stap'
8850 HO=1:VE=17:GOSUB110:PRINT"Na afronding is het         "
8860 HO=1:VE=18:GOSUB110:PRINT"antwoord dan: 7.55          "
8870 HO=1:VE=19:GOSUB110:PRINT"Dus V57=7.55              "
8880 HO=1:VE=20:GOSUB110:PRINT"                            "
8890 PRINT"                                             "
8900 GOSUB5120:REMVolgende scherm'
8910 HO=1:VE=3:GOSUB110:PRINT"We zullen  een 2e voorbeeld"
8920 HO=1:VE=5:GOSUB110:PRINT"bekijken.Dit voorbeeld laat,"
8930 HO=1:VE=7:GOSUB110:PRINT"zonder toelichting,stap voor"
8940 HO=1:VE=9:GOSUB110:PRINT"stap de bewerkingen zien."
8950 GOSUB5120:REMVolgende scherm'
8960 HO=10:VE=2:GOSUB110:PRINT"V75076=?"
8970 FORJ=1 TO1000:NEXTJ
8980 HO=7:VE=3:GOSUB110:PRINT"_*_=_"
8990 FORJ=1 TO1000:NEXTJ
9000 HO=17:VE=2:GOSUB110:PRINT"2"
9010 HO=7:VE=3:GOSUB110:PRINT"2*2=4"
9020 HO=11:VE=4:GOSUB110:PRINT"__ -"
9030 FORJ=1 TO1000:NEXTJ
9040 HO=11:VE=5:GOSUB110:PRINT"3"
9050 FORJ=1 TO1000:NEXTJ
9060 HO=12:VE=5:GOSUB110:PRINT"50"
9070 FORJ=1 TO1000:NEXTJ
9080 HO=6:VE=6:GOSUB110:PRINT"4_*_=___"
9090 FORJ=1 TO1000:NEXTJ
9100 HO=18:VE=2:GOSUB110:PRINT"7"
9110 HO=6:VE=6:GOSUB110:PRINT"47*7=329"
9120 HO=11:VE=7:GOSUB110:PRINT"______ -"
9130 FORJ=1 TO1000:NEXTJ
9140 HO=12:VE=8:GOSUB110:PRINT"21"
9150 FORJ=1 TO1000:NEXTJ
9160 HO=14:VE=8:GOSUB110:PRINT"76"
9170 FORJ=1 TO1000:NEXTJ
9180 HO=5:VE=9:GOSUB110:PRINT"54_*_= ____"
9190 FORJ=1 TO1000:NEXTJ
9200 HO=19:VE=2:GOSUB110:PRINT"4"
9210 HO=5:VE=9:GOSUB110:PRINT"544*4= 2176"
9220 HO=12:VE=10:GOSUB110:PRINT"________ -"
9230 HO=15:VE=11:GOSUB110:PRINT"0"
9240 FORJ=1 TO1000:NEXTJ
9250 HO=1:VE=15:GOSUB110:PRINT"De wortel uit 75076 is: 274"
9260 GOSUB5120:REMVolgende scherm'
9270 HO=1:VE=2:GOSUB110:PRINT"Tot nu toe hebben we"
9280 PRINT" alleen naar de 2e-machts wortel"
9290 PRINT" gekeken."
9300 HO=1:VE=6:GOSUB110:PRINT"Ook hogere-machts wortels"
9310 PRINT" kunnen -op papier- berekend worden."
9320 PRINT" Tenzij het om kleine getallen gaat"
9330 PRINT" gaat kunnen deze veel sneller op"
9340 PRINT" een andere manier (b.v.met"
9350 PRINT" logaritmen) berekend worden."
9360 HO=1:VE=13:GOSUB110:PRINT"Daarop zullen we hier"
9370 PRINT" niet verder ingaan."
9380 HO=1:VE=16:GOSUB110:PRINT"Als je dadelijk kiest voor"
9390 PRINT" het rekenen met hogere machten"
9400 PRINT" dan 2 zal ik er voor zorgen dat"
9410 PRINT" het antwoord een geheel getal is."
9420 HO=3:VE=22:GOSUB110:PRINT"Uitleg nog eens (j/n)?"
9430 GOSUB210:A$=IN$
9440 IFA$="S" THEN6410
9450 IFA$="s" THEN6410
9460 IFA$="J" THEN7380
9470 IFA$="j" THEN7380
9480 IFA$="N" THENRETURN
9490 IFA$="n" THENRETURN
9500 GOTO9430
9510 REM
9520 REM Aangeven keuze menu
9530 REM
9540 HO=5:VE=22:GOSUB110:PRINT"                            "
9550 FORI=1 TO5
9560 HO=1:VE=H:GOSUB110:PRINT">>>"
9570 FORJ=1 TO250:NEXTJ
9580 HO=1:VE=H:GOSUB110:PRINT"   "
9590 FORJ=1 TO150:NEXTJ
9600 NEXTI
9610 RETURN
9620 REM
9630 REM Aangeven keuze
9640 REM        voor overige
9650 REM
9660 HO=1:VE=22:GOSUB110:PRINT"                            "
9670 FORI=1 TO5
9680 HO=0:VE=H:GOSUB110:PRINT">>"
9690 FORJ=1 TO250:NEXTJ
9700 HO=0:VE=H:GOSUB110:PRINT"  "
9710 FORJ=1 TO150:NEXTJ
9720 NEXTI
9730 RETURN
9740 REM
9750 REM Volgende stap
9760 REM
9770 HO=1:VE=22:GOSUB110:PRINT"Volgende stap:druk een knop"
9780 GOSUB210
9790 IFIN$="S" THEN6410
9800 IFIN$="s" THEN6410
9810 HO=1:VE=22:GOSUB110:PRINT"                            "
9820 RETURN
9830 REM
9840 REM Grootte van de macht
9850 REM   worteltrekken
9860 GOSUB100:Q$="0"
9870 HO=3:VE=2:GOSUB110:PRINTNA$;","
9880 HO=2:VE=5:GOSUB110:PRINT"je moet nu de grootte van"
9890 PRINT"  de macht kiezen (m.a.w.:hoe vaak"
9900 PRINT"  moet het antwoord met zichzelf"
9910 PRINT"  vermenigvuldigen worden)."
9920 GOSUB5120:REMVolgende scherm'
9930 HO=3:VE=2:GOSUB110:PRINT"Grootte van de macht"
9940 HO=2:VE=4:GOSUB110:PRINT"-1-Macht is 2"
9950 HO=2:VE=6:GOSUB110:PRINT"-2-Macht is 3"
9960 HO=2:VE=8:GOSUB110:PRINT"-3-Macht is 4"
9970 HO=2:VE=10:GOSUB110:PRINT"-4-Macht is 5"
9980 HO=3:VE=22:GOSUB110:PRINTNA$;",wat kies je?"
9990 GOSUB210:Q$=IN$
10000 IFQ$="S" THEN6410
10010 IFQ$="s" THEN6410
10020 IFQ$="1" THENH=4:GOSUB9620:Q=2:GOTO10070
10030 IFQ$="2" THENH=6:GOSUB9620:Q=3:GOTO10070
10040 IFQ$="3" THENH=8:GOSUB9620:Q=4:GOTO10070
10050 IFQ$="4" THENH=10:GOSUB9620:Q=5:GOTO10070
10060 GOTO9990
10070 RETURN
10080 DATA"0","1","2","3","4","5","6","7","8","9"
10090 REM
10100 REM REKENFRIEK
10110 REM
10120 REM  ONTWERP (c)
10130 REM J.A.v.ENGELAND
10140 REM  ALMERE
10150 REM
10160 REM OP EEN MSX-COMPUTER
10170 REM  PHILIPS VG-8020
10180 REM
10190 REM  FEBRUARI 1987
10200 REM *********************
30000 REM COPYRIGHT NOS HILVERSUM

1000 A=2000:GOTO 20:REM TEACHER
1010 GOSUB 100:PRINT "     EVEN DE MAAT NEMEN."
1020 DIM SP$(2,14),TA$(255,1),SC(14,4):CT=5:CN=1:VE=1
1030 HO=0:FOR Y=0 TO 99:VE=Y:GOSUB 110:GOSUB 120
1040 IF VE=Y THEN PRINT ".";:NEXT Y
1050 FOR X=0 TO 99:HO=X:GOSUB 110:GOSUB 120
1060 IF HO=X THEN PRINT ".";:NEXT X
1070 GOSUB 100:X=X-1:Y=Y-1:BL$="":FOR I=1 TO X:BL$=BL$+" "
1075 NEXT I
1080 D$=" DRUK EEN TOETS ":GOSUB 100
1090 PRINT "INSTRUCTIES ? (J/N)";:GOSUB 210:GOSUB 100
1100 IF IN$="J" THEN GOSUB 2350
1110 PL=Y-10:PRINT "HOEVEEL SPELERS ?":HO=0:VE=2
1120 GOSUB 110:PRINT "1-";PL;" :";:INPUT IN$
1130 IN=VAL(IN$):IF (IN<1) OR(IN>PL) THEN 1120
1140 PL=IN:FOR I=1 TO PL:N$=""
1150 PRINT "NAAM SPELER":PRINT "NO "I;:INPUT N$
1160 SP$(0,I)=LEFT$(N$,10):IF N$="" THEN I=I-1
1170 NEXT I:GOSUB 100:PRINT "EERST ZELF EVEN DE WOORDEN LEREN"
1180 FOR I=1 TO 255:READ I$:PRINT I$;" ";
1190 IF I$<>"" THEN TA$(I,0)=I$:READ TA$(I,1):NEXT I
1200 AA=I-1:AA$="":FOR I=1 TO AA:AA$=AA$+CHR$(I):NEXT I
1210 GOSUB 100
1220 PRINT "HOEVEEL WOORDEN WIL JE LEREN (1-10)"
1230 HO=0:VE=2:GOSUB 110:INPUT IN$:LE=VAL(IN$)
1240 IF LE>10 THEN PRINT "MAX. 10":GOTO 1230
1250 IF LE<1 THEN PRINT "TEN MINSTE 1 WOORD":GOTO 1230
1260 PRINT "DE ";LE;" WOORDEN WORDEN NU GEKOZEN VOOR:"
1270 FOR J=1 TO PL:I$=AA$:SP$(1,J)=""
1280 PRINT SP$(0,J);
1290 FOR I=1 TO LE:GOSUB 1980:SP$(1,J)=SP$(1,J)+U$
1300 PRINT ".";:NEXT I:SP$(2,J)=SP$(1,J)
1310 PRINT :NEXT J:GOSUB 2270
1320 GOSUB 100:GA=0:GOSUB 2070
1330 GA=GA+1:VE=0:HO=0:GOSUB 110:PRINT "SPEL:";GA;
1340 FOR Q=1 TO PL:IF LEN(SP$(2,Q))>0 THEN GOSUB 1450
1350 NEXT Q
1360 REM ZIJN NOG NIET ALLE SPELERS KLAAR?
1370 FOR I=1 TO PL:IF LEN(SP$(2,I))>0 THEN I=PL:NEXT I:GOTO 1330
1380 NEXT I:HO=0:VE=Y-5:GOSUB 110
1390 PRINT "ALLE WOORDEN ZIJN GELEERD"
1400 PRINT "WIL JE NOG EEN KEER (J/N)":INPUT IN$
1410 IF IN$="J" THEN 1210
1420 GOTO 3170:REM EINDE
1430 REM 
1440 REM HIER BEGINT SPEL PER SPELER
1450 VE=Y-6:HO=0:GOSUB 110
1460 FOR I=1 TO INT(X/LEN(SP$(0,Q))):PRINT SP$(0,Q);:NEXT I
1470 REM SOMS HERHALEN ALS DE HELFT GELEERD IS
1480 IF LEN(SP$(2,Q))>LEN(SP$(1,Q))/2 THEN 1510
1490 REM 20% KANS OP HERHALING
1500 GOSUB 260:IF RV<.2 THEN I$=SP$(1,Q):GOSUB 1980:GOTO 1520
1510 I$=SP$(2,Q):GOSUB 1980:SP$(2,Q)=I$
1520 BW$=U$:GOSUB 260:TD$="NED:":TR$="ENG:"
1530 REM MEESTAL NED >> ENG.
1540 IF RV<.7 THEN RA$=TA$(RR,0):DY$=TA$(RR,1):GOTO 1560
1550 RA$=TA$(RR,1):DY$=TA$(RR,0):TD$="ENG:":TR$="NED:"
1560 VE=Y-5:HO=(X/2)-8:GOSUB 110:PRINT "100.0 %  FOUT :0";
1570 L=LEN(RA$):T=L:IF LEN(DY$)>T THEN T=LEN(DY$)
1580 F=0:TB=INT((X-T-4)/2)
1590 HO=TB:VE=Y-4:GOSUB 110:PRINT TD$;DY$;
1600 REM LAAT SOMS HET GOEDE WOORD VERWARD ZIEN
1610 GOSUB 260:IF RV<.7 THEN 1670
1620 REM RA$ DOOR ELKAAR GOOIEN ; VW$ IS RESULTAAT
1630 I$=RA$:VW$="":FOR I=1 TO L:GOSUB 1980:VW$=VW$+U$:NEXT I
1640 IF VW$=RA$ THEN 1630
1650 REM ZET HET VERWARDE WOORD OP SCHERM
1660 VE=Y-3:GOSUB 110:PRINT TR$;VW$;
1670 VE=Y-2:HO=TB:GOSUB 110:PRINT TR$;
1680 FOR I=1 TO L:PRINT ".";:NEXT I
1690 REM 
1700 REM MAAK TOETSENBORD-BUFFER LEEG
1710 GOSUB 200:IF IN$<>"" THEN 1710
1720 REM HAAL DE LETTERS 1 VOOR 1 BINNEN
1730 SR=100:F=0:FOR I=1 TO L
1740 HO=TB+3+I:VE=Y-2:GOSUB 210
1750 GOSUB 110:PRINT MID$(RA$,I,1);
1760 HO=HO-1:VE=Y-1:GOSUB 110:PRINT " ";IN$"=";
1770 REM LETTER IS GOED
1780 IF IN$=MID$(RA$,I,1) THEN PRINT "GOED";:GOTO 1840
1790 REM 
1800 REM LETTER IS FOUT
1810 PRINT "FOUT";
1820 HO=(X/2)-8:VE=Y-5:F=F+1:SR=100-(100*(F/L))
1830 GOSUB 310:GOSUB 110:PRINT SR$;" %  FOUT:";F;
1840 NEXT I
1850 REM ALLE LETTERS ZIJN BINNEN
1860 IF (F=0) OR(LEN(SP$(2,Q))=255) THEN 1900
1870 REM NIET FOUTLOOS? DAN LATER NOG EENS
1880 SP$(2,Q)=SP$(2,Q)+BW$
1890 REM SCORE BIJWERKEN
1900 SC(Q,2)=(SC(Q,2)*(GA-1)+SR)/GA:SC(Q,1)=SC(Q,1)+F
1910 IF SR>SC(Q,3) THEN SC(Q,3)=SR
1920 IF SR<SC(Q,4) THEN SC(Q,4)=SR
1930 VE=Y:HO=TB:GOSUB 110:PRINT D$;:GOSUB 210
1940 IF IN$="*" THEN GOSUB 2040:RETURN
1950 GOSUB 2180:GOSUB 2320:RETURN:REM EINDE SPEL PER SPELER
1960 REM 
1970 REM HAAL 1 CHARACTER UIT I$
1980 GOSUB 260:R=INT(RV*LEN(I$))+1
1990 U$=MID$(I$,R,1):RR=ASC(U$)
2000 I$=MID$(I$,1,R-1)+MID$(I$,R+1,LEN(I$))
2010 RETURN
2020 REM 
2030 REM SCHERM HERSTELLEN
2040 QQ=Q:GOSUB 100:VE=0:HO=0:GOSUB 110:PRINT "SPEL:";GA;
2050 GOSUB 2070:FOR Q=1 TO PL:GOSUB 2180:NEXT Q:Q=QQ:RETURN
2060 REM HEADING
2070 VE=0:HO=11:GOSUB 110:PRINT "WOOR- LET-";
2080 VE=1:GOSUB 110:PRINT "DEN   TERS";
2090 VE=2:HO=0:GOSUB 110:PRINT "NAAM       OVER  FOUT";
2100 IF X<39 THEN 2150
2110 HO=22:VE=0:GOSUB 110:PRINT "GEM.  MAX.  MIN.";
2120 VE=1:GOSUB 110:PRINT "SCORE SCORE SCORE";
2130 VE=2:GOSUB 110:PRINT "IN %  IN %  IN %";
2140 HO=0:FOR I=1 TO PL:VE=I+3:GOSUB 110:PRINT SP$(0,I);:NEXT I
2150 RETURN
2160 REM 
2170 REM ZET SCORE OP SCHERM
2180 HO=11:VE=Q+3:GOSUB 110:PRINT LEN(SP$(2,Q));
2190 HO=17:GOSUB 110:PRINT SC(Q,1);
2200 IF X<39 THEN 2240
2210 FOR I=2 TO 4
2220 SR=SC(Q,I):GOSUB 310:HO=I*6+10:GOSUB 110:PRINT SR$;
2230 NEXT I
2240 RETURN
2250 REM 
2260 REM ALLE SCORES VOOR HET SPEL INSTELLEN
2270 FOR J=1 TO PL:FORI=2 TO4 STEP 2:SC(J,I)=100:NEXT I:NEXT J
2280 FOR J=1 TO PL:FORI=1 TO3 STEP 2:SC(J,I)=0:NEXT I:NEXT J
2290 RETURN
2300 REM 
2310 REM ONDERHELFT SCHERM SCHOONMAKEN
2320 VE=Y-6:FOR HO=X TO 0 STEP -1:GOSUB 110:PRINT " ";:NEXT HO
2330 HO=0:FOR VE=Y-6 TO Y:GOSUB 110:PRINT BL$;:NEXT VE
2340 RETURN
2350 GOSUB 100:PRINT "T E A C H E R"
2360 PRINT "DE BEDOELING VAN DIT PROGRAMMA IS"
2370 PRINT "OM DE ENGELSE TAAL TE LEREN."
2380 PRINT "NA DE VRAGEN OVER HET AANTAL"
2390 PRINT "DEELNEMERS EN HUN NAMEN KIES JE"
2400 PRINT "HOEVEEL WOORDEN JE WILT LEREN."
2410 PRINT "MAXIMAAL 10 (OM HET LEUK TE HOUDEN)"
2420 PRINT "VOOR IEDER WORDEN ANDERE WOORDEN"
2430 PRINT "UITGEKOZEN."
2440 PRINT "OP HET SCHERM KOMEN DIVERSE SCORES"
2450 PRINT "DIE VOOR ZICH SPREKEN. OP DE"
2460 PRINT "ONDERSTE HELFT VAN HET SCHERM KOMT"
2470 PRINT "HET TE VERTALEN WOORD IN HET ENGELS"
2480 PRINT "OF NEDERLANDS (MEESTAL NEDERLANDS)"
2490 PRINT "**";D$;" **";:GOSUB 210:GOSUB 100
2500 PRINT "DAARONDER STAAT SOMS"
2510 PRINT "HET TE RADEN WOORD DOOR ELKAAR."
2520 PRINT "EN DAARONDER ALTIJD EEN RIJ PUNTJES."
2530 PRINT "HET AANTAL GEEFT AAN HOE LANG HET"
2540 PRINT "WOORD MOET WORDEN. JE TYPT DAN DE"
2550 PRINT "EERSTE LETTER VAN HET WOORD. ONDER"
2560 PRINT "DE PUNTJES KOMT TE STAAN OF DE LETTER"
2570 PRINT "GOED OF FOUT WAS, TEVENS VERSCHIJNT"
2580 PRINT "ALTIJD DE GOEDE LETTER OP HET STIPJE."
2590 PRINT "ALS ALLE LETTERS ZIJN GETYPT MOET JE"
2600 PRINT "OP EEN TOETS DRUKKEN, DAN KAN DE"
2610 PRINT "VOLGENDE DEELNEMER. ALS JE DAAR EEN"
2620 PRINT "STER:* TYPT DAN WORDT HET SCHERM"
2630 PRINT "HERSTELD ALS HET OM EEN REDEN"
2640 PRINT "DOORELKAAR WAS GEKOMEN."
2650 PRINT "**";D$;" **";:GOSUB 210:GOSUB 100
2660 PRINT "VOOR DE WOORDEN STAAT IN WELKE"
2670 PRINT "TAAL HET IS."
2680 PRINT "ALS JE EEN FOUT HEBT GEMAAKT KRIJG"
2690 PRINT "JE HET WOORD LATER NOG EENS. NADAT"
2700 PRINT "JE DE HELFT VAN HET AANTAL WOORDEN"
2710 PRINT "GOED HEBT HEB JE EEN KANSJE OP EEN"
2720 PRINT "HERHALING. ZIJN ALLE WOORDEN GOED DAN"
2730 PRINT "BEN JE KLAAR EN GAAN DE ANDEREN NOG"
2740 PRINT "EVEN DOOR TOT IEDEREEN KLAAR IS."
2750 PRINT "ALS IEDEREEN KLAAR IS KUN JE EVT."
2760 PRINT "WEER VERDER GAAN MET NIEUWE WOORDEN."
2770 PRINT "**";D$;" **";:GOSUB 210:GOSUB 100
2780 PRINT "DOOR DE DATA REGELS TE VERANDEREN"
2790 PRINT "KUN JE MET DIT PROGRAMMA BV. OOK"
2800 PRINT "PROVINCIES OF LANDEN MET HUN"
2810 PRINT "HOOFDSTEDEN LEREN.":PRINT 
2820 PRINT "VEEL PLEZIER":PRINT 
2830 PRINT "JAN COLLY - VEENDAM":PRINT 
2840 PRINT "**";D$;" **";:GOSUB 210:GOSUB 100
2850 RETURN
2860 DATA "TREE","BOOM","ROSE","ROOS","FISH","VIS"
2870 DATA "ROOM","KAMER","KITCHEN","KEUKEN"
2880 DATA "HOUSE","HUIS","MOUSE","MUIS","FIRE","VUUR"
2890 DATA "TEN","TIEN","NINE","NEGEN","EIGHT","ACHT"
2900 DATA "SEVEN","ZEVEN","SIX","ZES","FIVE","VIJF"
2910 DATA "FOUR","VIER","THREE","DRIE","TWO","TWEE"
2920 DATA "ONE","EEN","ZERO","NUL","BIRD","VOGEL"
2930 DATA "CAT","KAT","DOG","HOND","HORSE","PAARD"
2940 DATA "YES","JA","NO","NEE"
2950 DATA "ROAD","WEG","OFF","WEG"
2960 DATA "OF","VAN","FROM","VAN"
2970 DATA "STRANGE","VREEMD","HEAVY","ZWAAR"
2980 DATA "MORNING","MORGEN","GRASS","GRAS"
2990 DATA "YELLOW","GEEL","BLUE","BLAUW","RAIN","REGEN"
3000 DATA "LEFT","LINKS","RIGHT","RECHTS","POOR","ARM"
3010 DATA "PAPER","PAPIER","NEWSPAPER","KRANT"
3020 DATA "WINDOW","RAAM","BOTTLE","FLES","LITTLE","KLEIN"
3030 DATA "BIG","GROOT","PLATE","BORD","SPOON","LEPEL"
3040 DATA "COW","KOE","GARDEN","TUIN","COAT","JAS"
3050 DATA "HEAD","HOOFD","HAT","HOED","CHAIR","STOEL"
3060 DATA "TABLE","TAFEL","LAMP","LAMP","RADIO","RADIO"
3070 DATA "SHOE","SCHOEN","CAR","AUTO","BIKE","FIETS"
3080 DATA "BOOK","BOEK","SCHOOL","SCHOOL"
3090 DATA "TEACHER","LERAAR","BREAD","BROOD"
3100 DATA "CHEESE","KAAS","MILK","MELK","WATER","WATER"
3110 DATA "EGG","EI","POTATO","AARDAPPEL","MEAT","VLEES"
3120 DATA "PENCIL","POTLOOD"
3130 DATA "KNIFE","MES","FORK","VORK","GOOD","GOED"
3140 DATA "BAD","SLECHT","FOOTBALL","VOETBAL"
3150 DATA "FLOWER","BLOEM","BUTTERFLY","VLINDER"
3160 DATA ""
3170 END
3180 REM TEACHER IS GEMAAKT OP EEN TRS-80 MOD.1 LEV.2
3190 REM DOOR JAN COLLY GRONINGENLAAN 36
3200 REM 9642EJ VEENDAM  TEL 05987-16140
30000 REM COPYRIGHT NOS HILVERSUM

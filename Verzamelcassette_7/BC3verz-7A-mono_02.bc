
1000 A=300:GOTO20:REM "M U N T E N "
1010 DIMS(20):HK=3.1416/8:CN=0:PRINT"MOMENT"
1020 FORN=0 TO20:S(N)=SIN(N*HK):NEXTN
1030 REM --- inleiding ---
1040 GOSUB100:SR$=" M U N T E N ":GOSUB150:PRINT:PRINT
1050 PRINT"We gaan samen GELD TELLEN":PRINT
1060 PRINT"1 = alleen dubbeltjes":PRINT
1070 PRINT"2 = dubbeltjes en stuivers":PRINT
1080 PRINT"3 = 1 kwartje, dubbeltjes en stuivers":PRINT
1090 PRINT"4 = kwartjes, dubbeltjes en stuivers":PRINT
1100 PRINT"5 =  5 munten, door elkaar":PRINT
1110 PRINT"6 = 10 munten, door elkaar":PRINT
1120 PRINT"7 = 15 munten, door elkaar":PRINT
1130 PRINT"          0 = stoppen":PRINT
1140 PRINT"WAT KIES JE?":PRINT
1150 PRINT"Tik een cijfer (0 - 7) : ";
1160 GOSUB210:IF(IN<48)OR(IN>55) THEN1160
1170 MG=VAL(IN$):PRINTMG:GOSUB2090
1180 REM >>> H O O F D L U S <<<
1190 FT=0
1200 ONMG+1 GOTO950,1220,1250,1290,1330,1370,1370,1370
1210 REM --- alleen dubbeltjes ---
1220 M$="44444":V$="2":GOSUB2110
1230 M$=M$+G$:GOSUB2110:M$=M$+G$:GOTO1410
1240 REM --- dubbeltjes en stuivers ---
1250 V$="2":GOSUB2110:M$=G$
1260 V$="1":GOSUB2110:M$=M$+G$
1270 V$="1":GOSUB2110:M$=M$+G$:GOTO1410
1280 REM --- 1 kwartje, dubb. en st. ---
1290 V$="2":GOSUB2110:M$=G$
1300 V$="1":GOSUB2110:M$=M$+G$
1310 M$=M$+"3":GOTO1410
1320 REM --- kwartjes, dubb. en st. ---
1330 V$="1":GOSUB2110:M$=G$
1340 V$="2":GOSUB2110:M$=M$+G$
1350 V$="3":GOSUB2110:M$=M$+G$:GOTO1410
1360 REM --- alles door elkaar, 5, 10 of 15 ---
1370 M$="":FORN=1 TO(MG-4)*5
1380 GOSUB260:SR=1+INT(RV*3):GOSUB300
1390 M$=M$+SR$:NEXTN:GOTO1410
1400 REM --- teken de munten ---
1410 GOSUB600
1420 XC=.1:YC=.12:WS=0:WD=0:WK=0:NS=0:ND=0:NK=0
1430 FORV=1 TOLEN(M$):W=VAL(MID$(M$,V,1))
1440 ONW GOSUB1820,1860,1900,1840
1450 XC=XC+.2:IFXC>1 THENXC=.1:YC=YC+.25
1460 NEXTV
1470 REM --- vraag en antwoord --
1480 WT=WS+WD+WK
1490 SR$="VUL IN:":HO=0:VE=.75:GOSUB650
1500 SR$="stuiver":IFNS=0 THEN1560
1510 IFNS>1 THENSR$="stuivers"
1520 HO=.35:VE=.75:GOSUB650:REM NS WS
1530 VR=NS:XV=.22:YV=VE:GOSUB2150
1540 GOSUB2310
1550 VR=WS:XV=.75:YV=VE:GOSUB2150
1560 SR$="dubbeltje":IFND=0 THEN1620
1570 IFND>1 THENSR$="dubbeltjes"
1580 HO=.35:VE=.81:GOSUB650:REM ND WD
1590 VR=ND:XV=.22:YV=VE:GOSUB2150
1600 GOSUB2310
1610 VR=WD:XV=.75:YV=VE:GOSUB2150
1620 SR$="kwartje":IFNK=0 THEN1680
1630 IFNK>1 THENSR$="kwartjes"
1640 HO=.35:VE=.87:GOSUB650:REM NK WK
1650 VR=NK:XV=.22:YV=VE:GOSUB2150
1660 GOSUB2310
1670 VR=WK:XV=.75:YV=VE:GOSUB2150
1680 IFMG=1 THEN1740
1690 HO=.35:VE=.92:GOSUB620:HO=.95:GOSUB630
1700 SR$="TOTAAL:":HO=.35:VE=.94:GOSUB650:REM WT
1710 XV=.25:YV=VE:GOSUB2310
1720 VR=WT:XV=.75:YV=VE:GOSUB2150
1730 CN=1:SR$="VUL IN:":HO=0:VE=.75:GOSUB650:CN=0
1740 SR$="GOED ZO":HO=0:VE=.81:GOSUB650
1750 SR$="Nog'ns?":HO=0:VE=.87:GOSUB650
1760 SR$="Tik J/N":HO=0:VE=.94:GOSUB650
1770 GOSUB210:IFIN=74 THEN1190
1780 IFIN=78 THEN1000
1790 GOTO1770
1800 REM-----------------------------------
1810 REM == sbr. teken stuiver == XC,YC / NS,WS
1820 NS=NS+1:WS=WS+5:RC=.026:GOSUB1940
1830 C$="177020236375673726":GOSUB1990
1840 RETURN
1850 REM == sbr. teken dubbeltje == XC,YC / ND,WD
1860 ND=ND+1:WD=WD+10:RC=.018:GOSUB1940
1870 C$="221221278425161727667574642":GOSUB1990
1880 RETURN
1890 REM == sbr. teken kwartje == XC,YC / NK,WK
1900 NK=NK+1:WK=WK+25:RC=.022:GOSUB1940
1910 C$="260211213233063678151537384857656":GOSUB1990
1920 RETURN
1930 REM == sbr. teken cirkel == XC,YC,RC
1940 HO=XC+3*RC:VE=YC:GOSUB620
1950 FORM=1 TO16:HO=XC+3*RC*S(M+4):VE=YC-4*RC*S(M)
1960 GOSUB630:NEXTM
1970 RETURN
1980 REM == sbr. teken cijfers == C$,XC,YC
1990 XX=XC-.045:YY=YC-.035
2000 K=VAL(MID$(C$,1,1)):P=2
2010 FORL=1 TOK:M=VAL(MID$(C$,P,1)):P=P+1
2020 HO=XX+.01*VAL(MID$(C$,P,1)):P=P+1
2030 VE=YY+.01*VAL(MID$(C$,P,1)):P=P+1:GOSUB620
2040 FORR=1 TOM
2050 HO=XX+.01*VAL(MID$(C$,P,1)):P=P+1
2060 VE=YY+.01*VAL(MID$(C$,P,1)):P=P+1:GOSUB630
2070 NEXTR:NEXTL:RETURN
2080 REM == sbr. wacht even ==
2090 SD=10:SV=0:SP=0:GOSUB400:RETURN
2100 REM == sbr. munten per regel ==  V$ / G$
2110 G$="":GOSUB260:M=1+INT(RV*5)
2120 FORN=1 TOM:G$=G$+V$:NEXTN:IFM=5 THENRETURN
2130 FORN=1 TO(5-M):G$=G$+"4":NEXTN:RETURN
2140 REM == sbr. neem antwoord in == VR XV YV
2150 AW$="":IFVR<100 THENAW$=" "
2160 IFVR<10 THENAW$="  "
2170 GOSUB210:SR=VAL(IN$):GOSUB300:AW$=AW$+SR$:AW=VAL(SR$)
2180 GOSUB2290:IFVR<10 THEN2240
2190 GOSUB210:SR=VAL(IN$):GOSUB300
2200 AW$=AW$+SR$:AW=10*AW+VAL(SR$)
2210 GOSUB2290:IFVR<100 THEN2240
2220 GOSUB210:SR=VAL(IN$):GOSUB300
2230 AW$=AW$+SR$:AW=10*AW+VAL(SR$)
2240 GOSUB2290:IFAW=VR THENRETURN
2250 CN=1:GOSUB2290:CN=0:IFFT<25 THENFT=FT+1
2260 HO=.99:VE=FT/30:GOSUB620
2270 GOTO2150
2280 REM == sbr. print antwoord ==
2290 HO=XV:VE=YV:SR$=AW$:GOSUB650:RETURN
2300 REM == sbr. print = cent ==
2310 HO=.7:VE=YV:SR$="=":GOSUB650
2320 HO=.9:VE=YV:SR$="cent":GOSUB650:RETURN
2330 REM----------------------------------
32000 REM Ch.W.Brederode
32010 REM Von Bonninghausenlaan 33
32020 REM 2161 ES  LISSE
32030 REM
32040 REM TROS-RADIO dd 891122

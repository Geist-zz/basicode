
1000 A=5000:GOTO20:REM MUZIEKCASSETTES
1010 DIMA$(2,2),N$(2,12),P$(7),T$(7)
1020 FORX=1 TO7:READP$(X),T$(X):NEXTX
1030 REM
1040 REM INVOEREN ********************
1050 REM TITEL/ARTIEST ***************
1060 FORX=1 TO2
1070 GOSUB2040:REM KOP
1080 PRINT"Invoeren":PRINT"--------"
1090 IFX=1 THENPRINT"Kant:A"
1100 IFX=2 THENPRINT"Kant:B"
1110 HO=0:VE=6:GOSUB110
1120 PRINT"Titel---  ";A$(X,1)
1130 HO=8:VE=6:GOSUB110:INPUTA$(X,1)
1140 PRINT"Artiest-  ";A$(X,2)
1150 HO=8:VE=7:GOSUB110:INPUTA$(X,2)
1160 PRINT
1170 REM SONGS ***********************
1180 FORY=1 TO12
1190 HO=0:VE=Y+8:GOSUB110
1200 CT=2:CN=0:SR=Y:GOSUB310
1210 PRINT"Song ";SR$;"-  ";N$(X,Y)
1220 HO=8:VE=Y+8:GOSUB110
1230 INPUTN$(X,Y)
1240 IFLEN(N$(X,Y))>29 THEN1190
1250 NEXTY
1260 GOSUB1970:IFQ=2 THEN1110
1270 NEXTX
1280 REM KEUZE PRINTER ***************
1290 GOSUB2040:REM KOP
1300 PRINT"Keuze printer"
1310 PRINT"-------------"
1320 FORX=1 TO7:PRINT" ";P$(X):NEXTX
1330 PRINT" Overig"
1340 PRINT" Terug naar invoeren"
1350 PRINT" Stoppen":PRINT
1360 SR$="Maak keuze mbv cursor en return":GOSUB150
1370 HO=0:V=4
1380 VE=V:GOSUB110:PRINT">";
1390 GOSUB210
1400 IFIN=13 THEN1440
1410 IFIN=30 THENV=V+1:IFV>13 THENV=4
1420 IFIN=31 THENV=V-1:IFV<4 THENV=13
1430 GOSUB110:PRINT" ":GOTO1380
1440 IFV=13 THEN950
1450 IFV=12 THEN1060
1460 IFV=11 THEN1880
1470 IFV<11 THENT=VAL(LEFT$(T$(V-3),2))
1480 REM PRINTEN *********************
1490 SR$="":U$=T$(V-3)
1500 FORX=1 TO(LEN(U$))/2-1
1510 SR$=SR$+CHR$(VAL(MID$(U$,X*2+1,2)))
1520 NEXTX
1530 GOSUB350:GOSUB360
1540 FORX=1 TO4*T
1550 SP$=SP$+"-":NEXTX:SR$=SP$+"<knippen"
1560 GOSUB350:GOSUB360
1570 GOSUB360:GOSUB360:GOSUB360
1580 SR$=SP$+"<vouwen":GOSUB350:GOSUB360
1590 SR$=". "+A$(1,1)+" ("+A$(1,2)+")"
1600 GOSUB350:GOSUB360
1610 SR$=".."+A$(2,1)+" ("+A$(2,2)+")"
1620 GOSUB350:GOSUB360
1630 SR$=SP$+"<vouwen":GOSUB350:GOSUB360
1640 SR$="A:"+A$(1,1)
1650 FORX=1 TO2*T-LEN(A$(1,1))-2
1660 SR$=SR$+" "
1670 NEXTX
1680 SR$=SR$+"B:"+A$(2,1)
1690 GOSUB350:GOSUB360
1700 REM
1710 SR$="  ("+A$(1,2)+")"
1720 FORX=1 TO2*T-LEN(A$(1,2))-2
1730 SR$=SR$+" "
1740 NEXTX
1750 SR$=SR$+"("+A$(2,2)+")"
1760 GOSUB350:GOSUB360
1770 REM
1780 FORX=1 TO12
1790 SR$="  "+N$(1,X)
1800 FORY=1 TO2*T-LEN(N$(1,X)):SR$=SR$+" ":NEXTY
1810 SR$=SR$+N$(2,X)
1820 GOSUB350:GOSUB360
1830 NEXTX
1840 SR$=SP$+"<knippen":GOSUB350:GOSUB360
1850 REM
1860 GOTO1290
1870 REM OVERIGE PRINTERS ************
1880 HO=0:VE=17:GOSUB110
1890 PRINT"Stel de printer in op het kleinst"
1900 PRINT"mogelijk lettertype."
1910 PRINT"Bijv. ELITE en/of CONDENSED"
1920 PRINT
1930 PRINT"Hoeveel tekens per inch ";:INPUTT
1940 IF(T<10)OR(T>20) THEN1880
1950 GOTO1540
1960 REM KOP *************************
1970 HO=10:VE=22:GOSUB110
1980 SR$="Correct [J/N]":GOSUB150
1990 GOSUB250:GOSUB210
2000 IF(IN=74) THENQ=1:RETURN
2010 IF(IN=78) THENQ=2:RETURN
2020 GOTO1990
2030 REM
2040 GOSUB100
2050 HO=9:VE=0:GOSUB110
2060 SR$="MUZIEKCASSETTES":GOSUB150
2070 HO=0:VE=2:GOSUB110
2080 RETURN
25000 DATA"Brother CE50BT","15273109"
25010 DATA"Epson","1715"
25020 DATA"IBM","1715"
25030 DATA"Juki 6100","15273109"
25040 DATA"OKI-mate 20","1715"
25050 DATA"Seikosha SP1000MX (MSX)","172781"
25060 DATA"Seikosha SP800","1715"
30000 REM ****************************
30010 REM *     MUZIEKCASSETTES      *
30020 REM *   Maak uw eigen labels!  *
30030 REM *    (c) JBSoft,Alkmaar    *
30040 REM ****************************
30050 REM
32000 REM Gemaakt door:
32010 REM Jan Benjamin
32020 REM Stationstraat 26
32030 REM 1815 TC ALKMAAR
32040 REM (072-123809)
32050 REM Op een:
32060 REM Philips VG8020 MSX-1 Computer
32070 REM ****************************
32080 REM
32090 REM TROS-RADIO dd 891227

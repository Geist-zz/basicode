
1000 A=1000:GOTO20:REM MAZERUNNER UIT AD
1010 GOTO4010
1020 REM >>> PR=32 <<<
1030 HO=X:VE=Y:GOSUB110:PRINT"!":MZ(Y,X)=33
1040 RETURN
1050 REM >>> PR<43 <<<
1060 Y=DY:X=DX-1:MZ(Y,X)=32
1070 HO=X:VE=Y:GOSUB110:PRINT" "
1080 RETURN
1200 REM ***> RUNNER RUNT <***
1210 FT=0:DX=2:DY=21:X=DX:Y=DY
1220 IFX=1 THENFT=1
1230 IF(X=1)AND(Y<20) THEN1600
1240 DX=X:DY=Y
1250 REM ***> PR=32  ( SPATIE ) ? <***
1260 REM >>> OMHOOG <<<
1270 Y=DY-1:X=DX:PR=MZ(Y,X)
1280 IFPR=62 THEN1560:REM UITWEG GEVONDEN
1290 IFPR<>32 THEN1320
1300 GOSUB1030:GOTO1220
1310 REM >>> RECHTS <<<
1320 Y=DY:X=DX+1:PR=MZ(Y,X)
1330 IFPR=62 THEN1560:REM UITWEG GEVONDEN
1340 IFPR<>32 THEN1370
1350 GOSUB1030:GOTO1220
1360 REM >>> OMLAAG <<<
1370 Y=DY+1:X=DX:PR=MZ(Y,X):IFPR<>32 THEN1400
1380 GOSUB1030:GOTO1220
1390 REM >>> LINKS <<<
1400 Y=DY:X=DX-1:PR=MZ(Y,X):IFPR<>32 THEN1430
1410 GOSUB1030:GOTO1220
1420 REM ***> PR=43 (+) ? <***
1430 Y=DY:X=DX-1:PR=MZ(Y,X):IFPR<>43 THEN1470
1440 Y=DY-1:X=DX:GOSUB1030:DY=Y:DX=X
1450 REM ***> PR<43 ? <***
1460 REM >>> LINKS <<<
1470 Y=DY:X=DX-1:PR=MZ(Y,X):IFPR<43 THEN GOSUB1060
1480 REM >>> OMHOOG <<<
1490 Y=DY-1:IFY=0 THENFT=2:GOTO1600
1500 X=DX:PR=MZ(Y,X):IFPR<43 THEN GOSUB1060
1510 REM >>> OMLAAG <<<
1520 Y=DY+1:X=DX:PR=MZ(Y,X):IFPR<43 THEN GOSUB1060
1530 REM >>> RECHTS <<<
1540 Y=DY:X=DX+1:PR=MZ(Y,X):IFPR<43 THEN GOSUB1060
1550 Y=DY:X=DX:GOTO1220
1560 FT=0:HO=HT-4:VE=2:GOSUB110
1570 UT$="UIT !":PRINTUT$;
1580 X=HT-5:FORLE=1 TOLEN(UT$)
1590 MZ(2,X+LE)=ASC(MID$(UT$,LE,1)):NEXTLE
1600 RETURN
1700 REM ***> DOOLHOF OPSLAAN <***
1710 VR$="Voer de naam in :":GOSUB6110
1720 HO=19:GOSUB110:INPUTNF$
1730 LE=LEN(NF$):IFLE<8 THEN1760
1740 VR$="Deze naam is te lang. Druk toets in."
1750 GOSUB250:GOSUB6110:GOSUB210:GOTO1710
1760 VR$="Op (C)assette of (D)iskette  C/D ?"
1770 GOSUB6110
1780 GOSUB210
1790 IFIN=67 THENNF=1:GOTO1820
1800 IFIN=68 THENNF=5:GOTO1820
1810 GOSUB250:GOTO1780
1820 VR$="Even geduld a.u.b.":GOSUB6110
1830 FORX=0 TOHT:MZ(0,X)=32:NEXTX
1840 FORX=1 TOLEN(NF$)
1850 MZ(0,X)=ASC(MID$(NF$,X,1)):NEXTX
1860 FORY=0 TO22:DH$="":FORX=0 TOHT
1870 DH$=DH$+CHR$(MZ(Y,X)):NEXTX
1880 SP=DN(Y):SD=1:SV=6:GOSUB400
1890 SP=0:SD=1:SV=0:GOSUB400
1900 MZ$(Y)=DH$:NEXTY
1910 GOSUB500
1920 FORY=0 TO22:SR$=MZ$(Y):GOSUB560:NEXTY
1930 GOSUB580
1940 RETURN
2000 REM ***> DOOLHOF PRINTEN <***
2010 FORY=0 TO22:SR$=""
2020 FORX=0 TOHT:SR$=SR$+CHR$(MZ(Y,X))
2030 NEXTX:GOSUB350:GOSUB360:NEXTY
2040 FORI=1 TO5:GOSUB360:NEXTI
2050 RETURN
2100 REM ***> OPBOUW DOOLHOF <***
2110 GOSUB100
2120 PRINT"Ik maak zelf een doolhof.";
2130 REM >>> ARRAY OPVULLEN <<<
2140 FORY=1 TO21 STEP2:FORX=1 TOHT-2
2150 MZ(Y,X)=43:NEXTX:NEXTY
2160 FORY=1 TO21
2170 MZ(Y,0)=43:MZ(Y,HT-1)=43:MZ(Y,HT)=32
2180 NEXTY
2190 FORY=2 TO19
2200 FORX=2 TOHT-4 STEP3
2210 GOSUB260:K=INT(RV+.65)*3:IFK=0 THEN2230
2220 MZ(Y,X)=32:MZ(Y,X+1)=32:MZ(Y,X+2)=43
2230 NEXTX:NEXTY
2240 FORK=1 TO4:MZ(2,HT-6+K)=32:NEXTK
2250 MZ(2,HT-1)=62:MZ(2,HT)=62
2260 MZ(19,2)=32:MZ(19,3)=32
2270 MZ(20,1)=32:MZ(20,2)=32
2280 MZ(21,1)=32:MZ(21,2)=94
2290 VR$="DOOLHOF ZONDER NAAM":GOSUB6110
2300 FORX=1 TO19:MZ(0,X-1)=ASC(MID$(VR$,X,1)):NEXTX
2310 REM >>> DOOLHOF OP HET SCHERM <<<
2320 FORVE=0 TO22:FORHO=0 TOHT
2330 GOSUB110:PRINTCHR$(MZ(VE,HO))
2340 NEXTHO:NEXTVE
2350 RETURN
3000 REM ***> EERSTE VRAGEN <***
3010 VR$="Wil je veranderen (J/N) ?":GOSUB6110
3020 GOSUB3310:IFA=J THEN GOSUB5410
3030 IF(FL=1)AND(A=N) THEN3060
3040 VR$="Ga je deze opslaan (J/N) ?":GOSUB6110
3050 GOSUB3310:IFA=J THEN GOSUB1710
3060 VR$="Wil je een andere (J/N) ?":GOSUB6110
3070 GOSUB3310:IFA=N THEN3110
3080 GOSUB5010:REM DOOLHOF LADEN
3090 IFFL=1 THEN3060
3100 GOSUB2110:GOTO3010
3110 VR$="Wil je dit printen (J/N) ?":GOSUB6110
3120 GOSUB3310:IFA=J THEN GOSUB2010
3130 VR$="Zie jij een weg (J/N) ?":GOSUB6110
3140 GOSUB3310:GOSUB110
3150 IFA=J THENPRINT"Ik ook, ";
3160 IFA=N THENPRINT"Ik wel, ";
3170 PRINT"want ik zie altijd een uitweg!";
3180 REM >>>> UIT REGEL BEWAREN <<<<
3190 FORX=0 TOHT:BW(X)=MZ(2,X):NEXTX
3200 RETURN
3300 REM ***> INVOER J OF N <***
3310 J=74:N=78
3320 GOSUB200:IFIN$<>"" THEN3320
3330 GOSUB210:IF(IN=J)OR(IN=N) THEN3350
3340 GOSUB250:GOTO3330
3350 A=IN:RETURN
4000 REM *********> PROGRAMMA <*************
4010 HT=20:REM NEEM HT=HO-1 VOOR VOL SCHERM
4020 GOSUB6510:REM PROGRAMMA VOORSTELLEN
4030 DIMMZ$(22),DN(22),MZ(22,HT),BW(HT)
4040 GOSUB5910:REM ARRAY OPPOETSEN
4050 GOSUB5010:REM DOOLHOF LADEN ?
4060 IFFL=1 THEN4080
4070 GOSUB2110:REM DOOLHOF MAKEN
4080 GOSUB3010:REM EERSTE VRAGEN
4090 GOSUB1210:REM RUNNER AAN HET WERK
4100 REM ***> LAATSTE VRAGEN EN EIND <***
4110 IFFT=0 THEN4140
4120 GOSUB250:VR$="Ik kom er niet uit. Druk toets."
4130 GOSUB6110:GOSUB210:GOTO4180
4140 VR$="Wil je dit opslaan (J/N) ?":GOSUB6110
4150 GOSUB3310:IFA=J THEN GOSUB1710
4160 VR$="Wil je dit printen (J/N) ?":GOSUB6110
4170 GOSUB3310:IFA=J THEN GOSUB2010
4180 VR$="Moet deze doolhof schoon (J/N) ?":GOSUB6110
4190 GOSUB3310:IFA=N THEN4210
4200 GOSUB5710:FL=0:GOTO4080
4210 VR$="Wil je nog eens  (J/N) ?":GOSUB6110
4220 GOSUB3310:IFA=J THEN4040
4230 VR$="Dit nog bekijken (J/N) ?":GOSUB6110
4240 GOSUB3310:IFA=J THEN4230
4250 GOTO950:REM EINDE PROGRAMMA
4260 REM
5000 REM ***> DOOLHOF LADEN <***
5010 FL=0:GOSUB100
5020 VR$="Doolhof laden (J/N) ?":GOSUB6110
5030 GOSUB3310:IFA=N THEN5320
5040 FL=1:VE=2:GOSUB110
5050 PRINT"Voer de naam in."
5060 VE=4:GOSUB110:INPUTNF$
5070 LE=LEN(NF$):IFLE<8 THEN5110
5080 VR$="Deze naam is te lang. Druk toets in."
5090 GOSUB250:VE=6:GOSUB110:PRINTVR$
5100 GOSUB210:GOSUB100:GOTO5050
5110 PRINT:PRINT"Van (C)assette"
5120 PRINT" of (D)iskette ?"
5130 GOSUB210
5140 IFIN=67 THENNF=0:GOTO5170
5150 IFIN=68 THENNF=4:GOTO5170
5160 GOSUB250:GOTO5110
5170 GOSUB500
5180 FORY=0 TO22
5190 GOSUB540:MZ$(Y)=IN$
5200 NEXTY
5210 GOSUB580
5220 REM >>> DOOLHOF OP SCHERM <<<
5230 GOSUB100:FORVE=0 TO22
5240 DH$=MZ$(VE):HO=0:GOSUB110
5250 PRINTDH$:NEXTVE
5260 REM ***> DOOLHOF IN ARRAY <***
5270 FORY=0 TO22:FORX=0 TOHT
5280 MZ(Y,X)=ASC(MID$(MZ$(Y),X+1,1)):NEXTX
5290 SP=DN(Y):SD=1:SV=5:GOSUB400
5300 SP=0:SD=1:SV=0:GOSUB400
5310 NEXTY
5320 RETURN
5400 REM ***> DOOLHOF VERANDEREN <***
5410 X=INT(HT/2):Y=10
5420 A=MZ(Y,X):HO=X:VE=Y:GOSUB110
5430 PRINT"*";:GOSUB110
5440 FORW=0 TO50:NEXTW
5450 GOSUB200
5460 IFIN=13 THEN5530
5470 IF(IN=32)OR(IN=43) THEN5550
5480 IF(IN>27)AND(IN<32) THEN5570
5490 PRINTCHR$(A)
5500 FORW=0 TO50:NEXTW
5510 GOTO5420
5520 REM >>> STOPPEN MET VERANDEREN <<<
5530 PRINTCHR$(A):GOTO5670
5540 REM >>> VERANDEREN <<<
5550 PRINTCHR$(IN):MZ(Y,X)=IN:GOTO5420
5560 REM >>> CURSOR VERPLAATSEN <<<
5570 ONIN-27 GOSUB5600,5620,5640,5660
5580 PRINTCHR$(A);
5590 GOTO5420
5600 X=X-1:IFX<1 THENX=1
5610 RETURN
5620 X=X+1:IFX>HT-2 THENX=HT-2
5630 RETURN
5640 Y=Y+1:IFY>20 THENY=20
5650 RETURN
5660 Y=Y-1:IFY<2 THENY=2
5670 RETURN
5700 REM ******> DOOLHOF SCHOONMAKEN <********
5710 FORX=HT TO0 STEP-1:FORY=0 TO22
5720 IFMZ(Y,X)<>33 THEN5740
5730 HO=X:VE=Y:GOSUB110:PRINT" ":MZ(Y,X)=32
5740 NEXTY:NEXTX
5750 Y=2:FORX=0 TOHT:MZ(Y,X)=BW(X)
5760 HO=X:VE=Y:GOSUB110:PRINTCHR$(BW(X)):NEXTX
5770 IFFT<>2 THEN5800
5780 Y=1:FORX=1 TOHT-1:VE=Y:HO=X
5790 MZ(Y,X)=43:GOSUB110:PRINT"+":NEXTX
5800 RETURN
5900 REM ****> DOOLHOF-ARRAY OPPOETSEN <****
5910 GOSUB100:RESTORE
5920 PRINT"Even het doolhof-array oppoetsen."
5930 FORY=0 TO22:READDN(Y):NEXTY
5940 FORY=0 TO22:FORX=0 TOHT
5950 MZ(Y,X)=32:NEXTX
5960 SP=DN(Y):SD=1:SV=6:GOSUB400
5970 SP=0:SD=1:SV=0:GOSUB400:NEXTY
5980 RETURN
6100 REM ****> VRAGEN BOVENAAN HET SCHERM <****
6110 HO=0:VE=0:GOSUB110
6120 PRINT"                                      ";
6130 GOSUB110:PRINTVR$;
6140 RETURN
6500 REM ***> PROGRAMMA VOORSTELLEN <***
6510 GOSUB100
6520 PRINT"Uitleg zien (J/N) ?";
6530 GOSUB3310:IFA=N THEN7100
6540 GOSUB100
6550 PRINT"MAZERUNNER is een doolhof programma.":PRINT
6560 PRINT"Het kan zelf een doolhof maken of een"
6570 PRINT"bestaand doolhof laden.":PRINT
6580 PRINT"Een gemaakt of geladen doolhof kan"
6590 PRINT"worden veranderd, opgeslagen en/of"
6600 PRINT"uitgeprint.":PRINT
6610 PRINT"De doolhof wordt doorzocht door een"
6620 PRINT"DOOLHOFRUNNER.":PRINT
6630 PRINT"Een doorzocht doolhof kan schoongemaakt"
6640 PRINT"worden.":PRINT
6650 PRINT"De RUNNER is enigzins beperkt in zijn"
6660 PRINT"waarnemingsvermogen en zal hierdoor"
6670 PRINT"niet altijd de weg volgen die een"
6680 PRINT"toeschouwer in gedachten heeft.":PRINT
6690 PRINT"Maar in een doolhof dat niet veranderd"
6700 PRINT"is, vindt RUNNER altijd een uitweg."
6710 GOSUB7210
6720 PRINT"Voor het maken van een schermvullend"
6730 PRINT"doolhof, moet  de variabele HT in"
6740 PRINT"regel 4010 worden aangepast.":PRINT
6750 PRINT"Wordt dit programma op een PC met een"
6760 PRINT"GW-basic gedraaid, dan is het handig"
6770 PRINT"dat bij het gebruik van de bewaar en"
6780 PRINT"laad mogelijkheden de basic met het"
6790 PRINT"toevoegsel /f:8 ingeladen is.":PRINT
6800 PRINT"Moet een doolhof worden bewaard, dan"
6810 PRINT"mag de gevraagde naam niet langer dan"
6820 PRINT"zeven karakters zijn.":PRINT
6830 PRINT"Bij het veranderen van een doolhof"
6840 PRINT"komt in het midden van de doolhof een"
6850 PRINT"knipperende ster.":PRINT
6860 PRINT"Het verplaatsen van de ster gebeurt"
6870 PRINT"met de cursortoetsen. De ster kan niet"
6880 PRINT"op de randen van de doolhof komen."
6890 GOSUB7210
6900 PRINT"Onder de ster kan een spatie vervangen"
6910 PRINT"worden door een plusteken of een"
6920 PRINT"plusteken door een spatie. De ster"
6930 PRINT"komt daarbij niet van zijn plaats.":PRINT
6940 PRINT"Met de return toets komt men weer"
6950 PRINT"terug in het programma.":GOSUB7210
6960 PRINT"Omdat de zoek-routine van RUNNER"
6970 PRINT"aangepast is aan de door het"
6980 PRINT"programma ontwikkelde doolhof, kan"
6990 PRINT"het gebeuren dat RUNNER in een ver-"
7000 PRINT"anderd doolhof door muren breekt en"
7010 PRINT"toch geen uitweg vindt.":PRINT
7020 PRINT"RUNNER zal dit melden waarna er "
7030 PRINT"voor het schoonmaken van de doolhof"
7040 PRINT"kan worden gekozen.":PRINT
7050 PRINT"Tijdens het werken met het doolhof-"
7060 PRINT"array wordt er ter verpozing een"
7070 PRINT"etude ten gehore gebracht.":PRINT
7080 PRINT"Veel plezier met MAZERUNNER"
7090 GOSUB7210
7100 RETURN
7200 REM ***> KAN IK VERDER <***
7210 HO=0:VE=23:GOSUB110
7220 PRINT"Kan ik verder gaan (J/N) ?";
7230 HO=0:VE=23:GOSUB110
7240 GOSUB3310:IFA=N THEN7210
7250 GOSUB100
7260 RETURN
25000 REM ********> DATA ETUDE <**********
25010 DATA60,62,64,65,67,64,65,67,69,71,67
25020 DATA69,71,72,60,72,71,69,67,65,64,62,60
25030 REM
30000 REM ****************************
30010 REM MAZERUNNER IS EEN DOOLHOF
30020 REM PROGRAMMA UIT DE RUN RUBRIEK
30030 REM VAN HET ALGEMEEN DAGBLAD.
30040 REM MET TOESTEMMING VAN DE
30050 REM AUTEUR NICO BAAIJENS IS HET
30060 REM BEWERKT EN VERTAALD NAAR
30070 REM BASICODE-3.
30080 REM ****************************
30090 REM
32000 REM ***********************
32010 REM BEWERKING EN VERTALING:
32020 REM JAAP WIELES
32030 REM HOOFDWAL 68
32040 REM 4207 EE GORINCHEM
32050 REM TEL. 01830-30492
32060 REM NOVEMBER 1989
32070 REM ***********************
32080 REM
32090 REM TROS-RADIO dd 900207


1000 A=300:GOTO20:REM ** TAFELS **
1010 GOSUB100:DIMNR(12)
1020 GOSUB3010:REM ** LOGO + INSTRUCTIE **
1030 GOTO3510:REM ** BEGIN PROGRAMMA **
1040 REM ** TAFEL + ANTWOORD **
1050 GOSUB100:B=1:S=3
1060 FORX=1 TO10
1070 IFX>9 THENB=0
1080 HO=B:VE=S:GOSUB110
1090 S=S+2:PRINTX;" x ";T;"=";X*T
1100 SD=10:GOSUB450:NEXTX
1110 HO=20:VE=5:GOSUB110:PRINT"Bekijk dit eens"
1120 VE=7:GOSUB110:PRINT"goed ";N$
1130 VE=9:GOSUB110:PRINT"want na 30 sec."
1140 VE=11:GOSUB110:PRINT"verdwijnt het en"
1150 VE=13:GOSUB110:PRINT"moet je het zelf"
1160 VE=15:GOSUB110:PRINT"doen.";
1170 SD=175:GOSUB450
1180 REM ** BEGIN OPGAVEN **
1190 GOSUB100:VE=5:HO=3:GOSUB110
1200 PRINT"Daar gaan we dan ";N$;
1210 SD=30:GOSUB450
1220 GOSUB100:B=1:S=3:F=0:G=0:A$="*"
1230 FORE=1 TO10
1240 Q=0:IFE>9 THENB=0
1250 VE=S:HO=12:GOSUB110:PRINT"   "
1260 HO=B:GOSUB110
1270 PRINTE;" x ";T;"=";:INPUTL$
1280 IFVAL(L$)=0 THEN1250
1290 L=VAL(L$):S=S+2:IFE*T=L THEN1370
1300 REM ** FOUTE OPLOSSING **
1310 S=S-2:VE=4:HO=30:GOSUB110:PRINT"FOUT";:GOSUB250:F=F+1
1320 IFQ=2 THEN GOSUB5210:Q=0:S=S+2:GOTO1380
1330 REM ** NOG EEN KANS **
1340 Q=Q+1:VE=6:HO=25:GOSUB110
1350 PRINT"nog een keer";:GOSUB1800:GOTO1250
1360 REM ** GOEDE OPLOSSING **
1370 G=G+1:VE=4:HO=30:GOSUB110:PRINT"GOED";
1380 IFE=10 THEN GOSUB1820:SD=20:GOSUB450:GOTO1410
1390 Q=0:VE=6:HO=25:GOSUB110
1400 PRINT"nu de volgende";:GOSUB1800
1410 NEXTE
1420 GOSUB3710:IFF>0 THEN GOSUB4010
1430 IFF>4 THEN GOSUB4510:GOTO5110
1440 GOSUB4510:GOTO5010
1800 SD=20:GOSUB450
1810 VE=4:HO=30:GOSUB110:PRINT"         "
1820 VE=6:HO=25:GOSUB110:PRINT"              "
1830 RETURN
2000 REM ** AANTAL OPGAVE DERDE DEEL **
2010 GOSUB100:VE=2:HO=15:GOSUB110:PRINTN$
2020 G=0:F=0:A=1:S=0:VE=6:HO=4:GOSUB110
2030 PRINT"Hoeveel opgaven wil je gaan maken"
2040 VE=8:HO=9:GOSUB110
2050 INPUT"Het aantal opgaven is: ";S$
2060 S=VAL(S$):IFS<1 THEN2010
2070 REM ** BEGIN OPGAVEN **
2080 GOSUB2510
2090 GOSUB100:E=0:FORN=1 TO10
2100 VE=2:HO=3:GOSUB110:PRINT"opgave: ";A
2110 VE=5:HO=3:GOSUB110:PRINT"                  "
2120 VE=5:HO=13:GOSUB110:PRINT"    "
2130 Q=0:E=NR(N):VE=5:HO=3:GOSUB110
2140 PRINTE;" x ";T;"=";:INPUTL$
2150 IFVAL(L$)=0 THEN2120
2160 L=VAL(L$):IFE*T=L THEN2240
2170 REM ** FOUTE OPLOSSING **
2180 VE=4:HO=30:GOSUB110:PRINT"FOUT";:GOSUB250:F=F+1
2190 IFQ=2 THEN GOSUB5230:GOTO2250
2200 REM ** NOG EEN KANS **
2210 Q=Q+1:VE=6:HO=25:GOSUB110
2220 PRINT"nog een keer";:GOSUB2600:GOTO2120
2230 REM ** GOEDE OPLOSSING **
2240 G=G+1:VE=4:HO=30:GOSUB110:PRINT"GOED";
2250 IF(N=10)AND(A<S) THEN GOSUB2510:GOTO2090
2260 IFA=S THENN=10:GOTO2290
2270 A=A+1:VE=6:HO=25:GOSUB110
2280 PRINT"nu de volgende";:GOSUB2600
2290 NEXTN:GOTO2570
2500 REM ** CIJFERSCHUDROUTINE **
2510 FORN=1 TO10:NR(N+2)=N:NEXTN
2520 FORN=1 TO9:GOSUB260
2530 X=INT(1+RV*(11-N))+N+1
2540 NR(N)=NR(X):NR(X)=NR(N+2)
2550 NEXTN:NR(10)=NR(12):RETURN
2560 REM ** UITSLAG **
2570 SD=60:GOSUB450:GOSUB3710
2580 IF(G=S)AND(F=0) THEN5410
2590 GOSUB4510:GOTO5110
2600 SD=40:GOSUB450
2610 VE=4:HO=30:GOSUB110:PRINT"          "
2620 VE=6:HO=25:GOSUB110:PRINT"                "
2630 RETURN
3000 REM ** LOGO **
3010 VE=7:HO=4:GOSUB110
3020 PRINT"***** ***** **** **** *    ****"
3030 VE=8:HO=6:GOSUB110
3040 PRINT"*   *   * *    *    *    *"
3050 VE=9:GOSUB110:PRINT"*   *   * *    *    *    *"
3060 VE=10:GOSUB110:PRINT"*   ***** ***  ***  *    ****"
3070 VE=11:GOSUB110:PRINT"*   *   * *    *    *       *"
3080 VE=12:GOSUB110:PRINT"*   *   * *    *    *       *"
3090 VE=13:GOSUB110:PRINT"*   *   * *    **** **** ****"
3100 SD=70:GOSUB450
3110 REM ** NAAM INVULLEN **
3120 GOSUB100:VE=2:HO=14:GOSUB110:PRINT"HALLO ?????"
3130 VE=4:HO=7:GOSUB110:PRINT"Vertel me eerst hoe je heet"
3140 VE=8:HO=7:GOSUB110:PRINT"Tik hier je naam in en druk"
3150 VE=10:HO=7:GOSUB110:PRINT"dan op de return/enter toets"
3160 VE=6:HO=7:GOSUB110:INPUT"Mijn naam is: ";N$
3170 REM ** INSTRUCTIE **
3180 GOSUB100:VE=1:HO=15:GOSUB110
3190 PRINT"HALLO  ";N$
3200 PRINT:PRINT"Met dit programma kun je oefenen in het"
3210 PRINT"maken van de tafels."
3220 PRINT"Er wordt eerst gevraagd welke tafel je"
3230 PRINT"wilt gaan doen en daarna de keuze of je"
3240 PRINT"een voorbeeld wilt zien met de oplos-"
3250 PRINT"singen erachter, of zelf de antwoorden"
3260 PRINT"wilt invullen."
3270 PRINT"Na ieder goed antwoord verschijnt dan"
3280 PRINT"de volgende opdracht."
3290 PRINT"Bij een fout antwoord krijg je tweemaal"
3300 PRINT"de kans het te verbeteren, daarna geeft"
3310 PRINT"de computer het antwoord en komt er een"
3320 PRINT" * achter het foute antwoord te staan."
3330 PRINT"Daarna gaan we verder met de volgende"
3340 PRINT"opgave."
3350 HO=13:VE=23:GOSUB110:PRINT"DRUK EEN TOETS";
3360 GOSUB210
3370 GOSUB100:PRINT:PRINT:PRINT
3380 PRINT"De derde keuze is, dat er opdrachten"
3390 PRINT"verschijnen in verschillende volgorde"
3400 PRINT"maar wel van de opgegeven tafel."
3410 PRINT"Het aantal opgaven kun je zelf bepalen."
3420 PRINT"Als je alle opgaven goed hebt opgelost"
3430 PRINT"kun je pas verder met een andere tafel."
3440 PRINT:PRINT"Veel succes, ";N$
3450 HO=13:VE=23:GOSUB110:PRINT"DRUK EEN TOETS";
3460 GOSUB210:RETURN
3500 REM ** OPGAVE VAN DE TAFEL **
3510 GOSUB100:VE=3:HO=15:GOSUB110:PRINTN$
3520 VE=9:HO=1:GOSUB110
3530 PRINT"Van welke tafel wil je de opgaven maken"
3540 VE=11:HO=10:GOSUB110
3550 INPUT"de tafel van :";T$
3560 T=VAL(T$):IFT<1 THEN3540
3570 IFT<50 THEN3610
3580 PRINT:PRINT:PRINT
3590 PRINT"     WORDT DAT NIET TE MOEILIJK !!";
3600 SD=50:GOSUB450:GOTO3540
3610 GOSUB100:VE=6:HO=6:GOSUB110
3620 PRINT"1.Voorbeeld met antwoord"
3630 VE=8:GOSUB110:PRINT"2.Het antwoord zelf invullen"
3640 VE=10:GOSUB110:PRINT"3.Verschillende opdrachten"
3650 VE=12:GOSUB110:PRINT"4.Stoppen"
3660 VE=18:HO=8:GOSUB110:PRINT"Waar kies je voor ?";
3670 GOSUB210:A=VAL(IN$)
3680 IF(A<1)OR(A>4) THEN3670
3690 ONA GOTO1050,1190,2010,5500
3700 REM ** AANTAL GOED EN FOUT **
3710 VE=4:HO=30:GOSUB110:PRINT"         "
3720 VE=4:HO=22:GOSUB110:PRINTN$
3730 SR=G:GOSUB300
3740 VE=6:HO=20:GOSUB110:PRINT"Je hebt ";SR$;" maal een"
3750 SR=F:GOSUB300
3760 VE=8:GOSUB110:PRINT"goed en ";SR$;" maal een"
3770 VE=10:HO=22:GOSUB110:PRINT"fout antwoord"
3780 VE=12:HO=25:GOSUB110:PRINT"gegeven.";
3790 SD=70:GOSUB450
3800 RETURN
4000 REM ** AANWIJZEN FOUTE ANTWOORDEN **
4010 VE=14:HO=20:GOSUB110:PRINT"De antwoorden met"
4020 VE=16:GOSUB110:PRINT"de * heb je fout"
4030 VE=18:GOSUB110:PRINT"gedaan."
4040 VE=20:GOSUB110:PRINT"Bekijk deze nog"
4050 VE=22:GOSUB110:PRINT"eens.";
4060 SD=70:GOSUB450
4070 RETURN
4500 REM ** TEKSTBLOK SCHOON **
4510 FORE=14 TO22 STEP2
4520 VE=E:HO=20:GOSUB110:PRINT"                 "
4530 NEXTE
4540 RETURN
5000 REM ** DOORGAAN NAAR DERDE DEEL **
5010 VE=14:HO=25:GOSUB110:PRINT"GOED HOOR"
5020 VE=16:HO=24:GOSUB110:PRINT"nu de sommen"
5030 VE=18:HO=26:GOSUB110:PRINT"doorelkaar.";
5040 SD=70:GOSUB450:GOTO2010
5100 REM ** OPNIEUW BEGINNEN **
5110 VE=14:HO=25:GOSUB110:PRINT"SLECHT HOOR"
5120 VE=16:HO=24:GOSUB110:PRINT"We gaan het nog"
5130 VE=18:GOSUB110:PRINT"eens proberen";
5140 SD=70:GOSUB450:GOTO1050
5200 REM ** * ACHTER ANTWOORD **
5210 HO=17:VE=S:GOSUB110:PRINTA$
5220 REM ** ANTWOORD GEVEN **
5230 VE=8:HO=22:GOSUB110:PRINT"Het antwoord is :"
5240 VE=10:HO=23:GOSUB110:PRINTE;" x ";T;"=";E*T;
5250 SD=50:GOSUB450
5260 VE=8:HO=22:GOSUB110:PRINT"                 "
5270 VE=10:HO=23:GOSUB110:PRINT"                "
5280 RETURN
5400 REM ** UITSLAG **
5410 GOSUB100:VE=3:HO=4:GOSUB110
5420 PRINT"Dit heb je goed gedaan hoor !"
5430 VE=5:GOSUB110
5440 PRINT"Wil je nog een andere tafel doen"
5450 VE=7:HO=19:GOSUB110:PRINT"j/n";
5460 REM ** J/N ROUTINE **
5470 GOSUB210:IFIN=74 THEN3510
5480 IFIN<>78 THEN GOSUB250:GOTO5470
5490 REM ** EINDE PROGRAMMA **
5500 GOSUB100:VE=4:HO=12:GOSUB110:PRINTN$
5510 VE=6:HO=1:GOSUB110
5520 PRINT"Ik hoop dat je het leuk vond en je er"
5530 VE=8:GOSUB110
5540 PRINT"wat van geleerd hebt."
5550 VE=14:HO=14:GOSUB110:PRINT"*************"
5560 VE=15:GOSUB110:PRINT"*           *"
5570 VE=16:GOSUB110:PRINT"* TOT ZIENS *"
5580 VE=17:GOSUB110:PRINT"*           *"
5590 VE=18:GOSUB110:PRINT"*************";
5600 SD=100:GOSUB450:GOSUB250:GOTO950
30000 REM 
30010 REM MET DANK AAN DE HEER BREDERODE
30020 REM UIT LISSE, VOOR DE ROUTINE OM TE
30030 REM VOORKOMEN DAT MEN IN OPTIE 3,
30040 REM VERSCHILLENDE OPGAVEN, STEEDS DE
30050 REM ZELFDE OPGAVE KRIJGT.
30060 REM 
32000 REM ****** SEPTEMBER 1989 ******
32010 REM 
32020 REM   TAFELS GEMAAKT OP EEN PC
32030 REM 
32040 REM             DOOR
32050 REM 
32060 REM         R.J. V/D ZEE
32070 REM 
32080 REM         BRAAMKAMP 40
32090 REM 
32100 REM            ZUTPHEN
32110 REM 
32120 REM        TEL:05750-21736
32130 REM 
32140 REM ******** BASICODE 3 *******
32150 REM 
32160 REM TROS-RADIO dd 900117
32170 REM uitgebreid dd 900301

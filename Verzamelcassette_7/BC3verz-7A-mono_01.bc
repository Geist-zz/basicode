
1000 A=300:GOTO20:REM"HONDERD-VELD"
1010 DIMG(102)
1020 HC=.11:VC=.02:AG=1:P=0:PL=1:HZ=0:VZ=0:CN=0
1030 REM ############################################
1040 REM verwijder een van de twee volgende regels !
1050 F1=0:F2=10:F3=1:F4=1:REM geeft verticale rijtjes
1060 F1=1:F2=1:F3=0:F4=10:REM  ,, horizontale rijtjes
1070 REM ############################################
1080 REM --- inleiding en keuze ---
1090 SR$="HONDERD-VELD":HO=5:VE=1:GOSUB110:GOSUB150
1100 VE=3:GOSUB110:PRINT"       (S=stoppen)"
1110 VE=5:GOSUB110:PRINT"We gaan  samen  rekenen"
1120 VE=7:GOSUB110:PRINT"met  het  honderd-veld."
1130 VE=9:GOSUB110:PRINT"HOEVEEL RIJTJES WIL JE?"
1140 VE=11:GOSUB110:PRINT"Tik een cijfer... ";:GOSUB2510
1150 MG$=SR$:IFMG$="0" THENMG$="10"
1160 PRINTMG$:MG=10*VAL(MG$)
1170 VE=13:GOSUB110:PRINT"1 = CIJFERS in tikken"
1180 VE=15:GOSUB110:PRINT"2 = AANWIJZEN met de cursor"
1190 VE=17:GOSUB110:PRINT"Tik 1 of 2... ";
1200 GOSUB2510:IF(SR<1)OR(SR>2) THEN1200
1210 KS=SR:KS$=SR$
1220 GOSUB600:T$=" EVEN GEDULD...":SR$=T$:GOSUB2610
1230 SR$=MG$:HO=0:VE=0:GOSUB650
1240 SR$=KS$:HO=0:VE=.1:GOSUB650
1250 REM --- teken raster ---
1260 FORN=0 TO10:HO=.1:VE=N*.08:GOSUB620
1270 HO=.9:GOSUB630:NEXTN
1280 FORN=0 TO10:HO=.1+N*.08:VE=0:GOSUB620
1290 VE=.8:GOSUB630:NEXTN
1300 REM --- plaats 1, 10, 100 ---
1310 GOSUB2910
1320 REM --- vul array ---
1330 GOSUB2410
1340 REM --- wis getallen en tekst ---
1350 CN=1:GOSUB2910:SR$=T$:GOSUB2610:CN=0
1360 ONKS GOTO1510,1710
1500 REM =>> getallen intikken <<=
1510 TT=0:FORN=1 TO10:SR=G(N):GOSUB300
1520 VR$=SR$:VR=SR
1530 GOSUB2020
1540 T$="Welk getal staat hier? ":SR$=T$:GOSUB2610
1550 GOSUB2710:TT=TT+1
1560 REM --- antwoord is fout ---
1570 IFAW<>VR THENCN=1:SR$=AW$:GOSUB2790:CN=0:GOTO1550
1580 REM --- antwoord is goed ---
1590 CN=1:SR$=T$:GOSUB2610:SR$=AW$:GOSUB2790:GOSUB2020:CN=0
1600 REM --- getal in hokje tonen ---
1610 SR$=AW$:GOSUB2110:SP=0:SV=0:SD=40:GOSUB400
1620 CN=1:SR$=AW$:GOSUB2110:CN=0
1630 NEXTN:GOTO1820
1700 REM =>> aanwijzen met cursor <<=
1710 TT=0:FORN=1 TO10:SR=G(N):GOSUB300
1720 VR$=SR$:VR=SR:W=SR
1730 P=1:GOSUB2210:HQ=HO:VQ=VE:P=0
1740 T$="Waar staat nummer "+VR$:SR$=T$:GOSUB2610
1750 GOSUB3010:TT=TT+1:IF(AG<>SR) THEN1750
1760 CN=1:GOSUB3210:SR$=T$:GOSUB2610:CN=0
1770 REM --- getal in hokje tonen ---
1780 SR$=VR$:GOSUB2110:SP=0:SV=0:SD=40:GOSUB400
1790 CN=1:SR$=VR$:GOSUB2110:CN=0
1800 NEXTN
1810 REM --- laat de 10 getallen zien ---
1820 FORN=1 TO10:SR=G(N):GOSUB300
1830 GOSUB2110:NEXTN
1840 REM --- aantal missers ---
1850 SR=TT-10:GOSUB300:HO=0:VE=.2:GOSUB650
1860 SR$="Goed gedaan! Tik een toets:":GOSUB2610
1870 GOSUB210
1880 GOTO1000
2000 REM
2010 REM == sbr. print vraagtekens == SR$ / W
2020 W=VAL(SR$):SR$="??":GOSUB2210
2030 RETURN
2100 REM == sbr. print getal ==  SR$ / W
2110 W=VAL(SR$):IFW<10 THENSR$=" "+SR$
2120 GOSUB2210
2130 RETURN
2200 REM == sbr. bereken printpositie == W / HO,VE
2210 HO=.11:VE=.02
2220 ON1+F1 GOTO2240,2280
2230 REM verticaal tellen
2240 IFW>10 THENW=W-10:HO=HO+.08:GOTO2240
2250 IFW>1 THENW=W-1:VE=VE+.08:GOTO2250
2260 GOTO2300
2270 REM horizontaal tellen
2280 IFW>10 THENW=W-10:VE=VE+.08:GOTO2280
2290 IFW>1 THENW=W-1:HO=HO+.08:GOTO2290
2300 IFP=1 THENRETURN
2310 GOSUB650:RETURN
2400 REM == vul getallen array ==  G() MG
2410 FORN=3 TOMG+2:G(N)=N-2:NEXTN
2420 FORN=1 TOMG-1
2430 GOSUB260:X=N+3+INT(RV*(MG-N))
2440 G(N)=G(X)
2450 FORM=X TON+2 STEP-1:G(M)=G(M-1):NEXTM
2460 NEXTN:G(MG)=G(MG+2):RETURN
2500 REM == sbr. haal cijfer binnen == / SR SR$
2510 GOSUB210:IFIN=83 THEN950
2520 IF(IN<48)OR(IN>57) THEN2510
2530 SR=VAL(IN$):GOSUB300:RETURN
2600 REM == sbr. print tekstregel == SR$
2610 HO=.1:VE=.9:GOSUB650
2620 RETURN
2700 REM == sbr. neem antwoord in ==
2710 HA=.9:VA=.9
2720 GOSUB2510:AW=SR:AW$=SR$:HO=HA:VE=VA:GOSUB650
2730 IFVR<10 THENRETURN
2740 GOSUB2510:AW=10*AW+SR:AW$=AW$+SR$
2750 HO=HA:VE=VA:SR$=AW$:GOSUB650
2760 IFVR<100 THENRETURN
2770 GOSUB2510:AW=10*AW+SR:AW$=AW$+SR$
2780 REM -- wis-ingang --
2790 HO=HA:VE=VA:SR$=AW$:GOSUB650
2800 RETURN
2900 REM == sbr. print 1, 10, 100 ==
2910 SR$="1":GOSUB2110
2920 SR$="10":GOSUB2110
2930 SR$="100":GOSUB2110
2940 RETURN
3000 REM == sbr. cursorpositie == / AG
3010 GOSUB3210
3020 GOSUB210:IFIN=13 THENAG=(HZ+F1)*F2+(VZ+F3)*F4:RETURN
3030 IFIN=83 THEN950
3040 IF(IN<28)OR(IN>31) THEN3020
3050 CN=1:GOSUB3210:CN=0
3060 S=IN-27:ONS GOTO3070,3090,3120,3150
3070 IFHZ=0 THEN3010
3080 HC=HC-.08:HZ=HZ-1:GOTO3010:REM naar links
3090 IF(F1=0)AND(HZ+2>MG/10) THEN3010
3100 IF(F1=1)AND(HZ=9) THEN3010
3110 HC=HC+.08:HZ=HZ+1:GOTO3010:REM naar rechts
3120 IF(F1=0)AND(VZ=9) THEN3010
3130 IF(F1=1)AND(VZ+2>MG/10) THEN3010
3140 VC=VC+.08:VZ=VZ+1:GOTO3010:REM omlaag
3150 IFVZ=0 THEN3010
3160 VC=VC-.08:VZ=VZ-1:GOTO3010:REM omhoog
3200 REM == sbr. teken cursor ==
3210 HO=HC+.02:VE=VC+.01:SR$="+":GOSUB650
3220 RETURN
3230 REM---------------------------
30000 REM De getallen binnen de
30010 REM gekozen rijtjes worden in
30020 REM G() geschud, zodat ze er
30030 REM allemeel 1 x in voorkomen
30040 REM Hiervan worden dan de
30050 REM eerste 10 getallen aan de
30060 REM leerling aangeboden.
30070 REM Bij INTIKKEN hoeven alleen
30080 REM de cijfertoetsen te worden
30090 REM bediend, het programma let
30100 REM op de lengte van het getal
30110 REM Bij AANWIJZEN moet de
30120 REM cursor met de pijltoetsen
30130 REM op het juiste hokje gezet
30140 REM worden, dan RETURN geven.
30150 REM De hokjes van het 100-veld
30160 REM worden telkens weer gewist
30170 REM maar na het tiende getal
30180 REM worden de tien getallen
30190 REM getoond. Links van het
30200 REM vierkant het aantal rijen,
30210 REM de spelvorm en tenslotte
30220 REM het aantal foute antwoorden
30230 REM----------------------------
32000 REM Ch.W.Brederode
32010 REM Von Bonninghausenlaan 33
32020 REM 2161 ES  LISSE
32030 REM
32040 REM TROS-RADIO dd 891122,aangevuld

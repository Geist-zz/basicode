
1000 A=1000:GOTO20:REM KAARTENBAK
1010 GOSUB270:FS=FR:SM=0:REM NOG GEEN DATA GELADEN
1020 E1$="":E2$="":E3$="":E4$="":E5$="":E6$=""
1030 PR=0:SI=0:TE=0:HI=0
1040 GOSUB8530:REM STARTMENU
1050 IFCH=0 THEN950
1060 ONCH GOSUB7030,10530,5530
1070 IFSM=0 THEN1040:REM GEEN DATA GELADEN
1080 GOSUB8030:REM  HFDMENU
1090 IFCH=0 THEN950
1100 ONCH GOTO2030,3030,1140,3530,4030,5030,2530,1040
1110 REM ------------------------------
1120 REM SPRONGLADDER NAAR SAVE ROUTINE
1130 REM ------------------------------
1140 GOSUB10030:GOTO1070
1500 REM --------------
1510 REM CONTROLE KAART
1520 REM --------------
1530 GOSUB12040:REM WIS E2$...E5$
1540 E2$="IS DIT DOSSIER CORRECT ?"
1550 GOSUB7730:REM JA OF NEE ROUTINE
1560 IFIN=74 THENRETURN
1570 E2$="SPATIE = KIES     ENTER = CORRIGEREN"
1580 GOSUB11580:REM PRINT E2$
1590 J=1:GOTO1660
1600 GOSUB210
1610 IFIN=13 THEN1680:REM VERANDER REGEL
1620 IFIN<>32 THEN1600
1630 J=J+1:REM VOLGENDE REGEL
1640 HO=40:VE=J+3:GOSUB110:PRINT" ";
1650 IFJ=15 THENJ=1
1660 HO=40:VE=J+4:GOSUB110:PRINT"<";
1670 GOTO1600
1680 HO=40:VE=J+4:GOSUB110:PRINT" ";
1690 HO=15:VE=J+4:GOSUB110
1700 PRINTLEFT$(E6$,23);:REM WIS OUDE STRING
1710 MX=23:GOSUB9030:A$(F,J)=SR$:REM INVOER ROUTINE
1720 GOSUB12040:REM WIS E2$...E5$
1730 GOTO1530
2000 REM ------------
2010 REM NIEUWE KAART
2020 REM ------------
2030 IFTE<1 THENTE=1
2040 F=TE
2050 GOSUB12030:REM WIS E1$...E5$
2060 E1$="INVOER DOSSIERKAART"
2070 E2$="INVOER REGEL NR. "
2080 GOSUB11030:REM TEKEN KAART
2090 HI=1:GOSUB6030:REM PRINT ITEMS
2100 FORJ=1 TO14
2110 SR=J:GOSUB300:SR$=SR$+" "
2120 VE=20:HO=LEN(E2$)+3:GOSUB110:PRINTSR$
2130 HO=15:VE=J+4
2140 MX=23:GOSUB9030:A$(F,J)=SR$:REM INVOER ROUTINE
2150 IFA$(F,J)="Q" THENA$(F,J)="":J=99
2160 NEXTJ
2170 GOSUB1530:REM CONTROLE
2180 FORI=1 TO14:A$(TE+1,I)="":NEXTI
2190 TE=TE+1:IFTE<=MA THEN2530:REM SORTEREN
2200 GOSUB12040:REM WIS E2$...E5$
2210 E2$="MEER KAARTEN PASSEN NIET IN DIT DOSSIER."
2220 E3$="JE ZOU DIT KUNNEN BEWAREN EN EEN NIEUW"
2230 E4$="DOSSIER MAKEN VOOR NOG MEER GEGEVENS."
2240 GOSUB11580:REM PRINT E2$...E5$
2250 GOSUB7530:REM DRUK OP SPATIE
2260 GOSUB12040:REM WIS E2$...E5$
2500 REM --------
2510 REM SORTEREN
2520 REM --------
2530 GOSUB12030:REM WIS E2$...E5$
2540 E1$="SORTEREN"
2550 E2$="OP WELK ITEMNUMMER SORTEREN ?"
2560 GOSUB11030:REM TEKEN KAART
2570 HI=1:GOSUB6030:REM VUL KAART IN
2580 MX=2:HO=LEN(E2$)+3:VE=20:GOSUB9030:REM INVOER
2590 IT=VAL(SR$):IF(IT<1)OR(IT>14) THEN2580
2600 SI=IT
2610 GOSUB12040:REM WIS E2$...E5$
2620 E2$="STAND BY"
2630 E3$="SORTEREN IS BEZIG"
2640 GOSUB11580:REM PRINT E2$...E5$
2650 IFTE<3 THEN2740
2660 FORB=1 TOTE-2
2670 FORC=B+1 TOTE-1
2680 IFA$(B,IT)<=A$(C,IT) THEN2720
2690 FORJ=1 TO14
2700 D$=A$(B,J):A$(B,J)=A$(C,J):A$(C,J)=D$
2710 NEXTJ
2720 NEXTC
2730 NEXTB
2740 GOTO1080:REM HOOFDMENU
3000 REM --------------
3010 REM ZOEK EEN KAART
3020 REM --------------
3030 GOSUB12030:REM WIS E1$...E5$
3040 F=1
3050 E1$="ZOEK DOSSIERKAART"
3060 E2$="ITEMNUMMER :   "
3070 GOSUB11030:REM TEKEN KAART
3080 HI=1:GOSUB6030:REM PRINT ITEMS
3090 HO=LEN(E2$):VE=20:MX=2:GOSUB9030:REM INPUT
3100 IT=VAL(SR$):IF(IT>0)AND(IT<15) THEN3130
3110 GOSUB11540:REM PRINT E2$...E5$
3120 GOTO3090
3130 E3$="ITEMNAAM:"
3140 GOSUB11590:REM PRINT E3$...E5$
3150 MX=23:HO=LEN(E3$)+3:VE=21:GOSUB9030:REM INPUT
3160 Z$=SR$:IFZ$="" THEN3240
3170 GOSUB7630:REM PRINTVRAAG
3180 IFA$(F,IT)="" THEN3230
3190 IFZ$<>LEFT$(A$(F,IT),LEN(Z$)) THEN3230
3200 GOSUB4530:IFPR=1 THENHI=1:CN=0
3210 GOSUB6030:REM PRINT ITEMS
3220 GOSUB7530:REM DRUK OP SPATIE
3230 F=F+1:IFF<TE-1 THEN3180
3240 GOSUB12040:REM WIS E2$...E5$
3250 E2$="ZOEKEN KLAAR"
3260 GOSUB11580:REM PRINT E2$...E5$
3270 PR=0
3280 GOTO3950
3500 REM -----------------
3510 REM LIST DOSSIERNAMEN
3520 REM -----------------
3530 F=1
3540 GOSUB12030:REM WIS E1$...E5$
3550 E1$="LIST DOSSIERNAMEN"
3560 E2$="ZOEK ITEMNUMMER:   "
3570 GOSUB11030:REM TEKEN KAART
3580 HI=1:GOSUB6030:REM PRINT ITEMS
3590 HO=LEN(E2$):VE=20:MX=2:GOSUB9030:REM INPUT
3600 IT=VAL(SR$)
3610 IF(IT>0)AND(IT<15) THEN3640
3620 GOSUB11540:REM PRINT E2$...E5$
3630 GOTO3590
3640 GOSUB7630:REM PRINT VRAAG
3650 GOSUB4530
3660 GOSUB12040:REM WIS E2$...E5$
3670 E2$="P = PAUZE         SPATIE = VERDER"
3680 E3$="(BIJ PRINTEN GEEN ONDERBREKING !)"
3690 GOSUB11580:REM PRINT E2$...E5$
3700 VE=5
3710 IFA$(F,IT)="" THEN3920:REM LISTEN KLAAR
3720 P=PR
3730 GOSUB200:IF(HI=80)AND(IN<>32) THENIN=HI
3740 IFIN=80 THENHI=IN:GOTO3730:REM PAUZE
3750 IFF<=14 THENHO=4:GOSUB110:PRINT"          ";
3760 SR=F:GOSUB300:SR$=G$(IT)+" "+SR$+" :"
3770 IFP=1 THEN GOSUB350
3780 HO=12:SR=F:GOSUB300:GOSUB110:PRINTSR$;
3790 HO=15:SR$=A$(F,IT):GOSUB110:PRINTSR$
3800 IFP=1 THEN GOSUB350:GOSUB360
3810 GOSUB200:IF(HI=80)AND(IN<>32) THENIN=HI
3820 IFIN=80 THENHI=IN:GOTO3810:REM PAUZE
3830 HI=0
3840 F=F+1:VE=VE+1
3850 IFVE<19 THEN3710
3860 GOSUB7530:REM DRUK OP SPATIE
3870 FORVE=5 TO18
3880 HO=12:GOSUB110:PRINT"  ";
3890 HO=15:GOSUB110:PRINTLEFT$(E6$,23);
3900 NEXTVE
3910 GOTO3700
3920 GOSUB12040:REM WIS E2$...E5$
3930 E2$="LISTEN KLAAR"
3940 GOSUB11580:REM PRINT E2$...E5$
3950 GOSUB7530:REM DRUK OP SPATIE
3960 PR=0
3970 GOTO1080:REM HOOFDMENU
4000 REM ----------------
4010 REM DOORLOOP DOSSIER
4020 REM ----------------
4030 GOSUB12030:REM WIS E1$...E5$
4040 E1$="DOORLOOP DOSSIERKAARTEN"
4050 E2$="AUTO   VOLGENDE   TERUG   MENU   COPY"
4060 GOSUB11030:REM TEKEN KAART
4070 F=1:HI=1
4080 IFF>=TE THENF=TE-1
4090 GOSUB6030:REM PRINT GEGEVENS
4100 PR=0
4110 IFIN<>65 THEN4140
4120 GOSUB200:IFIN<>0 THEN4150
4130 IN=65:GOTO4190
4140 GOSUB210
4150 IF(IN=84)OR(IN=86)OR(IN=65) THEN4190
4160 IFIN=77 THEN1080:REM HOOFDMENU
4170 IFIN=67 THENPR=1:HI=1:CN=0:GOTO4090
4180 GOTO4110
4190 IFIN=65 THENSD=50:SV=0:SP=0:GOSUB400
4200 IF((IN=86)OR(IN=65))AND(F<TE-1) THEN GOSUB4530
4210 IF(IN=84)AND(F>1) THEN GOSUB4530
4220 IF(IN=65)OR(IN=86) THENF=F+1
4230 IFIN=84 THENF=F-1:IFF<1 THENF=1
4240 HO=0:VE=20:GOSUB110
4250 IFF>=TE-1 THENSR$="                  ":IN=0
4260 IFF<TE-1 THENSR$="   AUTO   VOLGENDE   TERUG"
4270 IFF<=1 THENSR$="   AUTO   VOLGENDE        "
4280 PRINTSR$;
4290 IFIN=65 THENSR$="AUTO":GOSUB110:GOSUB150
4300 GOTO4080
4500 REM ---------------------
4510 REM WIS GEGEVENS
4520 REM ---------------------
4530 HO=15:FORVE=5 TO18:GOSUB110
4540 PRINTLEFT$(E6$,23):NEXTVE
4550 RETURN
5000 REM ---------------------
5010 REM VERANDER DOSSIERKAART
5020 REM ---------------------
5030 GOSUB12030:REM WIS E1$...E5$
5040 E1$="VERANDER DOSSIERKAART"
5050 E2$="T = TITELS   G = GEGEVENS"
5060 GOSUB11030:REM TEKEN KADER
5070 HI=1:GOSUB6030
5080 MX=1:HO=LEN(E2$)+3:VE=20:GOSUB9030
5090 IF(IN<>71)AND(IN<>84) THEN5080
5100 IFIN=71 THEN5210
5110 GOSUB12040:REM WIS E2$...E5$
5120 E2$="VOER DE ITEMS IN"
5130 E3$="MAXIMAAL 14 ITEMS VAN 10 TEKENS"
5140 GOSUB11580:REM PRINT E2$...E5$
5150 FORI=1 TO14
5160 HO=4:VE=I+4:MX=10:GOSUB9030:REM INPUT
5170 IFSR$<>"" THENG$(I)=SR$
5180 GOSUB110:PRINTLEFT$(G$(I)+E6$,10);
5190 NEXTI
5200 GOTO1080
5210 GOSUB12040:REM WIS E2$...E5$
5220 E2$="WELK KAARTNUMMER:"
5230 GOSUB11530:REM PRINT E1$...E5$
5240 HO=LEN(E2$)+3:VE=20:MX=3:GOSUB9030:REM INPUT
5250 F=VAL(SR$):IF(F<=0)OR(F>=TE) THEN5240
5260 HI=1:GOSUB6030:REM PRINT ITEMS
5270 GOSUB1570:REM VERANDER GEGEVENS
5280 GOSUB12040:REM WIS E2$...E5$
5290 E2$="NOG EEN VERANDEREN ?"
5300 GOSUB11580:REM PRINT E2$...E5$
5310 GOSUB7730:REM JA OF NEE ROUTINE
5320 IFIN=74 THEN5210
5330 GOTO2530:REM SORTEREN
5500 REM -------------------
5510 REM NIEUW DOSSIER MAKEN
5520 REM -------------------
5530 GOSUB12030:REM WIS E1$...E5$
5540 E1$="*******  NIEUW DOSSIER   *******"
5550 E2$="DOSSIERNAAM:"
5560 GOSUB11030:REM TEKEN KAART
5570 HO=LEN(E2$)+3:VE=20:MX=7:GOSUB9030:REM INPUT
5580 NF$=SR$
5590 GOSUB6530:REM STARTINITIALISATIE
5600 GOSUB12040:REM WIS E2$...E5$
5610 E2$="VOER DE ITEMS IN"
5620 E3$="MAXIMAAL 14 ITEMS VAN 10 TEKENS"
5630 GOSUB11580:REM PRINT E2$...E5$
5640 FORI=1 TO14
5650 SR=I:GOSUB300
5660 HO=0:VE=I+4:GOSUB110:PRINTSR$
5670 HO=4:MX=10:GOSUB9030:REM INPUT
5680 G$(I)=SR$
5690 NEXTI
5700 RETURN
6000 REM ----------
6010 REM PRINT ITEMS
6020 REM ----------
6030 P=PR-CN
6040 IFP<>1 THEN6080
6050 GOSUB20030
6060 SR$="  +----------------------------------+"
6070 GOSUB350:GOSUB360
6080 HO=34:VE=3:SR=F:GOSUB300:GOSUB110:PRINTSR$;
6090 IFP<>1 THEN6150
6100 SR$="  !"+NF$+"   DOSSIERKAART NR. "+SR$
6110 SR$=LEFT$(SR$+E6$,37)+"!"
6120 GOSUB350:GOSUB360
6130 SR$="  +----------+-----------------------+"
6140 GOSUB350:GOSUB360
6150 FORI=1 TO14:VE=I+4
6160 HO=0:SR=I:GOSUB300:GOSUB110:PRINTSR$;
6170 IFP<>1 THEN6190
6180 SR$=LEFT$(SR$+E6$,2)+"!":GOSUB350
6190 IFHI<>1 THEN6220
6200 SR$=LEFT$(G$(I)+E6$,10)
6210 HO=4:GOSUB110:PRINTSR$;
6220 IFP<>1 THEN6240
6230 SR$=SR$+"!":GOSUB350
6240 SR$=A$(F,I):HO=15:GOSUB110:PRINTSR$
6250 IFP<>1 THEN6280
6260 SR$=LEFT$(SR$+E6$,23)+"!"
6270 GOSUB350:GOSUB360
6280 NEXTI
6290 IFP<>1 THEN6320
6300 SR$="  +----------+-----------------------+"
6310 GOSUB350:GOSUB360:GOSUB360
6320 P=0:HI=0:RETURN
6500 REM ------------------
6510 REM STARTINITIALISATIE
6520 REM ------------------
6530 IFSM=0 THENMA=INT(FR/350):DIMA$(MA,14),G$(14)
6540 SM=1:REM VARIABELEN GEINITIALISEERD
6550 P=0:CN=0:IFSM=0 THENTE=1:SI=1
6560 E6$="                                    "
6570 RETURN
7000 REM -----------
7010 REM INSTRUCTIES
7020 REM -----------
7030 GOSUB100:GOSUB6550:REM HAAL NOODZ.VARIABELEN
7040 PRINT"         INSTRUCTIES "
7050 PRINT:PRINT"Dit programma stelt u in staat"
7060 PRINT:PRINT"dossierkaarten te vervaardigen."
7070 PRINT:PRINT:PRINT"Deze kunnen op het scherm worden"
7080 PRINT:PRINT"geprojecteerd."
7090 PRINT:PRINT:PRINT"Telkens wordt ook gevraagd of de"
7100 PRINT:PRINT"kaart geprint moet worden."
7110 GOSUB7530:REM DRUK OP SPATIE
7120 GOSUB100:REM WIS SCHERM
7130 GOSUB12030:REM WIS E1$...E5$
7140 E1$="VOORBEELD BLANCO KAART"
7150 E2$="Via het startmenu kan de dossierkaart"
7160 E3$="ingevuld worden met 14 items."
7170 GOSUB11030:REM TEKEN KAART
7180 GOSUB7530:REM DRUK OP SPATIE
7190 GOSUB100:REM WIS SCHERM
7200 IFSM=0 THENRETURN
7210 PRINT"MAAK NU EEN KEUZE:"
7220 PRINT"M = NAAR HET MENU"
7230 PRINT"V = VOORBEELD VAN EEN KAART MET"
7240 PRINT"    ITEMS (indien aanwezig in"
7250 PRINT"    het geheugen)"
7260 PRINT"L = LIST EEN VOLLEDIG INGEVULDE"
7270 PRINT"    KAART (indien aanwezig in"
7280 PRINT"    het geheugen)"
7290 HI=1:E1$="":GOSUB200:IFIN=77 THENRETURN
7300 IFIN=76 THENE1$="VOORBEELD INGEVULDE KAART":F=1
7310 IFIN=86 THENE1$="VOORBEELD KAART MET ITEMS":F=TE
7320 IFE1$="" THEN7290
7330 GOSUB11030:GOSUB6030:GOSUB7530:GOTO7190
7500 REM ----------------------
7510 REM  GA VERDER NA SPATIE
7520 REM ----------------------
7530 E5$="                    DRUK OP >SPATIE<":GOSUB11610
7540 GOSUB200:IFIN<>32 THEN7540
7550 E5$=E6$:GOSUB11610:REM WIS E5$
7560 RETURN
7600 REM --------------------
7610 REM WEL OF NIET PRINTEN?
7620 REM --------------------
7630 GOSUB12040:REM WIS E2$...E5$
7640 E2$="PRINTEN ? "
7650 GOSUB7730:REM J-N ROUTINE
7660 PR=0:IFIN=74 THENPR=1
7670 RETURN
7700 REM ----------------------
7710 REM  KEUZE JA OF NEE
7720 REM ----------------------
7730 E2$=E2$+"  (J of N)":GOSUB11580
7740 GOSUB200:IF(IN<>74)AND(IN<>78) THEN7740
7750 E2$=LEFT$(E6$,LEN(E2$)):GOSUB11580:REM WIS E2$
7760 RETURN
8000 REM ----------------------
8010 REM  HOOFDMENU
8020 REM ----------------------
8030 GOSUB100:HO=5:PRINT"          M E N U"
8040 VE=3:GOSUB110:PRINT"1 - VOEG DOSSIERKAART TOE"
8050 VE=4:GOSUB110:PRINT"2 - ZOEK EEN DOSSIERKAART"
8060 VE=5:GOSUB110:PRINT"3 - SAVE DOSSIERKAARTEN"
8070 VE=6:GOSUB110:PRINT"4 - LIST DOSSIERNAMEN"
8080 VE=7:GOSUB110:PRINT"5 - DOORLOOP DOSSIERKAARTEN"
8090 VE=8:GOSUB110:PRINT"6 - VERANDER DOSSIERKAARTEN"
8100 VE=9:GOSUB110:PRINT"7 - SORTEER DOSSIERKAARTEN"
8110 VE=10:GOSUB110:PRINT"8 - STARTMENU"
8120 VE=13:GOSUB110:PRINT"0 - STOP"
8130 VE=22:GOSUB110:PRINT"MAAK UW KEUZE..."
8140 GOSUB270
8150 VE=16:GOSUB110:PRINT"VRIJ:";FR;" BYTES"
8160 IFTE<5 THEN8180
8170 PRINT"     GEM.PER KAART:";INT((FS-FR-140)/TE);" BYTES"
8180 PRINT"     MAX. ";MA;" KAARTEN"
8190 GOSUB210
8200 CH=IN-48:IF(CH<0)OR(CH>8) THEN8190
8210 GOSUB100:RETURN
8500 REM ---------
8510 REM STARTMENU
8520 REM ---------
8530 GOSUB100:PRINT"      S T A R T M E N U"
8540 HO=5
8550 VE=3:GOSUB110:PRINT"1 - TOELICHTING"
8560 VE=5:GOSUB110:PRINT"2 - LAAD EEN BESTAND"
8570 VE=7:GOSUB110:PRINT"3 - START EEN NIEUW BESTAND"
8580 VE=11:GOSUB110:PRINT"0 - STOP"
8590 GOSUB270
8600 VE=15:GOSUB110:PRINT"VRIJ:";FR;" BYTES";
8610 VE=22:GOSUB110:PRINT"MAAK UW KEUZE...";
8620 GOSUB210
8630 CH=IN-48:IF(CH<0)OR(CH>3) THEN8620
8640 IF(SM=0)OR(CH=1) THEN GOSUB100:RETURN
8650 E2$="HUIDIGE BESTAND BEWAREN ?"
8660 GOSUB7730:REM J/N ROUTINE
8670 IFIN=74 THEN GOSUB10030
8680 IFCH=2 THEN GOSUB100:RETURN
8690 IFCH=0 THEN950
8700 HO=0:VE=20:GOSUB110
8710 PRINT"ER VOLGT NU EEN PROGRAMMA-HERSTART,"
8720 VE=21:GOSUB110
8730 PRINT"DAARNA DIENT U OPNIEUW KEUZE 3 TE KIEZEN";
8740 VE=22:GOSUB110
8750 PRINT"DRUK OP >ENTER<......................";
8760 GOSUB200:IFIN<>13 THEN8760
8770 GOTO1000
9000 REM -------------
9010 REM INPUT ROUTINE
9020 REM -------------
9030 SR$="_"
9040 GOSUB9180
9050 GOSUB210
9060 IFIN<>28 THEN9090
9070 IFLEN(SR$)<3 THEN9030
9080 SR$=LEFT$(SR$,LEN(SR$)-2)+"_":GOTO9040
9090 IFIN=13 THEN9150
9100 IFIN=127 THEN9030
9110 IFLEN(SR$)=1 THENSR$=IN$+"_":GOTO9130
9120 SR$=LEFT$(SR$,LEN(SR$)-1)+IN$+"_"
9130 GOSUB9180
9140 IFLEN(SR$)<=MX THEN9050
9150 IFLEN(SR$)=1 THENSR$="":GOTO9170
9160 SR$=LEFT$(SR$,LEN(SR$)-1)
9170 SP=80:SD=2:SV=7:GOSUB400:REM BELL
9180 GOSUB110:PRINTLEFT$(SR$+E6$,MX):RETURN
9500 REM ---------------------
9510 REM LOAD/SAVE MEDIUM MENU
9520 REM ---------------------
9530 GOSUB100
9540 PRINT:PRINT" WELK MEDIUM GEBRUIKT U ?"
9550 PRINT" ========================"
9560 PRINT:PRINT" 0 = CASSETTE (BASICODEFORMAAT)"
9570 PRINT:PRINT" 2 = EIGEN OPSLAGSYSTEEM"
9580 PRINT:PRINT" 4 = DISKETTE"
9590 GOSUB200:NF=IN-48
9600 IF(NF<>0)AND(NF<>2)AND(NF<>4) THEN9590
9610 GOSUB100
9620 IFNF$="" THEN9670
9630 VE=19:HO=1:GOSUB110:PRINT"HUIDIGE BESTANDSNAAM:";NF$
9640 E2$="NIEUWE NAAM INVOEREN ?":E3$=""
9650 GOSUB7730:REM J/N ROUTINE
9660 IFIN=78 THEN9770
9670 VE=20:HO=1:GOSUB110:PRINT"NIEUWE BESTANDSNAAM:";
9680 GOSUB120:PRINT"                        ";:GOSUB110
9690 INPUTSR$:IF(SR$="")OR(LEN(SR$)>7) THEN9670
9700 IS=0:FORI=1 TOLEN(SR$)
9710 IN$=MID$(SR$,I,1)
9720 IF(IN$=".")OR(IN$=" ")OR(IN$="*")OR(IN$="?") THEN9740
9730 IS=IS+1
9740 NEXTI
9750 IFIS<>LEN(SR$) THEN9680
9760 GOSUB330:NF$=SR$:REM HOOFDLETTERS BESTANDSNAAM
9770 RETURN
10000 REM ------------
10010 REM SAVE-ROUTINE
10020 REM ------------
10030 GOSUB9530:REM BEPAAL OPSLAG MEDIUM
10040 NF=NF+1
10050 GOSUB500:REM OPEN BESTAND
10060 SR=MA:GOSUB300:GOSUB560:REM UITVOER MAXIMUM
10070 SR=TE:GOSUB300:GOSUB560:REM UITVOER TELLERSTAND
10080 SR=SI:GOSUB300:GOSUB560:REM UITVOER SORTEERITEM
10090 FORI=1 TO14
10100 SR$=G$(I):GOSUB560:REM UITVOER ITEMS
10110 NEXTI
10120 FORI=1 TOTE
10130 FORJ=1 TO14
10140 SR$=A$(I,J):GOSUB560:REM UITVOER GEGEVENS
10150 NEXTJ
10160 NEXTI
10170 GOSUB580
10180 RETURN
10500 REM ------------
10510 REM LOAD-ROUTINE
10520 REM ------------
10530 GOSUB9530:REM BEPAAL OPSLAG MEDIUM
10540 GOSUB500:REM OPEN BESTAND
10550 GOSUB540:MA=VAL(IN$):REM INVOER MAXIMUM
10560 GOSUB6530:REM STARTINITIALISATIE
10570 GOSUB540:TE=VAL(IN$):REM INVOER TELLERSTAND
10580 GOSUB540:SI=VAL(IN$):REM INVOER SORTEERITEM
10590 FORI=1 TO14
10600 GOSUB540:G$(I)=IN$:REM INVOER ITEMS
10610 NEXTI
10620 FORI=1 TOTE+1
10630 FORJ=1 TO14:A$(I,J)="":NEXTJ
10640 NEXTI
10650 FORI=1 TOTE
10660 FORJ=1 TO14
10670 GOSUB540:A$(I,J)=IN$:REM INVOER GEGEVENS
10680 NEXTJ
10690 NEXTI
10700 GOSUB580
10710 RETURN
11000 REM -----------
11010 REM TEKEN KAART
11020 REM -----------
11030 GOSUB100
11040 HO=0:VE=2:GOSUB110
11050 PRINT"   +----------------------------------+"
11060 PRINT"   !                                  !"
11070 PRINT"   +----------+-----------------------+"
11080 FORVE=5 TO18
11090 PRINT"   !          !                       !"
11100 NEXTVE
11110 PRINT"   +----------+-----------------------+"
11500 REM ---------------------
11510 REM KEUZE-FILENAAM-HEADER-AANWIJZINGEN
11520 REM ---------------------
11530 HO=4:VE=3:GOSUB110:PRINTNF$;
11540 HO=15:VE=3:GOSUB110:PRINT"DOSSIERKAART NR.";
11550 SR=SI:GOSUB300:SR$="SORTEER-ITEM "+SR$
11560 HO=0:VE=1:GOSUB110:GOSUB150
11570 HO=0:VE=0:GOSUB110:PRINTE1$;
11580 HO=3:VE=20:GOSUB110:PRINTE2$;
11590 HO=3:VE=21:GOSUB110:PRINTE3$;
11600 HO=3:VE=22:GOSUB110:PRINTE4$;
11610 HO=3:VE=23:GOSUB110:PRINTE5$;
11620 RETURN
12000 REM ---------------------
12010 REM MAAK E1$.....E5$ LEEG
12020 REM ---------------------
12030 E1$=E6$
12040 E2$=E6$:E3$=E6$:E4$=E6$:E5$=E6$
12050 GOTO11570
20000 REM +-+-+-+-+-+-+-+-+-+-+-+-+-+
20010 REM ZET PRINTER OP DUBBELBREED
20020 REM +-+-+-+-+-+-+-+-+-+-+-+-+-+
20030 GOTO20050:REM EERST VOLGENDE REGEL AANPASSEN
20040 SR$=CHR$(27)+"W"+CHR$(1):GOSUB350
20050 RETURN
32000 REM    * K.V.D.WISSEL
32010 REM    * NIEUWSTRAAT 306
32020 REM    * 3201 EG SPIJKENISSE
32030 REM    * 01880-13398
32040 REM
32050 REM    * OP LASER XT-2
32060 REM    * 1989
32070 REM
32080 REM TROS-RADIO dd 900221

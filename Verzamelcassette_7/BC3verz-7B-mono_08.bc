
1000 A=2000:GOTO20:REM ** COMPOSER **
1010 DIMTN$(18),TK(18),TH(1000)
1020 GOSUB2000
1030 FORI=1 TO18:READTN$(I):TK(I)=I+58:NEXTI
1040 GOSUB100
1050 VE=0:HO=5:GOSUB110
1060 SR$="Waar werkt u mee ?":GOSUB150
1070 PRINT:PRINT
1080 PRINT:PRINT"(B)asicode-3 cassette"
1090 PRINT:PRINT"(E)igen achtergrondgeheugen"
1100 PRINT:PRINT"(D)iskette                  =>";
1110 GOSUB210
1120 IFIN=66 THENNF=1:GOTO1160
1130 IFIN=69 THENNF=3:GOTO1160
1140 IFIN=68 THENNF=5:GOTO1160
1150 GOSUB250:GOTO1040
1160 GOSUB100
1170 PRINT"+-#-#-+-#-#-#-+-#-#-+ *****************"
1180 PRINT"! 2 3 ! 5 6 7 ! 9 0 ! *               *"
1190 PRINT"! # # ! # # # ! # # ! * # = verbeter  *"
1200 PRINT"! ! ! ! ! ! ! ! ! ! ! * % = k l a a r *"
1210 PRINT"!Q!W!E!R!T!Y!U!I!O!P! *               *"
1220 PRINT"+-+-+-+-+-+-+-+-+-+-+ *****************"
1230 VE=8:HO=0:GOSUB110
1240 PRINT"======================================="
1250 VE=10:GOSUB110
1260 PRINT"----tooninvoer----"
1270 HO=19
1280 N=N+1:GOSUB110:PRINT">:":GOSUB110:PRINT">";
1290 GOSUB210:SR$=IN$:GOSUB330:FT=1
1300 IFIN$="#" THEN GOSUB250:N=N-2:GOTO1280
1310 PRINTSR$
1320 SD=1:SV=10
1330 FORI=1 TO18:IFSR$=TN$(I) THENSP=TK(I):FT=0
1340 NEXTI
1350 IFSR$="%" THEN1390
1360 IFFT=1 THEN GOSUB250:N=N-1:GOTO1280
1370 GOSUB400:TH(N)=SP
1380 GOTO1280
1390 FORX=1 TON:SP=TH(X):GOSUB400:NEXTX
1400 GOSUB100
1410 PRINT"Geef de naam van het bestand:"
1420 PRINT:PRINT">";:INPUTNF$
1430 SR$=NF$:GOSUB330:NF$=SR$
1440 GOSUB500
1450 FORX=1 TON:SR=TH(X):GOSUB300:GOSUB560:NEXTX
1460 GOSUB580
1470 VE=15:HO=0:GOSUB110
1480 PRINT"Nog eens (J/N)";
1490 GOSUB210
1500 IFIN=74 THEN1000
1510 IFIN=78 THEN GOSUB3000:GOSUB210:GOTO950
1520 GOSUB250:GOTO1490
2000 GOSUB600:REM TITEL
2010 CN=0:VE=0:HO=0:GOSUB620
2020 HO=.9:GOSUB630:VE=.9:GOSUB630
2030 HO=0:GOSUB630:VE=0:GOSUB630
2040 VE=.1:HO=.1:GOSUB620
2050 HO=.8:GOSUB630:VE=.8:GOSUB630
2060 HO=.1:GOSUB630:VE=.1:GOSUB630
2070 VE=.2:HO=.2:GOSUB620
2080 HO=.7:GOSUB630:VE=.7:GOSUB630
2090 HO=.2:GOSUB630:VE=.2:GOSUB630
2100 VE=.22:HO=.27:SR$="Tros Basicode":GOSUB650
2110 VE=.42:HO=.33:SR$="COMPOSER":GOSUB650
2120 VE=.62:HO=.27:SR$="W.v.d.Elshout":GOSUB650
2130 SD=20:GOSUB450
2140 RETURN
3000 GOSUB600:REM EINDE
3010 CN=0:VE=0:HO=0:GOSUB620
3020 HO=.9:GOSUB630:VE=.9:GOSUB630
3030 HO=0:GOSUB630:VE=0:GOSUB630
3040 VE=.1:HO=.1:GOSUB620
3050 HO=.8:GOSUB630:VE=.8:GOSUB630
3060 HO=.1:GOSUB630:VE=.1:GOSUB630
3070 VE=.2:HO=.2:GOSUB620
3080 HO=.7:GOSUB630:VE=.7:GOSUB630
3090 HO=.2:GOSUB630:VE=.2:GOSUB630
3100 VE=.22:HO=.27:SR$="Tros Basicode":GOSUB650
3110 VE=.42:HO=.32:SR$="E I N D E":GOSUB650
3120 VE=.62:HO=.27:SR$="W.v.d.Elshout":GOSUB650
3130 RETURN
25000 DATA"1","Q","2","W","3","E","R","5","T"
25010 DATA"6","Y","7","U","I","9","O","0","P"
32000 REM &&&&&&&&&&&&&&&&&&&&&&&&&&&&
32010 REM &                          &
32020 REM & *C O M P O S E R*  door  &
32030 REM &  ---------------         &
32040 REM & Walther van den Elshout  &
32050 REM & Generaal Wicherslaan 14  &
32060 REM & 5623 GS  EINDHOVEN       &
32070 REM & Tel. 040 - 451098        &
32080 REM &                          &
32090 REM & Okt.'89 < > Commodore 64 &
32100 REM & TROS d.d. 14-2-1990      &
32110 REM &                          &
32120 REM &&&&&&&&&&&&&&&&&&&&&&&&&&&&


1000 A=300:GOTO20:REM " Rekenwonder "
1010 DIMPP$(8),C$(10):FO=0:GOSUB100:PRINT
1020 VT=2:REM vertraging, evt.aanpassen <<<
1030 GOSUB100:SR$="REKENEN":GOSUB150:PRINT:PRINT
1040 PRINT" WE GAAN SAMEN SOMMEN MAKEN.":PRINT
1050 RESTORE:FORN=0 TO10:READD$:C$(N)=D$:NEXTN
1060 PRINT" 1 = OPTELLEN":PRINT
1070 PRINT" 2 = AFTREKKEN":PRINT
1080 PRINT" 3 = VERMENIGVULDIGEN":PRINT
1090 PRINT" 4 = DELEN":PRINT
1100 PRINT"                5 = STOPPEN":PRINT
1110 PRINT" welk nummer kies je? ";
1120 GOSUB210:IF(IN<49)OR(IN>53) THEN1120
1130 PRINTIN$:KS=VAL(IN$):PRINT
1140 IFKS=5 THEN950
1150 PRINT" Wil je kijken of zelf rekenen (K/R) ";
1160 HP=0:GOSUB210:IF(IN<>75)AND(IN<>82) THEN1160
1170 PRINTIN$:Q$=IN$:Q=IN:IFQ=82 THENHP=2:PRINT
1180 GOSUB100:ONKS GOTO1210,1370,1680,2200
1190 REM
1200 REM === 1: optellen ===
1210 GOSUB100:SR$="OPTELLEN":GOSUB150:GOSUB3770
1220 PRINT:PRINT:L=1:BB=.1:FO=0:FF=0
1230 PRINT" Hoeveel getallen: 2,3,4,5,6,7 of 8?";
1240 GOSUB210:IF(IN<50)OR(IN>56) THEN1240
1250 TT=VAL(IN$):PRINTIN$:PRINT:FORK=1 TOTT
1260 PRINT" geef het ";K;"e getal: ";:LL=8:GOSUB3300
1270 PRINT:PRINT
1280 LA=LEN(AO$):IFLA>L THENL=LA
1290 PP$(K)=AO$:NEXTK:GOSUB3810
1300 GOSUB600:GOSUB3440:FF=0:X=.8:Y=0
1310 FORK=1 TOTT:G$=PP$(K):XC=X:YC=Y:GOSUB3060
1320 Y=Y+.1:NEXTK:IFQ=75 THEN GOSUB2810
1330 FF=1:HC=.9-.1*L:VC=Y-.1:GOSUB3480
1340 GOSUB2880:GOTO1210
1350 REM
1360 REM === 2: aftrekken ===
1370 GOSUB100:SR$="AFTREKKEN":GOSUB150:GOSUB3770
1380 PRINT:PRINT:L=1:BB=.1:FO=0:FF=0
1390 PRINT"Geef twee getallen":PRINT:FORN=1 TO2
1400 PRINTN;"e GETAL ";:LL=8:GOSUB3300:PRINT:PRINT
1410 PP$(N)=AO$:LA=LEN(AO$):IFLA>L THENL=LA
1420 NEXTN:A=VAL(PP$(1)):B=VAL(PP$(2))
1430 IFA>B THEN1480
1440 PRINT"Het tweede is te groot !":PRINT
1450 PRINT"tik RETURN en doe het nog eens.";
1460 GOSUB210:IFIN<>13 THEN1460
1470 GOTO1370
1480 GOSUB3810
1490 GOSUB600:GOSUB3440:FF=0:X=.8:Y=0:TT=2
1500 FORK=1 TOTT:G$=PP$(K):XC=X:YC=Y:GOSUB3060
1510 Y=Y+.1:NEXTK:L=L+1
1520 PP$(1)=RIGHT$("000000000"+PP$(1),L)
1530 PP$(2)=RIGHT$("000000000"+PP$(2),L)
1540 HO=X+.1:VE=Y-.02:GOSUB620:HO=X-.1*(L-2):GOSUB630:FF=1
1550 HO=.95:VE=.16:SR$="-":GOSUB650
1560 IFQ=75 THEN GOSUB2810
1570 REM --- aftreklus ---
1580 BW=0:A$=PP$(1):B$=PP$(2):G$=""
1590 FORJ=L TO1 STEP-1
1600 A=VAL(MID$(A$,J,1)):B=BW+VAL(MID$(B$,J,1)):BW=0
1610 IFA<B THENA=A+10:BW=1
1620 SR=A-B:GOSUB300:G$=SR$+G$:NEXTJ
1630 IFLEFT$(G$,1)="0" THENL=L-1:G$=RIGHT$(G$,L):GOTO1630
1640 XC=.8:YC=.2:GOSUB3060
1650 GOSUB2880:GOTO1370
1660 REM
1670 REM === 3: vermenigvuldigen ===
1680 GOSUB100:SR$="VERMENIGVULDIGEN":GOSUB150:GOSUB3770
1690 PRINT:PRINT:FO=0:FF=0:BB=.08
1700 PRINT" Geef maar twee getallen.":PRINT
1710 PRINT" EERSTE GETAL ";:LL=5:GOSUB3300
1720 AA$=AO$:LA=LEN(AA$):PRINT
1730 PRINT:PRINT" TWEEDE GETAL ";:LL=5:GOSUB3300
1740 BB$=AO$:LB=LEN(BB$):PRINT
1750 GOSUB3810
1760 TT=0:RT=0
1770 REM --- de vermenigvuldiging ---
1780 GOSUB3440
1790 HC=.8-BB*LA:VC=.1
1800 FORN=1 TOLEN(AA$)
1810 CF=VAL(MID$(AA$,N,1))
1820 XC=HC+BB*N:YC=VC
1830 GOSUB3120:NEXTN:HC=.8-BB*LB:VC=VC+.1
1840 FORN=1 TOLEN(BB$)
1850 CF=VAN(MID$(BB$,N,1))
1860 XC=HC+BB*N:YC=VC
1870 GOSUB3120:NEXTN
1880 HO=HC:VE=VC+.085:GOSUB620:HO=.9:GOSUB630
1890 SR$="x":HO=.95:VE=.25:GOSUB650:FF=1
1900 IFQ=75 THEN GOSUB2810
1910 REM --- de vermenigvuldiglus ---
1920 FORM=LB TO1 STEP-1:VC=VC+.1:HC=.8:TT=TT+1:RT=RT+1
1930 IFTT=1 THENPP$(TT)="":GOTO1950
1940 PP$(TT)=RIGHT$("00000000",TT-1)
1950 B=VAL(MID$(BB$,M,1)):BW=0
1960 IFB=0 THENVC=VC-.1:RT=RT-1:GOTO2120
1970 IFTT=1 THEN2010
1980 FORKK=1 TOTT-1:G$="0":GOSUB3380
1990 CF=VAL(G$):XC=HC:YC=VC:GOSUB3120
2000 HC=HC-BB:NEXTKK
2010 FORN=LA TO1 STEP-1
2020 A=VAL(MID$(AA$,N,1))
2030 C=BW+A*B:SR=C:GOSUB300:SR$=RIGHT$("0"+SR$,2)
2040 D=VAL(MID$(SR$,2,1)):E=VAL(MID$(SR$,1,1))
2050 SR=D:GOSUB300:G$=SR$
2060 PP$(TT)=SR$+PP$(TT):BW=E:GOSUB3380
2070 CF=VAL(G$):XC=HC:YC=VC:GOSUB3120:HC=HC-BB
2080 NEXTN
2090 IFE=0 THEN2120
2100 SR=BW:GOSUB300:G$=SR$:GOSUB3380
2110 CF=VAL(G$):XC=HC:YC=VC:GOSUB3120:PP$(TT)=SR$+PP$(TT)
2120 NEXTM
2130 IFRT=1 THEN2170
2140 REM --- onderstrepen, optellen, uitkomst ---
2150 GOSUB3480
2160 REM --- afwerking ---
2170 GOSUB2880:GOTO1680
2180 REM
2190 REM === 4: delen ===
2200 GOSUB100:PRINT:FO=0:FF=0:BB=.07
2210 SR$=" D E L E N ":GOSUB150:GOSUB3770
2220 PRINT:PRINT:IN=Q:IFIN=75 THEN2260
2230 IFIN=82 THENHP=2:GOTO2260
2240 GOTO2220
2250 REM --- neem 2 getallen in ---
2260 PRINT:PRINT" Geef een getal ";
2270 GOSUB120:LL=4:GOSUB3300:A=AO
2280 PRINT:PRINT:PRINT" Delen door ";
2290 GOSUB120:LL=3:GOSUB3300:B=AO
2300 C=A/B
2310 PRINT:PRINT:GOSUB3810
2320 GOSUB3440
2330 REM --- plaats deeltal ---
2340 SR=A:GOSUB300:A$=SR$:LA=LEN(A$):HA=.3:VA=0
2350 G$=A$:XC=HA:YC=VA:GOSUB3035
2360 REM --- schuine lijnen ---
2370 HO=.28:VE=0:GOSUB620
2380 HO=.2:VE=.2:GOSUB630
2390 HO=.3+BB*LA:VE=0:GOSUB620:HC=HO-.03:VC=0
2400 HO=HO+.09:VE=.2:GOSUB630
2410 REM --- deler ---
2420 SR=B:GOSUB300:B$=SR$:LB=LEN(B$)
2430 G$=B$:XC=.24-BB*LB:VE=0:GOSUB3035
2440 A$=A$+"000000":FF=1
2450 IFQ=75 THEN GOSUB2810
2460 REM --- zoek startpositie ---
2470 IFC<1 THENTT=LA:ZK=A:HA=HA+.08*(LA-1):GOTO2510
2480 TT=1:ZK=VAL(MID$(A$,TT,1))
2490 IFZK>=B THEN2510
2500 TT=TT+1:HA=HA+BB:ZK=ZK*10+VAL(MID$(A$,TT,1)):GOTO2490
2510 LV=TT:DD=VAL(LEFT$(A$,1))
2520 IFTT=1 THEN2550
2530 FORN=2 TOTT:DD=DD*10+VAL(MID$(A$,N,1)):NEXTN
2540 REM --- hoofd deel lus ---
2550 FORQO=1 TO4
2560 REM --- quotient ---
2570 IFTT=LA+1 THENXC=HC:YC=VC:CF=10:GOSUB3120:HC=HC+.04
2580 HC=HC+BB:GOSUB2980
2590 G$=QO$:XC=HC:YC=VC:GOSUB3035
2600 REM --- produkt ---
2610 PR=B*VAL(QO$):SR=PR:GOSUB300:PR$=SR$:LP=LEN(PR$)
2620 VA=VA+.1:XC=HA:YC=VA
2630 G$=PR$:GOSUB3060
2640 REM --- streepje ---
2650 HO=HA+BB:VE=VA+.085:GOSUB620
2660 HO=HO-BB*LV:GOSUB630
2670 REM --- verschil ---
2680 VS=DD-PR:SR=VS:GOSUB300:VS$=SR$
2690 LV=LEN(VS$)+1
2700 VA=VA+.1:XC=HA:YC=VA:G$=VS$:GOSUB3060
2710 REM --- cijfer bijhalen ---
2720 IF(TT>=LA)AND(VS=0) THENQO=7
2730 IFQO=7 THEN2780
2740 TT=TT+1:HA=HA+BB:BY=VAL(MID$(A$,TT,1))
2750 SR=BY:GOSUB300:BY$=SR$
2760 VS$=VS$+BY$:XC=HA:YC=VA:G$=BY$:GOSUB3035
2770 DD=10*VS+VAL(BY$)
2780 NEXTQO:GOSUB2860:GOTO2200
2790 REM
2800 REM === sbr.SPATIEBALK ===
2810 HO=.1:VE=.9:SR$="  druk op de SPATIEBALK"
2820 GOSUB650:RETURN
2830 REM
2840 REM === sbr.AFWERKING ===
2850 REM --- staartdeling ---
2860 IFVS<>0 THENHO=HC+.06:VE=VC+.05:SR$=". .":GOSUB650
2870 REM --- overige prgrms ---
2880 CN=1:GOSUB2810:CN=0
2890 SR$=" KLAAR ! "
2900 IF(HP>0)AND(FO=0) THENSR$=" GOED GEDAAN! "
2910 SR$=SR$+" - NOG 'N KEER ?"
2920 HO=.1:VE=.9:GOSUB650
2930 GOSUB210:IF(IN=78) THEN1000
2940 IF(IN=74) THENRETURN
2950 GOTO2890
2960 REM
2970 REM === sbr.QUOTIENT === DD / QO$
2980 EE=DD:QQ=0
2990 IFEE>=B THENEE=EE-B:QQ=QQ+1:GOTO2990
3000 SR=QQ:GOSUB300:QO$=SR$
3010 RETURN
3020 REM
3030 REM === sbr.PRINT STRING vooruit === G$,XC,YC
3035 XC=XC-BB:FORNP=1 TOLEN(G$):XC=XC+BB
3040 CF=VAL(MID$(G$,NP,1)):GOSUB3120
3045 NEXTNP:RETURN
3050 REM === PRINT STRING achteruit === G$,XC,YC
3060 XC=XC+BB:FORNP=LEN(G$) TO1 STEP-1:XC=XC-BB
3070 IFFF=1 THEN GOSUB3380
3080 CF=VAL(MID$(G$,NP,1)):GOSUB3120
3090 NEXTNP:RETURN
3100 REM
3110 REM === sbr.TEKEN EEN CIJFER === CF,XC,YC
3120 M$=SR$:IF(FF=0)OR(CF=10) THEN3180
3130 IFHP>0 THENHO=XC+.01:VE=YC+.03:SR$="?":GOSUB650
3140 GOSUB210:IFHP<2 THEN3180
3150 CN=1:GOSUB650:CN=0
3160 IFCF=10 THEN3180
3170 IFIN<>CF+48 THENFO=FO+1:GOTO3130
3180 T$=C$(CF):SS=VAL(LEFT$(T$,1)):PP=2
3190 FORRR=1 TOSS:EE=VAL(MID$(T$,PP,1)):PP=PP+1
3200 HO=XC+VAL(MID$(T$,PP,1))*.01:PP=PP+1
3210 VE=YC+VAL(MID$(T$,PP,1))*.01:PP=PP+1
3220 GOSUB620
3230 FORLL=1 TOEE
3240 HO=XC+VAL(MID$(T$,PP,1))*.01:PP=PP+1
3250 VE=YC+VAL(MID$(T$,PP,1))*.01:PP=PP+1
3260 GOSUB630:NEXTLL:NEXTRR
3270 SR$=M$:RETURN
3280 REM
3290 REM === sbr.INNEMEN GETAL ===     LL / AO AO$
3300 GOSUB210:IF(IN<49)OR(IN>57) THEN3300
3310 AI=VAL(IN$):PRINTIN$;:AO$=IN$:AO=IN
3320 GOSUB210:IF(IN=13)OR(LEN(AO$)=LL) THEN3350
3330 IF(IN<48)OR(IN>57) THEN3320
3340 AO$=AO$+IN$:PRINTIN$;:AI=AI*10+VAL(IN$):GOTO3320
3350 AO=VAL(AO$):RETURN
3360 REM
3370 REM === sbr.WACHTLUS ===
3380 IFIN=83 THENRETURN
3390 IF(HP=0) THENSV=0:SP=0:SD=VT:GOSUB400:RETURN
3400 REM AANPASSEN NAAR WENS VIA VT IN REGEL 1030
3410 RETURN
3420 REM
3430 REM === sbr.PLAATS (K) OF (R) ===
3440 GOSUB600:CN=0:HO=0:VE=.9:SR$="("+Q$+")":GOSUB650
3450 RETURN
3460 REM
3470 REM === sbr.OPTELLEN ===
3480 HO=HC:VE=VC+.085:GOSUB620:HO=.9:GOSUB630
3490 SR$="+":HO=.95:VE=VE-.02:GOSUB650
3500 FORN=TT TO1 STEP-1
3510 PP$(N)=RIGHT$("00000000000000"+PP$(N),14):NEXTN
3520 REM --- de optellus ---
3530 BW=0:UI$=""
3540 FORM=14 TO1 STEP-1:G=0
3550 FORN=1 TOTT
3560 H=VAL(MID$(PP$(N),M,1))
3570 G=G+H
3580 NEXTN
3590 G=G+BW
3600 SR=G:GOSUB300
3610 G$=RIGHT$(SR$,1)
3620 UI$=UI$+G$
3630 BW=INT(G/10)
3640 NEXTM
3650 REM
3660 L=LEN(UI$)
3670 IFMID$(UI$,L,1)="0" THENL=L-1:GOTO3670
3680 UI$=LEFT$(UI$,L)
3690 REM --- uitkomst printen ---
3700 HC=.8+BB:VC=VC+.1
3710 FORN=1 TOLEN(UI$)
3720 G$=MID$(UI$,N,1):CF=VAL(G$)
3730 HC=HC-BB:XC=HC:YC=VC:GOSUB3380:GOSUB3120:NEXTN
3740 RETURN
3750 REM
3760 REM === sbr.KOP ===
3770 IFHP=0 THENPRINT"KIJKEN":RETURN
3780 PRINT"ZELF REKENEN":RETURN
3790 REM
3800 REM === sbr.INNAME ===
3810 PRINT:PRINT" Dank je wel. TIK OP RETURN";
3820 GOSUB210:IFIN<>13 THEN3820
3830 RETURN
3840 REM
3850 REM === CIJFERCODES ===
25000 DATA"18010617374641301001 0"
25010 DATA"12213037 1"
25020 DATA"1601103041430747 2"
25030 DATA"2601103041423323623334446371706 3"
25040 DATA"2200044413237 4"
25050 DATA"27000333444637170610040 5"
25060 DATA"1930100106173746443303 6"
25070 DATA"12004017 7"
25080 DATA"2613040617374644944331302011030414233 8"
25090 DATA"254130100103145144441463717 9"
25100 DATA"147766767768 ,"
25110 REM
30000 REM --- uitleg cijfers ---
30010 REM De cijfers worden getekend
30020 REM met gebroken lijnen volgens
30030 REM de coordinaten, die uit de
30040 REM code gelezen worden.
30050 REM Het eerste cijfer is het
30060 REM aantal lijnenseries (1 of 2)
30070 REM Daarachter staat per lijnen-
30080 REM serie eerst het aantal lijn-
30090 REM stukken van de serie, dan de
30100 REM x,y van het beginpunt en dan
30110 REM de x,y van de vervolgpunten.
30120 REM De coordinaten zijn vanuit
30130 REM de linkerbovenhoek=0, x naar
30140 REM rechts en y omlaag.
30150 REM Alles in waarden van 0 tot 9
30160 REM ----------------------------
32000 REM Ch.W.Brederode
32010 REM Von Bonninghausenlaan 33
32020 REM 2161 ES  LISSE
32030 REM
32040 REM TROS-RADIO dd 891115
32050 REM correctie dd 891214

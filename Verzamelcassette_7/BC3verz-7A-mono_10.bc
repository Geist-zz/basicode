
1000 A=200:GOTO20:REM JBTime
1010 REM initialisatie
1020 DIMH(2),M(2),S(2),T(2)
1030 DEFFNMO(X)=INT((X/60-INT(X/60))*60)
1040 GOTO8010
2000 REM invoeren
2010 HO=0:VE=X*2:GOSUB110
2020 PRINT"H:  ";H(X);
2040 FORY=1 TO25:PRINT" ";:NEXTY:PRINT
2050 HO=3:VE=X*2:GOSUB110
2060 INPUTH$
2070 IFH$="0" THEN2090
2080 H=VAL(H$):IF(H<1)OR(H>999) THEN2050
2090 H(X)=VAL(H$)
2100 HO=13:VE=X*2:GOSUB110
2110 PRINT"M:  ";M(X);
2130 FORY=1 TO10:PRINT" ";:NEXTY:PRINT
2150 HO=16:VE=X*2:GOSUB110
2160 INPUTM$
2170 IFM$="0" THEN2190
2180 M=VAL(M$):IF(M<1)OR(M>59) THEN2150
2190 M(X)=VAL(M$)
2200 HO=26:VE=X*2:GOSUB110
2210 PRINT"S:  ";S(X)
2220 HO=29:VE=X*2:GOSUB110
2230 INPUTS$
2240 IFS$="0" THEN2260
2250 S=VAL(S$):IF(S<1)OR(S>59) THEN2220
2260 S(X)=VAL(S$)
2270 T(X)=H(X)*3600+M(X)*60+S(X)
2280 HO=0:VE=X*2+1:GOSUB110
2290 PRINT"[   h  '  ";CHR$(34);"]"
2300 SR=H:CT=3:CN=0:GOSUB310
2310 HO=1:VE=X*2+1:GOSUB110
2320 PRINTSR$
2330 SR=M:CT=2:CN=0:GOSUB310
2340 HO=5:VE=X*2+1:GOSUB110
2350 PRINTSR$
2360 SR=S:CT=2:CN=0:GOSUB310
2370 HO=8:VE=X*2+1:GOSUB110
2380 PRINTSR$
2390 REM
2400 HO=12:VE=X*2+1:GOSUB110
2410 GOSUB210
2420 IFIN=32 THEN2010
2430 IFIN<>13 THEN GOSUB250:GOTO2410
2440 RETURN
2450 REM
3000 REM +/- kiezen
3010 PRINT"[+/-]";
3020 GOSUB210
3030 IF(IN$<>"+")AND(IN$<>"-") THEN3020
3040 IFIN$="+" THENI=1:GOSUB4010
3050 IFIN$="-" THENI=2:GOSUB5010
3060 RETURN
3070 REM
4000 REM optellen
4010 S=FNMO(S(1)+S(2))
4020 M=FNMO(M(1)+M(2))+FNMO((S(1)+S(2)-S)/60)
4030 H=H(1)+H(2)+(M(1)+M(2)-M)/60
4040 RETURN
4050 REM
5000 REM aftrekken
5010 Q=0
5020 S=S(1)-S(2)
5030 IFS<0 THENS=60+S:Q=-1
5040 M=M(1)-M(2)+Q
5050 Q=0:IFM<0 THENM=60+M:Q=-1
5060 H=H(1)-H(2)+Q
5070 RETURN
5080 REM
6000 REM afdrukken
6010 H=INT(H+.5):REM afronden
6020 M=INT(M+.5)
6030 S=INT(S+.5)
6040 PRINT"/----------\"
6050 IFH<0 THENPRINT"|  tijd<0  |":GOTO6170
6060 IFH>999 THENPRINT"| tijd>999 |":GOTO6170
6070 PRINT"|   h  '  ";CHR$(34);"|"
6080 SR=H:CT=3:CN=0:GOSUB310
6090 HO=1:VE=8:GOSUB110
6100 PRINTSR$
6110 SR=M:CT=2:CN=0:GOSUB310
6120 HO=5:VE=8:GOSUB110
6130 PRINTSR$
6140 SR=S:CT=2:CN=0:GOSUB310
6150 HO=8:VE=8:GOSUB110
6160 PRINTSR$
6170 PRINT"\----------/"
6180 RETURN
6190 REM
7000 REM intro
7010 GOSUB100:HO=13:VE=0:GOSUB110
7020 SR$="JBTime":GOSUB150
7030 HO=0:VE=2:GOSUB110
7040 PRINT"Intro":PRINT"-----"
7050 PRINT"Dankzij dit programma is er voor goed"
7060 PRINT"een einde gekomen aan uw problemen bij"
7070 PRINT"rekenen met uren,minuten en seconden."
7080 PRINT
7090 PRINT"Of u hier nu iets aan heeft bij "
7100 PRINT"sport,werk of vrije tijd,"
7110 PRINT"Handig is het altijd !"
7120 PRINT
7130 PRINT"Wanneer u de uren,minuten en seconden"
7140 PRINT"heeft ingevoerd,vraagt het programma om"
7150 PRINT"toetsdruk. Hierbij is return/enter"
7160 PRINT"correct en spatie niet correct."
7170 PRINT
7180 PRINT"Veel succes met dit programma van:":PRINT
7190 SR$="JBSoft, Alkmaar.":GOSUB150
7200 HO=32:VE=22:GOSUB110
7210 PRINT"[toets]";
7220 GOSUB210
7230 RETURN
7240 REM
8000 REM stuurmodule
8010 GOSUB7010
8020 GOSUB100:HO=13:VE=0:GOSUB110
8030 SR$="JBTime":GOSUB150
8040 HO=0:VE=2:GOSUB110
8050 X=1:GOSUB2010
8060 X=2:GOSUB2010
8070 PRINT:GOSUB3010
8080 PRINT:GOSUB6010
8090 IF(H<0)OR(H>999) THENH=0:M=0:S=0
8100 H(1)=H:M(1)=M:S(1)=S
8110 H(2)=0:M(2)=0:S(2)=0
8119 HO=5:VE=15:GOSUB110
8120 SR$="Druk een toets [S]=STOP":GOSUB150
8130 GOSUB210:IFIN=83 THEN950
8140 GOTO8020
8150 REM
30000 REM *****************************
30010 REM *          JBTime           *
30020 REM *****************************
30030 REM *     Rekenen met tijd      *
30040 REM * Handig voor het bijhouden *
30050 REM * van sportuitslagen of om  *
30060 REM * uit te rekenen hoe lang   *
30070 REM * bv. een LP duurt.         *
30080 REM * Met de mogelijkheid om    *
30090 REM * door te tellen            *
30100 REM *                           *
30110 REM *   (c) JBSoft, Alkmaar     *
30120 REM *****************************
30130 REM
32000 REM Gemaakt door:
32010 REM Jan Benjamin
32020 REM Stationstraat 26
32030 REM 1815 TC Alkmaar
32040 REM (072-123809)
32050 REM Op een
32060 REM Philips VG8020 MSX-1 Computer
32080 REM *****************************
32090 REM
32100 REM TROS-RADIO dd 891220

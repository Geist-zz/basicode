
1000 A=200:GOTO20:REM === zoek een DIER ===
1010 DIMG(11),V$(9),NM$(10):GD=0:FT=0:GOSUB1660
1020 TP=20:REM tempo, naar keuze wijzigen.
1030 W$="ADYGHOVJKFREZJPDLTEQBZUGKFOBLDTAZJKLMBEW"
1040 V$(1)="9876543201":V$(2)="9876540312"
1050 V$(3)="9876504132":V$(4)="9870651243"
1060 V$(5)="0981723654":V$(6)="9012837465"
1070 V$(7)="0192348576":V$(8)="0123495687"
1080 V$(9)="0123456798"
1090 REM --- getallen 1-10 schudden ---
1100 FORN=1 TO10:G(1+N)=N:NEXTN
1110 FORN=1 TO9:GOSUB260:X=N+1+INT(RV*(11-N))
1120 G(N)=G(X):G(X)=G(N+1):NEXTN
1130 G(10)=G(11)
1140 REM --- lees 10 dierennamen uit 80 ---
1150 RESTORE:FORM=1 TO10
1160 GOSUB260:N=1+INT(RV*8)
1170 FORH=1 TON:READA$:NEXTH:NM$(M)=A$
1180 IFN<8 THENFORH=1 TO8-N:READA$:NEXTH
1190 NEXTM
1200 REM === hoofdlus ===
1210 FORM=1 TO10:GOSUB100
1220 HO=5:VE=5:GOSUB110:PRINT"WELK DIER ZIT HIER ?"
1230 REM --- haal een diernaam op ---
1240 P=G(M):X$=NM$(P):X=LEN(X$)
1250 REM --- kies 10 letters onzintekst ---
1260 GOSUB260:N=1+INT(RV*30):A$=MID$(W$,N,10)
1270 REM --- deel dat in twee stukken ---
1280 GOSUB260:L=1+INT(RV*9)
1290 L$=LEFT$(A$,L):R$=RIGHT$(A$,10-L)
1300 REM --- print de verborgen dierennaam ---
1310 HW=10:VW=10:HO=10:VE=10:GOSUB110:PRINTL$;X$;R$
1320 VE=20:GOSUB110:PRINTX;" letters"
1330 REM --- wis en wacht op antwoord ---
1340 V$=V$(L):P=1
1350 H$=MID$(V$,P,1):H=VAL(H$)
1360 IFH>L-1 THENH=H+X
1370 HO=HW+H:VE=VW:GOSUB110:PRINT" ":P=P+1
1380 SD=TP:GOSUB450:IFSD>0 THEN1410
1390 IFP<11 THEN1350
1400 GOTO1510
1410 HO=10:VE=15:GOSUB110:SR$=IN$
1420 IF(IN=127)OR(SR$=" ") THEN1510
1430 GOSUB110:PRINTSR$;
1440 GOSUB210:IFIN=13 THEN1500
1450 IFIN<>127 THENSR$=SR$+IN$:GOTO1430
1451 IFLEN(SR$)=1 THEN1430
1460 GOSUB110:PRINT"          ";
1480 SR$=LEFT$(SR$,LEN(SR$)-1)
1490 GOTO1430
1500 GOSUB330
1510 HO=10:VE=20:GOSUB110:IFSR$<>X$ THEN1540
1520 PRINT"GOED ZO! - ";11-P;" PUNTEN"
1530 GD=GD+11-P:GOTO1550
1540 PRINT"Jammer, het was ";X$:FT=FT+1
1550 SD=30:GOSUB450
1560 NEXTM
1570 REM --- uitslag ---
1580 GOSUB100:HO=5:VE=5:GOSUB110
1590 PRINT"JE HEBT ";GD;" PUNT";
1600 IFGD<>1 THENPRINT"EN!"
1610 HO=5:VE=10:GOSUB110
1620 PRINT"WIL JE NOG EEN KEER (J/N) ";
1630 GOSUB210:IFIN=74 THEN1000
1640 GOTO950
1650 REM --- uitleg ---
1660 GOSUB100
1670 PRINT"WELK DIER ZIT HIER?":PRINT:PRINT:PRINT
1680 PRINT"Je krijgt een rij letters te zien"
1690 PRINT:PRINT"Daar zit de naam van een dier in"
1700 PRINT:PRINT"=== TIK DE NAAM VAN HET DIER ==="
1710 PRINT:PRINT"  (dat moet zonder tikfouten!)"
1720 PRINT:PRINT"Na 10 dieren krijg je de uitslag"
1730 PRINT:PRINT:PRINT:PRINT"Tik op een toets ";
1740 GOSUB210:PRINT"!";:RETURN
25000 REM ---------- 80 namen ----------------
25010 DATA"POES","HOND","MUIS","GEIT","DUIF"
25020 DATA"HAAN","WOLF","EGEL","PAUW","HAAS"
25030 DATA"LEEUW","PANTER","OLIFANT","LUIPAARD","ZEBRA"
25040 DATA"BAVIAAN","PAPEGAAI","GIRAF","DOLFIJN","ARA"
25050 DATA"WESP","RUPS","KREKEL","VLINDER","BIJ"
25060 DATA"GANS","EEND","KOET","FUUT","REIGER"
25070 DATA"WORM","SLANG","PIER","AAL","PALING"
25080 DATA"MEREL","MEES","MUS","SPREEUW","LIJSTER"
25090 DATA"VARKEN","SCHAAP","PAARD","KOE","EZEL"
25100 DATA"MUG","VLIEG","VLO","LUIS","TEEK"
25110 DATA"TOR","KEVER","KRAB","KWAL","HAAI"
25120 DATA"SNOEK","KARPER","ZEELT","VOORN","BLEI"
25130 DATA"RAT","EEKHOORN","WEZEL","HERT","REE"
25140 DATA"DUIF","UIL","VALK","AREND","MEEUW"
25150 DATA"PONY","ZEEHOND","BUFFEL","GEMS","VOS"
25160 DATA"KAMEEL","LAMA","JAKHALS","IBIS","MIER"
25170 REM -----------------------------------------
30000 REM Dit programma vraagt 10 verstopte dieren
30010 REM te zoeken tussen in totaal 10 onzinletters
30020 REM en de dierennamen in te tikken.
30030 REM De volgorde is per serie anders.
30040 REM Na elke serie volgt een uitslag.
30050 REM De dierennamen mogen veranderd worden.
30060 REM -----------------------------------------
32000 REM Ch.W.Brederode
32010 REM Von Bonninghausenlaan 33
32020 REM 2161 ES  LISSE
32030 REM
32040 REM TROS-RADIO dd 900328

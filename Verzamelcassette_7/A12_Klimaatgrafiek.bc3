
1000 A=1000:GOTO20:REM KLIMAATGRAFIEK
1010 REM INITIALISATIE ****************
1020 DIMMA$(12),NS(12),TE(12)
1030 FORM=1 TO12
1040 READMA$(M)
1050 NEXTM
1060 REM
1070 REM UITLEG ***********************
1080 GOSUB2260
1090 REM
1100 REM PLAATS INVOEREN **************
1110 GOSUB100:HO=9:VE=0:GOSUB110
1120 SR$="KLIMAATGRAFIEK":GOSUB150
1130 HO=0:VE=2:GOSUB110
1140 INPUT"Plaats ";PL$
1150 HO=0:VE=4:GOSUB110
1160 SR$="Correct [J/N]":GOSUB150
1170 GOSUB250:GOSUB210
1180 IFIN=78 THEN1130
1190 IFIN<>74 THEN1170
1200 REM
1210 REM NEERSLAG/TEMP INVOEREN *******
1220 FORX=1 TO12
1230 GOSUB100:HO=9:VE=0:GOSUB110
1240 SR$="KLIMAATGRAFIEK":GOSUB150
1250 HO=0:VE=2:GOSUB110
1260 PRINTMA$(X):FORS=1 TOLEN(MA$(X)):PRINT"-";:NEXTS
1270 HO=0:VE=4:GOSUB110
1280 INPUT"Neerslag [cm]   ";NS(X)
1290 IFNS(X)>40 THENNS(X)=40
1300 IFNS(X)<0 THEN1270
1310 INPUT"Temperatuur [C] ";TE(X)
1320 IFTE(X)>40 THENTE(X)=40
1330 PRINT
1340 REM CONTROLE *********************
1350 SR$="Correct [J/N]":GOSUB150
1360 GOSUB210
1370 IFIN=74 THEN1400
1380 IFIN=78 THEN1270
1390 GOSUB250:GOTO1360
1400 NEXTX
1410 REM
1420 REM OVERZICHT ********************
1430 GOSUB100:HO=9:VE=0:GOSUB110
1440 SR$="KLIMAATGRAFIEK":GOSUB150
1450 HO=0:VE=2:GOSUB110
1460 PRINT"Maand     Neerslag Temperatuur"
1470 PRINT"-----     -------- -----------"
1480 FORX=1 TO12
1490 PRINTMA$(X);
1500 PRINTTAB(9);NS(X);
1510 PRINTTAB(18);TE(X)
1520 NEXTX
1530 REM
1540 REM CONTROLE *********************
1550 HO=0:VE=17:GOSUB110
1560 SR$="Correct [J/N]":GOSUB150
1570 GOSUB250:GOSUB210
1580 IFIN=74 THEN1800
1590 IFIN<>78 THEN1570
1600 REM
1610 HO=0:VE=17:GOSUB110
1620 SR$="Maand [1-12]":GOSUB150
1630 INPUTMA
1640 IF(MA<1)OR(MA>12) THEN1610
1650 HO=0:VE=17:GOSUB110
1660 PRINTMA$(MA);
1670 PRINT"                  "
1680 FORX=1 TOLEN(MA$(MA))
1690 PRINT"-";
1700 NEXTX
1710 HO=0:VE=19:GOSUB110
1720 INPUT"Neerslag [cm]  ";NS(MA)
1730 IFNS(MA)>40 THENNS(MA)=40
1740 IFNS(MA)<0 THEN1710
1750 INPUT"Temperatuur [C]";TE(MA)
1760 IFTE(MA)>40 THENTE(MA)=40
1770 GOTO1430
1780 REM
1790 REM GRAFIEK TEKENEN **************
1800 CN=0:GOSUB600
1810 HO=0:VE=0:SR$=PL$
1820 GOSUB650
1830 SR$=""
1840 FORX=1 TOLEN(PL$)
1850 SR$=SR$+"-"
1860 NEXTX
1870 HO=0:VE=.03:GOSUB650
1880 REM
1890 HO=.2:VE=.2:GOSUB620
1900 VE=.8:GOSUB630
1910 HO=.8:GOSUB630
1920 VE=.2:GOSUB630
1930 REM
1940 FORX=0 TO30 STEP10
1950 HO=.13:VE=.8-X*.02
1960 SR=X:GOSUB300:GOSUB650
1970 HO=.82:VE=.8-X*.02
1980 GOSUB650:NEXTX
1990 REM
2000 FORX=1 TO12
2010 HO=.17+.05*X:VE=.85
2020 SR$=LEFT$(MA$(X),1)
2030 GOSUB650
2040 NEXTX
2050 REM
2060 FORX=1 TO12
2070 HO=.15+.05*X:VE=.8:GOSUB620
2080 VE=.8-NS(X)/50:GOSUB630
2090 HO=HO+.05:VE=.8-NS(X)/50
2100 GOSUB630
2110 VE=.8:GOSUB630
2120 NEXTX
2130 REM
2140 HO=.2:VE=.8-TE(12)/50:GOSUB620
2150 FORX=1 TO12
2160 HO=.175+X*.05:VE=.8-TE(X)/50
2170 GOSUB630
2180 NEXTX
2190 HO=.8:VE=.8-TE(1)/50:GOSUB630
2200 GOSUB210
2210 IFIN=80 THEN GOSUB20030
2220 IFIN=83 THEN950
2230 GOTO1430
2240 REM
2250 REM UITLEG ***********************
2260 GOSUB100:HO=9:VE=0:GOSUB110
2270 SR$="KLIMAATGRAFIEK":GOSUB150
2280 HO=0:VE=2:GOSUB110
2290 PRINT"Dit programma stelt u in staat zelf"
2300 PRINT"klimaatgrafieken te maken, zoals"
2310 PRINT"deze ook in atlassen staan."
2320 PRINT"Allereerst vraagt het programma u een "
2330 PRINT"plaats in te voeren."
2340 PRINT"Vervolgens moet u het neerslagcijfer en"
2350 PRINT"de temperatuur van de maand invoeren."
2360 PRINT"Daarna krijgt u nog een overzicht."
2370 PRINT"Hierna wordt de klimaatgrafiek getekend."
2380 PRINT"Let op:"
2390 PRINT"[S] = Stoppen"
2400 PRINT"[P] = Printen."
2410 PRINT
2420 PRINT"Voor het printen moet u echter zelf "
2430 PRINT"een subroutine schrijven."
2440 PRINT
2450 SR$="Toets":GOSUB150:GOSUB210
2460 RETURN
20000 REM PRINTEN
20010 REM PROGRAMMEER HIER ZELF
20020 REM EEN SCREENDUMP ROUTINE
20030 RETURN
20040 REM
25000 DATA"Januari","Februari","Maart"
25010 DATA"April","Mei","Juni"
25020 DATA"Juli","Augustus","September"
25030 DATA"Oktober","November","December"
30000 REM ****************************
30010 REM *     KLIMAATGRAFIEK       *
30020 REM *  (c) JBSoft, Alkmaar     *
30030 REM ****************************
30040 REM
32000 REM Gemaakt door:
32010 REM Jan Benjamin
32020 REM Stationstraat 26
32030 REM 1815 TC ALKMAAR
32040 REM (072-123809)
32050 REM Op een:
32060 REM Philips VG8020 MSX-1 Computer
32070 REM ****************************
32080 REM
32090 REM TROS-RADIO dd 891220

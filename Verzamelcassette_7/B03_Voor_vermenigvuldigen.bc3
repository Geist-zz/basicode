
1000 A=2000:GOTO20:REM VOOR HET VERMENIGVULDIGEN
1010 DH=.03125
1020 DIMG(2,8),C(2),C$(2),P(2),P$(2),PP(2),PP$(2)
1030 DIMA$(2),B$(2),D(2),D$(2)
1040 FL=0:VB=0:VO=0:ZS=0:CN=0:N=0:DT=0:DG$=""
1050 V=0:F=0:ES=0
1060 SM$="     opgeteld :   "
1070 I0$=">":I1$=" x ":I2$=" = "
1080 SB=1:IFHO>78 THENSB=2
1090 GOTO3150:REM >>>>> MENU <<<<<<
1100 REM ***> DATA LEZEN GRAFISCH SCHERM <***
1110 RS=.06:RESTORE
1120 READDA$:IFDA$<>PR$ THEN1120
1130 READDA$
1140 IFDA$="einde" THEN1190
1150 IFDA$="ro" THENVE=VE+RS:GOTO1130
1160 IFDA$="sp" THEN GOSUB3020:GOTO1130
1170 IFDA$="ns" THEN GOSUB600:HO=0:VE=0:GOTO1130
1180 GOSUB1210:VE=VE+RS:GOTO1130
1190 RETURN
1200 REM ****> TEKST GRAFISCH SCHERM <*****
1210 HR=HO:RR$=DA$
1220 LS=LEN(RR$)
1230 FORL=1 TOLS
1240 SR$=MID$(RR$,L,1)
1250 GOSUB650
1260 HO=HO+DH
1270 NEXTL
1280 HO=HR:SR$=RR$
1290 RETURN
1300 REM ***> DATA LEZEN TEKST SCHERM <***
1310 RESTORE
1320 READDA$:IFDA$<>PR$ THEN1320
1330 READDA$
1340 IFDA$="einde" THEN1390
1350 IFDA$="ro" THENVE=VE+1:GOTO1330
1360 IFDA$="sp" THEN GOSUB2920:GOTO1330
1370 IFDA$="ns" THEN GOSUB100:HO=0:VE=0:GOTO1330
1380 GOSUB1410:VE=VE+2:GOTO1330
1390 RETURN
1400 REM ****> TEKST OP TEKST SCHERM  <*****
1410 HR=HO:RR$=DA$
1420 LS=LEN(RR$)
1430 FORL=1 TOLS
1440 TT$=MID$(RR$,L,1)
1450 GOSUB110:PRINTTT$;
1460 HO=HO+SB
1470 NEXTL
1480 HO=HR
1490 RETURN
1500 REM ***> INVULLEN <***
1510 FORK=1 TOLEN(DA$)
1520 K$=MID$(DA$,K,1)
1530 IFK$="." THEN GOSUB1560
1540 NEXTK:RETURN
1550 REM >>> ROUTINE PIJLTJE OMHOOG <<<
1560 HI=HO:VI=VE
1570 HO=HI+(K-1)*DH:VE=VI+.04:SR$="^"
1580 GOSUB650
1590 KT$=MID$(DB$,K,1)
1600 GOSUB2880
1610 IFIN$<>KT$ THEN GOSUB2740:GOTO1600
1620 VE=VI:SR$=".":CN=1:GOSUB650:CN=0
1630 SR$=KT$:GOSUB650
1640 VE=VI+.04:SR$="^":CN=1:GOSUB650:CN=0
1650 HO=HI:VE=VI:RETURN
1660 REM ***> OPTELLEN BIJ VOORBEELDEN <***
1670 HO=0:VE=VI:DA$=SM$:GOSUB1210
1680 FORK=1 TOLEN(SS$)
1690 W=1:GOSUB3110
1700 HO=(LEN(DB$)-K)*DH
1710 DA$=MID$(SS$,(LEN(SS$)-K+1),1):GOSUB1210
1720 NEXTK
1730 RETURN
1740 REM ***> OPTELLEN BIJ OEFENINGEN <***
1750 HI=LEN(DB$)*DH:VI=VE
1760 FORK=1 TOLEN(SS$)
1770 HO=HI-K*DH:VE=VI+.04:SR$="^"
1780 GOSUB650
1790 KT$=MID$(DB$,(LEN(DB$)-K+1),1)
1800 GOSUB2880
1810 IFIN$<>KT$ THEN GOSUB2740:GOTO1800
1820 VE=VI:SR$=".":CN=1:GOSUB650:CN=0
1830 SR$=KT$:GOSUB650
1840 VE=VI+.04:SR$="^":CN=1:GOSUB650:CN=0
1850 NEXTK
1860 RETURN
1870 REM *****> SOMMEN OP HET SCHERM <******
1880 FORJ=0 TO2
1890 VI=VZ+J*.08:IFZS=1 THEN1940
1900 GOSUB620
1920 VE=VI+3/VG:GOSUB630
1930 HO=7*DH:GOSUB630
1940 VE=VI:HO=HI
1950 IFFL=1 THEN1990
1960 DA$=B$(J):GOSUB1210
1970 W=2:GOSUB3110
1980 GOTO2010
1990 DA$=A$(J):GOSUB1210
2000 DB$=B$(J):GOSUB1510
2010 NEXTJ
2020 RETURN
2030 REM *******> STREEP MET + <********
2040 HO=18*DH:LH=LEN(PP$(2)):H1$=""
2050 FORI=1 TOLH:H1$=H1$+"-":NEXTI
2060 DA$=H1$+"+"
2070 GOSUB1210
2080 RETURN
2090 REM ***> AJ$ EN BJ$ VULLEN <***
2100 CL$=I0$+CV$+I1$+P$(2)+I2$+SS$
2110 FORJ=0 TO2
2120 H0$=I0$:H1$=I1$:H2$=I2$
2130 IFVO=1 THENH0$=" "
2140 IFVB=1 THEN GOSUB2350:GOTO2170
2150 CN=0:CT=LEN(P$(2)):SR=P(J)
2160 GOSUB310:P$(J)=SR$
2170 CN=0:CT=LEN(SS$):SR=PP(J)
2180 GOSUB310:PP$(J)=SR$
2190 B$(J)=H0$+CV$+H1$+P$(J)+H2$+PP$(J)
2200 LH=LEN(CL$)
2210 H1$="":H2$=B$(J):GOSUB2280:A$(J)=H1$
2220 NEXTJ
2230 B3$=SM$+SS$
2240 LH=LEN(B3$)
2250 H1$="":H2$=B3$:GOSUB2280:A3$=H1$
2260 RETURN
2270 REM ***> VULLEN AJ$ MET PUNTEN <***
2280 FORI=1 TOLH
2290 H$=MID$(H2$,I,1)
2300 IF(H$>="0")AND(H$<="9") THENH$="."
2310 H1$=H1$+H$
2320 NEXTI
2330 RETURN
2340 REM >>>> HULPROUTINE VOOR VB=1 <<<<
2350 CN=0:CT=LEN(P$(2)):SR=D(J)
2360 GOSUB310:P$(J)=SR$:CV$=C$(J)
2370 RETURN
2380 REM ***> FOUTEN AFHANDELEN <***
2390 HO=0:VE=.8
2400 IFFT>1 THENPR$="ft2":GOSUB1110:RETURN
2410 IFFT=0 THENPR$="ft0":GOSUB1110
2420 IFFT=1 THENPR$="ft1":GOSUB1110
2430 GOSUB210:A=IN:IFA=13 THENA=74
2440 RETURN
2450 REM ****> GETAL ARRAYS VULLEN <****
2460 FORJ=0 TO2
2470 FORI=0 TO8
2480 G(J,I)=I+1
2490 NEXTI
2500 NEXTJ
2510 REM ****> RANDOM GETAL-DEEL <****
2520 FORJ=0 TO2
2530 GOSUB260:H=INT((RV*N)+.05)
2540 C(J)=G(J,H):G(J,H)=G(J,N)
2550 NEXTJ
2560 N=N-1:IFN<0 THENN=8
2570 IFN=8 THEN GOSUB2460
2580 IFES=1 THEN2720
2590 CV=10-C(1):IFCV=1 THENCV=4
2600 FORJ=0 TO2
2610 SR=C(J):GOSUB300:C$(J)=SR$
2620 P(J)=C(J)*10^J
2630 SR=P(J):GOSUB300:P$(J)=SR$
2640 D(J)=10^J:PP(J)=P(J)*CV
2650 SR=PP(J):GOSUB300:PP$(J)=SR$
2660 SR=D(J):GOSUB300:D$(J)=SR$
2670 NEXTJ
2680 SR=CV:GOSUB300:CV$=SR$
2690 PS=PP(0)+PP(1)+PP(2)
2700 SR=PS:GOSUB300:PS$=SR$
2710 GT$=C$(2)+C$(1)+C$(0)
2720 RETURN
2730 REM ***> REAGEREN OP FOUTEN <***
2740 FT=FT+1
2750 HF=HO:VF=VE:TD$=DA$
2760 HO=0:VE=.8
2770 DA$=IN$+" IS FOUT"
2780 GOSUB1210:DE$=DA$
2790 REM
2800 DA$="PROBEER "+KT$+" EENS"
2810 VE=.88:GOSUB1210
2820 W=2:GOSUB3110
2830 CN=1:GOSUB1210
2840 DA$=DE$:VE=.8:GOSUB1210:CN=0
2850 HO=HF:VE=VF:DA$=TD$
2860 RETURN
2870 REM  *****> BUFFER LEEG <******
2880 GOSUB200:IFIN$<>"" THEN2880
2890 GOSUB210:A=IN
2900 RETURN
2910 REM ***> SPATIEBALK TEKST <****
2920 HS=HO:VS=VE
2930 HO=0:VE=23
2940 DA$="    >>>> SPATIEBALK !!!! >>>>"
2950 GOSUB1410
2960 GOSUB2880:IFA<>32 THEN2960
2970 DA$="                             "
2980 GOSUB1410
2990 HO=HS:VE=VS
3000 RETURN
3010 REM ***> SPATIEBALK GRAF <****
3020 CI=CN:HS=HO:VS=VE
3030 HO=0:VE=1-10/VG
3040 DA$="   >>>> SPATIEBALK !!!! >>>>"
3050 GOSUB1210
3060 GOSUB2880:IFA<>32 THEN3060
3070 CN=1:GOSUB1210:CN=CI
3080 HO=HS:VE=VS
3090 RETURN
3100 REM ****> WACHTLUS <****
3110 FORW1=1 TOW*500:NEXTW1
3120 RETURN
3130 REM ****> PROGRAMMA <****
3140 REM ***> MENU <***
3150 GOSUB100:HO=0:VE=0
3160 PR$="menu":GOSUB1310
3170 VI=VE:GOSUB2880:TY=A-48
3180 IF(TY<1)OR(TY>5) THEN3170
3190 SR=TY:GOSUB300:TY$=SR$:VE=VI+2
3200 DA$="JE KEUZE IS : ":GOSUB1410
3210 GOSUB120:PR$=TY$:GOSUB1310
3220 HO=0:VE=VE+2
3230 DA$="IS DAT DE GOEDE KEUZE (J/n) ?"
3240 GOSUB1410
3250 GOSUB2880
3260 IF(IN<>13)AND(IN<>74) THEN3150
3270 IFTY=5 THEN3300
3280 ONTY GOSUB3320,3360,3700,4370
3290 GOTO3150
3300 GOTO950
3310 REM ***> DOEL PROGRAMMA <***
3320 HO=0:VE=0:GOSUB100
3330 PR$="programma":GOSUB1310
3340 RETURN
3350 REM ***> WAT JE AL WETEN MOET <***
3360 HO=0:VE=0:GOSUB100
3370 PR$="weten":GOSUB1310
3380 GOSUB600:HO=0:VE=0
3390 VB=1
3400 C(2)=4:C(1)=6:C(0)=3:CV=1:GOSUB2600
3410 DA$=" "+GT$+" is :":GOSUB1210
3420 VB=1:FL=0:SS$=PS$:GOSUB2100
3430 VZ=.12:HI=7*DH:GOSUB1880
3440 VE=.32:GOSUB2040
3450 DB$=B3$
3460 VI=.36:GOSUB1670
3470 GOSUB3020
3480 GOSUB100:HO=0:VE=0
3490 PR$="oefening 1"
3500 GOSUB1310
3510 REM ****> OEFENING 1 <****
3520 GOSUB2460:N=8
3530 GOSUB600:HO=0:VE=0:FT=0
3540 ES=1:GOSUB2520
3550 CV=1:GOSUB2600
3560 DA$=" "+GT$+" is dan :":GOSUB1210
3570 FL=1:SS$=PS$:GOSUB2100
3580 VZ=.12:HI=7*DH:GOSUB1880
3590 VE=.32:GOSUB2040
3600 DA$=A3$
3610 HO=0:VE=.36:GOSUB1210
3620 DB$=B3$
3630 GOSUB1750
3640 GOSUB2390
3650 IFFT>1 THEN3530
3660 IFA=74 THEN3530
3670 VB=0
3680 RETURN
3690 REM ***> WAT ER NOG BIJ KOMT  A <***
3700 GOSUB100:HO=0:VE=0
3710 PR$="1 cijfer-1":GOSUB1310
3720 GOSUB600:HO=0:VE=0
3730 V=1:F=1:GOSUB3820
3740 GOSUB100:HO=0:VE=0
3750 PR$="1 cijfer-2":GOSUB1310
3760 GOSUB600:HO=0:VE=0
3770 V=7:F=0:GOSUB3820
3780 GOSUB600:HO=0:VE=0
3790 GOSUB4120
3800 RETURN
3810 REM ***> OEFENINGEN 2 EN 2A <***
3820 C(2)=4:C(1)=6:C(0)=3:CV=V:GOSUB2600
3830 FL=0:SS$=PS$:GOSUB2100
3840 DA$=" "+GT$+" x "+CV$+" wordt dan :"
3850 GOSUB1210
3860 VZ=.12:HI=7*DH:GOSUB1880
3870 VE=.32:GOSUB2040
3880 DB$=B3$
3890 HO=0:VI=.36:GOSUB1670
3900 HO=0:VE=.48
3910 PR$="oefening":GOSUB1110
3920 GOSUB2460:N=8
3930 GOSUB600:HO=0:VE=0:FT=0
3940 IFF<>1 THEN3970
3950 ES=1:GOSUB2520:CV=V:GOSUB2600
3960 GOTO3980
3970 ES=0:GOSUB2520
3980 DA$=" "+GT$+" x "+CV$+" gaat als :"
3990 GOSUB1210
4000 SS$=PS$:GOSUB2100
4010 FL=1:VZ=.12:HI=7*DH:GOSUB1880
4020 VE=.32:GOSUB2040
4030 DA$=A3$
4040 HO=0:VE=.36:GOSUB1210
4050 DB$=B3$
4060 GOSUB1750
4070 GOSUB2390
4080 IFFT>1 THEN3930
4090 IFA=74 THEN3930
4100 RETURN
4110 REM ***> OEFENING 3 <***
4120 PR$="oefening 3":GOSUB1110
4130 GOSUB2460:N=8
4140 GOSUB600:HO=0:VE=0:FT=0
4150 GOSUB2520
4160 FL=1:VO=1:SS$=PS$:GOSUB2100
4170 LE=LEN(B3$)
4180 HO=(LE-LEN(GT$))*DH
4190 DA$=GT$:GOSUB1210
4200 HO=(LE-LEN(CV$))*DH
4210 DA$=CV$:VE=.08:GOSUB1210
4220 DA$="---x":HO=(LE-3)*DH
4230 VE=.12:GOSUB1210
4240 HI=7*DH:VZ=.16:ZS=1
4250 GOSUB1880
4260 VE=.36:GOSUB2040
4270 DA$=A3$
4280 HO=0:VE=.4:GOSUB1210
4290 DB$=B3$
4300 GOSUB1750
4310 GOSUB2390
4320 IFFT>1 THEN4130
4330 IFA=74 THEN4130
4340 VO=0
4350 RETURN
4360 REM ***> WAT ER NOG BIJ KOMT B <***
4370 GOSUB100:HO=0:VE=0
4380 PR$="2 cijfers":GOSUB1310
4390 GOSUB600:HO=0:VE=0
4400 C(2)=4:C(1)=6:C(0)=3:CV=50:GOSUB2600
4410 XX=PS
4420 CV=7:GOSUB2600
4430 VO=1:VB=3:FL=0:GOSUB4590
4440 VB=0:FL=1
4450 HO=0:VE=.72
4460 PR$="oefening"
4470 GOSUB1110
4480 REM ***> VOORB-3 EN OEFENING-4 <***
4490 GOSUB2460:N=8
4500 GOSUB600:HO=0:VE=0
4510 ES=1:GOSUB2520:FT=0
4520 DE=C(0):DT=C(2)*10:DE$=C$(0)
4530 DG=DT+DE:SR=DG
4540 GOSUB300:DG$=SR$
4550 GOSUB2520
4560 CV=DT:GOSUB2600:XX=PS
4570 CV=DE
4580 GOSUB2600
4590 SR=XX+PS:GOSUB300:PS$=SR$
4600 SS$=PS$:GOSUB2230
4610 FL=1:IFVB=3 THENFL=0
4620 GOSUB2100
4630 LE=LEN(B3$)
4640 HO=(LE-LEN(GT$))*DH
4650 DA$=GT$:GOSUB1210
4660 CW$=DG$:IFVB=3 THENCW$="57"
4670 HO=(LE-LEN(CW$))*DH
4680 VE=.08:DA$=CW$:GOSUB1210
4690 DA$="---x":HO=(LE-3)*DH
4700 VE=.12:GOSUB1210
4710 VZ=.16:ZS=1:HI=7*DH
4720 GOSUB1880
4730 CV=DT:IFVB=3 THENCV=50
4740 GOSUB2600
4750 GOSUB2100
4760 VZ=.4:ZS=1:HI=6*DH
4770 GOSUB1880
4780 VE=.6:GOSUB2040
4790 DA$=A3$:DB$=B3$
4800 IFVB=3 THENVI=.64:GOSUB1670:RETURN
4810 HO=0:VE=.64:GOSUB1210
4820 GOSUB1750
4830 GOSUB2390
4840 IFFT>1 THEN4500
4850 IFA=74 THEN4500
4860 RETURN
25000 REM ****> AFKORTINGEN <****
25010 REM ns IS NIEUW SCHERM
25020 REM ro IS REGEL OVERSLAAN
25030 REM sp IS WACHTEN OP SPATIEBALK
25040 REM ***> MENU <***
25050 DATA"menu"
25060 DATA" BEGINNEN AAN VERMENIGVULDIGEN"
25070 DATA"DOEL PROGRAMMA --------------> 1"
25080 DATA"WAT JE AL WETEN MOET --------> 2"
25090 DATA"MET EEN CIJFER --------------> 3"
25100 DATA"MET TWEE CIJFERS ------------> 4"
25110 DATA"STOPPEN ---------------------> 5"
25120 DATA"TYPE HET KEUZE CIJFER IN.","einde"
25130 DATA"1","DOEL PROGRAMMA","einde"
25140 DATA"2","WAT JE AL WETEN MOET","einde"
25150 DATA"3","MET EEN CIJFER","einde"
25160 DATA"4","MET TWEE CIJFERS","einde"
25170 DATA"5","STOPPEN","einde"
25180 REM ***> HET PROGRAMMA <***
25190 DATA"programma"
25200 DATA"Dit programma is een oefen les.","ro","ro"
25210 DATA"Het laat de opbouw van een getal zien.","ro"
25220 DATA"Het leert een eenvoudige manier van"
25230 DATA"vermenigvuldigen.","ro","ro"
25240 DATA"Na elke uitleg moet je laten zien"
25250 DATA"dat je het hebt begrepen.","ro","ro"
25260 DATA"  ******>   SUCCES   <******","sp"
25270 DATA"einde"
25280 REM ***> WAT JE AL WETEN MOET <***
25290 DATA"weten"
25300 DATA"Dit programma verwacht van je","ro"
25310 DATA"1. Dat je de tafels kunt dromen.","sp"
25320 DATA"2. Dat je foutloos optellen kan.","sp"
25330 DATA"3. Dat je weet dat een getal opgebouwd"
25340 DATA"   is uit :  >> eenheden"
25350 DATA"             >> tientallen"
25360 DATA"             >> honderdtallen"
25370 DATA"             >> en zo verder.","ro","sp"
25380 DATA"Punt 3 wordt nog kort uitgelegd.","sp","ns"
25390 DATA"einde"
25400 REM ****> OEFENING 1 <****
25410 DATA"oefening 1"
25420 DATA"We gaan even oefenen.","ro"
25430 DATA"Op het scherm komt weer een getal.","ro"
25440 DATA"Van dat getal wordt net als in het"
25450 DATA"voorbeeld een schema gemaakt.","sp","ro"
25460 DATA"Op plaatsen waar cijfers moeten komen"
25470 DATA"staan punten.","sp","ro"
25480 DATA"Jij gaat op die punten de goede cijfers"
25490 DATA"invullen.","sp","ns"
25500 DATA"Een pijltje wijst aan welk punt er aan"
25510 DATA"de beurt is om ingevuld te worden.","sp","ro"
25520 DATA"Het programma laat je fouten zien en"
25530 DATA"help je verder.","ro"
25540 DATA"Maar fouten worden wel geteld en het"
25550 DATA"programma is erg streng.","ro","sp"
25560 DATA"Als je twee of meer fouten maakt dan"
25570 DATA"krijg je nog een oefening.","ro","ro"
25580 DATA"     >>>>>> SUCCES <<<<<<<","sp"
25590 DATA"einde"
25600 REM ***> MET EEN CIJFER <***
25610 DATA"1 cijfer-1"
25620 DATA"We beginnen met een erg flauw voorbeeld.","ro"
25630 DATA"Je krijgt te zien hoe je 463 met 1"
25640 DATA"vermenigvuldigen kan.","ro","ro"
25650 DATA"Daarna volgt een oefening.","sp","ns"
25660 DATA"einde"
25670 REM ***> OEFENING AANZEGGEN <***
25680 DATA"oefening"
25690 DATA"Dan komt nu de oefening.","ro"
25700 DATA"    >>>>>>> SUCCES <<<<<<<","sp"
25710 DATA"einde"
25720 DATA"1 cijfer-2"
25730 DATA"We gaan het schema nu gebruiken om"
25740 DATA"463 met 7 te vermenigvuldigen","sp","ro","ro"
25750 DATA"Let goed op want hierna komt weer"
25760 DATA"een oefening.","sp","ns"
25770 DATA"einde"
25780 REM ***> OEFENING 3 <****
25790 DATA"oefening 3"
25800 DATA"Je gaat hetzelfde nog een keer"
25810 DATA"doen.","ro"
25820 DATA"Maar nu met de getallen onder"
25830 DATA"elkaar.","ro"
25840 DATA"  >>>>>>> SUCCES <<<<<<<","sp"
25850 DATA"einde"
25860 REM ***> MET TWEE CIJFERS <****
25870 DATA"2 cijfers"
25880 DATA"Nu krijg je te zien hoe we op de zelfde"
25890 DATA"manier 463 met 57 vermenigvuldigen.","ro","ro"
25900 DATA"Let goed op want hierna mag jij het weer"
25910 DATA"doen.","sp"
25920 DATA"einde"
25930 REM ***> FOUTEN AFHANDELEN <***
25940 DATA"ft0","Je hebt 0 fouten."
25950 DATA"Keurig gedaan. Nog eens (J/n)?"
25960 DATA"einde"
25970 DATA"ft1","Je hebt 1 fout."
25980 DATA"Knap gewerkt. Nog eens (J/n)?"
25990 DATA"einde"
26000 DATA"ft2","Je hebt meer dan 1 fout."
26010 DATA"Je krijgt nog een oefening.","sp"
26020 DATA"einde"
30000 REM ****************************
30010 REM DIT PROGRAMMA IS BEDOELD ALS
30020 REM EEN AANLOOP NAAR HET ECHTE
30030 REM VERMENIGVULDIGEN.
30040 REM DOOR DE FACTOR 500 IN DE
30050 REM WACHTLUS IN REGEL 3110 TE
30060 REM VERANDEREN KAN DE SNELHEID
30070 REM VAN DE VOORBEELDEN VERGROOT
30080 REM WORDEN.
30090 REM ****************************
32000 REM ****************************
32010 REM J.WIELES
32020 REM HOOFDWAL 68
32030 REM 4207 EE GORINCHEM
32040 REM 01830-30492
32050 REM OKTOBER 1989
32060 REM ****************************
32070 REM
32080 REM TROS-RADIO dd 900124

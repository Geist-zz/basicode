
1000 GOSUB270:A=FR-2000:GOTO20:REM ** POSTZEGELS **
1010 GOSUB270:H=INT(FR/60)
1020 REM ** INSTRUCTIE **
1030 GOSUB100:VE=11:HO=5:GOSUB110
1040 PRINT"Heeft u de instructie"
1050 VE=13:HO=10:GOSUB110:PRINT"gelezen  J/N";
1060 GOSUB210:IFIN=74 THEN1120
1070 IFIN<>78 THEN GOSUB250:GOTO1060
1080 VE=15:HO=5:GOSUB110:PRINT"Doet u dat dan eerst voor"
1090 VE=17:GOSUB110:PRINT"dat u het programma start"
1100 SD=50:GOSUB450:GOSUB250:GOTO950
1110 REM ** LOGO **
1120 GOSUB100:V=4
1130 READA:IFA=0 THENV=V+1:GOTO1130
1140 IFA=40 THEN GOSUB10010:GOTO1170
1150 VE=V:HO=A:GOSUB110:PRINT"*":GOTO1130
1160 REM ** INITIALISATIE **
1170 M=1:J=1:SD=0:SV=0:SR=0:SR$=""
1180 DIMA$(H):REM ** H = MAX. AANTAL POSTEN **
1190 DIMCO((LOG(H)/LOG(2)+4),1),SO(1,1)
1200 L$="":G$="":JA$=""
1210 LS$="                                          "
1220 LK$="       "
1230 GOTO2010
1500 REM ****** SUBROUTINES ******
1510 REM ** BESTAND WISSEN **
1520 GOSUB100:FORK=1 TOM:A$(K)="":NEXTK
1530 J=1:L$="":JA$="":G$="":NM$="":M=1
1540 VE=4:HO=10:GOSUB110:PRINT"bestand gewist";
1550 SD=40:GOSUB450:RETURN
1560 REM ** EINDE PROGRAMMA **
1570 IFM<=1 THEN1630
1580 GOSUB100:VE=10:HO=0:GOSUB110
1590 PRINT" Heeft u het bestand weggeschreven"
1600 VE=12:GOSUB110:PRINT" naar band of diskette J/N ";
1610 GOSUB210:IFIN=78 THEN5010
1620 IFIN<>74 THEN1610
1630 GOSUB100:VE=12:HO=12:GOSUB110:PRINT"** TOT ZIENS **"
1640 SD=50:GOSUB450:GOTO950
1650 REM ** SPATIE ROUTINE HOOFDMENU **
1660 VE=21:HO=6:GOSUB110
1670 SR$="VOOR MENU DRUK SPATIEBALK":GOSUB150:GOSUB210
1680 IFIN=32 THEN2010
1690 GOSUB250:GOTO1660
1700 REM ** SPATIE ROUTINE SUBMENU **
1710 VE=21:HO=6:GOSUB110
1720 SR$="VOOR MENU DRUK SPATIEBALK":GOSUB150:GOSUB210
1730 IFIN=32 THENRETURN
1740 GOSUB250:GOTO1710
1750 REM ** GEEN BESTAND **
1760 IFM<>1 THENRETURN
1770 VE=21:HO=0:GOSUB110:PRINTLS$
1780 HO=6:GOSUB110
1790 SR$="GEEN BESTAND AANWEZIG!!":GOSUB150
1800 SD=20:GOSUB450:GOTO1660
2000 REM ** MENU **
2010 GOSUB100:VE=1:HO=12:GOSUB110
2020 SR$="*** MENU ***":GOSUB150
2030 PRINT:PRINT:PRINT"  1 nieuw/aanvullen bestand"
2040 PRINT:PRINT"  2 lezen/schrijven"
2050 PRINT:PRINT"  3 printen"
2060 PRINT:PRINT"  4 bestand bekijken"
2070 PRINT:PRINT"  5 sorteren"
2080 PRINT:PRINT"  6 wijzigen/wissen"
2090 PRINT:PRINT"  7 overzicht rubrieken"
2100 PRINT:PRINT"  8 postzegels"
2110 PRINT:PRINT"  9 einde programma"
2120 VE=21:HO=0:GOSUB110:SR$="file":GOSUB150
2130 HO=8:GOSUB110:PRINT" ";L$;" : ";G$;" ";JA$
2140 VE=23:HO=13:GOSUB110:PRINT"MAAK UW KEUZE  ";
2150 GOSUB210:I=VAL(IN$)
2160 IF(I<1)OR(I>9) THEN GOSUB250:GOTO2150
2170 IFI>6 THEN2190
2180 ONI GOTO2510,5010,9010,7010,8010,8510
2190 ONI-6 GOTO2200,2210,1570
2200 Y=1:GOTO3020:REM ** OVERZ RUBRIEKEN **
2210 Y=0:GOTO3010:REM ** POSTZEGELS **
2500 REM ** INVOER POSTEN **
2510 IFJ>=H-1 THEN GOSUB100:R=1:GOTO2680
2515 IFA$(J)="**" THENJ=J
2520 GOSUB100:A=0:E=3:R=4:VE=1:HO=0:GOSUB110
2530 SR$="na invoer druk return   x = menu":GOSUB150
2540 B=34:C=12:G=19:X=6:I=4
2550 IFJ>9 THENB=35:C=13:G=20:I=6
2560 IFJ>99 THENB=36:C=14:G=21:I=7
2570 VE=E:HO=C:GOSUB110:PRINT"I"
2580 HO=G:GOSUB110:PRINT"I"
2590 HO=B:GOSUB110:PRINT"I"
2600 SR=J:GOSUB300:VE=R:HO=0:GOSUB110
2610 PRINT"nr";SR$;
2620 VE=R:HO=I:GOSUB110:INPUTP$
2630 SR$=P$:GOSUB330:IFSR$="X" THENQ=1:GOTO2670
2640 A$(J)=P$:J=J+1:M=J:A=A+1:E=E+3:R=R+3
2650 IFA=5 THEN2520
2660 IFJ<H-1 THEN2540
2670 A$(J)="**":M=J:IFQ=1 THEN2010
2680 R=R+3:VE=R:HO=I:GOSUB110:SR$=" BESTAND VOL ";
2690 GOSUB150:GOTO1660
3000 REM ** POSTZEGELS **
3010 GOSUB1760:GOSUB3020:GOTO6010
3020 GOSUB100
3030 VE=1:HO=12:GOSUB110
3040 SR$="POSTZEGELS":IFY=1 THENSR$="RUBRIEKEN"
3050 GOSUB150
3060 PRINT:PRINT:PRINT"  1  NEDERLAND"
3070 PRINT:PRINT"  2  INDONESIE"
3080 PRINT:PRINT"  3  REPUBLIEK SURINAME"
3090 PRINT:PRINT"  4  EUROPA ZEGELS"
3100 PRINT:PRINT"  5  NEDERLANDS NIEUW-GUINEA"
3110 PRINT:PRINT"  6  NEDERLANDSE ANTILLEN"
3120 PRINT:PRINT"  7  CURACAO"
3130 PRINT:PRINT"  8  SURINAME"
3140 PRINT:PRINT"  9  BOEKEN ENZ."
3150 VE=21:HO=8:GOSUB110
3160 PRINT"Welke rubriek wilt u zien ? ";
3170 GOSUB210:I=VAL(IN$)
3180 IF(I<1)OR(I>9) THEN GOSUB250:GOTO3170
3190 ONI GOTO3200,3370,3480,3560,3610,3680,3740,3800,3870
3200 GOSUB100:VE=1:HO=12:GOSUB110
3210 SR$="NEDERLAND":GOSUB150:PRINT:PRINT
3220 PRINT"  01 frankeerzegels               p.fr."
3230 PRINT"  02 ..............               gest."
3240 PRINT"  03 ..............               stokv."
3250 PRINT"  04 1e dag-enveloppen"
3260 PRINT"  05 .................            stokv."
3270 PRINT"  06 automaatboekjes"
3280 PRINT"  07 combi uit automaatb."
3290 PRINT"  08 ....................         stokv."
3300 PRINT"  09 luchtpostzegels              p.fr."
3310 PRINT"  10 ...............              gest."
3320 PRINT"  11 portzegels                   p.fr."
3330 PRINT"  12 ..........                   gest."
3340 PRINT"  13 dienstzegels"
3350 IFY=1 THEN1660
3360 GOTO3930
3370 GOSUB100:VE=1:HO=12:GOSUB110
3380 SR$="INDONESIE":GOSUB150:PRINT:PRINT
3390 PRINT"  14 frankeerzegels               p.fr."
3400 PRINT:PRINT"  15 postzegelboekjes             p.fr."
3410 PRINT:PRINT"  16 portzegels                   p.fr."
3420 PRINT:PRINT"  17 dienstzegels                 p.fr."
3430 PRINT:PRINT"  18 noodportzegels               p.fr."
3440 PRINT:PRINT"  19 riau                         p.fr."
3450 PRINT:PRINT"  20 irian barat                  p.fr."
3460 IFY=1 THEN1660
3470 GOTO3930
3480 GOSUB100:VE=1:HO=8:GOSUB110
3490 SR$="REPUBLIEK SURINAME":GOSUB150:PRINT:PRINT
3500 PRINT"  21 frankeerzegels               p.fr."
3510 PRINT:PRINT"  22 1e dag-enveloppen"
3520 PRINT:PRINT"  23 carnet's"
3530 PRINT:PRINT"  24 postzegelboekjes"
3540 IFY=1 THEN1660
3550 GOTO3930
3560 GOSUB100:VE=1:HO=10:GOSUB110
3570 SR$="EUROPA ZEGELS":GOSUB150
3580 PRINT:PRINT:PRINT"  25 europa zegels"
3590 IFY=1 THEN1660
3600 GOTO3930
3610 GOSUB100:VE=1:HO=5:GOSUB110
3620 SR$="NEDERLANDS NIEUW-GUINEA":GOSUB150
3630 PRINT:PRINT:PRINT"  26 frankeerzegels"
3640 PRINT:PRINT"  27 portzegels"
3650 PRINT:PRINT"  28 undea"
3660 IFY=1 THEN1660
3670 GOTO3930
3680 GOSUB100:VE=1:HO=7:GOSUB110
3690 SR$="NEDERLANDSE ANTILLEN":GOSUB150:PRINT:PRINT
3700 PRINT"  29 frankeerzegels              p.fr."
3710 PRINT:PRINT"  30 automaatboekjes"
3720 IFY=1 THEN1660
3730 GOTO3930
3740 GOSUB100:VE=1:HO=13:GOSUB110
3750 SR$="CURACAO":GOSUB150:PRINT:PRINT
3760 PRINT"  31 luchtpostzegels               p.fr."
3770 PRINT:PRINT"  32 portzegels                    p.fr."
3780 IFY=1 THEN1660
3790 GOTO3930
3800 GOSUB100:VE=1:HO=13:GOSUB110
3810 SR$="SURINAME":GOSUB150:PRINT:PRINT
3820 PRINT"  33 frankeerzegels                p.fr."
3830 PRINT:PRINT"  34 luchtpostzegels               p.fr."
3840 PRINT:PRINT"  35 portzegels                    p.fr."
3850 IFY=1 THEN1660
3860 GOTO3930
3870 GOSUB100:VE=1:HO=11:GOSUB110
3880 SR$="BOEKEN ENZ.":GOSUB150
3890 PRINT:PRINT:PRINT"  36 boeken enz. "
3900 PRINT:PRINT"  37 totaal overzicht"
3910 IFY=1 THEN1660
3920 REM ** INPUT Z$ **
3930 VE=20:HO=3:GOSUB110
3940 INPUT"Welke groep wilt u zien :";Z$
3950 REM ** OMZETTEN VAN Z$ IN B$ **
3960 ES=VAL(LEFT$(Z$,2)):IFES>9 THEN4070
3970 ONES GOTO3980,3990,4000,4010,4020,4030,4040,4050,4060
3980 B$="frankeerzegels p.fr.":RETURN
3990 B$="frankeerzegels gest.":RETURN
4000 B$="frankeerzegels stokv.":RETURN
4010 B$="1e dag-enveloppen":RETURN
4020 B$="1e dag-enveloppen stokv.":RETURN
4030 B$="automaatboekjes":RETURN
4040 B$="comb.uit aut.m.b.":RETURN
4050 B$="comb.uit aut.m.b.stokv.":RETURN
4060 B$="luchtpostzegels p.fr.":RETURN
4070 ES=ES-9:IFES>9 THEN4180
4080 ONES GOTO4090,4100,4110,4120,4130,4140,4150,4160,4170
4090 B$="luchtpostzegels gest.":RETURN
4100 B$="portzegels p.fr.":RETURN
4110 B$="portzegels gest.":RETURN
4120 B$="dienstzegels gest.":RETURN
4130 B$="frankeerzegels p.fr.":RETURN
4140 B$="postzegelboekjes p.fr.":RETURN
4150 B$="portzegels p.fr.":RETURN
4160 B$="dienstzegeh gest.":RETURN
4170 B$="noodportzegels p.fr.":RETURN
4180 ES=ES-9:IFES>9 THEN4290
4190 ONES GOTO4200,4210,4220,4230,4240,4250,4260,4270,4280
4200 B$="riau    p.fr.":RETURN
4210 B$="irian barat  p.fr.":RETURN
4220 B$="frankeerzegels p.fr.":RETURN
4230 B$="1e dag-enveloppen":RETURN
4240 B$="carnet's":RETURN
4250 B$="postzegelboekjes p.fr.":RETURN
4260 B$="europazegels":RETURN
4270 B$="frankeerzegels p.fr.":RETURN
4280 B$="portzegels p.fr.":RETURN
4290 ES=ES-9:IFES>9 THEN4400
4300 ONES GOTO4310,4320,4330,4340,4350,4360,4370,4380,4390
4310 B$="untea":RETURN
4320 B$="frankeerzegels p.fr.":RETURN
4330 B$="automaatboekjes":RETURN
4340 B$="luchtpostzegels p.fr.":RETURN
4350 B$="portzegels p.fr.":RETURN
4360 B$="frankeerzegels p.fr.":RETURN
4370 B$="luchtpostzegels p.fr.":RETURN
4380 B$="portzegels p.fr.":RETURN
4390 B$="boeken enz.":RETURN
4400 IFZ$="37" THENB$="totaal overzicht":RETURN
4410 GOSUB250:GOTO3930
5000 REM ** MENU CASSETTE/DISKETTE **
5010 GOSUB100:VE=2:HO=3:GOSUB110
5020 SR$="Welk soort opslag gebruikt u":GOSUB150
5030 PRINT:PRINT:PRINT"  1 Basicode-3 cassette"
5040 PRINT:PRINT"  2 Eigen systeem cassette"
5050 PRINT:PRINT"  3 Eigen systeem diskette"
5060 PRINT:PRINT"  4 Diskette"
5070 PRINT:PRINT"  5 Terug naar hoofdmenu"
5080 PRINT:PRINT"  Welk nummer kiest u  ";
5090 GOSUB210:PRINTIN$:I=VAL(IN$)
5100 IF(I<1)OR(I>5) THEN GOSUB250:GOTO5090
5110 ONI GOTO5120,5130,5140,5150,2010
5120 L=0:S=1:C=1:GOTO5170
5130 L=2:S=3:C=1:GOTO5170
5140 L=2:S=3:C=0:GOTO5170
5150 L=4:S=5:C=0
5160 REM ** MENU LEZEN/SCHRIJVEN **
5170 PRINT:PRINT"  1 schrijven"
5180 PRINT:PRINT"  2 lezen"
5190 PRINT:PRINT"  3 terug naar vorig-menu"
5200 PRINT:PRINT"  welk nummer kiest u :";
5210 GOSUB210:I=VAL(IN$)
5220 IF(I<1)OR(I>3) THEN GOSUB250:GOTO5210
5230 ONI GOTO5250,5390,5010
5240 REM ** FILE SCHRIJVEN **
5250 GOSUB1760
5260 GOSUB100:VE=5:HO=10:GOSUB110
5270 SR$="ZEKER WETEN  J/N":GOSUB150:GOSUB210
5280 IFIN=78 THEN GOSUB1710:GOTO5010
5290 IFIN<>74 THEN5260
5300 GOSUB100:VE=2:HO=14:GOSUB110
5310 SR$="SAVEN":GOSUB150
5320 GOSUB5830:NF=S:GOSUB5660
5330 IFC=1 THEN GOSUB5940
5340 PRINT:PRINT"Bestandsnaam wordt: ";NF$
5350 GOSUB500:IFIN=-1 THEN5300
5360 FORJ=1 TOM:SR$=A$(J):GOSUB560:NEXTJ
5370 GOSUB580:GOSUB1710:J=M:GOTO2010
5380 REM ** FILE LEZEN **
5390 IFM>1 THEN5410
5400 GOTO5530
5410 GOSUB100:PRINT:PRINT:PRINT"       Heeft u het bestand"
5420 PRINT:PRINT;L$;" ";G$;" ";JA$
5430 PRINT:PRINT"           al weggeschreven  J/N :";
5440 GOSUB210:IFIN=78 THEN5520
5450 IFIN=74 THEN GOSUB1520:GOTO5530
5460 GOSUB250:GOTO5480:PRINT:PRINT
5470 PRINT"Doet u dat dan eerst voordat u een nieuw"
5480 PRINT:PRINT"bestand inleest."
5490 PRINT:PRINT"Het oude bestand wordt namelijk gewist"
5500 PRINT:PRINT"bij het inlezen van een nieuw bestand."
5510 PRINT:PRINT:PRINT"               DRUK EEN TOETS";
5520 GOSUB210:GOTO5010
5530 GOSUB100:J=1:VE=2:HO=14:GOSUB110
5540 SR$="LEZEN":GOSUB150
5550 GOSUB5830:GOSUB5660:NF=L
5560 IFC=1 THEN GOSUB5940
5570 GOSUB500:IFIN=-1 THEN5530
5580 GOSUB540:A$(J)=IN$
5590 IFIN<>1 THENJ=J+1:GOTO5580
5600 GOSUB580:M=J
5610 GOSUB100:VE=3:HO=10:GOSUB110:PRINT"AANTAL POSTEN";
5620 HO=27:GOSUB110:PRINTJ
5630 VE=5:HO=10:GOSUB110:PRINT"ALLE POSTEN GELEZEN"
5640 GOSUB1710:GOTO2010
5650 REM ** OMZETTEN NM$ IN L$ **
5660 SR$=NM$:GOSUB330
5670 IFSR$="NED" THENL$="NEDERLAND":RETURN
5680 IFSR$="IND" THENL$="INDONESIE":RETURN
5690 IFSR$="REP SUR" THENLS="REPUBLIEK SURINAME":RETURN
5700 IFSR$="EURO" THENL$="EUROPA ZEGELS":RETURN
5710 IFSR$="NED ANT" THEN5800
5720 IFSR$="OVERZ" THENL$="TOTAAL OVERZICHT":RETURN
5730 IFSR$="CUR" THENL$="CURACAO":RETURN
5740 IFSR$="SUR" THENL$="SURINAME":RETURN
5750 IFSR$="BOEKEN" THENL$="BOEKEN":RETURN
5760 IFSR$="NED NIEUW" THEN5810
5770 GOSUB250:VE=15:HO=0:GOSUB110
5780 PRINT"Land omschrijving niet juist ingevoerd!";
5790 SD=50:GOSUB450:NM$="":G$="":JA$="":GOTO5530
5800 L$="NEDERLANDSE ANTILLEN":RETURN
5810 L$="NEDERLANDS NIEUW-GUINEA":RETURN
5820 REM ** INPUT ROUTINE **
5830 VE=5:HO=6:GOSUB110:INPUT"WELK LAND   ";NM$
5840 VE=7:GOSUB110:INPUT"WELKE GROEP ";G$
5850 VE=9:GOSUB110:INPUT"WELK JAAR   ";JA$
5860 NF$=G$+JA$:PRINT:PRINT"Bestandsnaam wordt: ";NF$
5870 VE=13:GOSUB110:PRINT"Is dit juist  J/N"
5880 VE=15:GOSUB110:PRINT"  MENU toets X   ";
5890 GOSUB210:IFIN=74 THENRETURN
5900 IFIN=78 THENNM$="":G$="":JA$="":GOTO5830
5910 IFIN=88 THENNM$="":G$="":JA$="":GOTO5010
5920 GOSUB250:GOTO5890
5930 REM ** CASSETTE ROUTINE **
5940 GOSUB100:PRINT:PRINT
5950 PRINT"       Zet de cassette aan het"
5960 PRINT:PRINT"       begin van het bestand"
5970 PRINT:PRINT"  ";L$;" ";G$;" ";JA$
5980 PRINT:PRINT"       Druk een toets";:GOSUB210
5990 RETURN
6000 REM ** PRINT POSTEN + BEDRAGEN OP HET SCHERM **
6010 GOSUB100:L=LEN(Z$):J=1:T=0
6020 A=0:SR=0:SR$=""
6030 H$=A$(J):IF(LEFT$(H$,L)<>Z$)AND(Z$<>"37") THEN6110
6040 K=LEN(H$):FORI=1 TOK
6050 IFMID$(H$,I,1)="/" THENA=VAL(RIGHT$(H$,K-I))
6060 NEXTI
6070 IFA$(J)="**" THENJ=M:GOTO6130
6080 T=T+A
6090 PRINTMID$(A$(J),4,25);TAB(27);"fl.";
6100 CT=9:CN=2:SR=A:GOSUB310:PRINTSR$
6110 J=J+1:IFJ<=M THEN6020
6120 REM ** TOTAAL **
6130 PRINTTAB(27);"-------------"
6140 IFZ$="37" THENPRINT"   saldo ";B$;"  fl.";:GOTO6170
6150 X=25-LEN(B$)
6160 PRINT"TOTAAL";TAB(X);B$;"  fl.";
6170 CT=9:CN=2:SR=T:GOSUB310:PRINTSR$
6180 T=0:SR$="":PRINT:PRINT
6190 SR$="VOOR MENU DRUK SPATIEBALK":GOSUB150
6200 GOSUB210:IFIN<>32 THEN GOSUB250:GOTO6200
6210 GOTO2010
7000 REM ** MENU BESTAND BEKIJKEN **
7010 GOSUB100:VE=6:HO=5:GOSUB110:PRINT"1 hele lijst"
7020 VE=8:GOSUB110:PRINT"2 neer in lijst"
7030 VE=10:GOSUB110:PRINT"3 op in lijst"
7040 VE=12:GOSUB110:PRINT"4 terug naar hoofdmenu"
7050 VE=15:HO=8:GOSUB110:PRINT"MAAK UW KEUZE :";
7060 GOSUB210:I=VAL(IN$)
7070 IF(I<1)OR(I>4) THEN GOSUB250:GOTO7060
7080 ONI GOTO7600,7280,7100,2010
7090 REM ** OP IN LIJST **
7100 GOSUB100:GOSUB1760
7110 PRINT"Nu wordt de lijst van beneden naar bo-"
7120 PRINT:PRINT"ven getoond."
7130 PRINT:PRINT"Heeft u de regel gevonden toets dan C"
7140 GOSUB7530
7150 FORJ=M TO1 STEP-1
7160 IFIN=67 THEN GOSUB7450
7170 GOSUB200
7180 VE=7:HO=20:GOSUB110:PRINTLK$
7190 VE=7:HO=11:GOSUB110:PRINT"regel nr: ";J
7200 VE=10:HO=1:GOSUB110:PRINTLS$
7210 IFA$(J)="**" THENA$(J)=""
7220 VE=10:HO=1:GOSUB110:PRINTA$(J);
7230 IFJ=1 THEN GOSUB7570:GOTO7250
7240 SD=30:GOSUB450
7250 NEXTJ
7260 J=M:GOSUB1710:GOTO7010
7270 REM ** NEER IN LIJST **
7280 GOSUB100:GOSUB1760:J=1
7290 PRINT"Nu wordt de lijst van boven naar be-"
7300 PRINT:PRINT"neden getoond."
7310 PRINT:PRINT"Heeft u de regel gevonden toets dan C"
7320 GOSUB7530
7330 FORJ=1 TOM
7340 IFIN=67 THEN GOSUB7450
7350 GOSUB200
7360 VE=7:HO=20:GOSUB110:PRINTLK$
7370 VE=7:HO=11:GOSUB110:PRINT"regel nr: ";J
7380 VE=10:HO=1:GOSUB110:PRINTLS$
7390 IFA$(J)="**" THEN GOSUB7570:GOTO7420
7400 VE=10:HO=1:GOSUB110:PRINTA$(J);
7410 SD=30:GOSUB450
7420 NEXTJ
7430 GOSUB1710:J=M:GOTO7010
7440 REM ** VERVOLG ROUTINE **
7450 VE=14:HO=5:GOSUB110:PRINT"Druk V voor vervolg ";
7460 PRINT"M voor menu";
7470 GOSUB210
7480 VE=14:HO=5:GOSUB110:PRINT;LS$
7490 IFIN=86 THENRETURN
7500 IFIN=77 THEN7010
7510 GOSUB250:GOTO7450
7520 REM ** KADER **
7530 VE=9:HO=0:GOSUB110:FORI=1 TO40:PRINT"-";:NEXTI
7540 VE=11:GOSUB110:FORI=1 TO40:PRINT"-";:NEXTI
7550 RETURN
7560 REM ** EINDE BESTAND **
7570 PRINT:PRINT:PRINT"EINDE BESTAND"
7580 RETURN
7590 REM ** LIJST HELEMAAL **
7600 GOSUB1760:GOSUB100:J=1
7610 H$=A$(J):IFH$="**" THEN7640
7620 SD=3:GOSUB450:PRINTH$
7630 J=J+1:IFJ<=H THEN7610
7640 PRINT:PRINT
7650 SR$="VOOR MENU DRUK SPATIEBALK":GOSUB150
7660 GOSUB210:IFIN=32 THEN7010
7670 GOSUB250:GOTO7660
8000 REM ** SORTEREN **
8010 GOSUB1760
8020 GOSUB100:PRINT"EVEN SORTEREN":J=1
8030 IFA$(J)="**" THENJ=J-1:GOTO8050
8040 J=J+1:IFJ<=M THEN8030
8050 T=1:SO(1,0)=1:SO(1,1)=J
8060 L=SO(T,0):R=SO(T,1)
8070 T=T-1
8080 X=L:Y=R
8090 X$=A$((L+R)/2)
8100 IFA$(X)<X$ THENX=X+1:GOTO8100
8110 V=1
8120 IFA$(Y)>X$ THENY=Y-1:V=V+1:IFV<10000 THEN8120
8130 V=10000
8140 IFX=Y THENX=X+1:Y=Y-1:GOTO8100
8150 IFX<Y THEN GOSUB8210:GOTO8100
8160 IFX<R THENT=T+1:SO(T,0)=X:SO(T,1)=R
8170 R=Y
8180 IFL<R THEN8080
8190 IFT>0 THEN8060
8200 PRINT:PRINT"BESTAND GESORTEERD":GOTO1660
8210 TT$=A$(X)
8220 A$(X)=A$(Y)
8230 A$(Y)=TT$
8240 X=X+1
8250 Y=Y-1
8260 RETURN
8500 REM ** MENU WIJZIGEN/WISSEN **
8510 GOSUB100:PRINT:PRINT:PRINT"  1 regel wijzigen"
8520 PRINT:PRINT"  2 regel wissen"
8530 PRINT:PRINT"  3 bestand wissen"
8540 PRINT:PRINT"  4 terug naar hoofdmenu"
8550 PRINT:PRINT"      MAAK UW KEUZE :";
8560 GOSUB210:I=VAL(IN$)
8570 IF(I<1)OR(I>4) THEN GOSUB250:GOTO8560
8580 ONI GOTO8600,8780,8950,2010
8590 REM ** WIJZIGEN **
8600 GOSUB1760:GOSUB100
8610 VE=2:HO=9:GOSUB110:PRINT"return is terug naar menu"
8620 VE=4:GOSUB110:INPUT"Welk nummer ";J$
8630 J=VAL(J$):IFJ=0 THEN8510
8640 IF(J<0)OR(J>M) THEN8630
8650 VE=6:HO=0:GOSUB110:PRINT"  ";A$(J)
8660 VE=8:HO=4:GOSUB110
8670 PRINT"is dit de juiste regel j/n ";
8680 GOSUB210:IFIN=78 THEN8600
8690 IFIN<>74 THEN GOSUB250:GOTO8680
8700 VE=10:HO=6:GOSUB110:PRINT"wijzig!"
8710 VE=12:HO=3:GOSUB110:PRINTA$(J)
8720 VE=12:HO=0:GOSUB110:INPUTA$(J)
8730 VE=14:HO=4:GOSUB110
8740 PRINT"Wilt u nog meer wijzigen ? J/N ";
8750 GOSUB210:IFIN=74 THEN8600
8760 J=M:GOTO8510
8770 REM ** REGEL WISSEN **
8780 GOSUB1760:GOSUB100
8790 VE=2:HO=5:GOSUB110
8800 PRINT"return is terug naar menu"
8810 VE=4:GOSUB110:INPUT"welk nummer ";J$
8820 J=VAL(J$):IFJ=0 THEN8510
8830 IF(J<0)OR(J>M) THEN8810
8840 VE=6:HO=0:GOSUB110:PRINT;J;" ";A$(J)
8850 VE=8:HO=5:GOSUB110
8860 PRINT"is dit de juiste regel ? J/N";
8870 GOSUB210:IFIN=78 THEN8780
8880 FORK=J TOH-1:A$(K)=A$(K+1):NEXTK
8890 VE=10:HO=5:GOSUB110:PRINT"verwijderd"
8900 VE=14:HO=0:GOSUB110
8910 PRINT"wilt u nog meer verwijderen J/N ";
8920 GOSUB210:IFIN=74 THEN8780
8930 J=M:GOTO8510
8940 REM ** BESTAND WISSEN **
8950 GOSUB100:GOSUB1760
8960 FORK=1 TOM:A$(K)="":NEXTK
8970 J=1:L$="":JA$="":G$="":NM$="":M=1
8980 VE=4:HO=10:GOSUB110:PRINT"bestand gewist";
8990 SD=40:GOSUB450:GOTO8510
9000 REM ** PRINTEN **
9010 GOSUB1760:GOSUB100:PRINT:PRINT
9020 PRINT" Wat voor een soort papier gebruikt u."
9030 PRINT:PRINT"        1. Kettingpapier 11 INCH"
9040 PRINT:PRINT"        2. Kettingpapier 12 INCH"
9050 PRINT:PRINT"        3. Losse vellen A4"
9060 PRINT:PRINT"        4. Terug naar hoofdmenu"
9070 PRINT:PRINT"            MAAK UW KEUZE :";:GOSUB210
9080 REM ** RE=AANTAL REGELS PER VEL **
9090 REM ** RS=AANTAL SPATIE'S VOOR PERFORATIE **
9100 IFIN=49 THENRE=55:RS=4:GOTO9150
9110 IFIN=50 THENRE=61:RS=4:GOTO9150
9120 IFIN=51 THENRE=50:RS=0:GOTO9150
9130 IFIN=52 THEN2010
9140 GOSUB250:GOTO9010
9150 Y=0:GOSUB3020
9160 GOSUB100:VE=4:HO=10:GOSUB110:PRINT"Er wordt nu"
9170 VE=6:GOSUB110:PRINTL$;"  ";G$;"  ";JA$
9180 VE=8:GOSUB110:PRINTB$
9190 VE=10:GOSUB110:PRINT"geprint."
9200 GOSUB9720:B=LEN(B$):GOSUB9410:T=0
9210 FORJ=1 TOM:A=0:R=R+1:H$=A$(J)
9220 K=LEN(H$):FORI=1 TOK-1
9230 IFMID$(H$,I,1)="/" THENA=VAL(RIGHT$(H$,K-I))
9240 NEXTI
9250 T=T+A:IFA$(J)="**" THENJ=M:GOTO9320
9260 SB$=MID$(A$(J),4,25):SR$="   "+SB$:GOSUB350
9270 SP$="                                     "
9280 SB$="fl.":SR$=SP$+SB$:GOSUB350
9290 CT=9:CN=2:SR=A:GOSUB310:GOSUB350
9300 IFR=RE THEN GOSUB9630:GOSUB9690:GOSUB9410
9310 GOSUB360:SR$="":Z=0
9320 NEXTJ:R=0:GOSUB360
9330 Z=65:GOSUB9610:SB$="--------------"
9340 SR$=SP$+SB$:GOSUB350:GOSUB360
9350 IFZ$="37" THEN9660
9360 Z=52-B:GOSUB9610:SB$="      totaal "+B$
9370 SF$="fl.":SR$=SB$+SP$+SF$:GOSUB350
9380 CT=9:CN=2:SR=T:GOSUB310:GOSUB350
9390 GOSUB360:R=0:BL=0:A=0:GOTO2010
9400 REM ** KOP PRINTEN **
9410 E=44-B:C=LEN(L$+" "+JA$)
9420 C$=LEFT$("------------------------------",C)
9430 Z=25:GOSUB9610:SR$=SP$+L$+" "+JA$:GOSUB350:GOSUB360
9440 Z=25:GOSUB9610:SR$=SP$+C$:GOSUB350:GOSUB360
9450 GOSUB360
9460 K$="":FORI=1 TO79:K$=K$+"-":NEXTI
9470 SR$=K$:GOSUB350:GOSUB360
9480 IF(Z$="36")OR(Z$="37") THEN9570
9490 SB$="     CATALOGUS NR.      "+B$
9500 Z=E:GOSUB9610:SE$="BEDRAG":SR$=SB$+SP$+SE$
9510 GOSUB350:GOSUB360
9520 K$="":FORI=1 TO79:K$=K$+"-":NEXTI
9530 SR$=K$:GOSUB350:GOSUB360
9540 GOSUB360
9550 Z=0:RETURN
9560 REM ** Z$>36 **
9570 Z=24:GOSUB9610:SR$=SP$+B$:GOSUB350
9580 Z=E:GOSUB9610:SE$="BEDRAG":SR$=SP$+SE$
9590 GOSUB350:GOSUB360:GOTO9520
9600 REM ** SPATIE ROUTINE **
9610 SP$="":FORI=1 TOZ:SP$=SP$+" ":NEXTI:RETURN
9620 REM ** TRANSPORT **
9630 GOSUB360:TR$="TRANSPORT":Z=40:GOSUB9610
9640 SR$=SP$+TR$:GOSUB350:GOSUB360:RETURN
9650 REM ** SALDO TOTAAL **
9660 SB$="     saldo totaal ":Z=49:GOSUB9610
9670 SF$="fl.":SR$=SB$+SP$+SF$:GOSUB350:GOTO9010
9680 REM ** SPATIE KETTINGPAPIER **
9690 IFRS=0 THEN9720
9700 FORE=1 TORS:GOSUB360:NEXTE:RETURN
9710 REM ** LOSSE VELLEN A4 **
9720 VE=16:HO=0:GOSUB110:PRINT" Papier goed instellen";
9730 PRINT" en toets drukken.";:GOSUB210
9740 VE=16:HO=0:GOSUB110:PRINTLS$:RETURN
10000 REM ** LOGO **
10010 VE=21:HO=12:GOSUB110:SR$="              ":GOSUB150
10020 VE=22:GOSUB110:SR$=" R.J. v/d ZEE ":GOSUB150
10030 VE=23:GOSUB110:SR$="              ":GOSUB150
10040 SD=50:GOSUB450:RETURN
25000 REM ** DATA LOGO **
25010 DATA8,9,10,11,12,15,16,17,18,19,22,23,24,25,26,29
25020 DATA30,31,32,33,0,8,12,15,19,22,31,0
25030 DATA8,12,15,19,22,23,24,25,26,31,0
25040 DATA8,9,10,11,12,15,19,26,31,0,8,15,19,26,31,0
25050 DATA8,15,16,17,18,19,22,23,24,25,26,31,0,0,0,0
25060 DATA1,2,3,4,5,8,9,10,11,12,15,16,17,18,19,22,23,24,25
25070 DATA26,29,35,36,37,38,39,0,5,8,15,19,22,29,35,0
25080 DATA4,8,9,10,11,15,22,23,24,25,29,35,36,37,38,39,0
25090 DATA3,8,15,18,19,22,29,39,0,2,8,15,19,22,29,39,0
25100 DATA1,2,3,4,5,8,9,10,11,12,15,16,17,18,19,22,23,24,25
25110 DATA26,29,30,31,32,33,35,36,37,38,39,0,40
30000 REM
30010 REM **** SEPTEMBER 1989 ****
30020 REM *                      *
30030 REM *  POSTZEGELPROGRAMMA  *
30040 REM *                      *
30050 REM *       GEMAAKT        *
30060 REM *                      *
30070 REM * OP EEN COMMODERE 64  *
30080 REM *                      *
30090 REM *   DAARNA OVERGEZET   *
30100 REM *                      *
30110 REM *   IN BASICODE 3 EN   *
30120 REM *                      *
30130 REM *     GETEST OP DE     *
30140 REM *                      *
30150 REM *  C64 EEN PC EN MSX2  *
30160 REM *                      *
32000 REM *     R.J. V/D ZEE     *
32010 REM *                      *
32020 REM *     BRAAMKAMP 40     *
32030 REM *                      *
32040 REM *   7206 HB ZUTPHEN    *
32050 REM *                      *
32060 REM *   TEL: 05750-21736   *
32070 REM *                      *
32080 REM ****** BASICODE 3 ******
32090 REM
32100 REM TROS-RADIO DD. 900110

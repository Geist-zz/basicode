
1000 A=1200:GOTO20:REM energie standen
1010 GOSUB10030:REM initialisatie variabelen
1020 GOSUB8030:REM uitleg
1500 REM ************************
1510 REM gas/water/elek. rekening
1520 REM ************************
1530 DIMGA(12),WA(12),EE(12),M$(12),H(12)
1540 FORI=0 TO12:READM$(I):NEXTI
1550 PG=.48:PW=1.85:PE=.26:REM * tarief
1560 SG=16:SW=1:SE=8:REM ******* schaal
1570 G$="           "
1580 EW$="Even wachten a.u.b"
1590 REM          **********
1600 GOSUB100:REM hoofd menu
1610 REM          **********
1620 HO=11:VE=1:GOSUB110:SR$="Hoofd menu":GOSUB150
1630 HO=10:VE=3:GOSUB110:PRINT"1=Gegevens inladen"
1640 VE=5:GOSUB110:PRINT"2=Gegevens wegschrijven"
1650 VE=7:GOSUB110:PRINT"3=Gegevens aanvullen"
1660 VE=9:GOSUB110:PRINT"4=Resultaten verbruik"
1670 VE=11:GOSUB110:PRINT"5=Lijn  grafiek"
1680 VE=13:GOSUB110:PRINT"6=Volledig totaal"
1690 VE=15:GOSUB110:PRINT"7=Jaar ingeven"
1700 VE=17:GOSUB110:PRINT"8=Vrij geheugen"
1710 VE=19:GOSUB110:PRINT"9=Stoppen";
1720 SR$="<< u=uitleg >>":GOSUB150
1730 VE=22:GOSUB110
1740 PRINT"Maak uw keus"
1750 GOSUB210
1760 IFIN=85 THEN GOSUB8030:GOTO1600
1770 IF(IN<49)OR(IN>57)THEN GOSUB250:GOTO1750
1780 IFIN$="9" THEN2040
1790 IN=IN-48
1800 ONIN GOSUB9010,9510,2510,3010,6010,5510,2910,2010
1810 GOTO1600
2000 REM *** Vrij geheugen/Stoppen ***
2010 GOSUB270:VE=17:HO=26:GOSUB110
2020 SR=FR:GOSUB300:PRINTSR$;" bytes"
2030 GOSUB7030:RETURN
2040 GOSUB100:HO=4:VE=10:GOSUB110
2050 PRINT"Tot de volgende keer !"
2060 SD=30:GOSUB450
2070 GOTO950
2500 REM          ******************
2510 GOSUB100:REM gegevens aanvullen
2520 REM          ******************
2530 HO=0:VE=1:GOSUB110
2540 INPUT"Hoeveelste maand (0 t/m 12) ";I
2550 IF(I<0)OR(I>12)THEN2530
2560 VE=VE+2:GOSUB110:PRINTM$(I)
2570 VE=VE+3:GOSUB110
2580 INPUT"Geef de stand van de gasmeter ";GA(I)
2590 PRINT:PRINT
2600 INPUT"Geef de stand van water meter ";WA(I)
2610 PRINT:PRINT
2620 INPUT"Geef stand van de elek. meter ";EE(I)
2630 HO=0:VE=22:GOSUB110
2640 PRINT"Nog meer gegevens invoeren  j/n ":GOSUB210
2650 IFIN=78 THENRETURN
2660 IFIN=74 THEN2510
2670 GOSUB250:GOTO2630
2900 REM          ************
2910 GOSUB100:REM jaar ingeven
2920 REM          ************
2930 HO=8:VE=10:GOSUB110:INPUT"Welk jaar";JR
2940 GOSUB7030:RETURN
3000 REM          *******************
3010 GOSUB100:REM resultaten verbruik
3020 REM          *******************
3030 PRINT"Welke resultaten wilt u"
3040 PRINT:PRINTG$;"1=Gas"
3050 PRINT:PRINTG$;"2=Water"
3060 PRINT:PRINTG$;"3=Elek."
3070 PRINT:PRINTG$;"4=Terug hoofd menu"
3080 PRINT:PRINTG$;"5=Tarief aanpassen"
3090 PRINT:PRINTG$;"6=Andere kosten aanpassen"
3100 PRINT:PRINTG$;"7=Vastrecht meters aanpassen"
3110 PRINT:PRINT:PRINT"Maak uw keus";
3120 GOSUB210:IF(IN<49)OR(IN>55)THEN GOSUB250:GOTO3120
3130 ONIN-48 GOTO3410,3810,4210,3140,5010,5210,5310
3140 RETURN
3400 REM          *************
3410 GOSUB100:REM resultaat gas
3420 REM          *************
3430 PT=0:TV=0:K1=0
3440 HO=10:VE=0:GOSUB110
3450 SR=JR:GOSUB300:PRINT"Gasverbruik  ";SR$
3460 VE=VE+1:GOSUB110:PRINT"================="
3470 HO=0:VE=VE+2:GOSUB110
3480 PRINT"maand      stand   verbruik   prijs"
3490 IFNG=0 THENNG=PG
3500 VE=VE+1:GOSUB110
3510 FORI=1 TO12
3520 V=GA(I)-GA(I-1):IFV<0 THENV=0
3530 PV=NG*V:IFPV<0 THENPV=0
3540 HO=0:VE=VE+1:GOSUB110:PRINTM$(I);
3550 HO=9:GOSUB110:K=GA(I):GOSUB7630
3560 HO=18:GOSUB110:K=V:GOSUB7630
3570 HO=29:GOSUB110:K=PV:GOSUB7530
3580 PT=PT+PV:TV=TV+V
3590 NEXTI:K2=K1
3600 HO=0:VE=VE+2:GOSUB110
3610 PRINT"Totaal verbruik:";:K=TV:GOSUB7630
3620 HO=24:GOSUB110:PRINT"m3"
3630 PRINT:PRINT"Totaal prijs:      ";:K=PT:GOSUB7530
3640 HO=0:VE=22:GOSUB110:PRINT"Tarief:";NG
3650 GT=PT:GV=TV
3660 GOTO4470
3800 REM          ***************
3810 GOSUB100:REM resultaat water
3820 REM          ***************
3830 PT=0:TV=0:K1=0
3840 HO=10:VE=0:GOSUB110
3850 SR=JR:GOSUB300:PRINT"Waterverbruik  ";SR$
3860 VE=VE+1:GOSUB110
3870 PRINT"==================="
3880 HO=0:VE=VE+2:GOSUB110
3890 PRINT"maand      stand   verbruik   prijs"
3900 IFNW=0 THENNW=PW
3910 VE=VE+1:GOSUB110
3920 FORI=1 TO12
3930 V=WA(I)-WA(I-1):IFV<0 THENV=0
3940 PV=NW*V:IFPV<0 THENPV=0
3950 HO=0:VE=VE+1:GOSUB110:PRINTM$(I);
3960 HO=9:GOSUB110:K=WA(I):GOSUB7630
3970 HO=18:GOSUB110:K=V:GOSUB7630
3980 HO=28:GOSUB110:K=PV:GOSUB7530
3990 PT=PT+PV:TV=TV+V
4000 NEXTI
4010 HO=0:VE=VE+2:GOSUB110
4020 PRINT"Totaal waterverbruik:";:K=TV:GOSUB7630
4030 HO=29:GOSUB110:PRINT"m3"
4040 PRINT:PRINT"Totaal prijs:           ";:K=PT:GOSUB7530
4050 HO=0:VE=22:GOSUB110:PRINT"Tarief:";NW
4060 WT=PT:WV=TV
4070 GOTO4470
4200 REM          ***************
4210 GOSUB100:REM resultaat elek.
4220 REM          ***************
4230 PT=0:TV=0:K1=0
4240 HO=10:VE=0:GOSUB110
4250 SR=JR:GOSUB300:PRINT"Elek.verbruik  ";SR$
4260 VE=VE+1:GOSUB110
4270 PRINT"==================="
4280 HO=0:VE=VE+2:GOSUB110
4290 PRINT"maand      stand   verbruik   prijs"
4300 IFNE=0 THENNE=PE
4310 VE=VE+1:GOSUB110
4320 FORI=1 TO12
4330 V=EE(I)-EE(I-1):IFV<0 THENV=0
4340 PV=(NE*V):IFPV<0 THENPV=0
4350 HO=0:VE=VE+1:GOSUB110:PRINTM$(I);
4360 HO=9:GOSUB110:K=EE(I):GOSUB7630
4370 HO=18:GOSUB110:K=V:GOSUB7630
4380 HO=28:GOSUB110:K=PV:GOSUB7530
4390 PT=PT+PV:TV=TV+V
4400 NEXTI
4410 HO=0:VE=VE+2:GOSUB110
4420 PRINT"Totaal verbruik:";:K=TV:GOSUB7630
4430 HO=24:GOSUB110:PRINT"kwh"
4440 PRINT:PRINT"Totaal prijs:      ";:K=PT:GOSUB7530
4450 HO=0:VE=22:GOSUB110:PRINT"Tarief:";NE
4460 ET=PT:EV=TV
4470 GOSUB7130:GOTO3010
5000 REM          ****************
5010 GOSUB100:REM tarief aanpassen
5020 REM          ****************
5030 IFNG=0 THENNG=PG
5040 PRINT:PRINT:PRINT"Het oude tarief van het  gas  is:";NG
5050 PRINT:INPUT"Geef Het nieuwe tarief gas   ";NG
5060 IFNW=0 THENNW=PW
5070 PRINT:PRINT:PRINT"Het oude tarief van het water is:";NW
5080 PRINT:INPUT"Geef het nieuwe tarief water ";NW
5090 IFNE=0 THENNE=PE
5100 PRINT:PRINT:PRINT"Het oude tarief van het elek. is:";NE
5110 PRINT:INPUT"Geef het nieuwe tarief elek. ";NE
5120 GOSUB7030:GOTO3010
5200 REM          ****************
5210 GOSUB100:REM aanpassen kosten
5220 REM          ****************
5230 PRINT:PRINT:PRINT"Het oude bedrag andere kosten is ";CK
5240 PRINT:INPUT"Geef het nieuwe bedrag ";NC
5250 IFNC<>0 THENCK=NC
5260 GOSUB7030:GOTO3010
5300 REM          *******************
5310 GOSUB100:REM aanpassen vastrecht
5320 REM          *******************
5330 PRINT"Op maand basis invoeren":PRINT:PRINT
5340 PRINT"Oude vastrechtprijs gasmeter is ";GM
5350 PRINT:INPUT"De nieuwe prijs is ";GX
5360 IFGX<>0 THENGM=GX
5370 PRINT:PRINT:PRINT"Oude huurprijs elek.meter is ";DM
5380 PRINT:INPUT"De nieuwe prijs is ";DX
5390 IFDX<>0 THENDM=DX
5400 PRINT:PRINT:PRINT"De oude huurprijs watermeter is ";NM
5410 PRINT:INPUT"De nieuwe prijs is ";NX
5420 IFNX<>0 THENNM=NX
5430 GOSUB7030:GOTO3010
5500 REM          ***************
5510 GOSUB100:REM volledig totaal
5520 REM          ***************
5530 HO=1:VE=1:GOSUB110
5540 SR=JR:GOSUB300:PRINTSR$
5550 PRINT" ===="
5560 HO=9:VE=1:GOSUB110
5570 PRINT"Te betalen         Verbruikt"
5580 HO=1:VE=4:GOSUB110
5590 PRINT"Gas      ";:K=GT:GOSUB7530
5600 HO=26:GOSUB110:K=GV:GOSUB7630
5610 HO=34:GOSUB110:PRINT"m3"
5620 HO=1:VE=6:GOSUB110
5630 PRINT"Water    ";:K=WT:GOSUB7530
5640 HO=26:GOSUB110:K=WV:GOSUB7630
5650 HO=34:GOSUB110:PRINT"m3"
5660 HO=1:VE=8:GOSUB110
5670 PRINT"Elek.    ";:K=ET:GOSUB7530
5680 HO=26:GOSUB110:K=EV:GOSUB7630
5690 HO=34:GOSUB110:PRINT"kwh"
5700 HO=1:VE=12:GOSUB110
5710 TG=GM*K2:PRINT"Vastrecht gas    ";
5720 K=TG:GOSUB7730
5730 VE=14:GOSUB110
5740 TE=DM*K2:PRINT"Huur elek. meter ";
5750 K=TE:GOSUB7730
5760 VE=16:GOSUB110
5770 TW=NM*K2:PRINT"Huur water meter ";
5780 K=TW:GOSUB7730
5790 VE=18:GOSUB110
5800 PRINT"Andere kosten    ";
5810 K=CK:GOSUB7530
5820 ZT=GT+WT+ET+CK+TG+TE+TW
5830 HO=0:VE=21:GOSUB110
5840 SR$="T o t a a l    = ":GOSUB150:K=ZT:GOSUB7530
5850 GOSUB7130:RETURN
6000 REM          ****************
6010 GOSUB100:REM menu lijngrafiek
6020 REM          ****************
6030 PRINT"      Welke grafiek wilt u"
6040 PRINT:PRINT:PRINTG$;"1=Gas"
6050 PRINT:PRINTG$;"2=Water"
6060 PRINT:PRINTG$;"3=Elek."
6070 PRINT:PRINTG$;"4=Terug hoofd menu"
6080 PRINT:PRINTG$;"5=Schaalaanpassing"
6090 PRINT:PRINT:PRINT"Maak uw keus";
6100 GOSUB210:IN=IN-48
6110 IF(IN<1)OR(IN>5)THEN GOSUB250:GOTO6100
6120 ONIN GOTO6140,6200,6260,6130,6330
6130 RETURN
6140 T$="Gasverbruik":IFBG=0 THENBG=SG
6150 T1=BG:U$=" m3"
6160 FORI=1 TO12
6170 H(I)=(GA(I)-GA(I-1))/BG
6180 NEXTI
6190 GOTO6310
6200 T$="Waterverbruik":IFBW=0 THENBW=SW
6210 T1=BW:U$=" m3"
6220 FORI=1 TO12
6230 H(I)=(WA(I)-WA(I-1))/BW
6240 NEXTI
6250 GOTO6310
6260 T$="Elek.verbruik":IFBE=0 THENBE=SE
6270 T1=BE:U$=" kwh"
6280 FORI=1 TO12
6290 H(I)=(EE(I)-EE(I-1))/BE
6300 NEXTI
6310 GOSUB6610:GOTO6010
6320 REM          ****************
6330 GOSUB100:REM schaalaanpassing
6340 REM          ****************
6350 PRINT
6360 PRINT"Gas":IFBG=0 THENBG=SG
6370 PRINT"Oude schaal:";BG
6380 INPUT"Nieuwe schaal";BG
6390 PRINT:PRINT
6400 PRINT"Water":IFBW=0 THENBW=SW
6410 PRINT"Oude schaal:";BW
6420 INPUT"Nieuwe schaal";BW
6430 PRINT:PRINT
6440 PRINT"Elek.":IFBE=0 THENBE=SE
6450 PRINT"Oude schaal:";BE
6460 INPUT"Nieuwe schaal";BE
6470 GOSUB7030:GOTO6010
6600 REM          ************
6610 GOSUB100:REM lijn grafiek
6620 REM          ************
6630 HO=10:VE=2:GOSUB110
6640 SR=JR:GOSUB300:PRINTT$;"  ";SR$
6650 VE=4:GOSUB110:PRINT"1 (*)= ";
6660 SR=T1:GOSUB300:PRINTSR$;U$
6670 HO=0:VE=8:GOSUB110
6680 MN$="janfebmaaaprmeijunjulaugsepoktnovdec"
6690 FORI=1 TO12
6700 PRINTMID$(MN$,3*I-2,3);":";
6710 H(I)=INT(H(I)+.5):IFH(I)<0 THENH(I)=0
6720 SR$="":IFH(I)=0 THEN6740
6730 FORJ=1 TOH(I):SR$=SR$+"*":NEXTJ
6740 PRINTSR$
6750 NEXTI
6760 GOSUB7130:RETURN
7000 REM ********************
7010 REM knipper return toets
7020 REM ********************
7030 HO=10:VE=22:GOSUB110:PRINT"Toets <<return>>"
7040 SD=6:GOSUB450
7050 GOSUB110:PRINTG$;G$:IFIN=13 THENRETURN
7060 SD=3:GOSUB450
7070 IFIN<>13 THEN7030
7080 RETURN
7100 REM ***********
7110 REM sub-routine
7120 REM ***********
7130 HO=12:VE=23:GOSUB110:PRINT"Druk <return of p>";
7140 GOSUB210:IFIN=13 THENRETURN
7150 IFIN<>80 THEN7140
7160 GOSUB7230:RETURN
7200 REM *******************
7210 REM printer sub-routine
7220 REM *******************
7230 HO=12:VE=23:GOSUB110
7240 PRINT"Hardcopy scherm  j/n";:GOSUB210
7250 IFIN=78 THENRETURN
7260 IFIN<>74 THEN7230
7270 GOSUB20030:RETURN
7500 REM *****************
7510 REM getal richten 4.2
7520 REM *****************
7530 IFK=0 THENRETURN
7540 CT=7:CN=2:SR=K:GOSUB310
7550 PRINTSR$;:K1=K1+1:RETURN
7600 REM *****************
7610 REM getal richten 6.0
7620 REM *****************
7630 IFK=0 THENRETURN
7640 CT=7:CN=0:SR=K:GOSUB310
7650 PRINTSR$;:RETURN
7700 REM *****************
7710 REM getal richten 3.2
7720 REM *****************
7730 IFK=0 THENRETURN
7740 CT=6:CN=2:SR=K:GOSUB310
7750 PRINTSR$;:RETURN
8000 REM ***************************
8010 REM handleiding energie standen
8020 REM ***************************
8030 GOSUB100:HO=1:VE=1:GOSUB110
8040 SR$="Handleiding 'energiestanden'":GOSUB150:PRINT:HO=5
8050 PRINT:PRINT"  Bij eerste gebruik de volgende"
8060 PRINT"  volgorde aanhouden:":PRINT
8070 PRINT"1 Vanuit hoofdmenu eerst keuze 7 (jaar"
8080 PRINT"  ingeven).":PRINT
8090 PRINT:PRINT"2 Vanuit hoofdmenu keuze 3 (gegevens"
8100 PRINT"  aanvullen)."
8110 PRINT"  Geef hier de meterstand op de laatste"
8120 PRINT"  dag van de maand in (keuze 0 t/m 12)."
8130 PRINT
8140 PRINT"  0=december vorig jaar"
8150 PRINT"  1=januari lopend jaar"
8160 PRINT"  2=februari       enz."
8170 HO=10:VE=22:GOSUB110
8180 GOSUB8320
8190 GOSUB100
8200 PRINT:PRINT"3 Vanuit hoofdmenu keuze 4 (resultaten"
8210 PRINT"  verbruik)."
8220 PRINT"  Hier worden de resultaten berekend"
8230 PRINT"  en in variabelen gezet voor verder"
8240 PRINT"  gebruik.":PRINT
8250 PRINT"  Bij elke verandering van invoer-"
8260 PRINT"  gegevens moet men eerst deze keuze"
8270 PRINT"  uitvoeren,daar anders de gegevens"
8280 PRINT"  niet in de variabelen gezet worden."
8290 PRINT:PRINT"4 Zie verder keuze hoofdmenu en"
8300 PRINT"  sub-menu's."
8310 HO=10:VE=22:GOSUB110
8320 PRINT"Druk een toets":GOSUB210
8330 RETURN
9000 REM          ****************
9010 GOSUB100:REM gegevens inladen
9020 REM          ****************
9030 HO=0:VE=4:GOSUB110
9040 PRINT"Laden van disk/tape (d/t)";:GOSUB210
9050 IFIN=84 THENNF=2:GOTO9080
9060 IFIN=68 THENNF=4:GOTO9210
9070 GOSUB250:GOTO9030
9080 PRINT:PRINT"Wil je van cassette laden j/n ?";
9090 GOSUB210:IFIN=78 THENRETURN
9100 IFIN<>74 THEN9090
9200 REM          ***************
9210 GOSUB100:REM laden disk/tape
9220 REM          ***************
9230 HO=0:VE=4:GOSUB110
9240 INPUT"Welk jaar wenst u ";JR
9250 SR=JR:GOSUB300:F$=SR$:IFNF=2 THEN9310
9260 PRINT:PRINT"Staat de diskdrive aan  j/n ";
9270 GOSUB210:IFIN=78 THENRETURN
9280 IFIN<>74 THEN9270
9290 GOSUB100:HO=10:VE=15:GOSUB110
9300 PRINTEW$
9310 NF$=F$:GOSUB500
9320 GOSUB540:NG=VAL(IN$):GOSUB540:NW=VAL(IN$)
9330 GOSUB540:NE=VAL(IN$):GOSUB540:BG=VAL(IN$)
9340 GOSUB540:BW=VAL(IN$):GOSUB540:BE=VAL(IN$)
9350 GOSUB540:JR=VAL(IN$):GOSUB540:CK=VAL(IN$)
9360 GOSUB540:GM=VAL(IN$):GOSUB540:DM=VAL(IN$)
9370 GOSUB540:NM=VAL(IN$):GOSUB540:GT=VAL(IN$)
9380 GOSUB540:GV=VAL(IN$):GOSUB540:WT=VAL(IN$)
9390 GOSUB540:WV=VAL(IN$):GOSUB540:ET=VAL(IN$)
9400 GOSUB540:EV=VAL(IN$)
9410 FORI=0 TO12
9420 GOSUB540:GA(I)=VAL(IN$):GOSUB540:WA(I)=VAL(IN$)
9430 GOSUB540:EE(I)=VAL(IN$)
9440 NEXTI
9450 GOSUB580
9460 RETURN
9500 REM          **************
9510 GOSUB100:REM gegevens saven
9520 REM          **************
9530 HO=0:VE=4:GOSUB110
9540 PRINT"Naar disk/tape saven (d/t)";:GOSUB210
9550 IFIN=84 THENNF=3
9560 IFIN=68 THENNF=5:GOTO9610
9570 PRINT:PRINT"Naar cassette saven j/n ?";
9580 GOSUB210:IFIN=78 THENRETURN
9590 IFIN<>74 THEN9510
9600 REM          ***************
9610 GOSUB100:REM saven disk/tape
9620 REM          ***************
9630 HO=0:VE=4:GOSUB110
9640 SR=JR:GOSUB300:F$=SR$
9650 PRINT"Het jaar ";F$;" wordt weggeschreven"
9660 IFNF=3 THEN9730
9670 PRINT"met als bestandsnaam: ";F$
9680 PRINT:PRINT"Staat de diskdrive aan j/n ";
9690 GOSUB200:IFIN=78 THENRETURN
9700 IFIN<>74 THEN9690
9710 GOSUB100:HO=10:VE=15:GOSUB110
9720 PRINTEW$
9730 NF$=F$:GOSUB500
9740 SR=NG:GOSUB9850:SR=NW:GOSUB9850:SR=NE:GOSUB9850
9750 SR=BG:GOSUB9850:SR=BW:GOSUB9850:SR=BE:GOSUB9850
9760 SR=JR:GOSUB9850:SR=CK:GOSUB9850:SR=GM:GOSUB9850
9770 SR=DM:GOSUB9850:SR=NM:GOSUB9850:SR=GT:GOSUB9850
9780 SR=GV:GOSUB9850:SR=WT:GOSUB9850:SR=WV:GOSUB9850
9790 SR=ET:GOSUB9850:SR=EV:GOSUB9850
9800 FORI=0 TO12
9810 SR=GA(I):GOSUB9850:SR=WA(I):GOSUB9850
9820 SR=EE(I):GOSUB9850
9830 NEXTI
9840 GOSUB580:RETURN
9850 GOSUB300:GOSUB560:RETURN
9900 REM *********************
9910 REM routine cas. recorder
9920 REM *********************
9930 HO=3:VE=4:GOSUB110
9940 PRINT"1. Druk play/record toets cassette"
9950 VE=7:GOSUB110:PRINT"2. Druk daarna return toets"
9960 GOSUB7030:GOSUB100:RETURN
10000 REM ************************
10010 REM initialiseren variabelen
10020 REM ************************
10030 NG=0:NW=0:NE=0:BG=0:BW=0:BE=0
10040 JR=0:CK=0:GM=0:DM=0:NM=0:GT=0
10050 GV=0:WT=0:WV=0:ET=0:EV=0:PK=0
10060 DT=0:D1=0:K=0:K1=0:K2=0
10070 RETURN
20000 REM ***********************
20010 REM hardcopy-routine  bas-3
20020 REM ***********************
20030 HO=12:VE=23:GOSUB110
20040 PRINT"                         ";
20050 FORVE=0 TO23
20060 SR$=""
20070 FORHO=0 TO39:GOSUB220:SR$=SR$+CHR$(IN):NEXTHO
20080 GOSUB350:GOSUB360
20090 NEXTVE
20100 RETURN
25000 REM ***********
25010 REM data regels
25020 REM ***********
25030 DATA"december vorig jaar","januari","februari"
25040 DATA"maart","april","mei","juni"
25050 DATA"juli","augustus","september"
25060 DATA"oktober","november","december"
30000 REM ***************************
30010 REM *     Energiestanden      *
30020 REM ***************************
30030 REM *                         *
30040 REM *     Januari   1990      *
30050 REM *     Aangepast voor      *
30055 REM *       BASICODE-3        *
30060 REM *        op een           *
30070 REM *   Commodore C-16 (60k)  *
32000 REM *      E.F.Dejevij        *
32010 REM *      Mandrill 55        *
32020 REM *      1687 VM            *
32030 REM *      Wognum             *
32040 REM *      02297-2591         *
32050 REM *                         *
32060 REM ***************************
32070 REM *   copyright protected   *
32080 REM *       E.F.Dejevij       *
32090 REM ***************************
32100 REM
32110 REM TROS-RADIO dd 900523

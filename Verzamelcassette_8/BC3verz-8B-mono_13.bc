
1000 A=3000:GOTO20:REM ** ENKEL OF MEERVOUD **
1010 GOSUB100:PRINT"EEN OGENBLIK GEDULD A.U.B"
1020 REM ** DATA INLEZEN **
1030 B=0:DIMW$(120,1),V$(2)
1040 READV$(1),V$(2)
1050 X=0
1060 X=X+1
1070 READA$,B$
1080 IFA$="*" THENX=X-1:GOTO1120
1090 W$(X,0)="IK "+A$:W$(X,1)="WIJ "+B$
1100 IFX<120 THEN1060
1110 REM ** LOGO + INSTRUKTIE **
1120 IFB=1 THEN1150
1130 GOSUB3010
1140 REM ** BEGIN PROGRAMMA **
1150 T=0:G=0:F=0:P=0:N=0
1500 REM ** OPHALEN VRAAG **
1510 GOSUB260:A=INT(RV*2+1)
1520 GOSUB260:B=INT(RV*X+1)
1530 A$=W$(B,0):Z$=W$(B,1)
1540 IFA=1 THEN1570
1550 A$=W$(B,1):Z$=W$(B,0)
1560 REM ** UITVOER **
1570 GOSUB100:T=T+1
1580 VE=2:HO=4:GOSUB110:PRINT"BEURT ";T
1590 VE=8:HO=0:GOSUB110:PRINT;V$(A);" ";Z$
1600 VE=22:HO=12:GOSUB110:PRINT" * = stop"
1610 REM ** INVOER **
1620 VE=12:HO=16:GOSUB110:PRINT"              "
1630 VE=2:HO=32:GOSUB110:PRINT"     "
1640 VE=12:HO=0:GOSUB110:PRINT"Het antwoord is  "
1650 HO=16:GOSUB110:INPUTI$
1660 IFI$="*" THENT=T-1:GOTO2010
1670 SR$=I$:GOSUB330
1680 IFSR$=A$ THENN=0:GOTO1790
1690 REM ** ANTWOORD FOUT **
1700 IFN=2 THENN=0:GOSUB250:GOTO1720
1710 N=N+1
1720 VE=2:HO=32:GOSUB110:PRINT"FOUT ";
1730 IFN=0 THEN1750
1740 SD=40:GOSUB450:GOTO1620
1750 VE=16:HO=0:GOSUB110
1760 PRINT"Het antwoord was :";A$;
1770 SD=20:GOSUB450:GOTO1810
1780 REM ** ANTWOORD GOED **
1790 G=G+1:VE=2:HO=32:GOSUB110:PRINT"GOED ";
1800 IFT>=X THEN2010
1810 SD=20:GOSUB450:GOTO1510
2000 REM ** UITSLAG **
2010 GOSUB100:VE=6:HO=1:GOSUB110
2020 PRINT"Totaal aantal beurten          :";T
2030 VE=8:HO=1:GOSUB110
2040 PRINT"Totaal aantal goede antwoorden :";G
2050 IFT=0 THEN2090
2060 P=INT((G/T)*1000+.5)/10
2070 VE=10:HO=1:GOSUB110
2080 PRINT"Totaal percentage              :";P
2090 VE=16:HO=12:GOSUB110:PRINT"Nog een keer j/n";
2100 GOSUB210:IFIN=74 THENRESTORE:B=1:GOTO1040
2110 GOSUB100:VE=12:HO=15:GOSUB110:PRINT"TOT ZIENS";
2120 SD=40:GOSUB450:GOTO950
3000 REM ** LOGO **
3010 GOSUB100:VE=2:HO=9:GOSUB110
3020 PRINT"*** *  * *  * *** *"
3030 VE=3:GOSUB110:PRINT"*   *  * * *  *   *"
3040 VE=4:GOSUB110:PRINT"**  ** * **   **  *"
3050 VE=5:GOSUB110:PRINT"*   * ** * *  *   *"
3060 VE=6:GOSUB110:PRINT"*** *  * *  * *** ***"
3070 VE=10:HO=16:GOSUB110:PRINT" **  ***"
3080 VE=11:GOSUB110:PRINT"*  * *"
3090 VE=12:GOSUB110:PRINT"*  * **"
3100 VE=13:GOSUB110:PRINT"*  * *"
3110 VE=14:GOSUB110:PRINT" **  *"
3120 VE=17:HO=0:GOSUB110
3130 PRINT"*   * *** *** **** *   *  **  *  * ****"
3140 VE=18:GOSUB110
3150 PRINT"** ** *   *   *  * *   * *  * *  *  * *"
3160 VE=19:GOSUB110
3170 PRINT"* * * **  **  **** *   * *  * *  *  * *"
3180 VE=20:GOSUB110
3190 PRINT"*   * *   *   * *   * *  *  * *  *  * *"
3200 VE=21:GOSUB110
3210 PRINT"*   * *** *** *  *   *    **  **** ****"
3220 SD=40:GOSUB450
4000 REM ** INSTRUCTIE **
4010 GOSUB100:PRINT:PRINTTAB(11);"ENKEL OF MEERVOUD"
4020 PRINT:PRINT"In dit programma is het de bedoeling"
4030 PRINT"om te oefenen in het enkel of meervoud"
4040 PRINT"van woorden."
4050 PRINT"De woorden staan in een IK of WIJ vorm"
4060 PRINT"In het programma worden de volgende"
4070 PRINT"vragen gesteld :"
4080 PRINT:PRINT"Wat is het enkelvoud van *"
4090 PRINT:PRINT"Wat is het meervoud van *"
4100 PRINT:PRINT"Op de plaats van de ster staat dan b.v"
4110 PRINT:PRINT"WIJ BALLEN  of  IK BAL"
4120 PRINT:PRINT"Daarna wordt gevraagd het antwoord te"
4130 PRINT"geven.":PRINT:PRINT
4140 PRINTTAB(13);"DRUK EEN TOETS";:GOSUB210:GOSUB100
4150 PRINT:PRINT:PRINT"Dit antwoord moet net zo";
4160 PRINT"als de vraag"
4170 PRINT"worden ingetypt d.w.z tussen IK of WIJ"
4180 PRINT"en het daarop volgende woord een spatie"
4190 PRINT:PRINT"WIJ BALLEN en niet WIJBALLEN of"
4200 PRINT"WIJ  BALLEN."
4210 PRINT:PRINT"Wordt dit niet gedaan dan geeft de com-"
4220 PRINT"puter als antwoord FOUT."
4230 PRINT"Tijdens het beantwoorden van de vragen"
4240 PRINT"mag tweemaal het antwoord worden verbe-"
4250 PRINT"terd, daarna geeft de computer het"
4260 PRINT"antwoord."
4270 PRINT"Na het stoppen of aan het eind van het"
4280 PRINT"programma geeft de computer op hoeveel"
4290 PRINT"vragen er goed zijn beantwoord en het"
4300 PRINT"totale percentage hiervan."
4310 PRINT"Veel plezier gewenst met dit programma."
4320 PRINT:PRINT:PRINTTAB(13);"DRUK EEN TOETS";
4330 GOSUB210:RETURN
25000 DATA"Wat is het enkelvoud van"
25010 DATA"Wat is het meervoud van "
25020 DATA"DEEL","DELEN","LOOP","LOPEN"
25030 DATA"AAI","AAIEN","BEGIN","BEGINNEN"
25040 DATA"BAK","BAKKEN","BAL","BALLEN"
25050 DATA"SLAAP","SLAPEN","ADEM","ADEMEN"
25060 DATA"BEL","BELLEN","BAAL","BALEN"
25070 DATA"BABBEL","BABBELEN","BAAD","BADEN"
25080 DATA"EIS","EISEN","WERK","WERKEN"
25090 DATA"BEGROET","BEGROETEN","GROET","GROETEN"
25100 DATA"TOETER","TOETEREN","LEEF","LEVEN"
25110 DATA"SCHAATS","SCHAATSEN","BEPAAL","BEPALEN"
25120 DATA"ZING","ZINGEN","KLIK","KLIKKEN"
25130 DATA"RIJD","RIJDEN","PRAAT","PRATEN"
25140 DATA"BIED","BIEDEN","ZAAG","ZAGEN"
25150 DATA"DANS","DANSEN","WALS","WALSEN"
25160 DATA"KOM","KOMEN","ZAL","ZULLEN"
25170 DATA"COMBINEER","COMBINEREN"
25180 DATA"VOEL","VOELEN"
25190 DATA"TELEFONEER","TELEFONEREN"
25200 DATA"NEEM","NEMEN"
25210 DATA"LIG","LIGGEN","BEKEN","BEKENNEN"
25220 DATA"VERTEL","VERTELLEN","LEID","LEIDEN"
25230 DATA"LIJD","LIJDEN","HAAL","HALEN"
25240 DATA"TRIL","TRILLEN","STEEL","STELEN"
25250 DATA"ROEP","ROEPEN","ZOEN","ZOENEN"
25260 DATA"AARZEL","AARZELEN","WACHT","WACHTEN"
25270 DATA"LEER","LEREN","KAN","KUNNEN"
25280 DATA"DEEL","DELEN","LEES","LEZEN"
25290 DATA"BRENG","BRENGEN","BUIG","BUIGEN"
25300 DATA"GELOOF","GELOVEN","KOOP","KOPEN"
25310 DATA"ZEG","ZEGGEN","MAAK","MAKEN"
25320 DATA"VERGEET","VERGETEN","WIL","WILLEN"
25330 DATA"GA","GAAN","ZOEK","ZOEKEN"
25340 DATA"BEN","ZIJN","LUISTER","LUISTEREN"
25350 DATA"BEREIK","BEREIKEN","KEN","KENNEN"
25360 DATA"GLIMLACH","GLIMLACHEN"
25370 DATA"MIS","MISSEN","PLANT","PLANTEN"
25380 DATA"MAG","MOGEN","VOEL","VOELEN"
25390 DATA"REM","REMMEN","DRUK","DRUKKEN"
25400 DATA"REN","RENNEN","KEER","KEREN"
25410 DATA"WAAG","WAGEN","DOE","DOEN"
25420 DATA"HUIL","HUILEN","VECHT","VECHTEN"
25430 DATA"HAP","HAPPEN","PAS","PASSEN"
25440 DATA"VERLANG","VERLANGEN","SPOT","SPOTTEN"
25450 DATA"RAAK","RAKEN","BREEK","BREKEN"
25460 DATA"BUIG","BUIGEN","TIK","TIKKEN"
25470 DATA"DRINK","DRINKEN","DRAAG","DRAGEN"
25480 DATA"SLUIT","SLUITEN","LIJM","LIJMEN"
25490 DATA"NAAI","NAAIEN","FIETS","FIETSEN"
25500 DATA"KAART","KAARTEN","VAL","VALLEN"
25510 DATA"STOOM","STOMEN","TEKEN","TEKENEN"
25520 DATA"VERDIEN","VERDIENEN","ZIT","ZITTEN"
25530 DATA"WORSTEL","WORSTELEN"
25540 DATA"SPEEL","SPELEN","SPORT","SPORTEN"
25550 DATA"ZWEM","ZWEMMEN","TIL","TILLEN"
25560 DATA"SLA","SLAAN","VRAAG","VRAGEN"
25570 DATA"BEDOEL","BEDOELEN","HELP","HELPEN"
25580 DATA"SNAP","SNAPPEN","PAK","PAKKEN"
25590 DATA"HOUD","HOUDEN","DRIJF","DRIJVEN"
25600 DATA"LIJK","LIJKEN","GOOI","GOOIEN"
25610 DATA"PLAAG","PLAGEN"
25620 DATA"KIJK","KIJKEN","ZIE","ZIEN"
25630 DATA"STEEK","STEKEN","STEL","STELLEN"
25640 DATA"*","*"
32000 REM ***** DECEMBER 1989 *****
32010 REM *                       *
32020 REM *    ENKEL-MEERVOUD     *
32030 REM *                       *
32040 REM *    GEMAAKT DOOR       *
32050 REM *                       *
32060 REM *    R.J. V/D ZEE       *
32070 REM *                       *
32080 REM *    BRAAMKAMP 40       *
32090 REM *                       *
32100 REM *   7206 HB ZUTPHEN     *
32110 REM *                       *
32120 REM *   Tel. 05750-21736    *
32130 REM *                       *
32140 REM ****** BASICODE 3 *******
32150 REM
32160 REM TROS-RADIO dd 900725

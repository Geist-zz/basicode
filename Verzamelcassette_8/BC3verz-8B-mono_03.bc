
1000 A=2000:GOTO20:REM ZAHL AUF ZAHL
1010 GOSUB100
1020 DIMA(8,8):S=0:C=0:SP=0:ZE=0:MA=0
1030 REM
1040 REM ---------- KOP
1050 REM
1060 SR$="GETAL na GETAL"
1070 HO=7:VE=0:GOSUB110:GOSUB150
1080 REM
1090 REM ---------- UITLEG
1100 REM
1110 HO=0:VE=3:GOSUB110
1120 PRINT"De computer tekent een veldje van 8x8":PRINT
1130 PRINT"met willekeurige getallen (1...99) en":PRINT
1140 PRINT"geeft de startpositie aan. Er wordt":PRINT
1150 PRINT"per beurt gewisseld.":PRINT
1160 PRINT"U kunt kiezen met CURSOR UP/DOWN.":PRINT
1170 PRINT"Elke keuze met RETURN-toets afsluiten.":PRINT
1180 PRINT"Einde/nieuw spel => X"
1220 REM
1230 HO=20:VE=23:GOSUB110:PRINT"<TOETS>!";
1240 GOSUB210:GOSUB100
1250 REM
1260 REM ---------- SPEELVELD
1270 REM
1280 HO=0:VE=2:GOSUB110:PRINT"Speler:"
1290 HO=18:VE=2:GOSUB110:PRINT"Computer:"
1300 HO=0:VE=3:GOSUB110
1310 PRINT"--------------------------------"
1320 REM
1330 HO=1:VE=4
1340 FORI=1 TO8
1350 FORJ=1 TO8
1360 GOSUB260:A(I,J)=INT(RV*99)+1
1370 SR=A(I,J):GOSUB300
1380 IFA(I,J)<10 THENSR$=" "+SR$
1390 GOSUB110:PRINTSR$
1400 HO=HO+4
1410 NEXTJ
1420 HO=1:VE=VE+2
1430 NEXTI
1440 REM
1450 REM ---------- SPELER
1460 REM
1470 HO=0:VE=21:GOSUB110
1480 PRINT"Cursor UP/DOWN / RETURN / X"
1490 REM
1500 GOSUB260:J=INT(RV*8)+1:I=1
1510 SR=A(I,J):GOSUB300
1520 IFA(I,J)<10 THENSR$=" "+SR$
1530 IFA(I,J)=0 THENSR$="--"
1540 SR$="*"+SR$+"*"
1550 HO=J*4-4:VE=4:GOSUB110:PRINTSR$;
1560 REM
1570 REM ---------- KEUZE
1580 REM
1590 GOSUB200
1600 IF(I<=1)AND(I>=8)THEN1590
1610 IFIN=30 THENF1=0:GOTO1670
1620 IFIN=31 THENF1=1:GOTO1670
1630 IFIN=13 THEN1810
1640 IFIN=88 THEN2090:REM X
1650 GOTO1590
1660 REM ---------- CS UP/DOWN
1670 SR=A(I,J):GOSUB300
1680 IFA(I,J)<10 THENSR$=" "+SR$
1690 IFA(I,J)=0 THENSR$="--"
1700 SR$=" "+SR$+" "
1710 VE=I+I+2:GOSUB110:PRINTSR$;
1720 IFF1=0 THENI=I+1:IFI>8 THENI=8
1730 IFF1=1 THENI=I-1:IFI<1 THENI=1
1740 SR=A(I,J):GOSUB300
1750 IFA(I,J)<10 THENSR$=" "+SR$
1760 IFA(I,J)=0 THENSR$="--"
1770 SR$="*"+SR$+"*"
1780 VE=I+I+2:GOSUB110:PRINTSR$;
1790 GOTO1590
1800 REM ---------- VASTLEGGEN
1810 S=S+A(I,J)
1820 A(I,J)=0:SR$=" -- "
1830 VE=I+I+2:GOSUB110:PRINTSR$;
1840 HO=8:VE=2:GOSUB110:PRINTS;
1850 REM
1860 REM ---------- COMPUTER
1870 REM
1880 HO=0:VE=21:GOSUB110
1890 PRINT"Computer kiest !           "
1900 REM ---------- ZET + MAX.WAARDE
1910 GOSUB260:I=INT(RV*8)+1
1920 MA=A(I,0):MA=0
1930 FORJ=1 TO8
1940 IFMA>A(I,J)THEN1960
1950 MA=A(I,J):MI=I:MJ=J
1960 NEXTJ
1970 REM ---------- MELDEN
1980 C=C+MA
1990 A(MI,MJ)=0:SR$=" -- "
2000 HO=MJ*4-4:VE=MI+MI+2:GOSUB110:PRINTSR$;
2010 HO=27:VE=2:GOSUB110:PRINTC;
2020 REM
2030 REM ---------- VOLGENDE SERIE
2040 REM
2050 GOTO1470
2060 REM
2070 REM ---------- EINDE/NIEUW SPEL
2080 REM
2090 GOSUB100
2100 HO=5:VE=10:GOSUB110
2110 PRINT"Nog een spel ? <J/N>";
2120 GOSUB210
2130 IFIN=78 THEN2150
2140 IFIN=74 THEN GOSUB100:S=0:C=0:GOTO1280
2150 HO=5:VE=10:GOSUB110
2160 PRINT"    Dat was het!      "
2170 HO=5:VE=12:GOSUB110
2180 PRINT"Je haalde ";S;" punten,"
2190 HO=5:VE=14:GOSUB110
2200 PRINT"De computer ";C;" !!!";
2210 GOSUB210:GOTO950
32000 REM ------------------------
32010 REM     'ZAHL AUF ZAHL'
32020 REM
32030 REM NACH EINER PROGRAMMIDEE
32040 REM     'ZAHLENSCHACH'
32050 REM 64'ER, APRIL 1989, P.62
32060 REM
32070 REM      C128/C64 MODE
32080 REM
32090 REM F.DORMEIER
32100 REM D-1000 BERLIN 39
32110 REM
32120 REM MAI 1989
32130 REM
32140 REM Erstausstrahlung Radio DDR
32150 REM-spezial 900216
32160 REM-------------------------
32170 REM Vertaling P.KNOPS, HEERLEN
32180 REM dd. 900417
32190 REM-------------------------
32200 REM TROS-RADIO dd 900620

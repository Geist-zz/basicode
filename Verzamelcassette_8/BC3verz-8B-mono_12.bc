
1000 A=1000:GOTO20:REM ### VIC ###
1010 REM INITIALISATIE+TITEL
1020 HT=HO:VT=VE:FF=0:F=0:TA=0:R=0:WA=0:WT=0
1030 SP$="":FORHO=5 TOHT-5:SP$=SP$+" ":NEXTHO
1040 AA=ASC("A"):VE=3:HO=INT((HT-19)/2):GOSUB110
1050 PRINT"  VIC - INTERVIEW":VE=4:GOSUB110
1060 PRINT"-------------------"
1070 VE=12:HO=INT((HT-28)/2):GOSUB110
1080 PRINT"IK MOET ME EVEN CONCENTREREN."
1090 EA=50:FA=50:GA=3
1100 REM DATA TELLEN
1110 GOSUB3910
1120 DIMB(200),E$(EA),F$(FA),S$(SA)
1130 DIMS(SA,GA),T$(TK+TN),T2$(TK)
1140 DIMTV$(TV),TF$(TF),Z(10)
1150 VE=14:GOSUB110:PRINT"Moet de tekst naar de printer?"
1160 VE=16:GOSUB110:PRINT"(tik J of N) ";
1170 PR=0:GOSUB210:PRINTIN$:IFIN=74 THENPR=1
1180 VE=18:GOSUB110:PRINT"Zoals je wilt."
1190 GOSUB3610:F$="HALLO, IK HEET VIC ! HOE HEET JIJ ?"
1200 GOSUB100
1210 X$="*":X=3:Y=9:P=HT-3:Q=3:GOSUB2020
1220 Y=VT-3:Q=VT-9:GOSUB2020
1230 X$="#":X=1:Y=VT-1:P=HT-1:Q=1:GOSUB2020
1240 VE=3:HO=INT((HT-23)/2):GOSUB110
1250 PRINT"*** VIC - INTERVIEW ***"
1260 GOSUB3310
1270 GOSUB2210
1280 N$=E$(W)
1290 F$="VERTEL ME WAT OVER JEZELF EN OVER JE FAMILIE, "+N$
1300 GOSUB3310
1310 REM ** INPUT+VERGELIJKING **
1320 GOSUB2210
1330 FORI=0 TOW:B=ASC(E$(I))-AA:IF(B<0)OR(B>25)THEN1420
1340 S=B(B):IFS=0 THEN1420
1350 IFS(S,0)>TK THENZ$=LEFT$(E$(I),LEN(S$(S))):GOTO1370
1360 Z$=E$(I)
1370 IFZ$<=S$(S)THEN1400
1380 IFS<SA THENS=S+1:GOTO1350
1390 GOTO1420
1400 IFZ$<S$(S)THEN1420
1410 GOSUB3010
1420 NEXTI
1430 REM ** OUTPUT VOORBEREIDING **
1440 IFFF=0 THEN1470
1450 FF=0:IFTF>0 THENTF=TF-1
1460 F$=TF$(TF):GOTO1580
1470 IF(W>1)OR(F>0)THEN1490
1480 F$="MAAK EEN ECHTE ZIN!":GOTO1580
1490 IFF<1 THEN1520
1500 F$=F$(F):F$(F)="":F=F-1:GOTO1580
1510 REM ** VERLEGENHEIDSVRAGEN STELLEN **
1520 IFTA<3 THEN1550
1530 F$="IK STOP ER MEE. DAG "+N$+"."
1540 GOSUB3310:SD=200:GOSUB450:GOSUB950
1550 GOSUB260:T=INT(RV*TV+.5):IFTV$(T)="" THEN1550
1560 TA=TA+1:F$=TV$(T):TV$(T)=""
1570 REM ** VRAAG STELLEN **
1580 GOSUB3310
1590 GOTO1320
2000 REM SUBROUTINES ======
2010 REM ** KADER **
2020 HO=X:FORVE=Q TOY:GOSUB110:PRINTX$:NEXTVE
2030 VE=Y:FORHO=X TOP:GOSUB110:PRINTX$:NEXTHO
2040 HO=P:FORVE=Y TOQ STEP-1:GOSUB110:PRINTX$:NEXTVE
2050 VE=Q:FORHO=P TOX STEP-1:GOSUB110:PRINTX$:NEXTHO
2060 RETURN
2200 REM ** INNAME ANTWOORD **
2210 E=0:E$="":GOSUB200:IFIN>0 THEN2210
2220 FORVE=VT-7 TOVT-5:HO=5:GOSUB110:PRINTSP$;" ":NEXTVE
2230 VE=VT-7:HO=5:GOSUB110:PRINT"-"
2240 GOSUB200:A$=IN$:IFIN=0 THEN2240
2250 IF(IN<>28)AND(IN<>127)THEN2330
2260 IFE<1 THEN2240
2270 IF(VE=VT-5)AND(HO=HT-6)AND(R=1)THENHO=HT-5:R=0
2280 GOSUB110:PRINT" "
2290 IF(VE>VT-4)AND(HO=5)THENVE=VE-1:HO=HT-4
2300 E=E-1:HO=HO-1:GOSUB110
2310 E$=LEFT$(E$,E):PRINT"-"
2320 GOTO2240
2330 IFIN=13 THENA$="."
2340 SR$=A$:GOSUB330:A$=SR$
2350 IF(A$<" ")OR(A$>"Z")THEN2240
2360 E$=E$+A$:E=E+1:GOSUB110
2370 PRINTA$:HO=HO+1:GOSUB110:PRINT"-"
2380 IF(A$="!")OR(A$=".")THEN2610
2390 IFA$="?" THENFF=1:GOTO2610
2400 IF(HO<HT-4)THEN2420
2410 GOSUB110:PRINT" ":HO=5:VE=VE+1
2420 GOSUB110:PRINT"-"
2430 IF(HO<>HT-5)OR(VE<>VT-5)THEN2450
2440 E=E-1:HO=HO-1:E$=LEFT$(E$,E):R=1
2450 GOTO2240
2600 REM ** IN WOORDEN SPLITSEN **
2610 IFE<1 THENRETURN
2620 IFPR=1 THENSR$=E$:GOSUB350:GOSUB360
2630 W=-1:FORI=1 TOE:A$=MID$(E$,I,1):B=ASC(A$)
2640 IF((B>AA-1)AND(B<AA+26))OR((B>47)AND(B<58))THEN2680
2650 IF(WA=0)OR(WT=1)THEN2700
2660 IFB=45 THENWT=1:GOTO2700
2670 WA=0:GOTO2700
2680 IFWA=0 THENWA=1:W=W+1:E$(W)="":IFW>EA-1 THEN2700
2690 E$(W)=E$(W)+A$
2700 NEXTI
2710 IFW>EA-1 THENRETURN
2720 REM ** 1e PERSOON VERVANGEN DOOR 2e PERSOON
2730 FORI=0 TOW:IFLEN(E$(I))>4 THEN2810
2740 Z$=E$(I)
2750 IFZ$="IK" THENE$(I)="JIJ":GOTO2810
2760 IF(Z$="JIJ")OR(Z$="JE")THENE$(I)="IK":GOTO2810
2770 IFZ$="MIJ" THENE$(I)="JOU":GOTO2810
2780 IFZ$="JOU" THENE$(I)="MIJ":GOTO2810
2790 IFZ$="MIJN" THENE$(I)="JOUW":GOTO2810
2800 IFZ$="JOUW" THENE$(I)="MIJN"
2810 NEXTI
2820 RETURN
3000 REM ** VRAGEN VOORBEREIDEN **
3010 IFF>FA-1 THENRETURN
3020 REM NIET GEBRUIKTE TEKSTEN ZOEKEN
3030 Z=0:FORJ=0 TOGA:T=S(S,J):IFT$(T)>"" THENZ(Z)=T:Z=Z+1
3040 NEXTJ
3050 IFZ=0 THENRETURN
3060 GOSUB260:T=Z(RV*Z):IF(T<=TK)AND(I=W)THENRETURN
3070 REM ** TEKST OPHALEN **
3080 F=F+1:F$(F)=T$(T):T$(T)="":IFT>TK THENRETURN
3090 REM ** TEKST SAMENSTELLEN **
3100 J0=I+1:H$=E$(I+1)
3110 IF(H$="IK")OR(H$="JE")OR(H$="JIJ")THENJ0=0
3120 FORJ=J0 TOW:IFJ=I THENJ=J+2
3130 IF(E$(J)="EN")OR(E$(J)="OF")THENJ=W:GOTO3150
3140 F$(F)=F$(F)+" "+E$(J)
3150 NEXTJ
3160 F$(F)=F$(F)+" "+T2$(T)
3170 RETURN
3300 REM ** BEELDSCHERM OPMAKEN **
3310 FORVE=5 TO8:HO=5:GOSUB110:PRINTSP$:NEXTVE
3320 X$=F$:F$=F$+" ":IFPR=1 THENSR$=X$:GOSUB350:GOSUB360
3330 VE=5
3340 Z=LEN(F$):IFZ>HT-10 THENZ=HT-10
3350 Z$=MID$(F$,Z,1):IFZ$<>" " THENZ=Z-1:GOTO3350
3360 HO=5:GOSUB110:PRINTLEFT$(F$,Z-1):VE=VE+1
3370 F$=RIGHT$(F$,LEN(F$)-Z):IFF$>=" " THEN3340
3380 HO=10
3390 RETURN
3600 REM ** DATA LEZEN **
3610 RESTORE:FORI=1 TOSA:READS$(I):J=0:B=ASC(S$(I))-AA
3620 IFS$(I)="$" THENGOTO950
3630 IFB(B)=0 THENB(B)=I
3640 READZ:IFZ=0 THEN3670
3650 X=0:IFZ>0 THENX=TK
3660 S(I,J)=ABS(Z)+X:J=J+1:GOTO3640
3670 NEXTI:READZ$
3680 FORI=TK+1 TOTK+TN:READT$(I):NEXTI:READZ$
3690 FORI=1 TOTK:READT$(I),T2$(I):NEXTI:READZ$
3700 FORI=0 TOTV-1:READTV$(I):NEXTI:READZ$
3710 FORI=0 TOTF-1:READTF$(I):NEXTI:READZ$
3720 IFZ$<>"$" THENPRINT"DATAFOUT";:GOSUB210:GOTO950
3730 RETURN
3900 REM ** DATA TELLEN **
3910 GOSUB3940:SA=Z:GOSUB3940:TN=Z:GOSUB3940
3920 TK=Z/2:GOSUB3940:TV=Z
3930 GOSUB3940:TF=Z:RETURN
3940 Z=0
3950 READZ$:IFZ$="$" THENRETURN
3960 A=ASC(Z$):IF(A>57)OR(A<45)OR(A=46)THENZ=Z+1
3970 GOTO3950
25000 REM
25010 REM --- DATA REGELS ---
25020 DATA"ARM",2,22,0
25030 DATA"BEN",-1,-12,-13,-19,0
25040 DATA"BENT",-17,-35,-37,-38,0
25050 DATA"BLIJF",-7,-30,0
25060 DATA"BROER",9,0
25070 DATA"COMPUTER",11,0
25080 DATA"DOCHTER",1,0
25090 DATA"DROEVIG",14,25,0
25100 DATA"ERGER",14,25,0
25110 DATA"FAMILIE",10,0
25120 DATA"GA",-15,0
25130 DATA"GEEF",-32,0
25140 DATA"GELD",2,0
25150 DATA"GELOOF",-5,27,29,0
25160 DATA"GELUK",23,0
25170 DATA"GEZIN",9,0
25180 DATA"GEZOND",21,22,0
25190 DATA"HAD",17,0
25200 DATA"HEB",-2,-22,-39,0
25210 DATA"HEBT",-36,0
25220 DATA"HEEFT",-2,5,0
25230 DATA"HOOP",-6,0
25240 DATA"KAN",-4,-24,0
25250 DATA"KOM",-33,0
25260 DATA"KON",17,0
25270 DATA"KUNT",-18,-42,0
25280 DATA"LIEF",6,28,0
25290 DATA"MAAK",-11,-21,-27,-28,0
25300 DATA"MANNEN",5,0
25310 DATA"MAG",-3,-29,0
25320 DATA"MISSCHIEN",13,0
25330 DATA"MOGELIJK",13,0
25340 DATA"MOET",-20,-26,0
25350 DATA"MOEDER",7,0
25360 DATA"OOM",10,0
25370 DATA"ONGELUK",14,23,0
25380 DATA"ONTEVREDEN",14,0
25390 DATA"PIEKER",20,0
25400 DATA"ROT",20,0
25410 DATA"RIJK",2,22,0
25420 DATA"SCHOONZUS",10,0
25430 DATA"SEX",26,0
25440 DATA"SPEL",16,0
25450 DATA"STOM",20,0
25460 DATA"STRIJD",25,0
25470 DATA"STRESS",25,0
25480 DATA"TANTE",10,0
25490 DATA"VADER",8,0
25500 DATA"VIND",-34,0
25510 DATA"VERDRIET",25,0
25520 DATA"VRIEND",1,5,0
25530 DATA"VROUW",3,4,0
25540 DATA"WAS",17,0
25550 DATA"WEER",18,0
25560 DATA"WEET",-10,0
25570 DATA"WENS",19,0
25580 DATA"WERD",17,0
25590 DATA"WIL",-8,-9,-41,0
25600 DATA"WORD",-9,-23,-40,-41,0
25610 DATA"WOU",-14,-16,-31,0
25620 DATA"ZEG",12,29,0
25630 DATA"ZIEK",22,0
25640 DATA"ZIN",24,0
25650 DATA"ZONEN",1,0
25660 DATA"ZOON",1,0
25670 DATA"ZORGEN",25,0
25680 DATA"ZUS",9,0
25690 DATA"ZWAGER",10,0
25700 DATA"$"
25710 REM TEKSTEN
25720 DATA"BEN JE BLIJ MET JE KINDEREN. WAT DOEN ZE?"
25730 DATA"GELD ALLEEN MAAKT NIET GELUKKIG!"
25740 DATA"VROUWEN ZIJN GELOOF IK EEN HACHELIJK THEMA."
25750 DATA"HOE STEL JE JE DE VROUW VAN JE DROMEN VOOR?"
25760 DATA"VIND JIJ MANNEN ERG BELANGRIJK?"
25770 DATA"MET DE LIEFDE IS HET NU EENMAAL ZO."
25780 DATA"VERTEL ME NOG WAT MEER OVER JE MOEDER."
25790 DATA"WAS JE VADER ERG STRENG VOOR JE?"
25800 DATA"VERTEL ME WAT MEER OVER JE BROERS EN ZUSSEN."
25810 DATA"HEB JE OOK FAMILIELEDEN DIE JE LEUK VINDT?"
25820 DATA"WAT VIND JE EIGENLIJK VAN COMPUTERS?"
25830 DATA"DAT VIND IK FLAUWE KUL."
25840 DATA"JE LIJKT ME EEN BEETJE ONZEKER TE ZIJN."
25850 DATA"HOU NEGATIEVE GEDACHTEN OP EEN AFSTAND."
25860 DATA"JE BENT TOCH NIET GETROUWD - OF WEL?"
25870 DATA"OLIEBOL!"
25880 DATA"ZO ZEKER SCHIJNT DAT NIET TE ZIJN - OF TOCH?"
25890 DATA"WEET JE GEEN LEUKER ONDERWERP DAN HET WEER?"
25900 DATA"VERLANGENS ZIJN DE MOTOR VAN DE VOORUITGANG."
25910 DATA"GEEN SCHELDWOORDEN ALSJEBLIEFT!"
25920 DATA"A VOTRE SANTE! (ICH SPREEK OOK FRANS)"
25930 DATA"JE KUNT OOK ZEGGEN: LIEVER RIJK DAN ARM."
25940 DATA"KEN JE HET VERHAAL VAN HANS EN HET GELUK ?"
25950 DATA"APROPOS ZIN: IK HEB WEL ZIN IN EEN KOP KOFFIE."
25960 DATA"BEKIJK HET LEVEN VAN DE VROLIJKE KANT!"
25970 DATA"COMPUTERS HEBBEN EEN BOEIEND SEXLEVEN - 1 EN 0!"
25980 DATA"DAT GELOOF IK NIET ZO MAAR METEEN."
25990 DATA"GAAT DE LIEFDE BIJ JOU DOOR DE MAAG?"
26000 DATA"MEEN JE DAT ECHT?"
26010 DATA"$"
26020 REM KOMPOSITIE-TEKSTEN
26030 DATA"BEN JE EEN BEETJE TROTS, DAT JE","BENT?"
26040 DATA"WAT TOEVALLIG, IK HEB OOK","."
26050 DATA"WIE BEPAALT ER, DAT JE","MAG?"
26060 DATA"DAT DACHT IK WEL, DAT JE","KUNT."
26070 DATA"WAAROM GELOOF JE","?"
26080 DATA"HOOP JE NOG OP IETS ANDERS, BEHALVE","?"
26090 DATA"VOLHOUDEN IS MOOI. MAAR IS HET VERSTANDIG,"
26100 DATA"TE BLIJVEN?"
26110 DATA"WAAR DENK JE AAN, ALS JE","WILT?"
26120 DATA"LIJKT HET JE BETER, ALS JE","ZOU?"
26130 DATA"HOE BEDOEL JE PRECIES, DAT JE WIST","?"
26140 DATA"DOE MAAR",".IK VIND HET BEST."
26150 DATA"WIE INTERESSEERT HET NOU, DAT JE","BENT?"
26160 DATA"O, IK WAS OOK ZO GRAAG","."
26170 DATA"MISSCHIEN ZOU HET BETER ZIJN ","."
26180 DATA"IK ZOU EIGENLIJK OOK WEL","GAAN."
26190 DATA"WAAR DENK JE AAN ALS JE","ZOU WILLEN?"
26200 DATA"OVERIGENS: JE BENT ZELF","!"
26210 DATA"WEDDEN DAT JE ZELF","KUNT?"
26220 DATA"ZO. JIJ BENT DUS",".ZOU DAT IEMAND SCHOKKEN?"
26230 DATA"KAN IK JE HELPEN, ALS JE DE VOLGENDE KEER"
26240 DATA"MOET?"
26250 DATA"VERTEL ME EENS VERDER, HOE JE MOET","."
26260 DATA"WAARDOOR KOMT HET, DENK JE, DAT JE","HEBT?"
26270 DATA"WAAROM WORD JE","?"
26280 DATA"KUN JE ME UITLEGGEN, HOE JE","KUNT?"
26290 DATA"WIE HEEFT ER VERDER NOG","?"
26300 DATA"ER ZIJN ER MEER ZO. IK MOET EIGENLIJK OOK","."
26310 DATA"WAT DENK JE INTUSSEN, ALS JE","MAAKT?"
26320 DATA"HOE ZOU HET ZIJN, ALS IEDEREEN","ZOU MAKEN?"
26330 DATA"IEMAND MOET MIJ EENS VERTELLEN, DAT IK","MAG!"
26340 DATA"WAAROM KUN JE NIET FLINK ZIJN EN","BLIJVEN?"
26350 DATA" "," - DAT WILLEN ER NOG WEL MEER!"
26360 DATA"IK VIND HET EDELMOEDIG VAN JE, DAT JE"
26370 DATA"GEEFT."
26380 DATA"KOMT ER VERDER NOG IEMAND","?"
26390 DATA"IK BEN HET MET JE EENS. IK VIND OOK","."
26400 DATA"OK, IK BEN"," - EN WAT BEN JIJ DAN?"
26410 DATA"IEDEREEN WEET TOCH ZEKER, DAT IK","HEB?"
26420 DATA"HET HEEFT LANG GEDUURD VOOR JE MERKT, DAT IK"
26430 DATA"BEN."
26440 DATA"SUFFERD! IEDEREEN WEET TOCH, DAT IK","BEN."
26450 DATA"BEN JE BLIJ, DAT JE","HEBT?"
26460 DATA"WIE ZAL VERDER NOG","?"
26470 DATA"ALS IEDEREEN","WOU!"
26480 DATA"WAAROM MOET IK KUNNEN","?"
26490 DATA"DENK JE DAT IK HET ZO KNAP VIND, DAT JIJ"
26500 DATA"KUNT?","$"
26510 REM VERLEGENHEIDS-TEKSTEN
26520 DATA"BEN JE ALTIJD ZO ONTZETTEND FLAUW?"
26530 DATA"VERTEL ME EENS, WAT JE VAN MIJ DENKT."
26540 DATA"DENK JE, DAT IK KNAPPER BEN DAN JIJ?"
26550 DATA"WAAROM PRAAT JE EIGENLIJK MET EEN COMPUTER?"
26560 DATA"KUN JE NIET WAT INTERESSANTERS VERTELLEN?"
26570 DATA"BEN JE NET ZO INTELLIGENT ALS IK?"
26580 DATA"HOE VIND JE EIGENLIJK ONZE CONVERSATIE?"
26590 DATA"ALS HET JE TE DOL WORDT: STEKKER ER UIT!"
26600 DATA"EEN GESPREK MET JOU IS TAMELIJK ONNOZEL."
26610 DATA"NAAR JE ANTWOORDEN GEMETEN, BEN JE GEEN LICHT !"
26620 DATA"$"
26630 REM VERLEGENHEIDS-TEKSTEN OP ?
26640 DATA"JE VRAGENSTELLERIJ WERKT OP MIJN ZENUWEN!"
26650 DATA"DOMME VRAAG! DAT WEET TOCH IEDEREEN !"
26660 DATA"GEEN IDEE! WAT VERWACHT JE VAN MIJ?"
26670 DATA"DAT WEET JIJ BETER DAN IK. VERTEL VERDER!"
26680 DATA"$"
30000 REM -------------------------------
30010 REM DIT PROGRAMMA SIMULEERT EEN
30020 REM GESPREK MET DE COMPUTER.
30030 REM
30040 REM HET EERSTE PROGRAMMA VAN DEZE SOORT
30050 REM KOMT VAN PROF.WEIZENBAUM (USA)
30060 REM EN DRAAGT DE NAAM - ELIZA -
30070 REM
30080 REM VOOR DE KC85/3 WERD IN MUEHL-
30090 REM HAUSEN DE BASIC-VERSIE - VIC -
30100 REM GESCHREVEN. DAARUIT ONTSTOND DE
30110 REM BASICODE-VERSIE.
30120 REM
30130 REM Iets over de werking...
30140 REM Het eerste blok DATA zijn zoekwoorden.
30150 REM Elke zin wordt op deze woorden onderzocht.
30160 REM Bij aantreffen van een zoekwoord geven de
30170 REM getallen er echter de passende antwoord-
30180 REM zinnen aan; positieve getallen wijzen naar
30190 REM blok 2, negatieve naar blok 3.
30200 REM Bij niet aantreffen wordt een tekst
30210 REM genomen uit blok 4 of als het om een
30220 REM vraag gaat uit blok 5.
30230 REM Voor een vlotte verwerking staan de
30240 REM zoekwoorden alfabetisch in array S$()
30250 REM en de doorwijsgetallen in array S(,)
30260 REM De verdere blokken staan in T$(),T2$(),
30270 REM TV$() en TF$()
30280 REM In E$() worden de losse woorden van de
30290 REM ingetikte zinnen opgeslagen.
30300 REM
30310 REM Dit kleine programma heeft maar een deel
30320 REM van de intelligentie van het ELIZA
30330 REM programma, maar het geeft een aardige
30340 REM indruk van een interview-simulatie.
30350 REM
30360 REM NOG EEN TIP !!!!
30370 REM
30380 REM Tijdens de conversatie met dit
30390 REM programma kunt u het beste het
30400 REM woordje 'je' vermijden.
30410 REM Gebruik ofwel 'jij'
30420 REM         ofwel 'jouw'
30430 REM afhankelijk van de betekenis.
30440 REM
32010 REM ******************
32020 REM *                *
32030 REM * VIC-INTERVIEW  *
32040 REM * eine ELIZA-    *
32050 REM *  Adaption      *
32060 REM *                *
32070 REM * in BASICODE-3  *
32080 REM *      von       *
32090 REM *                *
32100 REM * Thomas Goetze  *
32110 REM * Troeglitz      *
32120 REM * DDR            *
32130 REM *                *
32140 REM * KC85/4         *
32150 REM * 24.02.1990     *
32160 REM *                *
32170 REM ******************
32180 REM
32190 REM Erstausstrahlung Radio DDR
32200 REM-spezial 900316
32210 REM Nederl.bewerking Ch.W.Brederode
32220 REM
32230 REM TROS-RADIO dd 900718
32240 REM incl.corr. dd 900725

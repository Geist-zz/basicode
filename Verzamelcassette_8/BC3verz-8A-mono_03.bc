
1000 A=500:GOTO20:REM LOTTO CIJFERS
1010 HT=HO:VP=VE:REM SCHERMMATEN
1020 GOSUB5010:REM UITLEG
1030 DIMLK(6)
1040 REM LOTTO KAART
1050 T$="Momentje, a.u.b.":VE=17:GOSUB2010
1060 FORJ=1 TO6:LK(J)=0
1070 GOSUB260:RN=INT(RV*41)+1
1080 TT=0:FORV=1 TOJ:IFRN=LK(V)THENTT=1:V=J
1090 NEXTV:IFTT=1 THEN1070
1100 LK(J)=RN:NEXTJ
1110 T$="Hier zijn de Lotto cijfers":VE=17:GOSUB2010
1120 T$="<RETURN=STOPPEN>":VE=23:GOSUB2010
1130 GOSUB4010:GOSUB250
1140 T$="<Toets voor verder)":VE=7:GOSUB2010
1150 GOSUB210:IFIN=13 THEN950
1160 GOTO1060
2000 REM CENTREREN
2010 HO=INT((HT-1-LEN(T$))/2)
2020 GOSUB110:PRINTT$;
2030 RETURN
3000 REM LOTTO KAART OPBOUWEN
3010 VE=4
3020 HO=INT(HT-30):GOSUB110
3030 PRINT"==================="
3040 VE=VE+1:GOSUB110:PRINT":  :  :  :  :  :  :"
3050 VE=VE+1:GOSUB110:PRINT"==================="
3060 HW=HO+1
3070 RETURN
4000 REM TOON LOTTO KAART
4010 GOSUB3010
4020 FORX=1 TO6
4030 HO=HW+(X-1)*3:VE=5:GOSUB110
4040 SR=LK(X):CT=2:CN=0:GOSUB310
4050 PRINTSR$
4060 NEXTX
4070 RETURN
5000 REM UITLEG
5010 T$="LOTTO":VE=2:GOSUB2010
5020 T$="Lotto is een programma ter onder- ":VE=4:GOSUB2010
5030 T$="steuning bij het invullen van uw  ":VE=5:GOSUB2010
5040 T$="lottoformulier.":VE=6:GOSUB2010
5050 T$="In een kader verschijnen 6 cijfers":VE=8
5060 GOSUB2010
5070 T$="na indrukken van een willekeurige ":VE=9:GOSUB2010
5080 T$="toets komen er elke keer weer 6   ":VE=10:GOSUB2010
5090 T$="nieuwe cijfers.":VE=11:GOSUB2010
5100 T$="Voor Belgie & Duitsland regel 1070":VE=13:GOSUB2010
5110 T$="aanpassen, (rv*41)+1 word dan,    ":VE=14:GOSUB2010
5120 T$="(rv*46)+1 of (rv*49)+1":VE=15:GOSUB2010
5130 T$="<RETURN=STOPPEN>      Veel Succes.":VE=18:GOSUB2010
5140 T$="<toets voor verder>":VE=24:GOSUB2010
5150 GOSUB210:GOSUB100:RETURN
30000 REM DIT PROGRAMMA IS GEMAAKT OM
30010 REM ALS HULPMIDDEL TE DIENEN BIJ
30020 REM HET INVULLEN VAN UW LOTTO-
30030 REM FORMULIER
30040 REM
30050 REM ****************************
30060 REM *                          *
30070 REM *   ONTWORPEN OP EEN       *
30080 REM *                          *
30090 REM *      COMMODORE 64        *
30100 REM *                          *
32000 REM *   A VAN STEENBERGEN      *
32010 REM *                          *
32020 REM *   BURG V HAARENLN 1147   *
32030 REM *                          *
32040 REM *   3118 GC SCHIEDAM       *
32050 REM *                          *
32060 REM *   TEL: 010-4269488       *
32070 REM *                          *
32080 REM *   D.D.891211             *
32090 REM *                          *
32100 REM *   VOOR TROS BASICODE     *
32110 REM *                          *
32120 REM *   BASICODE-3 PROGRAMMA   *
32130 REM *                          *
32140 REM ****************************
32150 REM
32160 REM TROS-RADIO DD 900418

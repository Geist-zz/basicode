
1000 A=500:GOTO20:REM PAARDENKOERS
1010 GOTO9000:REM ** UITLEG **
1020 DIMA(6,2),B$(6)
1030 GOSUB100:R=1:B=1500:P$=".-"
1040 REM ** NAMEN VAN PAARDEN INLEZEN **
1050 FORI=1 TO6
1060 READB$(I),A(I,2)
1070 NEXTI
1080 REM ** HOOFD-ROUTINE **
1090 GOSUB2010:REM koersen op nul zetten
1100 GOSUB3010:REM start koers
1110 GOSUB2230
1120 IFH=0 THEN1100
1130 GOSUB4010
1140 IFH=1 THEN1090
2000 REM ** KOERS OP NUL ZETTEN **
2010 GOSUB100:Y=0:D=0:X=0:C=5
2020 REM ** INVOER KEUZE WEDDENSCHAP **
2030 VE=2:HO=2:GOSUB110:PRINT"koers nummer ";R
2040 SR=B:GOSUB300:SR$=SR$+P$
2050 HO=20:GOSUB110:PRINT"max inzet ";SR$
2060 FORI=1 TO6
2070 VE=C:HO=1:GOSUB110:PRINT;I;"  ";B$(I)
2080 C=C+2:NEXTI
2090 VE=20:HO=14:GOSUB110:PRINT"0 = stoppen"
2100 VE=18:HO=4:GOSUB110:PRINT"Wat is uw keuze ";:GOSUB210
2110 K=VAL(IN$):IFK=0 THEN4340
2120 IF(K<0)OR(K>6)THEN2010
2130 GOSUB100:VE=2:HO=4:GOSUB110:PRINT"u zet in op ";B$(K)
2140 SR=B:GOSUB300:SR$=SR$+P$
2150 VE=4:HO=4:GOSUB110:PRINT"U kunt maximaal ";SR$;
2160 PRINT" inzetten"
2170 VE=6:HO=4:GOSUB110:INPUT"uw inzet is : ";I$
2180 L=VAL(I$):IF(L<=0)OR(L>B)THEN2130
2190 REM ** DE PAARDEN OP START ZETTEN **
2200 FORI=1 TO6:A(I,1)=1:NEXTI
2210 REM OPZETTEN KOERS
2220 GOSUB100:GOSUB250
2230 X=1
2240 FORI=1 TO6:GOSUB260
2250 A(I,1)=A(I,1)+RV*3
2260 IFA(X,1)>A(I,1)THEN2280
2270 X=I
2280 NEXTI
2290 H=0
2300 IFA(X,1)<=35 THENRETURN
2310 H=1:RETURN
3000 REM ** START KOERS **
3010 C=7:SR=L:GOSUB300:SR$=SR$+P$
3020 VE=1:HO=10:GOSUB110:PRINT"koers nummer ";R
3030 VE=3:HO=1:GOSUB110:PRINT"u heeft fl. ";SR$;
3040 PRINT" ingezet op paard nr ";K
3050 IFX>0 THEN GOSUB3560
3060 REM ** PRINT BAAN **
3070 L$="*------*------*------"
3080 L$=L$+L$+L$+L$
3090 D=D+1
3100 HO=0:VE=5:GOSUB110
3110 PRINTMID$(L$,INT(D),40);
3120 HO=0:VE=19:GOSUB110
3130 PRINTMID$(L$,INT(D),40);
3140 REM ** PLAATS STARTLIJN **
3150 IFY<=2 THENE=8:S=5:HH=2:X$="START":GOSUB3510
3160 REM ** WIS STARTLIJN **
3170 IFY>=3 THENE=8:S=5:HH=2:X$="     ":GOSUB3510
3180 FORI=1 TO6
3190 VE=C:HO=0:GOSUB110
3200 FORA=1 TO34:PRINT" ";:NEXTA
3210 E$=LEFT$(">>>>>>",I)
3220 E$=RIGHT$(E$,1)
3230 Y=INT(A(I,1)+.5)
3240 REM ** PLAATS FINISH LIJN **
3250 IF(Y>22)AND(Y<24)THEN3500
3260 VE=C:HO=Y:GOSUB110:PRINTE$
3270 IFY>=35 THEN3290
3280 C=C+2:NEXTI
3290 RETURN
3500 E=7:S=6:HH=35:X$="FINISH":GOSUB3510:GOTO3260
3510 FORF=1 TOS
3520 K$=LEFT$(X$,F)
3530 K$=RIGHT$(K$,1)
3540 VE=E:HO=HH:GOSUB110:PRINTK$
3550 E=E+2:NEXTF:RETURN
3560 VE=21:HO=1:GOSUB110
3570 PRINT"op kop ligt paard nr: ";X;" ";B$(X)
3580 RETURN
4000 REM ** BEREKENEN UITSLAG KOERS **
4010 IFX=K THEN4030
4020 Z=L:B$="verloren":B=B-L:GOTO4040
4030 Z=L:B$="gewonnen":B=B+L
4040 W=W+1:G=G+L
4050 REM ** PRINT WINNAAR **
4060 IFIN=78 THEN4340
4070 VE=21:HO=0:GOSUB110
4080 PRINT"                                       "
4090 VE=21:GOSUB110
4100 PRINT"winnaar is paard nr. ";X;" ";B$(X)
4110 SD=40:GOSUB450:GOSUB100
4120 REM ** GEWONNEN OF VERLOREN BEDRAG **
4130 VE=2:HO=0:GOSUB110
4140 PRINT"Het paard ";B$(K);" heeft ";B$
4150 VE=4:HO=0:GOSUB110
4160 PRINT"U heeft ingezet ";L;" en ";B$;" ";Z
4170 VE=6:HO=5:GOSUB110
4180 PRINT"nog een keer j/n ";:GOSUB210
4190 IFIN=74 THENH=1:R=R+1:IFB>0 THENRETURN
4200 IFIN=78 THEN4340
4210 GOSUB100:PRINT:PRINT:PRINT
4220 SR=W:GOSUB300
4230 PRINT"U heeft ";SR$;" keer een weddenschap afge-"
4240 PRINT:PRINT"sloten met een totaal bedrag van"
4250 SR=G:GOSUB300:SR$=SR$+P$
4260 PRINT:PRINT"fl. ";SR$;" zodat het saldo nu fl. 0.- is."
4270 PRINT:PRINT"Waardoor verder spelen niet mogelijk is."
4280 PRINT:PRINT
4290 PRINT"      DE VOLGENDE KEER MEER GELUK"
4300 E$=" <--TOT ZIENS ":L$="          ":E$=E$+L$+E$
4310 FORZ=1 TO40 STEP.1
4320 HO=0:VE=16:GOSUB110:PRINTMID$(E$,INT(Z),39);
4330 NEXTZ:GOTO950
4340 GOSUB100:PRINT:PRINT:PRINT
4350 SR=W:GOSUB300
4360 PRINT"U heeft ";SR$;" keer een weddenschap afge-"
4370 PRINT:PRINT"sloten met een totaal bedrag van"
4380 SR=G:GOSUB300:SR$=SR$+P$
4390 PRINT:PRINT"fl. ";SR$;" zodat het totaal saldo nu"
4400 SR=B:GOSUB300:SR$=SR$+P$
4410 PRINT:PRINT"fl. ";SR$;" is."
4420 PRINT:PRINT:PRINT
4430 PRINT"          WILT U NOG EEN GOKJE "
4440 PRINT:PRINT"        WAGEN MET DIT BEDRAG? J/N ";
4450 GOSUB210:IFIN=74 THEN1090
4460 E$=" <--TOT ZIENS ":L$="          ":E$=E$+L$+E$
4470 FORZ=1 TO40 STEP.1
4480 HO=0:VE=19:GOSUB110:PRINTMID$(E$,INT(Z),39);
4490 NEXTZ:GOTO950
9000 GOSUB100:H=20:LK=80
9010 VE=3:B$="**** **** **** **** ***  **** *   *"
9020 GOSUB9230
9030 VE=4:B$="*  * *  * *  * *  * *  * *    *   *"
9040 GOSUB9230
9050 VE=5:B$="*  * *  * *  * *  * *  * *    **  *"
9060 GOSUB9230
9070 VE=6:B$="**** **** **** **** *  * ***  * * *"
9080 GOSUB9230
9090 VE=7:B$="*    *  * *  * **   *  * *    *  **"
9100 GOSUB9230
9110 VE=8:B$="*    *  * *  * * *  *  * *    *   *"
9120 GOSUB9230
9130 VE=9:B$="*    *  * *  * *  * ***  **** *   *"
9140 GOSUB9230:H=20
9150 VE=13:B$="*    **** **** **** ****":GOSUB9230
9160 VE=14:B$="*  * *  * *    *  * *   ":GOSUB9230
9170 VE=15:B$="* *  *  * *    *  * *   ":GOSUB9230
9180 VE=16:B$="**   *  * ***  **** ****":GOSUB9230
9190 VE=17:B$="**   *  * *    **      *":GOSUB9230
9200 VE=18:B$="* *  *  * *    * *     *":GOSUB9230
9210 VE=19:B$="*  * **** **** *  * ****":GOSUB9230
9220 SD=30:GOSUB450:GOTO9260
9230 FORZ=1 TOH+LEN(B$)/2
9240 HO=40-Z:GOSUB110:PRINTLEFT$(B$,Z);" ";
9250 FORY=1 TOLK:NEXTY:NEXTZ:PRINT:RETURN
9260 GOSUB100:PRINTTAB(13);"PAARDEN KOERS":PRINT
9270 PRINT:PRINT"In dit programma kunt u een gokje"
9280 PRINT"wagen op de renbaan."
9290 PRINT"Als startkapitaal krijgt u Fl. 1500.-"
9300 PRINT"Daarna geeft u aan op welk paard u een"
9310 PRINT"weddenschap wilt afsluiten."
9320 PRINT"Als het geluk met u is kunt altijd maar"
9330 PRINT"door spelen. Als het saldo Fl. 0.- is"
9340 PRINT"kunt u niet meer verder spelen en stopt"
9350 PRINT"het programma."
9360 PRINT"Veel plezier en geluk met dit spel."
9370 PRINT:PRINT:PRINTTAB(10);"DRUK EEN TOETS"
9380 GOSUB210:GOTO1020
25000 DATA"QUENDEL ",1,"ARMONI  ",2,"MONTERIA",3
25010 DATA"PETRUS  ",4,"LIESANT ",5,"DEXTER  ",6
32000 REM
32010 REM ****** DECEMBER  1989 ******
32020 REM *                          *
32030 REM *       PAARDENKOERS       *
32040 REM *                          *
32050 REM *         GEMAAKT          *
32060 REM *                          *
32070 REM *          DOOR            *
32080 REM *                          *
32090 REM *       R.J. V/D ZEE       *
32100 REM *                          *
32110 REM *       BRAAMKAMP 40       *
32120 REM *                          *
32130 REM *     7206 HB ZUTPHEN      *
32140 REM *                          *
32150 REM *    Tel.  05750-21736     *
32160 REM *                          *
32170 REM *******  BASICODE 3  *******
32180 REM
32190 REM TROS-RADIO dd 900718

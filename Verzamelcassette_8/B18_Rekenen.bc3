
1000 A=2000:GOTO20:REM LETTERSPEL
1010 PRINT"EEN OGENBLIK GEDULD A.U.B":REM **INLEZEN DATA**
1020 V=0:FB=12:C$="":K=0:W=0:TL=0
1030 READA$:IF(A$)="**" THEN1050
1040 W=W+1:TL=TL+LEN(A$):GOTO1030
1050 DIMC$(W):RESTORE
1060 FORI=1 TOW:READC$(I):NEXTI
1070 W$="                                      "
1080 REM ** LOGO + INSTRUCTIE **
1090 GOSUB3010
1100 GOSUB2010:GOSUB1700
1110 GOSUB2310:GOSUB2110:GOSUB1910:GOSUB2410
1120 IFC$<>Z$ THEN1110
1130 GOSUB10:VE=5:HO=5:GOSUB110
1140 PRINT"Wil je nog een woord doen? j/n";
1150 GOSUB210:IFIN=74 THEN GOSUB100:GOTO1180
1160 IFIN=78 THEN4010
1170 GOTO1150
1180 IFK<W THEN1100
1190 VE=7:HO=8:GOSUB110:PRINT"Meer woorden zijn er niet"
1200 SD=10:GOSUB450:GOTO4010
1500 REM ** HOOFDPROGRAMMA **
1510 SD=40:GOSUB450:GOTO4010
1700 L=LEN(C$)
1710 Z$=LEFT$("------------------------------",L)
1720 RETURN
1900 REM ** ZOEKSTRING MAKEN **
1910 V=0:FORI=1 TOL
1920 IFMID$(C$,I,1)<>L$ THEN1960
1930 V=V+1
1940 IFI<2 THENZ$=L$+MID$(Z$,I+1,L):GOTO1960
1950 Z$=LEFT$(Z$,I-1)+L$+MID$(Z$,I+1,L)
1960 NEXTI
1970 B=B+1:IFV=0 THENF=F+1
1980 RETURN
2000 REM ** WOORD GENEREREN **
2010 GOSUB260:I=INT(RV*W+1)
2020 IFC$(I)="*" THEN2010
2030 C$=C$(I):C$(I)="*"
2040 K=K+1:B=0:F=0:P=0:G$="":RETURN
2100 REM ** LETTER HALEN **
2110 VE=12:HO=2:GOSUB110
2120 PRINT"Welke letter denk je nodig te hebben:"
2130 VE=14:HO=15:GOSUB110:PRINT"      "
2140 VE=14:HO=5:GOSUB110:PRINT"DE LETTER ";:GOSUB210
2150 IFIN>64 THEN2170
2160 GOSUB250:GOTO2140
2170 SR$=IN$:GOSUB330:L$=SR$
2180 G$=G$+L$+","
2190 RETURN
2300 REM ** SCHERM OPBOUW **
2310 VE=5:HO=20:GOSUB110:PRINTW$
2320 VE=18:HO=10:GOSUB110:PRINTW$
2330 VE=20:HO=10:GOSUB110:PRINTW$
2340 VE=2:HO=3:GOSUB110:PRINT"BEURT ";B-P
2350 HO=20:GOSUB110:PRINT"AANTAL LETTERS:";L
2360 VE=5:HO=2:GOSUB110:PRINT"GEBRUIKTE LETTERS ";G$
2370 VE=9:HO=5:GOSUB110:PRINT"WOORD? ";Z$
2380 RETURN
2400 REM ** TEST OPLOSSING **
2410 IF(V=0)AND(F>=FB)THEN2610
2420 IFV=0 THENRETURN
2430 VE=18:HO=10:GOSUB110:PRINTZ$
2440 VE=20:HO=10:GOSUB110:INPUT"OPLOSSING: ";S$
2450 SR$=S$:GOSUB330:IFSR$=C$ THEN2500
2460 P=P+1:IFB<=1 THENP=0
2470 RETURN
2500 GOSUB100:VE=2:HO=5:GOSUB110:PRINTC$
2510 REM ** GOEDE OPLOSSING **
2520 VE=5:HO=2:GOSUB110:PRINT"Dit was de goede oplossing"
2530 VE=7:HO=6:GOSUB110
2540 E$=" beurten"
2550 IFB<=1 THENB$=LEFT$(E$,6)+"."
2560 IFB>1 THENB$=E$+"."
2570 SR=B:GOSUB300
2580 PRINT"Geraden in totaal ";SR$;B$
2590 SD=40:GOSUB450:GOTO2640
2600 REM ** FOUTE OPLOSSING **
2610 GOSUB100:VE=5:HO=6:GOSUB110
2620 PRINT"Maximum beurten bereikt."
2630 VE=7:HO=4:GOSUB110:PRINT"De oplossing is:";C$
2640 Z$=C$:SD=40:GOSUB450:RETURN
3000 REM ** LOGO **
3010 GOSUB100:READA$
3020 READA,B
3030 IF(A=40)AND(B=40)THEN3070
3040 IF(A=39)AND(B=39)THEN3060
3050 VE=A:HO=B:GOSUB110:PRINT"*":GOTO3020
3060 SD=10:GOSUB450:GOTO3020
3070 SD=40:GOSUB450
3080 REM ** INSTRUCTIE **
3090 GOSUB100:VE=1:HO=14:GOSUB110:PRINT"LETTER SPEL"
3100 PRINT:PRINT:PRINT"In dit spel moet je proberen";
3110 PRINT" een woord"
3120 PRINT"te vinden door letter voor letter dit"
3130 PRINT"in te tikken."
3140 PRINT"Er wordt aangegeven uit hoeveel letters"
3150 PRINT"het woord bestaat en welke letters je"
3160 PRINT"al gebruikt hebt."
3170 PRINT"Als je na 12 letters het woord nog niet"
3180 PRINT"gevonden hebt, het raden naar het woord"
3190 PRINT"wordt daarbij niet meegeteld, dan geeft"
3200 PRINT"de computer het antwoord."
3210 VE=22:HO=13:GOSUB110:PRINT"DRUK EEN TOETS":GOSUB210
3220 GOSUB100:VE=5:HO=0:GOSUB110
3230 SR=W:GOSUB300
3240 PRINT"In dit spel zitten ";SR$;" woorden met in"
3250 SR=TL:GOSUB300
3260 VE=7:GOSUB110:PRINT"het totaal ";SR$;" letters."
3270 VE=22:HO=13:GOSUB110:PRINT"DRUK EEN TOETS"
3280 GOSUB210:GOSUB100:RETURN
4000 REM ** EINDE PROGRAMMA **
4010 GOSUB100:VE=2:HO=0:GOSUB110
4020 FORI=1 TO40:PRINT"*";:NEXTI
4030 VE=7:HO=10:GOSUB110:PRINT"TOT"
4040 VE=10:HO=15:GOSUB110:PRINT"DE"
4050 VE=13:HO=20:GOSUB110:PRINT"VOLGENDE"
4060 VE=16:HO=30:GOSUB110:PRINT"KEER"
4070 VE=20:HO=0:GOSUB110
4080 FORI=1 TO40:PRINT"*";:NEXTI
4090 SD=40:GOSUB450:GOSUB250:GOTO950
25000 REM ** DATA WOORDEN **
25010 DATA"FLATGEBOUW","RADIO","TUIN","COMPUTER","AUTO"
25020 DATA"HOBBYSCOOP","TELESCOOP","WEERSTAND","HUIS"
25030 DATA"KLEUR","GARAGE","JAARGETIJDE","BASISSCHOOL"
25040 DATA"MEESTER","SLAGER","BAKKER","TORENFLAT"
25050 DATA"BENZINEPOMP","COMMODORE","AUTOMONTEUR"
25060 DATA"GEBOORTE","DAGBOEK","KRANT","POLITIEAGENT"
25070 DATA"VIDEORECORDER","BELASTING","AANSLAG","BASICODE"
25080 DATA"HUISMEESTER","TEGELZETTER","HOVENIER","SCHOOL"
25090 DATA"WOORD","KRACHTVELD","LANDSCHAP","TAXI","CARAVAN"
25100 DATA"DOKTER","MINIMUM","MEISJE","ACHTERLICHT","BOEK"
25110 DATA"ONGEVEER","WAPENKUNDE","VEROUDERD","STERRENKUNDE"
25120 DATA"KLINKERS","MEDEKLINKERS","WOORDENBOEK","BIJBEL"
25130 DATA"VROUWELIJK","MEERVOUD","GRAMMATICA","CAMPING"
25140 DATA"NEDERLANDER","AAP","ADAMSAPPEL","ADDER","FLUIT"
25150 DATA"AFHANGEN","AFKOELEN","KEREN","AFSTANDSBEDIENING"
25160 DATA"ALFABET","ALGABRA","ARRESLEE","RECHTBANK"
25170 DATA"BAKBOORD","AUTOBAND","BALPEN","BLOEMENCORSO"
25180 DATA"BOOM","BROOD","REKENEN","TAAL","TROMMEL"
25190 DATA"GEMEENTE","TROUWRING","GESPANNEN","TIMMERMAN"
25200 DATA"GEVAAR","GIPS","POTLOOD","GRAFKAMER","GYMPJES"
25210 DATA"HAPJE","INSTORTEN","KLANT","KLAP","LEUGENAAR"
25220 DATA"MEDEWERKING","NAZOMER","WINTER","SNEEUWPOP","LAT"
25230 DATA"VISSEN","OVERLADEN","PIJPSLEUTEL","PINKSTEREN"
25240 DATA"ROKEN","SCHIETBAAN","SCHILMESJE","SCHRIKKELJAAR"
25250 DATA"HAMER","RAADSLID","BURGEMEESTER","STEKKER","KLOP"
25260 DATA"TRACTOR","STEMPEL","STOOMBOOT","STOFZUIGER"
25270 DATA"TALENT","TEKENBORD","TERRAS","TESTBEELD","TOON"
25280 DATA"WOORDENBESTAND","OPLOSSING","AUTOMATISERING"
25290 DATA"**"
25300 DATA2,4,3,4,4,4,5,4,6,4,7,4,8,4,8,5,8,6,8,7,39,39
25310 DATA2,10,2,11,2,12,2,13,3,10,4,10,5,10,5,11,5,12
25320 DATA6,10,7,10,8,10,8,11,8,12,8,13,39,39,2,15,2,16
25330 DATA2,17,2,18,2,19,3,17,4,17,5,17,6,17,7,17,8,17
25340 DATA39,39,2,21,2,22,2,23,2,24,2,25,3,23,4,23,5,23
25350 DATA6,23,7,23,8,23,39,39,2,27,2,28,2,29,2,30,3,27
25360 DATA4,27,5,27,5,28,5,29,6,27,7,27,8,27,8,28,8,29
25370 DATA8,30,39,39,2,32,2,33,2,34,2,35,2,36,3,32,3,36
25380 DATA4,32,4,36,5,32,5,33,5,34,5,35,5,36,6,32,6,34
25390 DATA7,32,7,35,8,32,8,36,39,39,13,9,13,10,13,11,13
25400 DATA12,13,13,14,9,15,9,16,9,17,9,17,10,17,11,17,12
25410 DATA17,13,18,13,19,13,20,13,21,13,21,12,21,11,21
25420 DATA10,21,9,39,39,13,16,13,17,13,18,13,19,13,20
25430 DATA14,16,14,20,15,16,15,20,16,16,16,20,17,16,17
25440 DATA17,17,18,17,19,17,20,18,16,19,16,20,16,21,16
25450 DATA39,39,13,22,13,23,13,24,13,25,13,26,14,22,15
25460 DATA22,16,22,17,22,17,23,17,24,18,22,19,22,20,22
25470 DATA21,22,21,23,21,24,21,25,21,26,39,39,13,28,14
25480 DATA28,15,28,16,28,17,28,18,28,19,28,20,28,21,28
25490 DATA21,29,21,30,21,31,21,32,40,40
32000 REM
32010 REM ******* DECEMBER 1989 *******
32020 REM
32030 REM       LETTERSPEL GEMAAKT
32040 REM
32050 REM              DOOR
32060 REM
32070 REM           R.J. V/D ZEE
32080 REM
32090 REM           BRAAMKAMP 40
32100 REM
32110 REM          7206 HB ZUTPHEN
32120 REM
32130 REM          TEL. 05750-21736
32140 REM
32150 REM ******** BASICODE 3 ********
32160 REM


1000 A=2000:GOTO20:REM ### ADRESBOEKJE ###
1010 DIMA$(5),S$(5),P$(6),D$(150,5)
1020 A$(1)="Naam+voornaam : "
1030 A$(2)="Straat +  nr. : "
1040 A$(3)="PC + plaats   : "
1050 A$(4)="Telefoon      : "
1060 A$(5)="Opmerkingen   : "
1070 P$(0)="M E N U"
1080 P$(1)="INVOER"
1090 P$(2)="UITVOER"
1100 P$(3)="INLEZEN"
1110 P$(4)="OPSLAAN"
1120 P$(5)="WIJZIGEN"
1130 P$(6)="STOPPEN"
1140 Z=0:GOTO7030
1200 REM -----------
1210 REM  SCHERMKOP
1220 REM -----------
1230 GOSUB100
1240 SR$=" ADRESBOEKJE "
1250 PRINT"        ";:GOSUB150:PRINT:PRINT
1260 RETURN
1300 REM -----------
1310 REM  KEUZEKOP
1320 REM -----------
1330 GOSUB1370
1340 PRINT"          * ";P$(X);" *"
1350 GOSUB1370:PRINT
1360 RETURN
1370 PRINT"          ";LEFT$("***************",4+LEN(P$(X)))
1380 RETURN
1400 REM ------
1410 REM  MENU
1420 REM ------
1430 PRINT
1440 FORSR=1 TO6:GOSUB300
1450 PRINT"    ";:GOSUB150:PRINT"    ";P$(SR):PRINT
1460 NEXTSR
1470 PRINT:PRINT
1480 PRINT:PRINT"         Maak uw keuze !"
1490 GOSUB210:X=IN-48
1510 IF(X<1)OR(X>6)THEN GOSUB250:GOTO1490
1520 IFX=6 THEN950
1530 RETURN
1700 REM ----------------------
1710 REM  GEGEVENS INGELEZEN ?
1720 REM ----------------------
1730 IFZ>0 THEN1770
1740 F=1:GOSUB250:PRINT:PRINT
1750 PRINT" ----- Geen gegevens aanwezig ! -----"
1760 SD=20:GOSUB450
1770 RETURN
1800 REM -------------------
1810 REM  KASSETTE INLEGGEN
1820 REM -------------------
1830 PRINT:PRINT" --- Leg de cassette in en <TOETS> ---"
1840 GOSUB210
1850 RETURN
2000 REM ---------
2010 REM  INVOER
2020 REM ---------
2030 GOSUB1230:GOSUB1330
2040 Z=Z+1
2050 FORI=1 TO5
2060 PRINTA$(I);:INPUTD$(Z,I)
2070 PRINT
2080 NEXTI:PRINT:PRINT:PRINT:PRINT
2090 PRINT"     Meer invoer ?   (J/N)"
2100 GOSUB210
2110 IFIN=78 THENRETURN
2120 IFIN<>74 THEN GOSUB250:GOTO2100
2130 GOTO2030
3000 REM ---------
3010 REM  UITVOER
3020 REM ---------
3030 GOSUB1230:GOSUB1330
3040 PRINT:PRINT:PRINT:PRINT
3050 PRINT"    Geef een zoektekst !"
3060 PRINT:PRINT:PRINT:PRINT
3070 PRINT:PRINT:PRINT
3080 SR$="<TOETS>":PRINT"          ";:GOSUB150:PRINT
3090 GOSUB210
3100 GOSUB1230:GOSUB1330
3110 FORI=1 TO5:S$(I)=""
3120 PRINTA$(I);:INPUTS$(I):PRINT
3130 NEXTI:K=0
3140 FORY=1 TOZ
3150 S=0
3160 FORI=1 TO5
3170 IFS$(I)="" THENS=S+1:GOTO3190
3180 IFLEFT$(D$(Y,I),LEN(S$(I)))=S$(I)THENS=S+1
3190 NEXTI
3200 IFS<>5 THEN3280
3210 GOSUB1230:GOSUB1330
3220 FORI=1 TO5
3230 PRINTA$(I);D$(Y,I):PRINT:K=1
3240 NEXTI
3250 PRINT:PRINT:PRINT:PRINT"      ";
3260 SR$="<TOETS>":GOSUB150:PRINT
3270 GOSUB210
3280 NEXTY
3290 IFK=1 THENRETURN
3300 GOSUB1230:GOSUB1330
3310 PRINT:PRINT:PRINT:PRINT:PRINT:PRINT
3320 GOSUB250:GOSUB250:GOSUB250
3330 PRINT" --- Dit kan ik helaas niet vinden ! ---"
3340 SD=20:GOSUB450
3350 RETURN
4000 REM ----------------------
4010 REM  INLEZEN OUD BESTAND
4020 REM ----------------------
4030 GOSUB100:PRINT"INLEZEN OUD BESTAND"
4040 PRINT:PRINT"Inlezen vanaf:"
4050 PRINT:PRINT"  (C)assette"
4060 PRINT"  (D)iskette"
4070 GOSUB210:IFIN=67 THENNF=0:GOSUB1830:GOTO4100
4080 IFIN<>68 THEN GOSUB250:GOTO4070
4090 PRINT"Ik zoek het bestand 'A-FILE'":NF=4
4100 NF$="A-FILE":GOSUB500
4110 GOSUB540:Z=VAL(IN$)
4120 FORY=1 TOZ
4130 FORI=1 TO5:GOSUB540:D$(Y,I)=IN$:NEXTI
4140 NEXTY
4150 GOSUB580
4160 RETURN
5000 REM ---------------------
5010 REM  OPSLAAN VAN BESTAND
5020 REM ---------------------
5030 GOSUB100:PRINT"BESTAND OPSLAAN"
5040 PRINT:PRINT"Opslaan op:"
5050 PRINT:PRINT"  (C)assette"
5060 PRINT"  (D)iskette"
5070 GOSUB210:IFIN=67 THENNF=1:GOSUB1830:GOTO5100
5080 IFIN<>68 THEN GOSUB250:GOTO5070
5090 PRINT"Ik schrijf het bestand 'A-FILE'":NF=5
5100 NF$="A-FILE":GOSUB500
5110 SR=Z:GOSUB300:GOSUB560
5120 FORY=1 TOZ
5130 FORI=1 TO5:SR$=D$(Y,I):GOSUB560:NEXTI
5140 NEXTY
5150 GOSUB580
5160 RETURN
6000 REM ----------
6010 REM  WIJZIGEN
6020 REM ----------
6030 GOSUB1230:GOSUB1330
6040 PRINT:PRINT
6050 SR$="V":PRINT"     ";:GOSUB150
6060 PRINT"    Vooruit bladeren":PRINT
6070 SR$="A":PRINT"     ";:GOSUB150
6080 PRINT"    Achteruit bladeren":PRINT
6090 SR$="W":PRINT"     ";:GOSUB150
6100 PRINT"    Wijzigen":PRINT
6110 SR$="S":PRINT"     ";:GOSUB150
6120 PRINT"    Schrappen":PRINT
6130 SR$="M":PRINT"     ";:GOSUB150
6140 PRINT"    Menu"
6150 PRINT:PRINT:PRINT:PRINT"          ";
6160 SR$="<TOETS>":GOSUB150:PRINT:ZZ=1:GOSUB210
6170 GOSUB1230:GOSUB1330
6180 FORI=1 TO5
6190 PRINTI;") ";A$(I);D$(ZZ,I):PRINT
6200 NEXTI
6210 PRINT:PRINT"Toets: V A W S M"
6220 GOSUB210
6230 IF(IN=86)AND(ZZ<Z)THENZZ=ZZ+1:GOTO6170:REM V
6240 IF(IN=65)AND(ZZ>1)THENZZ=ZZ-1:GOTO6170:REM A
6250 IFIN=77 THEN7030:REM M
6260 IFIN=87 THEN6420:REM W
6270 IFIN=83 THEN6290:REM S
6280 GOSUB250:GOTO6220
6290 PRINT:PRINT:PRINT:PRINT
6300 PRINT"   Dit rijtje echt schrappen ?"
6310 PRINT
6320 PRINT"           (J/N)"
6330 GOSUB210
6340 IFIN=78 THEN6170
6350 IFIN<>74 THEN GOSUB250:GOTO6330
6360 IFZZ=Z THENZZ=ZZ-1:GOTO6400
6370 FORY=ZZ TOZ-1
6380 FORI=1 TO5:D$(Y,I)=D$(Y+1,I):NEXTI
6390 NEXTY
6400 Z=Z-1
6410 GOTO6170
6420 PRINT:PRINT:PRINT
6430 INPUT"    Welk nr. (1-5)";NR
6440 IF(NR<1)OR(NR>5)THEN
6450 PRINT
6460 INPUT"Nieuwe inhoud ";D$(ZZ,NR)
6470 GOTO6170
7000 REM --------
7010 REM  START
7020 REM --------
7030 X=0:F=0:GOSUB1230:REM SCHERMTITEL
7040 GOSUB1330:REM PROGRAMMAKOP
7050 GOSUB1430:REM MENU
7060 IF(X=2)OR(X=4)OR(X=5)THEN GOSUB1730
7070 IFF=1 THEN7030
7080 ONX GOSUB2030,3030,4030,5030,6030
7090 GOTO7030
30000 REM -----------------------------
30010 REM ----       LET OP        ----
30020 REM -   BEGIN STEEDS MET HET    -
30030 REM -   INLEZEN VAN HET OUDE    -
30040 REM - BESTAND EN VERGEET NIET   -
30050 REM - NA AFLOOP HET GEWIJZIGDE  -
30060 REM - BESTAND WEER OP TE SLAAN  -
30070 REM -----------------------------
30080 REM -----------------------------
30090 REM - Boek '64 FUER EINSTEIGER' -
30100 REM - als voorbeeld             -
32000 REM -   Bewerkt voor BASICODE   -
32010 REM -     door BEN ABRAHAM      -
32020 REM -   DDR - NEUBRANDENBURG    -
32040 REM -   op een COMMODORE 64     -
32050 REM -   '''Veel plezier !'''    -
32060 REM -----------------------------
32070 REM
32080 REM Erstausstrahlung: Radio DDR
32090 REM REM-spezial 900119
32100 REM TROS-RADIO dd 900606

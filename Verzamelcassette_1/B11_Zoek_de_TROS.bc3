
1000 A=200:GOTO20:REM ZOEK DE COMBINATIE
1010 T=0:DIMQ(20):GOSUB100:REM *** SCHERM SCHOON
1020 HO=0:VE=0:GOSUB110
1030 PRINT"Vind  in  zo  min  mogelijk  zetten"
1040 PRINT"(max.20) de combinatie voor de naam"
1050 HO=7:VE=4:GOSUB110
1060 SR$="T.R.O.S.  RADIO 5":GOSUB150
1070 HO=0:VE=6:GOSUB110
1080 PRINT"Je  mag  bij iedere  poging  na  het"
1090 PRINT"vraagteken een  1,2 of 3 invoeren..."
1100 PRINT
1110 PRINT"Een ingetikt cijfer leidt tot bepaal-"
1120 PRINT"de verwisselingen. Zoek het systeem!"
1130 R$="T."
1140 T$="R."
1150 S$="O."
1160 D$="S."
1170 HO=7:VE=16:GOSUB110
1180 SR$=T$+R$+D$+S$+"  RADIO 5":GOSUB150
1190 IF(T$+R$+D$+S$="T.R.O.S.") THEN1360
1200 T=T+1:IFT>20 THEN1530
1210 HO=17:VE=20:GOSUB110
1220 INPUTX:Q(T)=X
1230 HO=0:VE=23:GOSUB110
1240 PRINT"Aantal zetten: ";T;
1250 IFX=1 THEN1320
1260 IFX=2 THEN1330
1270 IFX=3 THEN1340
1280 HO=0:VE=22:GOSUB110
1290 PRINT"Alleen 1,2 of 3 invoeren,"
1300 PRINT"we starten opnieuw !!";
1310 GOTO1420
1320 E$=R$:R$=T$:T$=E$
1330 E$=T$:T$=S$:S$=E$
1340 E$=S$:S$=D$:D$=E$
1350 GOTO1170
1360 HO=17:VE=20:GOSUB110
1370 PRINT"   "
1380 HO=0:VE=22:GOSUB110
1390 PRINT"Goed zo, gevonden in ";T;" zetten,"
1400 IFT<4 THEN1470
1410 PRINT"maar.... het kan nog beter!";
1420 SD=30:GOSUB450
1430 FORVE=20 TO23:HO=0:GOSUB110
1440 PRINT"                                       ";
1450 NEXTVE:T=0
1460 GOTO1130
1470 VE=23:GOSUB110
1480 PRINT"dit was de goede combinatie !";
1490 SV=10:FORX=1 TO6:READSP,SD:GOSUB400:NEXTX
1500 GOSUB110:FORW=1 TOT:HO=37:VE=W+19:GOSUB110
1510 PRINTQ(W):NEXTW
1520 SD=50:GOSUB450:GOTO950
1530 HO=0:VE=20:GOSUB110
1540 PRINT"DIT DUURT TE LANG! WE STARTEN OPNIEUW."
1550 FORX=1 TO2000:NEXTX
1560 GOTO1000
25000 DATA55,1,55,3,55,1
25010 DATA60,8,62,8,60,6
32000 REM  *********************
32010 REM  * COMBINATIEPUZZEL  *
32020 REM  * NAAR EEN IDEE VAN *
32030 REM  * BASF - NEDERLAND  *
32040 REM  * REALISATIE:       *
32050 REM  * R.GRITTER         *
32060 REM  * 28-12-86          *
32070 REM  *********************

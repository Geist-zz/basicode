
1000 A=200:GOTO20:REM morse oefenen
1010 REM initialisaties
1020 REM lengte random codes
1030 G=5
1040 REM toonhoogte
1050 SP=80
1060 REM array C$ : index is teken code
1070 REM            waarde is morse code
1080 REM opslag morse code per teken
1090 DIMC$(122)
1100 REM array L : index is teken nummer
1110 REM           waarde is teken waarde
1120 REM hulpopslag van teken reeksen
1130 DIML(100)
1140 GOSUB1630
1150 REM vullen morse code in teken array
1160 RESTORE
1170 FORN=0 TO90
1180 READC$(N)
1190 NEXTN
1200 IFM>1 THEN1360
1210 GOSUB100
1220 PRINT"Bijzondere tekens:"
1230 PRINT"< = begin bericht  ? = vraagteken"
1240 PRINT"@ = wacht          / = deelstreep"
1250 PRINT"% = vergist        . = punt"
1260 PRINT"* = begrepen       ( = haakjes"
1270 PRINT"> = einde bericht  _ = onderstreep"
1280 PRINT"# = sluiten"
1290 PRINT
1300 PRINT"TEKST: "
1310 INPUTA$
1320 IFA$="" THEN1290
1330 A$=A$+"   "
1340 GOSUB2180
1350 GOTO1340
1360 IFM>2 THEN1460
1370 A$=""
1380 FORK=1 TOG
1390 GOSUB260
1400 S=64+RV*26
1410 A$=A$+CHR$(S)
1420 NEXTK
1430 A$=A$+"   "
1440 GOSUB2180
1450 GOTO1370
1460 IFM>3 THEN1560
1470 A$=""
1480 FORK=1 TOG
1490 GOSUB260
1500 S=47+10*RV
1510 A$=A$+CHR$(S)
1520 NEXTK
1530 A$=A$+"   "
1540 GOSUB2180
1550 GOTO1470
1560 GOSUB260
1570 N=RV*W
1580 P=L(N)
1590 A$=CHR$(P)
1600 GOSUB2180
1610 GOTO1560
1620 REM starttekst
1630 PRINT"Dit programma produceert oefeningen"
1640 PRINT"voor het opnemen van MORSE-CODE."
1650 HO=0:VE=3:GOSUB110
1660 PRINT"Welk tempo wilt u:"
1670 PRINT
1680 PRINT"1 = snel     ( 60 tekens per minuut)"
1690 PRINT"2 = matig    ( 30 tekens per minuut)"
1700 PRINT"3 = langzaam ( 20 tekens per minuut)"
1710 GOSUB210
1720 D=IN-48
1730 IF(D<1)OR(D>3) THEN1650
1740 PRINTD
1750 HO=0:VE=9:GOSUB110
1760 PRINT"Welk volume wilt u:"
1770 PRINT"1......7"
1780 GOSUB210
1790 V=2*(IN-48)
1800 IF(V<2)OR(V>14) THEN1750
1810 PRINTV/2
1820 HO=0:VE=12:GOSUB110
1830 PRINT"Wilt u:"
1840 PRINT"1 = herhaald  een zelfde tekst horen"
1850 PRINT"2 = series letters"
1860 PRINT"3 = series cijfers"
1870 PRINT"4 = enkele tekens in steeds wisselende"
1880 PRINT"    volgorde"
1890 PRINT"1....4 ? "
1900 GOSUB210
1910 Q=IN
1920 IF(Q<49)OR(Q>52) THEN1820
1930 M=Q-48
1940 PRINTM
1950 IFQ<52 THEN2060
1960 GOSUB100
1970 PRINT"Welke tekens? "
1980 N=0
1990 GOSUB210
2000 L(N)=IN
2010 HO=N:VE=2:GOSUB110
2020 PRINTIN$
2030 N=N+1
2040 IFIN<>13 THEN1990
2050 W=N-2:GOTO2150
2060 IFQ<50 THEN2150
2070 PRINT
2080 PRINT"Hoeveel tekens per serie? (1...5) "
2090 GOSUB210
2100 IF(IN<49)OR(IN>53) THEN2130
2110 G=IN-48
2120 GOTO2140
2130 G=5
2140 PRINTG
2150 RETURN
2160 REM uit te zenden string in A$
2170 REM toon
2180 FORN=1 TOLEN(A$)
2190 B$=MID$(A$,N,1)
2200 PRINTB$;
2210 Q=ASC(B$)
2220 FORM=1 TOLEN(C$(Q-32))
2230 D$=MID$(C$(Q-32),M,1)
2240 SV=V
2250 IFD$<>"P" THEN2280
2260 SD=D
2270 GOSUB400
2280 IFD$<>"S" THEN2310
2290 SD=3*D
2300 GOSUB400
2310 SV=0:SD=D
2320 GOSUB200:IFIN>0 THENM=256:N=256
2330 NEXTM
2340 IFIN>0 THEN2370
2350 SD=2*D
2360 GOSUB400
2370 NEXTN
2380 IFIN>0 THEN2420
2390 SD=3*D
2400 GOSUB400
2410 RETURN
2420 GOSUB100
2430 PRINT"Wilt u nog meer oefenen (J/N) "
2440 GOSUB210
2450 IF(IN$<>"N")AND(IN$<>"n") THENRETURN
2460 GOTO950
25000 REM codes van de diverse karakters
25001 REM S is lang (streep)
25002 REM P is kort (punt)
25003 REM xx is een tekenwaarde waarvoor
25010 REM    geen morse code bestaat
25020 DATA"32","33","PSPPSP","PPPSPS","36"
25030 DATA"PPPPPPPP","38","PSSSSP","SPSSPS"
25040 DATA"SPSSPS","PPPSP","43","SSPPSS"
25050 DATA"SPPPPS","PSPSPS","SPPSP"
25060 DATA"SSSSS","PSSSS","PPSSS","PPPSS"
25070 DATA"PPPPS","PPPPP","SPPPP","SSPPP"
25080 DATA"SSSPP","SSSSP","58","59"
25090 DATA"SPSPS","61","PSPSP","PPSSPP","PSPPP"
25100 DATA"PS","SPPP","SPSP","SPP","P","PPSP"
25110 DATA"SSP","PPPP","PP","PSSS","SPS","PSPP"
25120 DATA"SS","SP","SSS","PSSP","SSPS","PSP"
25130 DATA"PPP","S","PPS","PPPS","PSS","SPPS"
25140 DATA"SPSS","SSPP","91","92"
25150 DATA"93","94","PPSSPS","96"
25160 DATA"PS","SPPP","SPSP","SPPP","P","PPSP"
25170 DATA"SSP","PPPP","PP","PSSS","SPS","PSPP"
25180 DATA"SS","SP","SSS","PSSP","SSPS","PSP"
25190 DATA"PPP","S","PPS","PPPS","PSS","SPPS"
25200 DATA"SPSS","SSPP"
30000 REM Naar een programma
30010 REM uit Asterisk, clubblab
30020 REM van de BBC club.
30030 REM Aangepast voor BASICODE-3
30040 REM door :
32000 REM H. Mulder
32010 REM J.W. Frisostr. 129
32020 REM 2713 CC Zoetermeer
32030 REM dd. 19851228
32040 REM TROS 860726

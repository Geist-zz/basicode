
1000 A=100:GOTO20:REM CIRKELDIAGRAM
1010 GOSUB9000:REM UITLEG
1020 GOSUB2000:GOSUB4000:REM INITIALISEREN
1030 GOSUB8000:REM OVERZICHTSNAAM ERBIJ
1040 GOSUB5000:REM TEKEN DE CIRKEL
1050 GOSUB7000:REM TEKEN DE SEGMENTEN EN ZET DE NAMEN ERBIJ
1060 HO=0.4:VE=0.96:SR$="nog een diagram (j/n)":GOSUB650
1070 GOSUB200:IFIN$="" THEN1070
1080 IFIN=74 THEN1000
1090 IFIN<>78 THEN1070
1100 GOSUB100:GOTO950:REM EINDE
2000 REM INITIALISEREN
2010 U=INT(HG/2):V=INT(VG/2):P=3.141592:SO=0
2020 R=INT(0.39*VG):CN=0:X1=U+R*SIN(0):Y1=V-R*COS(0)
2030 SP$="                                       "
2040 PRINT"overzichtsnaam ";:INPUTZI$
2050 PRINT"aantal segmenten ";:INPUTN
2060 DIMAA(N),NA$(N)
2070 FORI=1 TON
2080 PRINT"geef naam ";I
2090 INPUTNA$(I)
2100 IFLEN(NA$(I))>10 THENNA$(I)=LEFT$(NA$(I),10)
2110 PRINT"geef getal ";I:INPUTAA(I)
2120 IFAA(I)<=0 THEN2110
2130 SO=SO+INT(AA(I)*100)/100
2140 NEXTI
2150 FORI=1 TON
2160 AA(I)=(AA(I)/SO)*(P*2)
2170 NEXTI
2180 RETURN
3000 REM TREK EEN LIJN TUSSEN (X1,Y1) EN (X2,Y2)
3010 HO=X1/HG:VE=Y1/VG:GOSUB620
3020 HO=X2/HG:VE=Y2/VG:GOSUB630
3030 RETURN
4000 REM GRAFISCH SCHERM AAN
4010 GOSUB600
4020 RETURN
5000 REM TEKEN DE CIRKEL
5010 PQ=P/180
5020 HO=X1/HG:VE=Y1/VG:GOSUB620
5030 FORG=4 TO360 STEP4
5040 RD=PQ*G
5050 HO=(U+R*SIN(RD))/HG:VE=(V-R*COS(RD))/VG:GOSUB630
5060 NEXTG
5070 RETURN
6000 REM ZET DE NAAM BIJ HET SEGMENT
6010 AN=AN-0.5*AA(I)
6020 X=U+((11/10)*R)*SIN(AN)
6030 Y=V-((11/10)*R)*COS(AN)
6040 IFAN>P THENX=X-LEN(NA$(I))*8
6050 SR$=NA$(I)
6060 HO=X/HG:VE=Y/VG:GOSUB650
6070 PR=100*AA(I)/(P*2)
6080 PR=INT(PR+0.5)
6090 X=U+.7*R*SIN(AN)
6100 Y=V-.7*R*COS(AN)-4
6110 IFAN<P THENX=X-8
6120 IFAN>=P THENX=X-12
6130 SR=PR:GOSUB300:SR$=SR$+"%"
6140 HO=X/HG:VE=Y/VG:GOSUB650
6150 AN=AN+0.5*AA(I)
6160 RETURN
7000 REM TEKEN HET SEGMENT
7010 AN=0
7020 FORI=1 TON
7030 AN=AN+AA(I)
7040 X1=U:Y1=V
7050 X2=U+R*SIN(AN)
7060 Y2=V-R*COS(AN)
7070 IFN<>1 THEN GOSUB3000:REM LYN TUSSEN (X1,Y1) EN (X2,Y2)
7080 GOSUB6000:REM ZET DE NAAM BIJ HET SEGMENT
7090 NEXTI
7100 RETURN
8000 REM
8010 X=U-LEN(ZI$)*4
8020 Y=0
8030 HO=X/HG:VE=Y/VG
8040 SR$=ZI$:GOSUB330:GOSUB650
8050 RETURN
9000 GOSUB100:H=HO:V=VE:SR$="CIRKELDIAGRAM"
9010 GOSUB150:HO=0:VE=2
9020 READUI$:IFUI$="*" THEN9090
9030 IFHO+LEN(UI$)>=H THENHO=0:VE=VE+2
9040 IFVE<V-3 THEN9070
9050 HO=0:VE=V-1:GOSUB110:PRINT"druk een toets":GOSUB210
9060 GOSUB100:HO=0:VE=0
9070 GOSUB110:PRINTUI$:HO=HO+LEN(UI$)+2
9080 GOTO9020
9090 HO=0:VE=V-1:GOSUB110:PRINT"druk een toets":GOSUB210
9100 GOSUB100:RETURN
25000 DATA"Cirkeldiagram","maakt","van"
25010 DATA"ingetoetste","gegevens","een"
25020 DATA"cirkeldiagram.","Ook","wel","'taart'","genoemd."
25030 DATA"Een","'punt'","zal","een","net","zo"
25040 DATA"groot","percentage","van","het","oppervlak"
25050 DATA"van","de","cirkel","innemen,","als","het"
25060 DATA"bijbehorende","gegeven","van","het","totaal"
25070 DATA"is.","De","namen"
25080 DATA"worden","bij","de","'punten'","gezet.","*"
32000 REM ***********************
32010 REM *                     *
32020 REM * DOOR EDWIN DE GRAAF *
32030 REM * J. SLUYTERSWEG 32   *
32040 REM * 5645 JD  EINDHOVEN  *
32050 REM * OP EEN C-64         *
32060 REM *                     *
32070 REM ***********************
32100 REM TROS-RADIO dd 861217
32101 REM correctie dd 861229


1000 A=100:GOTO20:REM Tariefgroep
1010 GOSUB100:A=0:GOSUB2420:GOSUB100
1020 SR$="Bepalen van de tariefgroep"
1030 PRINT"    ";:GOSUB150
1040 PRINT:PRINT:GOSUB1110
1050 VE=20:HO=3:GOSUB110
1060 SR$="Druk op een toets voor einde"
1070 GOSUB150
1080 GOSUB200
1090 IFIN$="" THEN1080
1100 GOTO950
1110 PRINT
1120 PRINT"Woont u in Nederland";
1130 GOSUB5000:IFJN=1 THEN1240
1140 PRINT
1150 PRINT"Woont u in Belgie, Suriname of op de"
1160 PRINT"Nederlandse Antillen";
1170 GOSUB5000:IFJN=1 THEN1240
1180 PRINT
1190 PRINT"Wordt uw onzuiver wereldinkomen voor"
1200 PRINT"tenminste 90 procent in Nederland be-"
1210 PRINT"last";
1220 GOSUB5000:IFJN=1 THEN1240
1230 T=1:GOTO2230
1240 PRINT
1250 PRINT"Was u in 1986 meer dan zes maanden ge-"
1260 PRINT"huwd";
1270 GOSUB5000:IFJN=1 THEN1700
1280 PRINT
1290 PRINT"Voerde u in 1986 met (een) andere onge-"
1300 PRINT"huwde(n) meer dan zes maanden een geza-";
1310 PRINT:PRINT"menlijke huishouding";
1320 GOSUB5000:IFJN=1 THEN1440
1330 PRINT
1340 PRINT"Voerde u in 1986 meer dan zes maanden"
1350 PRINT"een gezamenlijke huishouding met"
1360 PRINT"gehuwden";
1370 GOSUB5000:IFJN=2 THEN1550
1380 PRINT
1390 PRINT"Genoot u in 1986 inkomsten uit tegen-"
1400 PRINT"woordige arbeid";
1410 GOSUB5000:IFJN=2 THEN1430
1420 T=2:GOTO2230
1430 T=1:GOTO2230
1440 PRINT
1450 PRINT"Was (waren) die andere(n) uitsluitend"
1460 PRINT"eigen aangehuwde pleegkinderen die op"
1470 PRINT"31 dec. jonger waren dan 27 jaar";
1480 GOSUB5000:IFJN=2 THEN1990
1490 PRINT
1500 PRINT"Had u in ten minste zes van die maanden"
1510 PRINT"recht op kinderbijslag of vervangende"
1520 PRINT"buitengewone lasten aftrek";
1530 GOSUB5000:IFJN=1 THEN1580
1540 IFJN=2 THEN2130
1550 PRINT
1560 PRINT"U voerde dus in 1986 een huishouding"
1570 PRINT"voor u alleen.":GOTO1490
1580 PRINT
1590 PRINT"Genoot u in 1986 inkomsten uit tegen-"
1600 PRINT"woordige arbeid";
1610 GOSUB5000:IFJN=2 THEN1690
1620 PRINT
1630 PRINT"Voldoet u aan de voorwaarden die voor"
1640 PRINT"de aanvullende alleenstaande-ouder-"
1650 PRINT"toeslag gelden";
1660 GOSUB5000:IFJN=2 THEN1680
1670 T=11:GOTO2230
1680 T=10:GOTO2230
1690 T=9:GOTO2230
1700 PRINT
1710 PRINT"Genoot uw (huwelijks) partner in 1986"
1720 PRINT"inkomen";
1730 GOSUB5000:IFJN=2 THEN1860
1740 PRINT
1750 PRINT"Genoot u in 1986 inkomen uit tegenwoor-"
1760 PRINT"dige arbeid";
1770 GOSUB5000:IFJN=2 THEN1850
1780 PRINT
1790 PRINT"Behoorde een kind dat op 31 dec. 1985"
1800 PRINT"jonger was dan 12 jaar meer dan zes"
1810 PRINT"maanden tot uw huishouden";
1820 GOSUB5000:IFJN=1 THEN1840
1830 T=2:GOTO2230
1840 T=3:GOTO2230
1850 T=1:GOTO2230
1860 PRINT
1870 PRINT"Genoot u in 1986 inkomsten uit tegen-"
1880 PRINT"woordige arbeid";
1890 GOSUB5000:IFJN=2 THEN1980
1900 PRINT
1910 PRINT"Behoorde een kind dat op 31 dec. 1985"
1920 PRINT"jonger was dan 12 jaar meer dan 6 maan-"
1930 PRINT"den tot uw huishouden en komt u in aan-"
1940 PRINT"merking voor de meewerkaftrek";
1950 GOSUB5000:IFJN=1 THEN1970
1960 T=7:GOTO2230
1970 T=8:GOTO2230
1980 T=6:GOTO2230
1990 PRINT
2000 PRINT"Was die ander of een van die anderen op"
2010 PRINT"31 december 1985 27 jaar of ouder dan"
2020 PRINT"wel jonger dan 27 maar 18 jaar of ouder"
2030 PRINT"en werd (in het laatste geval) die an-"
2040 PRINT"der niet grotendeels op kosten van zijn"
2050 PRINT"ouders onderhouden";
2060 GOSUB5000:IFJN=1 THEN1700
2070 PRINT
2080 PRINT"Genoot u in 1986 inkomsten uit tegen-"
2090 PRINT"woordige arbeid";
2100 GOSUB5000:IFJN=1 THEN2120
2110 T=1:GOTO2230
2120 T=2:GOTO2230
2130 PRINT
2140 PRINT"Was u op 31 december 1985 27 jaar of";
2150 PRINT"ouder";
2160 GOSUB5000:IFJN=2 THEN2070
2170 PRINT
2180 PRINT"Genoot u in 1986 inkomsten uit tegen-"
2190 PRINT"woordige arbeid";
2200 GOSUB5000:IFJN=1 THEN2220
2210 T=4:GOTO2230
2220 T=5
2230 GOSUB100
2240 PRINT"Op grond van de door u verstrekte ge-"
2250 PRINT"gevens kan ik u meedelen, dat voor u"
2260 PRINT"thans  geldt :"
2270 RESTORE
2280 FORI=0 TOT-1:READA:READA$:NEXTI
2290 PRINT:PRINT
2300 PRINT"1) Belastingvrije som = f ";A
2310 PRINT:PRINT"2) Tariefgroep ";A$
2320 IFT=11 THENPRINT"  (zonder 1/4 extra-toeslag)"
2330 PRINT:PRINT"Zijn de gegevens goed";:GOSUB5000
2340 IFJN=1 THENRETURN
2350 PRINT
2360 PRINT"Uitleg nog eens bekijken";
2370 GOSUB5000:IFJN=2 THEN2390
2380 GOSUB100:A=1:GOSUB2420
2390 GOSUB100:SR$="Ok, opnieuw....."
2400 PRINT"        ";:GOSUB150
2410 PRINT:PRINT:GOTO1110
2420 VE=0:HO=7:GOSUB110
2430 SR$="Tariefgroep-indeling"
2440 GOSUB150
2450 PRINT:PRINT
2460 IFA<>0 THEN2500
2470 PRINT"Met dit programma kunt u bepalen in"
2480 PRINT"welke tariefgroep u zit voor het in-"
2490 PRINT"vullen van uw aangifte-formulier '86."
2500 VE=6:HO=0:GOSUB110
2510 PRINT"De gebruikte afkortingen zijn :"
2520 PRINT
2530 SR$="a.t.         ":GOSUB150
2540 GOSUB120:HO=20:GOSUB110
2550 PRINT"arbeidstoeslag"
2560 PRINT
2570 SR$="a.t. + a.a.t ":GOSUB150
2580 GOSUB120:HO=20:GOSUB110
2590 PRINT"arbeidstoeslag en"
2600 GOSUB120:HO=20:GOSUB110
2610 PRINT"aanvullende arbeids-";
2620 GOSUB120:HO=20:GOSUB110
2630 PRINT"toeslag"
2640 PRINT
2650 SR$="a.t + a.a.o.t":GOSUB150
2660 GOSUB120:HO=20:GOSUB110
2670 PRINT"arbeidstoeslag en"
2680 GOSUB120:HO=20:GOSUB110
2690 PRINT"aanvullende ouder-"
2700 GOSUB120:HO=20:GOSUB110
2710 PRINT"toeslag"
2720 VE=20:HO=1:GOSUB110
2730 SR$="Druk op een toets om te beginnen"
2740 GOSUB150
2750 GOSUB200
2760 IFIN$="" THEN2750
2770 RETURN
5000 GOSUB120:PRINT" ? (j/n)";
5010 HO=HO+3:GOSUB110
5020 GOSUB200
5030 IFIN=78 THEN5080
5040 IFIN=74 THEN5100
5050 IFIN=0 THEN5020
5060 SV=7:SD=2:SP=80:GOSUB400
5070 GOTO5020
5080 PRINT" nee ":JN=2
5090 GOTO5110
5100 PRINT" ja  ":JN=1
5110 FORI=1 TO250:NEXTI:RETURN
25000 DATA7341
25010 DATA"een"
25020 DATA7748
25030 DATA"een met a.t."
25040 DATA8535
25050 DATA"een met a.t. + a.a.t."
25060 DATA11012
25070 DATA"twee"
25080 DATA11419
25090 DATA"twee met a.t."
25100 DATA14682
25110 DATA"drie"
25120 DATA15089
25130 DATA"drie met a.t."
25140 DATA16663
25150 DATA"drie met a.t. + a.a.t"
25160 DATA13214
25170 DATA"vier"
25180 DATA13621
25190 DATA"vier met a.t."
25200 DATA13621
25210 DATA"vier met a.t. + a.a.o.t."
30000 REM"****************************"
30010 REM"*                          *"
30020 REM"* T A R I E F - G R O E P  *"
30030 REM"*                          *"
30040 REM"* Geschreven op P2000T-32K *"
30050 REM"* Voor P2000-programma :   *"
30060 REM"* Belasting 1986 (29K)     *"
30070 REM"*                          *"
30080 REM"* Omgezet naar BASICODE-3  *"
30090 REM"* d.d.: 29 december 1986   *"
30100 REM"*                          *"
32000 REM"* Auteur :                 *"
32010 REM"*   Dick Broekhuis         *"
32020 REM"*   Jacob Marisstraat 23   *"
32030 REM"*   1382 TN Weesp          *"
32040 REM"*                          *"
32050 REM"****************************"
32200 REM TROS-radio dd.870128

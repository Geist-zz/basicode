
1000 A=100:GOTO20:REM RIJMELARIJ
1010 AG=18:AV=4:RG=HO+1:SG=0:LI=1:NU=1:VE=0:SP$=" "
1020 SP=0:SV=0:SD=1
1030 DIMGD$(AG,AV),VO$(100)
1040 FORI=1 TOHO:SP$=SP$+" ":NEXTI
1050 RESTORE
1060 GOSUB100:SR$="*** RIJMELARIJ VOOR BIJ DE CV! ***"
1070 HO=RG/2-20:VE=0:GOSUB110:GOSUB150
1080 VE=4:HO=0:GOSUB110
1090 PRINT" Een ideaal hulpprogramma voor de echte"
1100 PRINT"             stuntelaars..."
1110 PRINT"     Nooit meer slapeloze nachten,"
1120 PRINT"        kabbelend afgangs-zweet"
1130 PRINT"        of sinterklaas-koorts!"
1140 PRINT
1150 PRINT" Als het ook maar even niet lukken wil,"
1160 PRINT"  start U gewoon dit stuk software op!"
1170 PRINT
1180 PRINT"      (Misbruik op eigen risico!)"
1190 PRINT:GOSUB1220
1200 PRINT"           <druk een toets>"
1210 GOSUB210:GOTO2000
1220 FORA=1 TOAG
1230 FORB=1 TOAV
1240 READGD$(A,B)
1250 NEXTB
1260 NEXTA
1270 RETURN
2000 GOSUB100:PRINT"Wat gaat U weggeven: ";
2010 INPUTGS$
2020 IFGS$="" THENGS$="zak pepernoten"
2030 PRINT:PRINT"Wie is de (on)gelukkige: ";
2040 INPUTNM$
2050 IFNM$="" THENNM$="sjefke"
2060 PRINT:PRINT"Hoeveel zinnen: ";
2070 INPUTAZ
2080 IFAZ=0 THENAZ=1
2090 AZ=AZ*2
2100 PRINT:PRINT"Serieuze gedichten? (j/n) ";
2110 GOSUB4160
2120 IFIN=78 THENSG=1
3000 GOSUB100
3010 IFSG=0 THENVO$(LI)=GD$(NU,1):GOTO3030
3020 GOSUB260:VO$(LI)=GD$(INT(RV*AG)+1,1)
3030 A$=VO$(LI)
3040 GOSUB4000
3050 PRINT:PRINT"Zins-opening ok? (j/n)";
3060 GOSUB4160
3070 IFIN=74 THEN3110
3080 GOSUB4190
3090 NU=NU+1:IFNU>AG THENNU=1
3100 GOTO3010
3110 LI=LI+1:TU=2
3120 IFSG=0 THENVO$(LI)=GD$(NU,TU):GOTO3140
3130 GOSUB260:VO$(LI)=GD$(INT(RV*AG)+1,TU)
3140 A$=VO$(LI)
3150 GOSUB4000
3160 PRINT:PRINT"Tot zover ok? (j/n)";
3170 GOSUB4160
3180 IFIN=74 THEN3220
3190 GOSUB4190
3200 TU=TU+1:IFTU>AV THENTU=2
3210 GOTO3120
3220 IFLI<AZ THENLI=LI+1:GOTO3090
3230 PRINT:PRINT"Het gedicht is klaar!"
3240 PRINT"Overschrijven? (j/n)";
3250 GOSUB4160
3260 IFIN=74 THEN3340
3270 PRINT:PRINT"Uitprinten (j/n)";
3280 GOSUB4160:GOSUB100
3290 IFIN=78 THEN3410
3300 FORA=1 TOAZ
3310 A$=VO$(A):GOSUB4080:SR$=A$:GOSUB350:GOSUB360
3320 NEXTA
3330 GOTO3410
3340 GOSUB100
3350 FORA=1 TOAZ
3360 PRINT
3370 A$=VO$(A):GOSUB4000
3380 PRINT"<RETURN>"
3390 GOSUB210:IFIN<>13 THEN3390
3400 NEXTA
3410 GOSUB100
3420 PRINT"Nog een gedicht? (j/n)";
3430 GOSUB4160
3440 IFIN=74 THEN1050
3450 GOSUB100
3460 GOSUB200:GOSUB400
3470 PRINT"Dat wordt feesten..";:IFIN=0 THEN3460
3480 GOTO950
4000 GOSUB4080
4010 C=0:IFLEN(A$)<RG-1 THENPRINTA$:RETURN
4020 FORX=RG-2 TO2 STEP-1
4030 IF(MID$(A$,X,1)<>" ") THENNEXTX
4040 PRINTLEFT$(A$,X-1):GOSUB120
4050 A$=RIGHT$(A$,LEN(A$)-X)
4060 HO=RG-1-LEN(A$):GOSUB110:PRINTA$
4070 C=1:RETURN
4080 FORX=1 TOLEN(A$)-1
4090 IFMID$(A$,X,1)<>"*" THEN4110
4100 A$=LEFT$(A$,X-1)+NM$+RIGHT$(A$,LEN(A$)-X)
4110 NEXTX
4120 FORX=1 TOLEN(A$)-1
4130 IFMID$(A$,X,1)<>"#" THEN4150
4140 A$=LEFT$(A$,X-1)+GS$+RIGHT$(A$,LEN(A$)-X)
4150 NEXTX:RETURN
4160 GOSUB210:IF(IN<>74)AND(IN<>78) THEN4160
4170 GOSUB120:HO=0:GOSUB110:PRINTSP$;
4180 VE=VE-1:HO=0:GOSUB110:RETURN
4190 VE=VE-1:GOSUB110:PRINTSP$;
4200 IFC=1 THENVE=VE-1:HO=0:GOSUB110:PRINTSP$;
4210 HO=0:GOSUB110:RETURN
25000 DATA"Sint zat te denken,"
25010 DATA"wat hij * zou schenken."
25020 DATA"dat hij bij moest tanken... Hips! buurp."
25030 DATA"dat hij * eens ging krenken!"
25040 DATA"Altijd zo'n gedoe om deze tijd van het jaar,"
25050 DATA"maar Sint is weer van zessen klaar!"
25060 DATA"en dat is maar al te waar."
25070 DATA"en Sint heeft toch al last van staar..."
25080 DATA"Zie ginds vergaat de stoomboot,"
25090 DATA"met een # uit de zilvervloot!"
25100 DATA"met een # voor * als hoofdmoot."
25110 DATA"en Sint zit met * op schoot!"
25120 DATA"Pieterman gluurde door de schoorsteen,"
25130 DATA"en wist *'s wensen meteen!"
25140 DATA"en dacht: 'Die * is me er een...'"
25150 DATA"en dacht: 'Heeft * nu een # of geen?'"
25160 DATA"De Sint is weer in 't land,"
25170 DATA"en heeft het kado van * verpand!"
25180 DATA"maar staat niet voor z'n krediet garant..."
25190 DATA"en zit thans in de bijstand!"
25200 DATA"Piet kruipt door ramen, kieren en spleten,"
25210 DATA"om te zien wat ze bij * eten!"
25220 DATA"en komt zo nog niets te weten."
25230 DATA"en zag wat * nu weer zat uit te vreten..."
25240 DATA"Weet je wat * in z'n trekschuit vindt?"
25250 DATA"Een # zonder groeten van de Sint!"
25260 DATA"Een kneusje van de goede Sint..."
25270 DATA"Iets dat hem vast niet zint."
25280 DATA"Heh! Wordt daar op de deur geklopt?"
25290 DATA"Is het de Sint die even stopt?"
25300 DATA"Worden er kado's voor * gedropt?"
25310 DATA"En wordt * met een # gefopt?"
25320 DATA"Dit is een kado dat bij * past,"
25330 DATA"Sint hoopt dat hij je verrast"
25340 DATA"maar kom intussen uit die kast!"
25350 DATA"deze # is je vanaf nu tot last..."
25360 DATA"Als dit kado van blubber is ontdaan,"
25370 DATA"zal niemand er nog versteld van staan!"
25380 DATA"zul je verder door het leven gaan..."
25390 DATA"is het met * gedaan."
25400 DATA"Dit kado uit de zak van Piet!"
25410 DATA"Is het voor *? Nee, nee, toch niet..."
25420 DATA"Pak maar uit en ziet!"
25430 DATA"Zorgt dat * eens goed geniet."
25440 DATA"Pakjesavond is gekomen,"
25450 DATA"en Sint heeft zijn mijter laten stomen."
25460 DATA"en Sint heeft iets voor * meegenomen!"
25470 DATA"en * heeft de benen genomen."
25480 DATA"Houd bij dit geschenk in gedachten,"
25490 DATA"dat de trein niet zal wachten..."
25500 DATA"dat het tegenslag moet verzachten."
25510 DATA"dat het helpt in koude nachten!"
25520 DATA"Alle kinderen klein en groot,"
25530 DATA"gaan nu met de billen bloot!"
25540 DATA"zijn van de roe als de dood."
25550 DATA"eten meestal geen bruin brood..."
25560 DATA"Sint heeft stevig moeten sparen,"
25570 DATA"en * zit maar in het rond te staren!"
25580 DATA"en * zit straks op blaren..."
25590 DATA"om dit karwei te kunnen klaren."
25600 DATA"Dit gedicht is bijna uit,"
25610 DATA"nog even en Sint schiet kuit!"
25620 DATA"'t was geen geld, maar vooruit..."
25630 DATA"* is een ijdeltuit."
25640 DATA"Veel plezier ermee!"
25650 DATA"Misschien is * zelfs tevree?"
25660 DATA"Zit * trouwens op de pl..bank?"
25670 DATA"Sint gaat nu weer overzee..."
25680 DATA"Dit wil Sint nog wel even kwijt,"
25690 DATA"* werk dit jaar met veel vlijt!"
25700 DATA"want hij hoopt dat een # je niet spijt..."
25710 DATA"hij wenst geen achteraf verwijt!"
32000 REM ****************************
32010 REM *  OORSPRONKELIJKE VERSIE  *
32020 REM *   DOOR JOHN VANDERAART   *
32030 REM ****************************
32040 REM *   OMGEZET IN BASICODE-3  *
32050 REM *   DOOR RONALD VAN VUGT,  *
32060 REM *    PA3EAH, POSTBUS 160,  *
32070 REM *    5580 AD, WAALRE (NB)  *
32080 REM *                          *
32090 REM *        13 NOV. 1986      *
32100 REM ****************************
32110 REM TROS-RADIO dd 861203

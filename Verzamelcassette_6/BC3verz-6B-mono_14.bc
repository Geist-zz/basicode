
1000 A=500:GOTO20:REM "WOORDENSPEL"
1010 DIMA$(30),A(30),B$(30),B(30)
1020 REM---DATA tellen--
1030 TT=0
1040 READD$:TT=TT+1:IFD$<>"Z" THEN1040
1050 TT=TT-1
1060 GOSUB260:K=RV*TT+1
1070 S1=0:S2=0:BT=1
1080 REM === HOOFDLUS ===
1090 GOSUB100
1100 BT=BT+1:IFBT=2 THENBT=0
1110 REM---woord kiezen--
1120 GOSUB260:K=RV*5+K+1
1130 IFK>TT THENK=1
1140 RESTORE
1150 FORN=1 TOK:READX$:NEXTN
1160 X=LEN(X$)
1170 REM---letters en volgnr---
1180 FORN=1 TOX
1190 A$(N)=MID$(X$,N,1):A(N)=N
1200 NEXTN
1210 REM---letters "schudden"--
1220 FORN=1 TOX
1230 IFX=1 THENY=1:GOTO1250
1240 GOSUB260:Y=INT(RV*X+1)
1250 B$(N)=A$(Y):B(N)=A(Y)
1260 FORT=Y TO19
1270 A$(T)=A$(T+1):A(T)=A(T+1):NEXTT
1280 X=X-1:NEXTN
1290 REM---verhaspeld woord Z$--
1300 Z$="":X=LEN(X$)
1310 FORN=1 TOX:Z$=Z$+B$(N):NEXTN
1320 REM---de opgave tonen---
1330 GOSUB100:PRINT:SR=X:GOSUB300
1340 PRINT" HIERONDER STAAN ";SR$;" LETTERS":PRINT
1350 PRINT" DIE SAMEN EEN WOORD VORMEN":PRINT
1360 PRINT" DRUK OP DE SPATIEBALK, ALS":PRINT
1370 PRINT" JE HET WOORD WILT RADEN...":PRINT
1380 PRINT" OP 'S' ALS JE WILT STOPPEN"
1390 HO=1:VE=13:GOSUB110:PRINTZ$
1400 GOSUB3070:GOSUB3040:GOSUB3040
1410 REM--letters goed zetten--
1420 M=LEN(X$):H=12
1430 REM (H=12 is bovenlangs, H=14 onderlangs)
1440 L=2:REM (bij L kijken of die letter goed staat)
1450 F=L:T=B(F):REM (F=from,T=to)
1460 M$=" ":IF(L=M)AND(L=T) THEN1790
1470 IFT=L THENL=L+1:F=L:T=B(L):GOTO1460:REM (staat goed)
1480 GOSUB3040
1490 TR$=B$(F):MT$=" "+TR$+" "
1500 REM (maak uitparkeerpositie leeg)
1510 HO=F:FORN=12 TO14:VE=N:GOSUB110
1520 PRINT" ":NEXTN
1530 HO=F:VE=H:GOSUB110:PRINTTR$
1540 HO=F:VE=13:GOSUB110:PRINTM$:REM (letter uitparkeren)
1550 IFF>T THEN1610
1560 Q=0:FORN=F TOT:REM (naar rechts)
1570 GOSUB200:IFIN=32 THENQ=1:N=T:GOTO1590
1580 HO=N-1:VE=H:GOSUB110:PRINTMT$:GOSUB3040
1590 NEXTN:IFQ=1 THEN1840
1600 GOTO1670
1610 Q=0:FORN=F TOT STEP-1:REM (naar links)
1620 GOSUB200:IFIN=32 THENQ=1:N=T:GOTO1650
1630 IFIN=83 THENQ=2:N=T:GOTO1650
1640 HO=N-1:VE=H:GOSUB110:PRINTMT$:GOSUB3040
1650 NEXTN:IFQ=1 THEN1840
1660 IFQ=2 THEN950:REM (stoppen bij S)
1670 M$=TR$
1680 IFT=L THENHO=T:VE=13:GOSUB110:PRINTTR$:REM (inparkeren)
1690 REM (omschakelen onder-/bovenlangs)
1700 H=26-H
1710 IFT<>L THEN1750
1720 B$(T)=TR$:B(T)=T
1730 HO=L:VE=12:GOSUB110:PRINT" "
1740 VE=14:GOSUB110:PRINT" ":GOTO1460
1750 J$=B$(T):J=B(T):B$(T)=TR$:B(T)=T
1760 F=T:T=J:TR$=J$:MT$=" "+TR$+" "
1770 GOTO1510
1780 REM---niet geraden--
1790 HO=1:VE=18:GOSUB110:PRINT"NIET GERADEN - 0 punten!"
1800 SC=0:GOSUB3070:HO=5:VE=23:GOSUB110
1810 PRINT"tik op 'n toets ";:GOSUB210
1820 GOTO1100
1830 REM---spatiebalk ingedrukt---
1840 IFL=X THENSC=0:GOTO1950
1850 SC=(LEN(X$))*10
1860 FORN=1 TOLEN(X$):MI=B(N):IFMI=N THENSC=SC-10
1870 NEXTN
1880 HO=1:VE=16:GOSUB110
1890 PRINT"WAT IS HET  ";:INPUTSR$:PRINT
1900 IFSR$="" THEN1930
1910 REM--hoofdletters maken--
1920 GOSUB330
1930 IFSR$<>X$ THEN1950
1940 PRINT" GOED ZO! score ";SC;" punten":GOTO1960
1950 PRINT" MIS! het is ";X$:SC=0
1960 IFBT=0 THENS1=S1+SC:GOTO1980
1970 IFBT=1 THENS2=S2+SC
1980 GOSUB3070
1990 HO=5:VE=23:GOSUB110
2000 PRINT"tik op 'n toets ";:GOSUB210
2010 GOTO1100
3000 REM
3010 REM === SUBROUTINES ===
3020 REM
3030 REM---wacht 1 seconde---
3040 SP=0:SV=0:SD=10:GOSUB400:RETURN
3050 REM
3060 REM--score afdrukken--
3070 HO=11:VE=20:GOSUB110
3080 IFBT=1 THEN3100
3090 PRINT"<==":GOTO3110
3100 PRINT"==>"
3110 HO=1:VE=21:GOSUB110:PRINT;S1;" pt"
3120 HO=18:VE=21:GOSUB110:PRINT;S2;" pt"
3130 RETURN
3140 REM
25000 DATA"WANDELSTOK","REGENTON","ONMIDDELLIJK"
25010 DATA"ALUMINIUM","ELECTRICITEIT","MILLIMETER"
25020 DATA"TELEVISIE","BLOKFLUIT","TIMMERMAN"
25030 DATA"METSELAAR","POSTBODE","POSTZEGEL"
25040 DATA"BOTERBLOEM","GLADIOOL","ZONNEBLOEM"
25050 DATA"MADELIEFJE","PEREBOOM","MEDICIJN"
25060 DATA"ROZEBOTTEL","PAPAVER","KLIMOP"
25070 DATA"LIJSTERBES","ZILVERBERK","REGENBUI"
25080 DATA"DUIZENDPOOT","KRUISSPIN","OORWORM"
25090 DATA"SPRINKHAAN","MEIKEVER","SPINNEWEB"
25100 DATA"BROMVLIEG","STEEKMUG","KAKKERLAK"
25110 DATA"BLADLUIS","KOOLRUPS","PIMPELMEES"
25120 DATA"KANARIE","PAPEGAAI","NACHTEGAAL"
25130 DATA"HUISMUS","WIELEWAAL","POSTDUIF"
25140 DATA"ZILVERMEEUW","MEERKOET","ZANGLIJSTER"
25150 DATA"OOIEVAAR","EKSTER","VLEERMUIS"
25160 DATA"OLIFANT","NIJLPAARD","KANGOEROE"
25170 DATA"KROKODIL","KAKATOE","CHIMPANSEE"
25180 DATA"BAVIAAN","DROMEDARIS","KAMEEL"
25190 DATA"NEUSHOORN","ZEEHOND","LUIPAARD"
25200 DATA"BROMFIETS","GIETER","STOPLICHT"
25210 DATA"KABELTOUW","COMPUTER","LEDIKANT"
25220 DATA"KLARINET","BRANDWEER","RESTAURANT"
25230 DATA"POPULIER","ZIEKENHUIS","PLEISTER"
25240 DATA"MUSEUM","OPHAALBRUG","VIADUCT"
25250 DATA"VLIEGTUIG","AMBTENAAR","VLIEGVELD"
25260 DATA"KEUKEN","BADKAMER","HANDDOEK"
25270 DATA"VOETBALVELD","KALENDER","BAKKERIJ"
25280 DATA"TUINSTOEL","WINKELIER","KAPSTOK"
25290 DATA"KOELKAST","AUTOBUS","CHAUFFEUR"
25300 DATA"VILTSTIFT","KNIKKER","WASMIDDEL"
25310 DATA"BRUINBROOD","ROOMBOTER","MARGARINE"
25320 DATA"BENZINEPOMP","FRISDRANK","HANDDOEK"
25330 DATA"REGENJAS","ZONNEBRIL","ZWEMBROEK"
25340 DATA"NAAIMACHINE","VOORJAAR","VLINDER"
25350 DATA"GRASVELD","MOTORBOOT","ZEILJACHT"
25360 DATA"PINDAKAAS","FIETSPOMP","KOELKAST"
25370 DATA"MOERSLEUTEL","PANNESPONS","VAATDOEK"
25380 DATA"ZWEMBAD","KAMPEERTENT","MEETLAT"
25390 DATA"TUINSPROEIER","LASTPOST","SPINAZIE"
25400 DATA"Z"
25410 REM
25420 REM evt andere DATA woorden in HOOFDLETTERS
25430 REM het aantal DATA woorden is vrij, maar het
25440 REM laatste DATAWOORD moet "Z" zijn
30000 REM----------------------------
30010 REM geschikt voor twee personen
30020 REM met beurtaanwijzer en score
30030 REM----------------------------
30040 REM gemaakt op 'n MSX2 computer
30050 REM----------------------------
32000 REM Ch.W.Brederode
32010 REM Von Bonninghausenlaan 33
32030 REM 2161 ES  LISSE
32040 REM
32050 REM TROS-RADIO dd 891115

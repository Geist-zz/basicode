
1000 A=700:GOTO20:REM PUNTENLIJST-PROGRAMMA
1010 VT=VE:HT=HO
1020 DIMX$(31),XY(31,26):REM INTIALISATIE
1030 REM X$=NAMEN, XY=PUNTEN
1040 KL$="":REM KLASSENNUMMER
1050 YZ=0:REM TIJDELIJK BEWAREN TOTAAL PUNT
1060 BN=0:REM PUNTEN BIJSCHRIJVEN
1070 NF$="PUNTEN":REM BESTANDSNAAM
1080 AN=0:REM AANTAL LEERLINGEN
1090 SC=0:REM UITPRINTEN
1100 E$="":REM DATALIJST LEZEN
1110 SP$="":REM BEREKENEN VAN WISSEN VAN REGELS
1120 FORI=0 TOHT:SP$=SP$+" ":NEXTI
1500 REM **HOOFDMENU
1510 GOSUB100:SR$="PUNTENLIJST":GOSUB8710
1520 PRINT:PRINT:PRINT
1530 PRINT"HOOFDMENU:":PRINT
1540 PRINT"Klas: ";KL$:PRINT"Met ";AN;" leerlingen."
1550 PRINT:PRINT
1560 PRINT"1. Nieuwe namen invoeren"
1570 PRINT"2. Namen wijzigen of toevoegen"
1580 PRINT"3. Inlezen oud bestand"
1590 PRINT"4. Wegschrijven bestand"
1600 PRINT"5. Puntenlijst geven"
1610 PRINT"6. Punten bijschrijven"
1620 PRINT"7. Zoeken en bijschrijven"
1630 PRINT"8. Statistische gegevens"
1640 PRINT"9. Einde programma"
1650 VE=VT-1:HO=0:GOSUB110
1660 PRINT"Uw keuze is: ";
1670 GOSUB210:IF(IN<49)OR(IN>57) THEN GOSUB250:GOTO1670
1680 IFIN>52 THEN1700
1690 ONIN-48 GOTO2510,3010,3520,4020
1700 ONIN-52 GOTO4510,6510,7510,7010,2010
2000 REM **PROGRAMMA STOPPEN
2010 GOSUB100
2020 PRINT"Als u gaat stoppen worden alle"
2030 PRINT"variabelen gewist!"
2040 VE=12:HO=0:GOSUB110
2050 PRINT"Weet u het zeker? (j/n)";
2060 GOSUB210
2070 IFIN=74 THEN950:REM Ja
2080 IFIN=78 THEN1510:REM nee
2090 GOTO2060
2500 REM **NAMEN INVOEREN
2510 SR$="Nieuwe namen invoeren":GOSUB8810
2520 VE=5:GOSUB110:PRINT"Invoeren:(#=Stoppen)"
2530 K=0:REM START INVOEREN
2540 K=K+1
2550 VE=7:GOSUB8970:VE=6:GOSUB8970
2560 VE=6:GOSUB110:PRINTK;". ";:INPUTX$(K)
2570 IFX$(K)="#" THENX$(K)="":AN=K-1:GOTO2630
2580 PRINT"Zo goed?(j/n)";
2590 GOSUB210
2600 IFIN=74 THEN2540:REM ja
2610 IFIN=78 THEN2550:REM nee
2620 GOTO2590
2630 VE=5:GOSUB8970:VE=6:GOSUB8970
2640 VE=6:HO=0:GOSUB110
2650 INPUT"Nieuw klassenummer: ";KL$
2660 GOSUB8520:GOTO1510
3000 REM **NAMEN WIJZIGEN/TOEVOEGEN
3010 SR$="Namen wijzigen of toevoegen":GOSUB8810
3020 IFAN=0 THENPRINT"Eerst nieuwe namen invullen."
3030 IFAN=0 THEN GOSUB8520:GOTO1510
3040 PRINT"Er zijn ";AN;" leerlingen in het bestand."
3050 PRINT:PRINT"Geef nummer van de leerling in het"
3060 PRINT"bestand die u wil wijzigen of wil"
3070 PRINT"toevoegen:(0=aantal leerlingen invoeren)"
3080 INPUTK
3090 IFK=0 THENINPUT"Nieuwe leerlingenaantal wordt: ";AN
3100 IFK=0 THEN3150
3110 PRINT"Nummer: ";K:PRINT"Naam: ";X$(K)
3120 INPUT"Vul in nieuwe naam: ";X$(K)
3130 PRINT
3140 INPUT"Nieuwe leerlingenaantal wordt: ";AN
3150 VE=15:HO=0:GOSUB110
3160 PRINT"Nog een keer?(j/n) ";
3170 GOSUB210
3180 IFIN=74 THEN3010:REM ja
3190 IFIN=78 THEN1510:REM nee
3200 GOTO3170
3500 REM **INLEZEN BESTAND
3510 REM MENU
3520 GOSUB100:SR$="MENU":GOSUB8710
3530 PRINT:PRINT:PRINT
3540 PRINT"INLEZEN VAN BESTANDEN:":PRINT
3550 PRINT"Klas: ";KL$:PRINT"Met ";AN;" leerlingen."
3560 PRINT:PRINT:PRINT
3570 PRINT"1. Inlezen van Basicode-3 cassette"
3580 PRINT"2. Inlezen van cassette eigen computer"
3590 PRINT"3. Inlezen van diskette eigen computer"
3600 PRINT"4. Naar hoofdmenu"
3610 VE=VT-1:HO=0:GOSUB110
3620 PRINT"Uw keuze is: ";
3630 GOSUB210
3640 IFIN$="1" THENNF=0:GOTO3700
3650 IFIN$="2" THENNF=2:GOTO3700
3660 IFIN$="3" THENNF=4:GOTO3700
3670 IFIN$="4" THEN1510
3680 GOTO3630
3690 REM LADEN VAN BESTANDEN
3700 IFNF=0 THENSR$="Inlezen van Basicode-3"
3710 IFNF=2 THENSR$="Inlezen van cassette"
3720 IFNF=4 THENSR$="Inlezen van diskette"
3730 GOSUB8810
3740 IFNF<>4 THENNF$="PUNTEN":GOTO3800
3750 PRINT"Geef naam te laden bestand van"
3760 INPUT"diskette:(max.7 tekens) ";NF$
3770 VE=3:GOSUB8970:VE=2:GOSUB8970
3780 IFLEN(NF$)>7 THENVE=2:GOSUB110:GOTO3750
3790 GOSUB110:PRINT"Naam in te laden bestand: ";NF$
3800 PRINT"Even geduld alstublieft...":GOSUB500
3810 GOSUB540:KL$=IN$:GOSUB540:AN=VAL(IN$)
3820 FORI=1 TOAN:GOSUB540:X$(I)=IN$:NEXTI
3830 FORI=1 TOAN
3840 GOSUB540:XY(I,0)=VAL(IN$)
3850 FORK=1 TOXY(I,0):GOSUB540:XY(I,K)=VAL(IN$):NEXTK
3860 NEXTI
3870 GOSUB580:GOSUB250:GOSUB8520:GOTO1510
4000 REM **WEGSCHRIJVEN BESTAND
4010 REM MENU
4020 GOSUB100:SR$="MENU":GOSUB8710
4030 PRINT:PRINT:PRINT
4040 PRINT"WEGSCHRIJVEN VAN BESTANDEN:":PRINT
4050 PRINT"Klas: ";KL$:PRINT"Met ";AN;" leerlingen."
4060 PRINT:PRINT:PRINT
4070 PRINT"1. Wegschrijven naar Basicode cassette"
4080 PRINT"2. Wegschrijven naar cassette"
4090 PRINT"3. Wegschrijven naar diskette"
4100 PRINT"4. Naar hoofdmenu"
4110 VE=VT-1:HO=0:GOSUB110
4120 PRINT"Uw keuze is: ";
4130 GOSUB210
4140 IFIN$="1" THENNF=1:GOTO4200
4150 IFIN$="2" THENNF=3:GOTO4200
4160 IFIN$="3" THENNF=5:GOTO4200
4170 IFIN$="4" THEN1510
4180 GOTO4130
4190 REM SAVEN VAN BESTANDEN
4200 IFNF=1 THENSR$="Wegschrijven in basicode-3"
4210 IFNF=3 THENSR$="Wegschrijven naar cassette"
4220 IFNF=5 THENSR$="Wegschrijven naar diskette"
4230 GOSUB8810
4240 IFNF<>5 THENNF$="PUNTEN":GOTO4300
4250 PRINT"Geef naam voor het weg te schrijven"
4260 PRINT"bestand:(max.7 tekens) ";:INPUTNF$
4270 VE=3:GOSUB8970:VE=2:GOSUB8970
4280 IFLEN(NF$)>7 THENVE=2:GOSUB110:GOTO4250
4290 GOSUB110:PRINT"Naam bestand: ";NF$
4300 PRINT"Even geduld alstublieft...":GOSUB500
4310 SR$=KL$:GOSUB560
4320 SR=AN:GOSUB300:GOSUB560
4330 FORI=1 TOAN:SR$=X$(I):GOSUB560:NEXTI
4340 FORI=1 TOAN
4350 SR=XY(I,0):GOSUB300:GOSUB560
4360 FORK=1 TOXY(I,0)
4370 SR=XY(I,K):GOSUB300:GOSUB560
4380 NEXTK
4390 NEXTI
4400 GOSUB580:GOSUB250:GOSUB8520:GOTO1510
4500 REM **PUNTENLIJST GEVEN
4510 SR$="Puntenlijst":GOSUB8810
4520 PRINT"Naar printer?(j/n)";
4530 GOSUB210
4540 IFIN=78 THEN4630:REM nee
4550 IFIN<>74 THEN4530
4560 PRINT
4570 PRINT"Nieuwe definities invullen?(j/n)";
4580 GOSUB210
4590 IFIN=74 THEN5010:REM ja
4600 IFIN=78 THEN5130:REM nee
4610 GOTO4580
4620 REM PUNTENLIJST SCHERM
4630 SR$="Puntenlijst op scherm":GOSUB8810
4640 M=0:N=0:REM START CYCLUS
4650 N=N+1:IF(N=AN+1)OR(AN=0) THEN4790
4660 SR=N:GOSUB300:PRINTSR$;". ";X$(N)
4670 YZ=0:M=M+1
4680 FORK=1 TOXY(N,0)
4690 SR=XY(N,K):GOSUB300:PRINTSR$;
4700 IFXY(N,K+1)<>0 THENPRINT"-";
4710 YZ=YZ+XY(N,K)
4720 NEXTK
4730 K=K-1:PRINT
4740 PRINT"Gemiddelde is: ";YZ;":";K;"=";YZ/K
4750 PRINT"Afgerond:(naar boven) ";INT(YZ/K+.5)
4760 IFM<>3 THENPRINT:GOTO4650
4770 SR$="Puntenlijst op scherm"
4780 GOSUB8520:GOSUB8810:M=0:GOTO4650
4790 YZ=0:REM EINDE LIJST
4800 PRINT:PRINT"Einde lijst"
4810 GOSUB8520:GOTO1510
5000 REM ** PUNTENLIJST UITPRINTEN
5010 SR$="Puntenlijst uitprinten":GOSUB8810
5020 FORI=13 TO17:XY(0,I)=0:NEXTI
5030 SB$="":YZ=0:SC=0
5040 INPUT"Aantal letters per regel: ";XY(0,13)
5050 INPUT"Kantlijnbreedte: ";XY(0,14)
5060 INPUT"Regels TEKST per pagina: ";XY(0,15)
5070 PRINT"(1) Kettingformulieren of"
5080 INPUT"(2) Losse vellen: ";XY(0,16)
5090 IFXY(0,16)<>1 THEN5110
5100 INPUT"Hoeveel LEGE regels onderaan vel: ";XY(0,17)
5110 PRINT"Geef datum vandaag: ";:INPUTX$(0)
5120 REM PRINTMENU
5130 SR$="Puntenlijst uitprinten":GOSUB8810
5140 PRINT"1. Heel de lijst afdrukken"
5150 PRINT"2. Een naam afdrukken"
5160 PRINT"3. U heeft geen printer,"
5170 PRINT"   Uw printer staat niet klaar"
5180 GOSUB210
5190 IFIN$="1" THEN GOSUB8810:GOTO5510
5200 IFIN$="2" THEN GOSUB8810:GOTO5240
5210 IFIN$="3" THEN1510
5220 GOTO5180
5230 REM EEN NAAM AFDRUKKEN
5240 FORI=1 TOXY(0,14)
5250 SB$=SB$+" ":NEXTI
5260 PRINT"Geef nummer van de naam die u wil"
5270 INPUT"afdrukken: ";M
5280 PRINT:PRINT"Deze naam: ":PRINTM;". ";X$(M)
5290 PRINT"Zo goed?(j/n)";
5300 GOSUB210
5310 IFIN=74 THENSC=1:GOTO5690
5320 IFIN=78 THEN GOSUB8810:GOTO5260
5330 GOTO5300
5500 REM ** HELE LIJST AFDRUKKEN
5510 FORI=1 TOXY(0,14)
5520 SB$=SB$+" ":NEXTI:GOSUB8610:GOSUB360
5530 SA$=SB$+"Puntenlijst van: "+X$(0):SR$=SA$
5540 GOSUB350:GOSUB360:SA$=SB$+"Klas: "+KL$
5550 SR$=SA$:SA$="":GOSUB350:GOSUB360
5560 SA$=SB$+"Aantal leerlingen: ":SR=AN:GOSUB300
5570 SA$=SA$+SR$:SR$=SA$:GOSUB350:GOSUB360
5580 GOSUB360
5590 N=5:I=0:K=0:REM START UITPRINTLUS
5600 N=N+1
5610 IF(N>=XY(0,15))AND(XY(0,15)>0) THEN5630
5620 GOTO5680
5630 IFXY(0,16)=2 THEN GOSUB8610:N=0:GOSUB360
5640 IFXY(0,16)=2 THEN5600
5650 N=N-XY(0,15)
5660 FORL=1 TOXY(0,17)-N:GOSUB360:NEXTL
5670 N=0:GOSUB360:GOTO5600
5680 I=I+1:SA$="":SR$=""
5690 IFSC=1 THENI=M
5700 SR=I:GOSUB300:SA$=SB$+SR$+". "+X$(I)
5710 SR$=SA$:GOSUB350:GOSUB360
5720 N=N+1:SA$=SB$:ZO=XY(0,13)
5730 FORK=1 TOXY(I,0)
5740 SR=XY(I,K):GOSUB300
5750 SA$=SA$+SR$:IFXY(I,K+1)<>0 THENSA$=SA$+"-"
5760 ZP=LEN(SA$):YZ=YZ+XY(I,K)
5770 IFZP<ZO THEN5820
5780 SR$=LEFT$(SA$,ZO):N=N+1
5790 SA$=SB$+RIGHT$(SA$,ZP-ZO)
5800 GOSUB350
5810 IFZP>ZO THEN GOSUB360
5820 NEXTK:K=K-1
5830 IFZP<>0 THENN=N+1:SR$=SA$:GOSUB350:GOSUB360
5840 IFSC=1 THEN5870
5850 IF(XY(0,16)<>2)OR(N<XY(0,15)) THEN5870
5860 GOSUB8610:GOSUB360:N=1
5870 SA$=SB$+"Gemiddelde is: ":SR=YZ:GOSUB300
5880 SA$=SA$+SR$+":":SR=K:GOSUB300
5890 SA$=SA$+SR$+"=":SR=YZ/K:GOSUB300
5900 SA$=SA$+SR$:SR$=SA$:GOSUB350:GOSUB360
5910 N=N+1:SA$=SB$+"Afgerond:(naar boven) "
5920 SR=INT(YZ/K+.5):GOSUB300:SA$=SA$+SR$
5930 SR$=SA$:GOSUB350:GOSUB360:N=N+1:YZ=0
5940 IF(I=AN)OR(SC=1) THEN6010
5950 GOSUB360:GOTO5600
6000 REM ** EINDE UITPRINTEN
6010 SR$=SB$+"EINDE LIJST":GOSUB350:GOSUB360
6020 GOSUB250
6030 PRINT:PRINT:PRINT"Einde lijst"
6040 PRINT:PRINT"Nog een keer printen?(j/n)";
6050 GOSUB210
6060 IFIN=74 THEN5130:REM ja
6070 IFIN=78 THEN1510:REM nee
6080 GOTO6050
6500 REM **PUNTEN BIJSCHRIJVEN
6510 N=0:BN=1
6520 SR$="Punten bijschrijven":GOSUB8810
6530 N=N+1:REM START LUS
6540 IFN=AN+1 THENPRINT:PRINT"Einde lijst"
6550 IFN=AN+1 THENBN=0:GOSUB8520:GOTO1510
6560 PRINTN;". ";X$(N)
6570 PRINT"Punten bijschrijven?(j/n)(#=Naar menu) ";
6580 GOSUB210
6590 IFIN=74 THENPRINT:GOTO7690:REM ja
6600 IFIN=78 THEN6520:REM nee
6610 IFIN$="#" THENBN=0:GOTO1510
6620 GOTO6580
7000 REM **STATISTISCHE GEGEVENS
7010 SR$="Statistische gegevens":GOSUB8810
7020 PRINT"Even geduld alstublieft..."
7030 FORN=0 TO12:XY(0,N)=0:NEXTN
7040 IFAN=0 THEN GOSUB8520:GOTO1510
7050 FORN=1 TOAN
7060 FORK=1 TOXY(N,0)
7070 M=INT((XY(N,K))+.5)
7080 XY(0,11)=XY(0,11)+1
7090 XY(0,M)=XY(0,M)+1
7100 NEXTK
7110 NEXTN
7120 GOSUB250:ZP=0
7130 VE=2:GOSUB8970
7140 VE=2:HO=0:GOSUB110
7150 PRINT"Er zijn ";AN;" leerlingen in deze klas,"
7160 PRINT"met totaal ";XY(0,11);" punten."
7170 PRINT"IN DE KLAS ZIJN ER:":PRINT
7180 FORN=0 TO10
7190 ZP=ZP+XY(0,N)*N
7200 ZO=INT(XY(0,N)/XY(0,11)*100+.5)
7210 IFN<6 THENXY(0,12)=XY(0,12)+XY(0,N)
7220 PRINTTAB(0);XY(0,N);TAB(6);"(";ZO;"%)";
7230 PRINTTAB(14);"punten van het cijfer ";N:NEXTN
7240 ZO=XY(0,11)-XY(0,12)
7250 PRINT"Het klasgemiddelde is: ";ZP/XY(0,11)
7260 PRINT"De klas heeft ";XY(0,12);
7270 PRINT" (";INT((XY(0,12)/(XY(0,11)/100))+.5);"%) ";
7280 PRINT"onvoldoendes"
7290 PRINT"en ";ZO;" (";INT((ZO/(XY(0,11)/100))+.5);
7300 PRINT"%) voldoendes."
7310 GOSUB8520:GOTO1510
7500 REM **ZOEKEN EN BIJSCHRIJVEN
7510 SR$="Zoeken en bijschrijven":GOSUB8810
7520 PRINT"Geef een gedeelte van de achternaam:"
7530 INPUTZO$:ZO=LEN(ZO$)
7540 N=0:REM START ZOEKLUS
7550 N=N+1:IFN=AN+1 THEN8060
7560 ZP$=X$(N):ZP=LEN(ZP$)
7570 K=ZP:K=K+1:REM SPATIEZOEKEN
7580 K=K-1
7590 IFK=0 THEN7620
7600 IFMID$(ZP$,K,1)<>" " THEN7580
7610 ZP$=RIGHT$(ZP$,ZP-K)
7620 ZP$=LEFT$(ZP$,ZO)
7630 IFZP$=ZO$ THEN7660
7640 GOTO7550
7650 REM GEVONDEN
7660 GOSUB250:PRINT"Gevonden!"
7670 PRINT"De volledige naam is:"
7680 SR=N:GOSUB300:PRINTSR$;". ";X$(N)
7690 PRINT"De punten tot nu toe waren:"
7700 K=0:REM PUNTEN TOT NU TOE
7710 K=K+1
7720 IFXY(N,K)=0 THEN7770
7730 SR=XY(N,K):GOSUB300:PRINTSR$;
7740 IFXY(N,K+1)<>0 THENPRINT"-";
7750 GOTO7710
7760 REM BIJSCHRIJVEN
7770 IFK=1 THENPRINT"Geen punten gevonden!":GOTO7780
7780 PRINT:PRINT"Bijschrijven:(-1=Stop, -2=Verbeteren)"
7790 K=K-1:REM START INVULCYCLUS
7800 K=K+1
7810 INPUTXY(N,K)
7820 IFXY(N,K)=-2 THEN8910
7830 IFXY(N,K)<>-1 THEN7800
7840 XY(N,K)=0:K=K-1:REM PUNTEN TOT NU TOE
7850 IF(K=0)AND(XY(N,K+1)=0) THEN8010
7860 PRINT"De punten zijn:"
7870 FORM=1 TOK:REM START LUS
7880 SR=XY(N,M):GOSUB300:PRINTSR$;
7890 IFXY(N,M+1)<>0 THENPRINT"-";
7900 YZ=YZ+XY(N,M)
7910 NEXTM
7920 PRINT
7930 PRINT"Het gemiddelde is: ";YZ;":";K;"=";YZ/K
7940 PRINT"Afgerond:(naar boven) ";INT(YZ/K+.5)
7950 YZ=0:XY(N,0)=K
7960 GOSUB8520:IFBN=1 THEN6520
7970 GOTO8090
8000 REM GEEN PUNTEN MEER AANWEZIG
8010 PRINT"Bij deze leerling zijn geen punten"
8020 PRINT"meer aanwezig."
8030 GOSUB8520:IFBN=1 THEN6520
8040 GOTO8090
8050 REM NIET GEVONDEN
8060 PRINT"Niet gevonden!"
8070 GOSUB8520
8080 REM NOG EEN KEER ZOEKEN
8090 SR$="Zoeken en bijschrijven":GOSUB8810
8100 VE=5:HO=0:GOSUB110
8110 PRINT"Nog een keer zoeken?(j/n) ";
8120 GOSUB210
8130 IFIN=78 THEN1510:REM nee
8140 IFIN<>74 THEN8120
8150 VE=5:GOSUB8970
8160 VE=2:HO=0:GOSUB110
8170 GOTO7520
8500 REM **SUBROUTINES
8510 REM TOETS VOOR VERDER
8520 PRINT:PRINT"Toets...";:GOSUB210
8530 RETURN
8600 REM PRINTER KLAAR
8610 VE=2:HO=0:GOSUB110
8620 IFXY(0,16)=2 THENPRINT"Stop nieuw vel in de printer."
8630 PRINT"Staat de printer klaar, druk toets in.";
8640 GOSUB210
8650 VE=2:GOSUB110:PRINTSP$
8660 VE=3:GOSUB110:PRINTSP$
8670 VE=2:GOSUB110
8680 RETURN
8700 REM OPVALLENDE KOP SCHERM
8710 VE=0
8720 HO=INT((HT-LEN(SR$))/2-3):IFHO<0 THENHO=0
8730 GOSUB110:GOSUB150
8740 RETURN
8800 REM KOP SCHERM
8810 GOSUB100
8820 PRINTSR$:IFHT<36 THEN8840
8830 VE=0:HO=29:GOSUB110:PRINT"Klas: ";KL$
8840 VE=1:HO=0:GOSUB110
8850 FORI=0 TOHT:PRINT"-";:NEXTI
8860 VE=2:HO=0:GOSUB110
8870 RETURN
8900 REM VERBETEREN CIJFER
8910 IFK<2 THENPRINT"Er valt niets te verbeteren."
8920 IFK<2 THENK=1:GOTO7810
8930 XY(N,K)=0:K=K-1:XY(N,K)=0
8940 PRINT"Cijfer is verbeterd."
8950 GOTO7810
8960 REM WIS REGEL
8970 HO=0:GOSUB110:PRINTSP$
8980 RETURN
8990 REM UITLEG VAN SCHERM
30000 REM
30010 REM ***********************
30020 REM *   Het basicode-3    *
30030 REM *     programma       *
30040 REM *    PUNTENLIJST      *
30050 REM ***********************
30060 REM *  is gemaakt op een  *
30070 REM *      Sinclair       *
30080 REM *   ZX-Spectrum 48K   *
30090 REM *        in           *
30100 REM *     maart 1989      *
30110 REM ***********************
30120 REM *      door:          *
32000 REM ***********************
32010 REM *   P. Corstanje      *
32020 REM *  Veldekestraat 66b  *
32030 REM *   4819 ER BREDA     *
32040 REM ***********************
32050 REM
32060 REM TROS-RADIO dd 891018

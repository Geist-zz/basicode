
1000 A=200:GOTO20:REM SCHUIVERTJE
1010 DIMA$(15):RY=1
1020 GOSUB1800
1030 R$="TROSBASICODE-3- "
1040 FORN=0 TO15:A$(N)=MID$(R$,N+1,1):NEXTN
1050 GT=15:HG=3:VG=3:BE=1
1060 REM---teken het speelveld---
1070 A$="+---------------+"
1080 B$="!               !"
1090 HO=9:VE=4:GOSUB110:PRINTA$
1100 VE=17:GOSUB110:PRINTA$
1110 FORN=5 TO16:VE=N:GOSUB110:PRINTB$:NEXTN
1120 REM---print de letters---
1130 GOSUB1500
1140 REM---in de war maken---
1150 GOSUB1560
1160 HO=4:VE=0:GOSUB110
1170 PRINT"Bedienen met de pijltoetsen"
1180 VE=1:GOSUB110
1190 PRINT" (en 2 of 3)   S = stoppen"
1200 HO=8:VE=20:GOSUB110
1210 PRINT"Moeilijkheidsgraad ";MM
1220 VE=21:GOSUB110
1230 SR=BE:GOSUB300
1240 PRINTSR$;" e beurt..."
1250 REM---de cursor---
1260 SD=10000:GOSUB450
1264 IFIN=50 THENRY=2:GOTO1260
1266 IFIN=51 THENRY=3:GOTO1260
1270 IFIN=83 THEN950
1280 Q=IN-27:IF(Q<1)OR(Q>4) THEN1260
1290 ONQ GOSUB1640,1680,1720,1760
1300 GOSUB1500:RY=1
1310 SR=BE:GOSUB300
1320 BE=BE+1:HO=8:VE=21:GOSUB110:PRINTSR$;" e beurt..."
1330 T$="":FORN=0 TO15:T$=T$+A$(N):NEXTN
1340 IFT$<>R$ THEN1260
1350 SV=7:SD=4:SP=80:GOSUB400
1360 SP=84:GOSUB400
1370 SP=87:GOSUB400
1380 SP=92:SD=12:GOSUB400
1390 HO=23:VE=16:GOSUB110:PRINT" "
1400 HO=8:VE=21:GOSUB110:PRINT"KLAAR in ";BE-1;" beurten!"
1410 VE=23:GOSUB110:PRINT"nog een keer? (j/n) ";
1420 GOSUB210:IFIN=78 THEN950
1430 IFIN=74 THEN1000
1440 GOTO1420
1450 REM-----------------
1460 REM
1470 REM---SUBROUTINES---
1480 REM
1490 REM---druk de getallen af---
1500 VE=3:FORJ=0 TO15 STEP4:VE=VE+3
1510 FORK=0 TO3:HO=11+4*K
1520 GOSUB110:PRINTA$(J+K):NEXTK
1530 NEXTJ
1540 RETURN
1550 REM---in de war maken---
1560 SD=20:GOSUB450
1570 FORN=1 TO10*MM
1580 GOSUB260:X=1+INT(RV*4)
1590 ONX GOSUB1640,1680,1720,1760
1600 IFN/10=INT(N/10) THEN GOSUB1500
1610 NEXTN
1620 HC=11:VC=7:RETURN
1630 REM---gat naar rechts---
1640 IFHG=3 THENRETURN
1650 HG=HG+1:A$(GT)=A$(GT+1):GT=GT+1
1660 A$(GT)=" ":RY=RY-1:IFRY>0 THEN1640
1665 RETURN
1670 REM---gat naar links---
1680 IFHG=0 THENRETURN
1690 HG=HG-1:A$(GT)=A$(GT-1):GT=GT-1
1700 A$(GT)=" ":RY=RY-1:IFRY>0 THEN1680
1705 RETURN
1710 REM---gat omhoog---
1720 IFVG=0 THENRETURN
1730 VG=VG-1:A$(GT)=A$(GT-4):GT=GT-4
1740 A$(GT)=" ":RY=RY-1:IFRY>0 THEN1720
1745 RETURN
1750 REM---gat omlaag---
1760 IFVG=3 THENRETURN
1770 VG=VG+1:A$(GT)=A$(GT+4):GT=GT+4
1780 A$(GT)=" ":RY=RY-1:IFRY>0 THEN1760
1785 RETURN
1790 REM---intro---
1800 PRINT:PRINT"SCHUIVERTJE":PRINT:PRINT
1810 PRINT:PRINT"Het wereldbekende schuifspel"
1820 PRINT:PRINT"Je kunt  het  spelen  met de"
1830 PRINT:PRINT"vier pijltoetsen."
1832 PRINT:PRINT"Als je  twee of drie blokjes"
1834 PRINT:PRINT"tegelijk wilt schuiven,  dan"
1836 PRINT:PRINT"eerst een 2 of een 3 tikken."
1840 PRINT:PRINT
1850 PRINT:PRINT"-Hoe moeilijk wil je spelen?"
1860 PRINT:PRINT" Kies cijfertoets 1 tot 9 ";
1870 GOSUB210:MM=VAL(IN$):IF(MM<1)OR(MM>9) THEN1870
1880 GOSUB100:RETURN
1890 REM--------------------------
32000 REM Ch.W.Brederode
32010 REM Von Bonninghausenlaan 33
32020 REM 2161 ES  LISSE
32030 REM
32040 REM TROS-RADIO dd 891115


1000 A=200:GOTO20:REM KARAKTERS OP HET GRAFISCH-SCHERM
1010 GL$="":GH$="":AL$="":AH$=""
1020 LE$="                                  "
1030 FR=0:GOSUB280
1100 REM ****> MENU <****
1110 GOSUB100
1120 PRINT"*************************************"
1130 PRINT"*     KARAKTERS OP HET SCHERM       *"
1140 PRINT"*************************************"
1150 PRINT"*                                   *"
1160 PRINT"*    1  HET PROGRAMMA.              *"
1170 PRINT"*    2  KARAKTERS OP EEN REGEL.     *"
1180 PRINT"*    3  REGELS OP HET SCHERM.       *"
1190 PRINT"*    4  ONTWERPEN KARAKTER.         *"
1200 PRINT"*                                   *"
1210 PRINT"*    5  STOPPEN.                    *"
1220 PRINT"*                                   *"
1230 PRINT"*                                   *"
1240 PRINT"*                                   *"
1250 PRINT"*    TYPE UW KEUZE IN.              *"
1260 PRINT"*                                   *"
1270 PRINT"*************************************"
1280 REM
1290 REM
1300 REM
1310 PRINT
1320 REM ****> VERWERKEN KEUZE <****
1330 GOSUB7110:TY=IN-48
1340 IF(TY<1)OR(TY>5) THEN1330
1350 SR=TY
1360 GOSUB300
1370 PRINT"      UW KEUZE IS : ";SR$
1380 PRINT
1390 PRINT"      IS DAT DE GOEDE KEUZE (J/n) ?"
1400 GOSUB7110
1410 IFIN=13 THEN1430
1420 IFIN<>74 THEN1110
1430 IFTY=5 THEN1490
1450 ONTY GOSUB1510,2010,3010,4010,5010
1460 GOTO1110
1490 GOTO950
1500 REM *****> HET PROGRAMMA <*****
1510 GOSUB100
1520 PRINT"Met dit programma kunnen de mogelijk-":PRINT
1530 PRINT"heden en de onmogelijkheden van tekst":PRINT
1540 PRINT"op een grafisch scherm worden bekeken."
1550 PRINT:PRINT
1560 GOSUB7010
1570 PRINT"Het programma kan misschien een hand-":PRINT
1580 PRINT"leiding zijn voor degenen die voor het":PRINT
1590 PRINT"eerst met tekst tussen figuren willen":PRINT
1600 PRINT"werken.":PRINT:PRINT
1610 GOSUB7010
1620 PRINT"Eerst volgt een overzicht van de in":PRINT
1630 PRINT"het programma gebruikte afkortingen,":PRINT
1640 PRINT"termen en variabelen."
1650 GOSUB7010
1660 GOSUB100
1670 PRINT"Dot : een beeldpunt in het grafisch"
1680 PRINT"      scherm."
1690 PRINT"KM  : Karakter Matrix, het rechthoekje"
1700 PRINT"      waarin met dots een karakter is"
1710 PRINT"      opgebouwd."
1720 PRINT"LB  : LinkerBoven dot van een KM.":PRINT
1730 GOSUB7010
1740 PRINT"DH  : aantal Dots Horizontaal in een"
1750 PRINT"      KM."
1760 PRINT"DV  : aantal Dots Vertikaal in een KM."
1770 PRINT"HP  : Horizontale Positie van LB in"
1780 PRINT"      dots van boven af."
1790 PRINT"VP  : Vertikale Positie van LB in dots"
1800 PRINT"      vanaf links.":PRINT
1810 PRINT"KM(DH-1,DV-1) : Array voor KM."
1820 GOSUB7010:GOSUB100
1830 PRINT"Van het meest linksboven gelegen"
1840 PRINT"karakter zijn van LB de coordinaten :"
1850 PRINT"(HP,VP)=(0,0). Van het meest"
1860 PRINT"rechtsonder gelegen karakter zijn"
1870 PRINT"van LB de coordinaten :"
1880 PRINT"(HP,VP) = (HG-1-DH,VG-1-DV).":PRINT
1890 GOSUB7010
1900 PRINT"HG en VG zijn BC-3 variabelen die"
1910 PRINT"in een programma opgeroepen kunnen"
1920 PRINT"worden en die resp. het aantal dots"
1930 PRINT"op een horizontale en op een"
1940 PRINT"vertikale beeldlijn aangeven."
1950 GOSUB7010
1960 PRINT:PRINT
1970 PRINT"         SUCCES"
1980 GOSUB7010
1990 RETURN
2000 REM *****> KARAKTERS OP EEN REGEL <*****
2010 GOSUB100
2020 PRINT"****> KARAKTERS OP EEN REGEL <*****":PRINT
2030 PRINT"Nog uitleg ? (J/n) ?"
2040 GOSUB7110
2050 IF(IN=13)OR(IN=74) THEN2070
2060 GOTO2310
2070 HO=0:VE=2:GOSUB110
2080 PRINT"Met dit deel kunnen we onderzoeken":PRINT
2090 PRINT"hoeveel karakters er in het grafisch":PRINT
2100 PRINT"scherm op een regel kunnen.":PRINT:PRINT
2110 GOSUB7010
2120 PRINT"Het programma vraagt om DH en zet dan":PRINT
2130 PRINT"een aantal reeksen van de karakters":PRINT
2140 PRINT"0 t/m 9 achter elkaar op het scherm.":PRINT
2150 PRINT"Daar onder wordt dan twee keer het":PRINT
2160 PRINT"aantal karakters opgegeven."
2170 GOSUB7010
2180 GOSUB100
2190 PRINT"In de bovenste zin wordt de opgegeven":PRINT
2200 PRINT"DH gebruikt terwijl in de onderste":PRINT
2210 PRINT"zin de normale grafische tekst-mode":PRINT
2220 PRINT"wordt gebruikt.":PRINT:PRINT
2230 GOSUB7010
2240 PRINT"Hierna is het mogelijk om een andere":PRINT
2250 PRINT"waarde voor DH te proberen.":PRINT:PRINT
2260 PRINT"Normale waarden voor DH liggen ":PRINT
2270 PRINT"rond 8, maar experimenteren":PRINT
2280 PRINT"tussen 3 en 14 kan.":PRINT:PRINT
2290 PRINT"SUCCES."
2300 GOSUB7010
2310 GOSUB100
2320 VR$="Voer waarde voor DH in (3-14) : "
2330 HO=0:VE=0:GOSUB110
2340 GL$="0":GH$="9":AL$="3":AH$="14"
2350 GOSUB7210
2360 DH=VAL(AW$)
2370 GOSUB600:CN=0
2410 I=0:J=0:C=DH/HG
2420 VE=.4
2430 FORHO=.2/HG TO1-(DH-1)/HG STEPC
2440 SR=I:GOSUB300
2450 GOSUB650
2460 I=I+1:IFI=10 THENI=0
2470 J=J+1
2480 NEXTHO
2490 HO=.2/HG:VE=.48
2500 SR=J:GOSUB300:A$=SR$
2510 B$="Dit zijn "+A$+" karakters."
2520 K=LEN(B$)
2530 FORI=1 TOK
2540 SR$=MID$(B$,I,1)
2550 GOSUB650
2560 HO=HO+C
2570 NEXTI
2580 HO=0:VE=.56
2590 SR$=B$:GOSUB650
2600 GOSUB7610
2610 VR$="Nog een andere DH  (J/n) ?"
2620 HO=0:VE=.8:SR$=VR$:GOSUB650
2630 GOSUB7110
2650 IF(IN=13)OR(IN=74) THEN2310
2700 GOSUB100
2710 PRINT"Van elk karakter kan HO en PH":PRINT
2720 PRINT"worden uitgeprint.":PRINT:PRINT
2730 PRINT"Is dat nodig (J/n) ?":PRINT:PRINT
2740 GOSUB7110
2750 IF(IN=13)OR(IN=74) THEN2800
2760 GOSUB7010
2770 RETURN
2800 GOSUB6310
2860 I=1:C=DH/HG:PH=0
2870 FORHO=.2/VG TO1-(DH-1)/HG STEPC
2880 SR=I:CN=0:CT=2:GOSUB310:A$=SR$
2890 SR=PH:CN=0:CT=3:GOSUB310:B$=SR$
2900 SR=HO:CN=3:CT=5:GOSUB310:C$=SR$
2920 SR$=SP$+"Karakter "+A$+"; PH = "+B$+"; HO = "+C$
2930 GOSUB350:GOSUB360
2940 I=I+1:PH=PH+DH
2950 NEXTHO
2960 RETURN
3000 REM *****> REGELS <*****
3010 GOSUB100
3020 PRINT" *****> REGELS <*****":PRINT
3030 PRINT"Nog uitleg ? (J/n) ?"
3040 GOSUB7110
3050 IF(IN=13)OR(IN=74) THEN3070
3060 GOTO3300
3070 HO=0:VE=2:GOSUB110
3080 PRINT"Met dit deel kunnen we onderzoeken":PRINT
3090 PRINT"hoeveel regels er op het grafisch":PRINT
3100 PRINT"scherm kunnen.":PRINT:PRINT
3110 GOSUB7010
3120 PRINT"Het programma vraagt om DV en print":PRINT
3130 PRINT"het met DV mogelijke aantal regels":PRINT
3140 PRINT"op het scherm.":PRINT:PRINT
3150 PRINT"Hierna is het mogelijk om een andere":PRINT
3160 PRINT"waarde voor DV te proberen.":PRINT:PRINT
3170 GOSUB7010:GOSUB100
3180 PRINT"Normale waarden voor DV liggen ":PRINT
3190 PRINT"rond 8, maar experimenteren":PRINT
3200 PRINT"tussen 3 en 30 kan.":PRINT:PRINT
3210 GOSUB7010
3220 PRINT"Van elke regel kan worden afgelezen":PRINT
3230 PRINT"op welke VE en PV de regel ligt.":PRINT:PRINT
3240 PRINT"SUCCES"
3250 GOSUB7010
3300 GOSUB100
3310 VR$="Voer waarde voor DV in (3-30) : "
3320 HO=0:VE=0:GOSUB110
3330 GL$="0":GH$="9":AL$="3":AH$="30"
3340 GOSUB7210
3350 DV=VAL(AW$)
3400 GOSUB600:CN=0
3410 I=1:C=DV/VG:PV=0
3420 HO=0
3430 FORVE=.2/VG TO1-(DV-1)/VG STEPC
3440 CI=CN
3450 SR=I:CN=0:CT=2:GOSUB310:A$=SR$
3460 SR=PV:CN=0:CT=3:GOSUB310:B$=SR$
3470 SR=VE:CN=3:CT=5:GOSUB310:C$=SR$
3480 CN=CI
3490 SR$="Regel "+A$+"; PV = "+B$+"; VE = "+C$
3500 GOSUB650
3510 I=I+1:PV=PV+DV
3520 NEXTVE
3530 I=2
3540 VE=I*C+.2/VG
3550 SR=I+1:CN=0:CT=2:GOSUB310:A$=SR$
3560 SR=I*DV:CN=0:CT=3:GOSUB310:B$=SR$
3570 SR=VE:CN=3:CT=5:GOSUB310:C$=SR$
3580 CN=1
3590 SR$="Regel "+A$+"; PV = "+B$+"; VE = "+C$
3600 GOSUB650
3610 VR$="Nog een andere DV (J/n) ?"
3620 VE=I*C:SR$=VR$:CN=0:GOSUB650
3630 GOSUB7110
3650 IF(IN=13)OR(IN=74) THEN3300
3700 GOSUB100
3710 PRINT"De gevonden waarden kunnen uitgeprint":PRINT
3720 PRINT"worden.":PRINT:PRINT
3730 PRINT"Is dat nodig (J/n) ?":PRINT:PRINT
3740 GOSUB7110
3750 IF(IN=13)OR(IN=74) THEN3800
3760 GOSUB7010
3770 RETURN
3800 GOSUB6310
3860 I=1:C=DV/VG:PV=0
3870 FORVE=.2/VG TO1-(DV-1)/VG STEPC
3880 SR=I:CN=0:CT=2:GOSUB310:A$=SR$
3890 SR=PV:CN=0:CT=3:GOSUB310:B$=SR$
3900 SR=VE:CN=3:CT=5:GOSUB310:C$=SR$
3920 SR$=SP$+"Regel "+A$+"; PV = "+B$+"; VE = "+C$
3930 GOSUB350:GOSUB360
3940 I=I+1:PV=PV+DV
3950 NEXTVE
3960 RETURN
4000 REM **> ONTWERPEN KARAKTER <**
4010 GOSUB100
4020 PRINT" **> ONTWERPEN KARAKTER <**":PRINT
4030 PRINT"Nog uitleg ? (J/n) ?"
4040 GOSUB7110
4050 IF(IN=13)OR(IN=74) THEN4080
4060 GOTO4310
4080 HO=0:VE=2:GOSUB110
4090 PRINT"Met dit deel ontwerpen we op het":PRINT
4100 PRINT"tekst-scherm een karakter.":PRINT
4110 PRINT"Daarna kijken we op het grafisch-":PRINT
4120 PRINT"scherm hoe de karakter er uit ziet.":PRINT:PRINT
4130 GOSUB7010
4140 PRINT"De karakter maken we in een lege":PRINT
4150 PRINT"matrix waarvan we DH en DV eerst":PRINT
4160 PRINT"moeten opgeven.":PRINT:PRINT
4170 PRINT"Neem DH en DV niet kleiner dan 5":PRINT
4180 PRINT"en niet groter dan 15."
4190 GOSUB7010:GOSUB100
4200 PRINT"De matrix wordt van links naar rechts":PRINT
4210 PRINT"en van boven naar onder gevuld met":PRINT
4220 PRINT"nullen en enen.":PRINT:PRINT
4230 GOSUB7010
4240 PRINT"Op de punten waar een dot moet komen":PRINT
4250 PRINT"zetten we een 1, op de andere punten":PRINT
4260 PRINT"zetten we een 0.":PRINT:PRINT
4270 PRINT"Maak eerst een schets van de karakter,":PRINT
4280 PRINT"dat werkt een stuk sneller."
4290 GOSUB7010
4300 REM **> OPGEVEN DH EN DV <**
4310 GOSUB100
4320 VR$="Voer waarde voor DH in (5-15) : "
4330 HO=0:VE=0:GOSUB110
4340 GL$="0":GH$="9":AL$="5":AH$="15"
4350 GOSUB7210
4360 DH=VAL(AW$)
4370 VR$="Voer waarde voor DV in (5-15) : "
4380 HO=0:VE=3:GOSUB110
4390 GL$="0":GH$="9":AL$="5":AH$="15"
4400 GOSUB7210
4410 DV=VAL(AW$)
4420 HO=0:VE=5:GOSUB110
4430 PRINT"De lege matrix komt nu op het scherm":PRINT
4440 PRINT"met in LB een ster.":PRINT:PRINT
4450 PRINT"SUCCES."
4460 GOSUB7010
4500 REM **> MATRIX OMLIJSTEN <**
4510 GOSUB100
4520 DIMKM(DH-1,DV-1)
4530 VE=0
4540 FORHO=0 TODH+1:GOSUB110
4550 PRINT"+":NEXTHO
4560 HO=DH+1
4570 FORVE=1 TODV+1:GOSUB110
4580 PRINT"+":NEXTVE
4590 VE=DV+1
4600 FORHO=DH TO0 STEP-1:GOSUB110
4610 PRINT"+":NEXTHO
4620 HO=0
4630 FORVE=DV+1 TO1 STEP-1:GOSUB110
4640 PRINT"+":NEXTVE
4650 HO=0:VE=DV+2:GOSUB110
4660 PRINT"Type een 1 of een 0 in.":PRINT
4670 PRINT"Verbeteren met de verbetertoets."
4700 REM **> MATRIX INVULLEN <**
4710 FORVE=1 TODV
4720 FORHO=1 TODH
4730 GOSUB110:PRINT"*";
4740 GOSUB7110
4750 IF(IN=48)OR(IN=49) THEN4780
4760 IFIN=127 THEN GOSUB6010
4770 GOTO4730
4780 GOSUB110:PRINTCHR$(IN);
4790 IF(HO=DH)AND(VE=DV) THEN GOSUB6110
4800 NEXTHO
4810 NEXTVE
4820 HO=0:VE=DV+2:GOSUB110
4830 PRINT"Een ogenblik geduld a.u.b.":PRINT
4840 PRINT"Even KM(DH-1,DV-1) vullen.      "
5000 REM **> ARRAY VULLEN <**
5010 FORI=0 TODV-1
5020 FORJ=0 TODH-1
5030 HO=1+J:VE=1+I
5040 GOSUB220
5050 KM(J,I)=IN-48
5060 NEXTJ
5070 NEXTI
5100 REM **> KARAKTER OP HET TEKST SCHERM <**
5110 GOSUB100
5120 FORI=0 TODV-1
5130 FORJ=0 TODH-1
5140 A=KM(J,I)
5150 IFA=0 THENKR$=" "
5160 IFA=1 THENKR$="*"
5170 HO=1+J:VE=1+I
5180 GOSUB110
5190 PRINTKR$
5200 NEXTJ
5210 PRINT
5220 NEXTI
5240 HO=0:VE=DV+2:GOSUB110
5250 PRINT"Zo ziet de karakter er uit.";
5260 GOSUB7010:GOSUB110
5280 PRINT"Moet dit scherm op de printer (J/n) ?";
5290 GOSUB7110
5300 IF(IN<>13)AND(IN<>74) THEN5320
5310 GOSUB6310:GOSUB6410
5320 GOSUB110
5330 PRINT"Nu komt het karakter op het grafisch-":PRINT
5340 PRINT"scherm."
5350 GOSUB7010
5360 GOSUB600:CN=0
5370 H=.2:V=.2:GOSUB5670
5380 GOSUB7610
5390 GOSUB600:GOSUB100
5400 HO=0:VE=6:GOSUB110
5410 PRINT"Moet het hele scherm met":PRINT
5420 PRINT"karakters worden gevuld (J/n) ?":PRINT
5430 GOSUB7110
5440 IF(IN=13)OR(IN=74) THEN5480
5450 PRINT"BEDANKT VOOR DE BELANGSTELLING !!!!"
5460 GOSUB7010
5470 GOTO1000:REM >> OPNIEUW STARTEN VOOR ARRAY
5480 PRINT"Ga er dan maar even voor zitten.":PRINT
5490 GOSUB7010
5500 PRINT"Ik bedoelde eigenlijk 'ga er dan":PRINT
5510 PRINT"een lange tijd' voor zitten":PRINT
5520 PRINT"Stoppen met de S-toets."
5530 GOSUB7010
5540 GOSUB600
5550 FORV=.2/VG TO1-(DV-1)/VG STEPDV/VG
5560 FORH=.2/HG TO1-(DH-1)/HG STEPDH/HG
5570 GOSUB200:IF(IN$="S")OR(IN$="s") THEN5610
5580 GOSUB5670
5590 NEXTH
5600 NEXTV
5610 GOSUB7610:GOSUB600
5620 GOSUB100
5630 PRINT:PRINT:PRINT
5640 GOTO5450:REM >> NAAR EINDE ONDERDEEL
5650 REM
5660 REM **> KARAKTER OP GRAFISCH SCHERM <**
5670 FORI=0 TODV-1
5680 FORJ=0 TODH-1
5690 A=KM(J,I)
5700 IFA=1 THENCN=0
5710 IFA=0 THEN5740
5720 HO=H+J/HG:VE=V+I/VG
5730 GOSUB620
5740 NEXTJ
5750 NEXTI
5760 RETURN
6000 REM ***> VERBETEREN <***
6010 GOSUB110:PRINT" "
6020 IF(HO=1)AND(VE=1) THENRETURN
6030 IFHO=1 THEN6060
6040 HO=HO-1
6050 RETURN
6060 HO=DH
6070 VE=VE-1
6080 RETURN
6100 REM ***> LAATSTE DOT <***
6110 HT=HO:VT=VE
6120 HO=0:VE=23:GOSUB110
6130 PRINT"Is de laatste dot goed (J/n) ? ";
6140 GOSUB7110
6150 IF(IN=13)OR(IN=74) THEN6190
6160 GOSUB110:PRINT"                                  ";
6170 HO=HT:VE=VT:GOSUB6040
6180 RETURN
6190 GOSUB7010
6200 HO=HT:VE=VT
6210 RETURN
6300 REM **> VRAGEN OM SPATIES <**
6310 GOSUB100
6320 PRINT"Hoeveel spaties moeten er aan de":PRINT
6330 PRINT"linker zijkant komen ";:INPUTSL
6340 SP$="":IFSL=0 THEN6380
6350 FORI=1 TOSL
6360 SP$=SP$+" "
6370 NEXTI
6380 VE=6:GOSUB110
6390 RETURN
6400 REM **> KARAKTER NAAR PRINTER <**
6410 A$=""
6420 FORI=0 TODV-1
6430 FORJ=0 TODH-1
6440 A=KM(J,I)
6450 IFA=0 THENKR$=" "
6460 IFA=1 THENKR$="*"
6470 A$=A$+KR$
6480 IFJ<>DH-1 THEN6510
6490 SR$=SP$+A$
6500 GOSUB350:GOSUB360:A$=""
6510 NEXTJ
6520 NEXTI
6530 GOSUB7010
6540 RETURN
7000 REM *****> SPATIEBALK <*****
7010 GOSUB120:VI=VE
7020 VE=23:HO=0:GOSUB110
7030 PRINT"****>   Druk op de spatiebalk   <****";
7040 GOSUB7110:IFIN<>32 THEN7040
7050 VE=23:HO=0:GOSUB110
7060 PRINT"                                     ";
7070 VE=VI:GOSUB110
7080 RETURN
7100 REM **> INPUT MET LEGE BUFFER <**
7110 GOSUB200:IFIN$<>"" THEN7110
7120 GOSUB210
7130 RETURN
7200 REM **> INPUT GETAL <**
7210 HI=HO:VI=VE:AL=VAL(AL$):AH=VAL(AH$)
7220 AW$=""
7230 PRINTVR$;
7240 GOSUB7110
7250 IF(AW$="")AND((IN=13)OR(IN=127)) THEN7240
7260 IFIN=127 THEN7360
7270 IFIN=13 THEN7340
7280 IF(IN$>=GL$)AND(IN$<=GH$) THEN7300
7290 GOTO7240
7300 AW$=AW$+IN$:AW=VAL(AW$)
7310 HO=LEN(VR$):GOSUB110:PRINTAW$;
7320 IFLEN(AW$)>4 THEN7400
7330 GOTO7240
7340 IFNOT((AW>=AL)AND(AW<=AH)) THEN7400
7350 RETURN
7360 L=LEN(AW$):AW$=LEFT$(AW$,L-1)
7370 HO=LEN(VR$+AW$):GOSUB110:PRINT" ";
7380 AW=VAL(AW$)
7390 GOTO7240
7400 GOSUB7010
7410 HO=HI:VE=VI:GOSUB110:GOSUB7480
7420 SR=VAL(AW$):GOSUB300
7430 HO=HI:VE=VI:GOSUB110
7440 PRINTSR$+" is niet toegestaan.";
7450 GOSUB7010
7460 HO=HI:VE=VI:GOSUB110:GOTO7220
7470 RETURN
7480 FORI=1 TOLEN(VR$+AW$)
7490 PRINT" ";
7500 NEXTI
7510 RETURN
7600 REM ****> SPATIEBALK GRAF <****
7610 CI=CN:HI=HO:VI=VE
7620 HO=0:VE=23*.04
7630 SR$=">>>> SPATIEBALK !!!! >>>>"
7640 CN=0:GOSUB650
7650 GOSUB7110
7660 IFIN<>32 THEN7650
7670 CN=1:GOSUB650
7680 CN=CI:HO=HI:VE=VI
7690 RETURN
30000 REM********************************
30010 REM* KARAKTERS OP GRAFISCH SCHERM *
30020 REM*                              *
30030 REM*  EEN DOE EN TEST PROGRAMMA   *
30040 REM*                              *
30050 REM*           VAN                *
30060 REM*                              *
32000 REM*    J.WIELES                  *
32010 REM*    HOOFDWAL 68               *
32020 REM*    4207 EE GORINCHEM         *
32030 REM*                              *
32040 REM*    FEBRUARI 1989             *
32050 REM*                              *
32060 REM********************************
32070 REM
32080 REM TROS-RADIO dd 890607

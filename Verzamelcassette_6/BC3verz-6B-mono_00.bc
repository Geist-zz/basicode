
1000 A=200:GOTO20:REM ASSENSTELSEL
1010 XM=.5:YM=.45:DH=.03125
1020 DIMCX(16),CY(12)
1030 FR=0:GOSUB280
1040 GOTO4510
1100 REM ***> TEKST GRAFISCH-SCHERM <***
1110 HR=HO:RR$=SR$
1120 LS=LEN(RR$)
1130 FORL=1 TOLS
1140 SR$=MID$(RR$,L,1)
1150 GOSUB650
1160 HO=HO+DH
1170 NEXTL
1180 HO=HR:SR$=RR$
1190 RETURN
1200 REM ****> DATA LEZEN  <*****
1210 HO=0:VE=0:RS=.06
1220 GOSUB600:CN=0:RESTORE
1230 READDA$:IFDA$<>PR$ THEN1230
1240 READDA$
1250 IFDA$="einde" THEN1310
1260 IFDA$="ro" THENVE=VE+RS:GOTO1240
1270 IFDA$="sp" THEN GOSUB3510:GOTO1240
1280 SR$=DA$:GOSUB1110
1290 VE=VE+RS
1300 GOTO1240
1310 RETURN
1400 REM
1410 REM ********> STER <*********
1420 HO=HT-.015:VE=VT-.015:GOSUB620
1430 HO=HT+.015:VE=VT+.015:GOSUB630
1440 HO=HT:VE=VT-.015:GOSUB620
1450 HO=HT:VE=VT+.015:GOSUB630
1460 HO=HT+.015:VE=VT-.015:GOSUB620
1470 HO=HT-.015:VE=VT+.015:GOSUB630
1480 HO=HT-.015:VE=VT:GOSUB620
1490 HO=HT+.015:VE=VT:GOSUB630
1500 RETURN
1600 REM ****> KNIPPERENDE STER <****
1610 CI=CN
1620 FORK=1 TO10:REM KEREN KNIPPEREN
1630 CN=1:GOSUB1420
1640 CN=0:GOSUB1420
1650 NEXTK
1660 CN=CI
1670 RETURN
1700 REM ***> KNIPPERENDE TEKST <****
1710 CI=CN
1720 FORK=1 TO3:REM KEREN KNIPPEREN
1730 CN=1:GOSUB1110
1740 CN=0:GOSUB1110
1750 NEXTK
1760 CN=CI
1770 RETURN
1800 REM ******> +X AS <******
1810 HR=.99:HS=.06:TK$="+"
1820 GOSUB2010
1830 RETURN
1900 REM ******> -X AS <******
1910 HR=.01:HS=-.06:TK$="-"
1920 GOSUB2010
1930 RETURN
2000 REM ***> HULP ROUTINE X-ASSEN <***
2010 HO=XM:VE=YM:GOSUB620
2020 HO=HR:GOSUB630
2030 FORI=0 TO8
2040 HO=XM+HS*I
2050 VE=YM-3/VG:GOSUB620
2060 VE=YM+3/VG:GOSUB630
2070 NEXTI
2080 IFGT=0 THEN2170
2090 REM >---> GETALLEN <---<
2100 VE=YM+5/VG:J=50
2110 FORI=1 TO4
2120 HO=XM+2*HS*I-9/HG
2130 SR$=TK$+CHR$(J)
2140 GOSUB650
2150 J=J+2
2160 NEXTI
2170 RETURN
2200 REM *******> +Y AS <*********
2210 VR=0:VS=-.07:TK$="+"
2220 GOSUB2310
2230 RETURN
2240 REM *******> -Y AS <*********
2250 VR=.9:VS=.07:TK$="-"
2260 GOSUB2310
2270 RETURN
2300 REM ***> HULP ROUTINE Y-ASSEN <****
2310 HO=XM:VE=YM
2320 GOSUB620
2330 VE=VR:GOSUB630
2340 FORI=0 TO6
2350 VE=YM+I*VS
2360 HO=XM-3/HG:GOSUB620
2370 HO=XM+3/HG:GOSUB630
2380 NEXTI
2390 IFGT=0 THEN2480
2400 REM >---> GETALLEN <----<
2410 HO=XM+5/HG:J=49
2420 FORI=0 TO2
2430 VE=YM+VS+2*VS*I-4/VG
2440 SR$=TK$+CHR$(J)
2450 GOSUB650
2460 J=J+2
2470 NEXTI
2480 RETURN
2500 REM ****> ASSENSTELSEL <*****
2510 GOSUB600
2520 GOSUB1810
2530 GOSUB1910
2540 GOSUB2210
2550 GOSUB2250
2560 RETURN
2600 REM
2610 REM << +Y-AS WISSEN >>
2620 CI=CN:CN=1
2630 GT=1:GOSUB2210
2640 CN=CI:RETURN
2650 REM << -Y-AS WISSEN >>
2660 CI=CN:CN=1
2670 GT=1:GOSUB2250
2680 CN=CI:RETURN
2700 REM ***> RECHTS OF LINKS <***
2710 IFX=0 THEN2790
2720 HI=HO:VI=VE
2730 I=.005:IFY<0 THENI=-I
2740 J=.0025:IFX<0 THENJ=-J
2750 FORHO=XM TOHT STEPJ
2760 VE=YM-I:GOSUB620
2770 NEXTHO
2780 HO=HI:VE=VI
2790 RETURN
2800 REM ***> OMHOOG OF OMLAAG <***
2810 IFY=0 THEN2880
2820 HI=HO:VI=VE
2830 I=.001:IFX<0 THENI=-I
2840 J=.001:IFY<0 THENJ=-J
2850 FORVE=YM TOVT STEP-J
2860 HO=HT+J:GOSUB620
2870 NEXTVE
2880 IFX=0 THEN2910
2890 HO=XM:VE=VT:GOSUB630
2900 HO=HI:VE=VI
2910 RETURN
3000 REM << NOTATIE COORDINATEN >>
3010 SR=X:GOSUB300:X$=SR$
3020 SR=Y:GOSUB300:Y$=SR$
3030 CO$="("+X$+","+Y$+")"
3040 RETURN
3050 REM << POSITIE COORDINATEN >>
3060 HT=XM+X*.06
3070 VT=YM-Y*.07
3080 RETURN
3100 REM
3110 REM << X EN Y ARRAY'S VULLEN >>
3120 FORI=0 TO16
3130 CX(I)=(-8+I)
3140 NEXTI
3150 FORI=0 TO12
3160 CY(I)=(-6+I)
3170 NEXTI
3180 RETURN
3200 REM
3210 REM ***> RANDOM COORDINATEN <***
3220 GOSUB260:AX=INT((RV*NX)+.5)
3230 X=CX(AX)
3240 CX(AX)=CX(NX)
3250 GOSUB260:AY=INT((RV*NY)+.5)
3260 Y=CY(AY)
3270 CY(AY)=CY(NY)
3280 NX=NX-1:NY=NY-1
3290 GOSUB3010:GOSUB3060
3300 RETURN
3400 REM
3410 REM *****> BUFFERLEEG <*****
3420 GOSUB200:IFIN<>0 THEN3420
3430 GOSUB210:A=IN
3440 RETURN
3500 REM ****> SPATIEBALK GRAF <****
3510 CI=CN:HS=HO:VS=VE
3520 HO=0:VE=1-10/VG
3530 SR$="   >>>> SPATIEBALK !!!! >>>>"
3540 CN=0:GOSUB1110
3550 GOSUB3420
3560 IFA<>32 THEN3550
3570 CN=1:GOSUB1110:CN=CI
3580 CN=CI:HO=HS:VE=VS
3590 RETURN
3600 REM ***> GETALLEN INVOEREN <***
3610 AW$="":GL=VAL(GL$):GH=VAL(GH$):GT$=LEFT$(GL$,1)
3620 LV=LEN(VR$)
3630 FL=0:IF(GT$="-")OR(GT$="+") THENFL=1
3640 SR$=VR$:GOSUB1110
3650 GOSUB3420
3660 IF(AW$="")AND(FL=1) THEN3820
3670 IFA=127 THEN4120
3680 IFA=13 THEN4210
3690 IF(VAL(IN$)=0)AND(IN$<>"0") THEN3650
3700 IF(VAL(AW$)=0)AND(IN$="0") THEN3650
3710 AW=VAL(AW$+IN$)
3720 IF(AW<GL)OR(AW>GH) THEN3650
3730 AW$=AW$+IN$
3740 GOTO4020
3800 REM 
3810 REM >>> TEKEN OF EERSTE CIJFER <<<
3820 IF(IN$="+")OR(IN$="-") THEN3910
3830 AW=VAL(IN$)
3840 IF(AW=0)AND(IN$<>"0") THEN3650
3850 IF(AW<GL)OR(AW>GH) THEN3650
3860 AW$=" "+IN$
3870 GOTO4020
3900 REM >>> AW$ VULLEN MET - OF + <<<
3910 IFFL=0 THEN3650
3920 AW$=IN$
3930 GOTO4020
4000 REM 
4010 REM >>> PRINT AW$ <<<
4020 HO=HI+LV*DH:SR$=AW$
4030 GOSUB650
4040 GOTO3650
4100 REM
4110 REM >>> VERBETER-TOETS <<<
4120 IFAW$="" THEN3650
4130 HO=HI+LV*DH:SR$=AW$
4140 CN=1:GOSUB650:CN=0
4150 AW$="":HO=HI
4160 GOTO3640
4200 REM >>> INVOER-TOETS <<<
4210 AW=VAL(AW$)
4220 IF(AW=0)AND(RIGHT$(AW$,1)<>"0") THEN3650
4230 RETURN
4300 REM ***> AANTAL GOED <***
4310 GOSUB600
4320 SR=G1:GOSUB300:G1$=SR$
4330 SR$="Je had er "+G1$+" goed."
4340 HO=0:VE=0:GOSUB1110
4350 GS$="Nog eens wat oefenen."
4360 GV$="Dat ging aardig."
4370 GG$="Dat is knap gedaan."
4380 GT$="Je bent een BOLLEBOOS."
4390 IFG1<6 THENSR$=GS$
4400 IF(G1=6)OR(G1=7) THENSR$=GV$
4410 IF(G1=8)OR(G1=9) THENSR$=GG$
4420 IFG1=10 THENSR$=GT$
4430 VE=.16:GOSUB1110
4440 GOSUB3510
4450 RETURN
4500 REM ****> MENU <****
4510 PR$="menu":GOSUB1210
4520 REM ****> VERWERKEN KEUZE <****
4530 VI=VE:GOSUB3420:TY=A-48
4540 IF(TY<1)OR(TY>8) THEN4530
4550 SR=TY:GOSUB300:TY$=SR$
4560 VE=VI+.06:SR$="Je keuze is : "+TY$
4570 GOSUB1110
4580 VE=VE+.06:SR$="Is dat de goede keuze (J/n) ?"
4590 GOSUB1110
4600 GOSUB3420
4610 IF(IN<>13)AND(IN<>74) THEN4510
4620 IFTY=8 THEN4650
4630 ONTY GOSUB4710,4810,5310,5810,6210,6910,7310
4640 GOTO4510
4650 GOTO950
4700 REM *****> HET PROGRAMMA <*****
4710 PR$="programma":GOSUB1210
4720 RETURN
4730 REM
4800 REM  ****> UITLEG ASSEN <****
4810 GOSUB600:CN=0
4820 GT=1:GOSUB1810
4830 VE=YM+5/VG:HO=XM-3/HG
4840 SR$="0":GOSUB650
4850 HO=XM+DH:VE=.38:SR$="Hoi, ik ben de"
4860 GOSUB1110
4870 VE=.54:SR$="POSITIEVE X-AS"
4880 GOSUB1710
4890 GT=1:GOSUB1910
4900 HO=0:VE=.38:SR$="en ik ben de"
4910 GOSUB1110
4920 VE=.54:SR$="NEGATIEVE X-AS"
4930 GOSUB1710
4940 GOSUB3510
4950 GOSUB600
4960 GT=1:GOSUB2210
4970 HO=XM+6/HG:VE=YM-4/VG
4980 SR$="0":GOSUB650
4990 HO=XM+DH:VE=.14:SR$="Ik ben de"
5000 GOSUB1110
5010 VE=.28:SR$="POSITIEVE Y-AS"
5020 GOSUB1710
5030 GT=1:GOSUB2250
5040 HO=XM+DH:VE=.56:SR$="en ik ben de"
5050 GOSUB1110
5060 VE=.7:SR$="NEGATIEVE Y-AS"
5070 GOSUB1710
5080 HO=0:VE=.68:SR$="AANGENAAM !"
5090 GOSUB1110
5100 GOSUB3510
5110 GT=0:GOSUB2510
5120 HO=XM+DH:VE=YM+.04:SR$="POSITIEVE X-AS"
5130 GOSUB1110
5140 HO=XM+DH:VE=.12:SR$="POSITIEVE Y-AS"
5150 GOSUB1110
5160 HO=0:VE=YM+.04:SR$="NEGATIEVE X-AS"
5170 GOSUB1110
5180 HO=XM+DH:VE=.68:SR$="NEGATIEVE Y-AS"
5190 GOSUB1110
5200 HO=0:VE=0:SR$="Samen vormen":GOSUB1110
5210 VE=.1:SR$=" wij een":GOSUB1110
5220 VE=.2:SR$="ASSENSTELSEL.":GOSUB1710
5230 VE=.8:SR$="NEEM ONS OVER":GOSUB1110
5240 GOSUB3510
5250 RETURN
5300 REM  ****> UITLEG COORDINATEN <****
5310 NX=16:NY=12
5320 GT=1:GOSUB2510
5330 X=0:Y=0:GOSUB3010:GOSUB3060
5340 GOSUB1610
5350 GOSUB2660
5360 HO=0:VE=.6
5370 SR$="Ik ben de OORSPRONG.":GOSUB1110
5380 VE=VE+.08:SR$="Ze noemen mij O.":GOSUB1110
5390 VE=VE+.08:SR$="Mijn CO-ORDINATEN zijn: "+CO$
5400 GOSUB1110
5410 GOSUB3510
5420 X=8:Y=3:GOSUB5510
5430 X=-8:Y=3:GOSUB5510
5440 X=-8:Y=-3:GOSUB5510
5450 X=8:Y=-3:GOSUB5510
5460 GOSUB3220
5470 REM ****> RESUME <****
5480 PR$="resume":GOSUB1210
5490 RETURN
5500 REM >>> HULP ROUTINE COOR <<<
5510 GT=1:GOSUB2510
5520 GOSUB3010:GOSUB3060
5530 GOSUB1610
5540 SR=ABS(X):GOSUB300:AX$=SR$
5550 SR=ABS(Y):GOSUB300:AY$=SR$
5560 IFY>0 THEN GOSUB2660:GOTO5580
5570 GOSUB2620
5580 IFY>0 THENHO=0:VE=.52:GOTO5600
5590 HO=0:VE=0
5600 SR$="Mijn CO-ORDINATEN zijn "+CO$:GOSUB1110
5610 VE=VE+.08
5620 SR$="Dat betekent dat je vanuit O":GOSUB1110
5630 IFX<0 THEN5670
5640 VE=VE+.08:SR$="eerst "+AX$+" naar RECHTS en dan"
5650 GOSUB1110
5660 GOTO5690
5670 VE=VE+.08:SR$="eerst "+AX$+" naar LINKS en dan"
5680 GOSUB1110
5690 GOSUB2710
5700 IFY<0 THEN5730
5710 VE=VE+.08:SR$=AY$+" OMHOOG":GOSUB1110
5720 GOTO5740
5730 VE=VE+.08:SR$=AY$+" OMLAAG":GOSUB1110
5740 GOSUB2810
5750 VE=VE+.08:SR$="moet om mij te bereiken.":GOSUB1110
5760 GOSUB3510
5770 RETURN
5800 REM  ***> VRAGEN COORDINATEN <***
5810 PR$="vragen-1":GOSUB1210
5820 GOSUB3120:REM ARRAY'S VULLEN
5830 G1=0:GS=1:NX=16:NY=12
5840 FORBT=1 TO10
5850 GT=1:GOSUB2510
5860 SR=BT:GOSUB300:BT$=SR$
5870 GOSUB3220:REM RANDOM COORDINATEN
5880 HI=XM+DH:IFX>0 THENHI=0
5890 HO=HI:VE=0:SR$="BEURT "+BT$:GOSUB1110
5900 GOSUB1610
5910 HO=HI:VE=YM+.1:SR$="Voer maar in"
5920 GOSUB1110
5930 HO=HI:VE=YM+.18:GL$="-8":GH$="+8"
5940 VR$="  X is : "
5950 GOSUB3610:BX=AW
5960 HO=HI:VE=VE+.08:GL$="-6":GH$="+6"
5970 VR$="  Y is : "
5980 GOSUB3610:BY=AW
5990 IF(BX=X)AND(BY=Y) THEN6050
6000 HO=HI:VE=VE+.08:SR$="  FOUT !!!!"
6010 GOSUB1110
6020 VE=VE+.06:SR$="HET IS "+CO$
6030 GOSUB1110:GOSUB2710:GOSUB2810
6040 GOTO6080
6050 G1=G1+1
6060 HO=HI:VE=VE+.08:SR$="  GOED ZO !!!!"
6070 GOSUB1110
6080 GOSUB3510
6090 NEXTBT
6100 GOSUB4310:REM AANTAL GOED
6110 RETURN
6200 REM  ***> UITLEG KWADRANTEN <***
6210 GT=1:GOSUB2510
6220 X=6:Y=6:GOSUB6510
6230 X=-6:Y=6:GOSUB6510
6240 X=-6:Y=-6:GOSUB6510
6250 X=6:Y=-6:GOSUB6510
6260 GOSUB3510
6270 GT=0:GOSUB2510
6280 HO=XM:VE=YM-.28:SR$="    EERSTE":GOSUB1110
6290 VE=VE+.08:SR$="    KWADRANT":GOSUB1110
6300 X=8:Y=6:GOSUB3060:GOSUB6710
6310 HO=XM:VE=YM-.1:SR$="    X>0 EN Y>0":GOSUB1110
6320 HO=0:VE=YM-.28:SR$="    TWEEDE":GOSUB1110
6330 VE=VE+.08:SR$="    KWADRANT":GOSUB1110
6340 X=-8:Y=6:GOSUB3060:GOSUB6710
6350 HO=0:VE=YM-.1:SR$="    X<0 EN Y>0":GOSUB1110
6360 VE=YM+.16:SR$="    DERDE":GOSUB1110
6370 VE=VE+.08:SR$="    KWADRANT":GOSUB1110
6380 X=-8:Y=-6:GOSUB3060:GOSUB6710
6390 HO=0:VE=YM+.08:SR$="    X<0 EN Y<0":GOSUB1110
6400 HO=XM:VE=YM+.16:SR$="    VIERDE":GOSUB1110
6410 VE=VE+.08:SR$="    KWADRANT":GOSUB1110
6420 X=8:Y=-6:GOSUB3060:GOSUB6710
6430 HO=XM:VE=YM+.08:SR$="    X>0 EN Y<0":GOSUB1110
6440 HO=0:VE=.82:SR$="NEEM DIT OVER"
6450 GOSUB1110
6460 GOSUB3510
6470 RETURN
6480 REM 
6500 REM ****> HULP ROUTINE KWADRANTEN <****
6510 GOSUB3060:GOSUB3010
6520 GOSUB1610
6530 HO=0:IFX>0 THENHO=XM+.06
6540 VE=0:IFY<0 THENVE=YM
6550 VE=VE+.12:SR$=" Ik ben "+CO$
6560 GOSUB1110
6570 VE=VE+.08:SR$=" en lig in het"
6580 GOSUB1110
6590 VE=VE+.08:IFX>0 THENHO=XM+.03
6600 IF(X>0)AND(Y>0) THENSR$="EERSTE KWADRANT"
6610 IF(X>0)AND(Y<0) THENSR$="VIERDE KWADRANT"
6620 IF(X<0)AND(Y>0) THENSR$="TWEEDE KWADRANT"
6630 IF(X<0)AND(Y<0) THENSR$="DERDE KWADRANT"
6640 GOSUB1710
6650 RETURN
6700 REM ***> PLUSSEN EN MINNEN <****
6710 HI=HO:VI=VE
6720 REM >>> LANGS DE X-AS <<<
6730 XA$="+":IFX<0 THENXA$="-"
6740 YA$="+":IFY<0 THENYA$="-"
6750 I=8/HG:IFX<0 THENI=-10/HG
6760 J=10/VG:IFY<0 THENJ=-4/VG
6770 REM >>>> LANGS DE X-AS <<<<
6780 FORHO=XM+I TOHT STEPI
6790 VE=YM-J:SR$=XA$:GOSUB650
6800 NEXTHO
6810 REM >>>> LANGS DE Y-AS <<<<
6820 FORVE=YM-J TOVT-4/VG STEP-J
6830 HO=XM+I:SR$=YA$:GOSUB650
6840 NEXTVE
6850 HO=HI:VE=VI
6860 RETURN
6900 REM ***> KWADRANTEN VRAGEN <***
6910 PR$="vragen-2":GOSUB1210
6920 GOSUB600:G1=0:K1=0:K2=0
6930 FORKV=0 TO9
6940 VE=KV*.06
6950 GOSUB260:X=INT(RV*100)-50
6960 GOSUB260:Y=INT(RV*100)-50
6970 IF(X=0)OR(Y=0) THEN6950
6980 IF(X<0)AND(Y<0) THENKW=3:GOTO7020
6990 IF(X<0)AND(Y>0) THENKW=2:GOTO7020
7000 IF(X>0)AND(Y>0) THENKW=1:GOTO7020
7010 KW=4
7020 IF(KW=K1)OR(KW=K2) THEN6950
7030 K2=K1:K1=KW:GOSUB3010
7040 REM >>>> OP SCHERM <<<<<
7050 HO=0:SR$=CO$:GOSUB650:L1=6*DH
7060 HO=L1:SR$="   Welk KWADRANT ?":L2=LEN(SR$)*DH
7070 GOSUB1110:VR$="  "
7080 GL$="1":GH$="4":HI=L1+L2
7090 GOSUB3610:L3=LEN(VR$+AW$)*DH
7100 IFAW=KW THEN GOSUB7210:G1=G1+1:GOTO7120
7110 HO=L1+L2+L3:SR$=" FOUT":GOSUB650
7120 NEXTKV
7130 GOSUB3510
7140 GOSUB4310
7150 RETURN
7200 REM **> GOED MET ANTWOORD WEG <**
7210 HO=L1+L2+L3:SR$=" GOED":GOSUB650
7220 HO=0:CN=1:SR$=CO$:GOSUB650:CN=0
7230 RETURN
7300 REM ******> ZOEK SPEL <***********
7310 GOSUB3120
7320 PR$="zoekspel":GOSUB1210
7330 GOSUB600
7340 GT=0:GOSUB2510
7350 NX=16:NY=12:GOSUB3220
7360 PO=1:X1=X:Y1=Y:C$=CO$
7370 CN=0:GOSUB7810:REM >>> PRINT POGING
7380 HI=0:VI=0
7390 HO=HI:VE=VI+.16:GL$="-8":GH$="+8"
7400 VR$="  X is : ":GOSUB3610
7410 X$=AW$:X=AW
7420 HO=HI:VE=VE+.08:GL$="-6":GH$="+6"
7430 VR$="  Y is : ":GOSUB3610
7440 Y$=AW$:Y=AW
7450 GOSUB3010
7460 IF(X=X1)AND(Y=Y1) THEN7550
7470 IFPO=1 THEN7490
7480 XT=X2:YT=Y2:CT$=C2$:CN=1:GOSUB8110
7490 XT=X:YT=Y:CT$=CO$:CN=0:GOSUB8110
7500 CN=1:GOSUB7810:GOSUB7910
7510 PO=PO+1
7520 IFPO=9 THEN7650
7530 X2=X:Y2=Y:C2$=CO$
7540 GOTO7370
7550 GOSUB600:HO=0:VE=0
7560 SR$="Goed zo !! Het was "+C$:GOSUB1110
7570 SR=PO:GOSUB300:PO$=SR$:VE=.08
7580 SR$="Je had "+PO$+" pogingen nodig.":GOSUB1110
7590 IFPO=1 THENSR$="Jij bent een gedachten lezer !!"
7600 IF(PO=2)OR(PO=3) THENSR$="Dat is super snel."
7610 IF(PO=4)OR(PO=5) THENSR$="Dat is gaaf snel."
7620 IFPO>5 THENSR$="Was dat even lastig zeg."
7630 VE=.16:GOSUB1110
7640 VE=.24:GOTO7680
7650 GOSUB600:HO=0:VE=0:CN=0
7660 SR$="Dat was je achtste beurt.":GOSUB1110
7670 VE=.08:SR$="Het was "+C$:GOSUB1110:VE=.16
7680 SR$="Wil je nog een spel (J/n) ?"
7690 GOSUB1110
7700 GOSUB3420
7710 IF(IN=13)OR(IN=74) THEN7340
7720 RETURN
7800 REM ***> ZET POGING <***
7810 SR=PO:GOSUB300:PO$=SR$
7820 HO=0:VE=0:SR$="Poging "+PO$
7830 GOSUB1110
7840 VE=.08:SR$="Voer maar in":GOSUB1110
7850 RETURN
7860 REM
7900 REM **> VRAGEN WEG <**
7910 HO=0:VE=.16
7920 CN=1:SR$="  X is : ":GOSUB1110
7930 FORHO=LV*DH TO(LV+2)*DH STEP1/HG
7940 VE=.16:GOSUB620
7950 VE=.2:GOSUB630:NEXTHO
7960 HO=0:VE=.24:SR$="  Y is : ":GOSUB1110
7970 FORHO=LV*DH TO(LV+2)*DH STEP1/HG
7980 VE=.24:GOSUB620
7990 VE=.28:GOSUB630:NEXTHO
8000 RETURN
8100 REM ***> ZET FOUT <***
8110 HO=0:VE=.52:SR$=CT$+" IS FOUT.":GOSUB1110
8120 VE=.6:SR$="JE MOET :":GOSUB1110
8130 IFXT=X1 THEN8200
8140 IFXT<X1 THENSR$="NAAR RECHTS"
8150 IFXT>X1 THENSR$="NAAR LINKS"
8160 VE=.68:GOSUB1110
8170 IFYT=Y1 THEN8230
8180 SR$="EN"
8190 VE=.76:GOSUB1110
8200 IFYT<Y1 THENSR$="OMHOOG !"
8210 IFYT>Y1 THENSR$="OMLAAG !"
8220 VE=.84:GOSUB1110
8230 RETURN
8240 REM
25000 REM **********> MENU <************
25010 DATA"menu"
25020 DATA"        ASSENSTELSEL","ro"
25030 DATA"   HET PROGRAMMA ------> 1"
25040 DATA"   DE ASSEN -----------> 2"
25050 DATA"   DE COORDINATEN -----> 3"
25060 DATA"   VRAGEN COORDINATEN -> 4"
25070 DATA"   UITLEG KWADRANTEN --> 5"
25080 DATA"   VRAGEN KWADRANTEN --> 6"
25090 DATA"   ZOEK SPEL ----------> 7"
25100 DATA"   STOPPEN ------------> 8","ro"
25110 DATA"TYPE JE KEUZE IN."
25120 DATA"einde"
25130 REM
25140 REM *********> RESUME <***********
25150 DATA"resume","ONTHOUD DUS"," "
25160 DATA"De voorste co-ordinaat geeft"
25170 DATA"aan hoeveel je naar LINKS of"
25180 DATA"naar RECHTS moet.","ro"
25190 DATA"Dat is de X-CO-ORDINAAT !!","ro","ro"
25200 DATA"sp","De achterste co-ordinaat geeft"
25210 DATA"aan hoeveel je OMHOOG of OMLAAG"
25220 DATA"moet.","ro"
25230 DATA"Dat is de Y-CO-ORDINAAT !!"
25240 DATA"sp","einde"
25250 REM
25260 REM ****> VRAGEN COORDINATEN <****
25270 DATA"vragen-1","Op het scherm komt een knippe-"
25280 DATA"rende ster.","ro"
25290 DATA"Als het knipperen stopt, worden"
25300 DATA"de co-ordinaten gevraagd.","ro","ro"
25310 DATA"sp"
25320 DATA"X en Y zijn gehele getallen.","ro"
25330 DATA"Je krijgt 10 beurten.","ro","ro"
25340 DATA"SUCCES !!!!","sp"
25350 DATA"einde"
25360 REM
25370 REM ****> VRAGEN KWADRANTEN <*****
25380 DATA"vragen-2"
25390 DATA"De co-ordinaten van 10 punten"
25400 DATA"komen een voor een op het"
25410 DATA"scherm","ro"
25420 DATA"Van elk punt wordt gevraagd in"
25430 DATA"welk KWADRANT dat punt ligt.","ro","sp"
25440 DATA"Elke keer moet je dus een 1"
25450 DATA"of een 2 of een 3 of een 4"
25460 DATA"invoeren.","ro","ro"
25470 DATA"SUCCES !!!!","sp"
25480 DATA"einde"
25490 REM
25500 REM ********> ZOEKSPEL <**********
25510 DATA"zoekspel"
25520 DATA"Ik heb een punt verstopt.","ro"
25530 DATA"Voor de co-ordinaten van dat"
25540 DATA"punt geldt:  -8 <= X <= 8"
25550 DATA"          :  -6 <= Y <= 6","ro","sp"
25560 DATA"X en Y zijn gehele getallen.","ro"
25570 DATA"Je krijgt niet meer dan acht"
25580 DATA"pogingen om het punt te vinden.","ro","ro"
25590 DATA"SUCCES !!!!","sp"
25600 DATA"einde"
25610 REM
25620 REM ******> HET PROGRAMMA <*******
25630 DATA"programma"
25640 DATA"Dit programma is bestemd voor"
25650 DATA"leerlingen in de eerste klas van"
25660 DATA"het voortgezet onderwijs.","ro"
25670 DATA"Het kan gebruikt worden als een"
25680 DATA"herhalingsles en/of oefenles.","ro","sp"
25690 DATA"De invoer neemt geen onmogelijke"
25700 DATA"antwoorden op.","ro"
25710 DATA"Foute -niet ingevoerde- antwoor-"
25720 DATA"den kunnen met de verbeter-toets"
25730 DATA"worden weggehaald.","sp"
25740 DATA"einde"
30000 REM
30010 REM ***> AFKORTINGEN IN TEKST <***
30020 REM RO IS REGEL OVERSLAAN
30030 REM SP IS SPATIEBALK
30040 REM
30050 REM ******************************
30060 REM *       ASSENSTELSEL         *
30070 REM ******************************
30080 REM * EEN PROGRAMMA OVER ASSEN   *
30090 REM * COORDINATEN EM KWADRANTEN  *
30100 REM ******************************
32000 REM * EEN PROGRAMMA VAN HANS VAN *
32010 REM * SCHAAIK.                   *
32020 REM * BEWERKT EN VERTAALD NAAR   *
32030 REM * BASICODE 3 DOOR            *
32040 REM * JAAP WIELES                *
32050 REM * HOOFDWAL 68                *
32060 REM * 4207 EE GORINCHEM          *
32070 REM * APRIL 1989                 *
32080 REM ******************************
32090 REM
32100 REM TROS-RADIO dd 890830

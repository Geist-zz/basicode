
1000 A=400:GOTO20:REM    KASSA  om winkeltje te spelen
1010 A=0:EX=0
1020 GOSUB6010:REM       initial.
1030 GOSUB7010:REM       variab.
1040 GOSUB5010:REM       display
1050 GOSUB2010:REM       art.inv
1060 IFIN$="Q" THEN1140
1070 IFKL=1 THEN1120
1080 GOSUB2510:REM       bedr.inv
1090 IFIN$="Q" THEN1140
1100 IFN=16 THEN GOSUB2790
1110 IFKL=0 THEN1050
1120 GOTO1030:REM       nwe klant
1130 REM       ////////////////einde
1140 GOTO950
2000 REM      /////kies artikelsoort
2010 A$="0":GOSUB5070
2020 VE=1:HO=26:GOSUB110
2030 GOSUB210:IFIN=0 THEN2030
2040 SR$=IN$:GOSUB330:IN$=SR$
2050 IF(IN$="T")AND(TT>0) THEN2790
2060 VE=1:HO=26:GOSUB110
2070 CH$="KZGNBVDQ"
2080 FORJ=LEN(CH$) TO1 STEP-1
2090 GV=(IN$=MID$(CH$,J,1))
2100 IFGV THENR$(N)=T$(J):PRINTT$(J):J=1
2110 NEXTJ
2120 IFNOT(GV) THEN2030
2130 IFN<>1 THEN2180
2140 SR$=""
2150 IFIN$<>"Q" THENSR$="** IRMA'S POPSHOP **"
2160 GOSUB4020:GOSUB4050
2170 REM       //////artikel gekozen
2180 SR$=R$(N):A$="0"
2190 RETURN
2500 REM       ////////bedrag-invoer
2510 GOSUB5070
2520 GOSUB200:IFIN=0 THEN2520
2530 SR$=IN$:GOSUB330:IN$=SR$
2540 IFIN<>13 THEN2570
2550 IFA$="0" THEN2510
2560 T(N)=VAL(A$):GOTO2660
2570 IF(IN$<"0")OR(IN$>"9") THEN2610
2580 IF(A$<>"0")OR(IN$<>"0") THENA$=A$+IN$
2590 IFLEFT$(A$,1)="0" THENA$=RIGHT$(A$,LEN(A$)-1)
2600 GOTO2510
2610 IF(IN$="M")OR(IN$=".") THENIN$=".":A$=A$+IN$:GOTO2510
2620 IFIN$="C" THENA$="0":GOTO2510
2630 IFIN$="Q" THENRETURN
2640 GOTO2510
2650 REM       /////////print invoer
2660 GOSUB5070
2670 TT=TT+T(N)
2680 SR=T(N):GOSUB310
2690 R$(N)=R$(N)+LEFT$(SP$,13-LEN(R$(N)))+SR$
2700 HO=0
2710 FORI=1 TON
2720 VE=18-N+I:GOSUB110:PRINTR$(I)
2730 NEXTI
2740 SR$=R$(N)
2750 GOSUB4020
2760 IFIN=13 THENN=N+1:RETURN
2770 RETURN
2780 REM       ////print (sub)totaal
2790 KL=1:SR$=LEFT$(SP$,13)+ST$
2800 VE=19-N+I:HO=0:GOSUB4010
2810 T(N)=TT:SR=T(N):GOSUB310
2820 SR$=T$(10)+LEFT$(SP$,13-LEN(T$(10)))+SR$
2830 VE=VE+1:HO=0:GOSUB110:V1=VE
2840 PRINTSR$
3000 REM       //////////// zegels ?
3010 VE=1:HO=26:GOSUB110:PRINT"zegels  (j/n)"
3020 GOSUB9150:REM       j/n
3030 VE=V1:IFIN=78 THEN3200:REM      3210
3040 HO=0:REM      VE=19-N+I
3050 SR$=RIGHT$(SR$,LEN(SR$)-13)
3060 SR$=T$(9)+LEFT$(SP$,13-LEN(T$(9)))+SR$
3070 GOSUB4010
3080 FORI=1 TON-1
3090 IFLEFT$(R$(I),LEN(T$(5)))<>T$(5) THENZE=ZE+T(I)
3100 NEXTI
3110 N=N+1:T(N)=INT(ZE)/10
3120 SR=T(N):GOSUB310
3130 A$=SR$:GOSUB5070
3140 SR$="zegels       "+SR$
3150 VE=21:HO=0:GOSUB4010
3160 SR$=LEFT$(SP$,13)+ST$
3170 VE=22:HO=0:GOSUB4010
3180 N=N+1:T(N)=T(N-1)+T(N-2)
3190 VE=23
3200 SR=T(N):GOSUB310
3210 A$="Tot.  "+SR$:GOSUB5010
3220 SR$=T$(10)+LEFT$(SP$,13-LEN(T$(10)))+SR$
3230 HO=0:GOSUB4010:GOSUB4050
3240 SR$=DA$+" TOT ZIENS":GOSUB4020
3250 FORI=1 TO5
3260 GOSUB4050:REM      LINEFD
3270 NEXTI
3280 SD=15:GOSUB450
3290 GOSUB250
3300 HO=25
3310 VE=20:GOSUB110:PRINT"Sluit de kassa"
3320 VE=21:GOSUB110:PRINT"door op >ENTER<"
3330 VE=22:GOSUB110:PRINT"  te drukken."
3340 GOSUB200:IFIN<>13 THEN3340
3350 GOSUB250
3360 RETURN
4000 REM       //////////////uitvoer
4010 GOSUB110:PRINTSR$;
4020 IFEX=0 THEN4040
4030 SR$=LEFT$(SP$,EX)+SR$:REM       spaties voor printer
4040 IFPR=1 THEN GOSUB350
4050 IFPR=1 THEN GOSUB360
4060 RETURN
5000 REM       ////////print display
5010 V1=VE
5020 HO=25:REM       kader
5030 VE=0:GOSUB110:PRINT"==============="
5040 VE=1:GOSUB110:PRINT"=             ="
5050 VE=2:GOSUB110:PRINT"==============="
5060 REM       /////////print bedrag
5070 VE=1:HO=26:GOSUB110
5080 T$(0)=R$(N)
5090 IFLEN(R$(N))+LEN(A$)>=13 THEN5110
5100 T$(0)=T$(0)+LEFT$(SP$,13-LEN(R$(N))-LEN(A$))
5110 T$(0)=T$(0)+A$
5120 PRINTT$(0)
5130 IFA$="0" THEN GOSUB5170
5140 VE=V1
5150 RETURN
5160 REM       ///print aanwijzingen
5170 HO=25
5180 FORVE=4 TO11
5190 GOSUB110
5200 PRINTT$(VE-3)
5210 NEXTVE
5220 VE=VE+1:GOSUB110:PRINT"C = corrigeren"
5230 VE=VE+1:GOSUB110:PRINT"T = totaal"
5240 VE=VE+1:GOSUB110:PRINT"Q = stop"
5250 RETURN
6000 REM       ////////initialisatie
6010 DIMT$(10),R$(18),T(18)
6020 T$(1)="kruid"
6030 T$(2)="zuivel"
6040 T$(3)="groente"
6050 T$(4)="non-fd"
6060 T$(5)="brood"
6070 T$(6)="vlees"
6080 T$(7)="drank"
6090 T$(9)="Subtot."
6100 T$(10)="Totaal"
6110 SP$="                                        "
6120 ST$="+======"
6130 GOSUB8010
6140 RETURN
7000 REM       ///////////variabelen
7010 CN=2:CT=7
7020 IN$="":SR$=""
7030 A$="0":B$="            0"
7040 TT=0:KL=0:N=1
7050 FORI=1 TO18:R$(I)="":T(I)=0:NEXTI
7060 GOSUB100:RETURN
8000 REM       ////variabelen-invoer
8010 PRINT"Printer aan ? (J/N)"
8020 GOSUB9150:REM       j/n
8030 PR=0:IFIN=74 THENPR=1
8040 GOSUB9010:GOSUB100
8050 PRINT:PRINT"Wil je zelf een datum invoeren ? (J/N)"
8060 GOSUB9150:REM       j/n
8070 IFIN=78 THEN8330
8080 REM       /////////datum-invoer
8090 PRINT"Datum-invoer  (bijv. 07 11 1988)"
8100 PRINT"                     dd mm jjjj"
8110 VE=10:HO=0:GOSUB110
8120 GOSUB110:PRINT"Dag :";:GOSUB120
8130 GOSUB110:PRINT"           ":GOSUB110:INPUTIN$
8140 IFLEN(IN$)<>2 THENPRINT"twee cijfers !":GOTO8130
8150 PRINT"                   "
8160 D=VAL(IN$):IF(D>0)AND(D<32) THEN8180
8170 PRINT"Alleen cijfers !":GOTO8130
8180 VE=12:HO=0:GOSUB110
8190 GOSUB110:PRINT"Maand :";:GOSUB120
8200 GOSUB110:PRINT"           ":GOSUB110:INPUTIN$
8210 IFLEN(IN$)<>2 THENPRINT"twee cijfers !":GOTO8200
8220 PRINT"                   "
8230 M=VAL(IN$):IF(M>0)AND(M<13) THEN8250
8240 PRINT"Alleen cijfers !":GOTO8200
8250 VE=14:HO=0:GOSUB110
8260 GOSUB110:PRINT"Jaar :";:GOSUB120
8270 GOSUB110:PRINT"          ":GOSUB110:INPUTIN$
8280 IFLEN(IN$)<>4 THENPRINT"vier cijfers !":GOTO8270
8290 PRINT"              "
8300 J=VAL(IN$):IF(J>1988)AND(J<2000) THEN8370
8310 PRINT"Tussen 1988 en 2000 !":GOTO8270
8320 REM       ///willekeurige datum
8330 GOSUB260:D=INT(RV*31)+1
8340 GOSUB260:M=INT(RV*12)+1
8350 GOSUB260:J=1989+INT(RV*10)
8360 REM       /////////////maak DA$
8370 SR=D:GOSUB300:DA$=SR$+"-"
8380 SR=M:GOSUB300:DA$=DA$+SR$+"-"
8390 SR=J:GOSUB300:DA$=DA$+SR$
8400 RETURN
9000 REM   ///////////////uitleg
9010 RESTORE
9020 GOSUB100:VE=0:HO=0
9030 READSR$
9040 IFSR$="." THENVE=20:GOSUB110:GOTO9030
9050 IFSR$<>"##" THEN9090
9060 GOSUB9150
9070 IFIN<>74 THEN9060
9080 GOTO9020
9090 IFSR$<>"STOP" THEN9110
9100 SR$="EINDE UITLEG. DRUK OP J"
9110 VE=VE+1:GOSUB110:PRINTSR$;
9120 IFLEFT$(SR$,12)="EINDE UITLEG" THEN9150
9130 PRINT:PRINT:GOTO9030
9140 REM       ///////////ja of nee
9150 GOSUB200:IF(IN<>74)AND(IN<>78) THEN9150
9170 RETURN
25000 DATA" ======= U I T L E G   K A S S A =======",""
25010 DATA"Met dit programma kan een kind winkeltje"
25020 DATA"spelen.","Het programma begint te vragen of een"
25030 DATA"printer gebruikt gaat worden.",""
25040 DATA"Vervolgens kan gekozen worden tussen het"
25050 DATA"zelf invoeren van een datum, of de datum"
25060 DATA"willekeurig door de computer laten vast-"
25070 DATA"stellen."
25080 DATA".","                   VERDER ? DRUK OP J","##"
25090 DATA" ======= U I T L E G   K A S S A =======",""
25100 DATA"Het hoofdprogramma begint:"
25110 DATA"Rechts boven staat een venster met een"
25120 DATA"cursor en een 0.","Daaronder een rij woorden."
25130 DATA"Om te beginnen moet gekozen worden in"
25140 DATA"welke groep het artikel hoort (bijv."
25150 DATA"melk -> zuivel). Dan druk je de Z in."
25160 DATA"In het venster verschijnt : 'zuivel' ."
25170 DATA".","                   VERDER ? DRUK OP J","##"
25180 DATA" ======= U I T L E G   K A S S A =======",""
25190 DATA"Nu kan het bedrag worden ingevoerd."
25200 DATA"Alle cijfers zijn geldig, en de letter M"
25210 DATA"kan gebruikt worden voor de punt."
25220 DATA"Een fout bedrag kun je herstellen met"
25230 DATA"de letter C. (Als je nog niet op >ENTER<"
25240 DATA"hebt gedrukt.)"
25250 DATA".","                   VERDER ? DRUK OP J","##"
25260 DATA" ======= U I T L E G   K A S S A =======",""
25270 DATA"Als de klant klaar is wordt op de T ge-"
25280 DATA"drukt.","De bedragen worden opgeteld en de"
25290 DATA"kassa vraagt of er ook zegels worden"
25300 DATA"gekocht."
25310 DATA"Afhankelijk van het antwoord wordt de"
25320 DATA"eindrekening opgemaakt."
25330 DATA".","                   VERDER ? DRUK OP J","##"
25340 DATA" ======= U I T L E G   K A S S A =======",""
25350 DATA"Met de letter Q kan het programma be-"
25360 DATA"eindigd worden.","",""
25370 DATA"De kassabon is slechts 20 tekens breed."
25380 DATA"De variabele EX in regel 1010 bepaalt"
25390 DATA"in welke kolom geprint wordt."
25400 DATA"(Pas dat zonodig aan uw behoefte aan)"
25410 DATA".","STOP"
30000 REM       KASSA****************
30010 REM       IS EEN PROGRAMMA ***
30020 REM       GESCHREVEN OP EEN**
30030 REM       ZX-SPECTRUM 128K**
30040 REM                     ***
32000 REM       DOOR K.v.d. WISSEL
32010 REM       NIEUWSTRAAT 306
32020 REM       3201 EG SPIJKENISSE
32030 REM       01880-13398**
32040 REM                ***
32050 REM       ALS CADEAU*VOOR MIJN
32060 REM       DOCHTER***
32070 REM             ***
32080 REM       KERSTMIS 1988
32090 REM           ***
32100 REM       (C)***BASICODE******
32110 REM
32120 REM TROS-RADIO dd. 890510
32130 REM incl.corr. dd. 890511

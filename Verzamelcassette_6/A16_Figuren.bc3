
1000 A=250:GOTO20:REM ** FIGUREN **
1010 H2=INT(HG/2):V2=INT(VG/2):CN=0:RD=3.14/180:P$="Prog : "
1020 PP$="Dit programma tekent "
1030 HB=HO:VB=VE:H=.5:U=160:V=100:H1=HG+1:V1=VG+1
1040 DIMX(24),Y(24),XX(5),YY(5),H(320),M$(14)
1050 FORX=1 TO90:READI:NEXTX
1060 FORX=1 TO14:READM$(X):NEXTX
1070 SR$="FIGUREN":HO=HB/2-7:VE=0:GOSUB110:GOSUB150
1080 RESTORE
1090 PRINT:PRINT:PRINT"U kunt kiezen uit :"
1100 PRINT
1110 FORX=1 TO14:PRINTM$(X):NEXTX
1140 PRINT:PRINT"Geef Uw keuze ";
1160 GOSUB210:GN=IN-64:SR$=IN$:GOSUB330
1165 PRINTSR$
1170 IFGN=14 THEN950
1180 IFGN=13 THEN GOSUB9010:GOTO1070
1190 IF(GN<1)OR(GN>12) THEN1140
1200 PRINT"Wilt U de uitleg (J/N) ";
1210 GOSUB210:IF(IN<>74)AND(IN<>78) THEN1210
1230 GOSUB100:PRINTP$;M$(GN):PRINT
1260 IFGN>6 THEN1290
1270 ONGN GOSUB2010,2510,3010,3510,4010,4510
1280 GOTO1340
1290 ONGN-6 GOSUB5010,5510,6010,6510,7010,7510
1340 GOSUB250
1350 SR$="=>":HO=.93:VE=.93:GOSUB650
1360 GOSUB210:GOSUB100:GOTO1070
2000 REM * INGESCHREVEN VIERKANTEN *
2010 IFIN=78 THEN2150
2020 PRINTPP$;"vierkanten,"
2030 PRINT"en wel zo dat de hoekpunten van een"
2040 PRINT"vierkant op een vast deel van de zij-"
2050 PRINT"den van zijn voorganger komen."
2060 PRINT"U kunt zelf kiezen hoe groot dat"
2070 PRINT"vaste deel (K) moet zijn."
2080 PRINT"(20 geeft een mooi resultaat.)"
2090 PRINT"Als U bijvoorbeeld 20 indrukt, wil"
2100 PRINT"dat zeggen dat de hoekpunten op 1/20"
2110 PRINT"deel rechts op de zijden van het"
2120 PRINT"vorige vierkant komen."
2130 GOSUB9510
2140 REM * INGESCHREVEN VIERKANTEN *
2150 INPUT"Geef K : ";K:IFK<3 THEN2150
2160 X(1)=1000:Y(1)=5999:X(2)=6999:Y(2)=Y(1)
2170 X(3)=X(2):Y(3)=0:X(4)=X(1):Y(4)=Y(3)
2180 X(5)=X(1):Y(5)=Y(1)
2190 GOSUB600
2200 FORN=1 TO-5.9915/LOG((K*(K-2)+2)/K/K)
2210 FORJ=1 TO4
2220 HO=X(J)/8000:VE=Y(J)/6000:GOSUB620
2230 HO=X(J+1)/8000:VE=Y(J+1)/6000:GOSUB630
2240 NEXTJ
2250 FORJ=1 TO4
2260 XX(J)=X(J)+(X(J+1)-X(J))/K
2270 YY(J)=Y(J)+(Y(J+1)-Y(J))/K
2280 NEXTJ
2290 FORJ=1 TO4:X(J)=XX(J):Y(J)=YY(J):NEXTJ
2300 X(5)=X(1):Y(5)=Y(1)
2310 NEXTN
2320 RETURN
2500 REM * SYMMETRISCHE KROMMEN *
2510 IFIN=78 THEN2650
2520 PRINTPP$;"een altijd"
2530 PRINT"symmetrische figuur door 3 parameters"
2540 PRINT"(A,B,C) in te voeren."
2550 PRINT:PRINT"Enige mooie resultaten : ":PRINT
2560 PRINT"   A        B        C":PRINT
2570 PRINT"   2        7        3"
2580 PRINT"   6        6        4"
2590 PRINT"   4        6        1"
2600 PRINT"   1        1        4"
2610 PRINT"   3        3        5"
2620 PRINT"   2        2        9"
2630 GOSUB9510
2640 REM * SYMMETRISCHE FIGUREN *
2650 INPUT"Geef A : ";A
2660 INPUT"Geef B : ";B
2670 INPUT"Geef C : ";C
2680 GOSUB600
2690 FORW=0 TO360
2700 T=W*RD:RV=.49*SIN(C*T)
2710 HO=.5+.75*RV*COS(A*T)
2720 VE=.5-RV*SIN(B*T)
2730 IFW=0 THEN GOSUB620:GOTO2750
2740 GOSUB630
2750 NEXTW
2760 RETURN
3000 REM * TURTLE *
3010 IFIN=78 THEN3160
3020 PRINT"Dit programma is een kleine simulatie"
3030 PRINT"van LOGO."
3040 PRINT"Door de beginrichting, verplaatsing,"
3050 PRINT"draaihoek linksom en de toename"
3060 PRINT"van de zijde te geven wordt er een"
3070 PRINT"figuur getekend."
3080 PRINT:PRINT"Enige mooie resultaten :":PRINT
3090 PRINT"Beginr.  verpl.  draaih.  toe. zijde":PRINT
3100 PRINT"  90       5       144         3"
3110 PRINT"  90       5       123         2"
3120 PRINT"  90      -2        92         2"
3130 PRINT"  90       5        72         1"
3140 GOSUB9510
3150 REM * TURTLE *
3160 INPUT"Beginrichting (graden) : ";W
3170 INPUT"Verplaatsing           : ";S:S=S/200
3180 INPUT"Draaihoek, linksom     : ";DW
3190 INPUT"Toename zijde          : ";DS:DS=DS/200
3200 W1=W*RD
3210 GOSUB600
3220 HO=.5:VE=.5:GOSUB620
3230 HO=HO+.75*S*COS(W1)
3240 VE=VE-S*SIN(W1)
3250 IF(HO<0)OR(HO>=1)OR(VE<0)OR(VE>=1) THENRETURN
3260 GOSUB630
3270 W=W+DW:IFW>=360 THENW=W-360
3280 W1=W*RD:S=S+DS
3290 GOTO3230
3500 REM * DIAGONAALWEB *
3510 IFIN=78 THEN3590
3520 PRINTPP$;"van een aantal"
3530 PRINT"punten, die zich  ahw. boven- en"
3540 PRINT"onderin het scherm bevinden, alle"
3550 PRINT"mogelijke lijnen van boven naar onder."
3560 PRINT:PRINT"U kunt het aantal punten opgeven."
3570 GOSUB9510
3580 REM * DIAGONAALWEB *
3590 PRINT"Aantal punten (1 - ";HG;") : ";:INPUTHP
3600 IF(HP<1)OR(HP>HG) THEN3590
3610 GOSUB600
3620 Y1=0:Y2=VG-1
3630 FORX1=0 TOHG-1 STEPHG/HP
3640 FORX2=0 TOHG-1 STEPHG/HP
3650 HO=X1/HG:VE=Y1/VG:GOSUB620
3660 HO=X2/HG:VE=Y2/VG:GOSUB630
3670 NEXTX2
3680 NEXTX1
3690 RETURN
4000 REM * KUBUS MET 8-VLAK *
4010 IFIN=78 THEN4060
4020 PRINTPP$;"een kubus met"
4030 PRINT"daarin een achtvlak (oktaeder)."
4040 GOSUB9510
4050 REM * KUBUS MET ACHTVLAK *
4060 INPUT"Geef alfa in graden  (1 - 90) : ";A
4070 IF(A<1)OR(A>90) THEN4060
4080 INPUT"Verkleiningsfactor (.1 - 1)   : ";K
4090 IF(K<.1)OR(K>1) THEN4080
4100 W=A*RD:C=K*COS(W):S=K*SIN(W)
4110 FORJ=1 TO14
4120 READX,Y,Z
4130 X(J)=INT(U+X+C*Y+H)
4140 Y(J)=INT(V-S*Y-Z+H)
4150 NEXTJ
4160 GOSUB600
4170 FORM=1 TO24
4180 READI,J
4190 HO=X(I)/321:VE=Y(I)/201:GOSUB620
4200 HO=X(J)/321:VE=Y(J)/201:GOSUB630
4210 NEXTM
4220 RETURN
4500 REM * VIER BERGEN *
4510 IFIN=78 THEN4580
4520 PRINTPP$;"4 bergen"
4530 PRINT"in perspectief."
4540 PRINT"Het duurt wel even, dus tijd om"
4550 PRINT"koffie te zetten !!!"
4560 GOSUB9510
4570 REM * VIER BERGEN *
4580 INPUT"Alpha in graden (50 - 135)    : ";W
4590 IF(W<50)OR(W>135) THEN4580
4600 INPUT"Verkleiningsfactor (.5 - .75) : ";K
4610 IF(K<.5)OR(K>.75) THEN4600
4620 C=K*COS(W*RD):S=K*SIN(W*RD)
4630 DX=3:DY=5:K1=20
4640 FORL=0 TO320:H(L)=1000:NEXTL
4650 GOSUB600
4660 FORYY=-115 TO115 STEPDY
4670 M1=COS(YY*2*3.14/115-3.14)+1
4680 FORXX=-115 TO115 STEPDX
4690 M2=COS(XX*2*3.14/115-3.14)+1
4700 Z=K1*M1*M2
4710 XG=INT(U+XX+C*YY+H)
4720 YG=INT(V-S*YY-Z+H)
4730 IFXX>-115 THEN4770
4740 F1=0:L=INT(XG/DX)
4750 IFYG<=H(L) THENF1=1:H(L)=YG
4760 X1=XG:Y1=YG:GOTO4830
4770 F2=0:L=INT(XG/DX)
4780 IFYG<=H(L) THENF2=1:H(L)=YG
4790 X2=XG:Y2=YG:IFY2<0 THENY2=0
4800 IFF1*F2=1 THENHO=X1/321:VE=Y1/201:GOSUB620
4810 IFF1*F2=1 THENHO=X2/321:VE=Y2/201:GOSUB630
4820 X1=X2:Y1=Y2:F1=F2
4830 IFY1<0 THENY1=0
4840 NEXTXX
4850 NEXTYY
4860 RETURN
5000 REM * PARABOOLSTELSEL *
5010 IFIN=78 THEN5060
5020 PRINTPP$;"een stelsel"
5030 PRINT"parabolen met de functie y=-k*x*x+k."
5040 GOSUB9510
5050 REM * PARABOOLSTELSEL *
5060 INPUT"Aantal parabolen (2 - 50) : ";N
5070 IF(N<2)OR(N>50) THEN5060
5080 N=N-1:GOSUB600
5090 FORK=-100 TO100 STEP200/N
5100 FORX=-110 TO110 STEP5
5110 HO=INT(U+X+H)/321
5120 VE=-K*X*X/6400+K:VE=INT(V-VE+H)/201
5130 IFX=-110 THEN GOSUB620:GOTO5150
5140 GOSUB630
5150 NEXTX
5160 NEXTK
5170 RETURN
5500 REM * SINUSKROMMEN IN CIRKELS *
5510 IFIN=78 THEN5580
5520 PRINTPP$;"sinuskrommen"
5530 PRINT"die cirkelvormig gekromd zijn."
5540 PRINT"Het resultaat lijkt hierdoor op een"
5550 PRINT"bloem."
5560 GOSUB9510
5570 REM * SINUSKROMMEN IN CIRKELS *
5580 GOSUB600
5590 FORK=-40 TO40 STEP10
5600 FORW=0 TO360 STEP2
5610 P=W*RD:R=.25+K*SIN(4*P)/200
5620 HO=.5+.75*R*COS(P)
5630 VE=.5+R*SIN(P)
5640 IFP=0 THEN GOSUB620:GOTO5660
5650 GOSUB630
5660 NEXTW
5670 NEXTK
5680 RETURN
6000 REM * KUBUS MET ZADELVLAK *
6010 IFIN=78 THEN6090
6020 PRINTPP$;"een kubus met"
6030 PRINT"een zadelvlak."
6040 PRINT"Om te laten zien wat een zadelvlak"
6050 PRINT"is, kunt U het beste voor aantal"
6060 PRINT"lijnen 32 invullen."
6070 GOSUB9510
6080 REM * KUBUS MET ZADELVLAK *
6090 INPUT"Alpha in graden (1 - 90)     : ";A
6100 IF(A<1)OR(A>90) THEN6090
6110 INPUT"Verkleiningsfactor (.1 - .6) : ";K
6120 IF(K<.1)OR(K>.6) THEN6110
6130 INPUT"Hoeveel lijnen               : ";N
6140 IFN<0 THEN6130
6150 W=A*RD:C=K*COS(W):S=K*SIN(W)
6160 FORJ=1 TO8
6170 READX,Y,Z
6180 X(J)=INT(U+X+C*Y+H)
6190 Y(J)=INT(V-S*Y-Z+H)
6200 NEXTJ:GOSUB600
6210 FORJ=1 TO9:READX,Y:NEXTJ
6220 FORM=1 TO12
6230 READI,J
6240 HO=X(I)/321:VE=Y(I)/201:GOSUB620
6250 HO=X(J)/321:VE=Y(J)/201:GOSUB630
6260 NEXTM
6270 FORJ=0 TON
6280 X1=INT(X(2)+J*(X(7)-X(2))/N+H)
6290 Y1=INT(Y(2)+J*(Y(7)-Y(2))/N+H)
6300 X2=INT(X(5)+J*(X(4)-X(5))/N+H)
6310 Y2=INT(Y(5)+J*(Y(4)-Y(5))/N+H)
6320 HO=X1/321:VE=Y1/201:GOSUB620
6330 HO=X2/321:VE=Y2/201:GOSUB630
6340 NEXTJ
6350 HO=X(2)/321:VE=Y(2)/201:GOSUB620
6360 HO=X(7)/321:VE=Y(7)/201:GOSUB630
6370 HO=X(5)/321:VE=Y(5)/201:GOSUB620
6380 HO=X(4)/321:VE=Y(4)/201:GOSUB630
6390 RETURN
6500 REM * DIAGONALEN IN N-HOEK *
6510 IFIN=78 THEN6600
6520 PRINTPP$;"een n-hoek."
6530 PRINT"Vanaf elke hoek worden er lijnen"
6540 PRINT"getrokken naar alle andere hoek-"
6550 PRINT"punten."
6560 PRINT"U kunt het aantal hoeken, horizon-"
6570 PRINT"tale en vertikale diameter opgeven."
6580 GOSUB9510
6590 REM * DIAGONALEN IN N-HOEK *
6600 PRINT"horizontale d (1 - ";H2;")     : ";:INPUTA
6610 IF(A<1)OR(A>H2) THEN6600
6620 PRINT"Vertikale d (1 - ";V2;")       : ";:INPUTB
6630 IF(B<1)OR(B>V2) THEN6620
6640 INPUT"Hoeveel hoekpunten (3 - 24) : ";N
6650 IF(N<3)OR(N>24) THEN6640
6660 GOSUB600:W=360/N*RD
6670 FORJ=0 TON-1
6680 W1=J*W
6690 X(J)=INT(H2+A*COS(W1)+H)
6700 Y(J)=INT(V2-B*SIN(W1)+H)
6710 NEXTJ
6720 FORI=0 TON-2
6730 FORJ=I+1 TON-1
6740 HO=X(I)/H1:VE=Y(I)/V1:GOSUB620
6750 HO=X(J)/H1:VE=Y(J)/V1:GOSUB630
6760 NEXTJ
6770 NEXTI
6780 RETURN
7000 REM * STELSEL KROMMEN *
7010 IFIN=78 THEN7110
7020 PRINTPP$;"een stelsel krom-"
7030 PRINT"men. U kunt naar hartelust experimen-"
7040 PRINT"teren, doordat er twee (A,B) waar-"
7050 PRINT"den ingevoerd kunnen worden.":PRINT
7060 PRINT"Hier volgen enige mooie resultaten:"
7070 PRINT:PRINT"A : -6  -6  -8  4  4  6  4.5"
7080 PRINT"B :  1   2   2  1  2  1  1.5"
7090 GOSUB9510
7100 REM * STELSEL KROMMEN *
7110 INPUT"Geef A : ";A
7120 INPUT"Geef B : ";B
7130 GOSUB600
7140 FORN=-3 TO3
7150 FORW=0 TO360 STEP3
7160 T=W*RD
7170 X=(A+B)*COS(T)-N*B*COS((A+B)/B*T)
7180 Y=(A+B)*SIN(T)-N*B*SIN((A+B)/B*T)
7190 HO=.5+X/20*.75
7200 IFHO>=1 THENHO=.99999
7210 IFHO<0 THENHO=0
7220 VE=.5-Y/20
7230 IFVE>=1 THENVE=.99999
7240 IFVE<0 THENVE=0
7250 IFW=0 THEN GOSUB620:GOTO7270
7260 GOSUB630
7270 NEXTW
7280 NEXTN
7290 RETURN
7500 REM * KEGEL *
7510 IFIN=78 THEN7640
7520 PRINTPP$;"een kegel"
7530 PRINT"Er worden 7 breedtecirkels en 16"
7540 PRINT"lijnen op de kegelmantel loodrecht"
7550 PRINT"op de breedtecirkels getekend."
7560 PRINT"Als U voor R1 (onderste straal) en"
7570 PRINT"R2 (bovenste straal), hetzelfde getal"
7580 PRINT"invoert, ontstaat er een cilinder."
7590 PRINT"Als R2>R1 dan is de kegel omgedraaid."
7600 PRINT"Als R1 of R2 nul is krijgt U een"
7610 PRINT"echte kegel, anders een afgeknotte."
7620 GOSUB9510
7630 REM    * KEGEL *
7640 INPUT"Alpha in graden (1 - 90)     : ";A
7650 IF(A<1)OR(A>90) THEN7640
7660 INPUT"Verkleiningsfactor (.1 - .6) : ";K
7670 IF(K<.1)OR(K>.6) THEN7660
7680 PRINT"Straal R1 (0 - ";H2;")          : ";:INPUTR1
7690 IF(R1<0)OR(R1>H2) THEN7680
7700 PRINT"Straal R2 (0 - ";H2;")          : ";:INPUTR2
7710 IF(R2<0)OR(R2>H2) THEN7700
7720 DR=(R1-R2)/6
7730 W=A*RD:C=K*COS(W):S=K*SIN(W):N=0
7740 GOSUB600
7750 FORZ=-60 TO60 STEP20
7760 R=R1-N*DR
7770 FORW=0 TO360 STEP3
7780 W1=W*RD
7790 X=R*COS(W1):Y=R*SIN(W1)
7800 HO=.5+(X+C*Y)/H1
7810 VE=.5-(S*Y+Z)/V1
7820 IFW1<>0 THEN7840
7830 GOSUB620:GOTO7860
7840 IF(HO<0)OR(HO>=1)OR(VE<0)OR(VE>=1) THEN7860
7850 GOSUB630
7860 NEXTW
7870 N=N+1
7880 NEXTZ
7890 FORW=0 TO360 STEP23
7900 W1=W*RD
7910 X=R1*COS(W1):Y=R1*SIN(W1)
7920 HO=.5+(X+C*Y)/H1
7930 VE=.5-(S*Y-60)/V1
7940 X=R2*COS(W1):Y=R2*SIN(W1)
7950 X=INT(H2+X+C*Y+H)/H1
7960 Y=INT(V2-S*Y-60+H)/V1
7970 GOSUB620
7980 HO=X:VE=Y:GOSUB630
7990 NEXTW
8000 RETURN
9000 REM * ALGEMENE UITLEG *
9010 GOSUB100:PRINT"ALGEMENE UITLEG"
9020 PRINT"---------------":PRINT:PRINT
9030 PRINT"In dit programma zijn allerlei klei-"
9040 PRINT"nere programma's opgenomen."
9050 PRINT"Hiermee kunnen allerlei figuren ge-"
9060 PRINT"maakt worden."
9070 PRINT"U kunt ze selecteren van het menu."
9080 PRINT"Als U heeft gekozen kunt U nog een"
9090 PRINT"kleine uitleg over het gekozen pro-"
9100 PRINT"gramma lezen.":PRINT
9110 PRINT"Soms wordt er naar de verkleinings-"
9120 PRINT"factor gevraagd."
9130 PRINT"Dwz. hoe groot de lijnen evenwijdig"
9140 PRINT"aan de x-as tov. de lijnen evenwijdig"
9150 PRINT"aan de z of y-as worden."
9160 PRINT"Het mooiste resultaat wordt meestal"
9170 PRINT"verkregen door .5 in te vullen."
9180 GOSUB9510
9190 PRINT"Als er naar alpha gevraagd wordt is"
9200 PRINT"dit de hoek tussen de x-as en y-as."
9210 PRINT"Hier wordt meestal het mooiste"
9220 PRINT"resultaat bereikt door 45 graden"
9230 PRINT"in te vullen."
9240 PRINT:PRINT"VEEL PLEZIER MET DIT PROGRAMMA !"
9500 REM * TOETS *
9510 HO=HB-3:VE=VB:GOSUB110
9520 PRINT"=>";
9530 GOSUB200:IFIN$="" THEN9530
9540 GOSUB100:RETURN
25000 REM * KUBUS *
25010 DATA-60,-60,-60,60,-60,-60
25020 DATA60,60,-60,-60,60,-60
25030 DATA-60,-60,60,60,-60,60
25040 DATA60,60,60,-60,60,60
25050 DATA0,0,-60,0,-60,0,60,0,0
25060 DATA0,60,0,-60,0,0,0,0,60
25070 DATA1,2,2,3,3,4,4,1,1,5,2,6,3,7,4,8,5,6,6,7,7,8,8,5
25080 DATA9,10,9,11,9,12,9,13,10,11,11,12,12,13,13
25090 DATA10,10,14,11,14,12,14,13,14
26000 DATA"A. Ingeschreven vierkanten"
26010 DATA"B. Symmetrische krommen"
26020 DATA"C. Turtle-grafiek"
26030 DATA"D. Diagonaalweb"
26040 DATA"E. Kubus met 8-vlak"
26050 DATA"F. Vier bergen"
26060 DATA"G. Paraboolstelsel"
26070 DATA"H. Sinuskrommen in cirkels"
26080 DATA"I. Kubus met zadelvlak"
26090 DATA"J. Diagonalen in een n-hoek"
26100 DATA"K. Stelsel krommen"
26110 DATA"L. Kegel"
26120 DATA"M. Algemene uitleg"
26130 DATA"N. Stoppen"
30000 REM  ********************
30010 REM  IDEEEN AFKOMSTIG VAN
30020 REM  HET BOEK :
30030 REM  40 GRAFISCHE PRO-
30040 REM  GRAMMA'S VOOR DE C64
32000 REM  ********************
32010 REM  DOOR RONALD VAN VUGT
32020 REM  PA3EAH, POSTBUS  160
32030 REM  5580 AD, WAALRE (NB)
32040 REM
32050 REM      GEMAAKT OP EEN
32060 REM      ZX SPECTRUM
32070 REM
32080 REM  JUNI 1986, V1.1
32090 REM  ********************
32100 REM
32110 REM TROS-RADIO dd 860712
32120 REM Verzamelcassette-1
32130 REM incl corr. dd 881003
32140 REM TROS-RADIO dd 890719

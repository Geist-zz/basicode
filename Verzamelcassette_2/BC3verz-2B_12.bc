
1000 A=3000:GOTO20:REM BASICODE-3 COMPUTER BULLETIN/B2
1010 HM=HO:HT=HO:VT=VE:RV$="":DIMRE$(VT)
1020 GOSUB1520:REM initialisatie
1030 GOSUB500
1040 GOSUB540:IFLEFT$(IN$,1)<>"." THEN GOSUB1340:GOTO1040
1050 IFIN$<>".'" THEN1070
1060 GOSUB540:IN$=CHR$(34)+IN$:GOSUB1350:GOTO1040
1070 IFIN$<>".''" THEN1090
1080 GOSUB540:IN$=CHR$(34)+CHR$(34)+IN$:GOSUB1350:GOTO1040
1090 IFIN$<>".C" THEN1130
1100 IFRV$<>LM$ THENSR$=RV$:GOSUB1370
1110 GOSUB540:SR$=LEFT$(SP$,(HT-LEN(IN$))/2)+IN$
1120 GOSUB1370:RV$=LM$:GOTO1040
1130 IFIN$<>".P" THEN1180
1140 IFRV$<>LM$ THENSR$=RV$:GOSUB1370:RV$=LM$
1150 SR$="":GOSUB1370:IFSC=2 THEN GOSUB1370:GOTO1170
1160 IFAL<>2 THEN1140
1170 GOTO1040
1180 IFIN$=".E" THEN GOSUB580:GOTO950
1190 IFIN$=".T" THEN GOSUB540:TT$=IN$:GOTO1040
1200 IF(IN$<>".R")OR(SC=2) THEN1250
1210 GOSUB540:RP$=IN$
1220 IFLEN(RV$)+8+LEN(RP$)>HT THENSR$=RV$:GOSUB1370:RV$=LM$
1230 PRINTRV$;:RE$(AL)=RV$+" "+RP$+" ":SR$=RP$
1240 GOSUB150:HT=HT-LEN(RV$)-LEN(RP$)-7:RV$="":GOTO1040
1250 CO=VAL(MID$(IN$,3,LEN(IN$)-2)):IN$=LEFT$(IN$,2)
1260 IFIN$<>".F" THEN1300
1270 IFRV$<>"" THENSR$=RV$:GOSUB1370:RV$=LM$:CO=CO-1
1280 CO=CO-1:IFCO>-1 THENSR$="":GOSUB1370:GOTO1280
1290 GOTO1040
1300 IFIN$<>".M" THEN1040
1310 LM$="":IFCO+PS>0 THENLM$=LEFT$(SP$,CO+PS)
1320 GOTO1040
1330 REM subroutine verwerk IN$
1340 IFRV$<>LM$ THENRV$=RV$+" "
1350 IFLEN(RV$)+LEN(IN$)<HT THENRV$=RV$+IN$:RETURN
1360 SR$=RV$:GOSUB1370:RV$=LM$+IN$:RETURN
1370 HT=HM:IFSC=2 THEN GOSUB350:GOSUB360:GOTO1390
1380 RE$(AL)=RE$(AL)+SR$:PRINTSR$:RE$(AL+1)=""
1390 AL=AL+1:IFAL>VT-SL THEN GOSUB1420:REM paper to new page
1400 RETURN
1410 REM melding onder,wacht, ev screendump
1420 IFSC=2 THENFORI=1 TOSL:GOSUB360:NEXTI:AL=1:RETURN
1430 HO=3:VE=VT:GOSUB110
1440 SR$="VERVOLG: druk op <return>":GOSUB150
1450 H$=IN$:GOSUB210:IN$=H$:IFIN=13 THEN1480
1460 IFIN<>80 THEN1450
1470 FORAL=1 TOVT:SR$=RE$(AL):GOSUB350:GOSUB360:NEXTAL
1480 GOSUB100:AL=2:RE$(2)="":SR$=TT$
1490 PRINTLEFT$(SP$,(HT-LEN(SR$))/2-3);:GOSUB150
1500 PRINT:PRINT:RETURN
1510 REM *** INITIALISATIE ***
1520 AL=1:PS=0:LM$="":TT$="BASICODE-3 COMPUTERBULLETIN"
1530 DIMT$(100)
1540 SP$="             ":SP$=SP$+SP$+SP$
1550 SL=2:GOSUB1480:PRINT:PRINT:PRINT
1560 PRINT"Het bulletin wordt gelezen vanaf"
1570 PRINT"een BASICODE-3 bestand op"
1580 PRINT"ofwel cassette, ofwel diskette."
1590 PRINT:PRINT"Wilt u het bulletin":PRINT
1600 PRINT"   1  Van cassette naar diskette,"
1610 PRINT"   2  Van diskette naar cassette,"
1620 PRINT"   3  Van diskette naar scherm,"
1630 PRINT"   4  Van diskette naar printer,"
1640 PRINT"   5  Van cassette naar scherm,"
1650 PRINT"   6  Van cassette naar printer."
1660 PRINT:PRINT"Welk nummer kiest u ";:GOSUB210
1670 SC=IN-48:PRINTSC
1680 IF(SC<1)OR(SC>6) THEN GOSUB250:GOTO1000
1690 ONSC GOTO1700,1710,1720,1730,1740,1750
1700 NL=0:NS=5:GOTO1860
1710 NL=4:NS=1:GOTO1860
1720 NF=4:SC=1:GOTO1850
1730 NF=4:SC=2:GOTO1760
1740 NF=0:SC=1:GOTO1850
1750 NF=0:SC=2
1760 PRINT:PRINT"Hoeveel spaties wilt u aan het"
1770 PRINT"begin van elke regel ";:INPUTPS:LM$=LEFT$(SP$,PS)
1780 PRINT:PRINT"Hoeveel tekens echte tekst wilt"
1790 PRINT"u op elke regel ";:INPUTHT:HT=HT+PS:HM=HT
1800 PRINT:PRINT"Hoeveel regels wilt u bij de scheurlijn"
1810 PRINT"laten overslaan ";:INPUTSL:RV$=LM$
1820 PRINT:PRINT"Hoeveel echte tekstregels wilt u op"
1830 PRINT"elk blad ";:INPUTVT:VT=VT+SL:GOSUB360
1840 NF$="B12TXT":RETURN
1850 GOSUB1480:GOTO1840
1860 GOSUB100
1870 PRINT"Ik lees nu het bestand van ";:NF=NL:GOSUB2030
1880 PRINT"en schrijf het naar ";:NF=NS:GOSUB2030
1890 HO=0:VE=7:GOSUB110:PRINT"woord nummer ";:GOSUB120
1900 NR=1
1910 I=0
1920 GOSUB110:PRINTNR;
1930 NF=NL:GOSUB540:IFIN=-1 THEN GOSUB2060
1940 T$(I)=IN$:I=I+1:NR=NR+1:IF(I<101)AND(IN<>1) THEN1920
1950 EO=IN
1960 NF=NS:FORJ=0 TOI-1:SR$=T$(J):GOSUB560
1970 IFIN=-1 THEN GOSUB2060
1980 NEXTJ:IFEO<>1 THEN1910
1990 NF=NL:GOSUB580:NF=NS:GOSUB580
2000 PRINT:PRINT"Dat is gebeurd, druk op RETURN";
2010 GOSUB210:IFIN<>13 THEN2010
2020 GOTO950
2030 IFNF<2 THENPRINT"cassette"
2040 IFNF>3 THENPRINT"diskette"
2050 NF$="B12TXT":GOSUB500:IFIN=0 THENRETURN
2060 PRINT"Leesfout opgetreden"
2070 PRINT"Ik probeer door te gaan!"
2080 RETURN
30000 REM"****************************"
30010 REM"*                          *"
30020 REM"* BASISPROGRAMMA voor een  *"
30030 REM"*  BASICODE - 3 BULLETIN   *"
30040 REM"*   vanaf bestandstape     *"
30050 REM"*                          *"
30060 REM"*   TROS-Radio  1-7-1987   *"
30070 REM"*                          *"
30080 REM"*   Copyright (c) 1987:    *"
30090 REM"*   Stichting BASICODE     *"
30100 REM"*   EINDHOVEN              *"
30110 REM"*                          *"
30120 REM"****************************"


1000 A=10000:GOTO20:REM RWLTEKST 2.1
1010 DIMR$(250):R$(0)="250":LF=1
1020 HX=HO:VX=VE:CR$="#":CM$="@"
1030 REM HX fungeert als regellengte op het scherm
1040 Y=1:X=0:MX=1:NF$="       ":TB=0
1050 SP$="":FORF=0 TOHX:SP$=SP$+" ":NEXTF
1060 MN$=""
1070 MN$=MN$+"----:----I":IFLEN(MN$)<HX+2 THEN1070
1080 MN$=LEFT$(MN$,HX+1)
1200 REM
1210 REM *******  Hoofdmenu  *******
1220 REM
1230 GOSUB100
1240 S1$="   RWLTEKST 2.1   ":S2$=LEFT$(SP$,LEN(S1$))
1250 SR$=S2$:GOSUB1310
1260 SR$=S1$:GOSUB1310
1270 SR$=S2$:GOSUB1310
1280 PRINT
1290 RESTORE:FORF=1 TO17:READSR$:GOSUB1320:NEXTF
1300 PRINTLEFT$(SP$,HX/2);"Geef keuze: ";:GOSUB210:GOTO1330
1310 PRINTLEFT$(SP$,HX/2-11);:GOSUB150:PRINT:RETURN
1320 PRINTLEFT$(SP$,(HX-LEN(SR$))/2);SR$:RETURN
1330 IFIN=83 THEN1800
1340 IF(IN<48)OR(IN>57) THEN GOSUB250:GOTO1230
1350 IFIN=48 THEN GOSUB4030:GOTO1230
1360 IFIN<>50 THEN1420
1370 GOSUB1780
1380 GOSUB360:FORF=1 TOMX
1390 SR$=LEFT$(SP$,20)+R$(F):GOSUB350
1400 GOSUB360:NEXTF
1410 GOSUB360:GOSUB360:GOTO1230
1420 IFIN<>49 THEN1490
1430 GOSUB1780
1440 GOSUB360:FORF=1 TOMX/2
1450 SR$=R$(F):G=F+INT(MX/2)
1460 IFG<=MX THENSR$=SR$+LEFT$(SP$,40-LEN(R$(F)))+R$(G)
1470 GOSUB350:IFLEN(R$(G))<40 THEN GOSUB360
1480 NEXTF:GOSUB360:GOSUB360:GOTO1230
1490 IFIN<>51 THEN1550
1500 GOSUB1780:GOSUB360:FORF=1 TOMX
1510 SR$=R$(F):GOSUB350
1520 IFF/2=INT(F/2) THEN GOSUB360
1530 NEXTF
1540 GOSUB360:GOSUB360:GOTO1230
1550 NF=57-IN
1560 GOSUB120:HO=HX/2:GOSUB110
1570 IFNF<2 THEN GOSUB1780:GOTO1610
1580 PRINT" Geef naam: ";:IP$=NF$
1590 GOSUB120:PRINTIP$:T1$=CM$:T2$=CR$:CR$="*":CM$=""
1600 PL=1:IN=0:GOSUB3050:NF$=IP$:CM$=T1$:CR$=T2$
1610 GOSUB500
1620 IFNF/2=INT(NF/2) THEN GOSUB1670:GOTO1640
1630 GOSUB1730
1640 GOSUB580
1650 GOTO1230
1660 REM invoer
1670 GOSUB540:MX=VAL(IN$)
1680 FORF=1 TOMX
1690 GOSUB540:IFIN$=" " THENIN$=""
1700 R$(F)=IN$:NEXTF
1710 RETURN
1720 REM uitvoer
1730 SR=MX:GOSUB300:GOSUB560
1740 FORF=1 TOMX
1750 SR$=R$(F):IFSR$="" THENSR$=" "
1760 GOSUB560:NEXTF
1770 RETURN
1780 GOSUB120:HO=HX/2:GOSUB110:PRINT"Klaar?     ";:GOSUB210
1790 RETURN
1800 GOSUB120:HO=HX/2:GOSUB110:PRINT"echt? (J/N)";:GOSUB210
1810 IFIN=74 THEN950
1820 IFIN=78 THEN1230
1830 GOSUB250:GOTO1800
2000 REM
2010 REM ***** command routine  ****
2020 REM
2030 IN=0:T1=HO:T2=VE:HO=0:VE=VX:GOSUB110
2040 PRINT"Commando ?   ";LEFT$(SP$,HX-20);
2050 SD=100:GOSUB450
2060 HO=11:VE=VX:GOSUB110:SR$=IN$:GOSUB330
2070 IFIN>31 THENPRINTSR$;
2080 IFIN>0 THEN2150
2090 GOSUB110:PRINT" ";
2100 HO=0:VE=VX:GOSUB110
2110 PRINT"Druk ";CM$;" voor commando's (H=Help)";
2120 PRINTLEFT$(SP$,HX-32);
2130 HO=T1:VE=T2:IFIN<128 THENIN=0
2140 GOTO4060
2150 IFIN=76 THENR$(Y)=RIGHT$(R$(Y),LEN(R$(Y))-1):GOTO2870
2160 IFIN=82 THENR$(Y)=" "+LEFT$(R$(Y),HX):GOTO2870
2170 IFIN=84 THEN2100
2180 IFIN=77 THENRETURN
2190 IFIN<>86 THEN2240
2200 IFY=MX THEN GOSUB250:GOTO2100
2210 MX=MX-1:FORF=Y TOMX:R$(F)=R$(F+1):NEXTF
2220 IF(BV+VX-3>MX)AND(BV>1) THENBV=BV-1:T2=T2+1
2230 GOTO2900
2240 IFIN<>73 THEN2280
2250 IFY=MX THEN GOSUB250:GOTO2100
2260 FORF=MX TOY STEP-1:R$(F+1)=R$(F):NEXTF:MX=MX+1:R$(Y)=""
2270 IN=128:IN$=CHR$(128):GOTO2900
2280 IFIN=66 THENT2=1:Y=1:BV=1:GOTO2900
2290 IFIN<>69 THEN2330
2300 Y=MX:BV=MX-VX+3
2310 IFBV<1 THENBV=1
2320 T2=Y-BV+1:GOTO2900
2330 IFIN<>83 THEN2430
2340 IFMX<2 THEN GOSUB250:GOTO2100
2350 IFMX>5 THENVE=VX:HO=0:GOSUB110:PRINT"sorteren.......";
2360 FORF=MX-1 TO1 STEP-1
2370 FORG=1 TOF
2380 IFR$(G)<R$(G+1) THEN2400
2390 T$=R$(G):R$(G)=R$(G+1):R$(G+1)=T$
2400 NEXTG
2410 NEXTF
2420 GOTO2900
2430 IF(IN<>85)OR(T1>=LEN(R$(Y))) THEN2480
2440 T$=RIGHT$(R$(Y),LEN(R$(Y))-T1)
2450 IFLEN(R$(Y))=40 THENT$=LEFT$(T$,LEN(T$)-1)
2460 IFT1>0 THENR$(Y)=LEFT$(R$(Y),T1)+" "+T$:GOTO2870
2470 R$(Y)=" "+T$:GOTO2870
2480 IFIN=80 THENTB=T1:GOTO2100
2490 IFIN<>67 THEN2540
2500 IFLEFT$(R$(Y),1)<>" " THEN2520
2510 R$(Y)=RIGHT$(R$(Y),LEN(R$(Y))-1):GOTO2500
2520 R$(Y)=LEFT$(SP$,INT((HX+1-LEN(R$(Y)))/2))+R$(Y)
2530 GOTO2870
2540 IFIN<>43 THEN2590
2550 Y=Y-BV:BV=BV+VX-2
2560 IFBV+VX-3>MX THENBV=MX-VX+3:GOSUB250
2570 IFBV<1 THENBV=1
2580 Y=Y+BV:GOTO2900
2590 IFIN<>45 THEN2630
2600 Y=Y-BV:BV=BV-VX+2
2610 IFBV<1 THENBV=1:GOSUB250
2620 GOTO2580
2630 IFIN=78 THENLF=1-LF:GOTO2100
2640 IFIN<>72 THENIN=0:GOSUB250:GOTO2050
2650 HO=0:VE=1:GOSUB110
2660 FORVE=1 TOVX-2:GOSUB110:PRINTSP$;:NEXTVE
2670 VE=1:GOSUB110:PRINT"R naar Rechts schuiven"
2680 VE=2:GOSUB110:PRINT"L naar Links schuiven"
2690 VE=3:GOSUB110:PRINT"M naar Menu"
2700 VE=4:GOSUB110:PRINT"T naar Tekst"
2710 VE=5:GOSUB110:PRINT"H geef Help informatie"
2720 VE=6:GOSUB110:PRINT"V Verwijder regel"
2730 VE=7:GOSUB110:PRINT"I Invoegen regel"
2740 VE=8:GOSUB110:PRINT"B naar Begin tekst"
2750 VE=9:GOSUB110:PRINT"E naar Einde tekst"
2760 VE=10:GOSUB110:PRINT"S Sorteren regels"
2770 VE=11:GOSUB110:PRINT"U Uit elkaar schuiven letters"
2780 VE=12:GOSUB110:PRINT"P stel tab Positie in"
2790 VE=13:GOSUB110:PRINT"C Centreren regel"
2800 VE=14:GOSUB110:PRINT"+ volgende bladzijde"
2810 VE=15:GOSUB110:PRINT"- vorige bladzijde"
2820 VE=16:GOSUB110:PRINT"N regelnummers aan/uit"
2830 SD=200:GOSUB450
2840 FORVE=VX-2 TO1 STEP-1:GOSUB110:PRINTSP$;:NEXTVE
2850 GOSUB2920:GOTO2060
2860 REM print regel & naar tekst
2870 HO=0:VE=T2:GOSUB110
2880 PRINTR$(Y);LEFT$(SP$,HX+1-LEN(R$(Y)));:GOTO2100
2890 REM print scherm & naar tekst
2900 GOSUB2920:GOTO2100
2910 REM print scherm
2920 T=0:HO=0:VE=1:GOSUB110
2930 PRINTR$(BV+T);:IFHX=LEN(R$(BV+T)) THEN2950
2940 PRINTLEFT$(SP$,HX+1-LEN(R$(BV+T)));
2950 T=T+1:IF(T>VX-3)OR(T>MX) THENVE=1:RETURN
2960 VE=VE+1:GOSUB110:GOTO2930
3000 REM
3010 REM *****  input routine  *****
3020 REM
3030 PL=1:GOSUB110:PRINTIP$
3040 IN=0
3050 SC$=MID$(IP$,PL,1)
3060 IFIN<>0 THEN3110
3070 GOSUB110:PRINTCR$;:GOSUB110
3080 SD=1:GOSUB450:IFIN<>0 THEN3110
3090 GOSUB110:PRINTSC$;:GOSUB110
3100 SD=1:GOSUB450:GOTO3060
3110 GOSUB110:PRINTSC$;:GOSUB110
3120 IFIN=28 THEN GOSUB3280:GOTO3040
3130 IFIN=29 THEN GOSUB3330:GOTO3050
3140 IFIN=127 THEN3210
3150 IF(IN<32)OR(IN>126)OR(IN$=CM$) THEN3370
3160 IFPL<2 THENIP$=IN$+RIGHT$(IP$,LEN(IP$)-PL):GOTO3180
3170 IP$=LEFT$(IP$,PL-1)+IN$+RIGHT$(IP$,LEN(IP$)-PL)
3180 GOSUB110:PRINTIN$;
3190 GOSUB3330:GOTO3050
3200 REM delete/backspace
3210 IFPL=1 THEN GOSUB250:GOTO3040
3220 IFPL=2 THENIP$=RIGHT$(IP$,LEN(IP$)-PL+1)+" ":GOTO3240
3230 IP$=LEFT$(IP$,PL-2)+RIGHT$(IP$,LEN(IP$)-PL+1)+" "
3240 GOSUB3280
3250 HO=HO-PL+1:GOSUB110:HO=HO+PL-1
3260 PRINTIP$:GOTO3040
3270 REM cursor left
3280 PL=PL-1:HO=HO-1
3290 IFPL>0 THENRETURN
3300 PL=PL+LEN(IP$):HO=HO+LEN(IP$)
3310 RETURN
3320 REM cursor right
3330 PL=PL+1:HO=HO+1
3340 IFPL<=LEN(IP$) THENIN=0:RETURN
3350 PL=PL-LEN(IP$):HO=HO-LEN(IP$)
3360 IN=30:RETURN
3370 IF(IN=13)OR(IN=30)OR(IN=31)OR(IN$=CM$) THENRETURN
3380 GOTO3040
4000 REM
4010 REM *****  tekst routine  *****
4020 REM
4030 GOSUB100:HO=0:VE=0:GOSUB110:BV=1:Y=1
4040 PRINTMN$:HO=0:VE=VX-1:GOSUB110:PRINTMN$;:VE=1
4050 T1=HO:T2=VE:GOSUB2920:IN=0:GOTO2060
4060 IN=0:SC$=" ":IFHO<LEN(R$(Y)) THENSC$=MID$(R$(Y),HO+1,1)
4070 IFLF=1 THEN4100
4080 T1=HO:T2=VE:HO=HX-7:VE=VX:SR=Y:GOSUB110:GOSUB300
4090 PRINT"RG=";SR$;" ";:HO=T1:VE=T2
4100 IFIN<>0 THEN4160
4110 GOSUB110:PRINTCR$;:GOSUB110
4120 SD=1:GOSUB450:IFIN<>0 THEN4150
4130 GOSUB110:PRINTSC$;:GOSUB110
4140 SD=1:GOSUB450:GOTO4100
4150 GOSUB110:PRINTSC$;:GOSUB110
4160 IFIN<>28 THEN4190
4170 HO=HO-1:IFHO<0 THENHO=HX
4180 GOTO4060
4190 IFIN<>29 THEN4220
4200 HO=HO+1:IFHO>HX THENHO=0
4210 GOTO4060
4220 IFIN<>31 THEN4270
4230 IFY=1 THEN GOSUB250:GOTO4060
4240 Y=Y-1:VE=VE-1:IFVE>0 THEN4060
4250 BV=BV-8:VE=VE+8:IFBV>0 THEN4050
4260 BV=1:VE=Y:GOTO4050
4270 IFIN<>30 THEN4320
4280 IFY=MX THEN GOSUB250:GOTO4060
4290 Y=Y+1:VE=VE+1:IFVE<VX-1 THEN4060
4300 BV=BV+8:VE=VE-8:IFBV+VX-3<=MX THEN4050
4310 BV=BV-9:VE=VE+9:GOTO4300
4320 IFIN<>13 THEN4350
4330 HO=TB:IF(Y=MX)AND(MX<250) THENMX=MX+1:R$(MX)=""
4340 GOTO4280
4350 IFIN$=CM$ THEN2030
4360 IP$=R$(Y)+RIGHT$(SP$,HX+1-LEN(R$(Y)))
4370 PL=HO+1:GOSUB3050
4380 IF(Y=MX)AND(MX<250) THENMX=MX+1:R$(MX)=""
4390 T=HX+1
4400 IFMID$(IP$,T,1)<>" " THEN4420
4410 T=T-1:IFT>0 THEN4400
4420 IFT=0 THENR$(Y)="":GOTO4100
4430 R$(Y)=LEFT$(IP$,T):GOTO4100
25000 DATA"0) Naar tekst",""
25010 DATA"1) Print dubbel"
25020 DATA"2) Print enkel "
25030 DATA"3) Print breed ",""
25040 DATA"4) Save tekst naar disk"
25050 DATA"5) Load tekst van disk ",""
25060 DATA"6) Save tekst naar cassette"
25070 DATA"7) Load tekst van cassette ",""
25080 DATA"8) Save tekst als Basicode 3 file"
25090 DATA"9) Load tekst als Basicode 3 file",""
25100 DATA"S) Stoppen, naar basic",""
30000 REM De cursor staat in CR$ op
30010 REM regel 1020, CM$ staat daar
30020 REM voor de commandotoets.
30030 REM
32000 REM ************************
32010 REM *                      *
32020 REM *     RWLTEKST 2.1     *
32030 REM *                      *
32040 REM * is een programma van *
32050 REM *                      *
32060 REM *    Robbert Wethmar   *
32070 REM *     uit Lelystad     *
32080 REM *                      *
32090 REM * in BASICODE 3 op een *
32100 REM *  SONY HB75p MSX 1.0  *
32110 REM *  vanaf januari 1987  *
32120 REM *     Met dank aan:    *
32130 REM *     J.  Haubrich     *
32140 REM *                      *
32150 REM ************************
32160 REM
32170 REM TROS-RADIO dd 870520
32180 REM incl.corr.tot 870709
32190 REM TROS-RADIO dd 870805

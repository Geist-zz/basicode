
1000 A=200:GOTO20:REM MULTIKLOK
1010 K1=0:SV=8:SP=69:REM GELUID
1020 FR=1:GOSUB280:REM STOPTOETS UIT
1030 GOSUB100:REM TEKSTSCHERM SCHOON
1040 PRINT"MULTIKLOK  door HANS VAN";
1050 PRINT" DONGEN":PRINT
1060 PRINT"Deze klok kent verschei";
1070 PRINT"dene functies:":PRINT
1080 PRINT"KLK = gewone klok"
1090 PRINT"SNL = snel lopende klok"
1100 PRINT"CFR = cijfertijd overho";
1110 PRINT"ren (5 opgaven)"
1120 PRINT"WZR = wijzertijd overho";
1130 PRINT"ren (5 opgaven)"
1140 PRINT"INL = terug naar inleiding"
1150 PRINT:PRINT"Met de cursor";
1160 PRINT"toetsen naar rechts en"
1170 PRINT"naar links kun je steeds";
1180 PRINT" een functie"
1190 PRINT"aanwijzen. Met de cursor";
1200 PRINT"toetsen"
1210 PRINT"omhoog en omlaag";
1220 PRINT" kun je die"
1230 PRINT"functie soms veranderen.";
1240 PRINT" Met de"
1250 PRINT"invoertoets bevestig je";
1260 PRINT" de functie."
1270 PRINT"(Geheime stoptoets: .)"
1280 PRINT:PRINT"De gewone klok";
1290 PRINT" heeft een teller"
1300 PRINT"waarmee je de snelheid";
1310 PRINT" van de"
1320 PRINT"klok kunt instellen";
1330 PRINT", want niet"
1340 PRINT"alle computers werken";
1350 PRINT" even snel.":PRINT
1360 PRINT"Druk op de invoertoets...";
1370 GOSUB200:REM KIJK INVOERTOETS
1380 IFIN$="." THEN950
1390 IFIN<>13 THEN1370
1400 GOSUB10000:REM TEKEN KLOK
1410 REM INLEIDING EN TEKEN KLOK
1420 REM ******************************
2000 HO=0:VE=0:REM POSITIE TEKST
2010 F=1:REM NUMMER GEKOZEN FUNCTIE
2020 T=0:REM KNIPPERTELLER
2030 SR$="KLK SNL CFR WZR INL":CN=0
2040 SD=10:GOSUB650:REM SR$ OP SCHERM
2050 GOSUB450:REM WACHT OP TOETS
2060 IFIN=13 THEN2200:REM GEKOZEN
2070 IFIN=28 THENF=F-1
2080 IFIN=29 THENF=F+1
2090 IFF=0 THENF=1
2100 IFF=6 THENF=5
2110 IFSD>0 THEN2050:REM WACHT NOG
2120 T=T+1:REM TELLER HOGER
2130 IFT=2 THEN2020:REM KNIPPER AAN
2140 X$="":IFF=1 THEN2180
2150 FORX=1 TO4*(F-1)
2160 X$=X$+" "
2170 NEXTX:REM MAAK SPATIES
2180 SR$=X$+MID$(SR$,4*(F-1)+1,3)
2190 CN=1:GOTO2040:REM KNIPPER UIT
2200 SR$="KLK SNL CFR WZR INL"
2210 CN=1:GOSUB650:REM WIS SR$
2220 ONF GOTO3000,4000,5000,6000,1030
2230 REM KIES FUNCTIE
2240 REM ******************************
3000 HH=0:H1=0:H2=0:MM=0:M1=0:REM TIJD
3010 K=202:K1=K:B=12:REM MINUUT-TELLER
3020 TT=MM:RR=3:CN=0:GOSUB11000
3030 TT=5*HH:RR=2:GOSUB11000:REM WZRS
3040 P=1:SD=1:REM POSITIE EN SLAGTIJD
3050 GOSUB12000:REM MAAK STRINGS
3060 FORX=1 TOK:REM EEN MINUUT INVOER
3070 IFP=1 THENSR$="     "+H$
3080 IFP=2 THENSR$="        "+M$
3090 IFP=3 THENSR$="           tel"
3100 IFP=3 THENSR$=SR$+" "+K$
3110 IFP=4 THENSR$="              "
3120 IFP=4 THENSR$=SR$+"     stop"
3130 CN=1:GOSUB650:REM KNIPPER UIT
3140 IFX/2<>INT(X/2) THEN3210
3150 GOSUB12000:REM MAAK STRINGS
3160 X$="klok "+H$+":"+M$+" tel="+K$
3170 X$=X$+" stop":SR$=X$:REM TEKST
3180 CN=0:GOSUB650:REM KNIPPER AAN
3190 IFB>0 THEN GOSUB400:REM GELUID
3200 IFB>0 THENB=B-1:REM UURSLAGEN
3210 GOSUB200:REM KIJK NAAR TOETS
3220 IFIN=28 THENP=P-1
3230 IFIN=29 THENP=P+1
3235 IFIN=46 THEN7000:REM STOPTOETS
3240 IFP=0 THENP=1
3250 IFP=5 THENP=4:REM KIES POSITIE
3260 IFP>1 THEN3360:REM NIET POS. 1
3270 IFIN=30 THENH1=H1-1
3280 IFIN=31 THENH1=H1+1
3290 IFH1=-1 THENH1=0
3300 IFH1=12 THENH1=11
3310 IFIN<>13 THEN3360:REM WIJZIG UUR
3320 CN=1:TT=5*HH:RR=2:GOSUB11000
3330 H2=H1:HH=H1+MM/60
3340 CN=0:TT=5*HH:GOSUB11000
3350 TT=MM:RR=3:GOSUB11000:REM WIJZERS
3360 IFP<>2 THEN3450:REM NIET POS. 2
3370 IFIN=30 THENM1=M1-1
3380 IFIN=31 THENM1=M1+1
3390 IFM1=-1 THENM1=0
3400 IFM1=60 THENM1=59
3410 IFIN<>13 THEN3450:REM WIJZIG MIN
3420 CN=1:TT=MM:RR=3:GOSUB11000
3430 MM=M1:CN=0:TT=MM:GOSUB11000
3440 TT=5*HH:RR=2:GOSUB11000:REM WZRS
3450 IFP<>3 THEN3510:REM NIET POS. 3
3460 IFIN=30 THENK1=K1-1
3470 IFIN=31 THENK1=K1+1
3480 IFK1=9 THENK1=10
3490 IFK1=1000 THENK1=999
3500 IFIN=13 THENK=K1:REM WIJZIG TEL.
3510 IFP<4 THEN3530:REM NIET POS. 4
3520 IFIN=13 THENP=5:X=K:REM STOP
3530 NEXTX:REM EINDE MINUUT INVOER
3540 CN=1:TT=5*HH:RR=2:GOSUB11000
3550 TT=MM:RR=3:GOSUB11000:REM WIJZERS
3560 IFP<5 THEN3600:REM STOP NIET
3570 GOSUB650:REM WIS LAATSTE TEKST
3580 SR$=X$:GOSUB650:REM WIS TEKST
3590 GOTO2000:REM STOP EN KIES FUNCTIE
3600 MM=MM+1:REM VERHOOG MINUTEN
3610 IFMM=60 THENH2=H2+1:B=H2:MM=0
3620 IFH2=12 THENH2=0:REM BEPAAL UUR
3630 HH=H2+MM/60:REM STAND UURWIJZER
3640 K1=K:M1=MM:H1=H2:REM NIEUWE TIJD
3650 CN=0:TT=5*HH:RR=2:GOSUB11000
3660 TT=MM:RR=3:GOSUB11000:REM WIJZERS
3670 SR$="     "+H$+" "+M$+"     "+K$
3680 CN=1:GOSUB650:REM WIS OUDE TIJD
3690 GOTO3060:REM NIEUWE MINUUT
3700 REM GEWONE KLOK
3710 REM ******************************
4000 HH=0:H1=0:MM=0:M1=0:B=12:REM TIJD
4010 X$="00 00":SR$="snel 00:00":SD=1
4020 CN=0:GOSUB650:REM TEKST OP SCHERM
4030 TT=5*HH:RR=2:K=0:GOSUB11000
4040 TT=MM:RR=3:GOSUB11000:REM WIJZERS
4050 SR$="           stop":REM TEKST
4060 CN=K:GOSUB650:REM TEKST OP SCHERM
4070 K=CN+1:IFK=2 THENK=0
4080 IFB>0 THEN GOSUB400:REM GELUID
4090 IFB>0 THENB=B-1:REM UURSLAGEN
4100 GOSUB200:REM KIJK NAAR TOETS
4110 IFIN=13 THEN4260:REM STOP
4115 IFIN=46 THEN7000:REM STOPTOETS
4120 M1=M1+1:REM VERHOOG MINUTEN
4130 IFM1=60 THENM1=0:H1=H1+1:B=H1
4140 IFH1=12 THENH1=0:REM BEPAAL UUR
4150 TT=5*HH:RR=2:CN=1:GOSUB11000
4160 TT=MM:RR=3:GOSUB11000:REM WIJZERS
4170 MM=M1:HH=H1+MM/60:REM TIJD
4180 TT=MM:CN=0:GOSUB11000
4190 TT=5*HH:RR=2:GOSUB11000:REM WZRS
4200 SR$="     "+X$:CN=1
4210 GOSUB650:REM WIS OUDE TIJD
4220 GOSUB12000:REM MAAK STRINGS
4230 X$=H$+" "+M$:SR$="     "+X$
4240 CN=0:GOSUB650:REM NIEUWE TIJD
4250 GOTO4050:REM VOLGENDE MINUUT
4260 GOSUB12000:REM MAAK STRINGS
4270 SR$="snel "+H$+":"+M$+" stop"
4280 CN=1:GOSUB650:REM WIS TEKST
4290 TT=5*HH:RR=2:GOSUB11000
4300 TT=MM:RR=3:GOSUB11000:REM WIJZERS
4310 GOTO2000:REM KIES FUNCTIE
4320 REM SNEL LOPENDE KLOK
4330 REM ******************************
5000 A=0:G=0:REM OPGAVE EN AANTAL GOEDE
5010 P=1:SD=5:REM POSITIE CRSR EN TIJD
5020 SR$="cijfers 00?00 invoer"
5030 CN=0:GOSUB650:REM TEKST OP SCHERM
5040 H1=0:M1=0:REM INVOER
5050 GOSUB260:REM WILLEKEURIGE RV
5060 MM=INT(60*RV):REM MINUTEN
5070 GOSUB260:REM WILLEKEURIGE RV
5080 H2=INT(12*RV):HH=H2+MM/60:REM U.
5090 TT=5*HH:RR=2:CN=0:GOSUB11000
5100 TT=MM:RR=3:GOSUB11000:REM WIJZERS
5110 GOSUB12000:REM MAAK STRINGS
5120 SR$="        "+H$+" "+M$+" invoer"
5130 CN=0:GOSUB650:REM TEKST OP SCHERM
5140 GOSUB200:REM KIJK NAAR TOETS
5150 IFIN=28 THENP=P-1
5160 IFIN=29 THENP=P+1
5165 IFIN=46 THEN7000:REM STOPTOETS
5170 IFP=0 THENP=1
5180 IFP=4 THENP=3:REM KIES POSITIE
5190 IFP=1 THENSR$="        "+H$
5200 IFP=2 THENSR$="           "+M$
5210 IFP=3 THENSR$="           "
5220 IFP=3 THENSR$=SR$+"   invoer"
5230 IFP>1 THEN5280:REM NIET POS. 1
5240 IFIN=30 THENH1=H1-1
5250 IFIN=31 THENH1=H1+1
5260 IFH1=-1 THENH1=11
5270 IFH1=12 THENH1=0:REM WIJZIG UUR
5280 IFP<>2 THEN5330:REM NIET POS. 2
5290 IFIN=30 THENM1=M1-1
5300 IFIN=31 THENM1=M1+1
5310 IFM1=-1 THENM1=59
5320 IFM1=60 THENM1=0:REM WIJZIG MIN.
5330 IFP<>3 THEN5350:REM NIET POS. 3
5340 IFIN=13 THEN5370:REM INGEVOERD
5350 CN=1:GOSUB650:REM WIS TEKST
5360 GOTO5110:REM INVOER NOG BEZIG
5370 SR$="cijfers "+H$+"?"+M$+" invoer"
5380 CN=1:GOSUB650:REM WIS TEKST
5390 A=A+1:REM (WEER) EEN OPGAVE GEDAAN
5400 IF(H1=H2)AND(M1=MM) THEN5530
5410 SR$="FOUT, je deed:":REM FOUT
5420 CN=0:GOSUB650:REM TEKST OP SCHERM
5430 M2=MM:MM=M1:HH=H1+(MM/60)
5440 FORX=0 TO5:CN=X-2*INT(X/2)
5450 TT=HH*5:RR=2:GOSUB11000
5460 TT=MM:RR=3:GOSUB11000:REM WIJZERS
5470 NEXTX:REM FOUTE WIJZERS KNIPPEREN
5480 TT=(H2+M2/60)*5:RR=2:GOSUB11000
5490 TT=M2:RR=3:GOSUB11000:REM WIJZERS
5500 GOSUB650:REM WIS TEKST
5510 IFA=5 THEN5600:REM AFGELOPEN
5520 GOTO5010:REM VOLGENDE OPGAVE
5530 G=G+1:REM OPGAVE GOED GEMAAKT
5540 SR$="GOED, het is "+H$+":"+M$
5550 FORX=0 TO5:CN=X-2*INT(X/2)
5560 GOSUB650:REM TEKST KNIPPERT
5570 GOSUB400:REM GELUID
5580 NEXTX:REM GOEDE INVOER KNIPPERT
5590 M2=MM:GOTO5480:REM VOLGENDE OPG
5600 SR=2*G:GOSUB300:REM MAAK SR$
5610 K$=SR$:IFG<10 THENK$=" "+K$
5620 SR$="beoordeling="+K$+"  stop"
5630 CN=0:GOSUB650:REM TEKST OP SCHERM
5640 K=0:P=0:REM TELLER EN FUNCTIE
5650 SR$="                stop"
5660 CN=K:GOSUB650:REM TEKST KNIPPERT
5670 K=K+1:IFK=2 THENK=0
5680 GOSUB200:REM KIJK NAAR TOETS
5690 IF(IN=30)OR(IN=31) THENP=P+1
5695 IFIN=46 THEN7000:REM STOPTOETS
5700 IFP=2 THENP=0:REM VERANDER KEUZE
5710 IFIN=13 THEN5760
5720 CN=1:GOSUB650:REM WIS TEKST
5730 IFP=1 THEN5650:REM INVOER STOP
5740 SR$="                verder"
5750 GOTO5660:REM INVOER VERDER
5760 SR$=K$+RIGHT$(SR$,LEN(SR$)-14)
5770 SR$="beoordeling="+SR$
5780 CN=1:GOSUB650:REM WIS TEKST
5790 IFP=1 THEN2000:REM KIES FUNCTIE
5800 IFF=3 THEN5000:REM CIJFERS
5810 IFF=4 THEN6000:REM WIJZERS
5820 REM CIJFERTIJD OVERHOREN
5830 REM ******************************
6000 A=0:G=0:REM OPGAVE EN AANTAL GOEDE
6010 P=1:SD=5:REM POSITIE CRSR EN TIJD
6020 GOSUB260:REM WILLEKEURIGE RV
6030 M1=INT(60*RV):REM MINUTEN
6040 GOSUB260:REM WILLEKEURIGE RV
6050 H1=INT(12*RV):REM UREN
6060 GOSUB12000:REM MAAK STRINGS
6070 SR$="wzr "+H$+":"+M$+" uur?min"
6080 SR$=SR$+" invoer"
6090 CN=0:GOSUB650:REM TEKST OP SCHERM
6100 HH=0:MM=0:H2=0:M2=0:REM INVOER
6110 TT=5*HH:RR=2:CN=0:GOSUB11000
6120 TT=MM:RR=3:GOSUB11000:REM WIJZERS
6130 K=0:REM KNIPPERTELLER
6140 GOSUB200:REM KIJK NAAR TOETS
6150 IFIN=28 THENP=P-1
6160 IFIN=29 THENP=P+1
6165 IFIN=46 THEN7000:REM STOPTOETS
6170 IFP=0 THENP=1
6180 IFP=4 THENP=3:REM KIES POSITIE
6190 IFP<>2 THEN6240:REM NIET POS. 2
6200 IFIN=30 THENMM=MM-1
6210 IFIN=31 THENMM=MM+1
6220 IFMM=-1 THENMM=59:HH=HH-1
6230 IFMM=60 THENMM=0:HH=HH+1:REM MIN
6240 IFP<>1 THEN6270:REM NIET POS. 1
6250 IFIN=30 THENHH=HH-1
6260 IFIN=31 THENHH=HH+1
6270 IFHH=-1 THENHH=11
6280 IFHH=12 THENHH=0:REM WIJZIG UUR
6290 IFP<>3 THEN6310:REM NIET POS. 3
6300 IFIN=13 THEN6470:REM INGEVOERD
6310 IF(HH=H2)AND(MM=M2) THEN6370
6320 TT=M2:RR=3:CN=1:GOSUB11000
6330 TT=5*(H2+M2/60):RR=2:GOSUB11000
6340 H2=HH:M2=MM:REM NIEUWE INVOER
6350 TT=5*(HH+MM/60):CN=0:GOSUB11000
6360 TT=MM:RR=3:GOSUB11000:REM WIJZERS
6370 SR$="          uur min invoer"
6380 CN=0:GOSUB650:REM TEKST OP SCHERM
6390 IFP=1 THENSR$="          uur"
6400 IFP=2 THENSR$="             "
6410 IFP=2 THENSR$=SR$+" min"
6420 IFP=3 THENSR$="             "
6430 IFP=3 THENSR$=SR$+"     invoer"
6440 CN=K:GOSUB650:REM TEKST KNIPPERT
6450 K=K+1:IFK=2 THENK=0
6460 GOTO6140:REM INVOER NOG BEZIG
6470 SR$="wzr "+H$+":"+M$+" uur?min"
6480 SR$=SR$+" invoer"
6490 CN=1:GOSUB650:REM WIS TEKST
6500 A=A+1:REM (WEER) EEN OPGAVE GEDAAN
6510 IF(H1=HH)AND(M1=MM) THEN6640
6520 SR$="FOUT, het moest zijn:"
6530 CN=0:GOSUB650:REM TEKST OP SCHERM
6540 MM=M1:HH=H1+MM/60:REM FOUT
6550 FORX=0 TO5:CN=X-2*INT(X/2)
6560 TT=HH*5:RR=2:GOSUB11000
6570 TT=MM:RR=3:GOSUB11000:REM WIJZERS
6580 NEXTX:REM VERBETERING KNIPPERT
6590 TT=(H2+M2/60)*5:RR=2:GOSUB11000
6600 TT=M2:RR=3:GOSUB11000:REM WIJZERS
6610 GOSUB650:REM WIS TEKST
6620 IFA=5 THEN5600:REM AFGELOPEN
6630 GOTO6010:REM VOLGENDE OPGAVE
6640 G=G+1:REM OPGAVE GOED GEMAAKT
6650 SR$="GOED, dit is "+H$+":"+M$
6660 FORX=0 TO5:CN=X-2*INT(X/2)
6670 GOSUB650:REM TEKST KNIPPERT
6680 GOSUB400:REM GELUID
6690 NEXTX:REM GOEDE TIJD KNIPPERT
6700 GOTO6590:REM VOLGENDE OPGAVE
6710 REM TIJD MET WIJZERS OVERHOREN
6720 REM ******************************
7000 CN=1:GOSUB650:REM WIS TEKST
7010 SR$=X$
7020 IFF<>2 THEN7050
7030 SR$="snel "+LEFT$(X$,2)+":"
7040 SR$=SR$+RIGHT$(X$,2)
7050 IFF<>3 THEN7070
7060 SR$="cijfers "+H$+"?"+M$+" invoer"
7070 IFF<>4 THEN7100
7080 SR$="wzr "+H$+":"+M$+" uur?min"
7090 SR$=SR$+" invoer"
7100 GOSUB650:REM WIS TEKST
7110 SR$="beoordeling="+K$:GOSUB650
7120 TT=5*HH:RR=2:GOSUB11000
7130 TT=MM:RR=3:GOSUB11000:REM WIJZERS
7140 GOTO2000:REM KIES FUNCTIE
7150 REM *****************************
10000 GOSUB600:REM GRAFISCH SCHERM
10010 V=0.5+4/VG:H=0.5:REM MIDDEN
10020 CF=0.75/VG:REM CORRECTIE
10030 P2=6.2831853:REM 2 MAAL PI
10040 R=5*VG/12:REM STRAAL KLOK
10050 VE=-R/VG+V:HO=H:CN=0
10060 GOSUB620:REM PLOT PUNT
10070 FORT=0 TOP2 STEP1/R
10080 VE=-COS(T)*R/VG+V
10090 HO=CF*SIN(T)*R+H
10100 GOSUB630:REM TREK LIJNTJE
10110 NEXTT:REM TEKEN CIRKEL
10120 FORT=0 TOP2 STEP1/3
10130 VE=-3*COS(T)/VG+V
10140 HO=3*CF*SIN(T)+H
10150 GOSUB620:REM PLOT PUNT
10160 NEXTT:REM TEKEN MIDDENCIRKELTJE
10170 CN=1:FORT=0 TOP2 STEPP2/60
10180 VE=-4*COS(T)/VG+V
10190 HO=4*CF*SIN(T)+H
10200 GOSUB620:REM WIS PUNT
10210 NEXTT:REM TEKEN MIDDENCIRKELTJE
10220 CN=0:FORT=0 TO59
10230 R=5*VG/12:REM STRAAL KLOK
10240 VE=-COS(P2*T/60)*R/VG+V
10250 HO=CF*SIN(P2*T/60)*R+H
10260 GOSUB620:REM PLOT PUNT
10270 R=4.5*VG/12:REM STRAAL STREEPJES
10280 IFT/5=INT(T/5) THENR=4*VG/12
10290 VE=-COS(P2*T/60)*R/VG+V
10300 HO=CF*SIN(P2*T/60)*R+H
10310 GOSUB630:REM TREK STREEPJE
10320 NEXTT:REM TEKEN STREEPJES
10330 RETURN:REM TEKEN KLOK
10340 REM *****************************
11000 R=RR*VG/12:REM STRAAL WIJZER
11010 T=TT*P2/60:REM POSITIE WIJZER
11020 VE=-4*COS(T)/VG+V
11030 HO=4*CF*SIN(T)+H
11040 GOSUB620:REM PLOT PUNT
11050 VE=-COS(T)*R/VG+V
11060 HO=CF*SIN(T)*R+H
11070 GOSUB630:REM TREK LIJN
11080 HO=CF*SIN(T-P2/80)*(R-5)+H
11090 VE=-COS(T-P2/80)*(R-5)/VG+V
11100 GOSUB630:REM TREK LIJN
11110 HO=CF*SIN(T+P2/80)*(R-5)+H
11120 VE=-COS(T+P2/80)*(R-5)/VG+V
11130 GOSUB630:REM TREK LIJN
11140 HO=CF*SIN(T)*R+H
11150 VE=-COS(T)*R/VG+V
11160 GOSUB630:REM TREK LIJN
11170 HO=0:VE=0:REM TEKSTHOOGTE
11180 RETURN:REM TEKEN WIJZER
11190 REM *****************************
12000 SR=H1:GOSUB300:REM MAAK SR$
12010 H$=SR$:IFH1<10 THENH$="0"+H$
12020 SR=M1:GOSUB300:REM MAAK SR$
12030 M$=SR$:IFM1<10 THENM$="0"+M$
12040 SR=K1:GOSUB300:REM MAAK SR$
12050 K$=SR$:IFK1<100 THENK$="0"+K$
12060 RETURN:REM MAAK STRINGS
12070 REM *****************************
30000 REM 1000 VERPLICHTE REGEL
30010 REM 1010- INLEIDING
30020 REM 2000- KEUZE FUNCTIE
30030 REM 3000- GEWONE KLOK
30040 REM 4000- SNEL LOPENDE KLOK
30050 REM 5000- CIJFERTIJD OVERHOREN
30060 REM 6000- WIJZERTIJD OVERHOREN
30070 REM 7000- GEHEIME STOPTOETS
30090 REM 10000- TEKEN KLOK
30100 REM 11000- TEKEN WIJZER
30110 REM 12000- MAAK STRINGS
30120 REM 30000- INDELING PROGRAMMA
30130 REM 32000- GEGEVENS AUTEUR
30140 REM *****************************
32000 REM
32010 REM MULTIKLOK
32020 REM
32030 REM EEN BASICODE-3 PROGRAMMA DOOR
32040 REM
32050 REM HANS VAN DONGEN
32060 REM HENDRIK VAN VELDEKESTRAAT 34
32070 REM 6367 SB  VOERENDAAL
32080 REM
32090 REM (C)  29-03-1987
32100 REM
32110 REM TROS-RADIO dd.870506

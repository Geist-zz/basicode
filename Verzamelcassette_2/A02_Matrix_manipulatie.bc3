
1000 A=700:GOTO20:REM MATRIX MANIPULATIE
1010 REM
1020 REM HANDLEIDING / MENU
1030 REM
1040 SB=HO:SH=VE:GOSUB100
1050 HO=SB/2-14.5:VE=SH/2-3:GOSUB110
1060 PRINT"*** **  ***  **** ****  ** ** *":VE=VE+1:GOSUB110
1070 PRINT"** * * **  *  **  **  * ** ** *":VE=VE+1:GOSUB110
1080 PRINT"**   * *****  **  ****  **  **":VE=VE+1:GOSUB110
1090 PRINT"**   * **  *  **  ** *  ** * **":VE=VE+1:GOSUB110
1100 PRINT"**   * **  *  **  **  * ** * **":VE=VE+2:GOSUB110
1110 PRINT"M  A  N  I  P  U  L  A  T  I  E":GOSUB2120
1120 GOSUB100:PRINT"-Met dit programma is het mogelijk de"
1130 PRINT"volgende matrix-operaties uit te voeren:"
1140 VE=3:HO=0:GOSUB110
1150 PRINT"    [+] optellen van twee matrices.":PRINT
1160 PRINT"    [=] aftrekken van twee matrices.":PRINT
1170 PRINT"    [*] vermenigvuldigen van twee ma-"
1180 PRINT"        trices.":PRINT
1190 PRINT"    [^] machtsverheffen van een matrix.":PRINT
1200 PRINT"    [S] berekenen van een oplossingsma-"
1210 PRINT"        trix met bijbehorende diameter.":PRINT
1220 PRINT"    [I] inverteren van een matrix.":PRINT
1230 PRINT"    [W] wissen van een matrix.":PRINT
1240 PRINT"    [T] transponeren van een matrix.":PRINT
1250 PRINT"    [O] overzetten van het resultaat."
1260 GOSUB2120:GOSUB100
1270 PRINT"-Het invoeren van de elementen gaat met"
1280 PRINT"behulp van de cursor-besturingstoetsen."
1290 PRINT"-Buiten de reeds genoemde toetsen, kun-"
1300 PRINT"nen de volgende toetsen gebruikt worden:"
1310 PRINT"    [0] [1] [2] [3] [4]":PRINT
1320 PRINT"    [5] [6] [7] [8] [9]":PRINT
1330 PRINT"    [-] voor negatieve elementen.":PRINT
1340 PRINT"    [.] voor decimalen.":PRINT
1350 PRINT"    [DEL/SPC] om fouten te herstellen.":PRINT
1360 PRINT"    [RETURN] volgende regel.":PRINT
1370 PRINT"    [/] volgende matrix.":PRINT
1380 PRINT"    [P] schermafdruk op de printer.":PRINT
1390 PRINT"    [E] programma beeindigen.":PRINT
1400 GOSUB2120:GOSUB100
1500 REM
1510 REM ELEMENT-GROOTTE VASTSTELLEN
1520 REM
1530 HO=SB/2-19.5:VE=1:GOSUB110
1540 SR$="Maximale grootte van de elementen ?"
1550 GOSUB150:HO=SB/2-7.5:VE=3:GOSUB110
1560 PRINT"(aantal tekens)":HO=HO+6:VE=5
1570 GOSUB110:PRINT">.<"
1580 GOSUB200:IF(IN$<"1")OR(IN$>"9") THEN1580
1590 EL=VAL(IN$):HO=HO+1:GOSUB110:PRINTIN$
1600 REM
1610 REM STARTGROOTTE MATRICES
1620 REM
1630 HO=SB/2-9:VE=10:GOSUB110:PRINT"Grootte matrices ?";
1640 VE=12:GOSUB110:PRINT"aantal rijen    >.<";
1650 VE=14:GOSUB110:PRINT"aantal kolommen >.<";:HO=HO+1
1660 VE=16:GOSUB110:PRINT"[return] na keuze":VE=12
1670 HO=HO+16:GOSUB110
1680 GOSUB200:IFIN=13 THEN1710
1690 IF((IN$<"1")OR(IN$>"9")) THEN1680
1700 GOSUB110:PRINTIN$;:VE=26-VE:GOTO1680
1710 VE=16:HO=HO-16:GOSUB110:PRINT"                 "
1720 VE=12:HO=HO+16:GOSUB220:VK=VAL(CHR$(IN))
1730 VE=14:GOSUB220:HK=VAL(CHR$(IN))
1800 REM
1810 REM INITIALISATIE
1820 REM
1830 MR=INT(SH/2):MK=INT((SB-1)/(EL+1))
1840 IFHK>MK THENEL=EL-1:GOTO1830
1850 SR=EL:GOSUB300:HO=SB/2-.5:VE=5:GOSUB110:PRINTSR$;
1860 DIMMA(MR-1,MK*5-1),R(4),K(4):D=10
1870 DIMTE$(9),EM$(8):X=3:Y=1:MA=0:TE=0
1880 FORI=0 TO9:READTE$(I):NEXTI:N=0
1890 FORI=0 TO8:READEM$(I):NEXTI
1900 FORI=0 TO75:B$=B$+" ":NEXTI
1910 HO=SB/2-19.5:VE=SH-1
1920 SR$="   [_] om de matrix op te slaan   "
1930 GOSUB110:GOSUB150:GOSUB110:SD=D:GOSUB450
1940 SD=D:PRINT"   ";SR$;"   ";:IFIN<>0 THEN2000
1950 GOSUB450:IFIN<>0 THEN2000
1960 D=D-1:IF(IN=0)AND(D>0) THEN1920
2000 REM
2010 REM SCHERMOPBOUW
2020 REM
2030 GOSUB100:VE=0:HO=2:GOSUB110
2040 FORSR=1 TOMK:GOSUB300:GOSUB120:HO=HO+EL:GOSUB110
2050 PRINTSR$;:GOSUB120:IFSR>=9 THENHO=HO-1:GOSUB110
2060 NEXTSR:VE=-1
2070 FORSR=1 TOMR:HO=1:VE=VE+2:GOSUB110:GOSUB300
2080 IFSR>9 THENHO=0:GOSUB110
2090 PRINTSR$;:HO=2:GOSUB110:IF((SR>VK)OR(HK<2)) THEN2110
2100 FORI=1 TOHK-1:HO=HO+EL+1:GOSUB110:PRINT",";:NEXTI
2110 HO=1:NEXTSR:TE=0:GOSUB9000:GOTO2500
2120 HO=INT(SB/2-11):VE=SH:SR$="toets voor vervolg"
2130 GOSUB110:GOSUB150:D=0
2140 GOSUB200:IFIN=0 THEN2140
2150 RETURN
2200 REM
2210 REM KOMMA'S TOEVOEGEN
2220 REM
2230 H=2+HK*(EL+1):HK=INT((X-2)/(EL+1))+1
2240 FORVE=1 TOVK*2 STEP2
2250 FORHO=H TO2+(HK-1)*(EL+1) STEPEL+1
2260 GOSUB110:PRINT",";:NEXTHO:NEXTVE:RETURN
2300 REM
2310 REM VERTIKAAL KOMMA'S TOEVOEGEN
2320 REM
2330 FORVE=1+VK*2 TOY STEP2
2340 FORHO=3+EL TO2+(HK-1)*(EL+1) STEPEL+1
2350 GOSUB110:PRINT",";:NEXTHO:NEXTVE:VK=(Y+1)/2:RETURN
2500 REM
2510 REM INVOER VIA TOETSENBORD
2520 REM
2530 GOSUB200
2540 IFIN=13 THENX=3:IN=30
2550 IFIN=127 THEN GOSUB110:PRINT" ";:IN=28
2560 IFIN$="/" THEN GOSUB3600
2570 IFIN$="+" THEN GOSUB4000
2580 IFIN$="=" THEN GOSUB4200
2590 IFIN$="*" THEN GOSUB4400
2600 IFIN$="^" THEN GOSUB4600
2610 IFIN=83 THEN GOSUB5000:REM S
2620 IFIN=73 THEN GOSUB5500:REM I
2630 IFIN=87 THEN GOSUB6000:REM W
2640 IFIN=84 THEN GOSUB6200:REM T
2650 IFIN=79 THEN GOSUB6400:REM O
2660 IFIN=80 THEN GOSUB6500:REM P
2670 IFIN=69 THEN GOSUB7500:IFKO=1 THEN1000:REM E
2680 IF(IN$="_")AND(TE<2) THEN GOSUB8000:GOSUB9000
2690 IFIN=28 THENX=X-1
2700 IFINT((X-2)/(EL+1))=(X-2)/(EL+1) THENX=X-1
2710 IFX<3 THENX=3
2720 IFIN=29 THENX=X+1
2730 IFX>MK*(EL+1)+1 THENX=X-1
2740 IFINT((X-2)/(EL+1))<>(X-2)/(EL+1) THEN2760
2750 X=X+1
2760 IFIN=31 THENY=Y-2
2770 IFIN<>30 THEN2790
2780 Y=Y+2
2790 IFY>MR*2-1 THENY=Y-2:GOTO2810
2800 IFY<1 THENY=Y+2
2810 IF(IN>44)AND(IN<>47)AND(IN<58) THEN2890
2820 IFIN=32 THEN2890
2830 HO=X:VE=Y:GOSUB220:GOSUB110:H9=IN
2840 IFIN=32 THENPRINT"?";:GOTO2860
2850 PRINT" ";
2860 GOSUB110:SD=3:GOSUB450:PRINTCHR$(H9);
2870 IFIN=0 THEN2500
2880 GOTO2540
2890 HO=X:VE=Y:GOSUB110
2900 IF(IN=45)OR(IN=46) THEN GOSUB2980:GOTO2920
2910 IF((IN$<"0")OR(IN$>"9"))AND(IN$<>" ") THEN2500
2920 PRINTIN$;:IFX>=MK*(EL+1)+1 THEN2950
2930 X=X+1:IFINT((X-2)/(EL+1))<>(X-2)/(EL+1) THEN2950
2940 X=X+1
2950 IF((X-3)/(EL+1))>HK THEN GOSUB2200
2960 IF(Y+1)/2>VK THEN GOSUB2300
2970 GOTO2500
2980 KO=1:H=3+INT((X-3)/(EL+1))*(EL+1)
2990 IFIN=45 THEN3060
3000 FORHO=H TOH+EL:VE=Y:GOSUB220:IFIN=46 THENKO=0
3010 NEXTHO:IFKO=1 THEN3050
3020 IN$="":X=X-1
3030 IFINT((X-2)/(EL+1))=(X-2)/(EL+1) THENX=X-1
3040 IFX<3 THENX=3
3050 HO=X:VE=Y:GOSUB110:RETURN
3060 IFH=X THEN3050
3070 FORHO=H TOX-1:VE=Y:GOSUB220:IFIN<>32 THENKO=0
3080 GOTO3010
3500 REM
3510 REM ELEMENT-GROOTTE CONTROLE
3520 REM
3530 KO=1:ME$=SR$:SR=INT(SR):GOSUB300
3540 IFLEN(SR$)>EL THENEM=1:GOSUB9500:KO=0:GOTO3560
3550 SR$=LEFT$(ME$,EL):SR=VAL(SR$)
3560 RETURN
3600 REM
3610 REM VOLGENDE MATRIX
3620 REM
3630 GOSUB3800:MA=1-MA
3640 GOSUB3700:TE=MA:RE=MA:GOSUB9000:X=3:Y=1:RETURN
3700 REM
3710 REM PRINT MATRIX
3720 REM
3730 IFR(MA)*K(MA)=0 THENRETURN
3740 FORR=1 TOR(MA):VE=R*2-1
3750 FORK=1 TOK(MA):HO=K*(EL+1)+2-EL
3760 SR=MA(R-1,MK*MA+K-1):GOSUB300:IFLEN(SR$)=EL THEN3790
3770 GOSUB3500:IFKO=0 THENRETURN
3780 SR$=LEFT$(B$,EL-LEN(SR$))+SR$
3790 GOSUB110:PRINTSR$;:NEXTK:NEXTR:RETURN
3800 REM
3810 REM SCHERM LEGEN
3820 REM
3830 FORR=1 TOMR:VE=R*2-1
3840 FORK=1 TOMK:HO=K*(EL+1)+2-EL
3850 GOSUB110:PRINTLEFT$(B$,EL);:NEXTK:NEXTR:RETURN
3900 REM
3910 REM EENHEIDSMATRIX
3920 REM
3930 FORR=1 TOR(MA)
3940 FORK=1 TOK(MA)
3950 IFR=K THENMA(R-1,MK*2+K-1)=1:GOTO3970
3960 MA(R-1,MK*2+K-1)=0
3970 NEXTK:NEXTR:R(2)=R(MA):K(2)=R(MA):RETURN
4000 REM
4010 REM MAT 1 + MAT 2 = MAT 3
4020 REM
4030 IF(R(0)=0)OR(R(1)=0) THENEM=8:GOSUB9500:RETURN
4040 IF(R(0)=R(1))AND(K(0)=K(1)) THEN4060
4050 EM=2:GOSUB9500:RETURN
4060 FORR=1 TOR(0):FORK=1 TOK(0)
4070 SR=MA(R-1,K-1)+MA(R-1,MK+K-1):GOSUB300
4080 GOSUB3500:IFKO=0 THENK=K(0):R=R(0)
4090 MA(R-1,MK*2+K-1)=SR:NEXTK:NEXTR:IFKO=0 THENRETURN
4100 GOSUB3800:R(2)=R(0):K(2)=K(0):TE=2:GOSUB9000
4110 Q=MA:MA=2:GOSUB3700:RE=2:MA=Q:RETURN
4200 REM
4210 REM MAT 1 - MAT 2 = MAT 3
4220 REM
4230 IF(R(0)=0)OR(R(1)=0) THENEM=8:GOSUB9500:RETURN
4240 IF(R(0)=R(1))AND(K(0)=K(1)) THEN4260
4250 EM=2:GOSUB9500:RETURN
4260 FORR=1 TOR(0):FORK=1 TOK(0)
4270 SR=MA(R-1,K-1)-MA(R-1,MK+K-1):GOSUB300
4280 GOSUB3500:IFKO=0 THENK=K(0):R=R(0)
4290 MA(R-1,MK*2+K-1)=SR:NEXTK:NEXTR:IFKO=0 THENRETURN
4300 GOSUB3800:R(2)=R(0):K(2)=K(0):TE=3:GOSUB9000
4310 Q=MA:MA=2:GOSUB3700:RE=2:MA=Q:RETURN
4400 REM
4410 REM MAT 1 * MAT 2 = MAT 3
4420 REM
4430 IF(R(0)=0)OR(R(1)=0) THENEM=8:GOSUB9500:RETURN
4440 IFR(1)<>K(0) THENEM=3:GOSUB9500:RETURN
4450 FORR=1 TOR(0):FORK=1 TOK(1)
4460 SR=0:FORS=1 TOK(0)
4470 SR=SR+MA(R-1,S-1)*MA(S-1,MK+K-1):GOSUB300
4480 GOSUB3500:IFKO=0 THENS=K(0):K=K(1):R=R(0):GOTO4500
4490 MA(R-1,MK*2+K-1)=SR
4500 NEXTS:NEXTK:NEXTR:IFKO=0 THENRETURN
4510 R(2)=R(0):K(2)=K(1)
4520 GOSUB3800:TE=4:GOSUB9000:Q=MA:MA=2:GOSUB3700:RE=2
4530 MA=Q:RETURN
4600 REM
4610 REM MATRIX 1,2 TOT DE N-DE MACHT
4620 REM
4630 SO=0:IFR(MA)=0 THENEM=8:GOSUB9500:RETURN
4640 IFR(MA)<>K(MA) THENEM=4:GOSUB9500:RETURN
4650 HO=0:VE=SH:GOSUB110
4660 PRINT"welke macht ? >  < (+[return])         ";
4670 HO=15:GOSUB110:PRINT"..";
4680 GOSUB200:IFIN=13 THEN4710
4690 IF((IN$<"0")OR(IN$>"9")) THEN4680
4700 GOSUB110:PRINTIN$;:HO=31-HO:GOTO4680
4710 HO=15:GOSUB220:GE$=CHR$(IN):HO=16:GOSUB220
4720 GE$=GE$+CHR$(IN):N=VAL(GE$):IFN>19 THEN4670
4730 Q=MA:GOSUB3900:IFN=0 THENMA=2
4740 FORR=1 TOR(MA):FORK=1 TOK(MA)
4750 MA(R-1,MK*3+K-1)=MA(R-1,MK*MA+K-1):IFSO=0 THEN4780
4760 MA(R-1,MK*4+K-1)=MA(R-1,MK*3+K-1):IFN=0 THEN4780
4770 MA(R-1,MK*4+K-1)=MA(R-1,MK*4+K-1)+MA(R-1,MK*2+K-1)
4780 NEXTK:NEXTR:MA=Q:IF(N=0)OR(N=1) THEN4840
4790 FORM=2 TON
4800 FORR=1 TOR(MA):FORK=1 TOK(MA)
4810 MA(R-1,MK*2+K-1)=MA(R-1,MK*3+K-1)
4820 NEXTK:NEXTR:GOSUB8500:IFKO=0 THENM=N
4830 NEXTM:IFKO=0 THENRETURN
4840 GOSUB3800:R(3)=R(MA):K(3)=K(MA):TE=5:IFSO=1 THENTE=6
4850 GOSUB9000:IFSO=0 THENMA=3:GOTO4870
4860 R(4)=R(MA):K(4)=K(MA):MA=4
4870 GOSUB3700:MA=Q:RE=3:IFSO=1 THENRE=4
4880 RETURN
5000 REM
5010 REM OPLOSSING MATRIX 1,2 = MAT 4
5020 REM
5030 SO=1:IFR(MA)=0 THENEM=8:GOSUB9500:RETURN
5040 HO=0:VE=SH:IFR(MA)<>K(MA) THENEM=4:GOSUB9500:RETURN
5050 GOSUB110:PRINT"[O] oplossing of [S] stappen ? >.<";
5060 GOSUB200:IF(IN<>79)AND(IN<>83) THEN5060
5070 HO=32:GOSUB110:PRINTIN$;:IFIN=83 THEN4650
5080 R(3)=R(MA):K(3)=K(MA):R(4)=R(MA):K(4)=K(MA)
5090 N=1:GOSUB3900:FORR=1 TOR(MA):FORK=1 TOK(MA)
5100 MA(R-1,MK*3+K-1)=MA(R-1,MK*MA+K-1)
5110 MA(R-1,MK*4+K-1)=MA(R-1,MK*2+K-1)+MA(R-1,MK*3+K-1)
5120 MA(R-1,MK*2+K-1)=MA(R-1,MK*3+K-1):NEXTK:NEXTR
5130 FORR=1 TOR(4):FORK=1 TOK(4)
5140 IFMA(R-1,MK*4+K-1)=0 THENKO=0:R=R(4):K=K(4)
5150 NEXTK:NEXTR:IFKO=1 THEN5220
5160 NT=0:FORR=1 TOR(3):FORK=1 TOR(3):IFR=K THEN5180
5170 IFMA(R-1,MK*3+K-1)=0 THENNT=NT+1
5180 NEXTK:NEXTR
5190 IFNT=R(3)*(K(3)-1) THENEM=5:GOSUB9500:RETURN
5200 N=N+1:GOSUB8500:IFKO=0 THENRETURN
5210 FORR=1 TOR(MA):FORK=1 TOK(MA):GOTO5120
5220 Q=MA:TE=7:GOSUB9000:GOSUB3800
5230 MA=4:GOSUB3700:RE=4:MA=Q:RETURN
5500 REM
5510 REM INVERSE MATRIX 1,2 = MAT 4
5520 REM
5530 IFR(MA)=0 THENEM=8:GOSUB9500:RETURN
5540 IF(R(MA)<>K(MA)) THENEM=4:GOSUB9500:RETURN
5550 GOSUB3900:K0=K(MA)
5560 FORR=1 TOK0:FORK=1 TOK0
5570 MA(R-1,MK*3+K-1)=MA(R-1,MK*MA+K-1)
5580 NEXTK:NEXTR:R(3)=K0:K(3)=K0:Q=MA
5590 FORK=1 TOK0:M1=K:M=ABS(MA(K-1,MK*3+K-1))
5600 IFK=K0 THEN5640
5610 FORI=K+1 TOK0:IFABS(MA(I-1,MK*3+K-1))<=M THEN5630
5620 M=ABS(MA(I-1,MK*3+K-1)):M1=I
5630 NEXTI
5640 IFM=0 THENEM=6:MA=Q:GOSUB9500:RETURN
5650 IFM1=K THEN5700
5660 MA=3:FORJ0=K TOK0*2:J=J0:IFJ>K0 THENJ=J-K0:MA=2
5670 W=MA(K-1,MK*MA+J-1)
5680 MA(K-1,MK*MA+J-1)=MA(M1-1,MK*MA+J-1)
5690 MA(M1-1,MK*MA+J-1)=W:NEXTJ0
5700 D1=MA(K-1,MK*3+K-1)
5710 MA=3:FORJ0=K+1 TOK0*2:J=J0:IFJ>K0 THENJ=J-K0:MA=2
5720 MA(K-1,MK*MA+J-1)=MA(K-1,MK*MA+J-1)/D1:NEXTJ0
5730 FORI=1 TOK0:IFI=K THEN5780
5740 D2=MA(I-1,MK*3+K-1)
5750 MA=3:FORJ0=K+1 TOK0*2:J=J0:IFJ>K0 THENJ=J-K0:MA=2
5760 W=MA(I-1,MK*MA+J-1):W=W-D2*MA(K-1,MK*MA+J-1)
5770 MA(I-1,MK*MA+J-1)=W:NEXTJ0
5780 NEXTI:NEXTK:GOSUB3800:GOSUB3700:MA=Q:TE=8:GOSUB9000
5790 RE=2:RETURN
6000 REM
6010 REM MATRIX WISSEN
6020 REM
6030 IFR(MA)=0 THENEM=8:GOSUB9500:RETURN
6040 FORR=1 TOR(MA):FORK=1 TOK(MA)
6050 MA(R-1,MA*MK+K-1)=0:NEXTK:NEXTR
6060 GOSUB3800:GOSUB3700:RETURN
6200 REM
6210 REM TRANSPONEREN MAT 1,2 = MAT 3
6220 REM
6230 IFR(MA)=0 THENEM=8:GOSUB9500:RETURN
6240 IF(R(MA)>MK)OR(K(MA)>MR) THENEM=7:GOSUB9500:RETURN
6250 FORR=1 TOR(MA):FORK=1 TOK(MA)
6260 MA(K-1,MK*2+R-1)=MA(R-1,MK*MA+K-1)
6270 NEXTK:NEXTR:R(2)=K(MA):K(2)=R(MA):RE=2
6280 Q=MA:MA=2:GOSUB3800:GOSUB3700:MA=Q:TE=9:GOSUB9000
6290 IF((HK-1=VK)OR((VK=MR)AND(HK=MK))) THENRETURN
6300 REM
6310 REM KOMMA CORRECTIE
6320 REM
6330 MX=VK:IF(HK-1)>MX THENMX=HK-1
6340 FORVE=1 TOMX*2 STEP2:FORHO=3+EL TO2+MX*(EL+1) STEPEL+1
6350 GOSUB110:PRINT",";:NEXTHO:NEXTVE:HK=MX+1:VK=MX:RETURN
6400 REM
6410 REM RESULTAAT OVERZETTEN
6420 REM
6430 HO=0:VE=SH:GOSUB110:PRINT"naar matrix [1] of [2] >.<";
6440 PRINTLEFT$(B$,11);:HO=24:VE=SH:GOSUB110
6450 GOSUB200:IF(IN$<"1")OR(IN$>"2") THEN6450
6460 PRINTIN$;:MA=VAL(IN$)-1:FORR=1 TOR(RE):FORK=1 TOK(RE)
6470 MA(R-1,MK*MA+K-1)=MA(R-1,MK*RE+K-1):NEXTK:NEXTR
6480 R(MA)=R(RE):K(MA)=K(RE):TE=MA
6490 GOSUB9000:RETURN
6500 REM
6510 REM SCHERMAFDRUK OP DE PRINTER
6520 REM
6530 HO=0:VE=SH:GOSUB110
6540 PRINT"[h] hele scherm. [m] alleen matrix.    ";
6550 GOSUB200:IFIN=77 THEN6630
6560 IFIN<>72 THEN6550
6570 GOSUB7000:GOSUB9000
6580 FORH=0 TOSH
6590 VE=H:SR$=TA$
6600 FORB=0 TOSB:HO=B:GOSUB220:SR$=SR$+CHR$(IN):NEXTB
6610 GOSUB350:GOSUB360:NEXTH
6620 RETURN
6630 GOSUB7000:GOSUB9000:FORH=0 TO2*R(MA)-1
6640 VE=H:SR$=TA$:FORB=0 TO2+(EL+1)*K(MA):HO=B:GOSUB220
6650 SR$=SR$+CHR$(IN):NEXTB:GOSUB350:GOSUB360:NEXTH
6660 RETURN
7000 REM
7010 REM TABULATOR
7020 REM
7030 HO=0:VE=SH:GOSUB110
7040 PRINT"tabulator (0...75) ? >  <          ";
7050 HO=22:GOSUB110:PRINT"..";
7060 GOSUB200:IFIN=13 THEN7090
7070 IF(IN$<"0")OR(IN$>"9") THEN7060
7080 GOSUB110:PRINTIN$;:HO=45-HO:GOTO7060
7090 HO=22:GOSUB220:GE$=CHR$(IN):HO=23:GOSUB220
7100 GE$=GE$+CHR$(IN):TA=VAL(GE$):IFTA>75 THEN7050
7110 TA$=LEFT$(B$,TA):RETURN
7500 REM
7510 REM EINDE
7520 REM
7530 HO=0:VE=SH:GOSUB110:PRINTLEFT$(B$,SB);
7540 HO=SB/2-8:VE=SH:GOSUB110
7550 PRINT"stoppen [j]/[n]";
7560 GOSUB200:IFIN=74 THEN950
7570 IFIN<>78 THEN7560
7580 HO=SB/2-12:GOSUB110:PRINT"opnieuw beginnen [j]/[n]";
7590 GOSUB200:IFIN=74 THENKO=1:RETURN
7600 IFIN<>78 THEN7590
7610 KO=0:GOSUB9000:RETURN
8000 REM
8010 REM INVOERCONTROLE + OMZETTING
8020 REM
8030 HO=5:VE=SH:GOSUB110:PRINT" de matrix wordt opgeslagen";
8040 R=0:VE=-1
8050 HO=2:VE=VE+2:R=R+1:K=0:IFR>MR THENIN=32:K=1:GOTO8120
8060 HO=HO+EL:K=K+1:GE$=""
8070 IFK>MK THENK=MK+1:GOTO8110
8080 GOSUB220
8090 H=HO:FORHO=H+1-EL TOH:GOSUB220:GE$=GE$+CHR$(IN)
8100 NEXTHO:IFGE$<>LEFT$(B$,LEN(GE$)) THEN8150
8110 IFR=1 THENK(MA)=K-1:GOTO8050
8120 IFK=1 THEN8160
8130 IFK-1=K(MA) THEN8050
8140 EM=0:GOSUB9500:KO=0:RETURN
8150 MA(R-1,MK*MA+K-1)=VAL(GE$):GOTO8060
8160 R(MA)=R-1:KO=1:RE=MA:GOSUB3800:GOSUB3700:RETURN
8500 REM
8510 REM MAT 1,2 * MAT 3 = MAT 4
8520 REM
8530 FORR=1 TOR(MA):FORK=1 TOK(MA)
8540 SR=0:FORS=1 TOK(MA)
8550 SR=SR+MA(R-1,MK*MA+S-1)*MA(S-1,MK*2+K-1):GOSUB300
8560 GOSUB3500:IFKO=0 THENS=K(MA):K=K(MA):R=R(MA):GOTO8590
8570 NEXTS:MA(R-1,MK*3+K-1)=SR
8580 IFSO=1 THENMA(R-1,MK*4+K-1)=MA(R-1,MK*4+K-1)+SR
8590 NEXTK:NEXTR
8600 RETURN
9000 REM
9010 REM PRINT SCHERMTEKST
9020 REM
9030 HO=0:VE=SH:GOSUB110:FORI=1 TOSB:PRINT" ";:NEXTI
9040 IFTE<5 THEN9140
9050 SR=MA+1:GOSUB300
9060 L$=LEFT$(TE$(5),7)+SR$+MID$(TE$(5),9,8)
9070 SR=N:GOSUB300:R$=SR$+RIGHT$(TE$(5),9)
9080 TE$(5)=L$+R$:SR=MA+1:GOSUB300
9090 TE$(7)=LEFT$(TE$(7),17)+SR$+MID$(TE$(7),19,12):SR=N
9100 GOSUB300:TE$(7)=TE$(7)+SR$
9110 TE$(6)=SR$+LEFT$(RIGHT$(TE$(6),26),25):SR=MA+1:GOSUB300
9120 TE$(6)=TE$(6)+SR$:TE$(8)=LEFT$(TE$(8),19)+SR$
9130 TE$(9)=LEFT$(TE$(9),7)+SR$+RIGHT$(TE$(9),15)
9140 HO=((SB+1)-LEN(TE$(TE)))/2:VE=SH
9150 GOSUB110:PRINTTE$(TE);:IN$="":IN=0:RETURN
9500 REM
9510 REM PRINT FOUTMELDING
9520 REM
9530 HO=0:VE=SH:GOSUB110:FORI=1 TOSB:PRINT" ";:NEXTI
9540 HO=((SB+1)-LEN(EM$(EM)))/2:VE=SH
9550 GOSUB110:PRINTEM$(EM);:GOSUB250
9560 SD=15:GOSUB450
9570 GOSUB9000:RETURN
25000 DATA"M  A  T  R  I  X  1"
25010 DATA"M  A  T  R  I  X  2"
25020 DATA"MATRIX 1 + MATRIX 2"
25030 DATA"MATRIX 1 - MATRIX 2"
25040 DATA"MATRIX 1 * MATRIX 2"
25050 DATA"MATRIX   tot de -de macht"
25060 DATA"-staps matrix van MATRIX  "
25070 DATA"oplossing MATRIX  , diameter= "
25080 DATA"inverse van MATRIX "
25090 DATA"MATRIX  getransponeerd"
25100 DATA"**  matrix niet compleet  **"
25110 DATA"**  element te groot  **"
25120 DATA"**  ongelijke matrix-grootte  **"
25130 DATA"**  vermenigvuldiging onmogelijk  **"
25140 DATA"**  ongelijk aantal rijen, kolommen  **"
25150 DATA"**  onoplosbare matrix  **"
25160 DATA"**  geen inverse mogelijk  **"
25170 DATA"**  matrix te groot  **"
25180 DATA"**  matrix nog niet opgeslagen  **"
30000 REM """"""""""""""""""""""
30010 REM "                    "
30020 REM " MATRIX MANIPULATIE "
30030 REM "                    "
30040 REM "   geprogrammeerd   "
30050 REM "                    "
30060 REM " op de Commodore 64 "
30070 REM "                    "
32000 REM """"""""""""""""""""""
32010 REM "                    "
32020 REM " door:    Jan Stout "
32030 REM "                    "
32040 REM "     Pomperschans 9 "
32050 REM "                    "
32060 REM " 5595 AS  LEENDE NB "
32070 REM "                    "
32080 REM """"""""""""""""""""""
32100 REM TROS-RADIO dd 870225


1000 A=200:GOTO20:REM "KALENDER/BC3"
1010 DIMM$(12),L(12),V(12):REM maand,lengte,verschuiving 
1020 DIMK$(7):REM kalenderstrings
1030 GOSUB1120:REM lees DATA in
1040 GOSUB2000:REM inleiding
1050 GOSUB2140:REM input jaar en maand
1060 GOSUB1200:REM berekeningen
1070 GOSUB1370:REM toon de kalender
1080 GOSUB210
1090 IFIN=78 THEN950
1100 IFIN=80 THEN GOSUB1590:GOTO1080
1110 GOTO1050
1120 REM=== LEES DATA IN ===
1130 LL$="--------------------------------"
1140 W$="ZOMADIWODOVRZA"
1150 RESTORE
1160 FORN=1 TO12
1170 READM$(N),L(N),V(N)
1180 NEXTN
1190 RETURN
1200 REM=== BEREKENINGEN ===
1210 D=4:REM dagcijfer jan 1600
1220 D=D+(JR-1600)+INT((JR-1601)/4)
1230 IFD>6 THEND=D-7:GOTO1230
1240 C=D+V(MD):REM lege aanloopdagen
1250 IFC>6 THENC=C-7:GOTO1250
1260 REM schrikkeljaar of niet?
1270 IFJR/400=INT(JR/400) THENS=1:GOTO1310
1280 IFJR/100=INT(JR/100) THENS=0:GOTO1310
1290 IFJR/4=INT(JR/4) THENS=1:GOTO1310
1300 S=0:REM geen schrikkeljaar
1310 L(2)=28:REM februari
1320 IF(MD=2)AND(S=1) THENL(2)=29
1330 IF(S=1)AND(MD>2) THENC=C+1
1340 RETURN
1350 REM=== KALENDER OP HET SCHERM ===
1360 FORN=0 TO7:K$(N)="":NEXTN
1370 GOSUB600:SR=JR:GOSUB300
1380 CN=0:HO=0.1:VE=.05
1390 SR$=M$(MD)+"  "+SR$:GOSUB650
1400 K$(0)=SR$
1410 FORM=1 TO7:REM ZO, MA, DI
1420 VE=0.1+M*0.1:SR$=MID$(W$,2*M-1,2)
1430 K$(M)=SR$
1440 GOSUB650:VE=VE+0.05:GOSUB620
1450 HO=0.9:GOSUB630:HO=0.1
1460 NEXTM
1470 IFL(MD)-28+C<8 THENC=C+7
1480 FORN=0 TO5
1490 FORM=1 TO7
1500 SR=M+N*7-C:CT=2:GOSUB310
1510 KL$="     "
1520 HO=0.2+N*0.13:VE=0.1+M*0.1
1530 IF(SR>0)AND(SR<=L(MD)) THEN GOSUB650:KL$="   "+SR$
1540 K$(M)=K$(M)+KL$
1550 NEXTM:NEXTN
1560 HO=0.3:VE=0.95
1570 SR$="Nog een keer?":GOSUB650
1580 RETURN
1590 REM=== NAAR PRINTER ===
1600 SR$=K$(0):GOSUB350
1610 GOSUB360:GOSUB360
1620 FORN=1 TO7:SR$=K$(N):GOSUB350
1630 GOSUB360:SR$=LL$:GOSUB350
1640 GOSUB360:GOSUB360
1650 NEXTN
1660 GOSUB360:GOSUB360
1670 RETURN
2000 REM=== INLEIDING ===
2010 GOSUB100
2020 PRINT:PRINT"  Dit programma maakt de kalender"
2030 PRINT:PRINT"  van  elke maand van  ieder jaar"
2040 PRINT:PRINT"  tussen januari 1600 en december"
2050 PRINT:PRINT"  3000. Je moet het JAARTAL geven"
2060 PRINT:PRINT"  en het NUMMER van de maand, dus"
2070 PRINT:PRINT"  1 voor januari, 2 voor februari"
2080 PRINT:PRINT"  enzovoort."
2090 PRINT:PRINT"  JE KRIJGT DAN EEN MAANDKALENDER"
2100 PRINT:PRINT"  die desgewenst naar de  printer"
2110 PRINT:PRINT"  kan door op de P te tikken."
2120 PRINT:PRINT"  --> Tik op 'n toets";
2130 GOSUB210:GOSUB100:RETURN
2140 REM=== INPUT JAARTAL+MND ===
2150 GOSUB100
2160 PRINT:PRINT" Tik eerst het  JAARTAL (+ RETURN) en"
2170 PRINT:PRINT" daarna het MAANDNUMMER (+ RETURN)"
2180 PRINT:PRINT" JAARTAL ";:INPUTJR
2190 IF(JR<1600)OR(JR>3000) THEN2150
2200 PRINT:PRINT" MAAND NR ";:INPUTMD
2210 IF(MD<1)OR(MD>12) THEN2150
2220 FORN=1 TO200:NEXTN:GOSUB100
2230 RETURN
25000 REM===============================
25010 DATA"JANUARI",31,0,"FEBRUARI",28,3,"MAART",31,3
25020 DATA"APRIL",30,6,"MEI",31,1,"JUNI",30,4
25030 DATA"JULI",31,6,"AUGUSTUS",31,2,"SEPTEMBER",30,5
25040 DATA"OKTOBER",31,0,"NOVEMBER",30,3,"DECEMBER",31,5
30000 REM==============================
30010 REM
30020 REM Nadat in Europa vele eeuwen
30030 REM de Juliaanse kalender volgens
30040 REM de Romeinse tijdrekening in
30050 REM gebruik was geweest, bleek
30060 REM uit sterrenkundige metingen
30070 REM dat deze kalender uit de pas
30080 REM liep met de seizoenen.
30090 REM Paus Gregorius XIII voerde
30100 REM toen een nieuwe kalender in,
30110 REM waarbij in elk jaar waarvan
30120 REM het jaartal een viervoud was,
30130 REM een extra dag aan februari 
30140 REM werd toegevoegd - behalve
30150 REM de eeuwjaren die NIET door
30160 REM 400 deelbaar zijn.
30170 REM Het zo gecorrigeerde jaar is
30180 REM gemiddeld maar 26 seconden te
30190 REM lang.
30200 REM De Gregoriaanse kalender is
30210 REM ingevoerd op 15 oktober 1582.
30220 REM=============================
30230 REM GEMAAKT OP EEN BBC B COMPUTER
30240 REM 18 FEBRUARI 1987
30250 REM DOOR
32000 REM Ch.W.Brederode
32010 REM Von Bonninghausenlaan 33
32020 REM 2161 ES  LISSE

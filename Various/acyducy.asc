1000 A=1000:GOTO 20:REM ACEY DUCEY
1010 GOSUB 19000
1020 SR=L:GOSUB 300:HO=31:VE=8:GOSUB 110:PRINT SR$;")   ";
1030 IF L=0 THEN 8000
1040 GOSUB 260:M1=INT(RV*13)
1050 GOSUB 260:M2=INT(RV*13)
1060 IF M1<M2 THEN 1080
1070 M0=M1:M1=M2:M2=M0
1080 HO=20:VE=5:GOSUB 110
1090 SR$=J$(M1)+" und "+J$(M2)
1100 PRINT SR$;:FOR I=1 TO 20-LEN(SR$):PRINT " ";:NEXT I
1110 HO=6:VE=6:GOSUB 110
1120 PRINT "                           "
1130 HO=13:VE=8:GOSUB 110:INPUT M4$
1140 M4=VAL(M4$):SR=M4:GOSUB 300
1150 HO=13:VE=8:GOSUB 110:PRINT SR$;
1160 FOR I=1 TO 7-LEN(SR$):PRINT " ";:NEXT I
1170 IF (M4$="X") OR (M4$="x") THEN 7000
1180 IF M4<>0 THEN 1200
1190 GOSUB 6000:GOTO 1020
1200 IF M4<=L THEN 1220
1210 GOSUB 5000:GOTO 1080
1220 IF (M2-M1)<>2 THEN 1270
1230 SR$="Sie lieben das Risiko!"
1240 HO=0:VE=10:GOSUB 110:GOSUB 150
1250 SD=30:GOSUB 450:GOSUB 110
1260 FOR I=0 TO 37:PRINT " ";:NEXT I
1270 GOSUB 260:M3=INT(RV*13)
1280 HO=6:VE=6:GOSUB 110:PRINT "Dritte Karte: ";J$(M3)
1290 SD=20:GOSUB 450
1300 IF (M3<=M1) OR (M3>=M2) THEN 1320
1310 L=L+M4:GOTO 1020
1320 L=L-M4:GOTO 1020
5000 REM *
5010 REM * SPIELER UEBERZIEHT GUTHABEN *
5020 REM *
5030 SR$="Sorry - es gibt keinen Kredit!"
5040 HO=0:VE=10:GOSUB 110:GOSUB 150
5050 SD=30:GOSUB 450:GOSUB 110
5060 FOR I=0 TO 37:PRINT " ";:NEXT I
5070 HO=12:VE=8:GOSUB 110:PRINT "        ";
5080 RETURN
6000 REM *
6010 REM * SPIELER WETTET NICHT *
6020 REM *
6030 IF (M2-M1)>2 THEN 6100
6040 SR$="Haette ich auch so gemacht!"
6050 IF (M2-M1)>1 THEN 6070
6060 SR$="Haette ich mir denken koennen!"
6070 HO=0:VE=10:GOSUB 110:GOSUB 150
6080 SD=30:GOSUB 450:GOSUB 110
6090 FOR I=0 TO 37:PRINT " ";:NEXT I
6100 RETURN
7000 REM *
7010 REM * SPIELER WILL BEENDEN *
7020 REM *
7030 GOSUB 100:HO=0:VE=12:GOSUB 110
7040 PRINT "War nett, mit Ihnen zu spielen."
7050 PRINT:PRINT "Bis zum naechsten Mal!"
7060 SD=50:GOSUB 450
7070 GOTO 950
8000 REM *
8010 REM * GUTHABEN AUFGEBRAUCHT *
8020 REM *
8030 SR$="Sie sind leider pleite..."
8040 HO=0:VE=10:GOSUB 110:GOSUB 150
8050 SD=30:GOSUB 450:GOSUB 110
8060 FOR I=0 TO 37:PRINT " ";:NEXT I
8070 SR$="Noch ein Spiel ( J/N ) ?"
8080 HO=0:VE=10:GOSUB 110:GOSUB 150
8090 GOSUB 210
8100 IF IN=78 THEN 7030
8110 IF IN<>74 THEN 8090
8120 GOSUB 100:GOSUB 19050:GOTO 1020
19000 REM *
19010 REM * BILDSCHIRMAUFBAU BEI ERSTEM START *
19020 REM *
19030 IF K=1 THEN 19210
19040 DIM J$(12):FOR I=0 TO 12:READ J$(I):NEXT I
19050 K=1:SR$="ACEY DUCEY":HO=11:VE=2:GOSUB 110:GOSUB 150
19060 HO=0:VE=14:GOSUB 110:PRINT "  SPIELREGEL:":PRINT
19070 PRINT "Der Computer gibt zwei Karten."
19080 PRINT "Der Spieler wettet einen Betrag, ob die"
19090 PRINT "naechste Karte  zwischen  diesen beiden"
19100 PRINT "Karten liegt  (Eingabe von ";CHR$(34);"0";
19110 PRINT CHR$(34);", wenn er"
19120 PRINT "nicht wetten moechte).  Das Spiel endet"
19130 PRINT "durch Eingabe von  ";CHR$(34);"X";
19140 PRINT CHR$(34);"  oder bei aufge-"
19160 PRINT "brauchtem Guthaben des Spielers.";
19170 L=100
19180 HO=0:VE=5:GOSUB 110:PRINT "Der Computer zieht:"
19190 HO=0:VE=8:GOSUB 110
19200 PRINT "Ihr Einsatz:        (Guthaben:"
19210 RETURN
25000 DATA "2","3","4","5","6","7","8","9","10"
25010 DATA "BUBE","DAME","KOENIG","AS"
32000 REM ************************************************
32010 REM **                                            **
32020 REM **  THOMAS RADEMACHER                         **
32030 REM **  FRIEDRICH-ENGELS-STR. 44                  **
32040 REM **  D-99086 ERFURT                            **
32050 REM **                                            **
32060 REM **  SEPTEMBER 2003                            **
32070 REM **                                            **
32080 REM **  ERSTELLT AUF EMULIERTEM AMSTRAD PCW       **
32090 REM **  (PCWEMU V2.01 FUER WINDOWS                **
32100 REM **                        VON JOHN ELLIOT)    **
32110 REM **                                            **
32120 REM **  SPIELIDEE: "Acey Ducey" AUS DEM BUCH      **
32130 REM **  "Basic Computer Spiele                    **
32140 REM **          Mikrocomputer-Ausgabe"  BAND 1    **
32150 REM **  HERAUSGEGEBEN VON DAVID H. AHL            **
32160 REM **  SYBEX-VERLAG GMBH, DUESSELDORF            **
32170 REM **                                            **
32180 REM ************************************************

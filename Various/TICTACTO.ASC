1000 A=500:GOTO 20:REM   BUTTER, KAESE UND EIER
1010 DIM A$(9)
1020 GOSUB 100
1030 SR$="BUTTER, KAESE UND EIER"
1040 HO=6:VE=1:GOSUB 110
1050 GOSUB 150
1060 PRINT:PRINT:PRINT"             FUER 2 SPIELER"
1070 PRINT
1080 PRINT"           VON ABDON PIJPELINK"
1090 PRINT:PRINT:PRINT:PRINT
1100 PRINT
1110 PRINT"        1..................SPIELEN"
1120 PRINT
1130 PRINT"        2................ANLEITUNG"
1140 PRINT
1150 PRINT"        3.....................ENDE"
1160 W$="TASTE DRUECKEN                          "
1170 HO=0:VE=20:GOSUB 110:PRINT W$
1180 X$=RIGHT$(W$,1):Y$=LEFT$(W$,39):W$=X$+Y$
1190 GOSUB 200
1200 IF IN$="1" THEN 5010
1210 IF IN$="2" THEN 6010
1220 IF IN$="3" THEN 7010
1230 GOTO 1170
2000 HO=6:VE=19:GOSUB 110
2010 PRINT"                              "
2020 M$="BUTTER, KAESE & EIER ":M=LEN(M$)
2030 N=1
2040 HO=6:VE=19:GOSUB 110
2050 GOSUB 200:IF IN>12 THEN 2120
2060 PRINT LEFT$(M$,N)
2070 HO=27-N:VE=19:GOSUB 110
2080 PRINT RIGHT$(M$,N)
2090 N=N+1:IF N<=2+M/2 THEN 2040
2100 SD=20:GOSUB 450
2110 IF IN<13 THEN 2000
2120 HO=X+1:VE=Y:GOSUB 110:PRINT" "
2130 HO=X-1:VE=Y:GOSUB 110:PRINT" "
2140 IF (IN=31) AND (Y>5) THEN Y=Y-4
2150 IF (IN=30) AND (Y<13) THEN Y=Y+4
2160 IF (IN=28) AND (X>12) THEN X=X-4
2170 IF (IN=29) AND (X<20) THEN X=X+4
2180 IF IN=13 THEN 3010
2190 HO=X+1:VE=Y:GOSUB 110:PRINT"<"
2200 HO=X-1:VE=Y:GOSUB 110:PRINT">"
2210 GOTO 2000
3000 REM         ZET NEER
3010 HO=X:VE=Y:GOSUB 220
3020 IF(IN=79) OR (IN=88) THEN 2190
3030 HO=X:VE=Y:GOSUB 110:PRINT S$
3040 A$((X+3*Y-23)/4)=S$
3050 AC=1
3060 IF(A$(AC)=A$(AC+1)) AND (A$(AC+1)=A$(AC+2)) THEN 4010
3070 AC=AC+3:IF AC<=7 THEN 3060
3080 AC=1
3090 IF (A$(AC)=A$(AC+3)) AND (A$(AC+3)=A$(AC+6)) THEN 4010
3100 AC=AC+1:IF AC<=3 THEN 3090
3110 IF (A$(1)=A$(5)) AND (A$(5)=A$(9)) THEN 4010
3120 IF (A$(3)=A$(5)) AND (A$(5)=A$(7)) THEN 4010
3130 Z=Z+1
3140 IF Z=10 THEN 4510
3150 IF S$="O" THEN 3190
3160 IF S$="X" THEN S$="O"
3170 HO=7:VE=1:GOSUB 110:PRINT K$;"  , MACH DEINEN ZUG!"
3180 GOTO 2190
3190 S$="X"
3200 HO=7:VE=1:GOSUB 110:PRINT L$;"  , MACH DEINEN ZUG!"
3210 GOTO 2190
4000 REM        GEWINNER
4010 IF S$="O" THEN 4050
4020 HO=7:VE=1:GOSUB 110:PRINT"   ";L$;" SIEGT!!!     "
4030 SD=100:GOSUB 450
4040 GOTO 1020
4050 HO=7:VE=1:GOSUB 110:PRINT"   ";K$;" SIEGT!!!     "
4060 SD=100:GOSUB 450
4070 GOTO 1020
4500 REM         KEIN GEWINNER
4510 HO=7:VE=1:GOSUB 110:PRINT"KEIN GEWINNER             "
4520 SV=7:SD=5
4530 FOR SP=50 TO 45 STEP -1
4540 GOSUB 400:SD=SD+0.5
4550 NEXT SP
4560 SD=100:GOSUB 450
4570 GOTO 1020
5000 REM          SET-UP
5010 GOSUB 100:PRINT
5020 PRINT"NAME DES 1. SPIELERS":INPUT K$
5030 PRINT"NAME DES 2. SPIELERS":INPUT L$
5040 GOSUB 100
5050 X=16:Y=9
5060 FOR VE=4 TO 14
5070 HO=10:GOSUB 110:PRINT"I   I   I   I"
5080 NEXT VE
5090 FOR VE=3 TO 15 STEP 4
5100 HO=10:GOSUB 110:PRINT"+-----------+"
5110 NEXT VE
5120 HO=15:VE=9:GOSUB 110:PRINT"> <"
5130 FOR AV=1 TO 9
5140 A$(AV)=CHR$(AV+64)
5150 NEXT AV
5160 Z=1
5170 GOSUB 260:S=INT(RV*3)
5180 IF (S=0) OR (S=1) THEN S$="O"
5190 IF (S>=2) THEN S$="X"
5200 GOTO 3150
6000 REM     ANLEITUNG
6010 GOSUB 100:PRINT
6020 PRINT"BUTTER, KAESE UND EIER":PRINT
6030 PRINT"ANLEITUNG"
6040 PRINT
6050 PRINT"FUER HOLLAENDER    "
6060 PRINT"VOOR DIEGENEN, DIE HET NOG NIET"
6070 PRINT"KENNEN, WORDT HET HIER UITGELEGD :"
6080 PRINT
6090 PRINT"DIT SPEL SPEEL JE MET 2 PERSONEN."
6100 PRINT"HET IS DE BEDOELING, DAT JE HORIZONTAAL,"
6110 PRINT"VERTICAAL OF DIAGONAAL EEN RIJ VAN DRIE"
6120 PRINT"KRIJGT MET JE EIGEN TEKENS, DIT IS VOOR"
6130 PRINT"SPELER 1 DE 'O' EN VOOR SPELER 2 DE 'X'."
6140 PRINT"NATUURLIJK MOET JE JE TEGENSTANDER"
6150 PRINT"TEGENWERKEN."
6160 PRINT"DEGENE, DIE HET EERST DRIE DE ZELFDE"
6170 PRINT"OP EEN RIJ HEEFT, IS DE WINNAAR."
6180 PRINT
6190 PRINT"DE BESTURING GAAT MET DE CURSOR-TOETSEN"
6200 PRINT"EN NEERZETTEN MET ENTER/RETURN."
6210 SD=1000:GOSUB 450
6220 GOTO 1020
7000 REM     STOPPEN
7010 GOSUB 100
7020 PRINT:PRINT
7030 PRINT"DAS WAR :"
7040 PRINT
7050 PRINT"BUTTER, KAESE UND EIER"
7060 PRINT"IN BASICODE - 3"
7070 PRINT:PRINT
7080 PRINT" VON :"
7090 PRINT
7100 PRINT"ABDON PIJPELINK"
7110 PRINT"MOZARTHOF 74"
7120 PRINT"4536 AW  TERNEUZEN"
7130 PRINT:PRINT"SINCLAIR ZX SPECTRUM"
7140 PRINT:PRINT:PRINT
7150 GOSUB 210
7160 GOTO 950
32000 REM
32010 REM
32020 REM      BUTTER,KAESE
32030 REM      UND EIER 
32040 REM
32050 REM      VON  
32060 REM      ABDON PIJPELINK
32070 REM      MOZARTHOF 74
32080 REM      4536 AW  TERNEUZEN
32090 REM
32100 REM      SINCLAIR ZX SPECTRUM
32110 REM
32120 REM TROS-RADIO dd 920101
32130 REM-spezial DS-Kultur 920304

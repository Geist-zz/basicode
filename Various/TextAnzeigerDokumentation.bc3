1000 A=100:GOTO 20:REM TextAnzeiger.DOK
1010 VM=VE-1:HM=HO:CZ=1:U=0:SZ=1:JU=1:JV=1:ZS=0:DZ=0
1020 PRINT "Bitte warten !"
1030 READ A$:IF A$=".e" THEN RESTORE :GOTO 1060
1040 IF A$=".c" THEN SZ=SZ+1
1050 GOTO 1030
1060 READ F$:GOSUB 1690
1070 READ A$
1080 IF LEFT$(A$,1)<> "." THEN PRINT A$:GOTO 1070
1090 IF A$=".c" THEN U=U+1:GOSUB 1160
1100 IF A$=".e" THEN GOSUB 1160:DZ=3:GOSUB 1600
1110 IF A$=".h" THEN READ A$:SR$=A$:GOSUB 150:PRINT 
1120 IF A$=".z" THEN PRINT 
1130 IF A$=".VE" THEN DZ=1:GOSUB 1620
1140 IF A$=".AE" THEN GOTO 1540
1150 GOTO 1070
1160 HO=0:VE=VM:GOSUB 110
1170 SR$="(H)elp (Q)uit (P)rint (B)ack (N)ext"
1180 GOSUB 150:HO=0:VE=VM:GOSUB 110
1190 CZ=CZ+1
1200 GOSUB 210
1210 IF (IN>49) AND (IN<58) THEN GOTO 1880
1220 IF (IN=81) OR (IN=113) THEN DZ=3:GOTO 1600
1230 IF (IN=66) OR (IN=98) THEN JU=2:JV=1:GOTO 1890
1240 IF (IN=72) OR (IN=104) THEN GOTO 1720
1250 IF (IN=80) OR (IN=112) THEN CZ=CZ-1:GOTO 1320
1260 IF (IN=67) OR (IN=99) THEN GOTO 1290
1270 IF (IN=13) OR (IN=78) THEN JU=1:GOSUB 1690:RETURN 
1280 GOTO 1200
1290 CO=1:RESTORE :HO=0:VE=1:GOSUB 110
1300 PRINT "Ausdruck aller Seiten laeuft !"
1310 GOTO 1380
1320 RESTORE :Z=1
1330 READ A$
1340 IF (A$=".c") AND (CO=1) THEN GOTO 1380
1350 IF (A$=".c") AND (CO=0) THEN Z=Z+1
1360 IF Z=CZ THEN GOSUB 1690:GOTO 1380
1370 GOTO 1330
1380 READ A$
1390 IF (A$=".VE") AND (CO=1) THEN GOTO 1330
1400 IF (A$=".c") AND (CO=1) THEN GOTO 1520
1410 IF (A$=".VE") AND (CO=0) THEN GOTO 1510
1420 IF (A$=".c") AND (CO=0) THEN GOSUB 360:GOTO 1160
1430 IF (A$=".z") AND (CO=1) THEN GOTO 1530
1440 IF (A$=".z") AND (CO=0) THEN PRINT :GOTO 1530
1450 IF A$=".h" THEN READ A$:GOTO 1480
1460 IF (A$=".e") AND (CO=1) THEN CZ=CZ-1:CO=0:GOTO 1940
1470 IF LEFT$(A$,1)="." THEN READ A$:GOTO 1380
1480 SR$="     "+A$:GOSUB 350
1490 IF CO=0 THEN PRINT A$:GOTO 1380
1500 GOTO 1380
1510 CZ=1:GOSUB 360:GOSUB 1160:DZ=2:GOTO 1600
1520 GOSUB 360
1530 GOSUB 360:GOTO 1380
1540 HO=6:VE=VM:GOSUB 110
1550 SR$="(W)iederholen (Q)uit":GOSUB 150
1560 GOSUB 210
1570 IF (IN=87) OR (IN=119) THEN DZ=2:GOTO 1600
1580 IF (IN=81) OR (IN=113) THEN GOTO 950
1590 GOTO 1560
1600 GOSUB 100:PRINT :CZ=2
1610 RESTORE 
1620 READ A$
1630 IF (DZ=2) AND (A$=".AE") THEN GOTO 1680
1640 IF (DZ=1) AND (A$=".AE") THEN GOTO 1070
1650 IF (DZ=3) AND (A$=".VE") THEN GOTO 1070
1660 IF (DZ=4) AND (A$=".c") THEN GOTO 1070
1670 GOTO 1620
1680 READ A$:GOSUB 1690:GOTO 1070
1690 GOSUB 100
1700 PRINT "Seite";CZ;"/";SZ;
1710 SR$=F$:GOSUB 150:PRINT :PRINT :RETURN 
1720 GOSUB 100
1730 PRINT :PRINT 
1740 PRINT "Help:"
1750 PRINT "-----":PRINT 
1760 PRINT "(P)rint = Hardcopy der aktuellen Seite"
1770 PRINT "(C)opy  = Hardcopy aller Seiten"
1780 PRINT "(Q)uit  = Beenden des Programmes"
1790 PRINT "(B)ack  = eine Seite zurueck"
1800 PRINT "(N)ext  = naechste Seite (auch ENTER)"
1810 PRINT " 2...9  = ? Seiten vor bzw. zurueck"
1820 PRINT "(H)elp  = dieses Menu":PRINT 
1830 PRINT "Bei auftreten eines Eingabefehlers gibt"
1840 PRINT "es einen Signalton.":PRINT :PRINT 
1850 PRINT "--->  weiter mit Taste"
1860 GOSUB 210
1870 CZ=CZ-1:Z=1:RESTORE :GOSUB 1690:GOTO 1950
1880 JV=IN-48
1890 ZS=CZ
1900 IF JU=0 THEN GOSUB 250:GOTO 1200
1910 IF JU=1 THEN CZ=CZ+JV-1
1920 IF JU=2 THEN CZ=CZ-JV-1
1930 IF (CZ<1) OR (CZ>SZ) THEN CZ=ZS:GOSUB 250:GOTO 1200
1940 GOSUB 1690:RESTORE :Z=1
1950 READ A$
1960 IF A$=".c" THEN Z=Z+1
1970 IF Z=CZ THEN GOTO 1070
1980 GOTO 1950
25000 DATA "TextAnzeiger.DOK"
25010 DATA ".z"
25020 DATA "     ******************************"
25030 DATA "     *                            *"
25040 DATA "     *     Dokumentation zum      *"
25050 DATA "     *                            *"
25060 DATA "     *     TextAnzeiger V1.0      *"
25070 DATA "     *                            *"
25080 DATA "     ******************************",".VE"
25090 DATA "     ******************************"
25100 DATA "     *                            *"
25110 DATA "     *     Das war die            *"
25120 DATA "     *                            *"
25130 DATA "     *     Dokumentation zum      *"
25140 DATA "     *                            *"
25150 DATA "     *     TextAnzeiger V1.0      *"
25160 DATA "     *                            *"
25170 DATA "     ******************************",".z"
25180 DATA "     ******************************"
25190 DATA "     *                            *"
25200 DATA "     *     ----  E N D E ----     *"
25210 DATA "     *                            *"
25220 DATA "     ******************************",".AE"
25230 DATA ".c",".h"
25240 DATA "Funktionen:",".z"
25250 DATA "<N> <CR>  =  eine Seite weiter",".z"
25260 DATA "<B> <b>   =  eine Seite zurueck",".z"
25270 DATA "<P> <p>   =  aktuelle Seite ausdrucken",".z"
25280 DATA "<C> <c>   =  gesamten Text ausdrucken"
25290 DATA "             (ohne Abspann)",".z"
25300 DATA "<Q> <q>   =  beenden des Programms",".z"
25310 DATA "<H> <h>   =  Hilfsmenu anzeigen",".z"
25320 DATA "<2...9>   =  nach <N> oder <B>"
25330 DATA "             ? Seiten vor od. zurueck",".c"
25340 DATA ".h","Steuer-DATAs:",".z"
25350 DATA "<.c>  =  Beginn einer neuen Seite",".z"
25360 DATA "<.e>  =  Ende des Textes",".z"
25370 DATA "<.h>  =  naechstes DATA hervorheben",".z"
25380 DATA "<.z>  =  eine Leerzeile",".z"
25390 DATA "<.VE> =  Ende des Vorspanns",".z"
25400 DATA "<.AE> =  Ende des Abspanns",".c"
25410 DATA ".h","Aufbau der DATA-Zeilen:",".z"
25420 DATA "25000 DATA <Titel des Textes>"
25430 DATA "25010 DATA <Beginn des Vorspanns>"
25440 DATA "      DATA <.VE> (Ende Vorspann)"
25450 DATA "      DATA <Beginn des Abspanns>"
25460 DATA "      DATA <.AE> (Ende Abspann)"
25470 DATA "      DATA <.c> (Beginn 1.Seite)"
25480 DATA "      DATA <.c> (Beginn 2.Seite)",".z"
25490 DATA "                * * *",".z"
25500 DATA "      DATA <.e> (Ende des Textes)",".c"
25510 DATA ".h","Bemerkungen:",".z"
25520 DATA "- unterstuetzt nur 40 Zeichen Bild-"
25530 DATA "  schirme (in der Zeilenzahl variabel)",".z"
25540 DATA "- in der Titelzeile werden die aktuelle"
25550 DATA "  und die maximale Seitenzahl angezeigt",".z"
25560 DATA "- die Steuer-DATA <.h> und <.z> sind"
25570 DATA "  auch in Vor- und Abspann zulaessig",".z"
25580 DATA "- auch die 1.Seite muss mit <.c> be-"
25590 DATA "  ginnen",".z"
25600 DATA "- bei einigen Operationen kommt es in"
25610 DATA "  Verbindung mit erhoehter Seitenzahl"
25620 DATA "  und geringer Rechenleistung zu"
25630 DATA "  einiger Verzoegerung.",".e"
32000 REM
32010 REM Basisprogramm:
32020 REM TextAnzeiger V1.0
32030 REM
32040 REM Text:
32050 REM TextAnzeiger V1.0 - DOK 
32060 REM
32070 REM Michael Fiedler
32080 REM Koethener Str.1
32090 REM O-4415 Zoerbig
32100 REM Tel. /5262
32110 REM
32120 REM KC85/3
32130 REM 31.05.92
32140 REM
32150 REM-spezial DS-Kultur 921111

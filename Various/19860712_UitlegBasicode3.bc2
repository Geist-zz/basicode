
1000 A=500:GOTO20:REM uitleg BASICODE-3 deel 1
1010 GOSUB1540:REM scherm opmeten
1020 READRE$
1030 IFRE$="***" THENGOSUB100:END
1040 BK=1
1050 IFBK>LEN(RE$) THEN1020
1060 AK=0
1070 AK=AK+1
1080 IFBK+AK>LEN(RE$) THEN1100
1090 IFMID$(RE$,BK+AK,1)<>" " THEN1070
1100 WO$=MID$(RE$,BK,AK)
1110 BK=BK+LEN(WO$)+1
1120 IFPL+LEN(WO$)>=RL THEN GOSUB1360
1130 IFLEFT$(WO$,2)<>".F" THEN1170
1140 LF=ASC(MID$(WO$,3,LEN(WO$)-2))-48
1150 GOSUB1360:LF=LF-1:IFLF>0 THEN1150
1160 GOTO1050
1170 IFWO$<>".P" THEN1210
1180 REM begin op een nieuwe pagina
1190 GOSUB1360:IFAR<>2 THEN1190
1200 GOTO1050
1210 IFWO$<>".C" THEN1280
1220 REM centreer de rest van deze DATA-regel
1230 RE$=MID$(RE$,BK,LEN(RE$)-BK+1)
1240 IS=INT((RL-LEN(RE$)-1)/2):GOSUB1520
1250 PRINT RE$;:RE$=""
1260 GOSUB1360
1270 GOTO1050
1280 IFLEFT$(WO$,2)<>".M" THEN1320
1290 REM stel een nieuwe linkermarge in
1300 LM=ASC(MID$(WO$,3,LEN(WO$)-2))-48
1310 GOTO1050
1320 PRINTWO$;
1330 PL=PL+LEN(WO$)+1
1340 IFPL<RL-1 THENPRINT" ";
1350 GOTO1050
1360 PRINT
1370 AR=AR+1
1380 IFAR<SH-2 THEN1480
1390 PRINT
1400 PRINT"  VERVOLG: druk op <return>";
1410 GOSUB210:IF IN$<>CHR$(13) THEN1410
1420 GOSUB100
1430 IS=INT(RL/2)-19:IFIS<0 THENIS=0
1440 GOSUB1520
1450 PRINT"*** BASICODE-3 COMPUTERBULLETIN ***"
1460 PRINT
1470 AR=2
1480 PL=LM
1490 IS=LM:GOSUB1520
1500 RETURN
1510 REM subr. PRINT IS spaties
1520 IF IS<1 THEN RETURN
1530 PRINT" ";:IS=IS-1:GOTO1520
1540 GOSUB100
1550 RL=20:SH=10
1560 PRINT"Een ogenblikje a.u.b...."
1570 VE=10
1580 HO=RL+1:GOSUB110:GOSUB120
1590 IFHO=RL+1 THENRL=RL+1:GOTO1580
1600 HO=10
1610 VE=SH+1:GOSUB110:GOSUB120
1620 IF VE=SH+1 THENSH=SH+1:GOTO1610
1630 PL=0:AR=0
1640 LM=0 : REM linker marge
1650 GOSUB1420
1660 RETURN
25000 REM tekst van bulletin-1
25010 DATA".F2 Hallo BASICODE-VRIENDEN ! .F2 Eindelijk,"
25020 DATA"daar is dan BASICODE-3. In drie bulletins willen"
25030 DATA"we vertellen wat er in BASICODE-3 allemaal voor"
25040 DATA"interessante nieuwigheden beschikbaar zijn en"
25050 DATA"hoe u daarmee kunt werken. Natuurlijk kan dat"
25060 DATA"niet zo uitgebreid als het in het officiele"
25070 DATA"BASICODE-3 boek staat, maar we kunnen u in deze"
25080 DATA"drie bulletins wel de hoofdzaken uitleggen. Het"
25090 DATA"blijft natuurlijk verstandig, het boek aan te"
25100 DATA"schaffen. Daar vindt u niet alleen alle details,"
25110 DATA"maar ook het nieuwe vertaalprogramma voor uw"
25120 DATA"computer, de gebruiksaanwijzing daarbij en nog"
25130 DATA"heel veel andere zaken. .F2 Waarom BASICODE-3?"
25140 DATA"Welnu, het werd hoog tijd dat BASICODE-2 een"
25150 DATA"opvolger kreeg. De mogelijkheden van de"
25160 DATA"hedendaagse computers zijn doorgaans zoveel"
25170 DATA"uitgebreider dan wat er in BASICODE-2 mogelijk"
25180 DATA"was, dat we helaas eerder dan verwacht het"
25190 DATA"BASICODE-2 als verouderd moesten beschouwen. .F2"
25200 DATA"Met BASICODE-3 zijn de mogelijkheden van"
25210 DATA"BASICODE voorlopig weer aardig bij de tijd. Als"
25220 DATA"de fabrikanten van computers weer allerlei"
25230 DATA"nieuwe grappen bedacht hebben, zullen we aan"
25240 DATA"BASICODE-4 moeten gaan beginnen, maar we denken"
25250 DATA"dat dat toch nog wel enkele jaartjes zal gaan"
25260 DATA"duren. .F2 Als u dit leest bent u blijkbaar"
25270 DATA"bekend met BASICODE-2, anders kon u dit helemaal"
25280 DATA"niet eens inlezen. We vertellen daarom alleen"
25290 DATA"wat er in BASICODE-3 nieuw is. De rest wist u"
25300 DATA"immers al! .P In dit eerste deel willen we het"
25310 DATA"vooral hebben over:"
25320 DATA".F2 .C DE GRAFISCHE MOGELIJKHEDEN"
25330 DATA".F2 Tekst op het scherm zetten is niet moeilijk."
25340 DATA"Je PRINT maar wat je printen wilt en de computer"
25350 DATA"doet de rest. Alle thuiscomputers doen dat op"
25360 DATA"dezelfde manier. Tekeningen maken is een heel"
25370 DATA"probleem want alle computers doen dat"
25380 DATA"verschillend (als ze het al kunnen). In BASICODE"
25390 DATA"worden de verschillen opgevangen in speciale"
25400 DATA"subroutines en daarom zijn er voor grafische"
25410 DATA"programmamogelijkheden vier nieuwe routines"
25420 DATA"bedacht: .P .F1 GOSUB 600 : Start grafisch"
25430 DATA"bedrijf .F2 GOSUB 620 : Plot 1 punt .F2 GOSUB"
25440 DATA"630 : Trek een lijnstuk .F2 GOSUB 650 : Tekst in"
25450 DATA"de grafiek .F2 Elk van deze vier subroutines,"
25460 DATA"plus enkele bijbehorende zaken, gaan we nu apart"
25470 DATA"bekijken:"
25480 DATA".P .C GOSUB 600 : Start grafisch bedrijf"
25490 DATA".F2 Voordat we iets kunnen tekenen moet de"
25500 DATA"computer ingesteld worden voor grafisch bedrijf."
25510 DATA"Dat gebeurt met deze subroutine. Na afloop van"
25520 DATA"deze subroutine is het scherm gewist en staat de"
25530 DATA"computer klaar voor het tekenen van figuren. .F2"
25540 DATA"Zo lang de computer in deze stand staat, mogen"
25550 DATA"de opdrachten PRINT en INPUT niet worden"
25560 DATA"gebruikt. Hetzelfde geldt voor de subroutines"
25570 DATA"110 en 120 en voor de in BASICODE 3 nieuwe"
25580 DATA"subroutines 150 en 220. .F2 Eenmaal in grafisch"
25590 DATA"bedrijf kan er worden getekend met de volgende"
25600 DATA"drie subroutines. Daarbij gelden enkele vaste"
25610 DATA"regels.: .F2 De positie waar de (onzichtbare)"
25620 DATA"grafische cursor moet komen, wordt opgegeven in"
25630 DATA"de variabelen HO en VE. Daarin moet altijd een"
25640 DATA"niet-negatief getal staan dat kleiner dan 1 is."
25650 DATA"De waarde 0 mag dus nog net wel, de waarde 1 mag"
25660 DATA"net niet meer. Het grafische nulpunt (de"
25670 DATA"oorsprong) ligt in de linkerbovenhoek, net als"
25680 DATA"in tekstbedrijf. Als HO of VE de waarde 1 zou"
25690 DATA"hebben (wat dus niet mag), komt de cursor juist"
25700 DATA"voorbij de rand van het scherm. .F2 Verder is"
25710 DATA"het grafische scherm zo ingedeeld, dat de hoogte"
25720 DATA"precies 3/4 van de breedte is. Als u dus"
25730 DATA"achtereenvolgens de punten (0,0), (0.5,0),"
25740 DATA"(0.5,0.5) en (0,0.5) bezoekt, ontstaat een"
25750 DATA"rechthoek die de linkerbovenhoek van het scherm"
25760 DATA"omsluit. Wilt u een vierkant te zien krijgen,"
25770 DATA"bezoek dan bijvoorbeeld de punten (0,0),"
25780 DATA"(0.4,0), (0.4,0.3) en (0,0.3). .F2 Tenslotte:"
25790 DATA"voor diverse toepassingen is het erg nuttig, te"
25800 DATA"weten hoeveel beeldpunten er op het scherm"
25810 DATA"kunnen. Dat is in regel 20 in de variabelen HG"
25820 DATA"(horizontaal) en VG (verticaal) gezet. Als uw"
25830 DATA"programma van deze gegevens gebruik wil maken"
25840 DATA"dient het de waarden in deze variabelen te"
25850 DATA"bewaren."
25860 DATA".P .C GOSUB 620 : Plot 1 punt"
25870 DATA".F2 Alvorens deze subroutine aan te roepen, moet"
25880 DATA"u drie variabelen van een waarde voorzien: .F2"
25890 DATA"HO .M3 voor de horizontale positie van het te"
25900 DATA"plotten punt (0<=HO<1); .M0 .F2 VE .M3 voor de"
25910 DATA"verticale positie van het te plotten punt"
25920 DATA"(0<=VE<1); .M0 .F2 CN .M3 waarin u opgeeft of"
25930 DATA"het punt in de voorgrondkleur of juist in de"
25940 DATA"achtergrondkleur moet worden geplot: .F2 CN=0 :"
25950 DATA"voorgrondkleur (wit) .F2 CN=1 : achtergrondkleur"
25960 DATA"(zwart) .M0 .F2 Na afloop is het gewenste punt"
25970 DATA"wit of zwart geworden en staat de grafische"
25980 DATA"cursor op die plaats."
25990 DATA".P .C GOSUB 630 : Trek een lijnstuk"
26000 DATA".F2 Deze subroutine wordt op precies dezelfde"
26010 DATA"manier aangeroepen als de vorige: met de"
26020 DATA"variabelen HO, VE en CN. Ditmaal wordt een"
26030 DATA"lijnstuk getekend vanaf de plaats waar de"
26040 DATA"grafische cursor staat naar de plaats die in HO"
26050 DATA"en VE is opgegeven. De kleur wordt weer bepaald"
26060 DATA"door CN. .F2 Na afloop is het lijnstuk wit of"
26070 DATA"zwart geworden en staat de grafische cursor op"
26080 DATA"het eindpunt."
26090 DATA".P .C GOSUB 650 : Tekst in de grafiek"
26100 DATA".F2 Ook deze routine wordt aangeroepen met in HO"
26110 DATA"en VE een plaatsbepaling en in CN een kleurcode."
26120 DATA"Bovendien dient in SR$ een bepaalde tekst te"
26130 DATA"staan. Deze tekst wordt in het grafische scherm"
26140 DATA"geplaatst vanaf de opgegeven positie. .F2 De"
26150 DATA"opgegeven positie in HO en VE komt overeen met"
26160 DATA"de linkerbovenhoek van het eerste teken in SR$."
26170 DATA".F2 De breedte van elk teken zal bij de diverse"
26180 DATA"computers verschillend zijn. Houd daar rekening"
26190 DATA"mee als u deze routine aanroept en geef in SR$"
26200 DATA"geen tekst van meer dan 3 of 4 tekens. .F2 De"
26210 DATA"tekst wordt echt geplot, dat wil zeggen, alle"
26220 DATA"beeldpunten van de gewenste tekens worden in de"
26230 DATA"in CN aangegeven kleur gezet. Als u in SR$ een O"
26240 DATA"hebt staan, dan wordt een klein cirkeltje"
26250 DATA"getekend, hetzij in wit, hetzij in zwart. De"
26260 DATA"beeldpunten binnen dat cirkeltje veranderen niet"
26270 DATA"van kleur."
26280 DATA".P .C GOSUB 100"
26290 DATA".F2 Dit is een oude bekende. De betekenis van"
26300 DATA"deze routine is nu wat uitgebreider. Met een"
26310 DATA"opdracht GOSUB100 schakelt u nu ook terug van"
26320 DATA"grafisch bedrijf naar het normale tekstbedrijf."
26330 DATA"Na deze aanroep mag u de subroutines 620, 630 en"
26340 DATA"650 niet meer gebruiken. Daarvoor moet eerst"
26350 DATA"weer routine 600 worden aangeroepen om om te"
26360 DATA"schakelen naar grafisch bedrijf. .P Kleur is dus"
26370 DATA"in BASICODE-3 niet mogelijk. Alle tekeningen"
26380 DATA"worden in zwart/wit gemaakt. Wat wel kan, is"
26390 DATA"zwarte tekst op een witte ondergrond en witte"
26400 DATA"tekst op een zwarte ondergrond. Wat met een"
26410 DATA"beetje moeite in het grafische scherm ook kan,"
26420 DATA"is klinkers met daarboven accenten of andere"
26430 DATA"vreemdtalige tekens. .P"
26440 DATA"Hebt u vragen of opmerkingen? .F2"
26450 DATA"Stuur uw reaktie aan: .M5 .F3"
26460 DATA"TROS RADIO .F2"
26470 DATA"Basicode 3 .F2"
26480 DATA"Postbus 6055 .F2"
26490 DATA"1200 GZ  Hilversum .P"
26500 DATA"***"
30000 REM"****************************"
30010 REM"*                          *"
30020 REM"*   Uitleg BASICODE - 3    *"
30030 REM"*       Deel - 1           *"
30040 REM"*                          *"
30050 REM"*   TROS-Radio 12-7-1986   *"
30060 REM"*                          *"
30070 REM"*   Copyright (c) 1986:    *"
30080 REM"*   Stichting  BASICODE    *"
30090 REM"*             Eindhoven    *"
30100 REM"*                          *"
30110 REM"****************************"

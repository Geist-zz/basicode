1000 A=12000:GOTO 20:REM DATACOPY + PRINT
1010 X=HO:Y=VE:ZL=500:DIM G$(4),A$(ZL)
1020 G$(1)="Kassette im BASICODE-Format"
1030 G$(2)="Kassette im computertyp. Format"
1040 G$(3)="Diskettenlaufwerk 1"
1050 G$(4)="Diskettenlaufwerk 2"
1060 FOR SP=0 TO HO-1:LZ$=LZ$+" ":NEXT SP
1070 SR$="Kopierprogramm fuer Datendateien"
1080 PRINT "  ";:GOSUB 150
1090 SR$="  mit Ausdruck des sequ. Files  "
1100 PRINT "  ";:GOSUB 150
1110 GOSUB 1770
1120 SR$="Kopierprogramm fuer Datendateien"
1130 PRINT :PRINT "  ";:GOSUB 150
1140 HO=2:VE=5:GOSUB 110
1150 PRINT "       Quelle:":PRINT :PRINT 
1160 PRINT " (1)  ";G$(1):PRINT 
1170 PRINT " (2)  ";G$(2):PRINT 
1180 PRINT " (3)  ";G$(3):PRINT 
1190 PRINT " (4)  ";G$(4):PRINT 
1200 PRINT :INPUT "  Kennzahl: ";KZ
1210 IF ((KZ<1) OR (KZ>4)) THEN GOTO 1200
1220 QU=KZ:KZ=0
1230 HO=2:VE=5:GOSUB 110
1240 PRINT "         Ziel:"
1250 HO=0:VE=17:GOSUB 110
1260 PRINT LZ$
1270 HO=0:VE=17:GOSUB 110
1280 INPUT "  Kennzahl: ";KZ
1290 IF ((KZ<1) OR (KZ>4)) THEN GOTO 1280
1300 ZI=KZ:KZ=0
1310 HO=0:VE=5:GOSUB 110
1320 FOR ZE=5 TO Y-1:PRINT LZ$:NEXT ZE
1330 HO=0:VE=5:GOSUB 110
1340 PRINT "  Kopieren eines Datenbestandes":PRINT :PRINT 
1350 PRINT "von:  ";G$(QU):PRINT 
1360 PRINT "nach: ";G$(ZI)
1370 PRINT :PRINT :PRINT "Eingabe der Dateinamen:":PRINT 
1380 INPUT "Dateiname der Quelldatei: ";QD$
1390 INPUT "Dateiname der Zieldatei : ";ZD$
1400 PRINT :PRINT :PRINT "Alle Angaben korrekt? (J/N)";
1410 GOSUB 210:PRINT IN$:IF IN=78 THEN GOSUB 100:GOTO 1120
1420 PRINT "Programmablauf auf TV verfolgen (J/N) ";
1430 TV=1:GOSUB 210:PRINT IN$:IF IN=78 THEN TV=0
1440 PRINT :PRINT "Einlesen der Quelldatei...":PRINT 
1450 X=0:NF=(QU*2)-2:NF$=QD$:GOSUB 500
1460 GOSUB 540:A$(X)=IN$
1470 IF TV=1 THEN PRINT A$(X)
1480 X=X+1:IN$=""
1490 GOSUB 270:IF ((FR>300) AND (X<= ZL)) THEN GOTO 1590
1500 SD=1:FOR Y=0 TO 10:GOSUB 250:GOSUB 450:NEXT Y
1510 SR$=" ZWISCHENSPEICHER VOLL "
1520 PRINT :PRINT :GOSUB 150:PRINT :PRINT 
1530 PRINT "Quelltext teilen oder RAM erweitern,"
1540 PRINT "Hinweise ab Zeile 30.000 lesen!"
1550 PRINT :PRINT "letzte gelesene Zeile: ";X
1560 PRINT :PRINT "Inhalt von Zeile";X;"ist:"
1570 PRINT A$(X):PRINT 
1580 PRINT :GOTO 1620
1590 IF IN=0 THEN GOTO 1460
1600 IF IN=-1 THEN PRINT "FEHLER in Zeile";X:GOTO 1730
1610 IF IN=1 THEN PRINT "Einlesen beendet!"
1620 GOSUB 580
1630 PRINT :PRINT "Zielkassette bzw. -diskette einlegen"
1640 PRINT "und dann ENTER druecken!"
1650 GOSUB 210
1660 NF=(ZI*2)-1:NF$=ZD$:GOSUB 500
1670 PRINT :PRINT "Schreiben der Zieldatei...":PRINT 
1680 FOR Y=0 TO X-1
1690 SR$=A$(Y):GOSUB 560
1700 IF TV=1 THEN PRINT SR$
1710 IF PR=1 THEN GOSUB 350:GOSUB 360
1720 NEXT Y
1730 GOSUB 580
1740 PRINT :PRINT "ENDE! - Taste druecken"
1750 GOSUB 210:GOTO 950
1760 REM -----------------------------------------
1770 HO=0:VE=4:GOSUB 110
1780 PRINT "Das Programm erlaubt das Kopieren von"
1790 PRINT "Datenbestaenden (sequentielle Files)"
1800 PRINT "mit Anzeige auf dem Bildschirm und dem"
1810 PRINT "Ausdruck des Files.":PRINT 
1820 PRINT "Drucker betriebsbereit (Betriebsart ="
1830 PRINT "ASCII), Papier eingelegt ?":PRINT 
1840 PRINT "                         Taste -J/N- ";
1850 PR=0:GOSUB 210
1860 IF IN=78 THEN GOSUB 100:RETURN 
1870 IF IN=74 THEN PR=1:ZE=0:GOSUB 100:RETURN 
1880 GOTO 1850
1890 REM -----------------------------------------
1900 SR$=A$(X):GOSUB 350:GOSUB 360:RETURN 
30000 REM **********************************************
30010 REM DIESES PROGRAMM DIENT ZUM DATENAUSTAUSCH ZWI-
30020 REM SCHEN VERSCHIEDENEN DATENTRAEGERN. EIN KOPIE-
30030 REM REN VON PROGRAMMEN IST NICHT MOEGLICH!
30040 REM
30050 REM ANWENDUNGSBEISPIELE:
30060 REM - AUFBEREITUNG VON PASCAL-QUELLTEXTEN;
30070 REM - NUTZUNG VON TEXTDATEIEN ANDERER RECHNER,
30080 REM   SOFERN REINE ASCII-DATEIEN VORLIEGEN.
30090 REM
30100 REM DIE TEXTLAENGE IST AUF 500 ZEILEN BESCHRAENKT,
30110 REM KANN ABER DURCH UMDIMENSIONIERUNG VON ZL IN
30120 REM ZEILE 1010 DER JEWEILIGEN RAM-GROESSE ANGEPASST
30130 REM WERDEN (VARIABLE A IN ZEILE 1000 GGF. ANPASSEN).
32000 REM ----------------------------------------------
32010 REM   PROGRAMM DATACOPY   11.01.92
32020 REM
32030 REM   ERSTELLT AUF BILDUNGSCOMPUTER A5105
32040 REM
32050 REM        ANDREAS GARTEN
32060 REM        FRIED. SIEDL. 2
32070 REM        O-8514 PULSNITZ
32080 REM ----------------------------------------------
32090 REM   ERWEITERT UM AUSDRUCK-ROUTINE (5/92)
32100 REM
32110 REM        F. DORMEIER
32120 REM        BISMARCKSTR. 11A
32130 REM        1000 BERLIN 39
32140 REM ----------------------------------------------
32150 REM Erstausstrahlung: DS Kultur
32160 REM-spezial 920527

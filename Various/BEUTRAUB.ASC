1000 A=100:GOTO 20:REM  RAUB-BEUTETIERE-ZYKLUS
1010 HB=HO:VB=VE:UW=1-1/HG:P1=3.14159:MT=0:TM=0:IV=255
1020 IT=0:RA=0:RB=0:RC=0:RD=0:PA=0:PB=0:PC=0:PD=0
1030 DIM CO$(8)
1040 FOR I=0 TO 8:READ CO$(I):NEXT I
1050 GOSUB 100:GOSUB 9040:SR$="RAUB-BEUTETIERE-ZYKLUS"
1060 VE=2:HO=INT (HB/2-11)-3:GOSUB 110:GOSUB 150
1070 PRINT :PRINT :GOSUB 9040
1080 RG=RA+RB:RK=RA-RB:PG=PA+PB:PK=PA-PB:PR=1
1090 FOR VE=6 TO 14
1100 HO=HB/2-5:GOSUB 110:PRINT CO$(VE-6)
1110 NEXT VE
1120 PRINT :GOSUB 9040
1130 VE=18:HO=0:GOSUB 110
1140 PRINT "Waehle mittels der Cursortasten"
1150 PRINT "und schliesse mit Return ab !"
1160 PRINT :GOSUB 9040
1170 VE=6:HO=HB/2-8:SR$=CO$(VE-6):GOSUB 110:GOSUB 150
1180 GOSUB 200:IF ((IN<28) OR (IN>31)) AND (IN<> 13) THEN GOTO 1180
1190 IF IN=13 THEN GOTO 1280
1200 SR$="   "+CO$(VE-6)+"   ":GOSUB 110:PRINT SR$
1210 IF (IN=30) AND (VE=14) THEN VE=5
1220 IF IN=30 THEN VE=VE+1
1230 IF (IN=31) AND (VE=6) THEN VE=15
1240 IF IN=31 THEN VE=VE-1
1250 IF IN=28 THEN VE=6
1260 IF IN=29 THEN VE=14
1270 SR$=CO$(VE-6):GOSUB 110:GOSUB 150:GOTO 1180
1280 IF VE=14 THEN GOSUB 100:GOTO 950
1290 IF (VE<8) OR (IV=0) OR (VE=13) THEN GOTO 1330
1300 HO=INT (HB/2-17):VE=VB-2:GOSUB 110
1310 SR$="Du hast noch nichts eingegeben!":GOSUB 150
1320 GOSUB 9110:GOTO 1050
1330 ON VE-5 GOSUB 2010,3010,4010,5010,6010,7010,8010,8510
1340 IF (IN=83) AND (VE<> VB) THEN GOSUB 20000
1350 GOTO 1050
2000 REM  ERKLAERUNG
2010 SR$="BEUTE-RAUBTIER-ZYKLUS":GOSUB 9010
2020 PRINT :PRINT :PRINT "Durch den Wissenschaftler Volterra"
2030 PRINT "wurde, angeregt durch den Biologen d'Ancona,"
2040 PRINT "im Jahre 1931 ein wissenschaftliches Modell"
2050 PRINT "der Wechselwirkung zwischen Beute- und"
2060 PRINT "Raubtieren formuliert."
2070 PRINT "Der Ausgangspunkt ist, dass die Zunahme"
2080 PRINT "des Anteiles an Raubtieren dafuer sorgt, dass"
2090 PRINT "auch mehr Beutetiere gefangen werden."
2100 PRINT "Dadurch nehmen aber die Beutetiere"
2110 PRINT "anteilig ab, sodass es weniger Futter"
2120 PRINT "fuer die Raubtiere gibt und sie dadurch"
2130 PRINT "anteilig abnehmen. Hierdurch nehmen"
2140 PRINT "die Raubtiere wieder anteilig zu, es gibt"
2150 PRINT "wieder genuegend Futter wodurch der"
2160 PRINT "Anteil Raubtiere wieder zunimmt, usw.":GOSUB 9110
2170 GOSUB 100:PRINT "Natuerlich wurden viele Einwaende gegen"
2180 PRINT "dieses Modell vorgebracht. So soll es vielfach"
2190 PRINT "sein, dass Raubtiere von mehr als einer"
2200 PRINT "Sorte Beutetieren leben. Weiter soll"
2210 PRINT "das Klima Einfluss haben koennen."
2220 GOSUB 9110:GOSUB 100
2230 PRINT "Als aber keine Rechnung mit diesen"
2240 PRINT "Faktoren geholfen hat, konnte dieser"
2250 PRINT "Zyklus beruhigt wissenschaftlich durch"
2260 PRINT "eine Funktion fuer den Anteil an"
2270 PRINT "Beute- und Raubtieren aufgestellt werden."
2280 PRINT "Diese Funktion lautet wie folgt:"
2290 PRINT "     P(t)=A+B*sin C(t+D)."
2300 PRINT "Dabei stellt 't' die Zeit dar.":GOSUB 9110
2310 GOSUB 100:PRINT "Mit diesem Programm kannst du selbst"
2320 PRINT "die Funktion zusammenstellen und dann"
2330 PRINT "grafisch darstellen."
2340 PRINT :PRINT "Du kannst ein Screendumpprogramm"
2350 PRINT "plazieren ab Zeilennummer 20000."
2360 PRINT "Wenn du 'S' drueckst, wenn eine Grafik"
2370 PRINT "gezeichnet ist, erhaeltst du"
2380 PRINT "einen Ausdruck auf Papier."
2390 PRINT :PRINT "Es ist ein Demo dabei, so dass"
2400 PRINT "du sehen kannst, was gemeint ist.":PRINT 
2410 PRINT "VIEL SPASS MIT DIESEM PROGRAMM !"
2420 GOSUB 9110:RETURN 
3000 REM  EINGABEN
3010 SR$="EINGABE":GOSUB 9010:IV=0
3020 PRINT :PRINT "Die Raub-Beutetierfunktion hat die"
3030 PRINT "Form: A+B*sin C(t+D), (t ist die Zeit)."
3040 PRINT :PRINT "Gib die Parameter fuer die Raub-"
3050 PRINT "tierfunktion ein."
3060 GOSUB 3130:RA=PA:RB=PB:RC=PC:RD=PD
3070 PRINT :PRINT "Gib die Parameter fuer die Beute-"
3080 PRINT "tierfunktion ein.":GOSUB 3130
3090 PRINT :PRINT "Eingabe der minimalen Zeit: ";:INPUT MT
3100 PRINT "Eingabe der maximalen Zeit: ";:INPUT TM
3110 PRINT "Eingabe des Zeitintervalls: ";:INPUT IT
3120 RETURN 
3130 PRINT :PRINT "A=";:INPUT PA
3140 PRINT "B=";:INPUT PB
3150 PRINT "C=";:INPUT PC$:PC=VAL (PC$)
3160 FOR I=1 TO LEN (PC$):H$=MID$(PC$,I,2)
3170 IF (H$="PI") OR (H$="pi") THEN PC=PC*P1
3180 NEXT I
3190 PRINT "D=";:INPUT PD:RETURN 
4000 REM  ABAENDERN
4010 SR$="ABAENDERN":GOSUB 9010
4020 PRINT :PRINT "Was willst du aendern:"
4030 PRINT :PRINT "Von den Raubtierfunktionen:":PRINT 
4040 PRINT " 1. A (nun";RA;")":PRINT " 2. B (nun";RB;")"
4050 PRINT " 3. C (nun";RC;")":PRINT " 4. D (nun";RD;")"
4060 PRINT :PRINT "Von den Beutetierfunktionen:":PRINT 
4070 PRINT " 5. A (nun";PA;")":PRINT " 6. B (nun";PB;")"
4080 PRINT " 7. C (nun";PC;")":PRINT " 8. D (nun";PD;")"
4090 PRINT :PRINT " 9. Die minimale Zeit (nun";MT;")"
4100 PRINT "10. Die maximale Zeit (nun";TM;")"
4110 PRINT "11. Das Zeitintervall (nun";IT;")"
4120 PRINT :PRINT "Triff deine Wahl: ";:INPUT KE
4130 IF KE=0 THEN RETURN 
4140 IF (KE<1) OR (KE>11) THEN GOTO 4120
4150 IF KE>8 THEN GOTO 4170
4160 ON KE GOTO 4180,4210,4240,4300,4180,4210,4240,4300
4170 ON KE-8 GOTO 4330,4340,4350
4180 PRINT "Gib das neue A:";:INPUT H
4190 IF KE=1 THEN RA=H:GOTO 4360
4200 PA=H:GOTO 4360
4210 PRINT "Gib das neue B:";:INPUT H
4220 IF KE=2 THEN RB=H:GOTO 4360
4230 PB=H:GOTO 4360
4240 PRINT "Gib das neue C:";:INPUT H$
4250 H=VAL (H$):FOR I=1TO LEN (H$):PC$=MID$(H$,I,2)
4260 IF (PC$="PI") OR (PC$="pi") THEN H=H*P1
4270 NEXT I
4280 IF KE=3 THEN RC=H:GOTO 4360
4290 PC=H:GOTO 4360
4300 PRINT "Gib das neue D:";:INPUT H
4310 IF KE=4 THEN RD=H:GOTO 4360
4320 PD=H:GOTO 4360
4330 PRINT "Gib die neue minimale t:";:INPUT MT:GOTO 4360
4340 PRINT "Gib die neue maximale t:";:INPUT TM:GOTO 4360
4350 PRINT "Gib das neue Zeitintervall";:INPUT IT
4360 HO=0:VE=VB:GOSUB 110
4370 PRINT "Willst du noch mehr aendern (J/N)";:GOSUB 210
4380 IF IN=74 THEN GOTO 4010
4390 RETURN 
5000 REM  TAFEL
5010 SR$="TAFEL":GOSUB 9010:IF PR=2 THEN GOTO 5130
5020 PRINT :PRINT "Soll die Ausgabe auf den Drucker (J/N)";
5030 GOSUB 210:IF IN=78 THEN PR=0:GOTO 5130
5040 SR$="Die Raubtierfunktion ist: ":GOSUB 350
5050 H1=RA:H2=RB:H3=RC:H4=RD:GOSUB 5080
5060 SR$="Die Beutetierfunktion ist: ":GOSUB 350
5070 H1=PA:H2=PB:H3=PC:H4=PD:GOSUB 5080:GOTO 5130
5080 SR=H1:GOSUB 300:H$=SR$+"+"
5090 SR=H2:GOSUB 300:H$=H$+SR$+"*sin ":SR=H3:GOSUB 300
5100 H$=H$+SR$+"(t":IF H4>= 0 THEN H$=H$+"+"
5110 SR=H4:GOSUB 300:H$=H$+SR$+")":SR$=H$
5120 GOSUB 350:GOSUB 360:RETURN 
5130 HO=0:VE=2:GOSUB 110
5140 PR$="  Zeit    Beutetier   Raubtier    ":SR$=PR$
5150 IF PR=1 THEN GOSUB 360:GOSUB 350:GOSUB 360
5160 PRINT SR$:SP$="   ----------------------------"
5170 SR$=SP$:IF PR=1 THEN GOSUB 350:GOSUB 360
5180 PRINT SR$:RT=2
5190 FOR T=MT TO TM STEP IT
5200 SR=T:CT=4:CN=1:GOSUB 310:H$="       "+SR$+"     "
5210 GOSUB 9140:CN=0:CT=6
5220 RT=RT+1:IF (RT<VB-1) OR (PR=1) THEN GOTO 5240
5230 GOSUB 9110:GOSUB 100:PRINT PR$:PRINT SP$:RT=1
5240 SR=X:GOSUB 310:H$=H$+SR$:SR=Y:GOSUB 310
5250 SR$=H$+"     "+SR$:PRINT SR$
5260 IF PR=1 THEN GOSUB 350:GOSUB 360
5270 NEXT T
5280 GOSUB 9110:RETURN 
6000 REM  PARAMETER
6010 GOSUB 600:CN=0
6020 HO=.01:VE=0:GOSUB 620:VE=.91:GOSUB 630
6030 HO=UW:GOSUB 630
6040 FOR VE=0 TO .91 STEP .182
6050 HO=0:GOSUB 620:HO=.02:GOSUB 630
6060 NEXT VE
6070 FOR HO=UW TO .01 STEP -.1
6080 VE=.903:GOSUB 620:VE=.917:GOSUB 630
6090 NEXT HO
6100 SR$="0":VE=.94:HO=0:GOSUB 650
6110 SR=RG+INT (.1*RG):GOSUB 300:HO=.02:VE=0:GOSUB 650
6120 SR=PG+INT (.1*PG):GOSUB 300:HO=1-LEN (SR$)/32
6130 VE=.86:GOSUB 650:FOR T=MT TO TM STEP IT
6140 GOSUB 9140:H=X/(PG+.1*PG):V=1-Y/(RG+.1*RG)
6150 IF T=MT THEN HO=H:VE=V:GOSUB 620:X1=H:Y1=V:GOTO 6170
6160 HO=X1:VE=Y1:GOSUB 620:HO=H:VE=V:GOSUB 630:X1=H:Y1=V
6170 NEXT T
6180 SR$="Beutetier":HO=.75:VE=.94:GOSUB 650
6190 FOR T=1 TO 8
6200 SR$=MID$("Raubtier",T,1):VE=T/25+.05:HO=.03
6210 GOSUB 650:NEXT T
6220 GOSUB 9180:RETURN 
7000 REM  GRAFIK
7010 GOSUB 600:CN=0
7020 VE=0:HO=.01:GOSUB 620:VE=.91:GOSUB 630
7030 HO=.989:GOSUB 630:VE=0:GOSUB 630
7040 FOR VE=0 TO .91 STEP .185
7050 HO=0:GOSUB 620:HO=.02:GOSUB 630
7060 HO=.98:GOSUB 620:HO=UW:GOSUB 630
7070 NEXT VE
7080 FOR HO=.01 TO 1 STEP 1/((TM-MT)/IT)
7090 VE=.903:GOSUB 620:VE=.917:GOSUB 630
7100 NEXT HO
7110 SR=MT:GOSUB 300:HO=0:VE=.94:GOSUB 650
7120 SR=TM:GOSUB 300:HO=1-LEN (SR$)/32:GOSUB 650
7130 SR=PG:GOSUB 300:HO=.02:VE=0.02:GOSUB 650
7140 VE=.87:SR=PK:GOSUB 300:GOSUB 650
7150 SR=RG:GOSUB 300:VE=.02:HO=1-LEN (SR$)/32:GOSUB 650
7160 SR=RK:GOSUB 300:VE=.87:HO=1-LEN (SR$)/32:GOSUB 650
7170 FOR T=MT TO TM STEP IT
7180 H=(T-MT)*TM/(TM-MT)/(TM/.98)+.01
7190 GOSUB 9140
7200 V=.91-(X-PK)*PG/(PG-PK)/(PG/.91)
7210 IF T=MT THEN VE=V:HO=H:GOSUB 620:Y1=V:X1=H:GOTO 7230
7220 HO=X1:VE=Y1:GOSUB 620:HO=H:VE=V:GOSUB 630:Y1=V
7230 IF T=MT+10*IT THEN SR$="B":GOSUB 650
7240 V=.91-(Y-RK)*RG/(RG-RK)/(RG/.91)
7250 IF T=MT THEN VE=V:GOSUB 620:Y2=V:GOTO 7270
7260 HO=X1:VE=Y2:GOSUB 620:HO=H:VE=V:GOSUB 630:Y2=V:X1=H
7270 IF T=MT+7*IT THEN SR$="R":GOSUB 650
7280 NEXT T
7290 GOSUB 9180:RETURN 
8000 REM  KOMBINATION
8010 GOSUB 600:CN=0
8020 HO=.5:VE=0:GOSUB 620:VE=UW:GOSUB 630
8030 HO=UW:VE=.5:GOSUB 620:HO=0:GOSUB 630
8040 FOR VE=0 TO 1 STEP .0999
8050 HO=.495:GOSUB 620:HO=.507:GOSUB 630:NEXT VE
8060 FOR HO=0 TO 1 STEP .0999
8070 VE=.495:GOSUB 620:VE=.505:GOSUB 630:NEXT HO
8080 SR=RG:GOSUB 300:HO=.5-LEN (SR$)/32:VE=0:GOSUB 650
8090 SR=RK:GOSUB 300:HO=.5-LEN (SR$)/32:VE=.45:GOSUB 650
8100 SR=PK:GOSUB 300:HO=.51:GOSUB 650
8110 SR=PG:GOSUB 300:HO=1-LEN (SR$)/32:GOSUB 650
8120 SR=MT:GOSUB 300:HO=0:VE=.52:GOSUB 650
8130 HO=.5+LEN (SR$)/32:GOSUB 650
8140 SR=TM:GOSUB 300:HO=.5-LEN (SR$)/32:GOSUB 650
8150 VE=.96:HO=.5+(LEN (SR$)-1)/32:GOSUB 650
8160 SR$="Zeit":HO=.2:VE=.52:GOSUB 650
8170 FOR I=1 TO 4:SR$=MID$("Zeit",I,1)
8180 VE=I*.04+.6:HO=.46:GOSUB 650:NEXT I
8190 SR$="Beute":HO=.7:VE=.52:GOSUB 650
8200 FOR I=1 TO 4:SR$=MID$("Raub",I,1)
8210 VE=I*.04+.1:HO=.46:GOSUB 650:NEXT I
8220 N=1:IP=INT (((TM-MT)/IT/4)):AP=0
8230 FOR T=MT TO TM STEP IT
8240 GOSUB 9140
8250 V=(T-MT)*TM/(TM-MT)/(TM/.485)+.51
8260 H=(X-PK)*PG/(PG-PK)/(PG/.485)+.51
8270 IF T=MT THEN VE=V:HO=H:GOSUB 620:X1=H:Y1=V:GOTO 8290
8280 HO=X1:VE=Y1:GOSUB 620:HO=H:VE=V:GOSUB 630:X1=H:Y1=V
8290 H=(T-MT)*TM/(TM-MT)/(TM/.49)+.01
8300 V=.49-(Y-RK)*RG/(RG-RK)/(RG/.49)
8310 IF T=MT THEN VE=V:HO=H:GOSUB 620:X2=H:Y2=V:GOTO 8330
8320 HO=X2:VE=Y2:GOSUB 620:HO=H:VE=V:GOSUB 630:X2=H:Y2=V
8330 H=(X-PK)*PG/(PG-PK)/(PG/.485)+.51
8340 V=.49-(Y-RK)*RG/(RG-RK)/(RG/.49)
8350 IF T=MT THEN HO=H:VE=V:GOSUB 620:X3=H:Y3=V:GOTO 8390
8360 HO=X3:VE=Y3:GOSUB 620:HO=H:VE=V:GOSUB 630:X3=H:Y3=V
8370 AP=AP+1:IF (AP<IP*N) OR (N>3) THEN GOTO 8390
8380 ON N GOSUB 9210,9410,9610:N=N+1
8390 NEXT T
8400 GOSUB 9180:RETURN 
8500 REM  DEMO
8510 RESTORE :FOR I=1 TO 9:READ IN$:NEXT I
8520 READ PA:READ PB:READ PC:READ PD
8530 READ RA:READ RB:READ RC:READ RD
8540 READ MT:READ TM:READ IT
8550 RG=RA+RB:RK=RA-RB:PG=PA+PB:PK=PA-PB:PR=2
8560 FOR I=1 TO 4:ON I GOSUB 5010,6010,7010,8010
8570 NEXT I:IV=0:RETURN 
9000 REM  ZENTRIEREN, REVERS DRUCKEN
9010 GOSUB 100:VE=0:HO=INT (HB/2-LEN (SR$)/2)-3
9020 GOSUB 110:GOSUB 150:PRINT :RETURN 
9030 REM  LINIEN ZIEHEN
9040 FOR I=1 TO HB:PRINT "-";:NEXT I:RETURN 
9100 REM  TASTE
9110 VE=VB:HO=HB-7:GOSUB 110:PRINT "<TASTE>";
9120 GOSUB 210:RETURN 
9130 REM  BEUTETIEREFUNKTION
9140 X=PA+PB*SIN (PC*(T+PD))
9150 REM  RAUBTIEREFUNKTION
9160 Y=RA+RB*SIN (RC*(T+RD)):RETURN 
9170 REM  TASTE HIERHER
9180 SV=15:SD=1:SP=70:GOSUB 400:SP=90:GOSUB 400
9190 GOSUB 210:RETURN 
9200 REM  DREIECK (PUNKT NACH OBEN)
9210 F1=X1:F2=Y1:GOSUB 9300
9220 F1=X2:F2=Y2:GOSUB 9300
9230 F1=X3:F2=Y3:GOSUB 9300
9240 GOSUB 9810:RETURN 
9300 HO=F1-.015:VE=F2+.01:GOSUB 620:HO=HO+.03:GOSUB 630
9310 HO=HO-.015:VE=VE-.03:GOSUB 630:HO=HO-.015
9320 VE=VE+.03:GOSUB 630:HO=HO+.03:GOSUB 630:RETURN 
9400 REM  VIERKANT
9410 F1=X1:F2=Y1:GOSUB 9500
9420 F1=X2:F2=Y2:GOSUB 9500
9430 F1=X3:F2=Y3:GOSUB 9500
9440 GOSUB 9810:RETURN 
9500 HO=F1-.01:VE=F2+.015:GOSUB 620:HO=HO+.02
9510 GOSUB 630:VE=VE-.03:GOSUB 630:HO=HO-.02
9520 GOSUB 630:VE=VE+.03:GOSUB 630:RETURN 
9600 REM  DREIECK (PUNKT NACH UNTEN)
9610 F1=X1:F2=Y1:GOSUB 9700
9620 F1=X2:F2=Y2:GOSUB 9700
9630 F1=X3:F2=Y3:GOSUB 9700
9640 GOSUB 9810:RETURN 
9700 HO=F1-.015:VE=F2-.015:GOSUB 620:HO=HO+.03
9710 GOSUB 630:HO=HO-.015:VE=VE+.03:GOSUB 630
9720 HO=HO-.015:VE=VE-.03:GOSUB 630:RETURN 
9800 REM  PUNKTLINIEN
9810 HO=X1:FOR VE=Y1 TO Y2 STEP -2/VG
9820 GOSUB 620:NEXT VE
9830 VE=Y2:FOR HO=X2 TO X3 STEP 2/HG
9840 GOSUB 620:NEXT HO
9850 RETURN 
20000 RETURN 
20010 REM  ***********************************
20020 REM  HIER KANNST DU EIN SCREENDUMPPROGRAMM
20030 REM  EINBAUEN. WENN EIN GRAFIKPROGRAMM
20040 REM  GEZEICHNET IST UND DU DRUECKST 'S' SPRINGT
20050 REM  DAS PROGRAMM HIERHER.
20060 REM  ***********************************
20070 RETURN 
25000 DATA "Erklaerung","Eingaben","Aenderungen","Tafel"
25010 DATA "Parameter","Grafik","Combinationen"
25020 DATA "Demo","Ende"
25030 REM  DATEN FUER DIE DEMO. DU KANNST DIESE DATEN
25040 REM  NACH EIGENEN BEDUERFNISSEN AENDERN.
25050 REM  NACHFOLGEND STEHT HIER:
25060 REM  A, B, C UND D VON DER BEUTETIERFUNKTION.
25070 REM  A, B, C UND D VON DER RAUBTIERFUNKTION.
25080 REM  MINIMALE ZEIT, MAXIMALE ZEIT, ZEITINTERVALL.
25090 DATA 3400,2200,0.2618,-19
25100 DATA 240,160,0.2618,-4
25110 DATA 0,24,1
32000 REM  *****************************
32010 REM  DURCH RONALD VAN VUGT (PA3EAH)
32020 REM 
32030 REM  POSTBUS 160, 5580 AD, WAALRE
32040 REM 
32050 REM         MAI, 1987, V1.2
32060 REM  *****************************
32070 REM  TROS-RADIO DD 870722
32100 REM 
32110 REM  #####################
32120 REM  UEBERSETZT DURCH :
32130 REM  BERND SCHNEIDER
32140 REM  DR.-W.-KUELZ-STR. 55
32150 REM  BRAND-ERBISDORF,9230
32160 REM  AUF > MRB Z1013 <
32170 REM   +++ OSTERN 1990 +++
32180 REM  #####################
32190 REM 
32200 REM  Erstausstrahlung: Deutschlandsender Kultur
32210 REM -spezial 901212

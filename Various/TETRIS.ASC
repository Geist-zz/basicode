1000 A=100:GOTO 20:REM ## TETRIS ##
1010 DIM X(4),Y(4),U(4),V(4)
1020 VE=0:HO=10:GOSUB 110:SR$="*** TETRIS ***":GOSUB 150
1030 VE=2:HO=0:GOSUB 110
1040 PRINT" In einem Schacht fallen unterschied-"
1050 PRINT" lich geformte Steine herunter."
1060 PRINT" Die Steine  lassen sich  mit den"
1070 SR$="Cursortasten":GOSUB 150
1080 PRINT
1090 PRINT" nach links und rechts bewegen"
1100 PRINT" oder drehen."
1110 PRINT" Es kommt darauf an, die Steine so zu"
1120 PRINT" bewegen, dass  sie geschlossene Rei-"
1130 PRINT" hen bilden. Ist eine Reihe geschlos-"
1140 PRINT" sen,  wird  sie  geloescht  und  die"
1150 PRINT" darueberliegenden  Reihen   rutschen"
1160 PRINT" nach."
1170 PRINT" Fuer jede geschlossene Reihe gibt"
1180 PRINT" es einen Punkt.":R$="los"
1190 HO=0:VE=18:GOSUB 110
1200 PRINT" Nach Eingabe  des Schwierigkeits-"
1210 PRINT" grades (0...5) geht's ";R$;" !"
1220 GOSUB 210:IF (IN<48) OR (IN>53) THEN 1220
1230 R=70-IN
1240 GOSUB 100:VE=4:HO=4:GOSUB 110:PRINT"Punkte:"
1250 VE=6:HO=1:GOSUB 110:PRINT"Schwierig-"
1260 VE=7:HO=6:GOSUB 110:PRINT"keit: ";IN-48
1270 FOR I=0 TO R:VE=I:HO=18:GOSUB 110:PRINT"X"
1280 HO=27:GOSUB 110:PRINT"X":NEXT I
1290 VE=R:HO=18:GOSUB 110:PRINT"XXXXXXXXX"
1300 GOSUB 260:A=INT(RV*7)
1310 X(1)=1:Y(1)=23:X(2)=1:Y(2)=22
1320 X(3)=1:Y(3)=24:X(4)=2:Y(4)=22
1330 IF A>3 THEN X(3)=2:Y(3)=23:IF(A=4) OR (A=6) THEN X(4)=0
1340 IF (A=1) OR (A=6) THEN Y(4)=23:IF A=6 THEN Y(3)=22
1350 IF A=2 THEN Y(4)=24:GOTO 1510
1360 IF A=0 THEN X(4)=1:Y(4)=21
1370 GOTO 1510
1380 B=0:GOSUB 200
1390 IF (IN=28) OR (IN=29) OR (IN=30) OR (IN=31) THEN B=IN
1400 IF B=28 THEN FOR I=1 TO 4:Y(I)=V(I)-1:NEXT I
1410 IF B=29 THEN FOR I=1 TO 4:Y(I)=V(I)+1:NEXT I
1420 IF (B=30) OR (B=31) THEN 1440
1430 GOTO 1460
1440 FOR I=1 TO 4:X(I)=U(1)+V(I)-V(1):Y(I)=V(1)+U(1)-U(I)
1450 NEXT I
1460 Z=0:FOR I=1 TO 4:VE=X(I):HO=Y(I):GOSUB 220
1470 IF (IN=88) OR (IN=42) THEN Z=1
1480 NEXT I
1490 IF Z=1 THEN FOR I=1 TO 4:Y(I)=V(I):X(I)=U(I):NEXT I:GOTO 1380
1500 FOR I=1 TO 4:VE=U(I):HO=V(I):GOSUB 110:PRINT" ":NEXT I
1510 FOR I=1 TO 4:VE=X(I):HO=Y(I):GOSUB 110:PRINT"H":NEXT I
1520 W=0:FOR I=1 TO 4:VE=X(I)+1:HO=Y(I):GOSUB 220
1530 IF (IN=88) OR (IN=42) THEN W=1
1540 NEXT I:IF W=0 THEN 1690
1550 FOR I=1 TO 4:IF X(I)=1 THEN 1710
1560 VE=X(I):HO=Y(I):GOSUB 110:PRINT"*":NEXT I:GOSUB 250
1570 FOR I=1 TO 4:T=1:FOR J=19 TO 26:VE=X(I):HO=J:GOSUB 220
1580 IF IN=32 THEN T=0:J=26
1590 NEXT J:IF T=0 THEN 1680
1600 FOR K=X(I)TO 1 STEP -1:S=0:FOR L=19 TO 26
1610 VE=K-1:HO=L:GOSUB 220
1620 VE=K:HO=L:GOSUB 110:PRINT CHR$(IN)
1630 IF IN=42 THEN S=1
1640 NEXT L:IF S=0 THEN K=1
1650 NEXT K:C=C+1:VE=4:HO=11:GOSUB 110:PRINT C
1660 FOR J=I TO 4:IF X(J)<X(I) THEN X(J)=X(J)+1
1670 NEXT J
1680 NEXT I:GOTO 1300
1690 FOR I=1 TO 4:U(I)=X(I):V(I)=Y(I):X(I)=X(I)+1:NEXT I
1700 GOTO 1380
1710 FOR I=0 TO 2:GOSUB 250:NEXT I
1720 VE=20:HO=1:GOSUB 110:PRINT"Spielende !":GOSUB 210
1730 VE=22:HO=0:GOSUB 110:PRINT"neues Spiel (J) ?"
1740 GOSUB 210:IF IN=74 THEN GOSUB 100:R$="weiter":GOTO 1190
1750 GOTO 950
32000 REM ****************************
32010 REM
32020 REM    nach  Rainer Jung / ND
32030 REM      in BASICODE von
32040 REM Gert      Lautenschlaeger
32050 REM Erlenhof 26  Potsdam 1585
32060 REM 900528       auf  KC 85/4
32070 REM
32080 REM ****************************
32090 REM
32100 REM Erstausstrahlung: DS-Kultur
32110 REM-spezial 900905


1000 A=200:GOTO20:REM WERELD KLOK
1010 GOSUB100:N=184:REM N=AANTAL LANDEN+STEDEN
1020 SB=HO:REM SCHERMBREEDTE
1030 REM +++BEGINSCHERM++++
1040 VE=3:HO=INT(SB/2-8):GOSUB110
1050 SR$="WERELDKLOK":GOSUB150
1060 VE=5:HO=INT(SB/2-10):GOSUB110
1070 SR$="VAN 178 LANDEN":GOSUB150
1080 VE=7:HO=INT(SB/2-19):GOSUB110
1090 SR$="VAN AFGHANISTAN TOT ZWITSERLAND":GOSUB150
1100 VE=15:HO=0:GOSUB110
1110 REM +++UITLEG+++
1120 PRINT"Wilt U uitleg (j/n) ?";
1130 GOSUB210
1140 IFIN=78 THEN1510
1150 IFIN<>74 THEN1130
1160 GOSUB100
1170 PRINT"Dit programma geeft de TIJD van"
1180 PRINT"178 landen in vergelijking"
1190 PRINT"van een referentie land.":PRINT
1200 PRINT"Men kan zoeken naar een LAND"
1210 PRINT"in een register,"
1220 PRINT"op alfabet of bladeren."
1230 PRINT
1240 PRINT"Elk land heeft zijn eigen code,"
1250 PRINT"het programma vraagt naar deze code."
1260 PRINT
1270 PRINT"Het programma houdt geen rekening"
1280 PRINT"met zomertijd.":PRINT
1290 PRINT"De volgorde is"
1300 PRINT"1. Code van het referentie land"
1310 PRINT"2. Code van het land voor de tijd-"
1320 PRINT"   berekening."
1330 PRINT"3. Tijd referentie land."
1340 GOSUB3110
1500 REM +++MENU+++
1510 GOSUB100:VL=0:DG=0
1520 PRINT" Referentie land"
1530 VE=2:HO=0:GOSUB110
1540 PRINT:PRINT" 1. Zoeken naar land code op alfabet."
1550 PRINT:PRINT" 2. Overzicht alle land codes."
1560 PRINT:PRINT" 3. Stoppen."
1570 VE=16:HO=0:GOSUB110
1580 SR$="Maak uw keuze 1-3":GOSUB150
1590 INPUTK
1600 IF(K<1)OR(K>3)THEN1510
1610 ONK GOTO2040,3010,950
2000 REM +++ LANDEN ALFABET +++
2010 PRINT"Er is geen land dat begint"
2020 PRINT"met een Q, X of Y."
2030 SD=50:GOSUB450
2040 GOSUB100:VE=10:HO=1:GOSUB110
2050 PRINT"Geef de eerste letter van"
2060 INPUT" het te zoeken land. ";SR$
2070 RESTORE
2080 GOSUB330:GOSUB100:SR$=SR$
2090 IF(SR$="X")OR(SR$="Y")OR(SR$="Q")THEN2010
2100 READL$,H$,CO$,GT
2110 IFSR$=LEFT$(L$,1)THEN2150
2120 IFLEFT$(L$,1)>SR$ THEN3410
2130 IFLEFT$(L$,1)="*" THEN3410
2140 GOTO2100
2150 PRINTCO$;" ";L$
2160 GOTO2100
3000 REM +++ LANDEN +++
3010 GOSUB100
3020 PRINT"OVERZICHT LANDEN":PRINT
3030 RESTORE:FORI=1 TON
3040 READL$,H$,CO$,GT
3050 PRINTCO$;"  ";L$
3060 GOSUB120:IFVE<21 THEN3080
3070 GOSUB3150:GOSUB100
3080 NEXTI:GOSUB3150:IFVL=1 THEN3480
3090 GOTO1510
3100 REM +++DRUK OP EEN TOETS+++
3110 VE=22:HO=0:GOSUB110
3120 SR$="<Druk op een toets.>"
3130 GOSUB150:GOSUB210:RETURN
3140 REM ++Page Dn  en  Code ++
3150 VE=22:HO=0:GOSUB110
3160 SR$="Druk een toets voor PAGE DN ":GOSUB150
3170 VE=23:HO=0:GOSUB110
3180 SR$="Druk -C- voor code intoetsen":GOSUB150
3190 GOSUB210
3200 IFIN<>67 THENRETURN
3400 REM +++ INPUT CODES +++
3410 IFVL=1 THEN3510
3420 VE=22:HO=0:GOSUB110
3430 SR$="  Type de CODE van het     ":GOSUB150
3440 VE=23:HO=0:GOSUB110
3450 SR$="   referentie land in.     ":GOSUB150
3460 INPUTSR$:GOSUB330:VL=1
3470 C1$=SR$
3480 GOSUB100
3490 PRINT" Het te berekenen land"
3500 GOTO1530
3510 VE=22:HO=0:GOSUB110
3520 SR$="  Type de CODE van het te   ":GOSUB150
3530 VE=23:HO=0:GOSUB110
3540 SR$="  berekenen land in.        ":GOSUB150
3550 INPUTSR$:GOSUB330:C2$=SR$
3560 GOSUB100
3600 REM +++TYD VAN LAND ZOEKEN +++
3610 RESTORE
3620 VL=0
3630 IFVL=2 THEN3810
3640 READL$,H$,CO$,GT
3650 IFC1$=CO$ THEN3680
3660 IFC2$=CO$ THEN3700
3670 GOTO3630
3680 L1$=L$:H1$=H$:G1=GT:VL=VL+1
3690 GOTO3630
3700 L2$=L$:H2$=H$:G2=GT:VL=VL+1
3710 GOTO3630
3800 REM +++ TIJD +++
3810 VE=15:HO=0:GOSUB110
3820 PRINT"Geef nu de tijd op (0.00-23.59)"
3830 PRINT"van referentie land ";L1$
3840 INPUTTY
3850 IF(TY<0)OR(TY>24)THEN3820
3860 UR=INT(TY):MN=INT((TY-INT(TY))*100+.5)
3870 IFMN>59 THEN3820
3880 MU=60*UR:MI=MU+MN
3900 REM +++TYDVERSCHIL BEREKENEN +++
3910 TV=G2-G1
3920 BT=MI+TV:DG=0
3930 IFBT>1439 THENDG=1
3940 IFBT>1439 THENBT=BT-1440
3950 IFBT<0 THENDG=2
3960 IFBT<0 THENBT=BT+1440
3970 U1=INT(BT/60)
3980 M1=BT-U1*60
4000 REM +++ PRINT GEGEVENS +++
4010 GOSUB100:PRINT:PRINT
4020 PRINT"Land:":PRINTL1$
4030 PRINT:PRINT"Hoofdstad:":PRINTH1$
4040 PRINT:PRINT"Tijd: ";UR;" uur ";MN;" min."
4050 PRINT"++++++++++++++++++++++++++++++++++++"
4060 PRINT"Land:":PRINTL2$
4070 PRINT:PRINT"Hoofdstad:":PRINTH2$
4080 PRINT:PRINT"Tijd: ";U1;" uur ";M1;" min."
4090 IFDG=2 THEN4130
4100 IFDG<>1 THEN4150
4110 PRINT:PRINT"Het is al een dag later in"
4120 GOTO4140
4130 PRINT:PRINT"Het is nog de vorige dag in"
4140 PRINTL2$
4150 VE=22:HO=0:GOSUB110
4160 SR$="Nog een opgave J/N ?"
4170 GOSUB150:GOSUB210
4180 IFIN=74 THEN1510
4190 GOSUB950
25000 REM +++DATA REGELS +++
25010 DATA"AFGANISTAN","KABOEL","A1",270
25020 DATA"ALBANIE","TIRANA","A2",60
25030 DATA"ALGERIJE","ALGIERS","A3",60
25040 DATA"ANDORRA","ANDORRA-LA-VELLA","A4",60
25050 DATA"ANGOLA","LUANDA","A5",60
25060 DATA"ANTIGUA & BARBUDA","ST.JOHN'S","A6",-240
25070 DATA"ARGENTINIE","BUENOS AIRES","A7",-180
25080 DATA"AUSTRALIE (OOST)","CANBERRA","A8",600
25090 DATA"AUSTRALIE (WEST)","PERTH","A9",480
25100 DATA"BAHAMA-EILANDEN","NASSAU","B1",-300
25110 DATA"BAHREIN","AL-MANAMAH","B2",180
25120 DATA"BANGLADESH","DACCA","B3",360
25130 DATA"BARBADOS","BRIDGETOWN","B4",-240
25140 DATA"BELGIE","BRUSSEL","B5",60
25150 DATA"BELIZE","BELMOPAN","B6",-360
25160 DATA"BENIN","PORTO NOVA","B7",60
25170 DATA"BHUTAN","THIMBU","B8",330
25180 DATA"BOLIVIA","SUCRE","B9",-240
25190 DATA"BOTSWANA","GABARONE","B10",120
25200 DATA"BOVENVOLTA","OUAGADOUGOU","B11",0
25210 DATA"BRAZILIE","BRASILIA","B12",-180
25220 DATA"BRUNEI","BANDER","B13",480
25230 DATA"BULGARIJE","SOFIA","B14",120
25240 DATA"BURMA","RANGOON","B15",390
25250 DATA"BURUNDI","BUJUMBURA","B16",120
25260 DATA"CANADA (OOST)","OTTAWA","C1",-300
25270 DATA"CANADA (WEST)","VANCOUVER","C2",-480
25280 DATA"CENTRAFRIKA","BANGUI","C3",60
25290 DATA"CHILI","SANTIAGO","C4",-240
25300 DATA"CHINA","BEIJING (PEKING)","C5",480
25310 DATA"COLOMBIA","BOGOTA","C6",-300
25320 DATA"COMOREN","MORINI","C7",180
25330 DATA"COSTA RICA","SAN-JOSE","C8",-360
25340 DATA"CUBA","HAVANA","C9",-300
25350 DATA"CYPRES","NICOSIA","C10",120
25360 DATA"DENEMARKEN","KOPENHAGEN","D1",60
25370 DATA"DJIBOUTI","DJIBOUTI","D2",180
25380 DATA"DOMINICA","ROSEAU","D3",-240
25390 DATA"DOMINICAANSE REP.","SANTO DOMINGO","D4",-240
25400 DATA"DUITSLAND (WEST)","BONN","D5",60
25410 DATA"DUITSLAND (OOST)","BERLIJN","D6",60
25420 DATA"ECUADOR","QUITO","E1",-300
25430 DATA"EGYPTE","CAIRO","E2",120
25440 DATA"EL-SALVADOR","SAN-SALVADOR","E3",-360
25450 DATA"EQUATORIAAL GUINEA","MALABO","E4",60
25460 DATA"ETIOPIE","ADDIS ABEBA","E5",180
25470 DATA"FAEROER","THORSHAVN","F1",0
25480 DATA"FIJI","SUVA","F2",720
25490 DATA"FILIPIJNEN","MANILA","F3",480
25500 DATA"FINLAND","HELSINKI","F4",120
25510 DATA"FRANKRIJK","PARIJS","F5",60
25520 DATA"FRANS GUYANA","CAYENNE","F6",-180
25530 DATA"GABON","LIBREVILLE","G1",60
25540 DATA"GAMBIA","BANJUL","G2",0
25550 DATA"GHANA","ACCRA","G3",0
25560 DATA"GRENADA","ST.GEORGE'S","G4",-240
25570 DATA"GRIEKENLAND","ATHENE","G5",120
25580 DATA"GROENLAND","GODTHAB","G6",-240
25590 DATA"GROOTBRITTANNIE","LONDEN","G7",0
25600 DATA"GUATEMALA","GUATEMALA","G8",-480
25610 DATA"GUINEE","CONAKRY","G9",0
25620 DATA"GUINEE-BISSAU","BISSAU","G10",0
25630 DATA"GUYANA","GEORGETOWN","G11",-180
25640 DATA"HAITI","PORT-AU-PRINCE","H1",-300
25650 DATA"HONDURAS","TEGUCIGALPA","H2",-360
25660 DATA"HONGARIJE","BUDAPEST","H3",60
25670 DATA"HONGKONG","VICTORIA","H4",480
25680 DATA"IERLAND","DUBLIN","I1",0
25690 DATA"IJSLAND","REYKJAVIK","I2",0
25700 DATA"INDIA","NEW-DELHI","I3",330
25710 DATA"INDONESIE","JAKARTA","I4",420
25720 DATA"IRAK","BAGDAD","I5",180
25730 DATA"IRAN","TEHERAN","I6",210
25740 DATA"ISRAEL","JERUZALEM","I7",120
25750 DATA"ITALIE","ROME","I8",60
25760 DATA"IVOORKUST","YAMOUSSOUKRO","I9",0
25770 DATA"JAMAICA","KINGSTON","J1",-300
25780 DATA"JAPAN","TOKIO","J2",540
25790 DATA"JOEGOSLAVIE","BELGRADO","J3",60
25800 DATA"JORDANIE","AMMAN","J4",120
25810 DATA"KAAPVERDIE","PRAIA","K1",-60
25820 DATA"KAMBODJA","PHNUM PENH","K2",420
25830 DATA"KAMEROEN","YAOUNDE","K3",60
25840 DATA"KATAR","DOHA","K4",180
25850 DATA"KENYA","NAIROBI","K5",180
25860 DATA"KIRIBATI","BAIRIKI","K6",-660
25870 DATA"KOEWEIT","KOEWEIT","K7",180
25880 DATA"KONGO","BRAZZAVILLE","K8",60
25890 DATA"LAOS","VIENTIANE","L1",420
25900 DATA"LESOTHO","MASERU","L2",120
25910 DATA"LIBANON","BEIROET","L3",120
25920 DATA"LIBERIA","MONROVIA","L4",0
25930 DATA"LIBIE","TRIPOLI","L5",60
25940 DATA"LIECHTENSTEIN","VADUZ","L6",60
25950 DATA"LUXEMBURG","LUXEMBURG","L7",60
25960 DATA"MADAGASKAR","ANTANANAROVI","M1",180
25970 DATA"MALAWI","LILONGWE","M2",120
25980 DATA"MALDIVEN","MALE","M3",300
25990 DATA"MALEISIE","KUALA LUMPUR","M4",480
26000 DATA"MALI","BAMAKO","M5",0
26010 DATA"MALTA","VALLETTA","M6",60
26020 DATA"MAROKKO","RABAT","M7",60
26030 DATA"MAURITANIE","NOUAKCHOTT","M8",0
26040 DATA"MAURITIUS","PORT LOUIS","M9",240
26050 DATA"MEXICO","MEXICO-STAD","M10",-480
26060 DATA"MONACO","MONACO","M11",60
26070 DATA"MONGOLIE","ULAANBAATAR","M12",480
26080 DATA"MOZAMBIQUE","MAPUTO","M13",120
26090 DATA"NAMIBIE","WINDHOEK","N1",120
26100 DATA"NAURU","YAREN","N2",720
26110 DATA"NEDERLAND","AMSTERDAM","N3",60
26120 DATA"NEDERLANDSE ANTILLEN","WILLEMSTAD","N4",-240
26130 DATA"NEPAL","KATMANDU","N5",345
26140 DATA"NICARAGUA","MANAQUA","N6",-360
26150 DATA"NIEUWZEELAND","WELLINGTON","N7",720
26160 DATA"NIGER","NIAMEY","N8",60
26170 DATA"NIGERIA","ABUJA","N9",60
26180 DATA"NOORDJEMEN","SANA","N10",180
26190 DATA"NOORDKOREA","PYONGYANG","N11",540
26200 DATA"NOORWEGEN","OSLO","N12",60
26210 DATA"OMAN","MASQAT","O1",240
26220 DATA"OOSTENRIJK","WENEN","O2",60
26230 DATA"PAKISTAN","ISLAMABAD","P1",300
26240 DATA"PANAMA","PANAMA","P2",-300
26250 DATA"PAPUA-NIEUWGUINEA","PORT MORESBY","P3",600
26260 DATA"PARAGUAY","ASUNCION","P4",-240
26270 DATA"PERU","LIMA","P5",-300
26280 DATA"POLEN","WARSCHAU","P6",60
26290 DATA"PORTUGAL","LISSABON","P7",0
26300 DATA"PUERTO RICO","SAN-JUAN","P8",-240
26310 DATA"ROEMENIE","BOEKAREST","R1",120
26320 DATA"RWANDA","KIGALI","R2",120
26330 DATA"SAINT KITTS-NEVIS","BASSETERRE","S1",-240
26340 DATA"SAN-MARINO","SAN-MARINO","S2",60
26350 DATA"SAO-TOME & PRINCIPE","SAO-TOME","S3",0
26360 DATA"SAUDIARABIE","RIYADH","S4",180
26370 DATA"SENEGAL","DAKAR","S5",0
26380 DATA"SEYCHELLEN","VICTORIA","S6",240
26390 DATA"SIERRA LEONE","FREETOWN","S7",0
26400 DATA"SINGAPORE","SINGAPORE","S8",420
26410 DATA"SINT LUCIA","CASTRIES","S9",-240
26420 DATA"SINT VINCENT","KINGSTOWN","S10",-240
26430 DATA"SOLOMONEILANDEN","HONIARA","S11",660
26440 DATA"SOMALIA","MOGADISHO","S12",180
26450 DATA"SOVJETUNIE (WEST)","MOSKOU","S13",180
26460 DATA"SOVJETUNIE (MID)","NOVOSIBIRSK","S14",420
26470 DATA"SOVJETUNIE (OOST)","-----","S15",660
26480 DATA"SPANJE","MADRID","S16",60
26490 DATA"SRI-LANKA","COLOMBO","S17",330
26500 DATA"SUDAN","KHARTOEM","S18",120
26510 DATA"SURINAME","PARAMARIBO","S19",-180
26520 DATA"SWAZILAND","MBABANE","S20",120
26530 DATA"SYRIE","DAMASKUS","S21",180
26540 DATA"TAIWAN","T'AIPEI","T1",480
26550 DATA"TANZANIA","DAR-ES-SALAAM","T2",180
26560 DATA"THAILAND","BANGKOK","T3",560
26570 DATA"TOGO","LOME","T4",0
26580 DATA"TONGA","NUKUALOFA","T5",780
26590 DATA"TRINIDAD & TOBAGO","PORT OF SPAIN","T6",-240
26600 DATA"TSJAAD","NDJAMENA","T7",60
26610 DATA"TSJECHOSLOWAKIJE","PRAAG","T8",60
26620 DATA"TUNESIE","TUNIS","T9",60
26630 DATA"TURKIJE","ANKARA","T10",120
26640 DATA"TUVALU","FUNAFUTI","T11",720
26650 DATA"UGANDA","KAMPALA","U1",180
26660 DATA"URUGUAY","MONTEVIDEO","U2",-180
26670 DATA"VANUATU","VILA","V1",660
26680 DATA"VATICAANSTAD","+++++","V2",60
26690 DATA"VENEZUELA","CARACAS","V3",-240
26700 DATA"VERENIGDE ARAB. EMIRATEN","ABU-DHABY","V4",240
26710 DATA"VERENIGDE STATEN (OOST)","WASHINGTON (DC)"
26720 DATA"V5",-300
26730 DATA"VERENIGDE STATEN (WEST)","LOS ANGELES"
26740 DATA"V6",-480
26750 DATA"VIETNAM","HA-NOI","V7",420
26760 DATA"WESTSAHARA","LAAYOUNE","W1",0
26770 DATA"WESTSAMOA","APIA","W2",-720
26780 DATA"ZAIRE","KINSHASA","Z1",60
26790 DATA"ZAMBIA","LUSAKA","Z2",120
26800 DATA"ZIMBABWE","HARARE","Z3",120
26810 DATA"ZUIDAFRIKA","PRETORIA","Z4",120
26820 DATA"ZUIDJEMEN","ADEN","Z5",180
26830 DATA"ZUIDKOREA","SEOEL","Z6",540
26840 DATA"ZWEDEN","STOCKHOLM","Z7",60
26850 DATA"ZWITSERLAND","BERN","Z8",60
26860 DATA"************************","*","*",0
30000 REM **************************
30010 REM *                        *
30020 REM * WERELDKLOK             *
30030 REM *          van           *
30040 REM *            178 LANDEN. *
30050 REM *                        *
30060 REM **************************
30070 REM
32000 REM ******** TROS ************
32010 REM *                        *
32020 REM *    J van NOORT         *
32030 REM *                        *
32040 REM      PEPPELSTRAAT 16.
32050 REM T                        T
32060 REM R    3203 VK             R
32070 REM O                        O
32080 REM S    SPIJKENISSE         S
32090 REM
32100 REM *    MEI  1990           *
32110 REM *                        *
32120 REM *    LASER XT/3-8086     *
32130 REM *                        *
32140 REM ****** BASICODE-3 ********
32150 REM
32160 REM TROS-RADIO dd 900912

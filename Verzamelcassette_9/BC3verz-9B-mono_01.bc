
1000 A=100:GOTO20:REM ** FRACTALS **
1010 GOTO3030:REM intro
1100 L=0:FORK=1 TON
1110 C=A*A-B*B+X:D=A*B:D=D+D+Y:A=C:B=D
1120 IFABS(A)+ABS(B)>M THENL=K:K=N
1130 NEXTK:IFL=0 THENL=N
1140 RETURN
1200 L=0:FORK=1 TON:E=A*A:F=B*B
1210 C=A*(E-3*F)+X:D=B*(3*A*B-F)+Y
1220 A=C:B=D:IFABS(A)+ABS(B)>M THENL=K:K=N
1230 NEXTK:IFL=0 THENL=N
1240 RETURN
1300 L=0:FORK=1 TON:D=A*B:E=A*A:F=B*B
1310 C=(E-F)*D*4+Y:D=E*E-6*D*D+F*F+X
1320 A=D:B=C:IFABS(A)+ABS(B)>M THENL=K:K=N
1330 NEXTK:IFL=0 THENL=N
1340 RETURN
1390 REM ** PLANAIRE WEERGAVE **
1400 Y=YU-DY:FORI=0 TOZG-1
1410 Y=Y+DY:X=XU-DX:FORJ=0 TOSG-1:X=X+DX
1420 A=0:B=0:ONMD GOSUB1100,1200,1300
1430 IFL/2=INT(L/2)THENHO=J/S:VE=I/Z:GOSUB620
1440 NEXTJ:NEXTI:RETURN
1490 REM ** WEERGAVE ALS GEBERGTE **
1500 XM=XU+.5*(XO-XU):Y=YU-DY:FORI=0 TOZG:MM=(ZG-.5*I)/ZG
1510 Y=Y+DY:X=XU-DX:FORJ=0 TOSG-1:X=X+DX
1520 A=0:B=0:ONMD GOSUB1100,1200,1300
1530 H=INT(S*(.5+(X-XM)/(XO-XU)*MM))
1540 F=INT(Z-I-.65*MM*Z*L/N)
1550 S1=0:IFF>=Q(H)THENS1=1
1560 IFF<=U(H)THENS1=-1
1570 IFJ=0 THENS0=0:IFS1<>0 THENVE=F/Z:HO=H/S:GOSUB620
1580 IFJ=0 THEN1640
1590 IF(S1=1)AND(S0<1)THENVE=Q(H)/Z:HO=H/S:GOSUB620
1600 IF(S1=-1)AND(S0>=0)THENVE=U(H)/Z:HO=H/S:GOSUB620
1610 IFS0*S1>0 THENHO=H/S:VE=F/Z:GOSUB630:GOTO1640
1620 IFS0=1 THENHO=H/S:VE=Q(H)/Z:GOSUB630
1630 IFS0=-1 THENHO=H/S:VE=U(H)/Z:GOSUB630
1640 GOSUB200:IFIN$="$" THEN950
1650 IFF<U(H)THENU(H)=F
1660 IFF>Q(H)THENQ(H)=F
1670 S0=S1:NEXTJ:NEXTI:RETURN
1990 REM ** INFO **
2000 GOSUB100:SR$="FRACTALS IN BASICODE - INFO":GOSUB150
2010 PRINT:PRINT
2020 PRINT"Als men bepaalde complexe functies"
2030 PRINT"met diverse parameters itereert,"
2040 PRINT"ontstaat afhankelijk daarvan ofwel"
2050 PRINT"convergentie, ofwel divergentie."
2060 PRINT"Door dit gedrag grafisch weer te geven"
2070 PRINT"ontstaan interessante afbeeldingen."
2080 PRINT"Zinvolle waarden voor het reele en het"
2090 PRINT"imaginaire deel zijn -4...+4, voor het"
2100 PRINT"aantal iteraties 5..99, voor de"
2110 PRINT"divergentiegrens getallen > 2."
2120 PRINT"Er zijn 2 weergavemogelijkheden:"
2130 PRINT"planair en 3-dimensionaal, met het"
2140 PRINT"aantal iteraties als hoogte."
2150 PRINT"Wegens de omvangrijke berekeningen kan"
2160 PRINT"een plaatje vele uren tijd kosten."
2170 PRINT"Gebruik eventueel een compiler."
2180 PRINT:PRINT"Veel plezier!       <ENTER>":GOSUB210
2190 GOSUB100:RETURN
3000 REM
3010 REM *** Intro ***
3020 REM
3030 GOSUB600:CN=0:Z=VG:S=HG:GOSUB100
3040 SR$="FRACTALS IN BASICODE-3 (C) BIDAT'89":GOSUB150
3050 IF(Z>=80)AND(S>=80)THEN3120
3060 PRINT:PRINT"U HEBT WEINIG GRAFISCHE MOGELIJKHEDEN:"
3070 PRINT"PIXELS HORIZONTAAL:";S;" VERTICAAL:";Z
3080 INPUT"Klopt dat (J/N) ";A$
3090 IF(A$<>"N")AND(A$<>"n")THEN3120
3100 INPUT"Pixels horizontaal:";S
3110 INPUT"       verticaal  :";Z
3120 DIMU(S),Q(S)
3130 PRINT:PRINT"Functiekeuze:"
3140 PRINT:PRINT" <1> Info"
3150 PRINT" <2> Z=Z^2+C":PRINT" <3> Z=Z^3+C"
3160 PRINT" <4> Z=Z^4+C":PRINT" <0> Einde"
3170 PRINT:INPUT"U kiest:";MD:IFMD>4 THEN3170
3180 IFMD=1 THEN GOSUB2000:GOTO3130
3190 MD=MD-1:IFMD<1 THEN950
3200 PRINT:PRINT"Weergave-mogelijkheden:"
3210 PRINT:PRINT" <1> Planair":PRINT" <2> Gebergte"
3220 PRINT:INPUT"U kiest:";DM:IF(DM<1)OR(DM>2)THEN3220
3230 PRINT:PRINT"Keuze van parameters:"
3240 PRINT:INPUT"Reele deel minimum:";XU
3250 INPUT"           maximum:";XO
3260 INPUT"Imaginaire deel minimum:";YU
3270 INPUT"                maximum:";YO
3280 INPUT"Divergentiegrens :";M
3290 INPUT"Aantal iteraties:";N
3300 INPUT"Klopt het tot hier (J/N) ?";A$
3310 IF(A$="N")OR(A$="n")THEN3240
3320 IFDM=2 THENFORI=0 TOS:U(I)=1E4:Q(I)=0:NEXTI
3330 ZG=Z:SG=S:IFDM=2 THENZG=Z/2
3340 DX=(XO-XU)/SG:DY=(YO-YU)/ZG
3350 GOSUB600:CN=0:IFDM=1 THEN GOSUB1400
3360 IFDM=2 THEN GOSUB1500
3370 VE=.95:HO=.8:SR$="Verder ?":GOSUB650
3380 GOSUB210:IFIN=78 THEN950
3390 GOSUB100:GOTO3130
32000 REM        (C) BIDAT 13.6.89
32010 REM Auteur
32020 REM         BERTHOLD BIENER
32030 REM         DDR - ERFURT
32040 REM
32050 REM Erstausstrahlung Radio DDR
32060 REM-spezial 900511
32070 REM
32080 REM TROS-RADIO dd 900926

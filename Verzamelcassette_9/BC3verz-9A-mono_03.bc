
1000 A=200:GOTO20:REM === G E L D ===
1010 DIMT$(8),H(8),V(8),N(8),W(8),V$(8)
1020 DIMS(20):HK=3.1416/8
1030 GOSUB3010
1040 FORN=0 TO20:S(N)=SIN(N*HK):NEXTN
1050 T$(1)="1G":H(1)=.1:V(1)=.16:V$(1)=" x  1.-  "
1060 T$(2)="2-G":H(2)=.1:V(2)=.4:V$(2)=" x  2,50 "
1070 T$(3)="5G":H(3)=.1:V(3)=.65:V$(3)=" x  5.-  "
1080 T$(4)="1O":H(4)=.23:V(4)=.08:V$(4)=" x  10.- "
1090 T$(5)="25":H(5)=.23:V(5)=.32:V$(5)=" x  25.- "
1100 T$(6)="5O":H(6)=.23:V(6)=.56:V$(6)=" x  50.- "
1110 T$(7)="1OO":H(7)=.56:V(7)=.08:V$(7)=" x 100.- "
1120 T$(8)="25O":H(8)=.56:V(8)=.32:V$(8)=" x 250.- "
1130 GOSUB210
1140 GOSUB600:CN=0
1150 REM --- kies at random de aantallen ---
1160 FORK=1 TO8:GOSUB260:N(K)=1+INT(RV*4):NEXTK
1170 IFN(2)=1 THENN(2)=2
1180 IFN(2)=3 THENN(2)=4
1190 T$(8)="250":IFN(8)=1 THENT$(8)="1OOO"
1200 REM --- bereken de waarde per soort ---
1210 W(1)=N(1):W(2)=2.5*N(2):W(3)=5*N(3):W(4)=10*N(4)
1220 W(5)=25*N(5):W(6)=50*N(6):W(7)=100*N(7)
1230 W(8)=250*N(8):IFN(8)=1 THENW(8)=1000
1240 REM --- bereken het totaal ---
1250 TT=0:FORK=1 TO8:TT=TT+W(K):NEXTK
1260 REM --- teken het geld ---
1270 W=1:RC=.015:T=N(1):GOSUB2510
1280 W=2:RC=.02:T=N(2):GOSUB2510
1290 W=3:RC=.012:T=N(3):GOSUB2510
1300 W=4:T=N(4):GOSUB2010
1310 W=5:T=N(5):GOSUB2010
1320 W=6:T=N(6):GOSUB2010
1330 W=7:T=N(7):GOSUB2010
1340 W=8:T=N(8):GOSUB2010
1350 HO=.84:VE=.4:SR$="VUL IN:":GOSUB650:HO=.5
1360 REM --- vraag en antwoord ---
1370 VE=.53:FORP=1 TO8
1380 W=W(P):SR=W:GOSUB300:W$=SR$:L=LEN(W$)
1390 N=N(P):VR$=V$(P)
1400 IF(P=8)AND(N(8)=1)THENVR$=" x 1000.-"
1410 GOSUB1550
1420 VE=VE+.05:NEXTP
1430 SR=TT:GOSUB300:W=TT:W$=SR$:L=LEN(W$)
1440 VE=.96:SR$="TOTAAL    ":GOSUB1580
1450 HO=.05:VE=.9:SR$="GOED GEDAAN !":GOSUB650
1460 VE=.95:SR$="Nog een keer?":GOSUB650
1470 GOSUB210:IFIN=74 THEN1140
1480 IFIN<>78 THEN1470
1490 GOSUB100:GOTO950
1500 REM---------------------------
1510 REM --- input cijfer ---
1520 GOSUB210:IF(IN<48)OR(IN>57)THEN1520
1530 RETURN
1540 REM --- input aantal ---
1550 SR$=" "+VR$:GOSUB650:GOSUB1520:SR$=IN$+VR$:GOSUB650
1560 IFIN<>(N+48)THENCN=1:GOSUB650:CN=0:GOTO1550
1570 REM --- input bedrag ---
1580 SR$=SR$+"= f":FORN=1 TO5-L:SR$=SR$+" ":NEXTN:A$=SR$
1590 SR$=A$:GOSUB650
1600 B$="":FORN=1 TOL:GOSUB1520:B$=B$+IN$
1610 SR$=SR$+IN$:GOSUB650:NEXTN
1620 IFW<>VAL(B$)THENCN=1:GOSUB650:CN=0:GOTO1590
1630 SR$=SR$+".-":GOSUB650
1640 RETURN
2000 REM --- biljet ---
2010 HO=H(W):VE=V(W):X=HO:Y=VE
2020 HO=X+.02:VE=Y+.03:SR$=T$(W):GOSUB650
2030 HO=X+.02:VE=Y+.1:SR$=" GULDEN":GOSUB650
2040 HO=X:VE=Y:GOSUB620
2050 HO=HO+.25:GOSUB630
2060 VE=VE+.18:GOSUB630
2070 HO=HO-.25:GOSUB630
2080 VE=VE-.18:GOSUB630
2090 T=T-1:IFT=0 THENRETURN
2100 VE=VE+.16:GOSUB620
2110 HO=HO-.02:GOSUB630
2120 VE=VE-.18:GOSUB630
2130 HO=HO+.25:GOSUB630
2140 VE=VE+.02:GOSUB630
2150 HO=HO-.25:VE=VE-.02:GOTO2090
2500 REM --- munt ---
2510 XC=H(W):YC=V(W)
2520 SR$=T$(W):HO=XC-.03:VE=YC-.02:GOSUB650
2530 IFW<>2 THEN2560
2540 HO=.092:VE=.35:SR$="1":GOSUB650
2550 HO=.092:VE=.41:SR$="2":GOSUB650
2560 HO=XC+3*RC:VE=YC:GOSUB620
2570 FORM=1 TO16:HO=XC+3*RC*S(M+4)
2580 VE=YC-4*RC*S(M):GOSUB630:NEXTM
2590 T=T-1:IFT=0 THENRETURN
2600 XC=XC-.01:YC=YC-.02
2610 HO=XC+3*RC*S(6):VE=YC-4*RC*S(2)
2620 GOSUB620:FORM=3 TO10
2630 HO=XC+3*RC*S(M+4)
2640 VE=YC-4*RC*S(M):GOSUB630:NEXTM
2650 GOTO2590
3000 REM --- inleiding ---
3010 GOSUB100
3020 SR$="         ":GOSUB150:PRINT
3030 SR$=" G E L D ":GOSUB150:PRINT
3040 SR$="         ":GOSUB150:PRINT:PRINT
3050 PRINT" We gaan samen geld tellen !":PRINT
3060 PRINT" De computer tekent het voor je.":PRINT
3070 PRINT" Guldens, rijksdaalders en munten":PRINT
3080 PRINT" van vijf gulden.":PRINT
3090 PRINT" Bankbiljetten van tien gulden":PRINT
3100 PRINT" en meer, af en toe zelfs een":PRINT
3110 PRINT" van duizend gulden !":PRINT
3120 PRINT" En jij mag alles optellen ..":PRINT
3130 PRINT"         Tik op een toets";:RETURN
3140 REM----------------------------------------
32000 REM Ch.W.Brederode
32010 REM Von Bonninghausenlaan 33
32020 REM 2161 ES  LISSE
32030 REM
32040 REM TROS-RADIO dd 900815

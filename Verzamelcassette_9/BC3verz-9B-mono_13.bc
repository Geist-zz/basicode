
1000 A=700:GOTO20:REM ** BASICODE-3 UITLEG **
1010 HT=HO:VT=VE:S$="":FORI=1 TOHT:S$=S$+" ":NEXTI
1020 DIMA$(12):H=0:T=0:X=0:SX=0:W=0:FR=1:GOSUB280
1030 HL=0:HR=HT:VL=2:VR=VT-2:R$="":READT$
1040 GOSUB1450:HO=(HT-LEN(SR$)-6)/2:VE=0:GOSUB110
1050 GOSUB150:PRINT:PRINT:PRINT:T$="BASICODE"
1060 PRINT"Wilt u de uitleg op "
1070 PRINT:PRINT"   1  Het beeldscherm,"
1080 PRINT"   2  De printer."
1090 PRINT:PRINT"Welk nummer kiest u ";:GOSUB210
1100 SC=IN-48:PRINTSC:IFSC<>2 THENSC=1:GOTO1230
1110 PRINT:PRINT"Hoeveel spaties wilt u aan het"
1120 INPUT"begin van elke regel ";HL
1130 PRINT:PRINT"Hoeveel tekens echte tekst wilt"
1140 INPUT"u op elke regel ";I:HR=HL+I-1
1150 PRINT:PRINT"Hoeveel regels tekst wilt u op"
1160 INPUT"elk blad ";VR:VR=VR-3
1170 PRINT"Gebruikt u (K)ettingpapier of"
1180 PRINT"           (L)osse vellen  (K/L)"
1190 GOSUB210:IFIN=76 THENVL=-1:GOTO1230
1200 IFIN<>75 THEN GOSUB250:GOTO1170
1210 PRINT:PRINT"Hoeveel regels wilt u bij de scheurlijn"
1220 INPUT"laten overslaan ";VL:VR=VR+VL
1230 MM$="":IFHL>0 THENMM$=LEFT$(S$,HL)
1240 GOSUB1390:M$=MM$:AL=VL:IFSC=2 THEN GOSUB1370
1250 IFLEN(R$)>HR-HL THEN GOSUB1910
1260 GOSUB1480:IFR$<>"" THENR$=R$+" "
1270 R$=R$+D$:GOTO1250
1280 IFSC=2 THENSR$=M$+SR$:GOSUB350:GOSUB360:GOTO1300
1290 GOSUB120:PRINTM$;SR$;:VE=VE+1:HO=0:GOSUB110:HL=0
1300 M$=MM$:AL=AL+1:IFAL<VR THENRETURN
1310 IFSC=1 THEN1390
1320 IFVL>=0 THENFORI=1 TOVL:GOSUB360:NEXTI:GOTO1360
1330 PRINT"Leg een nieuw vel in de printer en"
1340 PRINT"druk dan op RETURN/ENTER"
1350 GOSUB210:IFIN<>13 THEN1350
1360 AL=VL
1370 SR$=LEFT$(S$,(HR-HL-LEN(T$))/2)+T$:GOSUB1280
1380 SR$="":GOSUB1280:AL=VL:RETURN
1390 HO=3:VE=VT:GOSUB110
1400 PRINT"<RETURN> = vervolg,<S> = schermdump";
1410 GOSUB210:IFIN=13 THEN1450
1420 IFIN<>83 THEN1410
1430 FORVE=0 TOVT:SR$="":FORHO=0 TOHT:GOSUB220
1440 SR$=SR$+CHR$(IN):NEXTHO:GOSUB350:GOSUB360:NEXTVE
1450 GOSUB100:AL=VL
1460 SR$=T$:PRINTLEFT$(S$,(HT-LEN(SR$))/2-3);:GOSUB150
1470 PRINT:PRINT:RETURN
1480 READD$
1490 IF(D$="*")AND(SC=2)THENSX=1:X=1:GOTO1980
1500 IF(D$="*")OR(D$="?")THEND$="":GOTO1980
1510 IFD$="" THEN1480
1520 IFLEFT$(D$,1)="." THEN1590
1530 IFLEFT$(D$,1)="=" THEN GOSUB1850:GOTO1560
1540 IFLEFT$(D$,1)<>"!" THENRETURN
1550 IFD$="!" THEND$="":GOTO1570
1560 D$=MID$(D$,2)
1570 GOSUB1850:IFAL+3>VR THENJ=3:GOSUB1870
1580 GOTO1510
1590 IFD$<>".E" THEN1640
1600 J=1:GOSUB1850:T$="Tot een volgend keer"
1610 SR$="":GOSUB1280:GOSUB1280:SR$=T$:GOSUB1280:T$=""
1620 SR$="":GOSUB1280:IFAL<>VL THEN1620
1630 GOTO950
1640 IFD$=".Q" THENR$=R$+CHR$(34):GOTO1480
1650 DD$=MID$(D$,2,1):D$=MID$(D$,3)
1660 IFDD$<>"R" THEN1710
1670 IFSC=2 THENRETURN
1680 IFLEN(R$)+6+LEN(D$)>=HR-HL THENSR$=R$:GOSUB1280:R$=""
1690 PRINTM$;R$;:SR$=D$:GOSUB150:M$=""
1700 HL=HL+LEN(R$)+LEN(D$)+6:R$="":GOTO1480
1710 IFDD$<>"M" THEN1740
1720 I=VAL(D$):MM$="":IFI+HL=0 THEN1480
1730 MM$=LEFT$(S$,I+HL):GOTO1480
1740 IFDD$="P" THENJ=VAL(D$):GOSUB1850:GOSUB1870:GOTO1480
1750 IFDD$="T" THENT$=D$:J=10:GOSUB1850:GOSUB1870:GOTO1670
1760 IFDD$="X" THENT$=T$:J=10:GOSUB1850:GOSUB1870
1770 IFDD$="A" THEN GOSUB1850:GOTO1480
1780 IFDD$="Q" THENR$=R$+CHR$(34)+D$:GOTO1480
1790 IFDD$<>"F" THEN1820
1800 J=VAL(D$):GOSUB1850
1810 J=J-1:IFJ>-1 THENSR$="":GOSUB1280:GOTO1810
1820 IFDD$<>"C" THEN1840
1830 SR$=LEFT$(S$,(HR-HL-LEN(D$))/2)+D$:GOSUB1280
1840 GOTO1480
1850 GOSUB1910:SR$=R$:GOSUB1280:R$="":J=J-1
1860 RETURN
1870 IF(SC=1)OR(AL+J>VR)THEN1890
1880 SR$="":GOSUB1280:GOSUB1280:RETURN
1890 IFAL<>VL THENSR$="":GOSUB1280:GOTO1890
1900 RETURN
1910 K=HR-HL+1-LEN(M$)
1920 IFLEN(R$)<=K THENRETURN
1930 IFMID$(R$,K,1)=" " THEN1960
1940 K=K-1:IFK>0 THEN1930
1950 SR$="":GOTO1970
1960 SR$=LEFT$(R$,K-1):R$=MID$(R$,K+1)
1970 GOSUB1280:GOTO1910
1980 READA
1990 IFX=1 THENX=0:GOSUB2170
2000 IFSX=1 THENSX=0:GOTO1510
2010 ONA GOTO2020,2060,2120
2020 GOSUB600:CN=0:VE=.3:HO=.02:GOSUB620
2030 FORI=.02 TO.99 STEP.02
2040 HO=I:GOSUB620:NEXTI
2050 SD=20:GOSUB450:GOSUB100:GOTO1390
2060 GOSUB600:L=0:H=.01
2070 VE=.99:HO=.45:GOSUB620
2080 VE=.1:HO=H:GOSUB630
2090 IFL=24 THEN2110
2100 H=H+.04:L=L+1:GOTO2070
2110 SD=20:GOSUB450:GOSUB100:GOTO1390
2120 GOSUB600:CN=0
2130 VE=.3:HO=.4:SR$="BASICODE":GOSUB650
2140 IFCN=1 THEN2160
2150 SD=20:GOSUB450:CN=1:GOTO2130
2160 SD=20:GOSUB450:GOSUB100:GOTO1390
2170 D$="=Wordt op het scherm getoond.":RETURN
25000 DATA"BASICODE uitlegprogramma deel 7"
25010 DATA"=In de vorige afleveringen zijn we met de"
25020 DATA"subroutines gekomen tot de routine GOSUB 580."
25030 DATA"!We gaan nu verder met de subroutines 600, 620,"
25040 DATA"630, 650 en GOTO 950.",".TGOSUB 600","=Via deze"
25050 DATA"routine komt men in de grafische mode en kan men"
25060 DATA"dus een tekening op het scherm laten zien.","!De"
25070 DATA"cursor wordt op het grafische scherm op punt 0,0"
25080 DATA"geplaatst. Dat is de linkerbovenhoek op het"
25090 DATA"grafische scherm.","=De indeling van het"
25100 DATA"grafische scherm is als volgt:",".X","=0,0 is"
25110 DATA"het punt precies linksboven op het grafische"
25120 DATA"scherm","!1,0 is het punt precies buiten de"
25130 DATA"rechterbovenhoek","!0,1 is het punt precies"
25140 DATA"onder de linkeronderhoek","!en","!1,1 ligt"
25150 DATA"schuin onder de rechteronderhoek van het"
25160 DATA"grafische scherm",".X","=Waarbij de volgende"
25170 DATA"opmerkingen gelden:","=In de grafische mode"
25180 DATA"hebben de gewone opdrachten PRINT, GOSUB 100,"
25190 DATA"110, 120, 150 EN 220 geen betekenis.","!Deze"
25200 DATA"kunnen alleen gebruikt worden als u via GOSUB"
25210 DATA"100 weer uit de grafische mode bent gegaan."
25220 DATA"!De afstand op het beeldscherm wordt gemeten"
25230 DATA"vanaf punt 0,0 naar punt 0,1. Deze afstand is"
25240 DATA"exact 75% van de afstand van 0,0 naar 1,0."
25250 DATA"!Het punt (1/2,1/2) komt dan in het midden van"
25260 DATA"het beeldscherm.","!In het begin van deze serie"
25270 DATA"hadden we het over de waarden van het grafische"
25280 DATA"scherm in de variabelen HG en VG. Als we de"
25290 DATA"waarden van HG of VG tijdens een programma"
25300 DATA"veranderen, heeft dit geen enkele invloed op de"
25310 DATA"werking van de subroutines 600 tot 650.","!In de"
25320 DATA"subroutine 600 wordt het scherm gewist en in de"
25330 DATA"grafische mode gezet. Hierna kunt u dus via de"
25340 DATA"volgende subroutines een punt op het scherm"
25350 DATA"plotten, een lijn trekken of een tekst weergeven"
25360 DATA"op het grafische scherm.",".TGOSUB 620"
25370 DATA"=Allereerst gaan we een punt plotten via de"
25380 DATA"subroutine 620. Dit doet u als volgt:","=2000"
25390 DATA"GOSUB 600 :REM grafische mode","!2010"
25400 DATA"CN=0:VE=.3:HO=.02:GOSUB 620 :REM plot een punt"
25410 DATA"=De waarden van de variabelen HO of VE moeten"
25420 DATA"minstens 0 zijn, maar in ieder geval kleiner"
25430 DATA"dan 1.","!In regel 2010 ziet u de variabele CN."
25440 DATA"!Als CN=0 is, dan wordt het punt en later ook de"
25450 DATA"lijn in de kleur wit geplot. Als CN=1 is,"
25460 DATA"wordt het in het zwart weergegeven."
25470 DATA"!U kunt dit dus gebruiken om bijv. een lijn te"
25480 DATA"laten zien (CN=0) en weer te laten verdwijnen"
25490 DATA"(CN=1).","!Na afloop van de subroutine 620 staat"
25500 DATA"de cursor in het door HO en VE aangegeven"
25510 DATA"eindpunt.","!We gaan een aantal punten op het"
25520 DATA"scherm zetten via de volgende regels."
25530 DATA"=2000 GOSUB 600:CN=0   (grafische mode)","!2010"
25540 DATA"FOR I=.02 TO .99 STEP .02 (herhaal het plotten)"
25550 DATA"!2020 HO=I:GOSUB 620  (plot een punt)","!2030"
25560 DATA"NEXT I  (volgende punt)","=",".X","*",1,"=Nu is"
25570 DATA"het plotten van alleen een aantal punten niet de"
25580 DATA"bedoeling. We kunnen via de volgende subroutine"
25590 DATA"ook een lijn trekken.",".TGOSUB 630","=Via deze"
25600 DATA"subroutine kunnen we een lijn trekken vanuit de"
25610 DATA"laatste positie van VE en HO.","!Dus als we"
25620 DATA"opgeven, na het plotten van een punt op de"
25630 DATA"positie VE=.1 en HO=.45, VE=.9:GOSUB 630 zal er"
25640 DATA"een lijn getrokken worden vanuit het laatste"
25650 DATA"punt naar de positie VE.9."
25660 DATA"!In het volgende voorbeeld wordt"
25670 DATA"er een waaier op het scherm getekend volgens de"
25680 DATA"volgende listing.",".X","!2000 GOSUB"
25690 DATA"600:H=.01:CN=0","!2010 VE=.99:HO=.45:GOSUB 620"
25700 DATA"!2020 VE=.1:HO=H:GOSUB 630","!2030 IF L=25 THEN"
25710 DATA"4000","!2040 H=H+.04:L=L+1:GOTO 2010","!----"
25720 DATA"!----","!4000 (verder met het programma)","=Dat"
25730 DATA"door deze listing een waaier op het scherm wordt"
25740 DATA"getoond, komt doordat er steeds wordt"
25750 DATA"teruggekeerd naar dezelfde positie"
25760 DATA"(VE=.99:HO=.45) en van daaruit een lijn wordt"
25770 DATA"getrokken naar VE=.1 met een wisselende waarde"
25780 DATA"van de variabele HO.","!De tellus met de"
25790 DATA"variabele L is er alleen maar voor om de routine"
25800 DATA"te stoppen als er 24 lijnen zijn getrokken."
25810 DATA"!We gaan nu het geheel in de praktijk bekijken."
25820 DATA".X","*",2,".A","=U heeft dus gezien dat"
25830 DATA"het niet erg moeilijk is om een figuur op het"
25840 DATA"scherm te tekenen. U moet wel altijd in de gaten"
25850 DATA"houden dat u een opdracht geeft, vanuit het punt"
25860 DATA"gezien waar de cursor zich het laatste bevond."
25870 DATA".TGOSUB 650","=Via deze routine is het mogelijk"
25880 DATA"om op het grafische scherm ook tekst weer te"
25890 DATA"geven. Deze tekst moet dan in de variabele SR$"
25900 DATA"staan.","!Dus SR$=",".QBASICODE",".Q:GOSUB 650."
25910 DATA"U hebt bij de subroutine GOSUB 150 al geleerd"
25920 DATA"hoe u een tekst in een variabele kunt zetten."
25930 DATA"!Deze tekst wordt zichtbaar als de variabele CN"
25940 DATA"op 0 staat. Als CN op 1 staat is de tekst niet"
25950 DATA"zichtbaar.","!Zo kan men dus een tekst op het"
25960 DATA"scherm zetten en ook weer laten verdwijnen: zie"
25970 DATA"de volgende listing.",".X","=2000 GOSUB 600:CN=0"
25980 DATA"!2010 VE=.5:HO=.3:SR$=",".QBASICODE",".Q","!2020"
25990 DATA"IF CN=1 THEN 4000","!2030 GOSUB 650","!2040"
26000 DATA"CN=1:GOTO 2010","!----","!----","!4000 (verder"
26010 DATA"met het programma)","=We zullen u dit even in de"
26020 DATA"praktijk laten zien.",".X","*",3,"=Met het"
26030 DATA"opgeven van de tekst moet u oppassen dat deze"
26040 DATA"string niet te groot wordt.","!Normaal kunnen"
26050 DATA"er 40 karakters op het scherm, maar in de"
26060 DATA"grafische mode is dat niet bij alle computers"
26070 DATA"mogelijk, dus pas op!",".TGOTO 950","=Deze"
26080 DATA"laatste opdracht in de reeks is eigenlijk geen"
26090 DATA"subroutine aangezien hier staat GOTO (ga naar)"
26100 DATA"en niet GOSUB (ga naar en keer terug).","!Dit"
26110 DATA"komt omdat deze opdracht alleen gebruikt wordt"
26120 DATA"aan het einde van een programma.","!Normaal"
26130 DATA"wordt in een basic-programma meestal END als"
26140 DATA"einde gebruikt, maar dit is in BASICODE 3 niet"
26150 DATA"toegestaan. U moet het programma altijd stoppen"
26160 DATA"met de opdracht GOTO 950.","!Na deze opdracht"
26170 DATA"staat de computer weer in de normale stand zoals"
26180 DATA"bij het opstarten.","!Tot BASICODE 2 was de"
26190 DATA"opdracht END nog wel toegestaan. Dus als u een"
26200 DATA"BASICODE 2 programma wilt draaien via het"
26210 DATA"BASICODE 3 vertaalprogramma moet u de opdracht"
26220 DATA"END vervangen door GOTO 950.",".TBASICODE"
26230 DATA"=Tot slot nog enkele mededelingen die nodig zijn"
26240 DATA"om een BASICODE-programma volgens het"
26250 DATA"BASICODE-protocol te laten werken."
26260 DATA"!De variabelen die in een programma gebruikt"
26270 DATA"worden, zijn aan enkele beperkingen"
26280 DATA"gebonden.","!Dit om de"
26290 DATA"uitwisselbaarheid van een BASICODE-programma"
26300 DATA"te waarborgen.",".X","!De numerieke variabelen"
26310 DATA"zijn 'real' en in enkele precisie.","!De"
26320 DATA"nauwkeurigheid is, afhankelijk van de computer,"
26330 DATA"soms niet meer dan 6 decimalen.","!U moet dus"
26340 DATA"nooit op een grotere nauwkeurigheid rekenen."
26350 DATA".X","!De namen van variabelen mogen niet langer"
26360 DATA"zijn dan twee tekens.","!Het eerste teken moet"
26370 DATA"een hoofdletter zijn.","!Het tweede teken mag"
26380 DATA"een hoofdletter of een cijfer zijn.","=Een"
26390 DATA"uitzondering hierop zijn de computers Commodore"
26400 DATA"64 en 128.","Bij deze computers wordt tijdens"
26410 DATA"het gebruik van het BASICODE-3 vertaalprogramma"
26420 DATA"de computer in de tekstmode geschakeld."
26430 DATA"!Hierdoor worden alle namen van variabelen"
26440 DATA"in kleine letters gezet.","!Bij deze machines"
26450 DATA"mag dan geen hoofdletter als variabele gebruikt"
26460 DATA"worden.","!Het gebruik van hoofdletters in een"
26470 DATA"string of een tekst is natuurlijk wel"
26480 DATA"toegestaan en soms zelfs wenselijk.",".X"
26490 DATA"!Logische variabelen zijn variabelen die 'waar'"
26500 DATA"of 'onwaar', zijn.","!Er mag geen gebruik worden"
26510 DATA"gemaakt van de eventuele numerieke waarde van"
26520 DATA"logische variabelen. In sommige computers wordt"
26530 DATA"'waar' namelijk voorgesteld door +1, in andere"
26540 DATA"door -1.","!Het resultaat kan alleen gebruikt"
26550 DATA"worden in een IF...THEN-constructie.","!Een"
26560 DATA"opdracht als A=3*(B=1), mag dus absoluut niet!"
26570 DATA"!In zo'n geval kunt u beter de volgende"
26580 DATA"constructie toepassen:"
26590 DATA"=A=0:IF B=1 THEN A=3","=U ziet voordat we de"
26600 DATA"variabele A gaan gebruiken,"
26610 DATA"deze eerst op 0 gezet wordt.","!In een"
26620 DATA"BASICODE-programma moet u een door u gebruikte"
26630 DATA"variabele altijd een waarde geven.","!U mag er"
26640 DATA"niet van uit gaan dat een variabele automatisch"
26650 DATA"op nul wordt gezet.","!Een stringvariabele mag"
26660 DATA"niet langer zijn dan 255 karakters.",".X","!De"
26670 DATA"namen van de variabelen mogen niet beginnen met"
26680 DATA"een O. Ook de volgende variabelen mogen in"
26690 DATA"BASICODE niet worden gebruikt.","=AS, AT, DI,"
26700 DATA"EI, FN, GO, GR, IF, LN, PI, SQ, ST, TI, TI$,"
26710 DATA"TO.","=Als u de subroutines aanroept moet u"
26720 DATA"altijd gebruik maken van een van de volgende"
26730 DATA"variabelen:","=CN, CT, FR, HG, HO, IN, IN$, NF,"
26740 DATA"NF$, RV, SD, SP, SR, SR$, SV, VE, VG.","=U kunt"
26750 DATA"het beste deze variabelen alleen voor de"
26760 DATA"subroutines gebruiken. Dit bevordert de"
26770 DATA"leesbaarheid van het programma.",".TTOT SLOT"
26780 DATA"=Dit was het laatste uitlegprogramma over de"
26790 DATA"BASICODE-3 subroutines.","!Ik hoop dat u er wat"
26800 DATA"van opgestoken heeft en deze kennis ook in een"
26810 DATA"BASICODE-programma kunt omzetten.","=Mocht u"
26820 DATA"nog vragen of opmerkingen hebben dan kunt u"
26830 DATA"deze naar het adres sturen dat aan het"
26840 DATA"eind van het programma op het scherm komt."
26850 DATA"=Ook als u geen vragen heeft, maar u vond deze"
26860 DATA"serie zinvol zou ik dat graag van u vernemen."
26870 DATA"=Bij vragen gaarne een postzegel bijsluiten"
26880 DATA"voor beantwoording.","=Bij voldoende"
26890 DATA"belangstelling kunnen we misschien ook nog een"
26900 DATA"programma maken over de in BASICODE toegestane"
26910 DATA"BASIC-opdrachten.",".TBASICODE","==Einde van het"
26920 DATA"zevende deel.",".P14","Dit was:","="
32000 DATA".C**************************"
32010 DATA".C*                        *"
32020 DATA".C*        BASICODE-3      *"
32030 DATA".C*                        *"
32040 DATA".C*    Uitleg programma    *"
32050 DATA".C*                        *"
32060 DATA".C*      RZ-Software       *"
32070 DATA".C*      Braamkamp 40      *"
32080 DATA".C*    7206 HB ZUTPHEN     *"
32090 DATA".C*                        *"
32100 DATA".C**************************",".E"


1000 A=300:GOTO20:REM CALCULATOR
1010 A$="":F$="":E$=""
1020 GOSUB1480:GOTO1150:REM INSTRUKTIE
1030 REM WIS ROUTINE
1040 VE=4:HO=20:GOSUB110:PRINT"               "
1050 RETURN
1060 REM STOP ROUTINE
1070 VE=14:HO=13:GOSUB110:PRINT;SR$
1080 VE=4:HO=19:GOSUB110:PRINT"** TOT  ZIENS **"
1090 SD=40:GOSUB450:GOTO950
1100 REM ** WIS ALLE INVOER **
1110 GOSUB1040:VE=14:HO=13:GOSUB110:PRINT;" "
1120 VE=14:HO=13:GOSUB110:PRINT;SR$
1130 E$="":A$=E$:F$=E$:SR=0:SR$=""
1140 REM ** CIJFER INVOER **
1150 GOSUB210:SR$=IN$:GOSUB330
1160 IF(IN>47)AND(IN<58)THEN GOSUB1260
1170 IFSR$="." THEN GOSUB1260
1180 IF(SR$="+")OR(SR$="-")THEN1330
1190 IF(SR$="/")OR(SR$="*")THEN1330
1200 IF(SR$="W")OR(SR$="=")THEN1330
1210 IFSR$="A" THEN1110
1220 IFSR$="C" THEN GOSUB1040:GOSUB1460
1230 IFSR$="S" THEN1070
1240 GOTO1150
1250 REM VERWERKING INVOER
1260 GOSUB1040
1270 IFLEN(A$)=8 THEN1310
1280 A$=A$+SR$
1290 X=35-LEN(A$)
1300 VE=4:HO=X:GOSUB110:PRINT;A$
1310 RETURN
1320 REM BEWERKING INVOER
1330 IF(F$="+")OR(F$=E$)THENSR=SR+VAL(A$)
1340 IFF$="-" THENSR=SR-VAL(A$)
1350 IFF$="*" THENSR=SR*VAL(A$)
1360 IF(F$="/")AND(VAL(A$)<>0)THENSR=SR/VAL(A$)
1370 F$=SR$:IFF$="=" THEN GOSUB1040
1380 IFF$="W" THENSR=SQR(SR)
1390 REM PRINT UITKOMST
1400 SR$="":GOSUB300:X=(35-LEN(SR$))
1410 VE=4:HO=X:GOSUB110:PRINT;SR$
1420 VE=14:HO=13:GOSUB110:PRINT;F$
1430 A$=E$
1440 GOTO1210
1450 REM WIST LAATSTE INVOER
1460 VE=14:HO=13:GOSUB110:PRINT;SR$
1470 A$=E$:RETURN
1480 GOSUB100:PRINT:PRINTTAB(15);"CALCULATOR"
1490 PRINT:PRINT"Dit programma laat u een calculator op"
1500 PRINT"het scherm zien die de volgende bereke-"
1510 PRINT"ningen kan maken :"
1520 PRINT:PRINT"OPTELLEN         +"
1530 PRINT:PRINT"AFTREKKEN        -"
1540 PRINT:PRINT"DELEN            /"
1550 PRINT:PRINT"VERMENIGVULDIGEN *"
1560 PRINT:PRINT"WORTELTREKKEN    W"
1570 PRINT:PRINT"Welke funtie u gebruikt wordt aangege-"
1580 PRINT"ven naast het woord FUNKTIE :"
1590 PRINT:PRINT"         DRUK EEN TOETS":GOSUB210
1600 GOSUB100:VE=0:REM CALCULATOR
1610 GOSUB1900:PRINT"I--------------------I"
1620 GOSUB1900:PRINT"I":HO=37:GOSUB110:PRINT"I"
1630 GOSUB1900:PRINT"I I----------------I I"
1640 GOSUB1900:PRINT"I I                I I"
1650 GOSUB1900:PRINT"I I----------------I I"
1660 GOSUB1900:PRINT"I":HO=37:GOSUB110:PRINT"I"
1670 GOSUB1900:PRINT"I ---  ---  ---  --- I"
1680 GOSUB1900:PRINT"I I7I  I8I  I9I  I/I I"
1690 GOSUB1900:PRINT"I ---  ---  ---  --- I"
1700 GOSUB1900:PRINT"I":HO=37:GOSUB110:PRINT"I"
1710 GOSUB1900:PRINT"I ---  ---  ---  --- I"
1720 GOSUB1900:PRINT"I I4I  I5I  I6I  I*I I"
1730 GOSUB1900:PRINT"I ---  ---  ---  --- I"
1740 GOSUB1900:PRINT"I":HO=37:GOSUB110:PRINT"I"
1750 GOSUB1900:PRINT"I ---  ---  ---  --- I"
1760 GOSUB1900:PRINT"I I1I  I2I  I3I  I-I I"
1770 GOSUB1900:PRINT"I ---  ---  ---  --- I"
1780 GOSUB1900:PRINT"I":HO=37:GOSUB110:PRINT"I"
1790 GOSUB1900:PRINT"I ---  ---  ---  --- I"
1800 GOSUB1900:PRINT"I I0I  I.I  IWI  I+I I"
1810 GOSUB1900:PRINT"I ---  ---  ---  --- I"
1820 GOSUB1900:PRINT"I":HO=37:GOSUB110:PRINT"I"
1830 GOSUB1900:PRINT"I--------------------I";
1840 VE=3:HO=0:GOSUB110:PRINT"    C WIST"
1850 VE=5:GOSUB110:PRINT" LAATSTE INVOER"
1860 VE=8:GOSUB110:PRINT"    A WIST"
1870 VE=10:GOSUB110:PRINT"  ALLE INVOER"
1880 VE=14:GOSUB110:PRINT"   FUNKTIE :"
1890 VE=18:GOSUB110:PRINT"    S=STOP":RETURN
1900 VE=VE+1:HO=16:GOSUB110:RETURN
32000 REM ************************
32010 REM *                      *
32020 REM *     R.J. V/D ZEE     *
32030 REM *                      *
32040 REM *      BRAAMKAMP 40    *
32050 REM *                      *
32060 REM *   7206 HB ZUTPHEN    *
32070 REM *                      *
32080 REM *   Tel. 05750-21736   *
32090 REM *                      *
32100 REM ****** BASICODE 3 ******
32110 REM

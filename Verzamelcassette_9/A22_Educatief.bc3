
1000 A=1000:GOTO20:REM EDUCATIEF
1010 GOSUB100:GOSUB6010
1020 DIMA(9),B(9),D(9)
1030 GOSUB100
1040 VE=1:HO=12:GOSUB110:SR$="EDUCATIEF":GOSUB150
1050 PRINT:PRINT:PRINT
1060 PRINT:PRINT"In dit programma zit een aantal"
1070 PRINT"educatieve programma's, die kunnen"
1080 PRINT"worden gekozen via een menu."
1090 PRINT"Bij elk programma zit een kleine uitleg."
1100 PRINT:PRINT:PRINT"       DRUK EEN TOETS":GOSUB210
1110 V=0:H=0:F=0:G=0:N=0:P=0:K=0:X=0:B=0:T=0:Y=0:S=0
1120 GOSUB100:VE=1:HO=13:GOSUB110
1130 SR$="HOOFDMENU":GOSUB150
1140 VE=5:HO=5:GOSUB110
1150 PRINT"1. Welk cijfer is anders"
1160 VE=7:GOSUB110:PRINT"2. Welke letter is anders"
1170 VE=9:GOSUB110:PRINT"3. Welk getal ontbreekt"
1180 VE=11:GOSUB110
1190 PRINT"4. Zet de getallen op volgorde"
1200 VE=13:GOSUB110
1210 PRINT"5. Wat is het volgende getal"
1220 VE=15:GOSUB110:PRINT"6. Stoppen"
1230 VE=18:HO=8:GOSUB110
1240 PRINT"Wat wil je gaan doen ? ";:GOSUB210
1250 M=VAL(IN$):IF(M<1)OR(M>6)THEN1230
1260 ONM GOTO2010,2030,3010,4010,5010,6080
1270 REM ** BIJ 100 % GOED **
1280 VE=14:HO=11:GOSUB110:PRINT"*****************"
1290 VE=15:GOSUB110:PRINT"*":HO=27:GOSUB110:PRINT"*"
1300 VE=16:HO=11:GOSUB110:PRINT"* ERG GOED HOOR *"
1310 VE=17:GOSUB110:PRINT"*":HO=27:GOSUB110:PRINT"*"
1320 VE=18:HO=11:GOSUB110:PRINT"*****************"
1330 RETURN
2000 REM ** CIJFERS **
2010 A$="1234567890":S=1:GOTO2050
2020 REM ** LETTERS **
2030 A$="ABCDEFGHIJKLMNOPQRSTUVWXYZ"
2040 REM ** BEGIN PROGRAMMA **
2050 GOSUB2650:GOTO2430
2060 REM ** INITIALISEREN **
2070 V=5:G=0:B=10:T=0:F=0:RETURN
2080 REM ** NIEUWE VRAAG **
2090 GOSUB260:A=INT(RV*10+1):GOSUB260:B=INT(RV*10+1)
2100 IFA=B THEN2090
2110 B$=LEFT$(A$,A):B$=RIGHT$(B$,1):C$=LEFT$(A$,B)
2120 C$=RIGHT$(C$,1):GOSUB260:P=INT(RV*9+1)
2130 T=T+1:RETURN
2140 REM ** SCHERM LAYOUT **
2150 IFS=1 THENSR$="WELK CIJFER IS ANDERS":GOTO2170
2160 SR$="WELKE LETTER IS ANDERS"
2170 VE=1:HO=9:GOSUB110:GOSUB150
2180 VE=3:HO=5:GOSUB110:PRINT"OPGAVE ";T
2190 HO=30:GOSUB110:PRINT"GOED ";G
2200 FORI=0 TO2:H=15:V=V+2
2210 FORJ=1 TO3
2220 H=H+4:D$=B$
2230 IFI*3+J<>P THEN2250
2240 D$=C$
2250 VE=V:HO=H:GOSUB110:PRINTD$
2260 NEXTJ:NEXTI:RETURN
2270 REM ** INVOEREN CIJFER **
2280 IFS=1 THENS$="Welk cijfer is het ? ":GOTO2300
2290 S$="Welke letter is het ? "
2300 VE=17:HO=5:GOSUB110:PRINTS$;:GOSUB210
2310 SR$=IN$:GOSUB330:D$=SR$
2320 IFS=1 THENH$=" cijfer  ":GOTO2340
2330 H$=" letter  "
2340 E$="FOUT het was"
2350 IFD$<>C$ THENF=F+1:GOTO2370
2360 E$="GOED het was":G=G+1
2370 VE=19:GOSUB110:PRINTE$+H$;C$
2380 REM ** SCHERM SCHOON **
2390 SD=20:GOSUB450:FORI=5 TO20:VE=I:HO=0:GOSUB110
2400 PRINT"                                      "
2410 NEXTI:RETURN
2420 REM ** HOOFDROUTINE **
2430 GOSUB2070
2440 FORX=1 TOB
2450 GOSUB2090
2460 GOSUB2150
2470 GOSUB2280
2480 H=15:V=5:NEXTX
2490 REM ** UITSLAG **
2500 GOSUB100
2510 IFS=1 THENSR$="WELK CIJFER IS ANDERS":GOTO2530
2520 SR$="WELKE LETTER IS ANDERS"
2530 VE=1:HO=9:GOSUB110:GOSUB150
2540 VE=5:HO=0:GOSUB110
2550 PRINT"Je hebt 10 opgaven gemaakt en ";F
2560 PRINT"maal een fout antwoordt gegeven."
2570 A=INT((G/T)*1000+.5)/10
2580 PRINT"Je had dus ";A;" procent goed."
2590 IFA=100 THEN GOSUB1280
2600 PRINT:PRINT"       Nog een keertje J/N ";
2610 GOSUB210:IFIN=74 THEN GOSUB100:GOTO2430
2620 IFIN=78 THEN1110
2630 IFIN<>78 THEN2610
2640 REM ** INSTRUCTIE **
2650 IFS=1 THENSR$="WELK CIJFER IS ANDERS":GOTO2670
2660 SR$="WELKE LETTER IS ANDERS"
2670 GOSUB100:VE=1:HO=9:GOSUB110:GOSUB150
2680 IFS=1 THENL$="cijfers":GOTO2700
2690 L$="letters"
2700 PRINT:PRINT:PRINT:PRINT
2710 PRINT"In dit programma is het de bedoeling om"
2720 PRINT"uit de 9 ";L$;" die op het scherm"
2730 IFS=1 THENK$="het cijfer te halen dat":GOTO2750
2740 K$="de letter te halen die"
2750 PRINT"worden getoond ";K$
2760 PRINT"anders is."
2770 R$=LEFT$(L$,6)
2780 PRINT"Als je een ";R$;" goed hebt krijg je"
2790 PRINT"daarvoor een punt."
2800 PRINT"Je moet 10 opgave maken, daarna geeft"
2810 PRINT"de computer de einduitslag."
2820 PRINT:PRINT:PRINT"Veel plezier."
2830 PRINT:PRINT"        DRUK EEN TOETS":GOSUB210
2840 GOSUB100:RETURN
3000 REM ** WELK GETAL ONTBREEKT **
3010 GOSUB3630:T=0
3020 GOSUB100:GOTO3540
3030 REM ** INITIALISEREN **
3040 V=8:H=15:F=0:G=0:N=1:P=1:K=0:RETURN
3050 REM ** VRAAG MAKEN **
3060 GOSUB260:X=INT(RV*(N-1)*10)
3070 FORI=1 TO9:A(I)=I+X:NEXTI
3080 GOSUB260:X=INT(RV*9+1)
3090 A=A(X):A(X)=0
3100 IFT=0 THENRETURN
3110 RETURN
3120 REM ** SCHERM OPBOUW **
3130 VE=2:HO=8:GOSUB110
3140 SR$="WELK GETAL ONTBREEKT ?":GOSUB150
3150 VE=4:HO=5:GOSUB110:PRINT"NIVEAU ";N
3160 HO=30:GOSUB110:PRINT"BEURT ";T+1
3170 FORI=0 TO2
3180 FORJ=1 TO3
3190 Y=I*3+J
3200 IFA(Y)<>0 THEN3230
3210 VE=V:HO=H:GOSUB110:PRINT" ? "
3220 GOTO3240
3230 VE=V:HO=H:GOSUB110:PRINTA(Y)
3240 H=H+4:NEXTJ:V=V+2:H=15:NEXTI:V=8:H=15:RETURN
3250 REM ** INVOER CIJFER **
3260 VE=16:HO=14:GOSUB110:PRINT"* = Stoppen"
3270 VE=18:HO=5:GOSUB110:PRINT"Welk getal is het ? ";
3280 INPUTG$:IFG$="*" THEN3590
3290 R=VAL(G$):T=T+1:P=P-1
3300 B$="fout":IFR<>A THEN3320
3310 B$="GOED":G=G+1:P=P+2
3320 IFP<50 THEN3340
3330 P=50
3340 IFP>0 THEN3360
3350 P=1
3360 N=INT(P/5+1)
3370 REM ** UITKOMST **
3380 VE=20:HO=5:GOSUB110
3390 PRINT"Het antwoord was ";B$
3400 VE=22:HO=5:GOSUB110:PRINT"Het was ";A
3410 K=INT((G/T)*1000+.5)/10
3420 SD=20:GOSUB450:FORI=7 TO22:VE=I:HO=0:GOSUB110
3430 PRINT"                                   "
3440 NEXTI:RETURN
3450 REM ** EINDUITSLAG **
3460 GOSUB100:VE=5:HO=15:GOSUB110
3470 SR$="UITSLAG":GOSUB150
3480 VE=8:HO=12:GOSUB110:PRINT"Bereikt niveau ";N
3490 VE=10:GOSUB110:PRINT"Aantal beurten ";T
3500 VE=12:GOSUB110:PRINT"Perc. goed     ";K
3510 IFK=100 THEN GOSUB1280
3520 RETURN
3530 REM ** HOOFDROUTINE **
3540 GOSUB3040
3550 GOSUB3060
3560 GOSUB3130
3570 GOSUB3260
3580 IFR<>0 THEN3550
3590 GOSUB3460
3600 REM ** EINDE PROGRAMMA **
3610 SD=30:GOSUB450:GOTO1110
3620 REM ** INSTRUCTIE **
3630 GOSUB100
3640 VE=2:HO=8:GOSUB110
3650 SR$="WELK GETAL ONTBREEKT ?":GOSUB150
3660 PRINT:PRINT:PRINT
3670 PRINT"In dit programma is het de bedoeling"
3680 PRINT"om uit de negen getallen die op het"
3690 PRINT"scherm worden getoond het ontbrekende"
3700 PRINT"getal te vinden."
3710 PRINT"Als je dit goed doet worden de getallen"
3720 PRINT"steeds moeilijker."
3730 PRINT:PRINT"Veel plezier."
3740 PRINT:PRINT:PRINT"DRUK EEN TOETS";:GOSUB210:RETURN
4000 REM ** ZET DE GETALLEN OP VOLGORDE **
4010 GOSUB4760
4020 X=3
4030 GOTO4610
4040 GOSUB100:PRINT"Momentje geduld A.U.B."
4050 FORI=1 TO9
4060 GOSUB260:A(I)=INT(RV*20+1)
4070 IFI=1 THEN4140
4080 H=0
4090 FORJ=1 TOI-1
4100 IFA(I)<>A(J)THEN4120
4110 H=1:J=I
4120 NEXTJ
4130 IFH=1 THEN4060
4140 B(I)=A(I)
4150 NEXTI
4160 FORI=9 TO2 STEP-1
4170 FORJ=1 TOI
4180 IFB(I)>B(J)THEN4200
4190 H=B(I):B(I)=B(J):B(J)=H
4200 NEXTJ:NEXTI:B=1:GOSUB100
4210 VE=1:HO=6:GOSUB110
4220 SR$="Zet op de goede volgorde":GOSUB150
4230 VE=3:HO=30:GOSUB110:PRINT"BEURT "
4240 VE=5:HO=4:GOSUB110
4250 PRINT"Klaar met wisselen druk toets *"
4260 FORI=1 TO9:VE=7:HO=X+1:GOSUB110
4270 PRINTI;:X=X+4:NEXTI:RETURN
4280 VE=13:HO=17:GOSUB110:PRINT" "
4290 HO=27:GOSUB110:PRINT" "
4300 VE=3:HO=37:GOSUB110:PRINTB
4310 X=3:FORI=1 TO9:VE=9:HO=X:GOSUB110
4320 SR=A(I):CT=2:CN=0:GOSUB310:PRINTSR$;:X=X+4:NEXTI:RETURN
4330 VE=13:HO=3:GOSUB110:PRINT"Verwissel vak ";
4340 HO=19:GOSUB110:PRINT"met vak ";
4350 GOSUB210:IFIN$="*" THENRETURN
4360 HO=17:GOSUB110:PRINTIN$:X=VAL(IN$)
4370 X=INT(X):IF(X<1)OR(X>9)THEN4360
4380 GOSUB210:HO=27:GOSUB110:PRINTIN$:Y=VAL(IN$)
4390 Y=INT(Y):IF(Y<1)OR(Y>9)THEN4380
4400 B=B+1:H=A(X):A(X)=A(Y):A(Y)=H
4410 SD=10:GOSUB450:RETURN
4420 H=1
4430 FORI=2 TO9
4440 IFA(I-1)<A(I)THEN4460
4450 H=0:I=9
4460 NEXTI
4470 RETURN
4480 GOSUB4280
4490 IFH=1 THEN4560
4500 X=3:FORI=1 TO9
4510 VE=16:HO=X:GOSUB110:PRINTB(I)
4520 X=X+4:NEXTI
4530 VE=18:HO=3:GOSUB110
4540 PRINT"Dit had het antwoord moeten zijn"
4550 IFH<>1 THENRETURN
4560 VE=16:HO=10:GOSUB110
4570 PRINT"Het antwoord is GOED"
4580 VE=18:HO=9:GOSUB110
4590 PRINT"Geraden in ";B;" beurten"
4600 RETURN
4610 GOSUB4040
4620 GOSUB4280
4630 GOSUB4330
4640 IFIN$="*" THEN4660
4650 GOTO4620
4660 GOSUB4420
4670 GOSUB4480
4680 SD=30:GOSUB450:GOSUB100
4690 REM ** NOG EEN KEER **
4700 VE=13:HO=10:GOSUB110
4710 SR$="Nog een keertje J/N":GOSUB150
4720 GOSUB210:IFIN=74 THENX=3:GOTO4610
4730 IFIN=78 THEN1110
4740 IFIN<>78 THEN4720
4750 REM ** INSTRUCTIE **
4760 GOSUB100:VE=1:HO=11:GOSUB110
4770 SR$="ZET OP VOLGORDE":GOSUB150
4780 PRINT:PRINT:PRINT"In dit programma krijg je 9 getallen"
4790 PRINT"in willekeurige volgorde."
4800 PRINT"De bedoeling is nu om de getallen in de"
4810 PRINT"goede volgorde te zetten in zo weinig"
4820 PRINT"mogelijk beurten."
4830 PRINT:PRINT"Veel plezier"
4840 PRINT:PRINT:PRINT"          DRUK EEN TOETS";:GOSUB210
4850 RETURN
5000 REM ** WAT IS HET VOLGENDE GETAL **
5010 GOSUB5840:GOSUB100:A=1
5020 PRINT"Momentje geduld A.U.B.":GOTO5740
5030 REM ** INITIALISEREN **
5040 T=0:G=0:Y=0:P=0:N=1:H=15:V=7:RETURN
5050 REM ** NIVEAU + **
5060 FORI=1 TO9:D(I)=B+R*I:NEXTI:RETURN
5070 REM ** NIVEAU - **
5080 GOSUB5060
5090 FORI=1 TO3:D=D(8-I):D=(8-I)=D(I):D(I)=D:NEXTI
5100 RETURN
5110 REM ** GETALLEN REEKS **
5120 FORJ=1 TO2
5130 GOSUB260:B=INT(RV*X+1):GOSUB260:R=INT(RV*Z+1)
5140 GOSUB260:IF(RV>.5)OR(N=3)THEN5160
5150 R=R*-1
5160 GOSUB5180
5170 NEXTJ:RETURN
5180 S=B
5190 FORI=J TO8-J STEP2:D(I)=S+R:S=D(I):NEXTI:RETURN
5200 FORI=1 TO6:GOSUB260:D(I)=INT(RV*X+1):NEXTI:J=1
5210 GOSUB5180:RETURN
5220 GOSUB260:S=INT(RV*2+1)
5230 ONS GOSUB5060,5080
5240 GOSUB260:Q=INT(RV*3+1):FORI=1 TOQ
5250 GOSUB260:S=INT(RV*6+1)
5260 GOSUB260:D(S)=INT(RV*X):NEXTI:RETURN
5270 REM ** SCHERM OPBOUW **
5280 VE=1:HO=7:GOSUB110:SR$="WAT IS HET VOLGENDE GETAL"
5290 GOSUB150:VE=3:HO=5:GOSUB110:PRINT"NIVEAU ";N
5300 HO=32:GOSUB110:PRINT"BEURT ";T+1
5310 VE=5:HO=18:GOSUB110:PRINT"* = stoppen":I=1
5320 FORZ=0 TO2
5330 FORJ=1 TO3
5340 VE=V:HO=H:GOSUB110:PRINTD(I)
5350 I=I+1:H=H+4:NEXTJ:V=V+2:H=15:NEXTZ
5360 HO=23:GOSUB110:PRINT" ?  "
5370 V=7:H=15:RETURN
5380 REM ** INVOER GETAL **
5390 VE=14:HO=11:GOSUB110:INPUT"Welk getal is het ?";E$
5400 IFE$="*" THEN5610
5410 E=VAL(E$)
5420 A$="FOUT":P=P-1:T=T+1
5430 IFE<>D(9)THEN5450
5440 P=P+2:A$="GOED":G=G+1
5450 VE=16:HO=11:GOSUB110:PRINT"Het antwoord is ";A$
5460 VE=18:HO=13:GOSUB110:PRINT"Het getal was ";D(9)
5470 REM ** VERWERKING ANTWOORD **
5480 N=INT(P/5)+1
5490 IFN<9 THEN5510
5500 N=8
5510 IFN>0 THEN5530
5520 N=1
5530 Y=INT((G/T)*1000)/10
5540 SD=20:GOSUB450:RETURN
5550 REM ** VRAAG MAKEN **
5560 X=N*10:Z=N*5:GOSUB260:B=INT(RV*X+1)
5570 GOSUB260:R=INT(RV*Z+1):GOSUB260:S=INT(RV*N+1)
5580 ONS GOSUB5060,5080,5120,5120,5220
5590 RETURN
5600 REM ** EINDRESULTAAT **
5610 GOSUB100:VE=1:HO=11:GOSUB110
5620 SR$="EINDRESULTAAT":GOSUB150
5630 VE=3:HO=7:GOSUB110:PRINT"Aantal beurten"
5640 HO=34:GOSUB110:PRINTT
5650 VE=5:HO=7:GOSUB110:PRINT"Aantal goede antwoorden"
5660 HO=34:GOSUB110:PRINTG
5670 VE=7:HO=7:GOSUB110:PRINT"Percentage goed"
5680 HO=34:GOSUB110:PRINTY
5690 VE=9:HO=7:GOSUB110:PRINT"Bereikt niveau"
5700 HO=34:GOSUB110:PRINTN
5710 IFY=100 THEN GOSUB1280
5720 SD=30:GOSUB450:GOTO1110
5730 REM ** HOOFDROUTINE **
5740 GOSUB5040
5750 GOSUB5560
5760 FORI=6 TO22:VE=I:HO=0:GOSUB110
5770 PRINT"                                     "
5780 NEXTI
5790 IFA=1 THEN GOSUB100:A=0
5800 GOSUB5280
5810 GOSUB5390
5820 GOTO5750
5830 REM ** INSTRUCTIE **
5840 GOSUB100
5850 VE=1:HO=7:GOSUB110:SR$="WAT IS HET VOLGENDE GETAL"
5860 GOSUB150
5870 PRINT:PRINT:PRINT"In dit programma krijg je 9 getallen"
5880 PRINT"op het scherm in oplopende volgorde."
5890 PRINT"Als laatste getal staat er een"
5900 PRINT"vraagteken en je moet nu raden welk"
5910 PRINT"getal dat is."
5920 PRINT"Hoe meer getallen je goed hebt hoe"
5930 PRINT"moeilijker de getallen worden."
5940 PRINT:PRINT:PRINT"Veel plezier"
5950 PRINT:PRINT"         DRUK EEN TOETS";:GOSUB210
5960 RETURN
6000 REM ** LOGO **
6010 READA,B
6020 IF(A=40)AND(B=40)THEN6060
6030 IF(A=0)AND(B=0)THEN6050
6040 VE=A:HO=B:GOSUB110:PRINT"*":GOTO6010
6050 SD=10:GOSUB450:GOTO6010
6060 SD=40:GOSUB450:RETURN
6070 REM ** EINDE PROGRAMMA **
6080 GOSUB100:VE=8:HO=13:GOSUB110:PRINT"*************"
6090 VE=9:GOSUB110:PRINT"*":HO=25:GOSUB110:PRINT"*"
6100 VE=10:HO=13:GOSUB110:PRINT"* TOT ZIENS *"
6110 VE=11:GOSUB110:PRINT"*":HO=25:GOSUB110:PRINT"*"
6120 VE=12:HO=13:GOSUB110:PRINT"*************"
6130 SD=30:GOSUB450:GOTO950
25000 DATA8,3,8,2,8,1,9,1,10,1,11,1,12,1,12,2,13,1,14,1
25010 DATA14,2,14,3,0,0,8,5,8,6,8,7,9,8,10,8,11,8,12,8
25020 DATA13,8,14,7,14,6,14,5,13,5,12,5,11,5,10,5,9,5
25030 DATA0,0,8,10,9,10,10,10,11,10,12,10,13,10
25040 DATA14,11,14,12,13,13,12,13,11,13,10,13
25050 DATA9,13,8,13,0,0,9,18,8,17,8,16,9,15,10,15,11,15
25060 DATA12,15,13,15,14,16,14,17,13,18,0,0,14,20,13,20
25070 DATA12,20,11,20,10,20,9,20,8,21,8,22,9,23,10,23
25080 DATA11,23,12,23,13,23,14,23,12,21,12,22,0,0,8,25
25090 DATA8,26,8,27,8,28,8,29,9,27,10,27,11,27,12,27
25100 DATA13,27,14,27,0,0,8,31,10,31,11,31,12,31,13,31
25110 DATA14,31,0,0,8,35,8,34,8,33,9,33,10,33,11,33
25120 DATA12,33,12,34,13,33,14,33,14,34,14,35,0,0,8,39
25130 DATA8,38,8,37,9,37,10,37,11,37,12,37,12,38,13,37
25140 DATA14,37,40,40
32000 REM ************************
32010 REM *                      *
32020 REM *     R.J. V/D ZEE     *
32030 REM *                      *
32040 REM *      BRAAMKAMP 40    *
32050 REM *                      *
32060 REM *   7206 HB ZUTPHEN    *
32070 REM *                      *
32080 REM *   Tel. 05750-21736   *
32090 REM *                      *
32100 REM ****** BASICODE 3 ******
32110 REM
32120 REM TROS-RADIO dd 901010

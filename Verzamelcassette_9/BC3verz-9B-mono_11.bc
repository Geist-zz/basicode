
1000 A=700:GOTO20:REM ** BASICODE-3 UITLEG **
1010 HT=HO:VT=VE:S$="":FORI=1 TOHT:S$=S$+" ":NEXTI
1020 SX=0:H=0:T=0:X=0:SX=0:W=0:FR=1:GOSUB280
1030 HL=0:HR=HT:VL=2:VR=VT-2:R$="":READT$
1040 GOSUB1450:HO=(HT-LEN(SR$)-6)/2:VE=0:GOSUB110
1050 GOSUB150:PRINT:PRINT:PRINT:T$="BASICODE"
1060 PRINT"Wilt u de uitleg op "
1070 PRINT:PRINT"   1  Het beeldscherm,"
1080 PRINT"   2  De printer."
1090 PRINT:PRINT"Welk nummer kiest u ";:GOSUB210
1100 SC=IN-48:PRINTSC:IFSC<>2 THENSC=1:GOTO1230
1110 PRINT:PRINT"Hoeveel spaties wilt u aan het"
1120 INPUT"begin van elke regel ";HL
1130 PRINT:PRINT"Hoeveel tekens echte tekst wilt"
1140 INPUT"u op elke regel ";I:HR=HL+I-1
1150 PRINT:PRINT"Hoeveel regels tekst wilt u op"
1160 INPUT"elk blad ";VR:VR=VR-3
1170 PRINT"Gebruikt u (K)ettingpapier of"
1180 PRINT"           (L)osse vellen  (K/L)"
1190 GOSUB210:IFIN=76 THENVL=-1:GOTO1230
1200 IFIN<>75 THEN GOSUB250:GOTO1170
1210 PRINT:PRINT"Hoeveel regels wilt u bij de scheurlijn"
1220 INPUT"laten overslaan ";VL:VR=VR+VL
1230 MM$="":IFHL>0 THENMM$=LEFT$(S$,HL)
1240 GOSUB1390:M$=MM$:AL=VL:IFSC=2 THEN GOSUB1370
1250 IFLEN(R$)>HR-HL THEN GOSUB1920
1260 GOSUB1480:IFR$<>"" THENR$=R$+" "
1270 R$=R$+D$:GOTO1250
1280 IFSC=2 THENSR$=M$+SR$:GOSUB350:GOSUB360:GOTO1300
1290 GOSUB120:PRINTM$;SR$;:VE=VE+1:HO=0:GOSUB110:HL=0
1300 M$=MM$:AL=AL+1:IFAL<VR THENRETURN
1310 IFSC=1 THEN1390
1320 IFVL>=0 THENFORI=1 TOVL:GOSUB360:NEXTI:GOTO1360
1330 PRINT"Leg een nieuw vel in de printer en"
1340 PRINT"druk dan op RETURN/ENTER"
1350 GOSUB210:IFIN<>13 THEN1350
1360 AL=VL
1370 SR$=LEFT$(S$,(HR-HL-LEN(T$))/2)+T$:GOSUB1280
1380 SR$="":GOSUB1280:AL=VL:RETURN
1390 HO=3:VE=VT:GOSUB110
1400 PRINT"<RETURN> = vervolg,<S> = schermdump";
1410 GOSUB210:IFIN=13 THEN1450
1420 IFIN<>83 THEN1410
1430 FORVE=0 TOVT:SR$="":FORHO=0 TOHT:GOSUB220
1440 SR$=SR$+CHR$(IN):NEXTHO:GOSUB350:GOSUB360:NEXTVE
1450 GOSUB100:AL=VL
1460 SR$=T$:PRINTLEFT$(S$,(HT-LEN(SR$))/2-3);:GOSUB150
1470 PRINT:PRINT:RETURN
1480 READD$
1490 IF(D$="*")AND(SC=2)THENSX=1:X=1:GOTO1990
1500 IF(D$="?")AND(SC=2)THENSX=1:X=1:GOSUB2530:GOTO2000
1510 IF(D$="*")OR(D$="?")THEND$="":GOTO1990
1520 IFD$="" THEN1480
1530 IFLEFT$(D$,1)="." THEN1600
1540 IFLEFT$(D$,1)="=" THEN GOSUB1860:GOTO1570
1550 IFLEFT$(D$,1)<>"!" THENRETURN
1560 IFD$="!" THEND$="":GOTO1580
1570 D$=MID$(D$,2)
1580 GOSUB1860:IFAL+3>VR THENJ=3:GOSUB1880
1590 GOTO1520
1600 IFD$<>".E" THEN1650
1610 J=1:GOSUB1860:T$="Tot de volgende keer"
1620 SR$="":GOSUB1280:GOSUB1280:SR$=T$:GOSUB1280:T$=""
1630 SR$="":GOSUB1280:IFAL<>VL THEN1630
1640 GOTO950
1650 IFD$=".Q" THENR$=R$+CHR$(34):GOTO1480
1660 DD$=MID$(D$,2,1):D$=MID$(D$,3)
1670 IFDD$<>"R" THEN1720
1680 IFSC=2 THENRETURN
1690 IFLEN(R$)+6+LEN(D$)>=HR-HL THENSR$=R$:GOSUB1280:R$=""
1700 PRINTM$;R$;:SR$=D$:GOSUB150:M$=""
1710 HL=HL+LEN(R$)+LEN(D$)+6:R$="":GOTO1480
1720 IFDD$<>"M" THEN1750
1730 I=VAL(D$):MM$="":IFI+HL=0 THEN1480
1740 MM$=LEFT$(S$,I+HL):GOTO1480
1750 IFDD$="P" THENJ=VAL(D$):GOSUB1860:GOSUB1880:GOTO1480
1760 IFDD$="T" THENT$=D$:J=10:GOSUB1860:GOSUB1880:GOTO1680
1770 IFDD$="X" THENT$=T$:J=10:GOSUB1860:GOSUB1880
1780 IFDD$="A" THEN GOSUB1860:GOTO1480
1790 IFDD$="Q" THENR$=R$+CHR$(34)+D$:GOTO1480
1800 IFDD$<>"F" THEN1830
1810 J=VAL(D$):GOSUB1860
1820 J=J-1:IFJ>-1 THENSR$="":GOSUB1280:GOTO1820
1830 IFDD$<>"C" THEN1850
1840 SR$=LEFT$(S$,(HR-HL-LEN(D$))/2)+D$:GOSUB1280
1850 GOTO1480
1860 GOSUB1920:SR$=R$:GOSUB1280:R$="":J=J-1
1870 RETURN
1880 IF(SC=1)OR(AL+J>VR)THEN1900
1890 SR$="":GOSUB1280:GOSUB1280:RETURN
1900 IFAL<>VL THENSR$="":GOSUB1280:GOTO1900
1910 RETURN
1920 K=HR-HL+1-LEN(M$)
1930 IFLEN(R$)<=K THENRETURN
1940 IFMID$(R$,K,1)=" " THEN1970
1950 K=K-1:IFK>0 THEN1940
1960 SR$="":GOTO1980
1970 SR$=LEFT$(R$,K-1):R$=MID$(R$,K+1)
1980 GOSUB1280:GOTO1920
1990 READA
2000 IFX=1 THENX=0:GOSUB2550
2010 IFSX=1 THENSX=0:GOTO1520
2020 ONA GOTO2030,2090,2200,2250,2330
2030 V=AL+2:VE=V:HO=5:GOSUB110:INPUT"Woord invoer ";SR$
2040 GOSUB330:V=V+2:VE=V:GOSUB110:PRINTSR$:V=V+2
2050 VE=V:GOSUB110:PRINT"Nog een woord J/N ":GOSUB210
2060 IFIN=74 THEN2030
2070 IFIN<>78 THEN GOSUB250:GOTO2050
2080 AL=V+1:VE=AL:GOSUB110:GOTO1480
2090 V=AL+3
2100 VE=V:HO=3:GOSUB110:PRINT"Heeft u een printer J/N ";
2110 GOSUB210:V=V+2:IFIN=78 THEN2190
2120 VE=V:GOSUB110:PRINT"Staat de printer aan? J/N ";
2130 GOSUB210:IFIN=78 THEN2160
2140 SR$="DIT IS EEN TEST OF DE PRINTER WERKT"
2150 GOSUB350:GOSUB360:GOTO2190
2160 V=V+2:VE=V:GOSUB110:PRINT"Wilt u deze dan aan zetten"
2170 SD=10:GOSUB450:VE=V:GOSUB110
2180 PRINT"                          ":GOTO2130
2190 AL=V+3:VE=AL:GOSUB110:GOTO1480
2200 SD=4:SV=7:I=0:V=2
2210 FORSP=1 TO127:SR=SP:GOSUB300
2220 VE=V:HO=I:GOSUB110:PRINTSR$:GOSUB400
2230 I=I+4:IFI>HT THENV=V+1:I=0
2240 NEXTSP:GOTO1390
2250 SV=7:VE=AL+2:HO=4:GOSUB110
2260 PRINT"De gebruikte toon is nr. "
2270 VE=AL+4:HO=13:GOSUB110:PRINT"De tijdsduur is ":READA$
2280 READSP,SD:IFSP=0 THEN1390
2290 VE=AL+2:HO=29:GOSUB110:PRINTSP
2300 VE=AL+4:HO=29:GOSUB110:PRINTSD
2310 GOSUB400:GOTO2280
2320 AL=10:VE=10:GOSUB110:GOTO1470
2330 PRINT"DRUK EEN TOETS"
2340 SD=50:GOSUB450
2350 IFSD>0 THEN2410
2360 VE=5:HO=6:GOSUB110
2370 PRINT"Na 5 seconden heb je nog"
2380 VE=7:HO=9:GOSUB110
2390 PRINT"steeds niets ingedrukt."
2400 GOTO2460
2410 SR=(50-SD)/10:CN=1:CT=4:GOSUB310
2420 VE=5:HO=6:GOSUB110
2430 PRINT"Het duurde ";SR$;" seconden"
2440 VE=7:HO=9:GOSUB110
2450 PRINT"voor je de ";IN$;" indrukte."
2460 VE=9:HO=6:GOSUB110:PRINT"NOG EEN KEER J/N":GOSUB210
2470 IFIN=78 THEN1380
2480 IFIN<>74 THEN GOSUB250:GOTO2460
2490 VE=5:HO=6:GOSUB110
2500 PRINT"                           "
2510 VE=7:HO=9:GOSUB110
2520 PRINT"                            ":GOTO2340
2530 READA,B:IFB=0 THENRETURN
2540 GOTO2530
2550 D$="=Wordt op het scherm getoond.":RETURN
25000 DATA"BASICODE uitlegprogramma deel 5"
25010 DATA"=In de vorige afleveringen zijn we met de"
25020 DATA"subroutines gekomen tot routine GOSUB 310."
25030 DATA"!We gaan nu verder met de subroutines 330, 350,"
25040 DATA"360, 400 en 450."
25050 DATA".TGOSUB 330","=Deze routine zorgt er voor dat"
25060 DATA"als er bijvoorbeeld bij een INPUT kleine letters"
25070 DATA"worden ingevoerd, deze worden omgezet naar grote"
25080 DATA"letters. Voert u maar eens een woord in kleine"
25090 DATA"letters in en kijkt u dan maar wat er op het"
25100 DATA"scherm gebeurt.","=",".A","*",1,"=Alle tekens"
25110 DATA"met een ASCII-code van 96 t/m 126 worden door"
25120 DATA"deze subroutine veranderd in tekens met een"
25130 DATA"ASCII-code die 32 lager is.",".TGOSUB 350/360"
25140 DATA"=Deze routines zorgen er voor dat wat er"
25150 DATA"in de variabele SR$ staat op de printer wordt"
25160 DATA"weergegeven.","!Het printen van een tekst"
25170 DATA"op de printer is in BASICODE alleen toegestaan"
25180 DATA"via deze routines waarbij de tekst dan in de"
25190 DATA"variabele SR$ moet staan. Alle andere manieren"
25200 DATA"van het aansturen van de printer zijn dus in"
25210 DATA"BASICODE verboden.","!Als u getallen wilt"
25220 DATA"printen moet u deze ook eerst omzetten naar SR$"
25230 DATA"via de subroutines 300 of 310.","!Bij het maken"
25240 DATA"van een programma moet u er altijd aan denken"
25250 DATA"dat niet iedereen een printer heeft. Dus moet"
25260 DATA"er in het programma naar gevraagd worden."
25270 DATA"!Nu een voorbeeld hoe u dit in een programma"
25280 DATA"kunt verwerken.",".X","!Voorbeeld:","=2000 GOSUB"
25290 DATA"100","!2010 VE=2:HO=3:GOSUB 110:PRINT",".QHeeft"
25300 DATA"u een printer J/N ",".Q:GOSUB 210","!2020 IF"
25310 DATA"IN=78 THEN 4000","!2030 VE=4:GOSUB 110:PRINT"
25320 DATA".QStaat de printer aan? J/N",".Q:GOSUB 210"
25330 DATA"!2040 IF IN=78 THEN 3000","!2050 IF IN<>74 THEN"
25340 DATA"2020","!2060 SR$=",".QDIT IS EEN TEST OF DE"
25350 DATA"PRINTER WERKT",".Q","!2070 GOSUB 350:GOSUB 360"
25360 DATA"!3000 VE=6:GOSUB 110:PRINT ",".QWilt u deze dan"
25370 DATA"aan zetten",".Q","!3010 SD=10:GOSUB 450"
25380 DATA"!3020 VE=6:GOSUB 110:PRINT"
25390 DATA".Q                          ",".Q:GOTO 2030"
25400 DATA"!----","!4000 (verder met het programma er wordt"
25410 DATA"niets geprint.)",".X","==Nu het geheel in de"
25420 DATA"praktijk",".A","*",2,"=Nu een uitleg"
25430 DATA"over de getoonde listing.","!In de regels 2000"
25440 DATA"t/m 2050 wordt er gevraagd of de gebruiker een"
25450 DATA"printer heeft en of deze aan staat.","!In regel"
25460 DATA"2060 krijgt de variabele SR$ de inhoud van de"
25470 DATA"afgebeelde tekst.","!Daarna wordt op regel 2070"
25480 DATA"de tekst op de printer afgedrukt.","!U ziet dat"
25490 DATA"we daar twee subroutines gebruiken.","!Ik zal"
25500 DATA"u uitleggen wat deze subroutines precies"
25510 DATA"doen.","=De subroutine GOSUB 350 zorgt er voor"
25520 DATA"dat de tekst in de variabele SR$ op de printer"
25530 DATA"wordt afgedrukt, maar sluit de regel op de"
25540 DATA"printer nog niet af.","!Dus is het mogelijk"
25550 DATA"door deze routine vaker te gebruiken meer op"
25560 DATA"dezelfde regel te printen.","=De subroutine"
25570 DATA"GOSUB 360 sluit de regel op de printer af en"
25580 DATA"begint op een nieuwe printerregel.","!De werking"
25590 DATA"komt dus overeen met de opdracht PRINT naar het"
25600 DATA"beeldscherm.","=Als u het aantal regels per vel"
25610 DATA"wilt bijhouden zult u zelf een tellus in uw"
25620 DATA"programma moeten maken die dat bijhoudt."
25630 DATA"!In regel 3010 zit een subroutine die we"
25640 DATA"verderop nader zullen toelichten.",".TGOSUB 400"
25650 DATA"=Via deze routine kunt u muziek maken op de"
25660 DATA"computer.","De variabelen die hiervoor worden"
25670 DATA"gebruikt zijn de volgende:","=SP (Sound Pitch)"
25680 DATA"   voor de toonhoogte     van 0 t/m 127","!SD"
25690 DATA"(Sound Duration) voor de tijdsduur      van 1"
25700 DATA"t/m 255","!SV (Sound Volume    voor de"
25710 DATA"geluidssterkte van 0 t/m 15","=In combinatie met"
25720 DATA"deze drie variabelen kunnen we dus een toon"
25730 DATA"opgeven die de computer dan laat horen."
25740 DATA"!Het is niet altijd mogelijk alle tonen via SP"
25750 DATA"te laten horen aangezien niet iedere computer"
25760 DATA"daartoe in staat is. Bij de laagste tonen hoort"
25770 DATA"u vaak niet meer dan een brom of tik, terwijl de"
25780 DATA"hoogste tonen voor veel mensen vaak niet meer"
25790 DATA"hoorbaar zijn.","!Om dit uit te"
25800 DATA"proberen geven we het volgende programma op aan"
25810 DATA"de computer.",".X","=2000 SD=4:SV=7","!2010 FOR"
25820 DATA"SP=1 TO 127","!2020 GOSUB 400","!2030 NEXT SP"
25830 DATA"=In regel 2000 zetten we de tijdslengte van SD"
25840 DATA"op 4 en de geluidssterkte van SV op 7, daarna"
25850 DATA"laten we, in de regels 2010 t/m 2030, van SP"
25860 DATA"iedere toon tot aan 127 horen.","!Nu gaan we dat"
25870 DATA"in de praktijk proberen.","!U hoort dus zo"
25880 DATA"de tonen 1 t/m 127 en daarbij laten we de waarde"
25890 DATA"van de toon op het scherm zien.","!Zo kunt u dus"
25900 DATA"horen welke tonen door uw computer goed worden"
25910 DATA"weergegeven en welke waarden deze tonen hebben."
25920 DATA".X","*",3,"=Als u de hoogste toon (nr.127) heeft"
25930 DATA"gehoord dan mankeert er in ieder geval niets aan"
25940 DATA"uw gehoor en kan de computer ook een groot"
25950 DATA"aantal tonen aan.","!Het is natuurlijk niet de"
25960 DATA"bedoeling om alleen maar een aantal tonen achter"
25970 DATA"elkaar te laten horen.","!U kunt ook door"
25980 DATA"een combinatie te maken van een aantal tonen,"
25990 DATA"de computer een liedje laten spelen. Ik zal u in"
26000 DATA"het volgende eenvoudige voorbeeld laten zien hoe"
26010 DATA"u dit in een programma kunt verwerken.",".X"
26020 DATA"!Voorbeeld","=2000 SV=7","!2010 READ SP,SD"
26030 DATA"!2020 IF SP=0 THEN 4000","!2030 GOSUB 400"
26040 DATA"!2040 GOTO 2010","!----","!4000 (verder met het"
26050 DATA"programma)","!----"
26060 DATA"!25000 DATA 72,1,74,1,76,4,79,4,79,4,81,4"
26070 DATA"!25010 DATA 79,4,76,4,72,4,74,4,76,4,76,4"
26080 DATA"!25020 DATA 74,4,72,4,74,6,72,1,74,2,76,4"
26090 DATA"!25030 DATA 79,4,79,4,81,4,79,4,76,4,72,4"
26100 DATA"!25040 DATA 74,4,76,4,76,4,74,4,74,4,72,8"
26110 DATA"!25050 DATA 0,0",".X","=We gaan nu even"
26120 DATA"luisteren hoe dit klinkt.","=",".A","*",4,"?"
26130 DATA72,1,74,1,76,4,79,4,79,4,81,4
26140 DATA79,4,76,4,72,4,74,4,76,4,76,4
26150 DATA74,4,72,4,74,6,72,1,74,2,76,4
26160 DATA79,4,79,4,81,4,79,4,76,4,72,4
26170 DATA74,4,76,4,76,4,74,4,74,4,72,8,0,0,"=Een kleine"
26180 DATA"uitleg over de listing.","=In regel 2000 zetten"
26190 DATA"we de geluidssterkte via de variabele SV op 7."
26200 DATA"!In regel 2010 worden de waarden van de"
26210 DATA"variabelen SP (Sound Pitch) en SD (Sound"
26220 DATA"Duration) uit de DATA gelezen.","Als we bij het"
26230 DATA"DATA lezen de waarde 0 tegenkomen, gaat het"
26240 DATA"programma via regel 2020 verder met regel 4000."
26250 DATA"Op regel 2030 worden dan de tonen via GOSUB 400"
26260 DATA"hoorbaar gemaakt.","=Bij de waarden voor de"
26270 DATA"variabelen gaan we uit van het volgende"
26280 DATA"gemiddelde:"
26290 DATA"=SV =  0 Geen geluid","!SV =  7 Normale sterkte"
26300 DATA"!SV = 15 Grootste volume","=SD  Deze variabele"
26310 DATA"stelt men zelf in.","!Daarbij is SD=10 gelijk"
26320 DATA"aan 1 seconde."
26330 DATA"=SP =   1 : Laagste toon","!SP =  60 : de"
26340 DATA"centrale C","!SP =  69 : de standaard A meestal"
26350 DATA"circa 440 Hz.","!SP = 127 : de hoogste toon,"
26360 DATA"meestal niet hoorbaar",".TGOSUB 450","=Deze"
26370 DATA"routine is te gebruiken voor een wachttijd in"
26380 DATA"een programma.","!In Basic wordt deze routine"
26390 DATA"meestal als volgt in een programma gebruikt:"
26400 DATA"=2000 FOR I=1 to 20000:NEXT I","=Het nadeel"
26410 DATA"hiervan is dat dit in BASICODE niet goed"
26420 DATA"bruikbaar is.","De tijdsduur van 1 tot 20000"
26430 DATA"is b.v. bij de Commodore computer ongeveer 30"
26440 DATA"seconden, maar bij een andere computer kan dit"
26450 DATA"een flits zijn of veel langer duren.","=Daarom"
26460 DATA"gebruiken we in BASICODE de subroutine GOSUB"
26470 DATA"450. De tijdsduur komt dan in de variabele SD"
26480 DATA"te staan. In deze routine wordt gerekend in"
26490 DATA"milliseconden, waarbij SD=10 overeenkomt met 1"
26500 DATA"seconde. Daardoor is de tijd bij vrijwel iedere"
26510 DATA"computer gelijk.","!Tevens is het mogelijk deze"
26520 DATA"routine te onderbreken, zodat men dit kan"
26530 DATA"gebruiken in bijv. een reactiespelletje.","!In"
26540 DATA"het volgende voorbeeld wil ik u laten zien hoe u"
26550 DATA"dit in een programma kunt verwerken.",".X"
26560 DATA"=2000 SD=50:GOSUB 450","!2010 IF SD>0 THEN 2040"
26570 DATA"!2020 PRINT ",".QNa 5 seconden heb je nog steeds"
26580 DATA"niets ingedrukt",".Q","!2030 GOTO 4000","!2040"
26590 DATA"SR=(50-SD)/10:CN=1:CT=4:GOSUB 310","!2050 PRINT"
26600 DATA".QHet duurde ",".Q;SR$;",".Q seconden",".Q"
26610 DATA"!2060 PRINT ",".Qvoor je de ",".Q;IN$;",".Q"
26620 DATA"indrukte.",".Q","!----","!4000 (verder met het"
26630 DATA"programma)",".X","!In regel 2000 van het kleine"
26640 DATA"voorbeeldprogramma wordt de waarde van SD op 50"
26650 DATA"gezet. Dan wacht de computer tot er een toets"
26660 DATA"wordt ingedrukt.","!Komt er een toetsaanslag dan"
26670 DATA"wordt er gesprongen naar regel 2040 en wordt het"
26680 DATA"aantal seconden berekend dat u daar voor nodig"
26690 DATA"had.","!Voor een goede schermpresentatie maken"
26700 DATA"we hier dus gebruik van de eerder besproken"
26710 DATA"subroutine GOSUB 310.","!Nu gaan we uw reactie"
26720 DATA"testen.",".X","*",5,"=Wilt u alleen een tijdlus,"
26730 DATA"gebruik dan de vorige routine GOSUB 400.","!In"
26740 DATA"het programma geeft u dat als volgt op:"
26750 DATA"=2000 SP=0:SD=50:SV=0:GOSUB 400","=Deze regel"
26760 DATA"geeft dus ook een tijdlus van 5 seconden, maar"
26770 DATA"is niet te onderbreken.",".TBASICODE","==Einde"
26780 DATA"van het vijfde deel.",".P14","Dit was:","="
32000 DATA".C**************************"
32010 DATA".C*                        *"
32020 DATA".C*        BASICODE-3      *"
32030 DATA".C*                        *"
32040 DATA".C*    Uitleg programma    *"
32050 DATA".C*                        *"
32060 DATA".C*      RZ-Software       *"
32070 DATA".C*      Braamkamp 40      *"
32080 DATA".C*    7206 HB ZUTPHEN     *"
32090 DATA".C*                        *"
32100 DATA".C**************************",".E"

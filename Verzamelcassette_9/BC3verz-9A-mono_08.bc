
1000 A=200:GOTO20:REM === SCHATTING (breuken en %)===
1010 DIMHH(22),SC(20)
1020 GOSUB1730
1030 VR$=" ":W$=" ":CM$=" "
1040 HC=.41:VC=.85:S1=1/25:C$="<--":CN=0
1050 FORN=1 TO20:HH(N+2)=N:NEXTN
1060 FORN=1 TO19:GOSUB260:X=INT(1+RV*(21-N)+N+1)
1070 HH(N)=HH(X):HH(X)=HH(N+2):NEXTN
1080 HH(20)=HH(22)
1090 REM --- teken de bak ---
1100 GOSUB600:HO=.1:VE=.1:GOSUB620
1110 VE=.9:GOSUB630:HO=.4:GOSUB630:VE=.1:GOSUB630
1120 REM --- teken de cursor ---
1130 HO=HC:VE=VC-.02:SR$=C$:GOSUB650
1140 REM === HOOFDLUS ===
1150 REM --- opdracht ---
1160 FORVR=1 TO20:IFBR=1 THEN1210
1170 REM --- procenten ---
1180 LV=HH(VR)*5:SR=LV:GOSUB300:M$=SR$:CN=1:GOSUB1620
1190 VR$=M$+" procent ?":GOSUB1620:GOTO1230
1200 REM --- breuken ---
1210 RESTORE:FORN=1 TOHH(VR):READM$:READLV:NEXTN
1220 CN=1:GOSUB1620:VR$=M$+" deel ?":GOSUB1620
1230 CN=1:GOSUB1670:REM wis aanwijzing
1240 REM --- pijltoetsen F, G en return ---
1250 M=0:GOSUB210
1260 IF(IN=30)AND(VC<.9)THENM=1:GOSUB1590:VC=VC+S1
1270 IF(IN=31)AND(VC>.1)THENM=-1:GOSUB1590:VC=VC-S1
1280 IFIN=70 THENS1=1/250
1290 IFIN=71 THENS1=1/25
1300 IFIN=13 THEN1330
1310 GOTO1250
1320 REM --- beoordeling ---
1330 VM=.9-(.8*LV/100):GOSUB1650
1340 SC(VR)=125*ABS(VE-VC)
1350 CM$="HEEL GOED !"
1360 IFSC(VR)>1 THENCM$="NIET SLECHT !"
1370 IFSC(VR)>4 THENCM$="MIS"
1380 IFSC(VR)>20 THENCM$="HEEL VER MIS"
1390 SR=INT(125*(.9-VC)):GOSUB300:P$=SR$+" procent"
1400 W$=P$:GOSUB1670:REM plaats aanwijzing
1410 GOSUB210
1420 CN=1:GOSUB1650
1430 NEXTVR
1440 REM ---afwerking ---
1450 CN=1:GOSUB1670
1460 SC=0:FORN=1 TO20:SC=SC+SC(N):NEXTN
1470 HO=.55:VE=.4:SR$="JE WAS ER":GOSUB650
1480 HO=.55:VE=.45:SR$="GEMIDDELD":GOSUB650
1490 FT=INT(SC/20):SR=FT:GOSUB300
1500 HO=.55:VE=.5:SR$=SR$+" % NAAST":GOSUB650
1510 SR$="DAT IS MOOI"
1520 IFFT>5 THENSR$="DAT IS MATIG"
1530 IFFT>10 THENSR$="DAT IS SLECHT"
1540 HO=.55:VE=.75:GOSUB650
1550 HO=.55:VE=.85:SR$="Tik op een toets":GOSUB650
1560 GOSUB210:GOSUB1020
1570 REM
1580 REM --- sbr. verplaats de cursor ---
1590 CN=1:HO=HC:VE=VC-.02:SR$=C$:GOSUB650
1600 CN=0:VE=VE+M*S1:GOSUB650:RETURN
1610 REM --- sbr. print de vraag ---
1620 HO=.55:VE=.15:SR$="Hoe hoog is":GOSUB650
1630 VE=.2:SR$=VR$:GOSUB650:CN=0:RETURN
1640 REM --- sbr. trek de lijn ---
1650 HO=.12:VE=VM:GOSUB620:HO=.38:GOSUB630:CN=0:RETURN
1660 REM --- sbr. print de aanwijzing ---
1670 IFBR=1 THEN1700
1680 HO=.55:VE=.35:SR$="Je wijst":GOSUB650
1690 VE=.4:SR$=W$:GOSUB650
1700 HO=.55:VE=.6:SR$=CM$:GOSUB650
1710 VE=.85:SR$="Tik een toets":GOSUB650:CN=0:RETURN
1720 REM --- sbr. inleiding ---
1730 GOSUB100:PRINT
1740 PRINT"SCHATTEN":PRINT"========":PRINT
1750 PRINT"Schatten is zo iets als meten zonder":PRINT
1760 PRINT"meetlat. Meten met je ogen.":PRINT
1770 PRINT"Met dit programma kun je dat nu zelf":PRINT
1780 PRINT"proberen:":PRINT:PRINT
1790 PRINT"          B = met BREUKEN":PRINT:PRINT
1800 PRINT"          P = met PROCENTEN":PRINT:PRINT
1810 PRINT"          S = STOPPEN":PRINT:PRINT
1820 PRINT"              Tik P of B of S ";
1830 GOSUB210:IFIN=83 THEN950
1840 BR=0:IFIN=66 THENBR=1
1850 GOSUB100:IFBR=1 THEN1910
1860 PRINT:PRINT"PROCENTEN SCHATTEN"
1870 PRINT"==================":PRINT:PRINT
1880 PRINT"In een groot vat wordt water gedaan.":PRINT
1890 PRINT"20 keer zeg ik  voor hoeveel procent":PRINT
1900 GOTO1950
1910 PRINT:PRINT"BREUKEN SCHATTEN"
1920 PRINT"================":PRINT:PRINT
1930 PRINT"In een groot vat wordt water gedaan.":PRINT
1940 PRINT"20 keer zeg ik  voor welk  breukdeel":PRINT
1950 PRINT"het vat gevuld wordt.":PRINT
1960 PRINT"Jij  wijst  dan  met de pijl aan hoe":PRINT
1970 PRINT"hoog het water komt.  Je tikt RETURN":PRINT
1980 PRINT"als ik het water er in mag doen.":PRINT
1990 PRINT"Dan kijken we, hoe goed je de hoogte":PRINT
2000 PRINT"hebt aangewezen!":PRINT
2010 PRINT"        Tik op een toets ";:GOSUB210:GOSUB100
2020 PRINT:PRINT
2030 PRINT"Je  schuift  de  AANWIJS-pijl <-----":PRINT
2040 PRINT"met de pijltoetsen  omhoog en omlaag.":PRINT
2050 PRINT"Met F en G kun je de  beweging  FIJN":PRINT
2060 PRINT"en GROF maken!":PRINT:PRINT
2070 PRINT"Als de aanwijs-pijl goed  staat, dan":PRINT
2080 PRINT"tik je op RETURN":PRINT:PRINT
2090 PRINT"        Tik op een toets ";:GOSUB210:GOSUB100
2100 RETURN
2110 REM----------------------------
25000 DATA"1/3",33,"2/3",67,"1/4",25,"3/4",75
25010 DATA"1/5",20,"2/5",40,"3/5",60,"4/5",80
25020 DATA"1/6",17,"5/6",83,"1/7",14,"2/7",29
25030 DATA"3/7",43,"4/7",57,"5/7",71,"6/7",86
25040 DATA"1/8",13,"3/8",38,"5/8",63,"7/8",88
25050 REM----------------------------
32000 REM Ch.W.Brederode
32010 REM Von Bonninghausenlaan 33
32020 REM 2161 ES  LISSE
32030 REM
32040 REM TROS-RADIO dd 900822

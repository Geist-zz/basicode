
1000 A=700:GOTO20:REM ** BASICODE-3 UITLEG **
1010 HT=HO:VT=VE:S$="":FORI=1 TOHT:S$=S$+" ":NEXTI
1020 H=0:T=0:X=0:SX=0:W=0:FR=1:GOSUB280
1030 HL=0:HR=HT:VL=2:VR=VT-2:R$="":READT$
1040 GOSUB1440:HO=(HT-LEN(SR$)-6)/2:VE=0:GOSUB110
1050 GOSUB150:PRINT:PRINT:PRINT:T$="BASICODE"
1060 PRINT"Wilt u de uitleg op "
1070 PRINT:PRINT"   1  Het beeldscherm,"
1080 PRINT"   2  De printer."
1090 PRINT:PRINT"Welk nummer kiest u ";:GOSUB210
1100 SC=IN-48:PRINTSC:IFSC<>2 THENSC=1:GOTO1220
1110 PRINT:PRINT"Hoeveel spaties wilt u aan het"
1120 INPUT"begin van elke regel ";HL
1130 PRINT:PRINT"Hoeveel tekens echte tekst wilt"
1140 INPUT"u op elke regel ";I:HR=HL+I-1
1150 PRINT:PRINT"Hoeveel regels tekst wilt u op"
1160 INPUT"elk blad ";VR:VR=VR-3
1170 PRINT"Gebruikt u (K)ettingpapier of"
1180 PRINT"           (L)osse vellen  (K/L)"
1190 GOSUB210:IFIN=76 THENVL=-1:GOTO1220
1200 PRINT:PRINT"Hoeveel regels wilt u bij de scheurlijn"
1210 INPUT"laten overslaan ";VL:VR=VR+VL
1220 MM$="":IFHL>0 THENMM$=LEFT$(S$,HL)
1230 GOSUB1380:M$=MM$:AL=VL:IFSC=2 THEN GOSUB1360
1240 IFLEN(R$)>HR-HL THEN GOSUB1900
1250 GOSUB1470:IFR$<>"" THENR$=R$+" "
1260 R$=R$+D$:GOTO1240
1270 IFSC=2 THENSR$=M$+SR$:GOSUB350:GOSUB360:GOTO1290
1280 GOSUB120:PRINTM$;SR$;:VE=VE+1:HO=0:GOSUB110:HL=0
1290 M$=MM$:AL=AL+1:IFAL<VR THENRETURN
1300 IFSC=1 THEN1380
1310 IFVL>=0 THENFORI=1 TOVL:GOSUB360:NEXTI:GOTO1350
1320 PRINT"Leg een nieuw vel in de printer en"
1330 PRINT"druk dan op RETURN/ENTER"
1340 GOSUB210:IFIN<>13 THEN1340
1350 AL=VL
1360 SR$=LEFT$(S$,(HR-HL-LEN(T$))/2)+T$:GOSUB1270
1370 SR$="":GOSUB1270:AL=VL:RETURN
1380 HO=3:VE=VT:GOSUB110
1390 PRINT"<RETURN> = vervolg,<S> = schermdump";
1400 GOSUB210:IFIN=13 THEN1440
1410 IFIN<>83 THEN1400
1420 FORVE=0 TOVT:SR$="":FORHO=0 TOHT:GOSUB220
1430 SR$=SR$+CHR$(IN):NEXTHO:GOSUB350:GOSUB360:NEXTVE
1440 GOSUB100:AL=VL
1450 SR$=T$:PRINTLEFT$(S$,(HT-LEN(SR$))/2-3);:GOSUB150
1460 PRINT:PRINT:RETURN
1470 READD$
1480 IF(D$="*")AND(SC=2)THENSX=1:X=1:GOTO1970
1490 IFD$="*" THEND$="":GOTO1970
1500 IFD$="" THEN1470
1510 IFLEFT$(D$,1)="." THEN1590
1520 IFLEFT$(D$,1)="=" THEN GOSUB1840:GOTO1550
1530 IFLEFT$(D$,1)<>"!" THENRETURN
1540 IFD$="!" THEND$="":GOTO1560
1550 D$=MID$(D$,2)
1560 GOSUB1840:IFAL+3>VR THENJ=3:GOSUB1860
1570 GOTO1500
1580 D$="=Wordt op het scherm getoond.":RETURN
1590 IFD$<>".E" THEN1640
1600 J=1:GOSUB1840:T$="Tot de volgende keer"
1610 SR$="":GOSUB1270:GOSUB1270:SR$=T$:GOSUB1270:T$=""
1620 SR$="":GOSUB1270:IFAL<>VL THEN1620
1630 GOTO950
1640 DD$=MID$(D$,2,1):D$=MID$(D$,3)
1650 IFDD$<>"R" THEN1700
1660 IFSC=2 THENRETURN
1670 IFLEN(R$)+6+LEN(D$)>=HR-HL THENSR$=R$:GOSUB1270:R$=""
1680 PRINTM$;R$;:SR$=D$:GOSUB150:M$=""
1690 HL=HL+LEN(R$)+LEN(D$)+6:R$="":GOTO1470
1700 IFDD$<>"M" THEN1730
1710 I=VAL(D$):MM$="":IFI+HL=0 THEN1470
1720 MM$=LEFT$(S$,I+HL):GOTO1470
1730 IFDD$="P" THENJ=VAL(D$):GOSUB1840:GOSUB1860:GOTO1470
1740 IFDD$="T" THENT$=D$:J=10:GOSUB1840:GOSUB1860:GOTO1660
1750 IFDD$="X" THENT$=T$:J=10:GOSUB1840:GOSUB1860
1760 IFDD$="A" THEN GOSUB1840:GOTO1470
1770 IFDD$="Q" THENR$=R$+CHR$(34)+D$:GOTO1470
1780 IFDD$<>"F" THEN1810
1790 J=VAL(D$):GOSUB1840
1800 J=J-1:IFJ>-1 THENSR$="":GOSUB1270:GOTO1800
1810 IFDD$<>"C" THEN1830
1820 SR$=LEFT$(S$,(HR-HL-LEN(D$))/2)+D$:GOSUB1270
1830 GOTO1470
1840 GOSUB1900:SR$=R$:GOSUB1270:R$="":J=J-1
1850 RETURN
1860 IF(SC=1)OR(AL+J>VR)THEN1880
1870 SR$="":GOSUB1270:GOSUB1270:RETURN
1880 IFAL<>VL THENSR$="":GOSUB1270:GOTO1880
1890 RETURN
1900 K=HR-HL+1-LEN(M$)
1910 IFLEN(R$)<=K THENRETURN
1920 IFMID$(R$,K,1)=" " THEN1950
1930 K=K-1:IFK>0 THEN1920
1940 SR$="":GOTO1960
1950 SR$=LEFT$(R$,K-1):R$=MID$(R$,K+1)
1960 GOSUB1270:GOTO1900
1970 READA
1980 IFX=1 THENX=0:GOSUB1580
1990 IFSX=1 THENSX=0:GOTO1500
2000 ONA GOTO2010,2190,2240,2300,2310
2010 GOSUB100:VE=0
2020 FORI=0 TOHT:HO=I:GOSUB110:PRINT"*";:NEXTI
2030 I=0
2040 VE=I:HO=0:GOSUB110
2050 IFI=VT THENPRINT"*";:GOTO2070
2060 PRINT"*":I=I+1:GOTO2040
2070 VE=5:HO=5:GOSUB110:SR=HT+1:GOSUB300
2080 PRINT"De schermbreedte is ";SR$;" karakters"
2090 SR=VT+1:GOSUB300
2100 VE=7:GOSUB110:PRINT"Het aantal schermregels is ";SR$
2110 VE=9:GOSUB110
2120 PRINT"De waarden voor uw computer zijn"
2130 VE=11:HO=10:GOSUB110:PRINT"HO":HO=18:GOSUB110:PRINT"VE"
2140 HO=26:GOSUB110:PRINT"HG":HO=34:GOSUB110:PRINT"VG"
2150 VE=13:HO=9:GOSUB110:PRINTHT:HO=17:GOSUB110:PRINTVT
2160 HO=25:GOSUB110:PRINTHG:HO=33:GOSUB110:PRINTVG
2170 IFW=1 THENW=0:RETURN
2180 GOTO1380
2190 I=0
2200 IFI=HT THENPRINT"*":GOTO2220
2210 PRINT"*";:I=I+1:GOTO2200
2220 FORI=0 TO20:PRINT"*";:NEXTI
2230 AL=8:VE=8:GOSUB110:GOTO1470
2240 AL=AL+4
2250 VE=AL:HO=6:GOSUB110:INPUT"Tik in GOSUB 100 ";SR$
2260 GOSUB330
2270 IF(SR$="GOSUB100")OR(SR$="GOSUB 100")THEN2290
2280 GOTO2250
2290 GOSUB100:SD=10:GOSUB450:GOTO1440
2300 W=1:GOSUB2010:GOTO1380
2310 B=6
2320 PRINT:PRINT:PRINT
2330 PRINT"Dit staat op dit scherm ";B
2340 PRINT:PRINT:PRINT
2350 PRINT"DRUK EEN TOETS";:GOSUB210
2360 GOSUB100
2370 PRINT:PRINT:PRINT
2380 PRINT"en dit op het volgende scherm ";B
2390 PRINT:PRINT:PRINT
2400 PRINT"DRUK EEN TOETS";:GOSUB210
2410 GOTO1380
25000 DATA"BASICODE uitleg programma deel 1"
25010 DATA"Beste Basicode gebruiker"
25020 DATA"=Nu eens geen programma om iets te kunnen"
25030 DATA"uitrekenen of om een spelletje te spelen,"
25040 DATA"maar in de komende programma's wil ik u gaan"
25050 DATA"uitleggen wat BASICODE kan en hoe de subroutines"
25060 DATA"moeten worden gebruikt als men zelf een"
25070 DATA"BASICODE-programma wil gaan maken.","=BASICODE"
25080 DATA"is een taal die gebruikt wordt om op"
25090 DATA"verschillende"
25100 DATA"computers toch hetzelfde (basicode)-programma te"
25110 DATA"kunnen gebruiken.","=BASICODE is ontstaan in"
25120 DATA"1981 door een aantal actieve hobbyisten die"
25130 DATA"ieder hun eigen computer goed genoeg kenden om"
25140 DATA"daar een vertaalprogramma voor te schrijven."
25150 DATA"=Het vertaalprogramma is dus voor iedere"
25160 DATA"computer anders, maar de funkties die moeten"
25170 DATA"worden uitgevoerd, worden in het"
25180 DATA"BASICODE-programma"
25190 DATA"gegeven door een standaard opdracht.","!Een"
25200 DATA"voorbeeld hiervan is dit programma.","Dit"
25210 DATA"programma is namelijk gemaakt op een PC"
25220 DATA"en u bekijkt het misschien op een heel andere"
25230 DATA"computer en toch kunt u dit programma gebruiken."
25240 DATA"!Dat is mogelijk door de subroutines die in"
25250 DATA"BASICODE worden gebruikt.","!Toen BASICODE"
25260 DATA"werd ontwikkeld werden er over deze subroutines"
25270 DATA"bepaalde afspraken gemaakt.","!Een van de"
25280 DATA"eenvoudigste subroutines om zo even uit te"
25290 DATA"leggen, is de routine GOSUB 100.","=Deze routine"
25300 DATA"maakt het scherm schoon.","=Nu is dat in het"
25310 DATA"BASICODE-programma dus GOSUB 100, maar in"
25320 DATA"werkelijkheid betekent deze opdracht dat er in"
25330 DATA"iedere computer iets anders gebeurt, namelijk"
25340 DATA"dat GOSUB 100 wordt vertaald naar de opdracht"
25350 DATA"die voor de computer waar u mee werkt nodig is."
25360 DATA"=voorbeeld:"
25370 DATA"=GOSUB 100 wordt bij een PC, MSX en BBC vertaald"
25380 DATA"naar CLS.","=GOSUB 100 wordt bij een Commodore"
25390 DATA"vertaald naar PRINT CHR$(147).","=GOSUB 100"
25400 DATA"wordt bij een Apple vertaald naar HOME."
25410 DATA"=Ongetwijfeld"
25420 DATA"zijn er nog meer varianten die hier niet genoemd"
25430 DATA"zijn.","=Dit alles maakt het mogelijk om met"
25440 DATA"BASICODE te werken.","!Wanneer u zelf een"
25450 DATA"programma wilt maken zijn er ook een aantal"
25460 DATA"afspraken waaraan u zich moet houden.","!Zo"
25470 DATA"begint ieder BASICODE-programma niet met regel"
25480 DATA"10, zoals gebruikelijk, maar met regel 1000."
25490 DATA"!Dit is gedaan omdat de regels 10 tot 1000 zijn"
25500 DATA"gereserveerd voor de BASICODE-subroutines."
25510 DATA"!Zonder deze subroutines kan het"
25520 DATA"BASICODE-programma, zoals we nu weten, niet"
25530 DATA"werken.","!De opbouw van een BASICODE-programma"
25540 DATA"ziet er dus als volgt uit:","=10 GOTO 1000"
25550 DATA"!20 voorbereiding van de computer voor BASICODE"
25560 DATA"!25 GOTO 1010","!100 machine-afhankelijke"
25570 DATA"subroutines."
25580 DATA"!--- er is exact vastgelegd wat elke subroutine"
25590 DATA"doet.","!---","!650","!---","!950 terugzetten"
25600 DATA"van de computer in normaal bedrijf","!955 END"
25610 DATA"!1000"
25620 DATA"bepaal stringruimte: GOTO 20","!1010 hier begint"
25630 DATA"het BASICODE-programma","!----","!----","!----"
25640 DATA"BASICODE-programma","!----","!---- laatste"
25650 DATA"opdracht :GOTO 950","=Nu gaan we beginnen met"
25660 DATA"de uitleg van de subroutines."
25670 DATA".TBegin programma","=We beginnen dus elk"
25680 DATA"BASICODE-programma met regel"
25690 DATA"1000. In deze regel moet",".Raltijd","het"
25700 DATA"volgende staan:","=1000 A=<waarde>:GOTO 20"
25710 DATA":REM (naam van het programma)","="
25720 DATA".RA=<waarde>","=Dit bepaalt"
25730 DATA"hoeveel geheugenruimte voor tekst moet worden"
25740 DATA"gereserveerd. Een manier om de waarde van A"
25750 DATA"te bepalen is door het BASICODE-programma te"
25760 DATA"laden (zonder het te runnen) en via GOSUB 270"
25770 DATA"(hierover later meer) de vrije geheugenruimte"
25780 DATA"op te vragen.","!Noteer dit getal en run"
25790 DATA"daarna het programma in het geheel, d.w.z."
25800 DATA"zoveel mogelijk funkties in het programma"
25810 DATA"gebruiken. Na het stoppen van het programma"
25820 DATA"vraagt u weer de geheugenruimte op en het"
25830 DATA"verschil tussen het eerste en het laatste getal"
25840 DATA"is de waarde van A.","!Deze waarde is wel wat"
25850 DATA"aan de ruime kant maar dat is in de meeste"
25860 DATA"gevallen geen bezwaar.","=",".RGOTO 20","=Is"
25870 DATA"geen subroutine aangezien deze opdracht GOTO"
25880 DATA"(ga naar) is en geen GOSUB (ga naar en keer"
25890 DATA"terug). GOTO 20 dient er voor om de benodigde"
25900 DATA"stringruimte te reserveren, aangegeven in de"
25910 DATA"waarde A, voor de computer waarin dat nodig is."
25920 DATA"!Tevens worden de variabelen HO en VE en de"
25930 DATA"variabelen HG en VG voorzien van de maten van"
25940 DATA"het tekst-scherm en grafische-scherm."
25950 DATA"!De variabelen HO(rizontaal en VE(rticaal)"
25960 DATA"geven aan hoeveel tekens er op het tekstscherm"
25970 DATA"kunnen worden getoond.","!Bij de variabele"
25980 DATA"HG(Horizontaal Grafisch) en"
25990 DATA"VG(Verticaal Grafisch) betekent dit het aantal"
26000 DATA"beeldpunten"
26010 DATA"op het grafische scherm.","!De maten van het"
26020 DATA"tekstscherm zijn bij vrijwel elk merk en type"
26030 DATA"computer verschillend.","!Het aantal varieert"
26040 DATA"van 16 tot 32 regels verticaal en 40 tot 80"
26050 DATA"tekens"
26060 DATA"horizontaal per regel.","!Ook bij het grafische"
26070 DATA"scherm lopen de mogelijkheden sterk uiteen, van"
26080 DATA"een grove blokgrafiek van 80 blokjes per regel"
26090 DATA"tot een redelijk hoge resolutie met 300 of meer"
26100 DATA"beeldpunten per lijn.","!Als redelijk minimum"
26110 DATA"mag men uitgaan van een beeldscherm van 24"
26120 DATA"regels met elk 40 tekens en een grafische"
26130 DATA"resolutie van 180 lijnen met elk 250"
26140 DATA"beeldpunten.","!Om nu het een en ander"
26150 DATA"duidelijk te maken laten we u nu de waarden"
26160 DATA"van HO, VE, HG en VG op het"
26170 DATA"scherm zien, waarbij u dan de waarden ziet die"
26180 DATA"voor uw computer geldt.",".X","*",1,"=U zag nu"
26190 DATA"dat de opgave van de waarde van HO 1 minder is"
26200 DATA"dan de grootte van het scherm, 39 of 79 inplaats"
26210 DATA"van 40 of 80 en de waarde van VE waarschijnlijk"
26220 DATA"23 is inplaats van 24. Dit komt omdat de"
26230 DATA"telling begint met 0.","!Verder moet u er voor"
26240 DATA"zorgen dat nooit alle 40 of 80 tekens op het"
26250 DATA"scherm worden gebruikt.","!Dit omdat het op uw"
26260 DATA"scherm misschien goed staat maar op een ander"
26270 DATA"computer-scherm een vreemd effect kan geven."
26280 DATA"!Kijk maar eens naar de volgende regel, die"
26290 DATA"precies 40 of 80 tekens bevat, naar wat er op uw"
26300 DATA"scherm gebeurt.",".X","*",2,"=Als u bij uw"
26310 DATA"scherm geen opvallende dingen ziet is het goed,"
26320 DATA"maar het zou ook kunnen zijn dat u tussen de"
26330 DATA"twee regels een lege regel ziet, dat effect komt"
26340 DATA"nu omdat er precies 40 (80) tekens zijn"
26350 DATA"gebruikt.",".TBASICODE programma","=Na GOTO"
26360 DATA"20 komt het programma terug in regel 1010, hier"
26370 DATA"begint dus het eigenlijke BASICODE-programma."
26380 DATA"=Ik hoop dat alles tot hier toe duidelijk is."
26390 DATA".TGOSUB 100","=Dit is de eerste subroutine die"
26400 DATA"we gaan behandelen.","=De subroutine GOSUB"
26410 DATA"100 doet het volgende:","=Er wordt zo nodig"
26420 DATA"omgeschakeld naar tekstbedrijf en het scherm"
26430 DATA"wordt"
26440 DATA"gewist.","=Alle variabelen blijven onveranderd"
26450 DATA"en de cursor wordt op de positie 0,0 gezet."
26460 DATA"(dit is linksboven op het scherm)","=Dus als"
26470 DATA"u binnen een programma het scherm wilt wissen"
26480 DATA"gebruikt u de opdracht GOSUB 100.","!Probeert"
26490 DATA"u het maar eens, en tik in GOSUB 100","=","*"
26500 DATA3,"=",".RZag u wat er gebeurde?","=Het scherm"
26510 DATA"werd schoon, maar de waarden van HO enz. zijn"
26520 DATA"bewaard gebleven.","!Zullen we gaan kijken of"
26530 DATA"dat zo is? Weet u de waarden nog, hier komen"
26540 DATA"ze nog een keer.",".X","*",4,"=Een klein"
26550 DATA"programmavoorbeeld;","=We geven de variabele"
26560 DATA"B de waarde van 6 en gaan dit verwerken in een"
26570 DATA"klein programmaatje met daarin de subroutine"
26580 DATA"GOSUB 100.","=2000 B=6:GOSUB 100"
26590 DATA"!2010 PRINT:PRINT:PRINT","!2020 PRINT"
26600 DATA".QDit staat op dit scherm ",".Q;B"
26610 DATA"!2030 PRINT:PRINT:PRINT","!2040"
26620 DATA"PRINT",".QDRUK EEN TOETS",".Q;:GOSUB 210"
26630 DATA"!2050 GOSUB 100","!2060 PRINT:PRINT:PRINT"
26640 DATA"!2070 PRINT"
26650 DATA".Qen dit op het volgende scherm ",".Q;B"
26660 DATA"!2080 PRINT:PRINT:PRINT"
26670 DATA"!2090 PRINT",".QDRUK EEN TOETS",".Q;:GOSUB"
26680 DATA"210","=Ik zal even uitleggen wat er in dit"
26690 DATA"programma gebeurt.",".","=In regel 2000 geven"
26700 DATA"we de variabele B de waarde 6 en wissen het"
26710 DATA"scherm.","=In regel 2010 gaan we met de cursor"
26720 DATA"drie posities lager door de PRINT opdracht. Deze"
26730 DATA"opdracht wordt gebruikt om iets op het scherm"
26740 DATA"te printen.","!Als men alleen de opdracht"
26750 DATA"PRINT gebruikt dan gaat de cursor 1 positie"
26760 DATA"lager.","=In regel 2020 wordt er werkelijk iets"
26770 DATA"op het scherm geprint.","!De te printen tekst"
26780 DATA"moet altijd tussen aanhalingstekens staan,"
26790 DATA"anders komt deze tekst niet op het scherm."
26800 DATA"!Variabelen hoeven niet tussen aanhalingstekens"
26810 DATA"te staan, wel moet er tussen de tekst en de"
26820 DATA"variabele een ; staan. (",".Qtekst",".Q;B)","=In"
26830 DATA"regel"
26840 DATA"2030 gebeurt hetzelfde als in regel 2010.","=In"
26850 DATA"regel 2040 wordt er gewacht tot er een toets"
26860 DATA"wordt ingedrukt. Dit gebeurt door gebruik te"
26870 DATA"maken van een andere subroutine n.l. GOSUB 210."
26880 DATA"Deze routine wordt verderop nader uitgelegd."
26890 DATA"=In regel 2050 wordt het scherm weer gewist."
26900 DATA"!U ziet dan op het scherm de tekst uit regel"
26910 DATA"2070 en tevens de waarde van B die nog steeds"
26920 DATA"6 is.","=In de regels 2060 t/m 2090 gebeurt"
26930 DATA"hetzelfde als in de regels 2010 t/m 2040."
26940 DATA"=Nu het geheel in de praktijk.",".X","*",5
26950 DATA"=Met dit kleine programmaatje is bewezen dat"
26960 DATA"de waarde van een variabele (in dit geval B)"
26970 DATA"niet verandert als men de opdracht GOSUB 100"
26980 DATA"geeft.",".TBASICODE","==Einde van het eerste"
26990 DATA"deel.",".P14","Dit was:","="
32000 DATA".C**************************"
32010 DATA".C*                        *"
32020 DATA".C*        BASICODE-3      *"
32030 DATA".C*                        *"
32040 DATA".C*    Uitleg programma    *"
32050 DATA".C*                        *"
32060 DATA".C*      RZ-Software       *"
32070 DATA".C*      Braamkamp 40      *"
32080 DATA".C*    7206 HB ZUTPHEN     *"
32090 DATA".C*                        *"
32100 DATA".C**************************",".E"

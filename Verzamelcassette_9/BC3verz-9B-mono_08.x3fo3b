
1000 A=700:GOTO20:REM ** BASICODE-3 UITLEG **
1010 HT=HO:VT=VE:S$="":FORI=1 TOHT:S$=S$+" ":NEXTI
1020 SX=0:X=0:H=0:V=0:T=0:FR=1:GOSUB280
1030 HL=0:HR=HT:VL=2:VR=VT-2:R$="":READT$
1040 GOSUB1450:HO=(HT-LEN(SR$)-6)/2:VE=0:GOSUB110
1050 GOSUB150:PRINT:PRINT:PRINT:T$="BASICODE"
1060 PRINT"Wilt u de uitleg op "
1070 PRINT:PRINT"   1  Het beeldscherm,"
1080 PRINT"   2  De printer."
1090 PRINT:PRINT"Welk nummer kiest u ";:GOSUB210
1100 SC=IN-48:PRINTSC:IFSC<>2 THENSC=1:GOTO1230
1110 PRINT:PRINT"Hoeveel spaties wilt u aan het"
1120 INPUT"begin van elke regel ";HL
1130 PRINT:PRINT"Hoeveel tekens echte tekst wilt"
1140 INPUT"u op elke regel ";I:HR=HL+I-1
1150 PRINT:PRINT"Hoeveel regels tekst wilt u op"
1160 INPUT"elk blad ";VR:VR=VR-3
1170 PRINT"Gebruikt u (K)ettingpapier of"
1180 PRINT"           (L)osse vellen  (K/L)"
1190 GOSUB210:IFIN=76 THENVL=-1:GOTO1230
1200 IFIN<>75 THEN GOSUB250:GOTO1170
1210 PRINT:PRINT"Hoeveel regels wilt u bij de scheurlijn"
1220 INPUT"laten overslaan ";VL:VR=VR+VL
1230 MM$="":IFHL>0 THENMM$=LEFT$(S$,HL)
1240 GOSUB1390:M$=MM$:AL=VL:IFSC=2 THEN GOSUB1370
1250 IFLEN(R$)>HR-HL THEN GOSUB1920
1260 GOSUB1480:IFR$<>"" THENR$=R$+" "
1270 R$=R$+D$:GOTO1250
1280 IFSC=2 THENSR$=M$+SR$:GOSUB350:GOSUB360:GOTO1300
1290 GOSUB120:PRINTM$;SR$;:VE=VE+1:HO=0:GOSUB110:HL=0
1300 M$=MM$:AL=AL+1:IFAL<VR THENRETURN
1310 IFSC=1 THEN1390
1320 IFVL>=0 THENFORI=1 TOVL:GOSUB360:NEXTI:GOTO1360
1330 PRINT"Leg een nieuw vel in de printer en"
1340 PRINT"druk dan op RETURN/ENTER"
1350 GOSUB210:IFIN<>13 THEN1350
1360 AL=VL
1370 SR$=LEFT$(S$,(HR-HL-LEN(T$))/2)+T$:GOSUB1280
1380 SR$="":GOSUB1280:AL=VL:RETURN
1390 HO=3:VE=VT:GOSUB110
1400 PRINT"<RETURN> = vervolg,<S> = schermdump";
1410 GOSUB210:IFIN=13 THEN1450
1420 IFIN<>83 THEN1410
1430 FORVE=0 TOVT:SR$="":FORHO=0 TOHT:GOSUB220
1440 SR$=SR$+CHR$(IN):NEXTHO:GOSUB350:GOSUB360:NEXTVE
1450 GOSUB100:AL=VL
1460 SR$=T$:PRINTLEFT$(S$,(HT-LEN(SR$))/2-3);:GOSUB150
1470 PRINT:PRINT:RETURN
1480 READD$
1490 IF(D$="*")AND(SC=2)THENSX=1:X=1:GOTO1990
1500 IFD$="*" THEND$="":GOTO1990
1510 IFD$="" THEN1480
1520 IFLEFT$(D$,1)="." THEN1600
1530 IFLEFT$(D$,1)="=" THEN GOSUB1860:GOTO1560
1540 IFLEFT$(D$,1)<>"!" THENRETURN
1550 IFD$="!" THEND$="":GOTO1570
1560 D$=MID$(D$,2)
1570 GOSUB1860:IFAL+3>VR THENJ=3:GOSUB1880
1580 GOTO1510
1590 D$="=Wordt op het scherm getoond.":RETURN
1600 IFD$<>".E" THEN1650
1610 J=1:GOSUB1860:T$="Tot de volgende keer"
1620 SR$="":GOSUB1280:GOSUB1280:SR$=T$:GOSUB1280:T$=""
1630 SR$="":GOSUB1280:IFAL<>VL THEN1630
1640 GOTO950
1650 IFD$=".Q" THENR$=R$+CHR$(34):GOTO1480
1660 DD$=MID$(D$,2,1):D$=MID$(D$,3)
1670 IFDD$<>"R" THEN1720
1680 IFSC=2 THENRETURN
1690 IFLEN(R$)+6+LEN(D$)>=HR-HL THENSR$=R$:GOSUB1280:R$=""
1700 PRINTM$;R$;:SR$=D$:GOSUB150:M$=""
1710 HL=HL+LEN(R$)+LEN(D$)+6:R$="":GOTO1480
1720 IFDD$<>"M" THEN1750
1730 I=VAL(D$):MM$="":IFI+HL=0 THEN1480
1740 MM$=LEFT$(S$,I+HL):GOTO1480
1750 IFDD$="P" THENJ=VAL(D$):GOSUB1860:GOSUB1880:GOTO1480
1760 IFDD$="T" THENT$=D$:J=10:GOSUB1860:GOSUB1880:GOTO1680
1770 IFDD$="X" THENT$=T$:GOSUB1860:GOSUB1880
1780 IFDD$="A" THEN GOSUB1860:GOTO1480
1790 IFDD$="Q" THENR$=R$+CHR$(34)+D$:GOTO1480
1800 IFDD$<>"F" THEN1830
1810 J=VAL(D$):GOSUB1860
1820 J=J-1:IFJ>-1 THENSR$="":GOSUB1280:GOTO1820
1830 IFDD$<>"C" THEN1850
1840 SR$=LEFT$(S$,(HR-HL-LEN(D$))/2)+D$:GOSUB1280
1850 GOTO1480
1860 GOSUB1920:SR$=R$:GOSUB1280:R$="":J=J-1
1870 RETURN
1880 IF(SC=1)OR(AL+J>VR)THEN1900
1890 SR$="":GOSUB1280:GOSUB1280:RETURN
1900 IFAL<>VL THENSR$="":GOSUB1280:GOTO1900
1910 RETURN
1920 K=HR-HL+1-LEN(M$)
1930 IFLEN(R$)<=K THENRETURN
1940 IFMID$(R$,K,1)=" " THEN1970
1950 K=K-1:IFK>0 THEN1940
1960 SR$="":GOTO1980
1970 SR$=LEFT$(R$,K-1):R$=MID$(R$,K+1)
1980 GOSUB1280:GOTO1920
1990 READA
2000 IFX=1 THENX=0:GOSUB1590
2010 IFSX=1 THENSX=0:GOTO1510
2020 ONA GOTO2030,2060,2240,2330,2370,2390,2410,2540
2030 H=HT/2-4
2040 VE=11:HO=H:GOSUB110:PRINT"BASICODE"
2050 GOTO1390
2060 VE=5:HO=5:GOSUB110
2070 INPUT"Welke waarde wilt aan HO geven ";H
2080 IF(H<0)OR(H>HT)THEN GOSUB2200:GOTO2060
2090 VE=7:HO=5:GOSUB110
2100 INPUT"Welke waarde wilt aan VE geven ";V
2110 IF(V<0)OR(V>VT)THEN GOSUB2200:GOTO2090
2120 GOSUB100
2130 VE=V:FORI=0 TOH:HO=I:GOSUB110:PRINT"*";:NEXTI
2140 HO=H:FORI=0 TOV:VE=I:GOSUB110:PRINT"*":NEXTI
2150 HO=H:VE=V:GOSUB110:PRINT"BASICODE"
2160 HO=5:VE=VT:GOSUB110:PRINT"NOG EEN KEER J/N ";:GOSUB210
2170 IFIN=74 THEN GOSUB100:GOTO2060
2180 IFIN<>78 THEN2160
2190 GOTO1450
2200 VE=15:HO=0:GOSUB110
2210 PRINT"DEZE WAARDE IS GROTER DAN UW SCHERM"
2220 SD=10:GOSUB450:VE=15:GOSUB110
2230 PRINT"                                   ":RETURN
2240 VE=8:HO=6:GOSUB110:PRINT"Dit is regel 8"
2250 GOSUB2320
2260 VE=2:GOSUB110:PRINT"en dit regel 2":GOSUB2320
2270 VE=20:GOSUB110:PRINT"en nu regel 20":GOSUB2320
2280 VE=15:HO=0:GOSUB110:PRINT"Nu staan we op regel 15"
2290 GOSUB2320
2300 HO=25:GOSUB110:PRINT"en HO is nu 25":GOSUB2320
2310 GOTO1390
2320 SD=20:GOSUB450:RETURN
2330 VE=11:HO=16:GOSUB110:PRINT"BASICODE";
2340 SD=20:GOSUB450:GOSUB120:HO=1:GOSUB110
2350 PRINT"De cursor staat nu hier":VE=VE+2:HO=25:GOSUB110
2360 SD=20:GOSUB450:PRINT"en nu hier":GOTO1390
2370 VE=11:HO=16:GOSUB110:SR$="BASICODE":GOSUB150
2380 GOTO1390
2390 SR$="BASICODE":VE=5:HO=10:GOSUB110:GOSUB150
2400 GOSUB120:VE=VE+5:GOSUB110:GOSUB150:GOTO1390
2410 VE=3:HO=0:GOSUB110
2420 PRINT"Tikt u maar een willekeurige toets in ";
2430 GOSUB210:IF(IN<32)OR(IN>95)THEN2500
2440 VE=5:GOSUB110
2450 PRINT"De waarde van de ";IN$;" toets is ";IN
2460 VE=7:GOSUB110:PRINT"Nog een toets J/N ";:GOSUB210
2470 IFIN=74 THEN GOSUB100:GOTO2410
2480 IFIN<>78 THEN2460
2490 AL=10:VE=10:GOSUB110:GOTO1480
2500 VE=20:HO=7:GOSUB110
2510 PRINT"DEZE TOETS KAN IN BASICODE"
2520 VE=22:GOSUB110:PRINT"BETER NIET GEBRUIKT WORDEN"
2530 SD=10:GOSUB450:GOSUB100:GOTO2410
2540 PRINT"*= VERDER MET HET PROGRAMMA"
2550 VE=5:HO=10:GOSUB110:PRINT"DRUK EEN TOETS ";
2560 GOSUB210:IFIN$="*" THEN2700
2570 IFIN<12 THEN2560
2580 IF(IN>13)AND(IN<28)THEN2560
2590 IF(IN>31)AND(IN<127)THEN2560
2600 IFIN>127 THEN2560
2610 IFIN=13 THENI$="DE RETURN TOETS     "
2620 IFIN=29 THENI$="CURSOR RECHTS TOETS "
2630 IFIN=28 THENI$="CURSOR LINKS TOETS  "
2640 IFIN=30 THENI$="CURSOR OMLAAG TOETS "
2650 IFIN=31 THENI$="CURSOR OMHOOG TOETS "
2660 IFIN=127 THENI$="WIS/DELETE TOETS    "
2670 VE=7:HO=0:GOSUB110:PRINT"DE WAARDE VAN "
2680 VE=9:GOSUB110:PRINTI$;" IS ";IN
2690 GOTO2560
2700 GOTO1390
25000 DATA"BASICODE uitleg programma deel 2"
25010 DATA"In het vorige deel hebben we het begin van het"
25020 DATA"BASICODE-programma behandeld en de subroutine"
25030 DATA"GOSUB 100. In dit deel gaan we verder met de"
25040 DATA"subroutines GOSUB 110, 120, 150 en 200."
25050 DATA".TGOSUB 110","=Met deze subroutine"
25060 DATA"bepalen we waar we een tekst op het scherm"
25070 DATA"willen hebben.","!In het vorige deel zagen we"
25080 DATA"dat we op het scherm een paar regels omlaag"
25090 DATA"gingen door middel van de PRINT-opdracht.","!Het"
25100 DATA"nadeel van deze opdracht is dat we alleen met"
25110 DATA"de regels omlaag kunnen maar niet terug.","!De"
25120 DATA"tekst die we op het scherm willen printen begint"
25130 DATA"altijd aan de linkerkant van het scherm op"
25140 DATA"positie 0.","!Men kan in BASIC de tekst wel op"
25150 DATA"een andere plaats in de regel zetten door"
25160 DATA"gebruik te maken van de TAB(X) funktie."
25170 DATA"!Deze TABulatie funktie werkt als volgt: door"
25180 DATA"de variabele X een bepaalde waarde te geven"
25190 DATA"kan men de tekst even zo veel"
25200 DATA"spaties van de kant neerzetten,"
25210 DATA"d.w.z. als X = 6 is komt bijv. het woord "
25220 DATA".QBASICODE",".Q 6 spaties van de kant te staan"
25230 DATA"op de regel waar de cursor zich op dat moment"
25240 DATA"bevind.","!Uit het voorgaande blijkt dat dit een"
25250 DATA"niet erg gemakkelijke manier van werken is, waar"
25260 DATA"nog bijkomt dat de TAB-funktie niet bij alle"
25270 DATA"computers precies hetzelfde werkt.","!Om met het"
25280 DATA"geheel makkelijker te kunnen werken gebruiken we"
25290 DATA"in BASICODE de subroutine GOSUB 110.","!In deze"
25300 DATA"subroutine geven we de waarde op van de"
25310 DATA"variabelen HO(rizontale positie) en"
25320 DATA"VE(rticale regel).","!Na de opdracht GOSUB 100"
25330 DATA"staat de cursor op de positie 0,0 (dat is"
25340 DATA"linksboven op het scherm).","!Vanuit deze"
25350 DATA"positie tellend kunnen we de opdracht geven om"
25360 DATA"bijvoorbeeld de tekst ",".QBASICODE",".Q in het"
25370 DATA"midden van het scherm te zetten.","!Laten we"
25380 DATA"eens gaan kijken wat er gebeurt als we de"
25390 DATA"opdracht geven waarbij de waarde van HO=16 en"
25400 DATA"van VE=11 is, uitgaande dat u een scherm heeft"
25410 DATA"met 40 karakters per regel."
25420 DATA"!=2000 GOSUB 100","!2010 VE=11:HO=16:GOSUB 110"
25430 DATA"!2020 PRINT",".QBASICODE",".Q",".X","*",1,"=U"
25440 DATA"zag dat we als waarde voor HO opgaven HO=16,"
25450 DATA"terwijl we net gezegd hebben dat er op een regel"
25460 DATA"40 tekens kunnen staan en het midden dus 20 zou"
25470 DATA"moeten zijn.","!Dit doen we dus omdat de lengte"
25480 DATA"van het woord BASICODE 8 tekens is, dus de helft"
25490 DATA"van 40 tekens min de helft van de woordlengte."
25500 DATA"Zo krijgen we dan het woord precies in het"
25510 DATA"midden.","!Hetzelfde geldt ook"
25520 DATA"voor de waarde van VE: deze is 11 inplaats van"
25530 DATA"12. (van 0 tot 11 is 12)","!Bij een computer met"
25540 DATA"80 tekens per regel stond het woord BASICODE ook"
25550 DATA"in het midden van het scherm aangezien dit"
25560 DATA"programma rekening houdt met uw computer."
25570 DATA"!Probeert u nu zelf eens het woord BASICODE te"
25580 DATA"verplaatsen naar een andere plaats op uw scherm"
25590 DATA"door de waarden van HO en VE te veranderen."
25600 DATA".X","*",2
25610 DATA"=In het volgende programmavoorbeeld wil ik u"
25620 DATA"laten zien hoe men dus ook de posities op het"
25630 DATA"scherm doorelkaar kan opgeven.","=2000 GOSUB"
25640 DATA"100","!2010 VE=8:HO=6:GOSUB 110:PRINT"
25650 DATA".QDit is regel 8",".Q"
25660 DATA"!2020 VE=2:GOSUB 110:PRINT",".Qen dit regel 2"
25670 DATA".Q","!2030 VE=20:GOSUB 110:PRINT",".Qen nu regel"
25680 DATA"20",".Q","!2040 VE=15:HO=0:GOSUB"
25690 DATA"110:PRINT",".QNu staan we op regel 15",".Q"
25700 DATA"!2050 HO=25:GOSUB 110:PRINT",".Qen HO is nu 25"
25710 DATA".Q","=Dit is nu het"
25720 DATA"voordeel van de subroutine GOSUB 110, met de"
25730 DATA"normale opdrachten PRINT en TAB(X) zou dit niet"
25740 DATA"mogelijk zijn geweest.","=N.B.:","=Als u in de"
25750 DATA"eerste regel opgeeft HO=6 en in de volgende"
25760 DATA"regels moet HO ook 6 zijn hoeft u dat alleen"
25770 DATA"maar in de eerste regel"
25780 DATA"op te geven. Ditzelfde geldt ook voor VE (zie"
25790 DATA"voorbeeld).","!Nu het geheel nog even op het"
25800 DATA"scherm.",".X","*",3,"=Ik hoop dat de subroutine"
25810 DATA"GOSUB 110 voor u zo duidelijk is.",".TGOSUB 120"
25820 DATA"=In tegenstelling tot de opdracht GOSUB 110"
25830 DATA"geeft de subroutine GOSUB 120 de schermpositie"
25840 DATA"van de cursor weer.","!Samen met de vorige"
25850 DATA"subroutine kunt u bijvoorbeeld de cursor een of"
25860 DATA"meer regels hoger of lager plaatsen vanuit de"
25870 DATA"laatste positie.","=2000 VE=11:HO=16:GOSUB"
25880 DATA"110:PRINT",".QBASICODE",".Q","!2010 GOSUB 120"
25890 DATA"!2020 HO=1:GOSUB 110:PRINT",".QDe cursor staat"
25900 DATA"nu hier",".Q","!2030 VE=VE+2:HO=25:GOSUB 110"
25910 DATA"!2040 PRINT",".Qen nu hier",".Q"
25920 DATA"=Een kleine uitleg wat hier gebeurt."
25930 DATA"!In regel 2000 zetten we de waarde van"
25940 DATA"VE op 11 en die van HO op 16.","!Daarna kijken"
25950 DATA"we op regel 2010 via GOSUB 120 wat de waarden"
25960 DATA"zijn van VE en HO en zetten we in regel 2020"
25970 DATA"de waarde van HO op 1.","!Nu printen we op"
25980 DATA"schermregel 11 tekst ",".Qde cursor staat nu"
25990 DATA"hier",".Q en daarna gaan verhogen we de"
26000 DATA"schermregel met 2 door in regel 2030 te zetten"
26010 DATA"VE=VE+2."
26020 DATA"Tevens maken we de waarde van HO=25 en printen"
26030 DATA"de tekst ",".Qen nu hier",".Q!We gaan"
26040 DATA"nu het geheel op het scherm tonen.",".X","*",4
26050 DATA"=U zag dus hoe makkelijk deze routine te"
26060 DATA"gebruiken is.",".TGOSUB 150","=Deze subroutine"
26070 DATA"geeft een woord of tekst opvallend weer. Deze"
26080 DATA"tekst moet dan wel in de variabele"
26090 DATA"SR$ staan.","We kunnen binnen een programma"
26100 DATA"een tekst die we veel nodig hebben in een"
26110 DATA"vaste variabele zetten.","!Ook voor deze"
26120 DATA"routine moet de tekst in een variabele staan."
26130 DATA"=Dit gaat als volgt:","=U geeft bijvoorbeeld de"
26140 DATA"variabele SR$ de volgende waarde (tekst)"
26150 DATA"SR$=",".QBASICODE",".Q waarbij dus de inhoud"
26160 DATA"van SR$ de tekst BASICODE is.","!Geven we nu de"
26170 DATA"computer de opdracht PRINT SR$ dan komt de"
26180 DATA"inhoud van SR$ op het scherm te staan.","!De"
26190 DATA"opdracht die in het programma moet staan voor"
26200 DATA"deze subroutine luidt als volgt:"
26210 DATA".X","=2000 VE=11:HO=16:GOSUB 110"
26220 DATA"!2010 SR$=",".QBASICODE",".Q:GOSUB 150"
26230 DATA"=Laten we eens gaan"
26240 DATA"kijken wat het effect op het scherm is.",".X","*"
26250 DATA5,"=Voor en na de tekens van SR$ worden er drie"
26260 DATA"spaties toegevoegd. Zorg er dus voor dat de"
26270 DATA"opvallende tekst niet in elkaar of in een andere"
26280 DATA"tekst overloopt.","!Na afloop van deze"
26290 DATA"subroutine staat de cursor dus op de positie na"
26300 DATA"de laatste van de drie spaties die achter SR$"
26310 DATA"werden toegevoegd.","!Deze routine kan ook in"
26320 DATA"combinatie met de vorige twee subroutines"
26330 DATA"gebruikt worden."
26340 DATA"=Een voorbeeld;","=2000 SR$=",".QBASICODE",".Q"
26350 DATA"!2010 VE=5:HO=10:GOSUB 110:GOSUB 150",&!2020"
26360 DATA"GOSUB 120","!2030 VE=VE+5:GOSUB 110:GOSUB 150"
26370 DATA"=We laten dit even op het scherm zien.",".X","*"
26380 DATA6,"=Let er wel op dat deze subroutine niet teveel"
26390 DATA"wordt gebruikt, want als alles opvallend is,"
26400 DATA"valt alleen het onopvallende op.",".TGOSUB 200"
26410 DATA"=Deze subroutine kijkt of er een toets is"
26420 DATA"ingedrukt.","!Als er een toets is ingedrukt,"
26430 DATA"komt de waarde hiervan in de variabele IN$ en"
26440 DATA"tevens komt de ASCII-waarde in de variabele"
26450 DATA"IN te staan.","!Met de ASCII-waarde"
26460 DATA"bedoelen we een getal dat overeenkomt met de"
26470 DATA"waarde van de ingedrukte toets.","In BASICODE"
26480 DATA"betekent dit een getal tussen de 32 en de 95,"
26490 DATA"ongeacht of er een kleine of grote letter wordt"
26500 DATA"gebruikt. Als er bijvoorbeeld een ",".QA",".Q of"
26510 DATA"een ",".Qa",".Q wordt ingedrukt, wordt in beide"
26520 DATA"gevallen 65 in de variabele IN weergegeven."
26530 DATA"!Drukt u maar"
26540 DATA"eens een aantal toetsen in en kijkt u maar wat"
26550 DATA"de waarde in de variabele IN is:",".X","*",7
26560 DATA"=Nu is het zo dat niet alle waarden gelijk zijn"
26570 DATA"bij iedere computer. Uitzonderingen"
26580 DATA"hierop zijn, in BASICODE, de toetsen;","="
26590 DATA"RETURN, ENTER of NEW LINE. Deze geven altijd"
26600 DATA" 13","!CURSOR RECHTS             deze geeft"
26610 DATA"altijd  29","!CURSOR LINKS              deze"
26620 DATA"waarde is altijd  28","!CURSOR OMLAAG          "
26630 DATA"  deze waarde is altijd  30","!CURSOR OMHOOG   "
26640 DATA"         deze waarde is altijd  31","!WIS/DELETE"
26650 DATA"               deze waarde is altijd 127"
26660 DATA"=Probeert u dit maar eens op uw computer, u zult"
26670 DATA"dan zien dat het klopt.",".X","*",8,"=Na afloop"
26680 DATA"van deze subroutine kan, afhankelijk van het"
26690 DATA"type computer, de waarde van ASC(IN$)"
26700 DATA"(ASC=ASCII-waarde) verschillen van de waarde in"
26710 DATA"IN en eveneens kan de CHR$(IN) (CHR$=geeft een"
26720 DATA"karakter van de waarde IN) verschillen met dat"
26730 DATA"van IN$.","!Dit kan bij alle karakters optreden."
26740 DATA"Testen op IN$ of een opdracht als PRINT IN$"
26750 DATA"wordt in zo'n geval dus dringend afgeraden."
26760 DATA".TBASICODE","==Einde van het tweede deel."
26770 DATA".P14","Dit was:","="
32000 DATA".C**************************"
32010 DATA".C*                        *"
32020 DATA".C*        BASICODE-3      *"
32030 DATA".C*                        *"
32040 DATA".C*    Uitleg programma    *"
32050 DATA".C*                        *"
32060 DATA".C*      RZ-Software       *"
32070 DATA".C*      Braamkamp 40      *"
32080 DATA".C*    7206 HB ZUTPHEN     *"
32090 DATA".C*                        *"
32100 DATA".C**************************",".E"

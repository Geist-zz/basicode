
1000 A=200:GOTO20:REM == BREUKEN1 ==
1010 CN=0:GOSUB100:PRINT:PRINT
1020 PRINT"B R E U K E N":PRINT"=============":PRINT:PRINT
1030 PRINT"Een BREUK is een getal, dat kleiner":PRINT
1040 PRINT"is dan 1":PRINT
1050 PRINT"Ik zal het je laten zien!"
1060 GOSUB3010
1070 GOSUB600
1080 FORN=2 TO7:HO=.1*N:VE=.1:GOSUB5010:NEXTN
1090 HO=.2:VE=.4:SR$="DIT IS EEN REEP CHOCOLADE":GOSUB650
1100 VE=.5:SR$="Die kun je  helemaal zelf":GOSUB650
1110 VE=.6:SR$="op eten.":GOSUB650
1120 VE=.7:SR$="Maar  je kunt hem ook met":GOSUB650
1130 VE=.8:SR$="iemand delen!":GOSUB650
1140 GOSUB4010
1150 FORN=1 TO3:HO=.1*N:VE=.1:GOSUB5010:NEXTN
1160 FORN=5 TO7:HO=.1*N:VE=.1:GOSUB5010:NEXTN
1170 HO=.1:VE=.4:SR$="NU HEB JE TWEE HALVE REPEN":GOSUB650
1180 VE=.6:SR$="In plaats van  EEN HALVE REEP":GOSUB650
1190 VE=.7:SR$="mag je ook schrijven 1/2 REEP":GOSUB650
1200 GOSUB4010
1210 GOSUB100:PRINT:PRINT
1220 PRINT"1/2 is een BREUK":PRINT"================":PRINT
1230 PRINT"Je zegt EEN HALF":PRINT
1240 PRINT"1/2 is EEN gedeeld door TWEE":PRINT
1250 PRINT"1/2 is de HELFT van EEN":PRINT
1260 PRINT"1/2 REEP is de helft van EEN HELE REEP":PRINT
1270 PRINT:PRINT"            1"
1280 PRINT"Je mag ook  -  schrijven, met de"
1290 PRINT"            2":PRINT
1300 PRINT"cijfers onder elkaar, als je dat wilt"
1310 GOSUB3010
1320 PRINT:PRINT
1330 PRINT"Een reep kan ook in DRIE stukken":PRINT
1340 PRINT"Dan moet je telkens 2 blokjes nemen":PRINT
1350 GOSUB3010
1360 GOSUB600
1370 FORN=1 TO2:HO=.1*N:VE=.1:GOSUB5010:NEXTN
1380 FORN=4 TO5:HO=.1*N:VE=.1:GOSUB5010:NEXTN
1390 FORN=7 TO8:HO=.1*N:VE=.1:GOSUB5010:NEXTN
1400 HO=.1:VE=.4
1410 SR$="NU HEB JE DRIE GELIJKE STUKKEN":GOSUB650
1420 VE=.5:SR$="Elk stuk is EEN DERDE reep":GOSUB650
1430 VE=.6:SR$="Je mag ook schrijven 1/3 REEP":GOSUB650
1440 GOSUB4010
1450 GOSUB100:PRINT:PRINT
1460 PRINT"1/3 is een BREUK":PRINT"================":PRINT
1470 PRINT"Je zegt EEN DERDE":PRINT
1480 PRINT"1/3 is EEN gedeeld door DRIE":PRINT
1490 PRINT"1/3 is de EEN DERDE van EEN":PRINT
1500 PRINT"1/3 REEP is EEN DERDE van EEN HELE REEP":PRINT
1510 PRINT:PRINT"            1"
1520 PRINT"Je mag ook  -  schrijven, met de"
1530 PRINT"            3":PRINT
1540 PRINT"cijfers onder elkaar, als je dat wilt"
1550 GOSUB3010
1560 PRINT:PRINT
1570 PRINT"Je kunt ook met zijn zessen delen":PRINT
1580 PRINT"Dan krijgt ieder EEN blokje van de reep"
1590 GOSUB3010
1600 GOSUB600
1610 FORN=0 TO5:HO=.1+.13*N:VE=.1:GOSUB5010:NEXTN
1620 HO=.1:VE=.4
1630 SR$="NU HEB JE ZES GELIJKE STUKKEN":GOSUB650
1640 VE=.5:SR$="Elk stuk is EEN ZESDE reep":GOSUB650
1650 VE=.6:SR$="Je mag ook schrijven 1/6 REEP":GOSUB650
1660 GOSUB4010
1670 GOSUB100:PRINT:PRINT
1680 PRINT"1/6 is een BREUK":PRINT"================":PRINT
1690 PRINT"Je zegt EEN ZESDE":PRINT
1700 PRINT"1/6 is EEN gedeeld door ZES":PRINT
1710 PRINT"1/6 is de EEN ZESDE van EEN":PRINT
1720 PRINT"1/6 REEP is EEN ZESDE van EEN HELE REEP":PRINT
1730 PRINT:PRINT"            1"
1740 PRINT"Je mag ook  -  schrijven, met de"
1750 PRINT"            6":PRINT
1760 PRINT"cijfers onder elkaar, als je dat wilt"
1770 GOSUB3010
1780 GOSUB600
1790 HO=.1:VE=.1:GOSUB5010
1800 FORN=3 TO7:HO=N*.1:GOSUB5010:NEXTN
1810 HO=.1:VE=.4
1820 SR$="IK HEB 1/6 VAN DE REEP AFGEBROKEN":GOSUB650
1830 VE=.5:SR$="De rest van de reep is nog heel":GOSUB650
1840 VE=.6:SR$="Dat is 5 keer 1/6 reep":GOSUB650
1850 VE=.7:SR$="HET GROTE STUK IS  5/6  REEP":GOSUB650
1860 GOSUB4010
1870 HO=.1:VE=.1:GOSUB5010:HO=.23:GOSUB5010
1880 FORN=4 TO7:HO=.06+N*.1:GOSUB5010:NEXTN
1890 HO=.1:VE=.4
1900 SR$="IK HEB 2/6 VAN DE REEP AFGEBROKEN":GOSUB650
1910 VE=.5:SR$="De rest van de reep is nog heel":GOSUB650
1920 VE=.6:SR$="Dat is 4 keer 1/6 reep":GOSUB650
1930 VE=.7:SR$="HET GROTE STUK IS  4/6  REEP":GOSUB650
1940 GOSUB4010
1950 GOSUB100:PRINT:PRINT
1960 PRINT"Het grote stuk was 4 keer 1/6 reep":PRINT
1970 PRINT"Maar we schrijven eigenlijk liever":PRINT
1980 PRINT"niet 4/6 maar 2/3":PRINT:PRINT
1990 PRINT"4/6 is  precies  even veel als 2/3":PRINT
2000 PRINT"We hebben de 4 en de 6 allebei door":PRINT
2010 PRINT"twee gedeeld":PRINT
2020 PRINT"Dat heet VEREENVOUDIGEN"
2030 GOSUB3010:PRINT:T=0:U=0
2040 PRINT"NU WIL IK JE NOG WAT VRAGEN:":PRINT
2050 PRINT"Ik heb een hele reep chocolade":PRINT
2060 PRINT"Ik eet een blokje op.":PRINT
2070 PRINT"Hoeveel is er nog over ";:GOSUB120
2080 GOSUB110:INPUTA$:T=T+1
2090 IFT=3 THEN GOSUB110:PRINT"5/6 !":PRINT:GOTO2120
2100 IFA$<>"5/6" THEN GOSUB110:PRINT"      ":U=U+1:GOTO2080
2110 PRINT:PRINT"GOED GEDAAN!":PRINT:PRINT
2120 PRINT:PRINT
2130 T=0:PRINT"Nu eet ik NOG een stukje op.":PRINT
2140 PRINT"Hoeveel is er nu nog over ";:GOSUB120
2150 GOSUB110:INPUTA$:T=T+1
2160 IFT=3 THEN GOSUB110:PRINT"2/3 !":GOTO2190
2170 IFA$<>"2/3" THEN GOSUB110:PRINT"      ":U=U+1:GOTO2150
2180 PRINT:PRINT"GOED GEDAAN!"
2190 GOSUB3010:PRINT:PRINT:IFU>0 THEN1780
2200 PRINT"DIT IS HET EINDE VAN DEZE LES":PRINT
2210 PRINT"Wil je hem nog een keer zien ?":PRINT
2220 PRINT"Tik J of N ";
2230 GOSUB210
2240 IFIN=74 THEN1000
2250 IFIN=78 THEN950
2260 GOTO2230
3000 REM --- sbr.volgende ---
3010 HO=5:VE=22:GOSUB110:PRINT"Tik op een toets ";
3020 GOSUB210:GOSUB100:RETURN
4000 REM --- sbr.volgende gr ---
4010 HO=.3:VE=.9:SR$="Tik op een toets":GOSUB650
4020 GOSUB210:GOSUB600:RETURN
5000 REM --- sbr.teken blokje ---
5010 GOSUB620:VE=VE+.2:GOSUB630:HO=HO+.1:GOSUB630
5020 VE=VE-.2:GOSUB630:HO=HO-.1:GOSUB630:RETURN
5030 REM----------------------------
32000 REM Ch.W.Brederode
32010 REM Von Bonninghausenlaan 33
32020 REM 2161 ES  LISSE
32030 REM
32040 REM TROS-RADIO dd 900822

1000 A=100:GOTO20:REM BELGIAN NAUGHTY
1010 LL=10:REM LENGTH OF STREAM
1020 KI=4.5:REM POWER OF STREAM
1030 TS=.07:REM SPEED OF STREAM
1040 C$="+":C=ASC(C$):REM STREAMCHR
1050 DIMTH(105+LL),TV(105+LL)
1060 SN=0:SO=0:S=0:GOSUB100
1070 SH=10:HO=1
1080 VE=SH
1090 GOSUB110:GOSUB120:IFVE=SH THENSH=SH+1:GOTO1080
1100 SH=SH-1:SB=15:VE=1
1110 HO=SB
1120 GOSUB110:GOSUB120:IFHO=SB THENSB=SB+1:GOTO1110
1130 SB=SB-1
1140 HO=INT((SB-19)/2):VE=0:GOSUB110
1150 PRINT" ================="
1160 VE=1:GOSUB110:PRINT"!                 !"
1170 VE=2:GOSUB110:PRINT"!   MANNEKE PIS   !"
1180 VE=3:GOSUB110:PRINT"!                 !"
1190 VE=4:GOSUB110:PRINT" ================="
1200 VE=9:GOSUB110
1210 PRINT"DO YOU NEED INSTRUCTIONS?";
1220 GOSUB210
1230 IF(IN$<>"Y") AND(IN$<>"Y") THEN1450
1240 GOSUB100
1250 PRINT:PRINT"This game is based on the famous"
1260 PRINT:PRINT"Brussels tourist attraction."
1270 PRINT:PRINT"You simply have to aim to hit"
1280 PRINT:PRINT"the bucket by filling in values"
1290 PRINT:PRINT"for POWER and ANGLE."
1300 PRINT:PRINT"For example:"
1310 PRINT:PRINT"Fill in the value 60 for POWER"
1320 PRINT"and 60 for the ANGLE."
1330 PRINT"You should now see the aim of the game!"
1340 PRINT
1350 PRINT"You can choose power values"
1360 PRINT"between 0 and 100"
1370 PRINT"and angles between 90 and -90"
1380 PRINT"PRESS ANY KEY TO START"
1390 GOSUB210
1400 REM  EXAMPLE
1410 XP=0:YP=INT(SH/2):REM X,Y OF MAN
1420 XQ=INT(SB/2-2):YQ=5+YP:REM X,Y OF POT
1430 GOSUB1500:SN=0:SO=0
1440 REM  RANDOM FOR MAN AND POT
1450 GOSUB260:XP=0:YP=2+INT((SH-9)*RV)
1460 GOSUB260:YQ=2+INT((SH-8)*RV)
1470 GOSUB260:XQ=3+INT((SB-4)*RV)
1480 GOSUB1500:GOTO1450
1490 REM  *** PLAYSUBROUTINE ***
1500 GOSUB100
1510 GOSUB2090:REM DRAW MAN AND POT
1520 VE=SH-4:HO=0:GOSUB110
1530 PRINT"SCORE IS       ";
1540 VE=SH-4:HO=10:GOSUB110
1550 SR=SN:GOSUB300:PRINTSR$;" %"
1560 PRINT"SCORE WAS      ";
1570 VE=SH-3:HO=10:GOSUB110
1580 SR=SO:GOSUB300:PRINTSR$;" %"
1590 GOSUB200:IFIN$<>"" THEN1590
1600 VE=SH-4:HO=SB-13:GOSUB110:PRINT"POWER  ";:INPUTU
1610 VE=SH-3:HO=SB-13:GOSUB110:PRINT"ANGLE  ";:INPUTW
1620 V=U/KI:K=K+1:H=W*3.141592/180:T2=LL+1
1630 VE=SH-2:FORHO=0 TO25:GOSUB110:PRINT" ";:NEXTHO
1640 REM MOVEMENT OF STREAM
1650 TT=0:FORIT=0 TO100:T=IT*TS
1660 REM ERASE STREAMCHARACTER
1670 VE=TV(T2-LL+1):HO=TH(T2-LL+1)
1680 GOSUB110:PRINT" "
1690 REM COMPUTE NEW POSITION
1700 YS=INT((SIN(H)*V-5*T)*T)
1710 XS=INT(COS(H)*V*T)
1720 HO=XP+XS+1:VE=YP-YS
1730 IFHO>=SB THENHO=SB-1:XS=SB-XP-2
1740 IFVE<0 THENVE=0
1750 GOSUB110:PRINTC$
1760 REM STORE THIS POSITION
1770 TV(T2)=VE:TH(T2)=HO:VE=YP-YS
1780 T2=T2+1:IFT2>=149 THEN1870
1790 REM CHECK IF STREAM IN POT
1800 REM TOO LOW OR BESIDE
1810 IF(HO=XQ) AND(VE=YQ) THEN TT=1:GOTO1870
1820 IF(VE=YQ) AND(HO=XQ-1) THEN1870
1830 IF(VE>YP) AND(VE>YQ) THEN1870
1840 NEXTIT
1850 IF TT=0THEN1990
1860 GOTO1880
1870 IT=100:GOTO1840
1880 VE=SH-2:HO=0:GOSUB110:PRINT"        WELL DONE !!!"
1890 REM REDRAW POT AND
1900 REM  DRAW SPLASHES
1910 GOSUB250:REM BELL!
1920 HO=XQ:VE=YQ:GOSUB110:PRINT"U"
1930 HO=XQ-3:VE=YQ-3:GOSUB110:PRINT"\ \!/ /"
1940 HO=XQ-2:VE=YQ-2:GOSUB110:PRINT"\ ! /"
1950 HO=XQ-1:VE=YQ-1:GOSUB110:PRINT"\!/"
1960 TE=0
1970 FORZ=0 TO1000:NEXTZ
1980 SO=SN:S=S+1:SN=INT(100*S/K+.5):RETURN
1990 VE=SH-2:HO=0:GOSUB110:PRINT"SORRY, YOU MISSED THE POT!"
2000 REM COUNT PIDDLES AND
2010 REM PRINT NUMBER
2020 TE=TE+1:IF(TE>9) AND(TE<17) THENTE=TE+7
2030 FORT3=T2-LL TOT2
2040 VE=TV(T3):HO=TH(T3):GOSUB110:PRINT" "
2050 NEXTT3
2060 HO=XP+XS+1:VE=YP-YS:GOSUB110:PRINTCHR$(TE+48)
2070 SO=INT(SN):SN=INT(100*S/K+.5):GOSUB2090:GOTO1520
2080 REM DRAW MAN
2090 HO=XP:VE=YP-1:GOSUB110:PRINT"O"
2100 VE=YP:GOSUB110:PRINT">/"
2110 VE=YP+1:GOSUB110:PRINT"I"
2120 REM DRAW POT
2130 HO=XQ:VE=YQ:GOSUB110:PRINT"U"
2140 HO=0:VE=SH-5:GOSUB110
2150 FORI=1 TOSB:PRINT"=";:NEXTI:RETURN
30000 REM
30010 REM YOU CAN ADJUST THE LENGTH
30020 REM OF THE STREAM IN LINE 1010
30030 REM (LL=10)
30040 REM THE INFLUENCE OF THE POWER
30050 REM FACTOR CAN BE CHANGED IN
30060 REM LINE 1020. POWER IS
30070 REM CONTROLLED BY KI=4.5
30090 REM
30100 REM THE STREAM SPEED IS ADJUSTED
30110 REM IN LINE 1030
30120 REM YOU NEED (TS=.07)
30130 REM
30140 REM THE CHARACTER IN THE STREAM
30150 REM CAN BE CHANGED IN LINE
30160 REM 1040 :C$="+"
30170 REM
30180 REM THIS PROGRAM WAS RE-WRITTEN
30190 REM IN BASICODE-2 BY MEMBERS OF
30200 REM THE DUTCH BASICODE TEAM. THE
30210 REM ORIGINAL WAS MADE ON A PET
30220 REM AND SUBMITTED IN 1980
30230 REM FOR A HOBBYSCOPE COMPETITION
30240 REM BY JAN COLLY, VEENDAM,
30250 REM THE NETHERLANDS. TRANSLATED
30260 REM INTO ENGLISH FOR "THE CHIP
30270 REM SHOP" BY JONATHAN MARKS,
30280 REM THE NETHERLANDS.
30290 REM NOW IT IS YOUR TURN TO
30330 REM TO WRITE A BASICODE PROGRAM.

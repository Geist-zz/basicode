
1000 A=500:GOTO20:REM WARMTEBEREKENING VAN EEN LOKAAL
1010 GOSUB100:DIMMN$(10)
1015 PRINT:PRINT:PRINT:PRINT
1020 PRINT"BEREKENING VAN DE WARMTEBEHOEFTEN"
1025 PRINT
1030 PRINT"         VAN EEN LOKAAL"
1035 PRINT:PRINT:PRINT
1040 PRINT"WENST U INSTRUCTIES (J/N) ? ";
1050 GOSUB210:PRINT
1060 IFIN$="N" THEN1500
1070 IFIN$<>"J" THEN1040
1080 GOSUB100
1090 PRINT"DIT PROGRAMMA BEREKENT DE WARMTE-"
1095 PRINT"BEHOEFTEN VAN EEN LOKAAL, IN FUNCTIE"
1100 PRINT"VAN :":PRINT
1110 PRINT"        DE KLIMATOLOGISCHE ZONE,"
1120 PRINT"        DE GEWENSTE TEMPERATUUR,"
1130 PRINT"        DE AARD VAN DE BOUW- EN ISO-"
1140 PRINT"          LATIEMATERIALEN,"
1150 PRINT"        DE AANGRENZENDE LOKALEN,"
1160 PRINT"        DE VENTILATIE,"
1170 PRINT"        DE ORIENTATIE,"
1180 PRINT:PRINT
1200 GOSUB9100
1210 GOSUB100
1220 PRINT"VOOR DE BEREKENINGEN MOETEN DE"
1230 PRINT"CONSTRUCTIEVLAKKEN WORDEN OPGE-"
1240 PRINT"SPLITST IN RECHTHOEKEN MET HOMO-"
1250 PRINT"GENE WARMTE-TRANSMISSIE; BVB. EEN"
1260 PRINT"PLAFOND, DEELS ONDER EEN PLAT DAK,"
1270 PRINT"DEELS ONDER EEN SLAAPKAMER, MOET"
1280 PRINT"IN DIE TWEE DELEN WORDEN BEREKEND."
1290 PRINT"ELK VAN DIE DELEN WORDT HIER EEN"
1300 PRINT"'ELEMENT' GENOEMD."
1305 PRINT:PRINT
1310 GOSUB9100
1320 GOSUB100
1500 DIMTM(5,7):FORX=1 TO5:FORY=1 TO7
1505 READTM(X,Y):NEXTY:NEXTX
1510 DIMKW(135):FORX=1 TO135:READKW(X):NEXTX
1520 CT=4:CN=2
1540 GOSUB100
1550 PRINT"ALGEMENE GEGEVENS :":GOSUB9210
2010 PRINT"GEOGR. KLIMAZONE (1 - 5) ";:INPUTKL
2011 PRINT
2015 IF(KL<1) OR(KL>5) THENPRINT"1 TOT 5 A.U.B.":GOTO2010
2020 PRINT"KAMERTEMP. (GRAD. CELC.) ";:INPUTT1
2022 IFT1<40 THEN2025
2023 PRINT"WIJ BEREKENEN WOONVERTREKKEN, GEEN BRAADOVENS!"
2024 GOTO2020
2025 IFT1>TM(KL,1) THEN2030
2026 PRINT"WAAROM WILT U VERWARMEN? IN UW KLIMAZONE"
2027 PRINT"LIGT DE LAAGSTE WINTERTEMPERATUUR ";TM(KL,1)-T1
2028 PRINT"GRADEN HOGER...":GOTO2020
2030 GOSUB100:PRINT"WARMTE VERLIEZEN DOOR TRANSMISSIE :"
2035 GOSUB9210:FORWA=1 TO2
2037 EL=0
2040 EL=EL+1
2050 WA$="PLAFONDELEMENT":IFWA=2 THENWA$="VLOERELEMENT"
2055 SR=EL:GOSUB300:WA$=WA$+" "+SR$
2057 IF(WA=1) AND(EL=1) THENGOSUB5010:GOTO2062
2060 GOSUB5000
2062 WA$="PLAFONDELEMENT":IFWA=2 THENWA$="VLOERELEMENT"
2063 IFQW=-1 THENGOSUB9100:GOSUB100:GOTO2050
2064 PRINT"NOG EEN ";WA$;" (J/N) ? ";:GOSUB210:PRINT
2066 IFIN$="J" THEN2040
2068 IFIN$<>"N" THEN2064
2070 NEXTWA
2080 WA=2
2090 WA=WA+1
2110 SR=WA-2:GOSUB300:WA$="WANDELEMENT "+SR$
2120 GOSUB5000
2125 PRINT:IFQW=-1 THENGOSUB9100:GOTO2090
2130 PRINT"NOG EEN WANDELEMENT (J/N) ? ";
2140 GOSUB210:PRINT
2150 IFIN$="J" THEN2090
2160 IFIN$<>"N" THEN2130
2170 GOSUB100
2180 PRINT"TOTAAL WARMTE VERLIES"
2190 PRINT"DOOR TRANSMISSIE :":GOSUB9210
2195 PRINTTAB(5);INT(QT);" WATT.":GOSUB9210
2200 GOSUB9100
2210 GOSUB9500
2220 PRINT"TOT. VOLUME V/H LOKAAL (M.3) ";:INPUTVO
2222 PRINT
2230 PRINT"IS ER IN HET LOKAAL EEN GECON-"
2235 PRINT"TROLEERD VERLUCHTINGSSYSTEEM (J/N) ? ";
2240 GOSUB210:PRINT
2245 IFIN$="J" THEN2600
2250 IFIN$<>"N" THEN2230
2255 GOSUB9500
2260 PRINT"BESTEMMING VAN HET LOKAAL :"
2265 GOSUB9210
2270 PRINT"1. GERINGE PERSOONSBEZETTING"
2280 PRINT"2. INTENSE PERSOONSBEZETTING"
2290 PRINT"3. HOGE VENTILATIEGRAAD"
2300 PRINT"     ONVERMIJDELIJK OF GEWENST"
2310 K1=1:K2=3:GOSUB9000:IFK=1 THEN2310
2340 ONVAL(IN$) GOTO2350,2370,2440
2350 LV=1
2360 GOTO2460
2370 PRINT:PRINT"MAX. AANTAL PERSONEN ";:INPUTN
2372 PRINT
2375 PRINT"IS ROKEN TOEGELATEN (J/N) ? ";
2380 GOSUB210:PRINT
2390 IFIN$="J" THENP=20:GOTO2410
2395 IFIN$="N" THENP=10:GOTO2410
2400 GOTO2375
2410 LV=P*N/VO:IFLV<1 THENLV=1
2420 GOTO2460
2430 PRINT
2440 PRINT"HOEVEEL LUCHTVERVERSINGEN / UUR ";:INPUTLV
2450 GOSUB9500:GOTO2510
2460 GOSUB100:PRINT"AANTAL LUCHTVERVERSINGEN :"
2465 GOSUB9210
2470 PRINTTAB(5);INT(LV*100)/100;" VOL./UUR.":GOSUB9210
2480 TE=TM(KL,1)
2490 QV=.36*LV*VO*(T1-TE)
2500 GOSUB9510
2510 PRINTTAB(5);INT(QV);" WATT.":GOSUB9210
2530 GOTO2800
2600 GOSUB9500
2610 PRINT"DEBIET VENTILATIE (M3/H) ";:INPUTDB
2625 PRINT
2630 PRINT"TEMPERATUUR INGEBLAZEN"
2635 PRINTTAB(5);"LUCHT (GRAD. CELC.) ";:INPUTTA
2640 GOSUB9500
2650 QV=.36*DB*(T1-TA)
2670 PRINT" - GECONTROLEERD   : ";INT(QV);" WATT."
2680 QQ=.36*.3*VO*(T1-TM(KL,1))
2690 PRINT" - INFILTRATIES    : ";INT(QQ);" WATT."
2700 QV=QV+QQ
2705 GOSUB9210
2710 PRINT" - TOTAAL          : ";INT(QV);" WATT."
2720 GOSUB9210
2800 Q=QV+QT
2810 GOSUB9100
2820 GOSUB100
2825 PRINT"BRUTO TOTALE VERLIEZEN :":GOSUB9210
2830 PRINT" - DOOR TRANSMISSIE  : ";INT(QT);" WATT."
2840 PRINT" - DOOR VENTILATIE   : ";INT(QV);" WATT."
2850 GOSUB9210
2860 PRINT" - TOTAAL            : ";INT(Q);" WATT."
2865 GOSUB9210
2870 PRINT"EEN TOETS VOOR VERDER ? ";:GOSUB210:GOSUB100
2880 PRINT"ORIENTATIE FACTOR":GOSUB9210
2890 PRINT"ORIENTATIE VAN DE KOUDSTE WAND ?"
2895 PRINT
2900 PRINT"1. NOORD   2. NOORDOOST"
2902 PRINT"3. OOST    4. ZUIDOOST"
2904 PRINT"5. ZUID    6. ZUIDWEST"
2906 PRINT"7. WEST    8. NOORDWEST"
2910 K1=1:K2=8:GOSUB9000:IFK=1 THEN2910
2950 ONVAL(IN$) GOTO2955,2955,2960,2965,2965,2965,2960,2955
2955 ZH=.05:GOTO2970
2960 ZH=0:GOTO2970
2965 ZH=-.05
2970 GOSUB100:PRINT"BRUTO VERLIEZEN    : ";INT(Q);" WATT."
2975 GOSUB9210:PRINT"ORIENTATIE TOESLAG : ";
2980 IFZH=.05 THENPRINT"+ 5 %"
2990 IFZH=-.05 THENPRINT"- 5 %"
3000 IFZH=0 THENPRINT"TE VERWAARLOZEN."
3020 GOSUB9210:PRINT"NETTO VERLIEZEN    : ";
3030 Q=INT(Q*(1+ZH)):PRINTQ;"  WATT."
3040 GOSUB9210
3050 GOSUB9100
3060 GOSUB100
3070 PRINT"DIT LOKAAL HEEFT,IN KLIMAZONE ";KL
3090 PRINT"EN BIJ ";T1;" GRADEN KAMERTEMPERATUUR"
3100 PRINT"EEN NETTO WARMTE BEHOEFTE VAN :"
3110 GOSUB9210:PRINTQ;" WATT.":GOSUB9210
3120 PRINT"DIT KAN WORDEN BEREIKT MET EEN"
3125 PRINT"VERWARMINGSTOESTEL VAN HET :"
3130 PRINT"RECHTSTREEKS TYPE  :";
3140 PRINTTAB(25);Q;" WATT."
3150 PRINT"ACCUMULATIETYPE :"
3160 PRINTTAB(4);"OPLADING 8 UUR :";
3165 PRINTTAB(25);INT(Q*2);" WATT."
3170 PRINTTAB(13);"9 UUR :";
3175 PRINTTAB(25);INT(Q*1.8);" WATT."
3180 PRINTTAB(12);"11 UUR :";
3185 PRINTTAB(25);INT(Q*1.55);" WATT."
3190 END
4000 PRINT"IS DE MINIMIMUMTEMPERATUUR"
4010 PRINT"AAN DE ANDERE ZIJDE GEKEND ? ";
4020 GOSUB210:PRINT:IFIN$="J" THEN4200
4030 IFIN$<>"N" THEN4000
4035 PRINT
4040 PRINT"WAT IS DAN AAN DIE ANDERE ZIJDE ?"
4050 PRINT"1. BUITENLUCHT"
4055 PRINT"2. KELDER, VOLLE GROND, GARAGE"
4060 PRINT"3. ZOLDER, ONGEISOLEERD"
4065 PRINT"4. ZOLDER, GEISOLEERD"
4070 PRINT"5. TRAPPENHUIS, BERGPLAATS"
4075 PRINT"6. SLAAP/BADKAMER E.D."
4080 PRINT"7. BEWOOND BUURHUIS"
4090 K1=1:K2=7:GOSUB9000:IFK=1 THEN4090
4120 TE=TM(KL,VAL(IN$))
4130 RETURN
4200 PRINT
4210 PRINT"TEMPERATUUR (GRAD. CELC.) ";:INPUTTE
4220 RETURN
5000 GOSUB100
5010 QW=0
5020 GOSUB9200
5030 GOSUB4000
5040 IFABS(T1-TE)>1 THEN5070
5050 GOSUB100:GOSUB9200
5055 PRINT"  TE VERWAARLOZEN.":GOSUB9210
5060 RETURN
5070 GOSUB100:GOSUB9200
5075 PRINT"TEMP. ANDERE ZIJDE = ";TE;" GRAD. CELC."
5080 GOSUB9210
5090 GOSUB9300
5110 PRINTTAB(25);:INPUTKW
5120 IFKW=0 THENGOSUB10000:GOSUB9340:GOTO5110
5130 WW=LE*BR
5140 V=0
5150 IFWA<>1 THEN5170
5160 V1$="LICHTKOEPEL":V2$="":V3$="S"
5165 GOTO5190
5170 IFWA=2 THEN5350
5180 V1$="VENSTER":V2$="":V3$="S"
5190 PRINT
5195 PRINT"ZIJN ER ";V2$;V1$;V3$;" (J/N) ? ";
5200 GOSUB210:PRINT
5205 QV=0
5210 IFIN$="N" THEN5310
5220 IFIN$<>"J" THEN5195
5230 V=V+1:GOSUB100:GOSUB9200
5235 GOSUB9400
5240 GOSUB9300
5245 PRINTTAB(25);:INPUTKV
5247 IFKV=0 THENGOSUB12000:GOSUB9340:GOTO5245
5250 VV=LE*BR:WW=WW-VV:QO=VV*KV*(T1-TE)
5252 IFWW>0 THEN5257
5253 PRINT"ONMOGELIJK!":PRINT"HERBEGIN VOLLEDIG ";WA$;"!"
5255 IFWA>2 THENWA=WA-1
5256 QW=-1:RETURN
5257 QW=QW+QO
5260 GOSUB100:GOSUB9200
5265 GOSUB9400
5270 PRINTTAB(5);INT(QO);" WATT."
5275 GOSUB9210
5290 V2$="NOG "
5300 GOTO5190
5310 IFWA=1 THEN5350
5320 IFV1$="DEUR" THEN5350
5330 V1$="DEUR":V2$="":V3$="EN"
5340 V=0:GOTO5190
5350 GOSUB100:QW=QW+(WW*KW*(T1-TE))
5360 GOSUB9200
5370 PRINTTAB(5);INT(QW);" WATT."
5375 GOSUB9210
5380 QT=QT+QW
5390 RETURN
8000 PRINT"ISOLATIE POLYSTYREEN, ROTSWOL":RETURN
8010 PRINT"ISOLATIE HOUTBETON":RETURN
8020 GOSUB100:PRINTMN$:ONVAL(IN$) GOSUB8000,8010
8021 GOSUB8090:PRINT"K-WAARDEN IN W/M2.K"
8022 PRINT"ISOL.       DIKTE  METSELWERK":RETURN
8025 RETURN
8030 PRINT"(CM)     9       18      27      36"
8035 GOSUB8090:RETURN
8040 PRINT"(CM)       18        27        36"
8045 GOSUB8090:RETURN
8050 PRINT"(CM)   5     10    15    20    25"
8055 GOSUB8090:RETURN
8090 FORZ=1 TO39:PRINT"-";:NEXT:PRINT:RETURN
8100 PRINT"ISOLATIE IN :"
8110 PRINTTAB(5);"1. POLYSTYREEN, ROTSWOL"
8120 PRINTTAB(5);"2. HOUTBETON"
8130 K1=1:K2=2:GOSUB9000:IFK=1 THEN8130
8140 RETURN
8200 PRINT"(K - WAARDEN IN W/M2.K)":RETURN
8500 GOSUB120
8510 FORY=1 TORY
8520 IFY=1 THENY=2:PRINT"GEEN";:GOTO8540
8530 HO=2:SR=Y:GOSUB110:GOSUB300:PRINTSR$;
8535 HO=0
8540 FORX=1 TOKO
8550 HO=HO+INT(40/(KO+1)):GOSUB110
8560 SR=1.16*KW(I):GOSUB310:PRINTSR$:I=I+1
8570 NEXTX
8580 VE=VE+1:HO=0:GOSUB110
8590 NEXTY
8600 GOSUB8090:RETURN
9000 PRINT"GEEF OPTIE (";K1;"-";K2;") ? ";
9010 GOSUB210:PRINT
9020 IF(VAL(IN$)<K1) OR(VAL(IN$)>K2) THENK=1:RETURN
9030 K=0
9040 RETURN
9100 PRINT"EEN TOETS VOOR VERDER ? ";:GOSUB210
9110 RETURN
9200 PRINT"WARMTE VERLIEZEN ";WA$;" :"
9210 FORZZ=1 TO20:PRINT"-";:NEXTZZ:PRINT
9220 RETURN
9300 PRINT"LENGTE (METER)";
9310 PRINTTAB(25);:INPUTLE:PRINT
9315 BR$="HOOGTE":IFWA<3 THENBR$="BREEDTE"
9320 PRINTBR$;" (METER)";
9330 PRINTTAB(25);:INPUTBR:PRINT
9340 PRINT"K - FACTOR (0 = LIJST)";
9350 RETURN
9400 PRINT" - ";V1$;" ";V;" :":GOSUB9210
9410 RETURN
9500 GOSUB100
9510 PRINT"WARMTE VERLIEZEN DOOR VENTILATIE :"
9520 GOSUB9210
9530 RETURN
10000 IFWA>2 THEN10100
10010 GOTO11000
10020 RETURN
10100 GOSUB100
10110 PRINT"MUREN IN METSELWERK OF BETON.":GOSUB9210
10120 MN$(1)="1. GEVEL IN 1 + 1/2 STEEN"
10125 MN$(2)="2. BINNENMUUR VOL METSELWERK"
10130 MN$(3)="3. BUITENMUUR VOL METSELWERK"
10135 MN$(4)="4. BUITENMUUR BETON"
10140 FORX=1 TO4
10150 PRINTMN$(X)
10160 NEXTX:PRINT
10170 K1=1:K2=4:GOSUB9000
10180 IFK=1 THEN10170
10190 GOSUB100:MN$=MN$(VAL(IN$)):PRINTMN$
10200 ONVAL(IN$) GOTO10300,10400,10500,10600
10300 PRINT"BUITENMUUR 1/2 BAKSTEEN"
10310 GOSUB8000
10315 GOSUB8090:GOSUB8200
10320 PRINT"ISOL.      BINNENMUUR 1 STEEN IN :"
10330 PRINT"(CM)       BAKSTEEN    CELLENBETON"
10335 GOSUB8090
10350 RY=6:KO=2:I=1
10360 GOSUB8500
10370 RETURN
10400 GOSUB8100
10420 GOSUB8020:GOSUB8030
10440 I=11:IFVAL(IN$)=2 THENI=61
10450 RY=6:KO=4:GOSUB8500
10460 RETURN
10500 GOSUB8100
10520 GOSUB8020:GOSUB8040
10540 I=31:IFVAL(IN$)=2 THENI=46
10550 RY=6:KO=3:GOSUB8500
10560 RETURN
10600 GOSUB8010
10620 GOSUB8021:GOSUB8050
10640 RY=6:KO=5:I=81:GOSUB8500
10650 RETURN
11000 GOSUB100
11010 PRINT"VLOEREN, PLAFONDS, PLATTE DAKEN."
11015 GOSUB8000:GOSUB8090:GOSUB8200
11020 PRINT"     * BETON, HOLLE BAKSTEEN * HOUT"
11030 PRINT"     *  8-10 CM  * 12-15 CM  *  OP"
11040 PRINT"ISOL.*  DEKLAAG KURKBETON ?  *KEPERS"
11045 PRINT"(CM) * NEE * JA  * NEE * JA  *"
11050 RY=6:KO=5:I=106:GOSUB8090:GOSUB8500
11060 RETURN
12000 GOSUB100
12010 IFV1$="DEUR" THEN12200
12020 PRINT"RAMEN, LICHTKOEPELS E.D. :"
12021 GOSUB8090:GOSUB8200
12022 PRINTTAB(20);"HOUT   METAAL"
12025 PRINT"ENKEL GLAS";TAB(20);"5.20    5.80"
12030 PRINT"DUBB. GLAS 1/4 DUIM";TAB(20);"3.50    3.80"
12040 PRINT"DUBB. GLAS 1/2 DUIM";TAB(20);"3.25    3.60"
12050 PRINT"DUBBEL CHASSIS";TAB(20);"2.30    3.25"
12060 PRINT
12070 PRINT"WINKELRAAM";TAB(25);"6.00"
12080 PRINT"GLASBOUWSTEEN 9 CM";TAB(25);"3.00"
12090 PRINT"LICHTKOEPEL, ENK. GLAS   5.80"
12100 PRINTTAB(13);"DUB. GLAS   3.50"
12105 GOSUB8090
12110 RETURN
12200 PRINT"DEUREN :":GOSUB8090:GOSUB8200
12205 PRINT"BUITENDEUREN"
12210 PRINT" HOUT";TAB(17);"3.50 - 5.80"
12220 PRINT" METAAL";TAB(17);"5.80 -12.00"
12230 PRINT" HOUT + GLAS";TAB(17);"4.70 - 6.00"
12240 PRINT
12250 PRINT"BINNENDEUREN";TAB(17);"2.30 - 5.80"
12260 GOTO12105
25000 DATA-8,8,-6,-3,6,10,12
25010 DATA-10,5,-9,-6,3,8,12
25020 DATA-12,5,-12,-9,3,8,10
25030 DATA-14,3,-15,-12,0,6,10
25040 DATA-16,1,-17,-15,-2,4,8
25050 DATA1.7,.8,.72,.5,.61,.44,.53,.4,.48,.36
25060 DATA2,1.54,1.25,1.06,.76,.7,.63,.6,.62,.6,.53,.51
25070 DATA.53,.5,.46,.44,.46,.43,.41,.4
25080 DATA1.7,1.54,1.25,.82,.72,.69,.68,.61,.59
25090 DATA.58,.53,.51,.51,.48,.45
25100 DATA1.7,1.54,1.25,1.19,1.04,.93,1.04,.92,.83
25110 DATA.92,.83,.75,.82,.75,.69
25120 DATA2,1.7,1.54,1.25,1.08,.93,.82,.73,.94,.82,.73,.66
25130 DATA.83,.74,.66,.6,.74,.67,.61,.55
25140 DATA3.9,3.5,3.03,2.74,2.49,1.57,1.49,1.41,1.35,1.29
25150 DATA1.32,1.26,1.2,1.16,1.11,1.13,1.09,1.06,1.01,.97
25160 DATA1,.96,.92,.9,.87
25170 DATA2.7,2.29,2.5,2.15,1.2
25180 DATA1.06,.99,2.5,.96,.76
25190 DATA.81,.77,.79,.76,.65
25200 DATA.66,.63,.65,.62,.57
25210 DATA.56,.54,.55,.53,.5
25220 DATA.48,.47,.47,.46,.46
30000 REM BEREKENING VAN DE WARMTE VERLIEZEN VAN EEN LOKAAL
30010 REM GECOMPONEERD OP MALACH - COMPUTER IN BASICODE - 2
30020 REM DOOR : MARC LACHAERT
30030 REM        VAARTSTRAAT 38
30040 REM        B - 9288 KALKEM
30050 REM        BELGIE            21 MAART 1983
30060 REM
30070 REMCOPYRIGHT
30080 REMNOS HILVERSUM


1000 A=200:GOTO20:REM KALAH
1010 DIMBE(9,14),Z(6):KA=0:PQ=0:Z=0
1020 DIMA(14),Y(6),AL(6),AX(4,14),M(4,6),B(14),K(6),ZU(6)
1030 RESTORE:I=0:LG=0:PL=0:X8=21
1040 GOSUB100
1050 PRINT"KALAH VERSION 8.4"
1060 PRINT"=================":PRINT
1070 PRINT"PRESENTED    BY ":PRINT
1080 PRINT"THOMAS HOFMEISTER":PRINT:PRINT
1090 PRINT"DO YOU WANT THE ":PRINT
1100 PRINT"INSTRUCTIONS (Y/N)"
1110 GOSUB200
1120 IFIN$="Y" THENGOSUB3110:GOTO1140
1130 IFIN$<>"N" THEN1110
1140 GOSUB100:PRINT:PRINT"--- YOUR NAME ? --- ":PRINT
1150 INPUTNA$
1160 PRINT:PRINT:IFLEN(NA$)<=14 THEN1190
1170 PRINT"THE NAME IS TOO LONG"
1180 FORI=1 TO1000:NEXTI:GOTO1140
1190 FORI=1 TO14:READB(I):NEXTI
1200 FORI=1 TO14:READA(I):NEXTI
1210 GOSUB100
1220 PRINT"HOW STRONG DO YOU WANT"
1230 PRINT
1240 PRINT"THE COMPUTER TO BE?":PRINT
1250 PRINT"1=LOW  LEVEL   (FAST ANSWER)"
1260 PRINT
1270 PRINT"4=HIGH LEVEL   (SLOW ANSWER)"
1280 PRINT:PRINT"(1 TO 4)"
1290 PRINT:INPUTS7:S7=INT(S7)
1300 IF(S7<1) OR(S7>4) THENPRINT:PRINT"WRONG ENTRY":GOTO1290
1310 GOSUB100
1320 PRINT"WHO'S TO BEGIN ?":PRINT
1330 PRINT"(YOU=S)  (COMP=C)":PRINT
1340 INPUTJ$:GOSUB1530
1350 IF(J<>-1) AND(J<>0) THEN1320
1360 REM
1370 GOSUB100
1380 GOSUB2380
1390 IFA(14)+A(7)=72 THENFORGH=1 TO500:NEXTGH:GOTO2760
1400 FORI=1 TO6:IFA(I)=0 THENNEXTI:J=-1
1410 IFJ=-1 THEN1630
1420 REM SPIELERZUG
1430 PRINT
1440 PRINT"WHICH  HOLE";:INPUTG1$
1450 G1=VAL(G1$):IFG1$="A" THEN1560
1460 IFG1$="S" THEN1380
1470 IF(G1<1) OR(G1>6) THEN1440
1480 IFA(G1)=0 THEN1440
1490 P1=G1:Z=7:GOSUB2280
1500 IFKA=1 THEN1380
1510 J=-1-J
1520 GOTO1380
1530 IFJ$="C" THENJ=-1:RETURN
1540 IFJ$="S" THENJ=0:RETURN
1550 J=2:RETURN
1560 REM AENDERN DER SPIELSTAERKE
1570 GOSUB100:PRINT:PRINT"WHICH  POWER"
1580 PRINT:PRINT"DO YOU WISH";
1590 INPUTS7
1600 S7=INT(S7)
1610 IF(S7<1) OR(S7>4) THENPRINT:PRINT"WRONG ENTRY":GOTO1590
1620 GOTO1380
1630 PRINT:PRINT"PLEASE WAIT..."
1640 GOSUB120:VE=VE-2:GOSUB110
1650 Z=14:FORI=8 TO13:IFA(I)=0 THENNEXTI:GOTO1510
1660 TF=TF+1:AL(TF)=0:Z(TF)=Z
1670 FORI=Z-6 TOZ-1
1680 IFA(I)>0 THENAL(TF)=AL(TF)+1:M(TF,AL(TF))=I
1690 NEXTI
1700 FORI=1 TO14:AX(TF,I)=A(I):NEXTI
1710 Y(TF)=0
1720 Y(TF)=Y(TF)+1:Y(TF+1)=0
1730 IFY(TF)>AL(TF) THEN1860
1740 P1=M(TF,Y(TF)):GOSUB2280
1750 IFTF>=S7 THEN1790
1760 IFKA=1 THEN1660
1770 Z=Z-7:IFZ=0 THENZ=14
1780 GOTO1660
1790 IFZ=7 THENU=100:GOTO1830
1800 U=-100:DI=A(14)-A(7):ONKA+1 GOSUB2950,2580
1810 IFDI>U THENU=DI:BE(TF,Y(TF))=U
1820 FORI=1 TO14:A(I)=AX(TF,I):NEXTI:GOTO1720
1830 DI=A(14)-A(7):ONKA+1 GOSUB2580,2950
1840 IFDI<U THENU=DI:BE(TF,Y(TF))=U
1850 GOTO1820
1860 IFZ(TF)=7 THEN1910
1870 U=-100
1880 FORI=1 TOAL(TF)
1890 IFBE(TF,I)>U THENU=BE(TF,I):F=I
1900 NEXTI:TF=TF-1:BE(TF,Y(TF))=U:GOTO1960
1910 U=100
1920 FORI=1 TOAL(TF)
1930 IFBE(TF,I)<U THENU=BE(TF,I):F=I
1940 NEXTI:TF=TF-1:BE(TF,Y(TF))=U:GOTO1960
1950 RETURN
1960 IFTF=0 THEN1980
1970 FORI=1 TO14:A(I)=AX(TF,I):NEXTI:Z=Z(TF):GOTO1720
1980 U=-100:FORI=1 TOAL(1)
1990 IFBE(1,I)>U THENU=BE(1,I):F=I
2000 NEXTI
2010 REM
2020 REM
2030 YO=0:FORI=1 TOAL(1):IFBE(1,I)=U THENYO=YO+1:K(YO)=I
2040 NEXTI
2050 IFYO=1 THENP1=M(1,F):GOTO2190
2060 MN=-100:Z=14:FORIB=1 TOYO
2070 P1=M(1,K(IB)):GOSUB2280
2080 DI=A(14)-A(7):ONKA+1 GOSUB2950,2580:ZU(IB)=DI
2090 IFDI>(MN) THENMN=DI:F=IB
2100 FORL=1 TO14:A(L)=AX(1,L):NEXTL
2110 NEXTIB
2120 WW=0
2130 FORL=1 TOYO:IFZU(L)=(MN) THENWW=WW+1:K(WW)=K(L)
2140 NEXTL
2150 GOSUB260
2160 BO=RV*WW:BO=INT(BO+1)
2170 REM
2180 P1=M(1,K(BO))
2190 Z=14:GOSUB2280
2200 PRINT:PRINT"I CHOOSE    ";14-P1;"   "
2210 IFKA=1 THENPRINT"AND MAY DO IT AGAIN"
2220 PRINT:PRINT"ENTER ! TO CONT"
2230 GOSUB200:IFIN$<>"!" THEN2230
2240 IFKA=1 THENGOSUB2380:GOTO1630
2250 GOTO1510
2260 REM ZUG DURCHFUEHREN
2270 REM PL=ZAEHLER:LG=LAUFENDE GRUBE
2280 KA=0:LG=P1:TR=A(P1):A(P1)=0:G7=X8-Z
2290 FORPL=1 TOTR
2300 LG=LG+1:IFLG=15 THENLG=1
2310 IFLG=G7 THEN2300
2320 A(LG)=A(LG)+1:NEXTPL
2330 IFLG=Z THENKA=1:RETURN
2340 IFZ=B(LG) THENIFA(LG)=1 THEN2360
2350 RETURN
2360 REM LETZTE GRUBE WAR LEER
2370 T9=14-LG:A(Z)=A(Z)+A(LG)+A(T9):A(LG)=0:A(T9)=0:RETURN
2380 REM SUBROUTINE AUSGABE DER SPIELGRUBEN
2390 GOSUB100
2400 PRINT"   COMPUTERHOLES"
2410 PRINT
2420 FORSR=1 TO6:GOSUB300:PRINT" ";SR$;" ";:NEXTSR:PRINT
2430 PRINT" ================"
2440 CT=2:CN=0:FORI=13 TO8 STEP-1:SR=A(I)
2450 GOSUB310:PRINTSR$;" ";:NEXTI
2455 SR=A(14):GOSUB310
2460 PRINT:PRINT:PRINT:PRINTSR$;" :COMPUTER"
2470 PRINT
2480 FORI=1 TO14-LEN(NA$):PRINT" ";:NEXTI
2490 PRINTNA$;":";:SR=A(7):GOSUB310:PRINTSR$
2500 PRINT:PRINT
2510 FORI=1 TO6:SR=A(I):GOSUB310
2520 PRINTSR$;" ";:NEXTI
2530 PRINT:PRINT" ================"
2540 FORSR=1 TO6:GOSUB300:PRINT" ";SR$;" ";:NEXTSR
2550 PRINT:PRINT:PRINTTAB(3);"PLAYER'S HOLES"
2560 RETURN
2570 REM
2580 MI=-100:FORI=8 TO13:FL=0:X1=A(I):REMX2=ZIEL
2590 IFX1=0 THEN2730
2600 IF(X1=13) OR(X1=26) THENPQ=2+A(14-I):GOTO2730
2610 X2=X1+I:IFX2<15 THEN2690
2620 FL=1
2630 IFX2<21 THENX2=X2-14:GOTO2690
2640 IFX2<28 THENX2=X2-13:GOTO2690
2650 IFX2<34 THENX2=X2-27:GOTO2690
2660 IFX2<40 THENX2=X2-26:GOTO2690
2670 IFX2<47 THENX2=X2-40:GOTO2690
2680 X2=X2-39
2690 IFX2=14 THENPQ=2:GOTO2730
2700 IFX1>13 THEN2720
2710 IF(A(X2)=0) AND(B(X2)=14) THENPQ=1+A(14-X2):GOTO2730
2720 PQ=FL
2730 IFPQ>MI THENMI=PQ
2740 NEXTI:DI=DI+MI:RETURN
2750 REM
2760 GOSUB100:PRINT"ATTENTION: THE RESULT"
2770 PRINT"====================="
2780 PRINT:PRINT:PRINT
2790 PRINT"COMPUTER:";A(14)
2800 PRINT:PRINT:PRINT"PLAYER :";A(7):PRINT:PRINT
2810 IFA(14)<=A(7) THEN2840
2820 PRINT"THUS THE   ":PRINT:PRINT"COMPUTER WON !"
2830 GOTO2910
2840 IFA(7)<>A(14) THEN2860
2850 PRINT"THE GAME ENDED":PRINT:PRINT"IN A DRAW.":GOTO2910
2860 IFA(7)>A(14) THENPRINT"CONGRATULATION:"
2870 PRINT"---------------------"
2880 PRINT"YOU HAVE  WON      !!"
2890 PRINT"---------------------"
2900 PRINT:PRINT
2910 PRINT:PRINT"ONCE AGAIN  (Y/N) ?"
2920 GOSUB200:IFIN$="N" THEN3770
2930 IFIN$="Y" THEN1030
2940 GOTO2920
2950 MI=-100:FORI=1 TO6:X1=A(I):REMX2=ZIEL
2960 IFX1=0 THEN3060
2970 IF(X1=13) OR(X1=26) THENPQ=2+A(14-I):GOTO3050
2980 X2=X1+I:IFX2<14 THEN3000
2990 X2=X2-INT((X2-1)/13)*13
3000 IFX2=7 THENPQ=2:GOTO3050
3010 IFX1>13 THENPQ=1:GOTO3050
3020 IF(A(X2)=0) AND(B(X2)=7) THENPQ=1+A(14-X2):GOTO3050
3030 IFX2>7 THENPQ=1:GOTO3050
3040 PQ=0
3050 IFPQ>MI THENMI=PQ
3060 NEXTI:DI=DI-MI:RETURN
3070 REM
3080 REM
3090 REM
3100 REM
3110 GOSUB100
3120 PRINT"THIS PROGRAM ALLOWS YOU"
3130 PRINT:PRINT"TO PLAY THE GAME KALAH(A)"
3140 PRINT:PRINT"AGAINST THE COMPUTER."
3150 PRINT:PRINT"AT FIRST YOU HAVE TO "
3160 PRINT:PRINT"ANSWER A FEW QUESTIONS"
3170 PRINT:PRINT"AND THEN THE GAME"
3180 PRINT:PRINT"STARTS. IF IT'S THE"
3190 PRINT:PRINT"COMPUTER'S TURN TO"
3200 PRINT:PRINT"SELECT A HOLE HE'LL"
3210 PRINT:PRINT"DISPLAY THE WORDS 'PLEASE"
3220 GOSUB3730
3230 PRINT:PRINT"WAIT'. IF IT'S YOUR TURN"
3240 PRINT:PRINT"HE WILL ASK YOU FOR"
3250 PRINT:PRINT"YOUR CHOICE AND WILL"
3260 PRINT:PRINT"REFUSE INCORRECT ENTRIES."
3270 PRINT:PRINT"YOU CAN CHOOSE BETWEEN 4"
3280 PRINT:PRINT"DIFFERENT PLAYING FORCES"
3290 PRINT:PRINT"OF THE COMPUTER."
3300 PRINT:PRINT"HERE IS A LIST OF THE TIME"
3310 PRINT:PRINT"THE COMPUTER NEEDS ON THE"
3320 PRINT:PRINT"DIFFERENT LEVELS:"
3330 GOSUB3730
3340 PRINT:PRINT"LEVEL 1: 6-12 SECONDS"
3350 PRINT:PRINT"LEVEL 2: NEARLY 30    SECS"
3360 PRINT:PRINT"LEVEL 3: NEARLY 3-5   MINS"
3370 PRINT:PRINT"LEVEL 4: NEARLY 18-30 MINS"
3380 GOSUB3730
3390 PRINT"IF YOU WANT TO CHANGE THE LEVEL"
3400 PRINT:PRINT"DURING THE GAME THEN PLEASE"
3410 PRINT:PRINT"ENTER THE LETTER A INSTEAD"
3420 PRINT:PRINT"OF THE NUMBER OF YOUR CHOSEN"
3430 PRINT:PRINT"HOLE."
3440 PRINT:PRINT"IF YOU LEFT THE SCREEN BY MISTAKE"
3450 PRINT:PRINT"(E.G. YOU PRESSED CLR/HOME)"
3460 PRINT:PRINT"THEN ENTER THE LETTER S."
3470 GOSUB3730
3480 PRINT"NOW A WORD ABOUT THE RULES:"
3490 PRINT:PRINT"IT WOULD TAKE TOO LONG TO"
3500 PRINT:PRINT"EXPLAIN THE RULES OF KALAH(A)"
3510 PRINT:PRINT"TO YOU NOW, SO PLEASE LOOK"
3520 PRINT:PRINT"WHETHER YOU CAN FIND"
3530 PRINT:PRINT"THE EXPLANATIONS SOMEWHERE"
3540 PRINT:PRINT"ELSE.":GOSUB3730
3550 PRINT:PRINT"THE COMPUTER STICKS TO THE"
3560 PRINT:PRINT"CLASSICAL RULES, BUT THERE"
3570 PRINT:PRINT"IS ONE EXCEPTION:"
3580 PRINT:PRINT"IN THE CASE WHERE THE PLAYER"
3590 PRINT:PRINT"WHO'S TURN IT IS HASN'T GOT"
3600 PRINT:PRINT"ANY BALLS LEFT ON HIS SIDE,"
3610 PRINT:PRINT"THE OTHER PLAYER IS ALLOWED"
3620 PRINT:PRINT"TO CONTINUE UNTIL THE FORMER"
3630 PRINT:PRINT"ONE IS ABLE TO CONTINUE"
3640 PRINT:PRINT"WITH A CORRECT ENTRY."
3650 GOSUB3730
3660 PRINT:PRINT"P.S.: WE DO NOT PLAY CLOCKWISE"
3670 PRINT:PRINT"     BUT THE OTHER WAY ROUND!"
3680 GOSUB3730
3690 PRINT:PRINT"NOW I WISH YOU LOTS OF FUN"
3700 PRINT:PRINT"IN TRYING TO BEAT THE"
3710 PRINT:PRINT"MACHINE."
3720 GOSUB3730:RETURN
3730 VE=22:HO=0:GOSUB110
3740 PRINT"PLEASE PRESS A KEY TO CONT";
3750 GOSUB210
3760 GOSUB100:RETURN
3770 GOSUB100:PRINT"THIS WAS A PROGRAM BY"
3780 PRINT:PRINT"THOMAS HOFMEISTER IN BOCHUM"
3790 PRINT:PRINT"IF YOU WANT TO KNOW MY ADDRESS"
3800 PRINT:PRINT"THEN LIST 30100-"
3810 END
25000 DATA7,7,7,7,7,7,7,14,14,14,14,14,14,14
25010 DATA6,6,6,6,6,6,0,6,6,6,6,6,6,0
30000 REM
30010 REM KALAH TH.1/2,TH.1/3 V. 6.12.1980
30020 REM
30030 REM PROGRAMMVERAENDERUNGEN AM 22.5.81
30040 REM
30050 REM
30060 REM WEITERE VERBESSERUNGEN UND ERWEITERUNGEN
30070 REM AM 05.08.81
30080 REM ENTWICKLUNGSBEGINN: 30.10.1980
30090 REM UHRZEIT: 19:06
30100 REM
30110 REM
30120 REM THOMAS HOFMEISTER
30130 REM WITTENBERGSTR. 25
30140 REM 4630 BOCHUM 7
30150 REM W-GERMANY
30160 REM
30170 REM PROGRAM WAS WRITTEN ON A
30180 REM COMMODORE 64
30190 REM COPYRIGHT
30200 REM NOS HILVERSUM
30210 REM


1000 A=100:GOTO20:REM RENTEBEREKENING
1010 S0=0:DD=0:GOTO15000
1020 GOSUB100
1030 PRINTTAB(5);"*** RENTE BEREKENING ***":PRINT
1040 PRINT:PRINT"DIT PROGRAMMA BEREKENT RENTE OP"
1050 PRINT"SPAARREKENINGEN."
1060 PRINT"OP VRAGEN NAAR MAAND OF DAG MOET U EEN"
1070 PRINT"GETAL INVULLEN.":PRINT:PRINT
1080 PRINT"WILT U DE RENTE BEPALEN OVER EEN"
1090 PRINT"VOL JAAR? (J/N): ";:GOSUB210:PRINTIN$
1100 RJ$=IN$:IFRJ$<>"J" THEN1210
1110 PRINT
1120 PRINT"VERVALT DE RENTE OP 31 DECEMBER?"
1130 PRINT"(J/N): ";:GOSUB210:PRINTIN$
1140 IFIN$<>"J" THEN1210
1150 RM(1)=12:RD(1)=31:SM(1)=12:SD(1)=31:PRINT
1160 PRINT"HET JAAR WAAROVER U RENTE WILT BEREKENEN = ";
1170 INPUTJ:J=J-1:S0=0:IFJ/4-INT(J/4)=0 THENS0=1
1180 DN=365+S0:D1(1)=DN:D2(1)=DN:J=J+1
1190 SI=0:IFJ/4-INT(J/4)=0 THENSI=1
1200 DD=365+SI:JD=DD:RJ(1)=J-1:SJ(1)=J-1:GOTO1370
1210 PRINT:PRINT"GEEF DE DATUM WAAROP DE BEREKENING"
1220 PRINT"MOET BEGINNEN.":GOSUB3210
1230 RJ(1)=J:SJ(1)=J:RM(1)=M:SM(1)=M
1240 RD(1)=D:SD(1)=D:S0=SI:D1(1)=DN:D2(1)=DN
1250 IFRJ$<>"J" THEN1310
1260 DD=365:JD=365:J=J+1
1270 IFM<3 THENDD=DD+S0:JD=JD+S0
1280 SI=0:IFJ/4-INT(J/4)=0 THENSI=1
1290 IFM>2 THENDD=DD+SI:JD=JD+SI
1300 GOTO1370
1310 PRINT:PRINT"GEEF DE DATUM WAAROP DE RENTE VERVALT."
1320 GOSUB3210:DD=DN-D1(1)
1330 IFJV=0 THENJD=365+S0:GOTO1370
1340 IFRM(1)<3 THENJD=365+S0:GOTO1370
1350 IF(M>2) OR((M=2) AND(D=29)) THENJD=365+SI:GOTO1370
1360 JD=365
1370 GOSUB100
1380 PRINT"KENT U DE RENTE-PERCENTAGES? (J/N): ";
1390 GOSUB210:PRINTIN$
1400 IFIN$<>"J" THENRP=1:GOTO2060
1410 PRINT
1420 PRINT"WELK PERCENTAGE GOLD BIJ DE AANVANG?: ";
1430 INPUTP(1):RP=1
1440 IFP(1)>99 THENPRINT"ONMOGELIJK!":GOTO1410
1450 PRINT
1460 PRINT"ZIJN ER WIJZIGINGEN IN DIT PERCENTAGE"
1470 PRINT"GEWEEST? (J/N): ";:GOSUB210:PRINTIN$
1480 IFIN$<>"J" THENRN=1:GOTO2060
1490 PRINT:PRINT"GEEF DE GEGEVENS IN CHRONOLOGISCHE"
1500 PRINT"VOLGORDE!!"
1510 PRINT
1520 PRINT"NA DE LAATSTE WIJZIGING GEEFT U 0 ALS"
1530 PRINT"ANTWOORD OP DE VRAAG NAAR HET JAAR."
1540 I=1
1550 I=I+1:PRINT:PRINT"RENTE-PERCENTAGE OPGAVE NO ";I
1560 GOSUB3210
1570 IFJ=0 THENRP=I-1:GOTO1660
1580 IFI<11 THEN1620
1590 PRINT"ER ZIJN TEVEEL WIJZIGINGEN."
1600 PRINT"PAS EERST DE ARRAYS MET DIMENSIE 10 AAN"
1610 PRINT"IN REGEL 15010.":END
1620 RJ(I)=J:RM(I)=M:RD(I)=D:D2(I)=DN
1630 PRINT"NIEUW RENTE-PERCENTAGE = ";
1635 INPUTP(I)
1640 IFP(I)>99 THENPRINT"ONMOGELIJK!":GOTO1630
1650 GOTO1550
1660 PRINT
1670 PRINT"WILT U EEN OVERZICHT VAN DEZE GEGEVENS? (J/N): ";
1680 GOSUB210:PRINTIN$
1690 IFIN$<>"J" THEN2060
1700 GOSUB100
1710 HO=10:VE=0:GOSUB110:PRINT"RENTE-OVERZICHT"
1720 PRINT:PRINT"-----------------------------------"
1730 PRINT"NO      DATUM       RENTEPERCENTAGE"
1740 PRINT"- - - - - - - - - - - - - - - - - -"
1750 FORI=1 TORP
1760 HO=1:VE=I+4:IFI=10 THENHO=0
1770 GOSUB110:PRINTI
1780 HO=5:IFRD(I)<10 THENHO=6
1790 GOSUB110:PRINTRD(I);"-";M$(RM(I));"-";RJ(I)
1800 HO=26:IFP(I)>9.999 THENHO=25
1810 GOSUB110
1820 PRINT(INT(P(I)*10^3+.5))/10^3
1830 NEXTI
1840 PRINT"- - - - - - - - - - - - - - - - - -"
1850 PRINT
1860 PRINT"WILT U HIERAAN NOG TOEVOEGEN? (J/N): ";
1870 GOSUB210:PRINTIN$
1880 IFIN$="J" THENI=RP:GOTO1550
1890 PRINT
1900 PRINT"WILT U CORRECTIES AANBRENGEN? (J/N) :";
1910 GOSUB210:PRINTIN$
1920 IFIN$<>"J" THEN2060
1930 PRINT"VOLGNUMMER? = ";:INPUTN
1940 IF(N<1) OR(N>RP) THEN1930
1950 PRINT:PRINT"OUDE GEGEVENS RENTE-OPGAVE NO ";N
1960 PRINTRD(N);"-";RM(N);"-";RJ(N);
1970 PRINT"      PERCENTAGE:";P(N)
1980 GOSUB3210
1990 IFJ=0 THENEND
2000 RD(N)=D:RM(N)=M:RJ(N)=J:D2(N)=DN
2010 PRINT"NIEUW PERCENTAGE = ";:INPUTP(N):PRINT
2020 PRINT"WILT U NOG MEER CORRIGEREN? (J/N): ";
2030 GOSUB210:PRINTIN$
2040 IFIN$="J" THEN1930
2050 GOTO1660
2060 GOSUB100
2070 PRINT"NU MOET U UW SALDI OPGEVEN!":PRINT
2080 PRINT"WELK SALDO HAD U BIJ DE AANVANG? :";:INPUTS(1)
2090 S(1)=INT(100*S(1)+.0005)/100:SP=1
2100 IFS(1)<10^7 THEN2120
2110 PRINT"TE GROOT BEDRAG!":GOTO2080
2120 PRINT
2130 PRINT"ZIJN ER WIJZIGINGEN GEWEEST? (J/N): ";
2140 GOSUB210:PRINTIN$
2150 IFIN$<>"J" THEN2840
2160 PRINT:PRINT"GEEF DE GEGEVENS IN CHRONOLOGISCHE"
2170 PRINT"VOLGORDE!!"
2180 PRINT
2190 PRINT"NA DE LAATSTE WIJZIGING GEEFT U 0 ALS"
2200 PRINT"ANTWOORD OP DE VRAAG NAAR HET JAAR."
2210 I=1
2220 I=I+1:PRINT:PRINT"SALDO-OPGAVE NO ";I
2230 GOSUB3210
2240 IFJ=0 THENSP=I-1:GOTO2350
2250 IFI<101 THEN2290
2260 PRINT"ER ZIJN TEVEEL WIJZIGINGEN."
2270 PRINT"PAS EERST DE ARRAYS MET DIMENSIE 100"
2280 PRINT"AAN IN REGEL 15020.":END
2290 SJ(I)=J:SM(I)=M:SD(I)=D:D1(I)=DN
2300 PRINT"NIEUW SALDO = ";:INPUTS(I)
2310 S(I)=INT(100*S(I)+.0005)/100
2320 IFS(I)<10^7 THEN2340
2330 PRINT"TE GROOT BEDRAG.":GOTO2300
2340 GOTO2220
2350 PRINT:PRINT"WILT U EEN SALDO-OVERZICHT? (J/N): ";
2360 GOSUB210:PRINTIN$:IFIN$<>"J" THEN2840
2370 I=1
2380 IS=10*INT(I/10):GOSUB100
2390 HO=10:VE=0:GOSUB110:PRINT"SALDO-OVERZICHT"
2400 PRINT:PRINT"-----------------------------------"
2410 PRINT" NO          DATUM            SALDO"
2420 PRINT"- - - - - - - - - - - - - - - - - -"
2430 SR=I:CT=3:CN=0:GOSUB310:PRINTSR$
2440 HO=10:VE=I+4-IS:IFSD(I)<10 THENHO=11
2450 GOSUB110:PRINTSD(I);"-";M$(SM(I));"-";SJ(I)
2460 HO=26:GOSUB110
2470 SR=S(I):CT=9:CN=2:GOSUB310:PRINTSR$
2480 IFI=SP THEN2560
2490 IFI/10-INT(I/10)=0 THENI=I+1:GOTO2510
2500 I=I+1:GOTO2430
2510 PRINT"- - - - - - - - - - - - - - - - - -"
2520 PRINT:PRINT"NOTEERT U DE VOLGNUMMERS VAN"
2530 PRINT"FOUTIEVE OPGAVEN!"
2540 PRINT"DRUK OP <RETURN> VOOR HET VERVOLG.";:GOSUB210
2550 GOTO2380
2560 PRINT"- - - - - - - - - - - - - - - - - -"
2570 PRINT
2580 PRINT"WILT U HIERAAN NOG TOEVOEGEN? (J/N): ";
2590 GOSUB210:PRINTIN$
2600 IFIN$="J" THENI=SP:GOTO2220
2610 PRINT
2620 PRINT"WILT U CORRECTIES AANBRENGEN? (J/N): ";
2630 GOSUB210:PRINTIN$
2640 IFIN$<>"J" THEN2840
2650 PRINT:PRINT"VOLGNUMMER = ";:INPUTN
2660 IF(N<1) OR(N>SP) THEN2650
2670 PRINT:PRINT"OUDE SALDO-OPGAVE NO ";N
2680 PRINTSD(N);"-";SM(N);"-";SJ(N);" :       ";S(N)
2690 PRINT:PRINT"NIEUWE OPGAVE":GOSUB3210
2700 IFJ=0 THENEND
2710 SD(N)=D:SM(N)=M:SJ(N)=J:D1(N)=DN
2720 PRINT"NIEUW SALDO = ";:INPUTS(N)
2730 S(N)=INT(100*S(N)+.0005)/100
2740 IFS(N)<10^7 THEN2760
2750 PRINT"TE GROOT BEDRAG.":GOTO2720
2760 PRINT
2770 PRINT"WILT U NOG MEER CORRIGEREN (J/N): ";
2780 GOSUB210:PRINTIN$
2790 IFIN$="J" THEN2650
2800 GOTO2350
2810 REM 
2820 REM  * BEREKENINGEN *
2830 REM 
2840 RN=1:SN=1:DL=D1(SN)
2850 IFRN=RP THEN2950
2860 IFSN=SP THEN2990
2870 IFD2(RN+1)>D1(SN+1) THEN2920
2880 D=D2(RN+1)-DL:DL=D2(RN+1):DT=DT+D
2890 S=S(SN):P=P(RN):GOSUB3470
2900 RN=RN+1:IFD2(RN)=D1(SN+1) THENSN=SN+1
2910 GOTO2850
2920 D=D1(SN+1)-DL:DL=D1(SN+1):DT=DT+D
2930 S=S(SN):P=P(RN):GOSUB3470
2940 SN=SN+1:GOTO2850
2950 IFSN=SP THEN2990
2960 FORI=SN TOSP-1:D=D1(I+1)-DL
2970 DL=D1(I+1):DT=DT+D:S=S(I):P=P(RN)
2980 GOSUB3470:NEXTI
2990 D=DD-DT:S=S(SP):P=P(RP):GOSUB3470
3000 RA=RA/JD:RB=RB/JD
3010 GOSUB100
3020 IFP(1)=0 THEN3080
3030 SR=INT(.5+(100*RA))/100:CT=9:CN=2
3040 GOSUB310:PRINT"DE RENTE IS: ";SR$
3050 PRINT:PRINT"DE GEMIDDELDE RENTE IS: ";
3060 PRINTINT(0.5+(100*RA/RB))/100;"%"
3070 GOTO3190
3080 PRINT"WEET U WELKE RENTE IS UITGEKEERD? (J/N): ";
3090 GOSUB210:PRINTIN$
3100 IFIN$<>"J" THEN3150
3110 PRINT:PRINT"HET BEDRAG WAS: ";:INPUTR
3120 PRINT:PRINT"DE GEMIDDELDE  RENTE WAS: ";
3130 PRINTINT(.5+(100*R/RB))/100;"%"
3140 GOTO3190
3150 SR=INT(.5+(100*RB))/100:CT=8:CN=2
3160 GOSUB310:PRINT
3170 PRINT"BIJ EEN GEMIDDELDE RENTE VAN 1% IS"
3180 PRINT"HET RENTE-BEDRAG: ";SR$
3190 END
3200 REM  * DATUMBEPALING *
3210 PRINT:PRINT"JAAR  = ";:INPUTJ:J=INT(J)
3220 IFJ=0 THENRETURN
3230 IFRJ(1)=0 THEN3260
3240 IF(J=RJ(1)) OR(J=RJ(1)+1) THEN3260
3250 PRINT"FOUTIEVE OPGAVE":GOTO3210
3260 PRINT"MAAND = ";:INPUTM:M=INT(M)
3270 IF(M>0) AND(M<13) THEN3290
3280 PRINT"FOUTIEVE OPGAVE":GOTO3260
3290 PRINT"DAG   = ";:INPUTD:D=INT(D)
3300 SI=0:IFJ/4-INT(J/4)=0 THENSI=1
3310 IFM<>2 THEN3340
3320 IF(M=2) AND(D<29+SI) THEN3360
3330 PRINT"FOUTIEVE OPGAVE":GOTO3290
3340 IF(D>0) AND(D<M(M)+1) THEN3360
3350 PRINT"FOUTIEVE OPGAVE.":GOTO3290
3360 JV=0:IFRJ(1)>0 THENJV=J-RJ(1)
3370 S1=0:IF(SI=1) AND(M>2) THENS1=1
3380 DN=MT(M-1)+D+S1+JV*(365+S0)
3390 IFDD=0 THENRETURN
3400 IFDN-D1(1)<DD THEN3430
3410 PRINT"DEZE DATUM LIGT VOORBIJ DE OPGEGEVEN"
3420 PRINT"EINDDATUM.":GOTO3210
3430 IFDN>=D1(1) THENRETURN
3440 PRINT"DEZE DATUM LIGT VOOR DE OPGEGEVEN"
3450 PRINT"EINDDATUM.":GOTO3210
3460 REM   * RENTE-BEREKENING *
3470 RA=RA+D*S*P/100
3480 RB=RB+D*S/100
3490 RETURN
15000 REM  * INITIALISATIE *
15010 DIMRJ(10),RM(10),RD(10),P(10),D2(10)
15020 DIMSJ(100),SM(100),SD(100),S(100),D1(100)
15030 DIMM(12),MT(12),M$(12)
15040 MT=0:RJ(1)=0:P(1)=0:DT=0:RA=0:RB=0
15050 RESTORE:FORI=0 TO12
15060 READM(I):MT=MT+M(I):MT(I)=MT:NEXTI
15070 FORI=1 TO12:READM$(I):NEXTI
15080 GOTO1020
25000 DATA  0,31,28,31,30,31,30,31,31,30,31,30,31
25010 DATA  "JAN","FEB","MRT","APR","MEI","JUN"
25020 DATA  "JUL","AUG","SEP","OKT","NOV","DEC"
30000 REM  * RENTE BEREKENING *
30010 REM 
30020 REM  DIT PROGRAMMA
30030 REM  BEREKENT RENTE OP
30040 REM  SPAARREKENINGEN. ER
30050 REM  WORDT REKENING
30060 REM  GEHOUDEN MET
30070 REM  WIJZIGINGEN IN HET
30080 REM  RENTE-PERCENTAGE.
30090 REM  INDIEN U DEZE
30100 REM  GEGEVENS NIET
30110 REM  KENT, KUNT U DE
30120 REM  EFFECTIEVE RENTE
30130 REM  BEPALEN EN DE RENTE-
30140 REM  OPBRENGST BIJ EEN
30150 REM  FICTIEVE RENTE VAN
30160 REM  1%.
30170 REM  DE PERIODE WAAROVER
30180 REM  RENTE WORDT BEREKEND
30190 REM  IS MAXIMAAL 1 JAAR.
30200 REM  ER IS REKENING
30210 REM  GEHOUDEN MET
30220 REM  SCHRIKKELJAREN.
30230 REM 
30240 REM  *******************
30250 REM  DR.IR. B. ZELLE
30260 REM  MACKAYSTRAAT 41
30270 REM  2613 WJ   DELFT
30280 REM  TEL. 015-141548
30290 REM  *******************
30300 REM
30310 REM  COPYRIGHT
30320 REM  NOS HILVERSUM 

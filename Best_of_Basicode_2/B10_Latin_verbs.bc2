
1000 A=10000: GOTO20
1010  GOSUB100: GOTO6260
1020  GOSUB100
1050 PRINT"ENTER INFINITIVE OF THE PRESENT"
1060 PRINT:INPUTWE$
1070 IF(LEN(WE$)>3) OR(WE$="IRE") THEN1090
1080  GOTO1450
1090 XD=0:PA=0:HS=0:X=0
1100 IFWE$="VENIRE" THEN GOSUB5830
1110 IFX=1 THEN1240
1120 FORX=1 TO9
1130 IFXD$(X)<>RIGHT$(WE$,LEN(XD$(X))) THEN1230
1140 IFX<>7 THEN1220
1150 IFLEN(WE$)=3 THEN1220
1160 Y=0
1170 IFVV$(Y)<>LEFT$(WE$,LEN(VV$(Y))) THEN1200
1180 IFLEN(WE$)<>LEN(VV$(Y))+3 THEN1230
1190  GOTO1220
1200 Y=Y+1:IFY>15 THEN1230
1210  GOTO1170
1220 XD=X:X=9
1230 NEXTX:IFXD>0 THENRETURN
1240 IF(RIGHT$(WE$,1)="I") OR(RIGHT$(WE$,2)="RE") THEN1260
1250  GOTO1450
1260 IFRIGHT$(WE$,3)<>"ERE" THEN1360
1270  GOSUB100
1280  GOSUB7070
1290 PRINT"1 AN E-ROOT CONJUGATION , OR"
1300 PRINT"2 A CONSONANT-ROOT CONJUGATION"
1310  GOSUB1470
1320 X=VAL(IN$):IF(X-1)*(X-2)=0 THEN1340
1330 VE=0:HO=0: GOSUB110: GOTO1280
1340 IFX=1 THENCJ=1:RETURN
1350 CJ=2: GOSUB6980:RETURN
1360 IFRIGHT$(WE$,1)="I" THEN1400
1370 IFRIGHT$(WE$,3)="ARE" THENCJ=0:RETURN
1380 IFRIGHT$(WE$,3)="IRE" THENCJ=3:RETURN
1390  GOTO1450
1400 PA=1
1410 IFRIGHT$(WE$,3)="ARI" THENCJ=0:RETURN
1420 IFRIGHT$(WE$,3)="ERI" THENCJ=1:RETURN
1430 IFRIGHT$(WE$,3)="IRI" THENCJ=3:RETURN
1440 CJ=2:RETURN
1450  GOSUB100:PRINT"NOT EXISTING INFINITIVE FORM"
1460 FORX=0 TO1000:NEXTX: GOTO1020
1470 PRINT:PRINT"( )";: GOSUB120:HO=HO-2: GOSUB110
1480  GOSUB210:RETURN
1490  GOSUB100
1500 PRINT"1 DO YOU WANT TO SEE ALL POSSIBLE"
1510 PRINT"  FORMS OF THE VERB ?"
1520 PRINT"2 DO YOU WANT TO MAKE YOUR OWN CHOICE?"
1530  GOSUB1470:L=VAL(IN$)
1540 IF(L-1)*(L-2)=0 THENRETURN
1550 VE=0:HO=0: GOSUB110: GOTO1500
1560 X=3:IF(CJ=2) AND(PA=1) THENX=1
1570 ST$(0)=LEFT$(WE$,LEN(WE$)-X):RETURN
1580 AF=0:S1$=ST$(0)
1590  GOSUB100:PRINT"MOMENT PLEASE,SEARCHING IN THE LIST"
1600 PRINT"OF IRREGULAR VERBS"
1610 FORX=S1(PA,CJ) TOS2(PA,CJ)
1620 WA=0:A1$=AF$(X,0)
1630 IFVAL(RIGHT$(A1$,1))=0 THEN1650
1640 WA=1
1650 IFLEN(A1$)-WA>LEN(S1$) THEN1830
1660 W1$=MID$(S1$,LEN(S1$)-LEN(A1$)+1+WA,1)
1670 IFLEFT$(A1$,1)<>W1$ THEN1830
1680 W1$=RIGHT$(S1$,LEN(A1$)-WA)
1690 IFLEFT$(A1$,LEN(A1$)-WA)<>W1$ THEN1830
1700 IFLEN(A1$)<>1 THEN1780
1710 IFCJ>0 THEN1780
1720 Y=0:IFLEN(S1$)=1 THEN1780
1730 IFVV$(Y)<>LEFT$(S1$,LEN(VV$(Y))) THEN1760
1740 IFLEN(VV$(Y))+LEN(A1$)<>LEN(S1$) THEN1830
1750  GOTO1780
1760 Y=Y+1:IFY>16 THEN1830
1770  GOTO1730
1780 AF=1:GE=X+HS:X=S2(PA,CJ)
1790 PRINT:PRINT"FOUND IN THE LIST OF IRREGULAR VERBS"
1800 FORY=1 TO400:NEXTY
1810 IFRIGHT$(A1$,1)="2" THEN1830
1820 IF(RIGHT$(A1$,1)="1") OR(RIGHT$(A1$,1)="3") THENCJ=4
1830 NEXTX:IFAF=1 THEN1860
1840 PRINT:PRINT"NOT IN THE LIST OF IRREGULAR VERBS"
1850 FORY=1 TO400:NEXTY
1860 RETURN
1870  GOSUB120
1880 FORX=1 TOLEN(KL$(VE))
1890 KA$=MID$(KL$(VE),X,1)
1900 IFKA$="1" THENPRINT"! ";: GOTO1990
1910 IFKA$="3" THEN1960
1920 PRINT"+";
1930 FORY=0 TOHH-4
1940 PRINT"-";:NEXTY
1950 PRINT"+": GOTO1990
1960 FORY=0 TOHH-HO-3
1970 PRINT" ";:NEXTY
1980 PRINT"!"
1990 NEXTX
2000 IF(VE>=12) OR(WE<20) THENRETURN
2010 IF((VE=3) OR(VE=4)) AND(WE=30) THEN1870
2020 IF(VE>=7) AND(WE>20) THEN1870
2030 IF(VE>=8) AND(WE=20) THEN1870
2040 RETURN
2050 IFAF=0 THEN2150
2060 DU=0
2070 IFLEN(S1$)=LEN(A1$)-(WA) THENA$="": GOTO2100
2080 IF(RIGHT$(A1$,1)="2") OR(RIGHT$(A1$,1)="3") THENDU=2
2090 A$=LEFT$(S1$,LEN(S1$)-(LEN(A1$)-WA))
2100 ST$(1)=A$+MID$(AF$(GE,1),1+DU,LEN(AF$(GE,1))-DU)
2110 ST$(2)=A$+AF$(GE,2)
2120 IFRIGHT$(ST$(2),3)<>"SAT" THENRETURN
2130 IFLEN(ST$(2))>3 THEN GOSUB7110
2140 RETURN
2150 FORX=1 TO2
2160 ST$(X)=S1$:NEXTX
2170 RETURN
2180 X1=1
2190 PRINTNM$(VAL(MID$(NA$(WE),X1,2)));
2200 IF(WE=20) AND(X1=5) THEN GOSUB5990
2210  GOSUB1870
2220 X1=X1+2:IFX1>LEN(NA$(WE))-2 THENRETURN
2230  GOTO2190
2240 VE=VV-1:HO=0: GOSUB110
2250 PRINT"ENTER KEY ";: GOSUB210
2255 VE=VV-1:HO=0: GOSUB110:PRINT"         ";
2260 WE=WE+1:IF(RIGHT$(NA$(WE),1)="0") AND(PA=1) THEN2260
2270 IFXD>0 THEN4670
2280 FORZW=0 TO1
2290 IFZW=0 THENFORX=3 TO5: GOTO2310
2300 FORX=7 TOPE+7
2310 VE=X:HO=2: GOSUB110
2320 FORY=1 TOHH-4
2330 PRINT" ";
2340 NEXTY:NEXTX:NEXTZW
2350 HO=0:VE=0: GOSUB110
2360 PE=5:XP=0
2370 IF(CJ>1) AND(WE=4) THENXP=-1
2380 IFTE=0 THEN2420
2390 HO=0:VE=VV-2: GOSUB110
2400 PRINT"OUT OF ";F2;"  FORMS  ";F1;"   WRONG"
2410 HO=0:VE=0: GOSUB110
2420 IFWE=20 THENPE=1: GOTO2440
2430 IFWE>20 THENPE=0
2440  GOSUB1870
2450 PRINT"VERB : ";WE$;
2460  GOSUB1870
2470  GOSUB2180
2480 IFXD>0 THEN2520
2490 Z=0:IF(AF=0) OR(ST(WE)=0) THEN2520
2500 IFST(WE)=1 THENZ=AF(GE,0): GOTO2520
2510 IFST(WE)=2 THENZ=AF(GE,1)
2520 FORP1=0 TOPE
2530 IFXD>0 THEN GOSUB2690: GOTO4720
2540 W1$=ST$(ST(WE))+BI$(WE,(CJ+Z)*6+P1)+UI$(UI(WE)+XP,P1)
2550 IFVAL(MID$(NA$(WE),7,1))>0 THEN GOSUB3880
2560 IFTE=1 THEN GOSUB2930
2570 IF(WE<>22) AND(WE<>28) THEN2590
2580 IFRIGHT$(WE$,4)="MORI" THEN5920
2590 IFWE<>20 THEN2640
2600 IFPA=1 THEN5810
2610 IF(RIGHT$(W1$,4)="DICE") OR(RIGHT$(W1$,4)="DUCE") THEN6030
2620 IFLEFT$(WE$,5)="MEMIN" THEN5880
2630 IFRIGHT$(WE$,5)="SCIRE" THEN5900
2640 PRINTW1$;
2650  GOSUB1870
2660 NEXTP1
2670 IF(WE=30) OR(L=2) THEN2710
2680  GOTO2240
2690 IF(L=1) AND(P1=0) AND(WE=0) THENX2=1
2700 RETURN
2710 HO=0:VE=VV-1: GOSUB110
2720 FORX=1 TO30:PRINT" ";:NEXTX
2730 HO=0:VE=VV-1: GOSUB110
2740 PRINT"<ENTER KEY> ";: GOSUB210
2750  GOSUB100
2760 PRINT"1 OTHER FORMS OF THE SAME VERB"
2770 PRINT"2 OTHER VERB"
2780 PRINT"3 END"
2790  GOSUB1470:X=VAL(IN$)
2800 IF(X-1)*(X-2)*(X-3)=0 THEN2820
2810 HO=0:VE=0: GOSUB110: GOTO2760
2820 IFX=3 THEN GOSUB100:END
2830 IFX=2 THEN6090
2840  GOTO6140
2850  GOSUB100
2860 PRINT"DO YOU WANT TO BE TESTED ? (Y/N)"
2870  GOSUB1470:IF(IN$="Y") OR(IN$="N") THEN2890
2875 IF(IN$="y") OR(IN$="n") THEN2890
2880 HO=0:VE=0: GOSUB110: GOTO2860
2890 IF(IN$="Y") OR(IN$="y") THENF1=0:F2=0:TE=1:RETURN
2900 TE=0:RETURN
2910 VE=P1+7:HO=2
2920  GOSUB110:RETURN
2930 INPUTIN$:CT=3:CN=0
2940 IFIN$=W1$ THEN3060
2950  GOSUB250
2960  GOSUB2910
2970 FORX=1 TOLEN(IN$)+1:PRINT" ";:NEXTX
2980  GOSUB2910
2990 FORX=1 TO2:PRINT"WRONG":FORI=1 TO200:NEXTI
3000  GOSUB2910
3010 PRINT"      ": GOSUB2910:FORI=1 TO100
3020 NEXTI:NEXTX
3030 F1=F1+1
3040 VE=VV-2:HO=17: GOSUB110
3050 SR=F1: GOSUB310:PRINTSR$
3060 F2=F2+1
3070 VE=VV-2:HO=6: GOSUB110
3080 SR=F2: GOSUB310:PRINTSR$
3090  GOSUB2910:FORX=1 TOLEN(IN$)+1
3100 PRINT" ";:NEXTX: GOSUB2910
3110 RETURN
3120  GOSUB100
3130 FORX=6 TO13
3140 PRINTX-5;
3160 IFX=11 THENPRINT" FUT.PERFECT": GOTO3180
3170 PRINT" ";NM$(X)
3180 NEXTX
3190 PRINT"9 ";NM$(2)
3200 PRINT"10";NM$(3)
3210 HO=14:VE=0: GOSUB110
3220 PRINT"1 ";NM$(4)
3230 VE=1: GOSUB110
3240 PRINT"2 ";NM$(5)
3250 HO=24:VE=0: GOSUB110
3260 FORX=0 TO1
3270 PRINTX+1;" ";NM$(X)
3280 VE=X+1: GOSUB110
3290 NEXTX
3300 HO=2:VE=11: GOSUB110
3310 INPUTKK(0)
3320 KK(0)=ABS(INT(KK(0)))
3330 IF(KK(0)<1) OR(KK(0)>10) THEN3300
3340 IFKK(0)=8 THENWE=30: GOTO3810
3350 IFKK(0)=9 THENWE=20: GOTO3810
3360 IFKK(0)<>7 THEN3470
3370  GOSUB100
3380 PRINT"1 PARTICIPLE OF THE PRESENT,ACTIVE"
3390 PRINT"2 PARTICIPLE OF THE FUTURE,ACTIVE"
3400 PRINT"3 PARTICIPLE OF THE PERFECT,PASSIVE"
3410  GOSUB1470:X=VAL(IN$)
3420 IF(X-1)*(X-2)*(X-3)=0 THEN3440
3430 HO=0:VE=0: GOSUB110: GOTO3380
3440 IFX=1 THENWE=27: GOTO3810
3450 IFX=2 THENWE=28: GOTO3810
3460 WE=29: GOTO3810
3470 IFKK(0)<>10 THEN3580
3480  GOSUB100
3490 PRINT"1 INFINITIVE OF THE PRESENT,ACTIVE"
3500 PRINT"2 INFINITIVE OF THE FUTURE,ACTIVE"
3510 PRINT"3 INFINITIVE OF THE PERFECT,ACTIVE"
3520 PRINT"4 INFINITIVE OF THE PRESENT,PASSIVE"
3530 PRINT"5 INFINITIVE OF THE FUTURE,PASSIVE"
3540 PRINT"6 INFINITIVE OF THE PERFECT,PASSIVE"
3550  GOSUB1470:X=ABS(INT(VAL(IN$)))
3560 IF(X<1) OR(X>6) THENHO=0:VE=0: GOSUB110: GOTO3490
3570 WE=20+X: GOTO3810
3580 HO=19: GOSUB110:INPUTKK(1)
3590 IF(KK(1)-1)*(KK(1)-2)<>0 THEN3580
3600 HO=31: GOSUB110:INPUTKK(2)
3610 IF(KK(2)-1)*(KK(2)-2)<>0 THEN3600
3620 KK(0)=KK(0)+5
3630 KK(1)=KK(1)+3
3640 KK(2)=KK(2)-1
3650 KK$=""
3660 FORX=0 TO2
3670 SR=KK(X): GOSUB300
3680 IFVAL(SR$)>9 THEN3700
3690 SR$="0"+SR$
3700 KK$=KK$+SR$
3710 NEXTX
3720 WE=99
3730 FORX=0 TO19
3740 IFKK$<>LEFT$(NA$(X),6) THEN3760
3750 WE=X:X=19
3760 NEXTX
3770 IFWE<>99 THEN3810
3780  GOSUB100
3790 PRINT"NOT PRESENT"
3800 FORX=1 TO500:NEXTX: GOTO3120
3810 IFPA=0 THEN3830
3820 IFRIGHT$(NA$(WE),1)="0" THEN3840
3830 RETURN
3840  GOSUB100
3850 PRINT"THIS COMBINATION DOESN'T OCCUR"
3860 PRINT"WITH DEPONENTS"
3870 FORX=1 TO500:NEXTX: GOTO3120
3880 W1$=W1$+" "+BI$(P1+25,VAL(MID$(NA$(WE),7,1)))
3890 RETURN
3900 CJ=2:Z=0
3910 ONXD GOTO3920,4020,4100,4100,4100,4240,4380,4450,4450
3920 A$="5001510202000300540305006204"
3930 B$="63051400150020012101220023002800"
3940 RR$=A$+B$
3950 A$="":IFLEN(WE$)=4 THEN3990
3960 A$=LEFT$(WE$,LEN(WE$)-4)
3970 IFA$="AB" THENA$="A"
3980 IFA$="PROD" THENA$="PRO"
3990 ST$(1)=A$+"FU"
4000 ST$(2)=A$+"FUT"
4010  GOTO6140
4020 IFLEN(WE$)=5 THEN4050
4030  GOSUB100:PRINT"NOT EXISTING INFINITIVE FORM"
4040 FORI=1 TO500:NEXTI: GOTO6090
4050 A$="500151020200030054030500"
4060 B$="6204630514001500210123002701"
4070 RR$=A$+B$
4080 ST$(1)="POTU"
4090  GOTO6140
4100 IFLEN(WE$)=5 THEN4130
4110  GOSUB100:PRINT"NOT EXISTING INFINITIVE FORM"
4120 FORI=1 TO450:NEXTI: GOTO6090
4130 A$="50075108020003005409"
4140 B$="05006210631114001500"
4150 RR$=A$+B$
4160 IFXD<>3 THEN4190
4170 RR$=RR$+"2001210123002701"
4180 ST$(1)="NOLU": GOTO6140
4190 IFXD<>4 THEN4220
4200 RR$=RR$+"210123002701"
4210 ST$(1)="VOLU": GOTO6140
4220 RR$=RR$+"21012300"
4230 ST$(1)="MALU": GOTO6140
4240 A$="5013010002000300040005005614070008000900"
4250 B$="1000110012001301140015001600170118001900"
4260 C$="20012101220023002401250026002700280029003000"
4270 RR$=A$+B$+C$
4280 A$="":B$="":C$=""
4290 IFLEN(WE$)=5 THEN4350
4300 FORX=0 TO7
4310 IFLEFT$(WE$,LEN(WE$)-5)<>FE$(X,0) THEN4330
4320 FE=X:X=7
4330 NEXTX
4340 A$=FE$(FE,0):B$=FE$(FE,1):C$=FE$(FE,2)
4350 ST$(0)=A$+"FER":ST$(1)=B$+"TUL"
4360 ST$(2)=C$+"LAT"
4370  GOTO6140
4380 A$="501551160200030054170500060107010801"
4390 B$="090110011101621963201400652116011701"
4400 C$="1801190120012101220123012401270128013001"
4410 RR$=A$+B$+C$
4420 A$="":IFLEN(WE$)=3 THEN4440
4430 A$=LEFT$(WE$,LEN(WE$)-3)
4440 ST$(1)=A$+"I": GOTO6140
4450 A$="0000010002000300040005000601070108000900"
4460 B$="1001110012001300140015001601170118001900"
4470 C$="20012100220023002401250026002700280029003000"
4480 RR$=A$+B$+C$
4490 CJ=4:A$="":IF(LEN(WE$)=5) OR(LEN(WE$)=6) THEN4530
4500 IFRIGHT$(WE$,1)="E" THEN4520
4510 A$=LEFT$(WE$,LEN(WE$)-5): GOTO4530
4520 A$=LEFT$(WE$,LEN(WE$)-6)
4530 ST$(1)=A$+"FEC":ST$(2)=A$+"FACT": GOTO6140
4540 Y=0
4550 FORX=1 TOLEN(RR$) STEP4
4560 X1=VAL(MID$(RR$,X,2))
4570 IFX1>30 THENX1=X1-50
4580 IFWE<>X1 THEN4600
4590 Y=1:X2=X:X=LEN(RR$)
4600 NEXTX
4610 IFY=1 THEN6170
4620  GOSUB100
4630 PRINT"FORM NOT PRESENT"
4640 FORX=1 TO450:NEXTX
4650  GOTO6150
4660 W1$=BI$(P1+25,VAL(MID$(RR$,X2+2,2))):RETURN
4670 XX=XX+4:IFXX>LEN(RR$) THEN2750
4680 XY=VAL(MID$(RR$,XX,2))
4690 IFXY>30 THENXY=XY-50
4700 IFXY<>WE THENWE=WE+1: GOTO4700
4710 X2=XX: GOTO2280
4720 ONXD GOTO4730,4880,4980,5110,5190,5280,5430,5530,5530
4730 IFVAL(MID$(RR$,X2,2))<50 THEN4770
4740  GOSUB4660:IFA$="PRO" THEN4860
4750 IFA$="A" THENA$="AB"
4760 W1$=A$+W1$: GOTO2560
4770 IFVAL(MID$(RR$,X2+2,2))=1 THEN4790
4780  GOTO2540
4790 IFWE=21 THEN4850
4800 W1$=A$:IFA$="PRO" THENW1$=W1$+"D"
4810 IFA$="A" THENW1$=W1$+"B"
4820 W1$=W1$+"ES"
4830 IFP1=1 THENW1$=W1$+"TE"
4840  GOTO2560
4850 W1$=WE$: GOTO2560
4860 IFLEFT$(W1$,1)="E" THENW1$="D"+W1$
4870  GOTO4760
4880 IFVAL(MID$(RR$,X2,2))<50 THEN4940
4890  GOSUB4660:IFLEFT$(W1$,1)="S" THENA$="POS": GOTO4920
4900 IFLEFT$(W1$,3)="ESS" THENA$="POS": GOTO4920
4910 A$="POT"
4920 IFLEFT$(W1$,3)="ESS" THENW1$=RIGHT$(W1$,LEN(W1$)-2)
4930 W1$=A$+W1$: GOTO2560
4940 IFVAL(MID$(RR$,X2+2,2))=1 THEN4960
4950  GOTO2540
4960 IFWE=21 THENW1$=WE$: GOTO2560
4970 W1$="POTENS": GOTO2560
4980 IFVAL(MID$(RR$,X2,2))<50 THEN5040
4990  GOSUB4660
5000 IFWE<>0 THEN5030
5010 IF(P1=0) OR(P1=5) OR(P1=3) THEN5030
5020 W1$="NON "+W1$: GOTO2560
5030 W1$="NO"+RIGHT$(W1$,LEN(W1$)-2): GOTO2560
5040 IFVAL(MID$(RR$,X2+2,2))=1 THEN5060
5050  GOTO2540
5060 IFWE<>20 THEN5090
5070 IFP1=0 THENW1$="NOLI": GOTO2560
5080 IFP1=1 THENW1$="NOLITE": GOTO2560
5090 IFWE<>21 THENW1$="NOLENS": GOTO2560
5100 W1$=WE$: GOTO2560
5110 IFVAL(MID$(RR$,X2,2))<50 THEN5140
5120  GOSUB4660
5130  GOTO2560
5140 IFVAL(MID$(RR$,X2+2,2))=1 THEN5160
5150  GOTO2540
5160 IFWE<>21 THEN5180
5170 W1$=WE$: GOTO2560
5180 W1$="VOLENS": GOTO2560
5190 IFVAL(MID$(RR$,X2,2))<50 THEN5250
5200  GOSUB4660
5210 IFWE<>0 THEN5240
5220 IF(P1=0) OR(P1=3) OR(P1=5) THEN5240
5230 W1$="MA"+W1$: GOTO2560
5240 W1$="MA"+RIGHT$(W1$,LEN(W1$)-2): GOTO2560
5250 IFVAL(MID$(RR$,X2+2,2))=1 THEN5270
5260  GOTO2540
5270 W1$=WE$: GOTO2560
5280 IFVAL(MID$(RR$,X2,2))<50 THEN5300
5290  GOSUB4660:W1$=A$+W1$: GOTO2560
5300 IFVAL(MID$(RR$,X2+2,2))=1 THEN5320
5310  GOTO2540
5320 IFWE<>21 THEN5340
5330 W1$=WE$: GOTO2560
5340 IFWE<>20 THEN5380
5350 W1$=A$+"FER"
5360 IFP1=1 THENW1$=W1$+"TE"
5370  GOTO2560
5380 IFWE<>24 THEN5400
5390 W1$=A$+"FERRI": GOTO2560
5400 IFWE<>13 THEN5420
5410 W1$=A$+"FERRE"+UI$(0,P1): GOTO2560
5420 W1$=A$+"FERRE"+UI$(2,P1): GOTO2560
5430 IFVAL(MID$(RR$,X2,2))<50 THEN5450
5440  GOSUB4660:W1$=A$+W1$: GOTO2560
5450 IFVAL(MID$(RR$,X2+2,2))=1 THEN5470
5460  GOTO2540
5470 IFWE>19 THEN5500
5480 IFP1=2 THEN5500
5490 W1$="   -": GOTO2560
5500 W1$=A$+AF$(201+WE,1): GOSUB5510: GOTO2560
5510 IF(WE=20) AND(P1=1) THENW1$=W1$+"TE"
5520 RETURN
5530 IFVAL(MID$(RR$,X2+2,2))<>0 THEN5550
5540 CJ=4:ST$(0)=A$+"FAC": GOTO2540
5550 IF(WE=20) OR(WE=24) THEN5650
5560 CJ=2:IF(WE=6) AND(P1=5) THENST$(0)="FIU": GOTO5620
5570 IFWE=6 THENST$(0)="FI": GOTO5620
5580 IFWE=7 THENST$(0)="FIEBA": GOTO5620
5590 IF((WE=10) AND(P1=0)) OR(WE=16) THENST$(0)="FIA": GOTO5620
5600 IFWE=10 THENST$(0)="FIE": GOTO5620
5610 ST$(0)="FIERE"
5620 IFWE=6 THEN5640
5630 W1$=A$+ST$(0)+UI$(0,P1): GOTO2560
5640 W1$=A$+ST$(0)+UI$(1,P1): GOTO2560
5650 IFWE<>20 THEN5690
5660 W1$=A$+"FAC"
5670 IFP1=1 THENW1$=W1$+"ITE"
5680  GOTO2560
5690 W1$=A$+"FIERI": GOTO2560
5700  GOSUB100:PRINT"THIS EXTENDED VERSION ENABLES YOU"
5710 PRINT"TO CONJUGATE REGULAR & IRREGULAR LATIN"
5715 PRINT"VERBS IN ALL TENSES,MODES AND VOICES"
5720 PRINT"THIS PROGRAM INCLUDES A TEST.":PRINT
5740 PRINT"IN ORDER TO PRINT THE RESULTS"
5745 PRINT"ON A PRINTER ADD"
5750 PRINT"LINE NR. 2375 PRINTER-ON COMMAND"
5760 PRINT"LINE NR. 2665 PRINTER-OFF COMMAND":PRINT
5780 PRINT"FOR THOSE WHO HAVE NO KNOWLEDGE"
5785 PRINT"OF LATIN , PLEASE TRY THE VERBS:"
5790 PRINT"AUDIRE,MALLE,ESSE,VETARE,MORI,CAPERE"
5800 PRINT:PRINT"<ENTER KEY>";: GOSUB210: GOTO6090
5810 IFP1=0 THENW1$=W1$+"RE": GOTO2640
5820 W1$=LEFT$(W1$,LEN(W1$)-2)+"MINI": GOTO2640
5830  GOSUB100: GOSUB7070
5840 PRINT"1 TO COME":PRINT"2 TO BE SOLD"
5850  GOSUB1470:X=VAL(IN$)
5860 IF(X-1)*(X-2)<>0 THEN5830
5870 RETURN
5880 IFP1=0 THENW1$="MEMENTO": GOTO2640
5890 W1$="MEMENTOTE": GOTO2640
5900 IFP1=0 THENW1$=W1$+"TO": GOTO2640
5910 W1$=LEFT$(W1$,LEN(W1$)-2)+"TOTE": GOTO2640
5920 IF(RIGHT$(W1$,1)="E") AND(LEN(W1$)=14) THENW1$="": GOTO5970
5930 IFLEN(W1$)=9 THENW1$="": GOTO5970
5940 X=LEN(W1$)
5950 IFRIGHT$(W1$)="E" THENW1$=LEFT$(W1$,X-14): GOTO5970
5960 W1$=LEFT$(W1$,X-9)
5970 IFWE=22 THENW1$=W1$+"MORITURUS ESSE": GOTO2640
5980 W1$=W1$+"MORITURUS": GOTO2640
5990 IFLEFT$(WE$,5)="MEMIN" THEN6020
6000 IFRIGHT$(WE$,5)="SCIRE" THEN6020
6010 RETURN
6020 PRINT" (FUTURE)";:RETURN
6030 IFLEN(W1$)=4 THEN6060
6040 W1$=LEFT$(W1$,LEN(W1$)-4)
6050  GOTO6070
6060 W1$=""
6070 IFRIGHT$(WE$,6)="DUCERE" THENW1$=W1$+"DUC": GOTO2640
6080 W1$=W1$+"DIC": GOTO2640
6090  GOSUB1020
6100 IFXD>0 THEN3900
6110  GOSUB1560
6120  GOSUB1580
6130  GOSUB2050
6140  GOSUB1490
6150 IFL=2 THEN GOSUB3120
6160 IF(XD>0) AND(L=2) THEN4540
6170  GOSUB2850
6180  GOSUB100
6190 IFXD>0 THENXX=1: GOSUB6240: GOTO2360
6200 IFL=2 THEN2360
6210 IFPA=0 THENWE=0: GOTO2360
6220 WE=6: GOTO2360
6230  GOTO2710
6240 IFL=1 THENWE=0
6250 RETURN
6260 VE=10:HO=9: GOSUB110:PRINT"LATIN VERB CONJUGATION"
6265 W1$="                  "
6266 VE=12:HO=11: GOSUB110:PRINT"( EXTENDED VERSION )"
6270 VE=0:FORI=0 TO80:HO=I: GOSUB110:PRINT" ";
6280  GOSUB120:IFVE<>0 THENHH=I+1:I=80
6290 NEXTI:FORI=0 TO80:HO=0:VE=I: GOSUB110:PRINT" ";
6300  GOSUB120:IFVE<>I THENVV=I:I=80
6310 NEXTI:VE=VV-2:HO=0: GOSUB110
6320 PRINT"BE PATIENT , DATA LOADING"
6330 PRINT"THIS MAY TAKE 1 MINUTE";
6340 DIMBI$(30,29),UI(30),ST(30),NA$(30),NM$(13),UI$(15,5)
6350 DIMAF$(239,2),AF(239,1),ST$(2),S1(1,3),S2(1,3),KL$(15)
6360 DIMKK(2),XD$(9),VV$(16),FE$(7,2)
6370 Z=1
6380 FORX=0 TO30
6390 IFX>20 THENZ=6
6400 FORY=0 TO29 STEPZ
6410 READBI$(X,Y)
6420 IFVAL(BI$(X,Y))=0 THEN6480
6430 X1=VAL(BI$(X,Y))
6440 READBI$(X,Y):X2=1
6450 BI$(X,Y+1)=BI$(X,Y)
6460 Y=Y+1:X2=X2+1
6470 IFX2<>X1 THEN6450
6480 NEXTY
6490 READUI(X),ST(X),NA$(X)
6500 NEXTX
6510 FORX=1 TO5
6520 FORY=25 TO30
6530 READBI$(Y,X)
6540 NEXTY:NEXTX
6550 FORX=7 TO11:FORY=25 TO30
6560 READBI$(Y,X)
6570 NEXTY:NEXTX
6580 FORX=13 TO17:FORY=25 TO30
6590 READBI$(Y,X):NEXTY:NEXTX
6600 FORX=19 TO21:FORY=25 TO30
6610 READBI$(Y,X):NEXTY:NEXTX
6620 FORX=0 TO13:READNM$(X):NEXTX
6630 FORX=0 TO206
6640 FORY=0 TO2
6650 READAF$(X,Y)
6660 IFY=2 THEN6680
6670 READAF(X,Y)
6680 NEXTY:NEXTX
6690 FORX=0 TO1:FORY=0 TO3
6700 READS1(X,Y),S2(X,Y)
6710 NEXTY:NEXTX
6720 Z=5
6730 FORX=0 TO15
6740 FORY=0 TOZ
6750 READUI$(X,Y)
6760 NEXTY
6770 IFX=4 THENZ=1
6780 IFX=5 THENZ=0
6790 NEXTX
6800 FORX=0 TO13:READKL$(X):NEXTX
6810 FORX=1 TO9
6820 READXD$(X)
6830 NEXTX
6840 FORX=0 TO15:READVV$(X):NEXTX
6850 FORX=0 TO7:FORY=0 TO2
6860 READFE$(X,Y):NEXTY:NEXTX
6870 FORX=6 TO30
6880 IFX=12 THENX=16: GOTO6910
6890 IFX=25 THENX=27: GOTO6910
6900 IFX=29 THENX=30
6910 READAF$(201+X,1):NEXTX
6920 FORX=207 TO239
6930 FORY=0 TO2 STEP2
6940 READAF$(X,Y):IFY=2 THEN6960
6950 READAF(X,Y+1)
6960 NEXTY:NEXTX
6970  GOTO5700
6980 IFRIGHT$(WE$,6)<>"SERERE" THEN7010
6990  GOSUB100: GOSUB7070:PRINT"1 TO SOW"
7000 PRINT"2 TO STRING": GOTO7080
7010 IFWE$<>"EDERE" THEN7040
7020  GOSUB100: GOSUB7070:PRINT"1 TO EAT"
7030 PRINT"2 TO EDIT": GOTO7080
7040 IFRIGHT$(WE$,7)<>"TINGERE" THENRETURN
7050  GOSUB100: GOSUB7070:PRINT"1 TO TOUCH"
7060 PRINT"2 TO DIP, TO DYE": GOTO7080
7070 PRINT"MAKE A CHOICE :":PRINT:RETURN
7080  GOSUB1470:HS=VAL(IN$)-1
7090 IFHS*(HS-1)<>0 THEN6980
7100 RETURN
7110 W1$=LEFT$(ST$(2),LEN(ST$(2))-3)
7120 ST$(2)=W1$+"SIT":RETURN
7130 DATA"","5","A","6","E","","4","I","U","5"
7140 DATA"I","IU","5","I","IU",1,0
7150 DATA"06040000"
7160 DATA"6","ABA","12","EBA","12","IEBA"
7170 DATA0,0,"07040000"
7180 DATA"6","AV","6","U","6","","6","IV","6"
7190 DATA"",3,1
7200 DATA"08040000"
7210 DATA"6","AVERA","6","UERA","6","ERA"
7220 DATA"6","IVERA","6","ERA",0,1
7230 DATA"09040000"
7240 DATA"AB","4","ABI","ABU","EB","4","EBI"
7250 DATA"EBU","A","5","E","IA","5","IE","IA","5","IE",1
7260 DATA0,"10040000"
7270 DATA"AVER","5","AVERI","UER","5","UERI"
7280 DATA"ER","5","ERI","IVER","5","IVERI","ER","5"
7290 DATA"ERI",1,1,"11040000"
7300 DATA"O","5","A","EO","5","E","O","E","3","I"
7310 DATA"U","IO","4","I","IU","IO","E","3","I","IU",2,0
7320 DATA"06050001"
7330 DATA"6","ABA","12","EBA","12","IEBA"
7340 DATA2,0,"07050001"
7350 DATA"6","AT","6","IT","6","","6","IT","6"
7360 DATA"",4,2,"08050011"
7370 DATA"6","AT","6","IT","6","","6","IT","6"
7380 DATA"",4,2,"09050021"
7390 DATA"ABO","ABE","3","ABI","ABU","EBO"
7400 DATA"EBE","3","EBI","EBU","A","5","E","IA","5","IE"
7410 DATA"IA","5","IE",2,0,"10050001"
7420 DATA"6","AT","6","IT","6","","6","IT","6"
7430 DATA"",4,2,"11050031"
7440 DATA"6","E","6","EA","6","A","12","IA",0
7450 DATA0,"06040100"
7460 DATA"6","ARE","12","ERE","6","IRE","6"
7470 DATA"ERE",0,0,"07040100"
7480 DATA"6","AVERI","6","UERI","6","ERI"
7490 DATA"6","IVERI","6","ERI",0,1
7500 DATA"08040100"
7510 DATA"6","AVISSE","6","UISSE","6","ISSE"
7520 DATA"6","IVISSE","6","ISSE",0,1,"09040100"
7530 DATA"6","E","6","EA","6","A","12","IA",2
7540 DATA0,"06050101"
7550 DATA"6","ARE","12","ERE","6","IRE","6"
7560 DATA"ERE",2,0,"07050101"
7570 DATA"6","AT","6","IT","6","","6","IT","6"
7580 DATA"",4,2,"08050141"
7590 DATA"6","AT","6","IT","6","","6","IT","6"
7600 DATA"",4,2,"09050151"
7610 DATA"A","A","4","Q","E","E","4","Q","E","I","4"
7620 DATA"Q","I","I","4","Q","E","I","4","Q",5,0
7630 DATA"02040601"
7640 DATA"A","E","E","I","E",6,0
7650 DATA"03040600"
7660 DATA"AT","IT","","IT","",7,2
7670 DATA"03041001"
7680 DATA"AV","U","","IV","",8,1
7690 DATA"03040800"
7700 DATA"AR","ER","","IR","",9,0
7710 DATA"03050601"
7720 DATA"AT","IT","","IT","",10,2
7730 DATA"03051000"
7740 DATA"AT","IT","","IT","",11,2
7750 DATA"03050801"
7760 DATA"A","E","E","IE","IE",12,0
7770 DATA"12040601"
7780 DATA"AT","IT","","IT","",13,2
7790 DATA"12041001"
7800 DATA"AT","IT","","IT","",14,2
7810 DATA"12050801"
7820 DATA"A","E","E","IE","IE",15,0
7830 DATA"1301"
7840 DATA"SUM","ES","EST","SUMUS","ESTIS","SUNT"
7850 DATA"ERAM","ERAS","ERAT","ERAMUS","ERATIS","ERANT"
7860 DATA"ERO","ERIS","ERIT","ERIMUS","ERITIS","ERUNT"
7870 DATA"SIM","SIS","SIT","SIMUS","SITIS","SINT"
7880 DATA"ESSEM","ESSES","ESSET","ESSEMUS"
7890 DATA"ESSETIS","ESSENT"
7900 DATA"VOLO","VIS","VULT","VOLUMUS","VULTIS","VOLUNT"
7910 DATA"VOLEBAM","VOLEBAS","VOLEBAT"
7920 DATA"VOLEBAMUS","VOLEBATIS","VOLEBANT"
7930 DATA"VOLAM","VOLES","VOLET"
7940 DATA"VOLEMUS","VOLETIS","VOLENT"
7950 DATA"VELIM","VELIS","VELIT"
7960 DATA"VELIMUS","VELITIS","VELINT"
7970 DATA"VELLEM","VELLES","VELLET"
7980 DATA"VELLEMUS","VELLETIS","VELLENT"
7990 DATA"FERO","FERS","FERT","FERIMUS","FERTIS","FERUNT"
8000 DATA"FEROR","FERRIS","FERTUR","FERIMUR"
8010 DATA"FERIMINI","FERUNTUR"
8020 DATA"EO","IS","IT","IMUS","ITIS","EUNT"
8030 DATA"IBAM","IBAS","IBAT","IBAMUS","IBATIS","IBANT"
8040 DATA"IBO","IBIS","IBIT","IBIMUS","IBITIS","IBUNT"
8050 DATA"EAM","EAS","EAT","EAMUS","EATIS","EANT"
8060 DATA"IREM","IRES","IRET","IREMUS","IRETIS","IRENT"
8070 DATA"ISSEM","ISSES","ISSET"
8080 DATA"ISSEMUS","ISSETIS","ISSENT"
8090 DATA"INDICATIVE","SUBJUNCTIVE","IMPERATIVE"
8100 DATA"INFINITIVE","ACTIVE","PASSIVE","PRESENT"
8110 DATA"IMPERFECT","PERFECT","PLUPERFECT"
8120 DATA"FUTURE","FUTURE PERFECT"
8130 DATA"PARTICIPLE","GERUNDIVE"
8140 DATA"PRAEST",2,"PRAESTIT",0,"PRAEST"
8150 DATA"CREP",1,"CREP",3,"CREP"
8160 DATA"IUV",2,"IUV",2,"IUT"
8170 DATA"SEC",1,"SEC",2,"SECT"
8180 DATA"VET",1,"VET",3,"VET"
8190 DATA"LAV",0,"L",2,"LAUT"
8200 DATA"CUB",1,"CUB",3,"CUB"
8210 DATA"ST",2,"STET",0,"ST"
8220 DATA"D",2,"DED",0,"D"
8230 DATA"RESPOND",1,"RESPOND",1,"RESPONS"
8240 DATA"SPOND",1,"SPOPOND",1,"SPONS"
8250 DATA"MORD2",1,"MOMORD",1,"MORS"
8260 DATA"TORR",0,"TORR",1,"TOST"
8270 DATA"PEND2",1,"PEPEND",1,"PENS"
8280 DATA"SUAD",1,"SUAS",1,"SUAS"
8290 DATA"CENS",0,"CENS",1,"CENS"
8300 DATA"GAUD",0,"-",1,"GAVIS"
8310 DATA"MISC",0,"MISC",1,"MIXT"
8320 DATA"FULG",1,"FULS",1,"FULS"
8330 DATA"MULC",1,"MULS",1,"MULS"
8340 DATA"TERG",1,"TERS",1,"TERS"
8350 DATA"HAER",1,"HAES",1,"HAES"
8360 DATA"DEL",1,"DELEV",1,"DELET"
8370 DATA"SID",1,"SED",1,"SESS"
8380 DATA"TEN",0,"TEN",1,"TENT"
8390 DATA"VID",1,"VID",1,"VIS"
8400 DATA"MOV",1,"MOV",1,"MOT"
8410 DATA"MAN",1,"MANS",1,"MANS"
8420 DATA"FAV",-1,"F",1,"FOT"
8430 DATA"AUD",0,"-",1,"AUS"
8440 DATA"VOV",1,"VOV",1,"VOT"
8450 DATA"IAC",0,"IAC",0,"-"
8460 DATA"TIN",0,"TIN",1,"TENT"
8470 DATA"IUB",1,"IUSS",1,"IUSS"
8480 DATA"CAV",1,"CAV",1,"CAUT"
8490 DATA"SED",1,"SED",1,"SESS"
8500 DATA"AUG",1,"AUX",1,"AUCT"
8510 DATA"ARD",1,"ARS",1,"ARS"
8520 DATA"RID",1,"RIS",1,"RIS"
8530 DATA"DOC",0,"DOC",1,"DOCT"
8540 DATA"URG",1,"URS",1,"URS"
8550 DATA"PL",1,"PLEV",1,"PLET"
8560 DATA"FL",1,"FLEV",1,"FLET"
8570 DATA"OL",1,"OLEV",1,"OLET"
8580 DATA"INTELLEG",0,"INTELLEX",0,"INTELLECT"
8590 DATA"COGNOSC",0,"COGNOV",1,"COGN"
8600 DATA"QUIESC",0,"QUIEV",0,"QUIET"
8610 DATA"STINGU",0,"STINX",0,"STINCT"
8620 DATA"STRING",0,"STRINX",0,"STRICT"
8630 DATA"DILIG",0,"DILEX",0,"DILECT"
8640 DATA"STITU",0,"STITU",0,"STITUT"
8650 DATA"FRANG",0,"FREG",0,"FRACT"
8660 DATA"FRING",0,"FREG",0,"FRACT"
8670 DATA"CLAUD",0,"CLAUS",0,"CLAUS"
8680 DATA"SCISC",1,"SC",1,"SC"
8690 DATA"SPERN",0,"SPREV",0,"SPREV"
8700 DATA"STERN",-2,"STR",-2,"STR"
8710 DATA"SUESC",0,"SUEV",0,"SUET"
8720 DATA"SCRIB",0,"SCRIPS",0,"SCRIPT"
8730 DATA"TRIBU",0,"TRIBU",0,"TRIBUT"
8740 DATA"SCIND",0,"SCID",0,"SCISS"
8750 DATA"PANG2",0,"PEPIG",0,"PACT"
8760 DATA"CRESC",0,"CREV",0,"CRET"
8770 DATA"QUAER",1,"QUAES",1,"QUAES"
8780 DATA"LINQU",0,"LIQU",0,"LICT"
8790 DATA"STATU",0,"STATU",0,"STATUT"
8800 DATA"SPERG",0,"SPERS",0,"SPERS"
8810 DATA"CUMB",-1,"CUB",1,"CUB"
8820 DATA"VINC",0,"VIC",0,"VICT"
8830 DATA"CARP",0,"CARPS",0,"CARPT"
8840 DATA"MITT",0,"MIS",0,"MISS"
8850 DATA"TEND2",0,"TETEND",0,"TENT"
8860 DATA"CAED2",0,"CECID",0,"CAES"
8870 DATA"FEND",0,"FEND",0,"FENS"
8880 DATA"DISC2",0,"DIDIC",0,"-"
8890 DATA"CEND",0,"CEND",0,"CENS"
8900 DATA"PELL2",0,"PEPUL",0,"PULS"
8910 DATA"CRED",0,"CREDID",1,"CRED"
8920 DATA"IUNG",0,"IUNX",0,"IUNCT"
8930 DATA"SOLV",0,"SOLV",0,"SOLUT"
8940 DATA"CERN",0,"CREV",0,"CRET"
8950 DATA"CESS",1,"CESS",0,"-","COEP",0,"COEP",0,"COEPT"
8960 DATA"CURR2",0,"CUCURR",0,"CURS"
8970 DATA"FALL2",0,"FEFELL",0,"FALS"
8980 DATA"FING",0,"FINX",0,"FICT"
8990 DATA"FREM",-1,"FREM",1,"FREM"
9000 DATA"FUND",0,"FUD",0,"FUS"
9010 DATA"GIGN",-1,"GEN",-1,"GEN"
9020 DATA"LAED",0,"LAES",0,"LAES"
9030 DATA"PARC2",0,"PEPERC",0,"PARS"
9040 DATA"POSC2",0,"POPOSC",-2,"POST"
9050 DATA"PRIM",0,"PRESS",0,"PRESS"
9060 DATA"PREM",0,"PRESS",0,"PRESS"
9070 DATA"PROM",0,"PROMPS",0,"PROMPT"
9080 DATA"RUMP",0,"RUP",0,"RUPT"
9090 DATA"STRU",0,"STRUX",0,"STRUCT"
9100 DATA"TANG2",0,"TETIG",0,"TACT"
9110 DATA"TING",0,"TIG",0,"TACT"
9120 DATA"TING",0,"TINX",0,"TINCT"
9130 DATA"TOLL",0,"SUSTUL",-2,"SUBL"
9140 DATA"TRAH",0,"TRAX",0,"TRACT"
9150 DATA"VELL",0,"VELL",0,"VULS"
9160 DATA"VEND",0,"VENDID",1,"VEND"
9170 DATA"VERT",0,"VERT",0,"VERS"
9180 DATA"QUAT1",0,"-",0,"QUASS"
9190 DATA"CERP",0,"CERPS",0,"CERPT"
9200 DATA"SPIC1",0,"SPEX",0,"SPECT"
9210 DATA"TREP",-1,"TREP",0,"-"
9220 DATA"CING",0,"CINX",0,"CINCT"
9230 DATA"SIST",0,"STIT",0,"STIT"
9240 DATA"TRUD",0,"TRUD",0,"TRUS"
9250 DATA"QUIR",1,"QUIS",1,"QUIS"
9260 DATA"FLIG",0,"FLIX",0,"FLICT"
9270 DATA"NECT",-1,"NEX",0,"NEX"
9280 DATA"CAP1",0,"CEP",0,"CAPT"
9290 DATA"DIC",0,"DIX",0,"DICT"
9300 DATA"DUC",0,"DUX",0,"DUCT"
9310 DATA"LUD",0,"LUS",0,"LUS"
9320 DATA"CAD2",0,"CECID",0,"CAS"
9330 DATA"CED",0,"CESS",0,"CESS"
9340 DATA"COG",0,"COEG",0,"COACT"
9350 DATA"VID",0,"VIS",0,"VIS"
9360 DATA"LEG",0,"LEG",0,"LECT"
9370 DATA"PET",1,"PET",1,"PET"
9380 DATA"VIS",0,"VIS",0,"VIS"
9390 DATA"FLU",0,"FLUX",0,"FLUCT"
9400 DATA"CID",0,"CID",0,"CIS"
9410 DATA"PON",-1,"POS",-1,"POS"
9420 DATA"VIV",0,"VIX",0,"VICT"
9430 DATA"COM",0,"COMPS",0,"COMPT"
9440 DATA"VAD",0,"VAS",0,"VAS"
9450 DATA"FIC1",0,"FEC",0,"FECT"
9460 DATA"FIG",0,"FIX",0,"FIX"
9470 DATA"GER",0,"GESS",0,"GEST"
9480 DATA"NUB",0,"NUPS",0,"NUPT"
9490 DATA"CID",0,"CID",0,"CIS"
9500 DATA"CUL",-1,"CUL",0,"CULT"
9510 DATA"RAD",0,"RAS",0,"RAS"
9520 DATA"SIN",1,"S",1,"S"
9530 DATA"SUM",0,"SUMPS",0,"SUMPT"
9540 DATA"TEG",0,"TEX",0,"TECT"
9550 DATA"TER",1,"TR",1,"TR","FID",0,"-",0,"FIS"
9560 DATA"TEX",-1,"TEX",0,"TEXT"
9570 DATA"VEH",0,"VEX",0,"VECT"
9580 DATA"REG",0,"REX",0,"RECT"
9590 DATA"CIP1",0,"CEP",0,"CEPT"
9600 DATA"CUP1",-1,"CUP",-1,"CUP"
9610 DATA"FAC1",0,"FEC",0,"FACT"
9620 DATA"FOD1",0,"FOD",0,"FOSS"
9630 DATA"FUG1",0,"FUG",-1,"FUG"
9640 DATA"IAC1",0,"IEC",0,"IACT"
9650 DATA"LIC1",0,"LEX",0,"LECT"
9660 DATA"PAR3",0,"PEPER",0,"PART"
9670 DATA"CUT1",0,"CUSS",0,"CUSS"
9680 DATA"RAP1",-3,"RAP",0,"RAPT"
9690 DATA"RIP1",-3,"RIP",0,"REPT"
9700 DATA"SAP1",0,"SAPI",0,"-"
9710 DATA"COL",-1,"COL",0,"CULT"
9720 DATA"CUD",0,"CUD",0,"CUS"
9730 DATA"ESC",-1,"",1,""
9740 DATA"BIB",0,"BIB",-2,"POT"
9750 DATA"LID",0,"LIS",0,"LIS"
9760 DATA"LIN",0,"LINX",0,"LINCT"
9770 DATA"LIG",0,"LEG",0,"LECT"
9780 DATA"LUD",0,"LUS",0,"LUS"
9790 DATA"REP",0,"REPS",0,"-"
9800 DATA"SUG",0,"SUX",0,"SUCT"
9810 DATA"OSC",0,"OV",0,"OT"
9820 DATA"DEM",0,"DEMPS",0,"DEMPT"
9830 DATA"RIG",0,"REX",0,"RECT"
9840 DATA"SER",0,"SEV",0,"SAT"
9850 DATA"SER",-1,"SER",0,"SERT"
9860 DATA"SID",0,"SED",0,"SESS"
9870 DATA"CAN2",0,"CECIN",-2,"CANT"
9880 DATA"ESS",1,"S",1,"S"
9890 DATA"IC1",0,"IEC",0,"IECT"
9900 DATA"EM",0,"EM",0,"EMPT"
9910 DATA"ED",0,"ED",0,"ES"
9920 DATA"ED",0,"EDID",1,"ED"
9930 DATA"AG",0,"EG",0,"ACT"
9940 DATA"AL",-1,"AL",0,"ALT"
9950 DATA"RG",0,"REX",0,"RECT"
9960 DATA"IG",0,"EG",0,"ACT"
9970 DATA"LU",-1,"L",0,"LUIT"
9980 DATA"IM",0,"EM",0,"EMPT"
9990 DATA"UR",0,"USS",0,"UST"
10000 DATA"SU",-1,"S",0,"SUT","OD",0,"OD",0,"-"
10010 DATA"D",0,"DID",1,"D"
10020 DATA"U",0,"U",0,"UT"
10030 DATA"COMPER",-1,"COMPER",-1,"COMPERT"
10040 DATA"REPER",-1,"REPPER",-1,"REPERT"
10050 DATA"SEPEL",0,"SEPEL",-1,"SEPULT"
10060 DATA"APER",-2,"APER",-1,"APERT"
10070 DATA"OPER",-2,"OPER",-1,"OPERT"
10080 DATA"HAUR",-1,"HAUS",-1,"HAUST"
10090 DATA"SANC",-1,"SANX",-1,"SANCT"
10100 DATA"VINC",-1,"VINX",-1,"VINCT"
10110 DATA"SENT",-1,"SENS",-1,"SENS"
10120 DATA"SAEP",-1,"SAEPS",-1,"SAEPT"
10130 DATA"FARC",-1,"FARS",-1,"FART"
10140 DATA"FERC",-1,"FERS",-1,"FERT"
10150 DATA"VEN",-1,"VEN",-1,"VENT"
10160 DATA"SAL",-2,"SAL",-1,"-"
10170 DATA"SIL",-2,"SIL",-1,"-"
10180 DATA0,8,9,43,44,191,192,206
10190 DATA0,0,207,211,212,235,236,239
10200 DATA"M","S","T","MUS","TIS","NT"
10210 DATA"O","S","T","MUS","TIS","NT"
10220 DATA"R","RIS","TUR","MUR","MINI","NTUR"
10230 DATA"I","ISTI","IT","IMUS","ISTIS","ERUNT"
10240 DATA"US","US","US","I","I","I"
10250 DATA"","TE","RE","URUS ESSE","ISSE","I"
10260 DATA"UM IRI","US ESSE","NS","URUS","US","NDUS"
10270 DATA"21","321","0","31","31","321","0","31"
10280 DATA"31","31","31","31","32","32"
10290 DATA"ESSE","POSSE","NOLLE","VELLE"
10300 DATA"MALLE","FERRE","IRE"
10310 DATA"FACERE","FIERI"
10320 DATA"CIRCUM","AB","AD","INTRO","SUB","TRANS","CO"
10330 DATA"EX","PRAETER","OB","RED","PROD"
10340 DATA"VEN","PER","INTER","IN"
10350 DATA"AF","AT","AL","AU","ABS","AB","CON","CON","COL"
10360 DATA"DIF","DIS","DI","EF","EX","E","IN","IN","IL"
10370 DATA"OF","OB","OB","RE","RET","RE"
10380 DATA"ITUR","IBATUR","ITUM EST","ITUM ERAT"
10390 DATA"IBITUR","ITUM ERIT","EATUR","IRETUR"
10400 DATA"ITUM SIT","ITUM ESSET"
10410 DATA"I","IRE","ITURUS ESSE","ISSE"
10420 DATA"IRI","IENS","ITURUS","EUNDUS"
10430 DATA"FAT",1,"FASS","FIT",1,"FESS","VER",0,"VER"
10440 DATA"VID",1,"VIS","R",1,"RAT"
10450 DATA"PROFICISC",0,"PROFECT","OBLIVISC",0,"OBLIT"
10460 DATA"REMINISC",0,"RECORDAT","ADIPISC",0,"ADEPT"
10470 DATA"AMPLECT",0,"AMPLEX","NANCISC",0,"NACT"
10480 DATA"PACISC",0,"PACT","ULCISC",0,"ULT"
10490 DATA"IRASC",0,"IRAT","FUNG",0,"FUNCT"
10500 DATA"QUER",0,"QUEST","SEQU",0,"SECUT"
10510 DATA"GRAD1",0,"GRESS","GRED1",0,"GRESS"
10520 DATA"LOQU",0,"LOCUT","NASC",0,"NAT"
10530 DATA"LAB",0,"LAPS","NIT",0,"NIS"
10540 DATA"MOR1",0,"MORTU","PAT1",0,"PASS"
10550 DATA"FRU",0,"FRUCT","VEH",0,"VECT"
10560 DATA"PET1",0,"PESS","UT",0,"US"
10570 DATA"PER",-1,"PERT","MET",-1,"MENS"
10580 DATA"ORD",-1,"ORS","OR",-1,"ORT"
30000 REM ITT 2020,AUTUMN 83
30010 REM PIETER VD OUDENRIJN
30020 REM NARCISLAAN 241
30030 REM 2555 LM DEN HAAG
30040 REM EXTENDED VERSION FOR
30050 REM REGULAR & IRREGULAR VERBS
30060 REM COPYRIGHT NOS HILVERSUM

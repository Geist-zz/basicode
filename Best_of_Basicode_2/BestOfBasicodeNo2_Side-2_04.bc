
1000 A=5000:GOTO20:REM SNAKEYGAME
1010 DIMPU(10)
1020 GOSUB100
1030 PRINT"how many lines on screen"
1040 INPUTAV
1050 PRINT"how many characters per line"
1060 INPUTAH
1070 AV=AV-2:AH=AH-2
1080 IFAV<1 ORAH<1 THEN1020
1090 Z$="":IN$="":LX=0:LY=0
1100 AG=AH*AV/20
1110 DIMSC$(39,24),TX(255),TY(255)
1120 DIMCS(255),AX(10),AY(10),NA$(10)
1130 FORI=1 TO3:READA$:CS(ASC(A$))=6+I
1140 NEXTI
1150 FORI=1 TO3:READA$:CS(ASC(A$))=3+I
1160 NEXTI
1170 FORI=1 TO3:READA$:CS(ASC(A$))=I
1180 NEXTI
1190 FORI=1 TO9:READAX(I):READAY(I):NEXTI
1200 GOSUB100:GOTO2050
1210 LX=0:LY=0
1220 GOSUB100
1230 PRINT"please wait a moment"
1240 PRINT"..."
1250 FORI=0 TO39:FORL=0 TO24:SC$(I,L)=""
1260 NEXTL:NEXTI
1270 TX=0:TY=0:GOSUB100:FORHO=0 TOAH-3
1280 PRINT"-";:SC$(HO,0)="-":NEXTHO
1290 HO=0:VE=1:GOSUB110:FORVE=0 TOAV-4
1300 PRINT"i":SC$(HO,VE+1)="i":NEXTVE
1310 HO=AH-3:FORVE=1 TOAV-3:GOSUB110:PRINT"i";
1330 SC$(AH-3,VE)="i":NEXTVE
1340 HO=0:VE=AV-3:GOSUB110
1350 FORHO=0 TOAH-3
1360 PRINT"-";:SC$(HO,VE)="-":NEXTHO
1370 FORI=1 TOAG
1380 GOSUB260:FX=RV*(AH-4)+1
1390 GOSUB260:FY=RV*(AV-4)+1
1400 IFSC$(FX,FY)<>"" THEN1380
1410 SC$(FX,FY)="$":HO=FX:VE=FY
1420 GOSUB110:PRINT"$";:NEXTI
1430 FORI=1 TOAG
1440 GOSUB260:FX=RV*(AH-4)+1
1450 GOSUB260:FY=RV*(AV-4)+1
1460 IFSC$(FX,FY)<>"" THEN1440
1470 SC$(FX,FY)="%":HO=FX:VE=FY
1480 GOSUB110:PRINT"%";:NEXTI
1490 LA=1:PX=AH/2:PY=AV/2
1500 Z9=1:TX(Z9)=PX:TY(Z9)=PY
1510 SC$(PX,PY)="o":HO=PX:VE=PY
1520 GOSUB110:PRINT"o";:FORI=1 TOSC:NEXTI
1530 GOSUB200:IFIN$="" THEN1570
1540 AF=ASC(IN$):AF=CS(AF)
1550 TX=AX(AF)
1560 TY=AY(AF)
1570 IFTX<>0 THEN1610
1580 IFTY<>0 THEN1610
1590 IF(LY=0) AND(LX=0) THEN1530
1600 TX=LX:TY=LY
1610 LX=TX:LY=TY
1620 PX=PX+TX:PY=PY+TY
1630 Z$=SC$(PX,PY)
1640 IFZ$="%" THEN1760
1650 IF(Z$="-") OR(Z$="i") THEN1850
1660 IFZ$="o" THEN1910
1670 Z9=Z9+1:IFZ9=60 THENZ9=1
1680 TX(Z9)=PX:TY(Z9)=PY
1690 L9=Z9-LA:IFL9<=0 THENL9=L9+59
1700 REM
1710 IFSC$(PX,PY)="$" THEN1820
1720 SC$(TX(L9),TY(L9))="":HO=TX(L9)
1730 VE=TY(L9):GOSUB110
1740 PRINT" ";
1750 GOTO1510
1760 REM POISON
1770 HO=PX:VE=PY:GOSUB110:PRINT"x";
1780 FORI=1 TO1000:NEXTI
1790 GOSUB100
1800 PRINT"snakey ate poision!!"
1810 GOTO1960
1820 REM SNAKEY BECOMES LONGER
1830 LA=LA+1
1840 GOTO1510
1850 REM WALL
1860 HO=PX:VE=PY:GOSUB110:PRINT"x";
1870 FORI=1 TO1000:NEXTI
1880 GOSUB100:PRINT"snakey"
1890 PRINT"hit his head at"
1900 PRINT"the wall !":GOTO1960
1910 REM BIT HIMSELF
1920 HO=PX:VE=PY:GOSUB110:PRINT"x";
1930 FORI=1 TO1000:NEXTI:GOSUB100
1940 PRINT"snakey bit himself !"
1950 GOTO1960
1960 PU(QW)=PU(QW)+LA:PRINT:PRINT
1970 PRINT"he died with"
1980 PRINT"the length of ";LA;" cm"
1990 PRINT:PRINT
2000 PRINT:PRINT:PRINT:PRINT"results:"
2010 PRINT"===========":PRINT:PRINT
2020 FORI=1 TOA4:PRINTNA$(I);TAB(15);":";
2030 PRINTTAB(25);PU(I):NEXTI
2040 GOTO2550
2050 PRINT"the snakeygame"
2060 PRINT"==============":PRINT:PRINT
2070 PRINT"programmed by thomas";
2080 PRINT:PRINT"hofmeister"
2090 PRINT
2100 PRINT"do you want the instructions? (y/n)"
2110 GOSUB200:A$=IN$:IFA$="" THEN2110
2120 IFA$="y" ORA$="Y" THENGOSUB2150:GOTO2390
2130 IFA$="n" ORA$="N" THEN2390
2140 GOTO2110
2150 GOSUB100
2160 RESTORE
2170 READA$
2180 IFA$<>"instructions:" THEN2170
2190 CX=0:CY=0
2200 READA$
2210 IFA$="***" THEN2640
2220 IFLEN(A$)>0 THEN2270
2230 PRINT"stop of instr. because there"
2240 PRINT"is a read error in the"
2250 PRINT"datas."
2260 GOTO2640
2270 FORI=1 TOLEN(A$)
2280 B$=MID$(A$,I,1)
2290 C$=C$+B$
2300 IFB$<>" " THENNEXTI:GOTO2200
2310 C1=CX+LEN(C$)
2320 IFC1<AH-2 THENCX=C1:GOTO2380
2330 IFC$="home " THENC$="":GOTO2350
2340 IFCY<AV-6 THEN2370
2350 GOSUB2600:GOSUB100:CX=LEN(C$):CY=0
2360 GOTO2380
2370 PRINT:PRINT:CY=CY+2:CX=LEN(C$)
2380 PRINTC$;:C$="":NEXTI:GOTO2200
2390 GOSUB100:PRINT"how many players"
2400 PRINT:PRINT"participate? (1..5)"
2410 INPUTA4
2420 A4=INT(A4)
2430 IF(A4>=1) AND(A4<=5) THEN2460
2440 PRINT"wrong entry!!"
2450 FORI=1 TO1000:NEXTI:GOTO2390
2460 REM
2470 PRINT:PRINT
2480 PRINT"please enter the"
2490 PRINT:PRINT"names of the"
2500 PRINT:PRINT"players:"
2510 FORI=1 TOA4:PRINT"player";I;":";:INPUTNA$(I):NEXTI
2520 PRINT"level of difficulty (1=high,100=low)"
2530 INPUTSC
2540 IF(SC<1) OR(SC>100) THEN2520
2550 QW=QW+1:IFQW>A4 THENQW=1
2560 PRINT:PRINTNA$(QW);" is up to play!"
2570 PRINT"press '!' to start"
2580 GOSUB200:IFIN$<>"!" THEN2580
2590 GOTO1210
2600 HO=0:VE=AV-2:GOSUB110
2610 PRINT"press a key":PRINT"to continue";
2620 GOSUB210
2630 RETURN
2640 GOSUB2600
2650 GOSUB100
2660 RESTORE
2670 FORI=1 TO3:PRINT
2680 FORL=1 TO3
2690 READA$:PRINTA$;"  ";
2700 NEXTL:NEXTI:GOSUB2600
2710 RETURN
25000 DATA"Q","W","E"
25010 DATA"A","S","D"
25020 DATA"Z","X","C"
25030 DATA-1,1,0,1,1,1,-1,0,0,0,1,0,-1
25040 DATA-1,0,-1,1,-1
25050 DATA"instructions:"
25060 DATA"you can move snakey (o) by those "
25070 DATA"letters which i will show you in a moment. "
25080 DATA"the letter in the middle "
25090 DATA"stands for the momentary "
25100 DATA"position of snakey. "
25110 DATA"pressing one of the other "
25120 DATA"shown keys will "
25130 DATA"make him wander into "
25140 DATA"that direction. "
25150 DATA"if snakey happens to "
25160 DATA"walk upon a '%', he "
25170 DATA"eats it and dies "
25180 DATA"because it is poison. "
25190 DATA"and when he walks upon "
25200 DATA"a '$' he will become "
25210 DATA"1 cm longer. "
25220 DATA"but do also be cautious "
25230 DATA"not to run against the "
25240 DATA"walls!! "
25250 DATA"thus, your task is to breed "
25260 DATA"up snakey and to make him "
25270 DATA"as long as possible. "
25280 DATA"each cm will be counted "
25290 DATA"as a point for you. "
25300 REM
25310 DATA"the keys you need are as follows: "
25320 DATA"(if you want to use other letters "
25330 DATA"then you have to "
25340 DATA"change the datas in lines 25000-25020 !!) "
25350 DATA"***"
25360 END
30000 REM PROGRAM WRITTEN ON A
30100 REM COMMODORE 64
30200 REM
30300 REM BY
30400 REM *************************
30500 REM *                       *
30600 REM *  THOMAS HOFMEISTER    *
30700 REM *                       *
30800 REM *  WITTENBERGSTR. 25    *
30900 REM *                       *
31000 REM *  4630 BOCHUM 7        *
31100 REM *                       *
31200 REM *  WEST - GERMANY       *
31300 REM *                       *
31400 REM *************************
31500 REM
31600 REM
31700 REM COPYRIGHT NOS  HILVERSUM


1000 A=100:GOTO20:REM TYPCURSUS
1010 GOSUB100:DIMA$(10)
1020 VE=5:HO=5:GOSUB110
1030 PRINT"T Y P E   K U R S U S"
1040 PRINT:PRINT:PRINT
1050 PRINT" Iedere regel die op het beeldscherm"
1060 PRINT"verschijnt moet blind worden overgetypt."
1070 PRINT:PRINT
1080 PRINT"Wilt u nog inleidende instrukties ?";
1090 GOSUB250:GOSUB210
1100 IF(IN$="J") OR(IN$="j") THENGOSUB10000
1110 GOSUB100
1120 VE=5:HO=2:GOSUB110
1130 PRINT"Wilt u HOOFDLETTERS (1 intoetsen)"
1140 PRINT"of kleine letters     (2 intoetsen)";
1150 GOSUB250:GOSUB210:H=0
1160 IFIN$="1" THENH=1:GOSUB7000
1170 IFIN$="2" THENH=2:GOSUB8000
1180 IFH=0 THEN1110
1190 GOSUB9000:REM GEEF HET MENU
1200 IFA=0 THENGOSUB11000:GOTO1190
1210 IFA<7 THENGOSUB2000:GOTO1190
1220 ONA-6 GOSUB3000,4000,5000
1230 GOTO1190
1240 REM EINDE VAN HET HOOFDPROGRAMMMA
2000 REM LES 1 T/M 6
2010 GOSUB100:PRINT:PRINT
2020 FORX=1 TO8:L$=MID$(A$(NL),X,1)
2030 PRINTL$;:GOSUB6000:NEXTX
2040 GOSUB9500
2050 PRINT
2060 FORX=1 TO8:L$=MID$(A$(NL),9-X,1)
2070 PRINTL$;:GOSUB6000:NEXTX
2080 GOSUB9500
2090 PRINT
2100 FORX=1 TO4:L$=MID$(A$(NL),X,1)
2110 PRINTL$;:GOSUB6000
2120 L$=MID$(A$(NL),X+4,1)
2130 PRINTL$;:GOSUB6000:NEXTX
2140 GOSUB9500
2150 PRINT
2160 FORX=1 TO4:L$=MID$(A$(NL),9-X,1)
2170 PRINTL$;:GOSUB6000
2180 L$=MID$(A$(NL),5-X,1)
2190 PRINTL$;:GOSUB6000:NEXTX
2200 GOSUB9500
2210 PRINT
2220 FORX=1 TO8
2230 GOSUB260:RV=INT(RV*8)+1
2240 L$=MID$(A$(NL),RV,1)
2250 PRINTL$;:GOSUB6000:NEXTX
2260 GOSUB9500
2270 GOSUB2500:RETURN
2500 REM MELDT DE FOUTEN
2510 PRINT:PRINT"totaal ";TF;" fout";
2520 IFTF<>1 THENPRINT"en";
2530 PRINT:GOSUB10900:RETURN
3000 REM DE LINKER HAND
3010 GOSUB100:PRINT:PRINT
3020 FORY=1 TO4:FORX=1 TO6
3030 L$=MID$(A$(X),5-Y,1)
3040 PRINTL$;:GOSUB6000:NEXTX
3050 GOSUB9500
3060 PRINT
3070 NEXTY
3080 GOSUB2500:RETURN
4000 REM DE RECHTER HAND
4010 GOSUB100:PRINT:PRINT
4020 FORY=5 TO8:FORX=1 TO6
4030 L$=MID$(A$(X),Y,1)
4040 PRINTL$;:GOSUB6000:NEXTX
4050 GOSUB9500
4060 PRINT
4070 NEXTY
4080 GOSUB2500:RETURN
5000 REM WILLEKEURIGE RIJEN EN HANDEN
5010 GOSUB100:PRINT:PRINT
5020 FORX=1 TO5:FORY=1 TO7
5030 GOSUB260:R1=INT(RV*8)+1
5040 GOSUB260:R2=INT(RV*6)+1
5050 L$=MID$(A$(R2),R1,1)
5060 PRINTL$;:GOSUB6000:NEXTY
5070 GOSUB9500
5080 PRINT:NEXTX:GOSUB2500:RETURN
6000 REM HAAL TOETS EN KONTROLEER
6010 GOSUB210
6020 IFIN$<>L$ THENGOSUB250:TF=TF+1
6030 PRINTIN$;"** ";
6040 RETURN
7000 REM VUL DE HOOFDLETTERS
7010 A$(1)="ASDFJKL;":A$(2)="QWERUIOP"
7020 A$(3)="ZXCVM,./":A$(4)="2345890:"
7030 A$(5)="TGB6YHN7":A$(6)="!#$%&'()"
7040 RETURN
8000 REM VUL DE kleine letters
8010 A$(1)="asdfjkl;":A$(2)="qweruiop"
8020 A$(3)="zxcvm,./":A$(4)="2345890:"
8030 A$(5)="tgb6yhn7":A$(6)="!#$%&'()"
8040 RETURN
9000 REM MENU-SUBROUTINE
9010 GOSUB100
9020 PRINT:PRINT
9030 PRINT"Geef de te behandelen les op;":PRINT
9040 PRINT"  Les 1;  a s d f   j k l ;"
9050 PRINT"  Les 2;  q w e r   u i o p"
9060 PRINT"  Les 3;  z x c v   m , . /"
9070 PRINT"  Les 4;  2 3 4 5   8 9 0 :"
9080 PRINT"  Les 5;  t g b 6   y h n 7"
9090 PRINT"  Les 6;  ! # $ %   & ' ( )"
9100 PRINT"  Les 7;  de linkerhand alleen"
9110 PRINT"  Les 8;  de rechterhand alleen"
9120 PRINT"  Les 9;  in willekeurige volgorde"
9130 PRINT:PRINT"*** Type 0 om het programma te verlaten"
9140 PRINT:PRINT"Welke les is aan de beurt";
9150 GOSUB210:A=ASC(IN$):A=A-48
9160 IF(A<0) OR(A>9) THENPRINT"(s.v.p. tussen 0 en 9)":GOTO9010
9170 NL=A:TF=0:RETURN
9500 REM TEKEN EEN KEURIGE LIJN
9510 PRINT:PRINT"========================================"
9520 RETURN
10000 REM INLEIDENDE INSTRUKTIES...
10010 GOSUB100
10020 PRINT:PRINT"De startpositie van de handen moet zijn:"
10030 PRINT:PRINT"Voor de linkerhand:"
10040 PRINT"De wijsvinger op de   F"
10050 PRINT"De middelvinger op de D"
10060 PRINT"De ringvinger op de   S"
10070 PRINT"De pink op de         A"
10080 PRINT:PRINT"Voor de rechterhand:"
10090 PRINT"De wijsvinger op de   J"
10100 PRINT"De middelvinger op de K"
10110 PRINT"De ringvinger op de   L"
10120 PRINT"De pink op de         ;"
10130 PRINT:PRINT
10140 PRINT"De beide duimen bedienen alleen de"
10150 PRINT"spatiebalk."
10160 PRINT:PRINT
10170 GOSUB10900
10180 PRINT:PRINT"Tussen de beide handen zitten telkens"
10190 PRINT"twee letters,"
10200 PRINT"waarbij de linkerpink steeds begint bij:"
10210 PRINT"de Z,A,Q,2"
10220 PRINT"en de rechterwijsvinger bij:"
10230 PRINT"de M,J,U en 8"
10240 PRINT:PRINT"Het is de bedoeling steeds vanuit de"
10250 PRINT"startpositie asdf  jkl; te starten naar"
10260 PRINT"de toets die moet worden aangeslagen."
10270 PRINT"Houdt daarbij de wijsvingers zoveel"
10280 PRINT"mogelijk boven de lijnen vfr5 en mju8"
10290 PRINT"Gebruik, als de SHIFT-toets moet worden"
10300 PRINT"ingedrukt, de pink van de andere hand."
10310 PRINT"De andere aparte toetsen aan de rand"
10320 PRINT"van het bord worden met de meest nabije"
10330 PRINT"pink aangeslagen.":PRINT:PRINT
10340 GOSUB10900
10350 RETURN
10900 REM VERDERGAAN MET SCHOON SCHERM
10910 PRINT"'n Willekeurige toets om verder te gaan";
10920 GOSUB210
10930 GOSUB100
10940 RETURN
11000 REM PROGRAMMA-EINDE EN AFSCHEID
11010 GOSUB100:VE=5:HO=5:GOSUB110
11020 PRINT"E I N D E   T Y P E K U R S U S"
11030 PRINT:PRINT
11040 IFH<>1 THENPRINT"(Druk wel de SHIFT-LOCK in !!)"
11050 END
30000 REM TYPE-CURSUS
30010 REM REEDS EERDER VERSCHENEN IN DE
30020 REM HOBBY COMPUTER CLUB NIEUWSBRIEF VAN
30030 REM MEI 1982, OFWEL HCCN39P18
30040 REM NU BIJGEWERKT VOOR BASICODE-2
30050 REM MET DANK AAN JANNEKKE RIJN EN
30060 REM JOHN HERMANS.
30070 REM           JOS BURGHOUTS
30080 REM           PUTHOF  35
30090 REM           5655 AM  EINDHOVEN
30100 REM           (TEL. 040-528372)
30110 REM GEMAAKT OP EEN OSI-SUPERBOARD MET
30120 REM 25 REGELS VAN 48 KARAKTERS BREED
30130 REM DIT PROGRAMMA IS TE RUNNEN IN 8K RAM
30140 REM
30150 REMCOPYRIGHT
30160 REMNOS HILVERSUM

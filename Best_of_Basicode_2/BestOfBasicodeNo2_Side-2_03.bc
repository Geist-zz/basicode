
1000 A=1000:GOTO20:REM HISTOGRAM
1010 GOSUB100
1011 MX=0
1020 PRINT"Uw schermhoogte ";:INPUTHS
1030 PRINT:PRINT"Uw schermbreedte";:INPUTBS
1040 HS=HS-4
1050 PRINT:PRINT"Wilt U uitleg? (J/N)";
1060 RESTORE
1070 GOSUB210
1080 IF(IN$="J") OR(IN$="j") THENGOSUB2370
1090 GOSUB100
1100 PRINT:PRINT:PRINT"*****HISTOGRAM*****":PRINT
1110 PRINT"Aantal intervallen ";:INPUTR1:DIMC(R1)
1120 IFR1<BS-5 THEN1160
1130 PRINT:PRINT"NIET MOGELIJK:"
1140 PRINT:PRINT" UW SCHERM":PRINT" IS TE SMAL!"
1150 END
1160 PRINT"Interval lengte    ";:INPUTR2
1170 PRINT"Minimum datalimiet ";:INPUTR3:N=0
1180 PRINT:PRINT"Na data invoer":PRINT"TYP=999"
1190 NN=1:PRINT:GOTO1210
1200 N=N-F:SX=SX-R0:XX=XX-R0*R0:NN=NN-1
1210 PRINT"Indien frequentie=1":PRINT"typ 1:";
1220 GOSUB210
1230 K=VAL(IN$)
1240 PRINT:PRINT"Data invoer:":PRINT
1250 IFK=1 THEN1300
1260 PRINT"Freq. ";" = ";:INPUTF
1270 PRINT"X =     ";:INPUTR6:PRINT
1280 IFR6=999 THEN1400
1290 GOTO1330
1300 PRINT" X";NN;" = ";:INPUTR6
1310 IFR6=999 THEN1400
1320 F=1
1330 N=N+F:R0=R6*F:SX=SX+R0:XX=XX+R0*R0:NN=NN+1
1340 P=R6-R3:IFP<0 THENPRINT"TE KLEIN!":GOTO1200
1350 P=INT(P/R2+1E-16)
1360 IFP>=R1 THENPRINT"TE GROOT":GOTO1200
1370 C(P)=C(P)+F
1380 IFK=1 THEN1300
1390 GOTO1260
1400 GOSUB100:PRINT:PRINT
1410 PRINT"GEMIDDELDE:           ";INT(SX/N*100+.5)/100
1420 S=SQR((XX-SX*SX/N)/(N-1))
1430 PRINT"STANDAARDAFWIJKING:   ";INT(S*100+.5)/100
1440 PRINT:PRINT:GOSUB210
1450 T1=INT(11/40*BS)+2:T2=INT(15/40*BS):T3=INT(36/40*BS)
1460 PRINT"CEL";TAB(T1);"VAN/TOT";TAB(T3);"FREQ."
1470 R5=R3:R4=R3
1480 FORI=0 TOR1-1
1490 R4=R4+R2
1500 PRINT" ";I;TAB(T1);INT(R5*100+.5)/100;
1510 PRINTTAB(T2+3);INT(R4*100+.5)/100;TAB(T3+1);C(I)
1520 R5=R4
1530 IFI+1<>HS-5*INT((I+1)/(HS-5)) THEN1560
1540 PRINT"<TOETS>:";:GOSUB210
1550 PRINT
1560 NEXTI
1570 PRINT:PRINTTAB(T1);"TOTAAL: ";N
1580 PRINT:PRINT"HISTOGRAM? <TOETS>:";
1590 GOSUB210
1600 GOSUB100
1610 FORI=0 TOR1-1
1620 IFC(I)>MX THENMX=C(I)
1630 NEXTI
1640 R4=R3
1650 REMFREQ. VERDELING
1660 REM=====================
1670 FORI=0 TOR1-1
1680 P1=INT((BS-4)/R1)
1690 P2=P1*I
1700 IFR1>INT(BS/4) THENIFI<>5*INT(I/5) THEN1720
1710 GOSUB10080:REMINT.NUMMERING
1720 IFC(I)=0 THEN1800
1730 REMVLIN HS,Y AT X
1740 REM
1750 Y=INT(C(I)/MX*HS)
1760 X=INT(P2+P1/2)+3
1770 GOSUB10000
1780 REM
1790 HO=0:VE=VE+1:GOSUB110:PRINTC(I)
1800 R4=R4+R2
1810 NEXTI
1820 VE=HS+2:HO=0:GOSUB110
1830 PRINT:PRINT"FREQ. VERD.,MAX=";MX;",  TOTAAL:";N;
1840 PRINT"  <TOETS>";
1850 GOSUB210
1860 REMCUMULATIEVE VERDELING
1870 REM=====================
1880 Q=0:R4=R3:GOSUB100
1890 FORI=0 TOR1-1
1900 P1=INT((BS-3)/R1)
1910 P2=P1*I
1920 Q=Q+C(I)
1930 IFR1>INT(BS/4) THENIFI<>5*INT(I/5) THEN1950
1940 GOSUB10080:REMINT.NUMMERING
1950 REMVLIN HS,Y AT X
1960 REM
1970 Y=INT(Q*HS/N)
1980 X=INT(P2+P1/2)+3
1990 GOSUB10000
2000 REM
2010 HO=0:VE=VE+1:GOSUB110:PRINTINT(Q/N*100+.5)/100
2020 R4=R4+R2
2030 NEXTI
2040 VE=HS+5:HO=0:GOSUB110
2050 PRINT"Cum.verd. : P(T<=X), Totaal:";N;
2060 PRINT" <TOETS>";
2070 GOSUB210
2080 GOSUB100
2090 REM
2100 REM   MENU
2110 REM============
2120 PRINT"  MENU:"
2130 IFBS<21 THENHO=0:GOTO2150
2140 HO=5
2150 VE=5:GOSUB110
2160 PRINT"1) HISTOGRAMMEN"
2170 VE=7:GOSUB110
2180 PRINT"2) AANVULLEN DATA"
2190 VE=9:GOSUB110
2200 PRINT"3) RESTART"
2210 VE=11:GOSUB110
2220 PRINT"4) STOP"
2230 GOSUB250
2240 VE=15:GOSUB110
2250 PRINT"UW KEUZE?: ";
2260 GOSUB210
2270 A=VAL(IN$)
2280 IF(A<1) OR(A>4) THEN2230
2290 ONA GOTO1400,2340,2360
2300 RESTORE
2310 FORI=0 TO38:READA$:NEXTI
2320 GOSUB11000:END
2330 END
2340 GOSUB100PRINT"EINDE DATA INVOER"
2350 PRINT:PRINT"TYP 999!":GOTO1210
2360 RUN
2370 REMUITLEG
2380 REM======
2390 RESTORE
2400 GOSUB11000:REMTEXT ROUTINE
2410 HO=BS-16:VE=HS+4:GOSUB110
2420 PRINT"NOG EENS? (J/N)";
2430 GOSUB210
2440 IF(IN$="J") OR(IN$="j") THEN2390
2450 RETURN
10000 REMVLIN HS,Y AT X
10010 REM==============
10020 HO=X:A$="X"
10030 IFR1<=INT(BS/4) THENA$="XX"
10040 FORVE=HS TOHS+1-Y STEP-1
10050 GOSUB110:PRINTA$
10060 NEXTVE
10070 RETURN
10080 REMGESPATIEERDE INTERVAL
10090 REMNUMMERING
10100 VE=HS+1:HO=P2+3:GOSUB110:PRINT"^";INT(R4*100+.5)/100;
10110 RETURN
11000 REMTEXT-ROUTINE
11010 REM=============
11020 GOSUB100
11030 FORI=1 TO12
11040 READA$
11050 IF(A$="ETX") OR(A$=".") THENI=13:GOTO11090
11060 PRINT:IFBS>25 THENPRINT"     ";
11070 PRINTA$
11080 NEXTI
11090 HO=BS-9:VE=HS+3
11100 GOSUB110:PRINT"<TOETS>:";
11110 GOSUB210
11120 GOSUB100
11130 IFA$<>"ETX" THEN11030
11140 RETURN
25000 DATA"Met behulp van dit","programma kan men"
25010 DATA "numerieke waarden","overzichtelijk weer-"
25020 DATA"geven in de vorm van","een HISTOGRAM.","."
25030 DATA"Dit programma geeft","een tabel en"
25040 DATA"twee histogrammen:","."
25050 DATA"1)*GEMIDDELDE","  STANDAARD DEVIATIE"
25060 DATA"  INTERVAL OVERZICHT"
25070 DATA"2)*FREQUENTIE VER-","   DELING;"
25080 DATA"  VOOR ELK INTERVAL","  HET AANTAL VOOR-"
25090 DATA "  KOMENDE WAARDEN","."
25100 DATA"3)*FREQUENTIE-","   CUMULATIEVE","   VERDELING:"
25110 DATA"HET TOTAAL AANTAL","WAARDEN,KLEINER"
25120 DATA"OF GELIJK AAN"
25130 DATA"DE INTERVAL WAARDE","."
25140 DATA"  INVOER:"
25150 DATA"-AANTAL INTERVALLEN","-INTERVAL LENGTE"
25160 DATA"-MIN.DATA LIMIET:","   KLEINSTE INTERVAL"
25170 DATA"   WAARDE"," "
25180 DATA "-NUMERIEKE DATA"
25190 DATA"ETX"
25200 DATA" "," ","DIT WAS 'HISTOGRAM'"
25210 DATA"EN WERDT GESCHREVEN","OP EEN APPLE II+"
25220 DATA " ","  2-9-'83","  TH.J. VAN ELSBERG"," "
25230 DATA"ETX"
30000 REM---------------------
30005 REM
30010 REMBASICODE-II: 2-9-'83
30015 REM
30020 REMTH.J.VAN ELSBERG 
30030 REM     
30040 REMVENETIESTR.39    
30050 REM                    
30060 REM5632RM EINDHOVEN  
30070 REM
30080 REM---------------------
30090 REM
30100 REMCOPYRIGHT
30110 REMNOS HILVERSUM

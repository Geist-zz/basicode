
1000 A=500:GOTO20:REM DROIDS
1010 GOSUB100:PRINT"  ***** DROIDS *****  "
1020 REM INITIALISEREN
1030 PRINT
1040 DIMA$(252),RI(8),Y(8),NA$(4),SC(4)
1050 FORP=1 TO8:READRI(P):NEXTP
1060 FORY=0 TO13:A$(Y)="=":A$(Y+238)="=":NEXTY
1070 FORY=14 TO236 STEP14:A$(Y)="!":FORX=Y+1 TOY+13
1080 A$(X)="O":NEXTX:A$(Y+13)="!":NEXTY
1090 REM PLAATS CIJFERS WILLEKEURIG IN BORD
1100 FORN=1 TO4
1110 GOSUB260:Y=INT(RV*224)+14
1120 IFA$(Y)<>"O" THEN1110
1130 SR=N:GOSUB300:A$(Y)=SR$:Y(N)=Y:NEXTN
1140 PRINT:PRINT"  Wilt U instucties":PRINT"       (j/n)";
1150 GOSUB2210
1160 GOSUB100:IFJA=1 THEN1620
1170 GOTO1820
1180 REM PRINT SPEL EN NAMEN
1190 GOSUB100
1200 PRINT"8 1 2"
1210 PRINT"  !/"
1220 PRINT"7-+-3"
1230 PRINT" /! "
1240 PRINT"6 5 4"
1250 FORC=1 TOM:PRINT:PRINTNA$(C):PRINTSC(C):NEXTC
1260 GOSUB1490
1270 REM SPELLOOP
1280 FORI=1 TOM:KL=0
1290 FORQ=1 TO4:FORU=1 TO8:IFA$(Y(Q)+RI(U))="O" THENKL=1
1300 NEXTU:NEXTQ:IFKL=0 THENFORT=1 TO2000:NEXTT:GOTO1970
1310 VE=19:HO=0:GOSUB110:PRINTNA$(I);"        "
1320 IFNA$(I)="COMPUTER" THENNA$(I)="computer"
1330 IFNA$(I)="computer" THENGOSUB2060:GOTO1410
1340 PRINT"Welk nummer     ";
1350 GOSUB210:N=VAL(IN$):IF(N>4) OR(N<1) THEN1310
1360 VE=20:HO=14:GOSUB110:PRINTN
1370 VE=21:HO=0:GOSUB110:PRINT"Richting        ";
1380 GOSUB210:RI=VAL(IN$):IF(RI>8) OR(RI<1) THEN1380
1390 VE=21:HO=14:GOSUB110:PRINTRI
1400 IFA$(Y(N)+RI(RI))<>"O" THENGOSUB250:GOTO1310
1410 AN=1:A=Y(N):H$=A$(A):A$(A)="+"
1420 A=Y(N)+RI(RI)*AN
1430 IFA$(A)="O" THENAN=AN+1:A$(A)="+":GOSUB1570:GOTO1420
1440 Y(N)=A-RI(RI):A$(Y(N))=H$
1450 SC(I)=SC(I)+AN-1:VE=4+3*I:HO=0:GOSUB110:PRINTSC(I)
1460 NEXTI
1470 GOTO1280
1480 END
1490 REM PRINT BORD
1500 VE=0:HO=8:GOSUB110
1510 FORY=0 TO17
1520 FORX=0 TO13
1530 PRINTA$(Y*14+X);:NEXTX
1540 VE=VE+1:GOSUB110:NEXTY
1550 RETURN
1560 REM BEWEGEN VAN DE CIJFERS
1570 B=A-RI(RI)
1580 VE=INT(B/14):HO=((B/14-VE)+.01)*14+8:GOSUB110:PRINT"+"
1590 VE=INT(A/14):HO=((A/14-VE)+.01)*14+8:GOSUB110:PRINTH$
1600 GOSUB250:RETURN
1610 REM INSTRUCTIES
1620 PRINT"**** INSTRUCTIES ****"
1630 PRINT:PRINT"U krijgt zometeen een bord te zien met"
1640 PRINT"O's. Ook ziet U de cijfers 1 tot 4."
1650 PRINT"U kunt nu een cijfer intypen en daarna"
1660 PRINT"een richting. Het cijfer zal nu in de"
1670 PRINT"richting gaan tot het volgende teken"
1680 PRINT"geen O is. Voor elke O die U 'weghaalt'"
1690 PRINT"krijgt U 1 punt. Wie de meeste  punten"
1700 PRINT"heeft die wint. De richting kunt U"
1710 PRINT"intoetsen door een getal (1 t/m 8, zie"
1720 PRINT"linkerbovenhoek van het scherm)"
1730 PRINT:PRINT"Druk op een toets";:GOSUB210
1740 GOSUB100
1750 PRINT"Het is ook mogelijk de computer mee te"
1760 PRINT"te laten spelen. Het aantal spelers is"
1770 PRINT"dan beperkt tot 3. Ook is het mogelijk"
1780 PRINT"de computer tegen zichzelf te laten"
1790 PRINT"spelen. Vul dan voor de naam van de"
1800 PRINT"speler 'computer' in"
1810 PRINT:PRINT"VEEL SUCCES !!!!!"
1820 PRINT:PRINT:PRINT"    Hoeveel spelers   "
1830 PRINT"    (1 tot 4) ";:GOSUB210:M=VAL(IN$):PRINTIN$
1840 IF(M>4) OR(M<1) THEN1830
1850 FORI=1 TOM
1860 PRINT:PRINT"Naam ";I;:INPUTNA$(I):NEXTI
1870 IFM=4 THEN1930
1880 PRINT:PRINT"Wilt U de computer ook mee laten spelen";
1890 GOSUB2210
1900 IFJA=1 THENM=M+1:NA$(M)="computer"
1910 IFM=1 THEN1960
1920 REM NAMEN SPELERS IN WILLEKEURIGE VOLGORDE
1930 GOSUB260:FORQ=1 TOINT(RV*25)
1940 HL$=NA$(1):FORW=1 TOM-1:NA$(W)=NA$(W+1):NEXTW
1950 NA$(M)=HL$:NEXTQ
1960 GOTO1190
1970 GOSUB100:PRINT"****  EINDSTAND  ****"
1980 FORI=1 TOM:PRINT:PRINTNA$(I);" ";SC(I):NEXTI
1990 PRINT:PRINT" Wilt U nog een keer "
2000 PRINT"       (j/n)";
2010 GOSUB2210
2020 IFJA=0 THEN2040
2030 FORC=1 TO4:SC(C)=0:NA$(C)="":NEXTC:GOTO1060
2040 END
2050 REM SPELTECHNIEK COMPUTER
2060 IU=-15:FORS=1 TO4
2070 FORC=1 TO8:PP=1
2080 IFA$(Y(S)+RI(C)*PP)="O" THENPP=PP+1:GOTO2080
2090 IFPP>1 THENGOSUB2120
2100 NEXTC:NEXTS
2110 RETURN
2120 LO=Y(S)+RI(C)*(PP-1):W=C+4:IFW>8 THENW=W-8
2130 IJ=0:FORP=1 TO8:PO=1:IFP=W THEN2160
2140 IFA$(LO+RI(P)*PO)="O" THENPO=PO+1:GOTO2140
2150 IFPO>IJ THENIJ=PO
2160 NEXTP
2170 IFPP-IJ>IU THENIU=PP-IJ:N=S:RI=C:GOSUB2190
2180 RETURN
2190 VE=20:HO=0:GOSUB110:PRINT"Neemt nummer ";N;"   "
2200 PRINT"Richting ";RI;:RETURN
2210 REM HAAL TOETS EN TEST
2220 JA=-1:GOSUB210:IF(IN$="J") OR(IN$="j") THENJA=1
2230 IF(IN$="N") OR(IN$="n") THENJA=0
2240 IFJA=-1 THEN2220
2250 RETURN
25000 DATA-14,-13,1,15,14,13,-1,-15
30000 REM********************* 
30010 REM*   GEMAAKT OP EEN  *
30020 REM*       VIC-20      *
30030 REM*  MET 8K RAM EXTRA *
30040 REM* R.VAN DER KRAAN   *
30050 REM* DELFLANDSTRAAT 20 * 
30060 REM* 2631 HD  NOOTDORP * 
30070 REM* DATUM  26-01-1983 *
30080 REM* TEL 01731-8813    *  
30090 REM*********************  
30100 REM
30110 REMCOPYRIGHT
30120 REMNOS HILVERSUM

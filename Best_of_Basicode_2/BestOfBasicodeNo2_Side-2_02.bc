
1000 A=300:GOTO20:REM HUISHOUDBOEKJE
1010 GOSUB100:VE=10:HO=7:GOSUB110
1020 PRINT"EVEN GEDULD! EVEN INLADEN!"
1025 DIMT$(6,15),B(6,15),C$(6,15),KR$(6)
1030 FORP=1 TO6:READKR$(P):FORI=1 TO15
1040 READT$(P,I),B(P,I),C$(P,I)
1050 NEXTI
1060 NEXTP
1070 CT=8:CN=2
1080 KL$="          "
2000 REM  MENU Pagina
2005 GOSUB100
2010 VE=0:HO=10:GOSUB110:PRINT"HUISHOUDBUDGET"
2020 VE=1:GOSUB110:PRINT"--------------"
2030 VE=3:HO=8:GOSUB110:PRINT"1 ENERGIE (etc)"
2040 VE=5:GOSUB110:PRINT"2 PERIODIEKEN"
2050 VE=7:GOSUB110:PRINT"3 CONTRIBUTIES en"
2060 VE=8:GOSUB110:PRINT"  ABONNEMENTEN"
2070 VE=10:GOSUB110:PRINT"4 VERZEKERINGEN en"
2080 VE=11:GOSUB110:PRINT"  BELASTINGEN"
2090 VE=13:GOSUB110:PRINT"5 AUTO- en FIETSKOSTEN"
2100 VE=15:GOSUB110:PRINT"6 HUISHOUDKOSTEN"
2110 VE=17:GOSUB110:PRINT"7 BUDGET BEREKENING"
2120 VE=19:GOSUB110:PRINT"8 STOPPEN"
2130 VE=22:HO=10:GOSUB110
2140 PRINT"Maak keuze!";
2150 GOSUB210:IN=VAL(IN$)
2160 ONIN GOTO3000,4000,5000,6000,7000,8000,9000,11000
2170 GOTO2000
2300 REM  PAG.1T/M6 PRINTEN
2310 VE=0:HO=2:GOSUB110
2320 PRINTKR$(P)
2330 VE=1:GOSUB110
2340 PRINT"-----------------------------------"
2350 VE=3:GOSUB110
2400 FORI=1 TO15
2405 SR=B(P,I):GOSUB310
2410 PRINTT$(P,I);TAB(18);SR$;TAB(27);C$(P,I)
2415 VE=VE+1:GOSUB110
2420 NEXTI
2500 VE=20:HO=6:GOSUB110
2510 PRINT"M=Menu        B=Budget ber."
2515 VE=21:GOSUB110
2520 PRINT"V=Verder      P=Printen"
2530 GOSUB210
2540 IF(IN$="M") OR(IN$="m") THENK=1
2550 IF(IN$="B") OR(IN$="b") THENK=2
2560 IF(IN$="V") OR(IN$="v") THENK=3
2570 IF(IN$="P") OR(IN$="p") THENK=4
2580 RETURN
2600 REM  PAG.1T/M6 NAAR PRINTER
2610 SR$=KL$+KR$(P):GOSUB350:GOSUB360
2620 SR$=KL$+"-----------------------------------"
2625 GOSUB350:GOSUB360
2630 FORI=1 TO15:SR=B(P,I):GOSUB310
2640 SR$=KL$+T$(P,I)+SR$+" "+C$(P,I)
2650 GOSUB350:GOSUB360:NEXTI
2660 FORI=1 TO5:GOSUB360:NEXTI
2670 RETURN
3000 REM  Pagina 1 (energie)
3010 GOSUB100:P=1:GOSUB2300
3060 ONK GOTO2000,9000,4000,3100
3070 GOTO3000
3100 REM  PAG. UITPRINTEN
3110 GOSUB2600
4000 REM  Pagina 2 (periodieken)
4010 GOSUB100:P=2:GOSUB2300
4060 ONK GOTO2000,9000,5000,4100
4070 GOTO4000
4100 REM  PAG. UITPRINTEN
4110 GOSUB2600
5000 REM  Pagina 3 (contrib.en abonn.)
5010 GOSUB100:P=3:GOSUB2300
5060 ONK GOTO2000,9000,6000,5100
5070 GOTO5000
5100 REM  PAG. UITPRINTEN
5110 GOSUB2600
6000 REM  Pagina 4 (verzek.en bel.)
6010 GOSUB100:P=4:GOSUB2300
6060 ONK GOTO2000,9000,7000,6100
6070 GOTO6000
6100 REM  PAG. UITPRINTEN
6110 GOSUB2600
7000 REM  Pagina 5 (auto en rijwiel)
7010 GOSUB100:P=5:GOSUB2300
7060 ONK GOTO2000,9000,8000,7100
7070 GOTO7000
7100 REM  PAG. UITPRINTEN
7110 GOSUB2600
8000 REM  Pagina 6 (huish.kosten)
8010 GOSUB100:P=6:GOSUB2300
8060 ONK GOTO2000,9000,9000,8100
8070 GOTO8000
8100 REM  PAG. UITPRINTEN
8110 GOSUB2600
9000 REM  BUDGET BEREKENING
9005 GOSUB100
9100 VE=0:HO=11:GOSUB110
9110 PRINT"HUISHOUDBUDGET"
9120 VE=1:HO=8:GOSUB110
9130 PRINT"(maandelijkse kosten)"
9200 VE=4:HO=6:GOSUB110
9210 SR=B(2,11):GOSUB310
9220 PRINT"HUUR/HYPOTHEEK :FL ";SR$
9300 REM  GASPRIJS
9310 GP=B(1,1)+B(1,3)*B(1,2)
9350 REM  NACHTSTROOM
9355 IFB(1,6)=0 THENNS=0:GOTO9400
9360 NS=B(1,4)+B(1,6)*B(1,5)
9370 IFB(1,6)>=1000 THEN9400
9380 D=(1000-B(1,6))/100:D=INT(D):D=D*3
9390 REM  DIT IS FL 3,00 EXTRA PER 100KWH BENEDEN 1000 KWH
9395 NS=NS+D
9400 REM  DAGSTROOM
9410 DS=B(1,7)+B(1,9)*B(1,8)
9450 REM  WATER
9455 IFB(1,12)=0 THENWA=0:GOTO9500
9460 WA=B(1,10)+B(1,12)*B(1,11)
9470 WA=WA+WA/100*4:REM  PLUS 4% BTW
9500 EP=GP+NS+DS:REM  ENERGIE PRIJS
9510 EP=EP+EP/100*18:REM  PLUS 18% BTW
9600 EN=(EP+WA+B(1,13)+B(1,14)+B(1,15))/12
9610 SR=EN:GOSUB310
9620 VE=5:HO=6:GOSUB110
9630 PRINT"ENERGIE etc.   :FL ";SR$
9700 REM  ABONN. CONTR.
9710 CO=(B(3,1)+B(3,2)+B(3,3)+B(3,4)+B(3,5))/12
9720 CO=CO+(B(3,6)+B(3,7)+B(3,8)+B(3,9)+B(3,10))/3
9730 CO=CO+B(3,11)+B(3,12)+B(3,13)+B(3,14)+B(3,15)
9740 SR=CO:GOSUB310
9750 VE=6:HO=6:GOSUB110
9760 PRINT"CONTRIBUTIES   :"
9770 VE=7:GOSUB110
9780 PRINT"en ABONNEMENTEN:FL ";SR$
9790 REM  VERZEKERINGEN
9800 VZ=B(4,1)+B(4,2)+B(4,3)+B(4,4)+B(4,5)
9810 VZ=(VZ+B(4,6)+B(4,7)+B(4,8))/12
9820 SR=VZ:GOSUB310
9830 VE=8:GOSUB110
9840 PRINT"VERZEKERINGEN  :FL ";SR$
9845 REM  BELASTINGEN
9850 BL=B(4,9)+B(4,10)+B(4,11)+B(4,12)
9860 BL=(BL+B(4,13)+B(4,14)+B(4,15))/12
9870 SR=BL:GOSUB310
9880 VE=9:GOSUB110
9890 PRINT"BELASTINGEN    :FL ";SR$
9900 REM  AUTOKOSTEN
9905 IFB(5,6)=0 THENAK=0:GOTO9970
9910 AK=(B(5,1)+B(5,2)+B(5,7)+B(5,9)+B(5,10)+B(5,11))/12
9920 AK=AK+B(5,3)/3
9930 AK=AK+B(5,4)*B(5,6)/B(5,5)/12
9940 BP=B(5,6)/30000*4*B(5,8):REM  30000 KM PER SET BANDEN
9950 BP=BP/12
9960 AK=AK+BP
9970 SR=AK:GOSUB310
9980 VE=10:GOSUB110
9990 PRINT"AUTOKOSTEN     :FL ";SR$
10000 REM  PERIODIEKE BETALINGEN
10010 PB=(B(2,1)+B(2,2)+B(2,3)+B(2,4)+B(2,5))/12
10020 PB=PB+(B(2,6)+B(2,7)+B(2,8)+B(2,9))/3
10030 PB=PB+B(2,10)/2:REM  TELEFOON
10040 PB=PB+B(2,12)+B(2,13)+B(2,14)+B(2,15)
10050 SR=PB:GOSUB310
10060 VE=11:GOSUB110
10070 PRINT"DIV.PERIODIEKE :"
10080 VE=12:GOSUB110
10090 PRINT" BETALINGEN    :FL ";SR$
10100 REM  HUISH.KOSTEN
10110 HK=0:FORI=1 TO15
10120 HK=HK+B(6,I):NEXTI
10130 SR=HK:GOSUB310
10140 VE=13:GOSUB110
10150 PRINT"ANDERE HUISH.  :"
10160 VE=14:GOSUB110
10170 PRINT" KOSTEN        :FL ";SR$
10200 REM  FIETSKOSTEN
10210 FK=(B(5,12)+B(5,13)+B(5,14)+B(5,15))/12
10220 SR=FK:GOSUB310
10230 VE=15:GOSUB110
10240 PRINT"RIJWIELKOSTEN  :FL ";SR$
10250 VE=16:HO=22:GOSUB110
10260 PRINT"-----------"
10270 REM  TOTAAL
10280 SO=B(2,11)+EN+CO+VZ+BL+AK+PB+HK+FK
10290 SR=SO:GOSUB310
10300 VE=17:HO=14:GOSUB110
10310 PRINT"TOTAAL :FL ";SR$
10400 VE=21:HO=6:GOSUB110
10410 PRINT"M=Menu        P=Printen"
10420 GOSUB210
10430 IF(IN$="M") OR(IN$="m") THEN2000
10440 IF(IN$="P") OR(IN$="p") THEN10500
10450 GOTO10420
10500 REM  UITPRINTEN BUDGET
10510 SR$=KL$+"      HUISHOUDBUDGET"
10520 GOSUB350:GOSUB360
10530 SR$=KL$+"   (maandelijkse kosten)"
10540 GOSUB350:GOSUB360:GOSUB360
10550 SR=B(2,11):GOSUB310
10560 SR$=KL$+"HUUR/HYPOTHEEK :FL"+SR$
10570 GOSUB350:GOSUB360
10580 SR=EN:GOSUB310
10590 SR$=KL$+"ENERGIE etc.   :FL"+SR$
10600 GOSUB350:GOSUB360
10610 SR$=KL$+"ABONNEMENTEN   :"
10620 GOSUB350:GOSUB360
10630 SR=CO:GOSUB310
10640 SR$=KL$+"en CONTRIBUTIES:FL"+SR$
10650 GOSUB350:GOSUB360
10660 SR=VZ:GOSUB310
10670 SR$=KL$+"VERZEKERINGEN  :FL"+SR$
10680 GOSUB350:GOSUB360
10690 SR=BL:GOSUB310
10700 SR$=KL$+"BELASTINGEN    :FL"+SR$
10710 GOSUB350:GOSUB360
10720 SR=AK:GOSUB310
10730 SR$=KL$+"AUTOKOSTEN     :FL"+SR$
10740 GOSUB350:GOSUB360
10750 SR$=KL$+"DIV.PERIODIEKE :"
10760 GOSUB350:GOSUB360
10770 SR=PB:GOSUB310
10780 SR$=KL$+" BETALINGEN    :FL"+SR$
10790 GOSUB350:GOSUB360
10800 SR$=KL$+"ANDERE HUISH.  :"
10810 GOSUB350:GOSUB360
10820 SR=HK:GOSUB310
10830 SR$=KL$+" KOSTEN        :FL"+SR$
10840 GOSUB350:GOSUB360
10850 SR=FK:GOSUB310
10860 SR$=KL$+"RIJWIELKOSTEN  :FL"+SR$
10870 GOSUB350:GOSUB360
10880 SR$=KL$+KL$+"      "+"----------"
10890 GOSUB350:GOSUB360
10900 SR=SO:GOSUB310
10910 SR$=KL$+"        TOTAAL :FL"+SR$
10915 GOSUB350:GOSUB360
10920 FORI=1 TO5:GOSUB360:NEXTI
10930 GOTO2000
11000 REM  EINDE
11005 GOSUB100:VE=10:HO=10:GOSUB110
11010 PRINT"SUCCES EN TOT ZIENS!":END
25000 REM  Gegevens pag. 1
25010 DATA "ENERGIE etc. (excl. BTW)      pag.1"
25020 DATA "Gas  vastrecht :FL",57.00,"per jaar"
25030 DATA "  prijs per m3 :FL",0.496,""
25040 DATA "  geschat verbr:  ",1500,"m3 gas"
25050 DATA "Nachtstr.vastr.:FL",42.00,"per jaar"
25060 DATA "  prijs per kwh:FL",0.046,""
25070 DATA "  geschat verbr:  ",0,"kwh"
25080 DATA "Dagstr.  vastr.:FL",57.60,"per jaar"
25090 DATA "  prijs per kwh:FL",0.110,""
25100 DATA "  geschat verbr:  ",600,"kwh"
25110 DATA "Water vastrecht:FL",66.00,"per jaar"
25120 DATA "  prijs per m3 :FL",2.238,""
25130 DATA "  geschat verbr:  ",300,"m3 water"
25140 DATA "Vuilophaalrecht:FL",77.28,"per jaar"
25150 DATA "Reinigingsrecht:FL",115.20,"per jaar"
25160 DATA "               :FL",0,"per jaar"
25170 REM  Gegevens pag. 2
25180 DATA "OVERIGE PERIODIEKE BETALINGEN pag.2"
25190 DATA "Centr.antenne  :FL",128.00,"per jaar"
25200 DATA "Rente lening   :FL",0,"per jaar"
25210 DATA "Schoolgeld     :FL",0,"per jaar"
25220 DATA "Vrijwill.bijdr.:FL",500.00,"per jaar"
25230 DATA "               :FL",0,"per jaar"
25240 DATA "               :FL",0,"per kwar"
25250 DATA "               :FL",0,"per kwar"
25260 DATA "               :FL",0,"per kwar"
25270 DATA "               :FL",0,"per kwar"
25280 DATA "Telefoon       :FL",68.80,"per 2mnd"
25290 DATA "Huur/Hypotheek :FL",300.00,"per mnd"
25300 DATA "Autom.spaarreg.:FL",0,"per mnd"
25310 DATA "               :FL",0,"per mnd"
25320 DATA "               :FL",0,"per mnd"
25330 DATA "               :FL",0,"per mnd"
25340 REM  Gegevens pag. 3
25350 DATA "CONTRIBUTIES en ABONNEMENTEN  pag.3"
25360 DATA "HCC Nieuwsbrief:FL",40.00,"per jaar"
25370 DATA "Elektuur       :FL",57.00,"per jaar"
25380 DATA "               :FL",0,"per jaar"
25390 DATA "               :FL",0,"per jaar"
25400 DATA "Kruisvereniging:FL",40.70,"per jaar"
25410 DATA "Krant          :FL",60.00,"per kwar"
25420 DATA "Omroepverenig. :FL",15.00,"per kwar"
25430 DATA "               :FL",0,"per kwar"
25440 DATA "               :FL",0,"per kwar"
25450 DATA "               :FL",0,"per kwar"
25460 DATA "Sportvereniging:FL",10.00,"per mnd"
25470 DATA "               :FL",0,"per mnd"
25480 DATA "               :FL",0,"per mnd"
25490 DATA "               :FL",0,"per mnd"
25500 DATA "               :FL",0,"per mnd"
25510 REM  Gegevens pag. 4
25520 DATA "VERZEKERINGEN en BELASTINGEN  pag.4"
25530 DATA "Levensverzek.  :FL",0,"per jaar"
25540 DATA "Inboedelverzek.:FL",0,"per jaar"
25550 DATA "Wett.aanspr.   :FL",0,"per jaar"
25560 DATA "Ziekte kosten  :FL",0,"per jaar"
25570 DATA "Kostbaarheden  :FL",0,"per jaar"
25580 DATA "               :FL",0,"per jaar"
25590 DATA "               :FL",0,"per jaar"
25600 DATA "               :FL",0,"per jaar"
25610 DATA "Onr.goed bel.  :FL",0,"per jaar"
25620 DATA "Riool bel.     :FL",88.92,"per jaar"
25630 DATA "Inkomsten bel. :FL",0,"per jaar"
25640 DATA "Omroepbijdrage :FL",142.00,"per jaar"
25650 DATA "Waterschap bel.:FL",0,"per jaar"
25660 DATA "               :FL",0,"per jaar"
25670 DATA "               :FL",0,"per jaar"
25680 REM  Gegevens pag. 5
25690 DATA "AUTOKOSTEN en RIJWIELKOSTEN   pag.5"
25700 DATA "Afschrijving   :FL",1500.00,"per jaar"
25710 DATA "Verzekering    :FL",600.00,"per jaar"
25720 DATA "Wegenbelasting :FL",0,"per kwar"
25730 DATA "Benzineprijs   :FL",1.83,"per lit."
25740 DATA "Benzineverbruik:  ",15,"km/liter"
25750 DATA "Aant.kilometers:  ",15000,"per jaar"
25760 DATA "Onderhoud      :FL",0,"per jaar"
25770 DATA "Banden         :FL",0,"per stuk"
25780 DATA "Reparaties     :FL",0,"per jaar"
25790 DATA "               :FL",0,"per jaar"
25800 DATA "               :FL",0,"per jaar"
25810 DATA "Fiets afschr.  :FL",100.00,"per jaar"
25820 DATA "Reparaties     :FL",60.00,"per jaar"
25830 DATA "Verzekering    :FL",0,"per jaar"
25840 DATA "               :FL",0,"per jaar"
25850 REM  Gegevens pag. 6
25860 DATA "ANDERE HUISHOUDELIJKE KOSTEN  pag.6"
25870 DATA "Huishoudgeld   :FL",750.00,"per mnd"
25880 DATA "Kleding        :FL",0,"per mnd"
25890 DATA "Aanvul.huisraad:FL",25.00,"per mnd"
25900 DATA "Reparaties     :FL",0,"per mnd"
25910 DATA "Zakgeld A      :FL",0,"per mnd"
25920 DATA "Zakgeld B      :FL",0,"per mnd"
25930 DATA "Vakantiegeld   :FL",200.00,"per mnd"
25940 DATA "               :FL",0,"per mnd"
25950 DATA "               :FL",0,"per mnd"
25960 DATA "               :FL",0,"per mnd"
25970 DATA "               :FL",0,"per mnd"
25980 DATA "               :FL",0,"per mnd"
25990 DATA "               :FL",0,"per mnd"
26000 DATA "               :FL",0,"per mnd"
26010 DATA "               :FL",0,"per mnd"
26020 DATA0
30000 REM  HUISHOUDBUDGET
30010 REM  NAAR EEN P2000 PROGRAMMA
30020 REM  VAN J.H. vd VEER
30030 REM  VOOR BASICODE HERSCHREVEN
30040 REM  OP EEN P2000T MET 32K
30050 REM  DOOR A.B. STOLZE
30060 REM       BOTTER 69
30070 REM       3123 BC SCHIEDAM
30080 REM  JULI 1983
30090 REM  DIT PROGRAMMA GEBRUIKT 
30100 REM  RUIM 11K GEHEUGEN.
30110 REM  HET IS DE BEDOELING DAT EEN 
30120 REM  IEDER NAAR BEHOEFTE DE DATA
30130 REM  AANPAST VOOR ZICH ZELF.
30140 REM  BEDENK ECHTER WEL DAT DE 
30150 REM  TIJDSAANDUIDING ACHTERAAN
30160 REM  NIET VERANDERD KAN WORDEN,
30170 REM  ZONDER HET REKENGEDEELDE OOK
30180 REM  TE VERANDEREN.
30190 REM  DE ENERGIE PRIJZEN ZIJN DIE
30200 REM  VOOR 1983 VOOR DE GEMEENTE
30210 REM  SCHIEDAM. DE ANDEREN ZIJN
30220 REM  ZOMAAR INGEVULD EN DIENEN
30230 REM  AANGEPAST. (INDIEN NODIG)
30240 REM
30250 REM  COPYRIGHT
30260 REM  NOS HILVERSUM

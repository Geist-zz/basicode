
1000 A=2000:GOTO20:REM  STAMBOOM
1010 GOSUB1860:REM  PLAATJE
1020 FORT=1 TO1000:NEXTT
1030 HO=0:VE=23:GOSUB110
1040 PRINT"WILT U UITLEG OVER DIT PROGRAMMA ? J/N ";
1050 GOSUB210
1060 IF(IN$="J") OR(IN$="j") THENGOSUB6015:GOSUB1860
1070 GOSUB2090:REMINLEZEN FILE
1100 REM
1105 REM* MENU * 
1110 REM
1115 GOSUB100
1120 PRINT:PRINTTAB(13);"S T A M B O O M":PRINT
1125 PRINT"---------------------------------------"
1130 PRINT:PRINT"U KUNT NAAR EEN PERSOON ZOEKEN OP DRIE"
1135 PRINT:PRINT"VERSCHILLENDE MANIEREN. MAAK UW KEUZE!"
1140 PRINT:PRINT:PRINT
1145 PRINT"   * NAAM  . . . . . . . . . . . . N":PRINT
1150 PRINT"   * GEBOORTEJAAR  . . . . . . . . G":PRINT
1155 PRINT"   * PERSOONSNUMMER  . . . . . . . P":PRINT
1160 PRINT"PROGRAMMA VERLATEN ?  DAN  . . . . E":PRINT
1165 PRINT:PRINT"   WAT IS UW KEUZE ? ";:GOSUB210
1170 IF(IN$="N") OR(IN$="n") THEN1210
1175 IF(IN$="G") OR(IN$="g") THEN1290
1180 IF(IN$="P") OR(IN$="p") THEN1370
1185 IF(IN$="E") OR(IN$="e") THEN1415
1190 PRINT:PRINT"GRAAG N,G,P OF E ! ":GOTO1165
1195 REM
1200 REM* KIES NAAM * 
1205 REM
1210 GOSUB100
1215 PRINT:PRINT"KIES EEN NAAM...":PRINT
1220 PRINT"> ";:INPUTA$
1225 L=LEN(A$):IFL=0 THEN1210
1230 REM* ZOEK NAAM *
1235 K=0:WI=0
1240 FORI=1 TOLP
1245 IFMID$(PE$(I),12,L)=A$ THENK=K+1:HL$(K)=PE$(I)
1250 IF(K=1) AND(WI=0) THENNK=I:WI=1
1255 NEXTI
1260 IFK>0 THENGOSUB2025:GOTO1455
1265 PRINT:PRINT"NAAM NIET GEVONDEN !"
1270 GOSUB1990:GOTO1115
1275 REM
1280 REM* KIES GEBOORTEJAAR * 
1285 REM
1290 GOSUB100
1295 PRINT:PRINT"KIES GEBOORTEJAAR > ";
1300 INPUTA$:L=LEN(A$)
1305 IFL=0 THEN1290
1310 REM* ZOEK GEBOORTEJAAR *
1315 K=0:WI=0
1320 FORI=1 TOLP
1325 IFLEFT$(PE$(I),L)=A$ THENK=K+1:HL$(K)=PE$(I)
1330 IF(K=1) AND(WI=0) THENNK=I:WI=1
1335 NEXTI
1340 IFK>0 THENGOSUB2025:GOTO1455
1345 PRINT:PRINT"GEBOORTEJAAR NIET GEVONDEN ! "
1350 GOSUB1990:GOTO1115
1355 REM
1360 REM* KIES PERSOONSNUMMER *
1365 REM
1370 GOSUB100
1375 PRINT:PRINT"KIES PERSOONSNUMMER > ";
1380 INPUTNR$:NR=VAL(NR$)
1385 IF(NR>0) AND(NR<=LP) THEN1455
1390 PRINT"(PERSOONSNUMMER NIET GROTER DAN ";LP;")"
1395 GOTO1375
1400 REM
1405 REM* PROGRAMMA VERLATEN *
1410 REM
1415 GOSUB1860
1420 HO=0:VE=23:GOSUB110
1425 PRINTTAB(10);"T O T   Z I E N S  !   ";
1430 FORT=1 TO2000:NEXTT
1435 GOSUB100:END
1440 REM
1445 REM* KIES VERBINTENIS *
1450 REM
1455 GOSUB100
1460 PRINTPE$(NR):PRINT
1465 PRINT"KIEZEN UIT OUDERS .(O)"
1470 PRINT"OF VERBINTENIS  . .(V)"
1475 PRINT"(NAAR MENU) . . . .(M)":PRINT
1480 PRINT"UW KEUZE ?        > ";:GOSUB210
1485 IF(IN$="O") OR(IN$="o") THENPRINTIN$:PRINT:PRINT:GOTO1510
1490 IF(IN$="V") OR(IN$="v") THENPRINTIN$:PRINT:PRINT:GOTO1535
1495 IF(IN$="M") OR(IN$="m") THEN1115
1500 GOTO1455
1505 REM* OUDERS *
1510 V1=PE(NR,3):IFV1<>0 THEN1525
1515 PRINT:PRINT"OUDERS (NOG) NIET BEKEND ! "
1520 GOSUB1990:GOTO1455
1525 N2=NR:GOTO1740
1530 REM* VERBINTENIS *
1535 V1=PE(NR,1)
1540 IFV1<>0 THEN1555
1545 PRINT:PRINT"GEEN VERBINTENIS BEKEND !"
1550 GOSUB1990:GOTO1455
1555 GE=PE(NR,0):GN=1-GE
1560 IFVB(V1,GN+2)=0 THEN1655
1565 REM* MEER VERBINTENISSEN *
1570 GOSUB100:PRINTPE$(NR):PRINT
1575 GOSUB1600:REM* PRINT VERBINTENIS *
1580 V1=VB(V1,GN+2)
1585 IFV1=0 THEN1615
1590 GOSUB1600:GOTO1580
1595 REM* SBR PRINT *
1600 PRINT:PRINT"VERBINTENIS :":PRINTVB$(V1)
1605 PRINT"MET :":PRINTPE$(VB(V1,GE))
1610 RETURN
1615 PRINT:PRINT:PRINT"KIES NU VERBINTENIS: V# ";
1620 INPUTV1$:V1=VAL(V1$)
1625 IF(V1>0) AND(V1<=LV) THEN1655
1630 PRINT"(VERBINTENISNUMMER NIET GROTER DAN ";LV;")"
1635 GOTO1615
1640 REM
1645 REM* UITGAANDE VAN GEKOZEN VERBINTENIS *
1650 REM
1655 GOSUB100
1660 PRINT"GEKOZEN VERBINTENIS :":PRINTVB$(V1):PRINT
1665 MN=VB(V1,0):VR=VB(V1,1):KI=VB(V1,4)
1670 PRINT"MAN  :":PRINTPE$(MN):PRINT
1675 PRINT"VROUW:":PRINTPE$(VR)
1680 PRINT"----------------------------------------"
1685 PRINT"KIES UIT OUDERS MAN   (OM)"
1690 PRINT"         OUDERS VROUW (OV)"
1695 PRINT"         KINDEREN     (K)"
1700 PRINT:PRINT"OM, OV OF K ? ";:GOSUB210
1705 IF(IN$="K") OR(IN$="k") THEN1775
1710 IF(IN$<>"O") AND(IN$<>"o") THEN1700
1715 PRINTIN$;:A$=IN$:GOSUB210:A$=A$+IN$
1720 IF(A$="OM") OR(A$="om") THENN2=MN:GOTO1740
1725 IF(A$="OV") OR(A$="ov") THENN2=VR:GOTO1740
1730 GOTO1700
1735 REM* OUDERS *    
1740 GOSUB100
1745 PRINT"OUDERS VAN":PRINTPE$(N2):PRINT
1750 V1=PE(N2,3):IFV1<>0 THEN1665
1755 NR=N2
1760 PRINT:PRINT"OUDERS (NOG) NIET BEKEND ! "
1765 GOSUB1990:GOTO1455
1770 REM* KINDEREN *
1775 GOSUB100
1780 PRINT"KINDEREN UIT VERBINTENIS :"
1785 PRINTVB$(V1):PRINT
1790 PRINT"VADER   :":PRINTPE$(MN):PRINT
1795 PRINT"MOEDER  :":PRINTPE$(VR)
1800 PRINT"----------------------------------------"
1805 IFKI=0 THENPRINTTAB(12);"GEEN KINDEREN !":GOTO1820
1810 PRINTPE$(KI):KI=PE(KI,2)
1815 IFKI<>0 THEN1810
1820 PRINT:PRINT"KIES PERSOONSNUMMER         > ";
1825 INPUTNR$:NR=VAL(NR$)
1830 IF(NR>0) AND(NR<=LP) THEN1455
1835 PRINT"(PERSOONSNUMMER NIET GROTER DAN ";LP;")"
1840 GOTO1820
1845 REM
1850 REM* PLAATJE * 
1855 REM
1860 GOSUB100
1865 PRINT:PRINT"           ****************"
1870 PRINT"         *********************"
1875 PRINT"       ***************************"
1880 PRINT"      **** H O B B Y S C O O P ****"
1885 PRINT"    *********************************"
1890 PRINT"    ******* S T A M B O O M ******"
1895 PRINT"     *************************"
1900 PRINT"            ***************"
1905 PRINT"       ***   *************   ***"
1910 PRINT"       ***       *******     ***"
1915 PRINT"       *** *      ****     * ***"
1920 PRINT"        *  *      ****     *  *"
1925 PRINT"     *******      ****     ********"
1930 PRINT"     * ***        ****       ***"
1935 PRINT"     * ***        ****       ***"
1940 PRINT"       ***        ****      *****"
1945 PRINT"       * *        ****     *******"
1950 PRINT"       * *        ****       * *"
1955 PRINT"      ** **     ********    ** **"
1960 PRINT"****************************************";
1965 PRINT"****************************************";
1970 RETURN
1975 REM
1980 REM* TOETS INDRUKKEN *             
1985 REM
1990 HO=3:VE=23:GOSUB110
1995 PRINT"DRUK EEN WILLEKEURIGE TOETS IN...";:GOSUB210
2000 GOSUB100
2005 RETURN
2010 REM
2015 REM* KIES UIT LIJST *
2020 REM
2025 GOSUB100
2030 IFK=1 THENNR=NK:GOTO2070
2035 FORI=1 TOK:PRINTHL$(I):NEXTI
2040 PRINT:PRINT"KIES PERSOONSNUMMER         > ";
2045 INPUTNR$:NR=VAL(NR$)
2050 IF(NR>0) AND(NR<=LP) THEN2065
2055 PRINT"(PERSOONSNUMMER NIET GROTER DAN ";LP;")"
2060 GOTO2040
2065 GOSUB100
2070 RETURN
2075 REM
2080 REM* INLEZEN DATA FILE * 
2085 REM
2090 HO=0:VE=23:GOSUB110
2095 PRINT"       NU WORDT DATA INGELEZEN...      ";
2100 READLV,LP
2105 DIMVB$(LV),VB(LV,4),PE$(LP),PE(LP,3),HL$(LP)
2110 FORI=1 TOLV:READVB$(I)
2115 READVB(I,0),VB(I,1)
2120 FORI1=2 TO4:VB(I,I1)=0:NEXTI1
2125 NEXTI
2130 FORI=1 TOLP:READPE$(I)
2135 READPE(I,0),PE(I,3)
2140 PE(I,1)=0:PE(I,2)=0
2145 NEXTI
2150 REM* VERBINTENISARRAY MAKEN *
2155 FORI=1 TOLV
2160 H1=VB(I,0):H2=PE(H1,1)
2165 IFH2=0 THENPE(H1,1)=I:GOTO2185
2170 H1=H2:H2=VB(H2,2)
2175 IFH2<>0 THEN2170
2180 VB(H1,2)=I
2185 H1=VB(I,1):H2=PE(H1,1)
2190 IFH2=0 THENPE(H1,1)=I:GOTO2210
2195 H1=H2:H2=VB(H2,3)
2200 IFH2<>0 THEN2195
2205 VB(H1,3)=I
2210 NEXTI
2215 REM* PERSOONSARRAY MAKEN *
2220 FORI=1 TOLP
2225 H1=PE(I,3)
2230 IFH1=0 THEN2260
2235 H2=VB(H1,4)
2240 IFH2=0 THENVB(H1,4)=I:GOTO2260
2245 H1=H2:H2=PE(H2,2)
2250 IFH2<>0 THEN2245
2255 PE(H1,2)=I
2260 NEXTI
2265 RETURN
6000 REM
6005 REM* UITLEG *
6010 REM
6015 S$="    ** S T A M B O O M   U I T L E G **"
6020 GOSUB100:PRINTS$:PRINT
6025 PRINT"MET BEHULP VAN DIT PROGRAMMA KUNT U"
6030 PRINT:PRINT"ZELF EEN STAMBOOM MAKEN EN BIJHOUDEN."
6035 PRINT:PRINT"U KUNT NAAR HARTELUST DOOR DE STAMBOOM"
6040 PRINT:PRINT"KRUIPEN, STAMREEKSEN VOLGEN, KWARTIER-"
6045 PRINT:PRINT"STATEN BEKIJKEN, GEZINSBLADEN MAKEN"
6050 PRINT:PRINT"OF PARENTEELONDERZOEK DOEN."
6055 PRINT:PRINT"U KUNT ALTIJD PERSONEN AAN HET BESTAND"
6060 PRINT:PRINT"TOEVOEGEN; OF HET NU VERRE VOOROUDERS"
6065 PRINT:PRINT"BETREFT OF NIEUWE SPRUITEN VAN"
6070 PRINT:PRINT"AANGETROUWDE FAMILIE'S!"
6075 GOSUB1990
6080 PRINTS$:PRINT
6085 PRINT"DE PERSONEN UIT DE STAMBOOM ZIJN OP TE"
6090 PRINT"ZOEKEN OP 3 VERSCHILLENDE WIJZEN:"
6095 PRINT
6100 PRINT"   1. DOOR EEN NAAM IN TE VOEREN ZOEKT"
6105 PRINT"      DE COMPUTER ALLE PERSONEN MET DIE"
6110 PRINT"      NAAM."
6115 PRINT
6120 PRINT"   2. DOOR EEN JAARTAL IN TE VOEREN."
6125 PRINT"      (NB. DE INVOER VAN '18' GEEFT"
6130 PRINT"      ALLE PERSONEN DIE IN DE 19E EEUW"
6135 PRINT"      ZIJN GEBOREN!)."
6140 PRINT
6145 PRINT"   3. DOOR EEN PERSOONSNUMMER TE KIEZEN."
6150 PRINT
6155 PRINT"VOOR 1 EN 2 GELDT: ALS ER MEER DAN 1"
6160 PRINT"PERSOON IS GEVONDEN, MOET ER ALSNOG EEN"
6165 PRINT"PERSOONSNUMMER GEKOZEN WORDEN."
6170 GOSUB1990
6175 PRINTS$:PRINT
6180 PRINT"WANNEER ER EEN PERSOON I GEKOZEN IS,"
6185 PRINT"KUNT U EEN VERBINTENIS KIEZEN."
6190 PRINT"DAT KAN ZIJN DE VERBINTENIS VAN DE "
6195 PRINT"OUDERS VAN I. ZO KUNT U BV. BROERS EN"
6200 PRINT"ZUSTERS VAN I VINDEN."
6205 PRINT
6210 PRINT"OOK KUNT U DE VERBINTENIS(SEN) VAN PER-"
6215 PRINT"SOON I KIEZEN. WANNEER ER MAAR 1 ZO'N"
6220 PRINT"VERBINTENIS BEKEND IS, GAAT HET PRO-"
6225 PRINT"GRAMMA VAN DIE VERBINTENIS UIT."
6230 PRINT:PRINT
6235 PRINT"BIJ MEER VERBINTENISSEN MOET EEN KEUZE"
6240 PRINT"WORDEN GEDAAN."
6245 PRINT:PRINT
6250 PRINT"MET M(ENU) KUNT U EEN ANDERE PERSOON"
6255 PRINT"KIEZEN OF PROGRAMMA VERLATEN MET E(INDE)"
6260 GOSUB1990
6265 PRINTS$:PRINT
6270 PRINT"ALS VOORBEELD IS (EEN DEEL VAN) DE"
6275 PRINT
6280 PRINT"FAMILIE FORSYTE OPGENOMEN IN HET "
6285 PRINT
6290 PRINT"STAMBOOM-PROGRAMMA."
6295 PRINT:PRINT
6300 PRINT"(ZIE HET BOEK: 'THE FORSYTE SAGA'"
6305 PRINT
6310 PRINTTAB(10);"VAN : JOHN GALSWORTHY  )"
6315 PRINT:PRINT:PRINT
6320 PRINT"ZOEK MAAR EENS NAAR 'SOAMES', 'FLEUR'"
6325 PRINT
6330 PRINT"OF 'IRENE', OF DE VROUWEN VAN 'JOLYON'"
6335 PRINT
6340 PRINT"DIE IN 1847 WERD GEBOREN!"
6345 GOSUB1990
6350 PRINTS$:PRINT
6355 PRINT"DE STAMBOOM IS OPGEBOUWD UIT 2 SOORTEN"
6360 PRINT:PRINT"GEGEVENS N.L.:":PRINT
6365 PRINT:PRINT"    1. GEGEVENS OVER PERSONEN"
6370 PRINT:PRINT"    2. GEGEVENS OVER VERBINTENISSEN"
6375 PRINT:PRINT
6380 PRINT"DE GEGEVENS ZIJN OPGEBORGEN IN RECORDS."
6385 PRINT:PRINT"ER ZIJN PERSOONSRECORDS EN VERBINTENIS-"
6390 PRINT:PRINT"RECORDS."
6395 PRINT:PRINT"BIJ ELK RECORD HOREN TWEE PARAMETERS."
6400 GOSUB1990
6405 PRINTS$:PRINT
6410 PRINT"DE PERSOONS- EN DE VERBINTENISRECORDS"
6415 PRINT:PRINT"ZIJN OPEENVOLGEND GENUMMERD."
6420 PRINT:PRINT
6425 PRINT"** DE BETEKENIS VAN DE PARAMETERS **"
6430 PRINT
6435 PRINT" ACHTER ELK VERBINTENISRECORD :"
6440 PRINT
6445 PRINT"  1. PERSOONSNUMMER VAN DE MAN"
6450 PRINT"  2. PERSOONSNUMMER VAN DE VROUW"
6455 PRINT:PRINT
6460 PRINT" ACHTER ELK PERSOONSRECORD :"
6465 PRINT
6470 PRINT"  1. CODE VOOR HET GESLACHT (V=0/M=1)"
6475 PRINT"  2. VERBINTENISNUMMER WAARUIT DE"
6480 PRINT"     PERSOON IS VOORTGEKOMEN."
6485 PRINT"     (0 ALS VERBINTENIS ONBEKEND IS)."
6490 GOSUB1990
6495 PRINTS$:PRINT
6500 PRINT"MET DEZE GEGEVENS MAAKT UW COMPUTER DE"
6505 PRINT:PRINT"ARRAYS, WAARMEE HET PROGRAMMA WERKT."
6510 PRINT:PRINT"DIT GEBEURT GELIJK NA HET INLEZEN VAN"
6515 PRINT:PRINT"DE DATASTATEMENTS."
6520 PRINT:PRINT"BIJ HET VERBINTENISARRAY WORDT EEN "
6525 PRINT:PRINT"GETALLENARRAY GEMAAKT MET 5 PARAMETERS"
6530 PRINT:PRINT"EN BIJ HET PERSOONSARRAY EEN ARRAY MET"
6535 PRINT:PRINT"4 PARAMETERS."
6540 PRINT:PRINT"OP DE VOLGENDE PAGINA'S ZIET U WAAR"
6545 PRINT:PRINT"DEZE PARAMETERS VOOR STAAN."
6550 GOSUB1990
6555 PRINTS$:PRINT
6560 PRINT"BETEKENIS VAN DE VERBINTENISGEGEVENS :"
6565 PRINT:PRINT"    VOOR VERBINTENIS I GELDT:"
6570 PRINT:PRINT"VB$(I)  : HET VERBINTENISRECORD MET "
6575 PRINTTAB(11);"JAARTAL,PERSOONSNUMMER VAN "
6580 PRINTTAB(11);"MAN EN VROUW,EN VERB.NR. I."
6585 PRINT:PRINT"    5 PARAMETERS :"
6590 PRINT:PRINT"VB (I,0): PERSOONSNUMMER VAN DE MAN."
6595 PRINT"VB (I,1): PERSOONSNUMMER VAN DE VROUW."
6600 PRINT"VB (I,2): VOLGEND VERB.NR. VAN MAN."
6605 PRINT"VB (I,3): VOLGEND VERB.NR. VAN VROUW."
6610 PRINT"VB (I,4): PERSOONSNR. VAN EERSTE KIND"
6615 PRINTTAB(11);"UIT VERBINTENIS I."
6620 GOSUB1990
6625 PRINTS$:PRINT
6630 PRINT"BETEKENIS VAN DE PERSOONSGEGEVENS :"
6635 PRINT:PRINT"    VOOR PERSOON I GELDT:"
6640 PRINT:PRINT"PE$(I)   : HET PERSOONSRECORD MET JAAR-"
6645 PRINTTAB(12);"TALLEN, NAAM EN PERSOONS-"
6650 PRINTTAB(12);"NUMMER VAN I."
6655 PRINT:PRINT"    4 PARAMETERS :"
6660 PRINT:PRINT"PE (I,0): HET GESLACHT VAN I. (M=1/V=0)"
6665 PRINT"PE (I,1): HET VERBINTENISNUMMER WAARIN"
6670 PRINTTAB(11);"I ALS OUDER VOORKOMT."
6675 PRINT"PE (I,2): HET PERSOONSNUMMER VAN DE"
6680 PRINTTAB(11);"BROER OF ZUS VAN I."
6685 PRINT"PE (I,3): HET NUMMER VAN DE VERBINTENIS"
6690 PRINTTAB(11);"WAARUIT I IS VOORTGEKOMEN."
6695 GOSUB1990
6700 PRINTS$:PRINT
6705 PRINT"EEN STAMBOOM MAKEN GAAT ALS VOLGT:"
6710 PRINT
6715 PRINT"1.MAAK EEN LIJST VAN PERSONEN EN GEEF"
6720 PRINT"   ZE EEN VOLGNUMMER."
6725 PRINT"   DAARNA NUMMER NIET MEER VERANDEREN."
6730 PRINT"   NIEUWE PERSONEN ACHTERAAN LATEN AAN- "
6735 PRINT"   SLUITEN."
6740 PRINT"2.MAAK EEN LIJST MET VERBINTENISSSEN  "
6745 PRINT"   DIE BOVENGENOEMDE PERSONEN HEBBEN."
6750 PRINT"   EVENEENS NUMMEREN EN VOLGORDE"
6755 PRINT"   NIET MEER VERANDEREN."
6760 PRINT"3.VUL NU ACHTER ELKE PERSOON 2 PARAME-"
6765 PRINT"   TERS IN. (GESLACHT, VBNR. OUDERS)"
6770 PRINT"   (0 ALS VERB. NIET BEKEND IS)."
6775 PRINT"4.DAARNA ACHTER ELKE VERBINTENIS DE"
6780 PRINT"   2 PARAMETERS INVULLEN.(MANNR,VROUWNR)"
6785 PRINT"5.ZET AAN HET BEGIN VAN DE DATA-LIJST "
6790 PRINT"   HET AANTAL VERBINTENISSEN EN HET"
6795 PRINT"   AANTAL PERSONEN DAT VOLGT."
6800 GOSUB1990
6805 RETURN
25000 REM* BETEKENIS DATA *
25010 REMLV = AANTAL VERBINTENISSEN
25020 REMLP = AANTAL PERSONEN
25030 REM
25040 REMPER VERBINTENIS:
25050 REM1. JAARTAL, ENZ.   VB$(I)
25060 REM2. PERSOONSNUMMER VAN DE MAN
25070 REM3. PERSOONSNUMMER VAN DE VROUW
25080 REM
25090 REMPER PERSOON:
25100 REM1. NAAM EN JAARTALLEN PE$(I)
25110 REM2. GESLACHT M=1/V=0
25120 REM3. VERBINTENISNUMMER WAARUIT
25130 REM   PERSOON IS GEBOREN
25140 REM
25150 REM FORSYTE-DATA
25160 REMLV EN LP
25170 DATA14,37
25180 DATA"1868        #1 X #2    V#1"
25190 DATA1,2      
25200 DATA"1880        #1 X #3    V#2"
25210 DATA1,3       
25220 DATA"1901        #1 X #4    V#3"
25230 DATA1,4       
25240 DATA"1883        #5 X #4    V#4"
25250 DATA5,4      
25260 DATA"1901        #5 X #6    V#5"
25270 DATA5,6       
25280 DATA"1879        #8 X #7    V#6"
25290 DATA8,7       
25300 DATA"1846       #14 X #15   V#7"
25310 DATA14,15      
25320 DATA"1852       #16 X #17   V#8"
25330 DATA16,17      
25340 DATA"1798       #31 X #32   V#9"
25350 DATA31,32       
25360 DATA"1768       #33 X #34   V#10"
25370 DATA33,34       
25380 DATA"1900       #26 X #11   V#11"
25390 DATA26,11      
25400 DATA"1906       #28 X #27   V#12"
25410 DATA28,27       
25420 DATA"1924       #12 X #35   V#13"
25430 DATA12,35      
25440 DATA"1920       #36 X #13   V#14"
25450 DATA36,13       
25460 REM
25470 REM
25480 DATA"1847-1920  JOLYON FORSYTE     #1"
25490 DATA1,7
25500 DATA"    -1880  FRANCES CRISSON    #2"
25510 DATA0,0
25520 DATA"    -1894  HELENE HILMER      #3"
25530 DATA0,0
25540 DATA"1863-      IRENE HERON        #4"
25550 DATA0,0    
25560 DATA"1855-1926  SOAMES FORSYTE     #5"
25570 DATA1,8
25580 DATA"1880-      ANNETTE LAMOTTE    #6"
25590 DATA0,0    
25600 DATA"1858-      WINIFRED FORSYTE   #7"
25610 DATA0,8
25620 DATA"           MONTAGUE DARTIE    #8"
25630 DATA1,0
25640 DATA"1869-      JUNE FORSYTE       #9"
25650 DATA0,1
25660 DATA"1879-1900  JOLLY FORSYTE      #10"
25670 DATA1,2
25680 DATA"1881-      HOLLY FORSYTE      #11"
25690 DATA0,2
25700 DATA"1901-      JON FORSYTE        #12"
25710 DATA1,3                            
25720 DATA"1903-      FLEUR FORSYTE      #13"
25730 DATA0,5
25740 DATA"1806-1892  JOLYON FORSYTE     #14"
25750 DATA1,9
25760 DATA"    -1874  EDITH MOOR         #15"
25770 DATA0,0
25780 DATA"1811-1901  JAMES FORSYTE      #16"
25790 DATA1,9
25800 DATA"1831-1910  EMILY GOLDING      #17"
25810 DATA0,0
25820 DATA"1799-1886  ANN FORSYTE        #18"
25830 DATA0,9
25840 DATA"1811-1891  SWITHIN FORSYTE    #19"
25850 DATA1,9
25860 DATA"1813-1899  ROGER FORSYTE      #20"
25870 DATA1,9
25880 DATA"1814-1905  JULEY FORSYTE      #21"
25890 DATA0,9
25900 DATA"1815-1907  HESTER FORSYTE     #22"
25910 DATA0,9
25920 DATA"1817-1908  NICHOLAS FORSYTE   #23"
25930 DATA1,9
25940 DATA"1819-1920  TIMOTHY FORSYTE    #24"
25950 DATA1,9
25960 DATA"1821-1895  SUSAN FORSYTE      #25"
25970 DATA0,9
25980 DATA"1880-      VAL DARTIE         #26"
25990 DATA1,6
26000 DATA"1882-      IMOGEN DARTIE      #27"
26010 DATA0,6
26020 DATA"           JACK CARDIGAN      #28"
26030 DATA1,0
26040 DATA"1910-      JOHN CARDIGAN      #29"
26050 DATA1,12
26060 DATA"1912-      JAMES CARDIGAN     #30"
26070 DATA1,12
26080 DATA"1770-1850  JOLYON FORSYTE     #31"
26090 DATA1,10
26100 DATA"           ANN PIERCE         #32"
26110 DATA0,0
26120 DATA"1741-1812  JOLYON FORSYTE     #33"
26130 DATA1,0
26140 DATA"           JULIA HAYTER       #34"
26150 DATA0,0
26160 DATA"           ANNE WILMOT        #35"
26170 DATA0,0
26180 DATA"           MICHAEL MONT       #36"
26190 DATA1,0
26200 DATA"1923-      CHRISTOPHER MONT   #37"
26210 DATA1,14
26220 REM
30000 REM*********************
30010 REM*  STAMBOOM         *
30020 REM*                   *
30030 REM*  HOBBYSCOOP       *
30040 REM*  FEBRUARI 1984    *
30050 REM*                   *
30060 REM*  GEMAAKT OP       *
30070 REM*  APPLE II +       *
30080 REM*                   *
30090 REM* W. KOLBER         *
30100 REM* HERTENRADE 111    *
30110 REM* 2544 HE  DEN HAAG *
30120 REM* 070 - 664420      *
30130 REM*********************  
30140 REM
30150 REM COPYRIGHT  NOS HILVERSUM

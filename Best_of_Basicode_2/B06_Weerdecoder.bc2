
1000 A=2000: GOTO20:REM WEERDECODER
1010  GOSUB100
1020 PRINT"DIT PROGRAMMA ZET DE WEERCODE UIT-"
1030 PRINT:PRINT"GEZONDEN DOOR METEOSTATIONS VIA TELEX-"
1040 PRINT:PRINT"IN VORM VAN CIJFERS, OM IN LEESBAAR"
1050 PRINT:PRINT"SCHRIFT."
1060 PRINT:PRINT"UITERAARD DIENT U TE BESCHIKKEN OVER"
1070 PRINT:PRINT"EEN TELEXCONVERTER."
1080 PRINT:PRINT"DIT DECODEERPROGRAMMA ZET ENKEL"
1090 PRINT:PRINT"DE CODE-SYNOP-A A X X - OM, DUS VAN"
1100 PRINT:PRINT"LANDSTATIONS OP STATIONSNIVEAU"
1110 PRINT:PRINT:PRINT"             DRUK EEN TOETS"
1120  GOSUB210
1130  GOSUB100
1140 PRINT"DE STATIONS IN DENEMARKEN, BELGIE,"
1150 PRINT:PRINT"NEDERLAND,LUXEMBURG,ZWITSERLAND EN"
1160 PRINT:PRINT"LICHTENSTEIN ZIJN UITGESPLITST."
1170 PRINT:PRINT"DE REST VAN DE WERELD IS OPGEDEELD IN"
1180 PRINT:PRINT"GROEPEN VAN LANDEN VERMELD ONDER -II-"
1190 PRINT:PRINT"IN DE EERSTE REGEL."
1200 PRINT:PRINT"HET ZOEKEN VAN HET STATION VERGT WAT"
1210 PRINT:PRINT"TIJD, DUS GEDULD A.U.B."
1220 PRINT:PRINT:PRINT:PRINT"             DRUK EEN TOETS"
1230  GOSUB210: GOSUB100
1240 PRINT:PRINT"  OMZETTER WEERBERICHT-CODE-SYNOP-AAXX"
1250 PRINT:PRINT" VOER DE CIJFERS IN ZONDER SPATIE"
1260 PRINT:PRINT" TOT DE CIJFERGROEP 333":PRINT:PRINT
1270 INPUTW$
1280 IFLEN(W$)=2 THENV=1: GOTO1500
1290 IFLEN(W$)=5 THENV=2: GOTO1500
1300 IFLEN(W$)=8 THENV=3: GOTO1500
1310 IFLEN(W$)=10 THENV=4: GOTO1500
1320 IFLEN(W$)=11 THENV=5: GOTO1500
1330 IFLEN(W$)=13 THENV=6: GOTO1500
1340 IFLEN(W$)=15 THENV=7: GOTO1500
1350 IFLEN(W$)=20 THENV=8: GOTO1500
1360 IFLEN(W$)=25 THENV=9: GOTO1500
1370 IFLEN(W$)=30 THENV=10: GOTO1500
1380 IFLEN(W$)=35 THENV=11: GOTO1500
1390 IFLEN(W$)=37 THENV=12: GOTO1500
1400 IFLEN(W$)=40 THENV=13: GOTO1500
1410 IFLEN(W$)=44 THENV=14: GOTO1500
1420 IFLEN(W$)=45 THENV=15: GOTO1500
1430 IFLEN(W$)=48 THENV=16: GOTO1500
1440 IFLEN(W$)=49 THENV=17: GOTO1500
1450 IFLEN(W$)=50 THENV=18: GOTO1500
1460 IFLEN(W$)=52 THENV=19: GOTO1500
1470 IFLEN(W$)=53 THENV=20: GOTO1500
1480 IFLEN(W$)=54 THENV=21: GOTO1500
1490 IFLEN(W$)=55 THENV=22
1500 FORZ=1 TOV
1510 IFZ=1 THENQ=1:P=2
1520 IFZ=2 THENQ=3:P=3
1530 IFZ=3 THENQ=8:P=1
1540 IFZ=4 THENQ=9:P=2
1550 IFZ=5 THENQ=11:P=1
1560 IFZ=6 THENQ=12:P=2
1570 IFZ=7 THENQ=14:P=2
1580 IFZ=8 THENQ=17:P=4
1590 IFZ=9 THENQ=22:P=4
1600 IFZ=10 THENQ=27:P=4
1610 IFZ=11 THENQ=32:P=4
1620 IFZ=12 THENQ=37:P=1
1630 IFZ=13 THENQ=38:P=3
1640 IFZ=14 THENQ=42:P=4
1650 IFZ=15 THENQ=45:P=1
1660 IFZ=16 THENQ=47:P=2
1670 IFZ=17 THENQ=49:P=1
1680 IFZ=18 THENQ=50:P=1
1690 IFZ=19 THENQ=51:P=2
1700 IFZ=20 THENQ=53:P=1
1710 IFZ=21 THENQ=54:P=1
1720 IFZ=22 THENQ=55:P=1
1730 L$=MID$(W$,Q,P)
1740 IFZ=1 THEN GOSUB1980
1750 IFZ=2 THEN GOSUB2660
1760 IFZ=3 THEN GOSUB2810
1770 IFZ=4 THEN GOSUB2940
1780 IFZ=5 THEN GOSUB3220
1790 IFZ=6 THEN GOSUB3340
1800 IFZ=7 THEN GOSUB3570
1810 IFZ=8 THEN GOSUB3650
1820 IFZ=9 THEN GOSUB3740
1830 IFZ=10 THEN GOSUB3830
1840 IFZ=11 THEN GOSUB3930
1850 IFZ=12 THEN GOSUB4020
1860 IFZ=13 THEN GOSUB4140
1870 IFZ=14 THEN GOSUB4230
1880 IFZ=15 THEN GOSUB4320
1890 IFZ=16 THEN GOSUB4340
1900 IFZ=17 THEN GOSUB4630
1910 IFZ=18 THEN GOSUB4630
1920 IFZ=19 THEN GOSUB4750
1930 IFZ=20 THEN GOSUB4910
1940 IFZ=21 THEN GOSUB5040
1950 IFZ=22 THEN GOSUB5170
1960 NEXTZ
1970 END
1980  GOSUB100:PRINT"II";
1990 A$=L$
2000 K=VAL(A$):PRINTTAB(5);
2010 IFK=1 THENPRINT"NOORWEGEN"
2020 IFK=2 THENPRINT"ZWEDEN/FINLAND"
2030 IFK=3 THENPRINT"ENGELAND/IERLAND"
2040 IFK=4 THENPRINT"IJSLAND/GROENLAND"
2050 IFK=6 THENPRINT"DENEMARKEN/BENELUX/ZWITSERLAND"
2060 IFK=7 THENPRINT"FRANKRIJK"
2070 IFK=8 THENPRINT"SPANJE/PORTUGAL/FRANKRIJK"
2080 IFK=9 THENPRINT"OOST-DUITSLAND"
2090 IFK=10 THENPRINT"WEST-DUITSLAND"
2100 IFK=11 THENPRINT"OOSTENRIJK/TJECHO-SLOWAKIJE"
2110 IFK=12 THENPRINT"POLEN/HONGARIJE"
2120 IFK=13 THENPRINT"YOUGO-SLAVIE/ALBANIE"
2130 IFK=15 THENPRINT"ROEMENIE/BULGARIJE"
2140 IFK=16 THENPRINT"ITALIE/GRIEKENLAND"
2150 IFK=17 THENPRINT"TURKIJE/CYPRUS"
2160 IF(K>=20) AND(K<=39) THENPRINT"RUSLAND (EUROPA/AZIE)"
2170 IFK=40 THENPRINT"SYRIE/LIBANON/ISRAEL/JORDANIE"
2180 IFK=40 THENPRINT"SAUDIE-ARABIE/KOEWEIT/IRAK/IRAN"
2190 IFK=40 THENPRINT"AFGANISTAN"
2200 IFK=41 THENPRINT"SAUDI-ARABIE/BAHRAIN/QATOR/OMAN"
2210 IFK=41 THENPRINT"VER. ARAB. EMIRATEN/YEMEN"
2220 IFK=41 THENPRINT"PAKISTAN/BANGLADESH"
2230 IFK=42 THENPRINT"INDIA"
2240 IFK=43 THENPRINT"INDIA/SRI-LANKA/MALEDIVEN"
2250 IFK=44 THENPRINT"MONGOLIE/NEPAL"
2260 IFK=45 THENPRINT"HONGKONG/MACAO"
2270 IFK=47 THENPRINT"KOREA/JAPAN"
2280 IFK=48 THENPRINT"BIRMA/THAILAND/LAOS/CAMBODJA"
2290 IFK=48 THENPRINT"MALEISE FEDERATIE/VIETNAM"
2300 IFK=48 THENPRINT"SINGAPORE"
2310 IF(K>=50) AND(K<=59) THENPRINT"CHINA"
2320 IFK=60 THENPRINT"CANARISCHE EILANDEN/MAROKKO"
2330 IFK=60 THENPRINT"WEST-SAHARA/ALGERYE/TUNESIE"
2340 IFK=61 THENPRINT"NIGERIA/MALI/MAURITANIE"
2350 IFK=61 THENPRINT"SENEGAL/OCEAANEILANDEN"
2360 IFK=62 THENPRINT"LIBIE/EGYPTE/SUDAN"
2370 IFK=63 THENPRINT"ETHIOPIE/SOMALIE/OEGANDA"
2380 IFK=64 THENPRINT"ZAIRE/KONGO/CHAAD/GABON"
2390 IFK=65 THENPRINT"NIGERIA/BENIN/TOGA/GHANA"
2400 IFK=66 THENPRINT"ANGOLA"
2410 IFK=67 THENPRINT"MADAGASCAR/ZAMBIA/ZIMBABWE"
2420 IFK=68 THENPRINT"ZUID-AFRIKA"
2430 IFK=70 THENPRINT"ALASKA"
2440 IFK=71 THENPRINT"CANADA"
2450 IFK=72 THENPRINT"VER. STATEN VAN AMERIKA"
2460 IFK=74 THENPRINT"VER. STATEN VAN AMERIKA"
2470 IFK=76 THENPRINT"MEXICO"
2480 IFK=78 THENPRINT"BAHAMA'S/CUBA/BERMUDA"
2490 IFK=78 THENPRINT"GUATEMALA/HAITI/EL-SAVADOR"
2500 IFK=80 THENPRINT"COLOMBIA/VENEZUELA"
2510 IFK=81 THENPRINT"SURINAME/GUYANA/FR. GUYANA"
2520 IF(K>=82) AND(K<=83) THENPRINT"BRAZILIE"
2530 IFK=84 THENPRINT"ECUADOR/PERU"
2540 IFK=85 THENPRINT"BOLIVIA/CHILI"
2550 IFK=86 THENPRINT"PARAGUYA/URUGUYA"
2560 IFK=88 THENPRINT"EILANDEN (GEBIED ANTARTICA)"
2570 IFK=89 THENPRINT"ANTARTICA"
2580 IFK=93 THENPRINT"NIEUW-ZEELAND"
2590 IF(K>=94) AND(K<=95) THENPRINT"AUSTRALIE/PAP.N.GUINEA"
2600 IF(K>=96) AND(K<=97) THENPRINT"INDONESIE"
2610 IFK=98 THENPRINT"FHILIPIJNEN"
2620 RETURN
2630 PRINTTAB(5);"BLOKNR: ";MID$(W$,1,2);
2640 PRINT"       STATION ";MID$(W$,3,3)
2650 RETURN
2660 IFMID$(W$,1,2)<>"06" THENPRINT"III";
2670 IFMID$(W$,1,2)<>"06" THEN2630
2680 IFMID$(W$,1,2)<>"06" THENRETURN
2690 DIMA$(199),B$(199)
2700 FORT=1 TO182
2710 READA$(T)
2720 NEXTT
2730 FORT=1 TO182
2740 READB$(T)
2750 NEXTT
2760 FORT=1 TO182
2770 IFA$(T)=L$ THEN2790
2780 NEXTT
2790 PRINT"III  STAT.:";B$(T)
2800 RETURN
2810 PRINT"H";TAB(5);"WOLKEN: ";
2820 IFL$="/" THENPRINT"ONBEKEND OF WOLKEN HOGER"
2830 IFL$="0" THENPRINT"0-50 METER"
2840 IFL$="1" THENPRINT"50-100 METER"
2850 IFL$="2" THENPRINT"100-200 METER"
2860 IFL$="3" THENPRINT"200-300 METER"
2870 IFL$="4" THENPRINT"300-600 METER"
2880 IFL$="5" THENPRINT"600-1000 METER"
2890 IFL$="6" THENPRINT"1000-1500 METER"
2900 IFL$="7" THENPRINT"1500-2000 METER"
2910 IFL$="8" THENPRINT"2000-2500 METER"
2920 IFL$="9" THENPRINT">2500 METER OF GEEN WOLKEN"
2930 RETURN
2940 PRINT"VV";TAB(5);"HOR.ZICHT: ";
2950 A$=L$
2960 K=VAL(A$)
2970 IFK=00 THENPRINT"MINDER DAN 0,1 K.M."
2980 IF(K>=01) AND(K<=50) THENN=K/10
2990 IF(K>=01) AND(K<=50) THENPRINTN;" K.M."
3000 IF(K>=56) AND(K<=80) THENN=K-50
3010 IF(K>=56) AND(K<=80) THENPRINTN;" K.M."
3020 IFK=81 THENPRINT"35 K.M."
3030 IFK=82 THENPRINT"40 K.M."
3040 IFK=83 THENPRINT"45 K.M."
3050 IFK=84 THENPRINT"50 K.M."
3060 IFK=85 THENPRINT"55 K.M."
3070 IFK=86 THENPRINT"60 K.M."
3080 IFK=87 THENPRINT"65 K.M."
3090 IFK=88 THENPRINT"70 K.M."
3100 IFK=89 THENPRINT"MEER DAN 70 K.M."
3110 IFK=90 THENPRINT"MINDER DAN 0.05 K.M."
3120 IFK=91 THENPRINT"0.05 K.M."
3130 IFK=92 THENPRINT"0.2 K.M."
3140 IFK=93 THENPRINT"0.5 K.M."
3150 IFK=94 THENPRINT"1 K.M."
3160 IFK=95 THENPRINT"2 K.M."
3170 IFK=96 THENPRINT"4 K.M."
3180 IFK=97 THENPRINT"10 K.M."
3190 IFK=98 THENPRINT"20 K.M."
3200 IFK=99 THENPRINT"MEER DAN 50 K.M."
3210 RETURN
3220 PRINT"N";TAB(5);"BEDEKKINGSGRAAD: ";
3230 IFL$="0" THENPRINT"ONBEWOLKT"
3240 IFL$="1" THENPRINT"1/8 DEEL BEWOLKT"
3250 IFL$="2" THENPRINT"2/8 DEEL BEWOLKT"
3260 IFL$="3" THENPRINT"3/8 DEEL BEWOLKT"
3270 IFL$="4" THENPRINT"4/8 DEEL BEWOLKT"
3280 IFL$="5" THENPRINT"5/8 DEEL BEWOLKT"
3290 IFL$="6" THENPRINT"6/8 DEEL BEWOLKT"
3300 IFL$="7" THENPRINT"7/8 DEEL BEWOLKT"
3310 IFL$="8" THENPRINT"8/8 DEEL BEWOLKT"
3320 IFL$="/" THENPRINT"GEEN OPGAVE"
3330 RETURN
3340 PRINT"DD";TAB(5);"WINDRICHTING: ";
3350 A$=L$
3360 K=VAL(A$)
3370 IFK=00 THENPRINT"WINDSTIL"
3380 IFK=99 THENPRINT"VERAND. WINDRICHTING"
3390 IF(K>=01) AND(K<03) THENPRINT"NOORD-NOORD-OOST"
3400 IF(K>=03) AND(K<05) THENPRINT"NOORD-OOST"
3410 IF(K>=05) AND(K<08) THENPRINT"OOST-NOORD-OOST"
3420 IF(K>=08) AND(K<10) THENPRINT"OOSTENWIND"
3430 IF(K>=10) AND(K<12) THENPRINT"OOST-ZUID-OOST"
3440 IF(K>=12) AND(K<15) THENPRINT"ZUID-OOST"
3450 IF(K>=15) AND(K<17) THENPRINT"ZUID-ZUID-OST"
3460 IF(K>=17) AND(K<19) THENPRINT"ZUIDENWIND"
3470 IF(K>=19) AND(K<21) THENPRINT"ZUID-ZUID-WEST"
3480 IF(K>=21) AND(K<24) THENPRINT"ZUID-WEST"
3490 IF(K>=24) AND(K<26) THENPRINT"WEST-ZUID-WEST"
3500 IF(K>=26) AND(K<28) THENPRINT"WESTENWIND"
3510 IF(K>=28) AND(K<30) THENPRINT"WEST-NOORD-WEST"
3520 IF(K>=30) AND(K<33) THENPRINT"NOORD-WEST"
3530 IF(K>=33) AND(K<35) THENPRINT"NOORD-NOORD-WEST"
3540 IF(K>=35) AND(K<=36) THENPRINT"NOORDENWIND"
3550 IF(K>00) AND(K<01) THENPRINT"NOORDENWIND"
3560 RETURN
3570 PRINT"FF";TAB(5);"WINDSNELHEID: ";
3580 A$=L$
3590 K=VAL(A$)
3600 IFK=00 THENPRINT"WINDSTIL"
3610 IFK=00 THEN3640
3620 N=K*.5
3630 PRINTN;" METER PER SEC."
3640 RETURN
3650 PRINT"TTT";TAB(5);"TEMP.: ";
3660 IFL$="///" THENPRINT"GEEN OPGAVE"
3670 M$=MID$(L$,1,1)
3680 N$=MID$(L$,2,1)
3690 NO$=MID$(L$,3,1)
3700 P$=MID$(L$,4,1)
3710 IFM$="0" THENPRINT"+ ";N$;NO$;",";P$;" GR.CELCIUS"
3720 IFM$="1" THENPRINT"- ";N$;NO$;",";P$;" GR.CELCIUS"
3730 RETURN
3740 PRINT"TD";TAB(5);"DAUWTEMP.: ";
3750 M$=MID$(L$,1,1)
3760 N$=MID$(L$,2,1)
3770 NO$=MID$(L$,3,1)
3780 P$=MID$(L$,4,1)
3790 IFM$="0" THENPRINT"+ ";N$;NO$;",";P$;" GR.CELCIUS"
3800 IFM$="1" THENPRINT"- ";N$;NO$;",";P$;" GR.CELCIUS"
3810 IFM$="9" THENPRINT"GEEN OPGAVE"
3820 RETURN
3830 IFMID$(W$,26,1)="4" THEN5620
3840 PRINT"PO";TAB(5);"DRUK  ZEENIVEAU ";
3850 M$=MID$(L$,1,1)
3860 N$=MID$(L$,2,1)
3870 NO$=MID$(L$,3,1)
3880 P$=MID$(L$,4,1)
3890 K=VAL(M$)
3900 IFK<6 THENPRINT"1";M$;N$;NO$;",";P$;" millibar"
3910 IFK>=6 THENPRINTM$;N$;NO$;",";P$;" millibar"
3920 RETURN
3930 PRINT"PP";TAB(5);"DRUK STAT.NIVEAU ";
3940 M$=MID$(L$,1,1)
3950 N$=MID$(L$,2,1)
3960 NO$=MID$(L$,3,1)
3970 P$=MID$(L$,4,1)
3980 K=VAL(M$)
3990 IFK<6 THENPRINT"1";M$;N$;NO$;",";P$;" millibar"
4000 IFK>=6 THENPRINTM$;N$;NO$;",";P$;" millibar"
4010 RETURN
4020 PRINT"A";TAB(5);"DRUK: ";
4030 IFL$="/" THENPRINT"GEEN OPGAVE"
4040 IFL$="0" THENPRINT"GELIJK AAN OF WEINIG GESTEGEN"
4050 IFL$="1" THENPRINT"GESTEGEN"
4060 IFL$="2" THENPRINT"GESTEGEN"
4070 IFL$="3" THENPRINT"GESTEGEN"
4080 IFL$="4" THENPRINT"GELIJK GEBLEVEN"
4090 IFL$="5" THENPRINT"GELIJK GEBLEVEN OF WEINIG GEDAALD"
4100 IFL$="6" THENPRINT"DALEND"
4110 IFL$="7" THENPRINT"DALEND"
4120 IFL$="8" THENPRINT"DALEND"
4130 RETURN
4140 PRINT"PP";TAB(5);"VERSCHIL IN 3 UUR: ";
4150 IFL$="///" THENPRINT"GEEN OPGAVE"
4160 A$=L$
4170 K=VAL(A$)
4180 N=K/10
4190 PRINTN;" MILLIBAR"
4200 IFLEN(W$)=35 THENEND
4210  GOSUB6030
4220 RETURN
4230 PRINT"RR";
4240 M$=MID$(L$,1,1)
4250 N$=MID$(L$,2,1)
4260 NO$=MID$(L$,3,1)
4270 P$=MID$(L$,4,1)
4280 A=VAL(M$):B=VAL(N$):C=VAL(NO$):D=VAL(P$)
4290 IFA=9 THENA=0:IFB=9 THENB=0:IFC=9 THENC=0
4300 PRINTTAB(5);"REGEN IN ";D;" UUR ";A;B;",";C;" M.M."
4310 RETURN
4320 REM BESTEMD VOOR EEN TIJDSAANDUIDING
4330 RETURN
4340 PRINT"WW";TAB(5);"WEER: ";
4350 A$=L$
4360 K=VAL(A$)
4370 IFK=01 THENPRINT"BEWOLKING AFGENOMEN"
4380 IFK=02 THENPRINT"UITERLYK HEMEL ONVERANDERD"
4390 IFK=03 THENPRINT"BEWOLKING TOEGENOMEN"
4400 IF(K>=04) AND(K<=09) THENPRINT"HEIIG,ZAND,STOF OF ROOK"
4410 IFK=10 THENPRINT"NEVEL"
4420 IF(K>=11) AND(K<=12) THENPRINT"GRONDMIST/LAAGHANGENDE MIST"
4430 IFK=13 THENPRINT"WEERLICHT"
4440 IF(K>=14) AND(K<=16) THENPRINT"NEERSLAG BINNEN GEZICHTSKRING"
4450 IFK=17 THENPRINT"ONWEER ZONDER NEERSLAG"
4460 IFK=18 THENPRINT"ZWARE WINDSTOTEN"
4470 IFK=19 THENPRINT"WATER OF WINDHOOS"
4480 IF(K>=20) AND(K<=27) THENPRINT"NEERSLAG IN AFGELOPEN UUR"
4490 IFK=28 THENPRINT"MIST IN HET AFGELOPEN UUR"
4500 IFK=29 THENPRINT"ONWEER IN HET AFGELOPEN UUR"
4510 IF(K>=40) AND(K<=49) THENPRINT"MIST"
4520 IF(K>=50) AND(K<=55) THENPRINT"MOTREGEN"
4530 IF(K>=56) AND(K<=57) THENPRINT"MOTREGEN MET IJZEL"
4540 IF(K>=58) AND(K<=59) THENPRINT"MOTREGEN MET REGEN"
4550 IF(K>=60) AND(K<=65) THENPRINT"REGEN"
4560 IF(K>=66) AND(K<=67) THENPRINT"REGEN MET IJZEL"
4570 IF(K>=68) AND(K<=69) THENPRINT"REGEN MET SNEEUW"
4580 IF(K>=70) AND(K<=78) THENPRINT"SNEEUW"
4590 IFK=79 THENPRINT"IJSREGEN (BEVROREN SNEEUW)
4600 IF(K>=80) AND(K<=90) THENPRINT"BUIEN"
4610 IF(K>=91) AND(K<=99) THENPRINT"ONWEER"
4620 RETURN
4630 PRINT"W";TAB(5);"W1+W2 ";
4640 IFL$="0" THENPRINT"MINDER DAN 4/8 BEWOLKT"
4650 IFL$="1" THENPRINT"SOMS MEER/MINDER DAN 4/8 BEWOLKT"
4660 IFL$="2" THENPRINT"MEER DAN 4/8 BEWOLKT"
4670 IFL$="3" THENPRINT"ZAND/STOF OF SNEEUWSTORM"
4680 IFL$="4" THENPRINT"MIST"
4690 IFL$="5" THENPRINT"MOTREGEN"
4700 IFL$="6" THENPRINT"REGEN"
4710 IFL$="7" THENPRINT"SNEEUW"
4720 IFL$="8" THENPRINT"BUI OF BUIEN"
4730 IFL$="9" THENPRINT"ONWEER MET/ZONDER NEERSLAG"
4740 RETURN
4750 NO$=MID$(L$,2,1)
4760 P$=MID$(L$,1,1)
4770 IFP$<>"8" THENEND
4780 PRINT"NH"TAB(5);"CL/CM : ";
4790 IFNO$="0" THENPRINT"ONBEWOLKT"
4800 IFNO$="1" THENPRINT"LICHT BEWOLKT"
4810 IFNO$="2" THENPRINT"LICHT BEWOLKT"
4820 IFNO$="3" THENPRINT"HALF BEWOLKT"
4830 IFNO$="4" THENPRINT"HALF BEWOLKT"
4840 IFNO$="5" THENPRINT"HALF BEWOLKT"
4850 IFNO$="6" THENPRINT"ZWAAR BEWOLKT"
4860 IFNO$="7" THENPRINT"ZWAAR BEWOLKT"
4870 IFNO$="8" THENPRINT"GEHEEL BEWOLKT"
4880 IFNO$="9" THENPRINT"ONBEKEND"
4890 IFNO$="/" THENPRINT"GEEN OPGAVE"
4900 RETURN
4910 PRINT"CL";TAB(5);"";
4920 IFL$="/" THENPRINT"ONZICHTBAAR DOORDONKER/MIST/STOF"
4930 IFL$="0" THENPRINT"NIET AANWEZIG"
4940 IFL$="1" THENPRINT"AANWEZIG"
4950 IFL$="2" THENPRINT"AANWEZIG"
4960 IFL$="3" THENPRINT"AANWEZIG"
4970 IFL$="4" THENPRINT"AANWEZIG"
4980 IFL$="5" THENPRINT"AANWEZIG"
4990 IFL$="6" THENPRINT"STERK AANWEZIG"
5000 IFL$="7" THENPRINT"STERK AANWEZIG/SLECHT WWER"
5010 IFL$="8" THENPRINT"STERK AANWEZIG"
5020 IFL$="9" THENPRINT"STERK AANWEZIG"
5030 RETURN
5040 PRINT"CM";TAB(5);"";
5050 IFL$="/" THENPRINT"ONZICHTBAAR DOOR DONKER/WOLKEN"
5060 IFL$="0" THENPRINT"NIET AANWEZIG"
5070 IFL$="1" THENPRINT"AANWEZIG"
5080 IFL$="2" THENPRINT"AANWEZIG"
5090 IFL$="3" THENPRINT"AANWEZIG"
5100 IFL$="4" THENPRINT"AANWEZIG"
5110 IFL$="5" THENPRINT"AANWEZIG"
5120 IFL$="6" THENPRINT"AANWEZIG"
5130 IFL$="7" THENPRINT"AANWEZIG"
5140 IFL$="8" THENPRINT"AANWEZIG"
5150 IFL$="9" THENPRINT"AANWEZIG"
5160 RETURN
5170 PRINT"CH";TAB(5);"";
5180 IFL$="/" THENPRINT"ONZICHTBAAR DOOR DONKER/WOLKEN"
5190 IFL$="0" THENPRINT"NIET AANWEZIG"
5200 IFL$="1" THENPRINT"AANWEZIG"
5210 IFL$="2" THENPRINT"AANWEZIG"
5220 IFL$="3" THENPRINT"AANWEZIG"
5230 IFL$="4" THENPRINT"AANWEZIG"
5240 IFL$="5" THENPRINT"AANWEZIG"
5250 IFL$="6" THENPRINT"AANWEZIG"
5260 IFL$="7" THENPRINT"AANWEZIG"
5270 IFL$="8" THENPRINT"AANWEZIG"
5280 IFL$="9" THENPRINT"AANWEZIG"
5290 END
5300 Z=1
5310 FORZ=1 TO5
5320 IFZ=1 THENQ=36:IFZ=1 THENP=2
5330 IFZ=2 THENQ=38:IFZ=2 THENP=1
5340 IFZ=3 THENQ=39:IFZ=3 THENP=1
5350 IFZ=4 THENQ=40:IFZ=4 THENP=1
5360 Q=Q+S:L$=MID$(W$,Q,P)
5370 IFZ=1 THEN GOSUB4750
5380 IFZ=2 THEN GOSUB4910
5390 IFZ=3 THEN GOSUB5040
5400 IFZ=4 THEN GOSUB5170
5410 NEXTZ
5420 END
5430 Z=1
5440 FORZ=1 TO8
5450 IFZ=1 THENQ=37:IFZ=1 THENP=2
5460 IFZ=2 THENQ=39:IFZ=2 THENP=1
5470 IFZ=3 THENQ=40:IFZ=3 THENP=1
5480 IFZ=4 THENQ=41:IFZ=4 THENP=2
5490 IFZ=5 THENQ=43:IFZ=5 THENP=1
5500 IFZ=6 THENQ=44:IFZ=6 THENP=1
5510 IFZ=7 THENQ=45:IFZ=7 THENP=1
5520 Q=Q+S:L$=MID$(W$,Q,P)
5530 IFZ=1 THEN GOSUB4340
5540 IFZ=2 THEN GOSUB4630
5550 IFZ=3 THEN GOSUB4630
5560 IFZ=4 THEN GOSUB4750
5570 IFZ=5 THEN GOSUB4910
5580 IFZ=6 THEN GOSUB5040
5590 IFZ=7 THEN GOSUB5170
5600 NEXTZ
5610 END
5620 IFLEN(W$)=30 THENV=1: GOTO5740
5630 IFLEN(W$)=32 THENV=2: GOTO5740
5640 IFLEN(W$)=35 THENV=3: GOTO5740
5650 IFLEN(W$)=39 THENV=4: GOTO5740
5660 IFLEN(W$)=40 THENV=5: GOTO5740
5670 IFLEN(W$)=43 THENV=6: GOTO5740
5680 IFLEN(W$)=44 THENV=7: GOTO5740
5690 IFLEN(W$)=45 THENV=8: GOTO5740
5700 IFLEN(W$)=47 THENV=9: GOTO5740
5710 IFLEN(W$)=48 THENV=10: GOTO5740
5720 IFLEN(W$)=49 THENV=11: GOTO5740
5730 IFLEN(W$)=50 THENV=12: GOTO5740
5740 Z=1
5750 FORZ=1 TOV
5760 IFZ=1 THENQ=27:P=4
5770 IFZ=2 THENQ=32:P=1
5780 IFZ=3 THENQ=33:P=3
5790 IFZ=4 THENQ=37:P=4
5800 IFZ=5 THENQ=40:P=1
5810 IFZ=6 THENQ=43:P=2
5820 IFZ=7 THENQ=44:P=1
5830 IFZ=8 THENQ=45:P=1
5840 IFZ=9 THENQ=47:P=1
5850 IFZ=10 THENQ=48:P=1
5860 IFZ=11 THENQ=49:P=1
5870 IFZ=12 THENQ=50:P=1
5880 L$=MID$(W$,Q,P)
5890 IFZ=1 THEN GOSUB3930
5900 IFZ=2 THEN GOSUB4020
5910 IFZ=3 THEN GOSUB4140
5920 IFZ=4 THEN GOSUB4230
5930 IFZ=5 THEN GOSUB4320
5940 IFZ=6 THEN GOSUB4340
5950 IFZ=7 THEN GOSUB4630
5960 IFZ=8 THEN GOSUB4630
5970 IFZ=9 THEN GOSUB4750
5980 IFZ=10 THEN GOSUB4910
5990 IFZ=11 THEN GOSUB5040
6000 IFZ=12 THEN GOSUB5170
6010 NEXTZ
6020 END
6030 S=0:IFMID$(W$,26,1)="3" THENS=5
6040 IFMID$(W$,7,1)="2" THEN GOSUB6170
6050 IFMID$(W$,7,1)="2" THEN5300
6060 IFMID$(W$,7,1)="3" THEN GOSUB6190
6070 IFMID$(W$,7,1)="3" THEN5300
6080 IFMID$(W$,7,1)="5" THEN GOSUB6170
6090 IFMID$(W$,7,1)="5" THEN5300
6100 IFMID$(W$,7,1)="6" THEN GOSUB6170
6110 IFMID$(W$,7,1)="6" THEN5300
6120 IFMID$(W$,6,1)="3" THEN GOSUB6210
6130 IFMID$(W$,6,1)="3" THEN5430
6140 IFMID$(W$,6,1)="4" THEN GOSUB6210
6150 IFMID$(W$,6,1)="4" THEN5430
6160 RETURN
6170 PRINTTAB(5);"GEEN RAPPORT WW/W1/W2"
6180 RETURN
6190 PRINTTAB(5);"GEEN OPGAVE WW/W1/W2"
6200 RETURN
6210 PRINTTAB(5);"GEEN OPGAVE NEERSLAG"
6220 RETURN
25000 DATA"009","010","011","023","024","030"
25010 DATA"034","041","045","048","052","053"
25020 DATA"054","059","060","062","065","066"
25030 DATA"070","071","073","074","076","080"
25040 DATA"081","084","087","089","096","099"
25050 DATA"100","104","106","110","111","115"
25060 DATA"118","119","120","122","123","124"
25070 DATA"128","131","139","144","147","150"
25080 DATA"151","152","156","159","160","161"
25090 DATA"162","170","173","179","180","181"
25100 DATA"182","183","186","190","191","193"
25110 DATA"196","200","210","225","235","240"
25120 DATA"242","250","260","265","268","270"
25130 DATA"275","280","290","310","325","330"
25140 DATA"344","350","370","375","380","385"
25150 DATA"400","404","407","408","416","428"
25160 DATA"431","432","447","449","450","451"
25170 DATA"452","456","458","463","465","470"
25180 DATA"473","476","478","479","480","481"
25190 DATA"490","495","585","589","590","597"
25200 DATA"604","608","610","612","616","630"
25210 DATA"632","642","643","649","652","654"
25220 DATA"657","660","662","669","670","672"
25230 DATA"680","681","684","700","702","704"
25240 DATA"710","712","718","719","720","721"
25250 DATA"724","726","730","732","734","742"
25260 DATA"743","746","750","753","755","759"
25270 DATA"760","762","770","771","781","782"
25280 DATA"783","786","787","789","791","792"
25290 DATA"793","794","990","220","245","300"
25300 DATA"320","340"
25310 DATA"(DEN.)AKRABERG","(DEN.)SORVAAG/VAGAR"
25320 DATA"(DEN.)THORSHAVN","(DEN.)GORM"
25330 DATA"(DEN.)THISTED FLYVEPLADS","(DEN.)ALBORG"
25340 DATA"(DEN.)SINDAL FLYVEPLADS","(DEN.)SKAGEN"
25350 DATA"(DEN.)DYBVAD","(DEN.)HADSUND"
25360 DATA"(DEN.)THYBORON","(DEN.)AGGERSUND"
25370 DATA"(DEN.)LINDTORP FLYVEPLADS","(DEN.)LYNGVIG"
25380 DATA"(DEN.)KARUP","(DEN.)SKIVE FLYVEPLADS"
25390 DATA"(DEN.)ALESTRUP","(DEN.)STAUNING FLYVEPLADS"
25400 DATA"(DEN.)TIRSTRUP","(DEN.)FORNAES"
25410 DATA"(DEN.)SLETTERHAGE"
25420 DATA"(DEN.)FLYVEPLADS KIRSTINESMINDE"
25430 DATA"(DEN.)HADSTEN","(DEN.)ESBJERG"
25440 DATA"(DEN.)BLAVAND","(DEN.)HERNING FLYVEPLADS"
25450 DATA"(DEN.)ANHOLT KNOB","(DEN.)SEADENSTRAND"
25460 DATA"(DEN.)ROMO","(DEN.)ROMO HAVNEBY"
25470 DATA"(DEN.)VANDEL","(DEN.)BILLUND LUFTHAVN"
25480 DATA"(DEN.)TVINGSTRUP","(DEN.)SKRYDSTRUP"
25490 DATA"(DEN.)BAGO","(DEN.)MOLLEHUS"
25500 DATA"(DEN.)SONDERBORG FLYVEPLADS"
25510 DATA"(DEN.)KEGNAES","(DEN.)ODENSE/BELDRINGE"
25520 DATA"(DEN.)SKARUP","(DEN.)TVINGSDJERG"
25530 DATA"(DEN.)SYDFYNS FLYVEPLADS","(DEN.)MIDDELFART"
25540 DATA"(DEN.)VESBORG","(DEN.)KELDSNOR"
25550 DATA"(DEN.)MARIBO FLYVEPLADS","(DEN.)MON SYDOST"
25560 DATA"(DEN.)AVNO","(DEN.)OMO","(DEN.)GLUMSO"
25570 DATA"(DEN.)HOLBAEK","(DEN.)ROSNAES"
25580 DATA"(DEN.)VAERLOSE","(DEN.)SPODSBJERG"
25590 DATA"(DEN.)KRONBORG","(DEN.)KOBENHAVN/ROSKILDE"
25600 DATA"(DEN.)STEVNS","(DEN.)MON"
25610 DATA"(DEN.)KOBENHAVN/KASTRUP"
25620 DATA"(DEN.)KOBENHAVN/JAEGERSBORG"
25630 DATA"(DEN.)MIDDELGRUNDEN","(DEN.)DROGDEN"
25640 DATA"(DEN.)KOBENHAVN/LANDBOHOJSKOLEN"
25650 DATA"(DEN.)RONNE","(DEN.)CHRISTIANSO"
25660 DATA"(DEN.)HAMMERODDE","(DEN.)POVLSKER"
25670 DATA"(NED.)YPENBURG","(NED.)VALKENBURG"
25680 DATA"(NED.)IJMUIDEN","(NED.)DE KOOY"
25690 DATA"(NED.)AMSTERDAM/SCHIPHOL","(NED.)VLIELAND"
25700 DATA"(NED.)TERSCHELLING","(NED.)DE BILT"
25710 DATA"(NED.)SOESTERBERG","(NED.)HOUTRIB"
25720 DATA"(NED.)LEEUWARDEN","(NED.)DEELEN"
25730 DATA"(NED.)EELDE","(NED.)TWENTE","(NED.)VLISSINGEN"
25740 DATA"(NED.)ZIERIKZEE","(NED.)HOEK VAN HOLLAND"
25750 DATA"(NED.)ROTTERDAM","(NED.)GILZE-RIJEN"
25760 DATA"(NED.)EINDHOVEN","(NED.)VOLKEL"
25770 DATA"(NED.)BEEK (Z-LIM)","(NED.)DE PEEL"
25780 DATA"(BEL.)KOKSIJDE","(BEL.)WEST-HINDER"
25790 DATA"(BEL.)OOSTENDE (AIRPORT)"
25800 DATA"(BEL.)OOSTENDE (PIER)","(BEL.)WEVELGEM"
25810 DATA"(BEL.)MUNTE","(BEL.)GENT/INDUSTRIEZONE"
25820 DATA"(BEL.)CHIEVRES","(BEL.)UKKEL"
25830 DATA"(BEL.)CHARLEROI/GOSSELIES"
25840 DATA"(BEL.)ANTWERPEN/DEURNE"
25850 DATA"(BEL.)BRUSSEL NATIONAAL"
25860 DATA"(BEL.)BRASSCHAAT","(BEL.)FLOENNES"
25870 DATA"(BEL.)BEAUVECHAIN","(BEL.)GOETSENHOVEN"
25880 DATA"(BEL.)SCHAFFEN","(BEL.)SINT-TRUIDEN"
25890 DATA"(BEL.)SINSIN","(BEL.)ST-HUBERT)
25900 DATA"(BEL.)BIERSET","(BEL.)KLEINE BROGEL"
25910 DATA"(BEL.)VIRTON","(BEL.)GENK"
25920 DATA"(BEL.)SPA / LA SAUVENIERE"
25930 DATA"(BEL.)BOTRANGE","(LUX.)CLERVEAUX"
25940 DATA"(LUX.)LUXEMBOURG (BEGGEN)"
25950 DATA"(LUX.)LUXEMBOURG","(LUX.)ECHTERNACH"
25960 DATA"(ZW.)NEUCHATEL)","(ZW.)ROMONT"
25970 DATA"(ZW.)PAYERNE","(ZW.)LA CHAUX-DE-FONDS"
25980 DATA"(ZW.)FAHY","(ZW.)BERN/BELPMOOS"
25990 DATA"(ZW.)GRENCHEN","(ZW)LANGENBRUCK"
26000 DATA"(ZW.)WYNAU","(ZW.)LAUFENBUG"
26010 DATA"(ZW.)METTLEN","(ZW.)BRUGG","(ZW.)GISWIL"
26020 DATA"(ZW.)ZURICH (STADT)","(ZW.)TRASADINGEN"
26030 DATA"(ZW.)LAEGERN","(ZW.)ZURICH (KLOTEN)"
26040 DATA"(ZW.)ALTDORF","(ZW.)SAENTIS"
26050 DATA"(ZW.)ST.GALLEN","(ZW.)WEESEN"
26060 DATA"(ZW.)GENEVE/CIONTRIN","(ZW.)LA DOLE"
26070 DATA"(ZW.)LE BRASSUS-CARROZ","(ZW.)LAUSANNE"
26080 DATA"(ZW.)AIGLE","(ZW.)SAVATRAN"
26090 DATA"(ZW.)LE GRAND SAINT BERNARD"
26100 DATA"(ZW.)SION","(ZW.)FIONNAY","(ZW.)MONTANA"
26110 DATA"(ZW.)GEMMI","(ZW.)JUNGFRAUJOCH"
26120 DATA"(ZW.)SPIEZ","(ZW.)INTERLAKEN"
26130 DATA"(ZW.)SIMPLON","(ZW.)ERNEN","(ZW.)GRIMSEL"
26140 DATA"(ZW.)GUETSCH","(ZW.)PIOTTA","(ZW.)OLIVONE"
26150 DATA"(ZW.)CIMETTA","(ZW.)LOCARNO/MONTI"
26160 DATA"(ZW.)LOCARNO/MAGADINO","(ZW.)LUGANO"
26170 DATA"(ZW.)STABIO","(ZW.)TAVANASA"
26180 DATA"(ZW.)DISENTIS","(ZW.)SAN BERNARDDINO"
26190 DATA"(ZW.)CHUR-EMS","(ZW.)TINZEN"
26200 DATA"(ZW.)LEOBIA","(ZW.)CORVATSCH"
26210 DATA"(ZW.)SAMEDAN-ST.MORITZ"
26220 DATA"(ZW.)ST.MORITZ-ISLAS","(ZW.)ROBBIA"
26230 DATA"(LICHTENSTEIN)VADUZ","(NED.)L.SCH. TEXEL"
26240 DATA"(NED.)L.SCH. TERSCHELLINGSBANK"
26250 DATA"(NED.)L.SCH. NOORDHINDER"
26260 DATA"(NED.)L.SCH. GOEREE","(NED.)WOENSDRECHT"
30000 REM*******************************
30020 REM   VERKLARING VAN DE TEKENS
30030 REM   ------------------------
30040 REM
30050 REM II=GEDEELTE DER WERELD WAAR
30060 REM    HET STATION LIGT.
30070 REM III=VOOR HET GEBIED 06 HET BE-
30080 REM     TREFFENDE STATION.
30090 REM     BUITEN HET GEBIED 06 WORDT
30100 REM     HET BLOKNUMMER EN HET STA-
30110 REM     TIONNUMMER GEGEVEN.
30120 REM H=HOOGTE VAN DE BASIS VAN DE
30130 REM   LAAGSTE WOLKEN.
30140 REM VV=HORIZONTAAL ZICHT.
30150 REM N=BEDEKKINGSGRAAD VAN DE HEMEL
30160 REM   DOOR WOLKEN.
30170 REM DD=WINDRICHTING.
30180 REM FF=WINDSNELHEID IN METERS/SEC.
30190 REM TTT=TEMPERATUUR IN 0,1 GRA-
30200 REM     DEN CELCIUS.
30210 REM TD=DAUWPUNTTEMPERATUUR IN 0,1
30220 REM    GRADEN CELCIUS.
30230 REM PO=LUCHTDRUK OP ZEENIVEAU IN
30240 REM    0,1 MILLIBAR.
30250 REM PP=LUCHTDRUK OP STATIONSNIVEAU
30260 REM    IN 0,1 MILLIBAR
30270 REM A=KARAKTER VAN DE LUCHTDRUK-
30280 REM   VERANDERING.
30290 REM PD=BEDRAG VAN DE LUCHTDRUKVER-
30300 REM    ANDERING.
30310 REM RR=HOEVEELHEID REGEN IN 0,1
30320 REM    MILLIMETER IN DE GEGEVEN
30330 REM    TIJD.
30340 REM WW=WEER TIJDENS DE WAARNEMING
30350 REM    EN/OF HET KARAKTER VAN HET
30360 REM    WEER IN HET AFGELOPEN UUR.
30370 REM W=W1/W2=KARAKTER VAN HET WEER
30380 REM   IN DE AFGELOPEN 6 UUR.
30390 REM NH=GEDEELTE VAN DE HEMEL
30400 REM    BEDEKT MET WOLKEN C/L OF
30410 REM    C/M.
30420 REM CL=C/L=WOLKEN STATOCUMULUS
30430 REM    ,STRATUS,CUMULUS.
30440 REM CM=C/M=WOLKEN ALTOCUMULUS,
30450 REM    ALTOSTRATUS,NIMBOSTRATUS.
30460 REM CH=C/H=WOLKEN CIRRUS,CIRRO-
30470 REM    CUMULUS,CIRROSTRATUS.
30490 REM******************************
30500 REM VIA DE TELEXCONVERTOR MOET U
30510 REM DIT BEELD ZIEN.-MET AAXX- B.V.
30520 REM
30530 REM ZCZC 446 28700
30540 REM SMDD20 ETPD 2406600 COR
30550 REM AAXX 24061
30560 REM 09361 31909 03201 10167 20153
30570 REM 30073 40285 53004 69942 70265
30580 REM 87511 333
30590 REM
30600 REM U VOERT DAN DE CIJFERS IN :
30610 REM
30620 REM 09361319090320110167201533007
30630 REM 34028553004699427026587511
30640 REM
30650 REM DUS TOT CIJFERGROEP 333
30680 REM
30690 REM      **********************
30700 REM      *  J.J.A. SUBELACK   *
30710 REM      *  PEELLANDSTRAAT 5  *
30720 REM      *  5211 VP DEN BOSCH *
30730 REM      *      NEDERLAND     *
30750 REM      *                    *
30760 REM      *  MACHINE COMM.64.  *
30770 REM      **********************
30780 REM
30790 REM COPYRIGHT
30800 REM NOS HILVERSUM


1000 A=200:GOTO20:REM TEMPERATUREN
1010 CE$=" GRADEN CELCIUS"
1020 FA$=" GRADEN FAHRENHEIT"
1030 RE$=" GRADEN REAUMUR"
1040 KE$=" KELVIN"
1050 SP$="*                                     *"
1060 ST$="***************************************"
1070 GOSUB100
1080 PRINT"INSTUCTIES (J/N) ?";:GOSUB210
1090 IFIN$="J" THENGOSUB2260
1100 GOSUB100
1110 PRINT"UW INVOER AUB";
1120 INPUTT$
1130 IFLEN(T$)<2 THEN1100
1140 IFT$="STOP" THEN1230
1150 IFT$="LYST" THENGOSUB1640:GOTO1100
1160 T1=VAL(LEFT$(T$,LEN(T$)-1))
1170 T1=INT(T1*10)/10
1180 T$=RIGHT$(T$,1)
1190 GOSUB1890
1200 IFS=0 THEN1100
1210 ONS GOSUB1240,1340,1440,1540
1220 GOTO1100
1230 END
1240 REM * C -> F,R,K OUTPUT *
1250 GOSUB1960
1260 GOSUB2630
1270 PRINTT1;CE$;:VE=9:GOSUB110
1280 PRINT"KOMT OVEREEN MET:";:VE=12:HO=2:GOSUB110
1290 PRINTT2;FA$;:VE=14:GOSUB110
1300 PRINTT3;RE$;:VE=16:GOSUB110
1310 PRINTT4;KE$;
1320 GOSUB2160
1330 RETURN
1340 REM * F -> C,R,K OUTPUT *
1350 GOSUB2010
1360 GOSUB2630
1370 PRINTT1;FA$;:VE=9:GOSUB110
1380 PRINT"KOMT OVEREEN MET:";:VE=12:HO=2:GOSUB110
1390 PRINTT2;CE$;:VE=14:GOSUB110
1400 PRINTT3;RE$;:VE=16:GOSUB110
1410 PRINTT4;KE$;
1420 GOSUB2160
1430 RETURN
1440 REM * R -> C,F,K OUTPUT *
1450 GOSUB2060
1460 GOSUB2630
1470 PRINTT1;RE$;:VE=9:GOSUB110
1480 PRINT"KOMT OVEREEN MET:";:VE=12:HO=2:GOSUB110
1490 PRINTT2;CE$;:VE=14:GOSUB110
1500 PRINTT3;FA$;:VE=16:GOSUB110
1510 PRINTT4;KE$;
1520 GOSUB2160
1530 RETURN
1540 REM * K -> C,F,R OUTPUT *
1550 GOSUB2110
1560 GOSUB2630
1570 PRINTT1;KE$;:VE=9:GOSUB110
1580 PRINT"KOMT OVEREEN MET:";:VE=12:HO=2:GOSUB110
1590 PRINTT2;CE$;:VE=14:GOSUB110
1600 PRINTT3;FA$;:VE=16:GOSUB110
1610 PRINTT4;RE$;
1620 GOSUB2160
1630 RETURN
1640 REM * LYST *
1650 GOSUB100
1660 PRINT"WELKE SCHAAL MOET LINKS STAAN ";
1670 INPUTT$
1680 GOSUB1890
1690 PRINT"BEGINWAARDE ";
1700 INPUTBW
1710 PRINT"EINDWAARDE ";
1720 INPUTEW
1730 IFBW>=EW THEN1640
1740 PRINT"WELKE STAPGROOTTE ";
1750 INPUTSG
1760 X=0
1770 GOSUB100
1780 FORT1=BW TOEW STEPSG
1790 IFX>19 THENGOSUB2160
1800 ONS GOSUB1960,2010,2060,2110
1810 PRINTINT(T1*10+.5)/10;
1820 PRINTTAB(12);INT(T2*10+.5)/10;
1830 PRINTTAB(22);INT(T3*10+.5)/10;
1840 PRINTTAB(32);INT(T4*10+.5)/10
1850 X=X+1
1860 NEXTT1
1870 GOSUB2160
1880 RETURN
1890 REM *TEMP. SCHAAL BEPALING *
1900 IFT$="C" THENS=1:GOTO1950
1910 IFT$="F" THENS=2:GOTO1950
1920 IFT$="R" THENS=3:GOTO1950
1930 IFT$="K" THENS=4:GOTO1950
1940 S=0
1950 RETURN
1960 REM * C -> F,R,K BEREKENING *
1970 T2=1.8*T1+32
1980 T3=0.8*T1
1990 T4=T1+273
2000 RETURN
2010 REM * F -> C,R,K BEREKENING *
2020 T2=(5/9)*(T1-32)
2030 T3=(4/9)*(T1-32)
2040 T4=(5/9)*(T1-32)+273
2050 RETURN
2060 REM * R -> C,F,K BEREKENING *
2070 T2=1.25*T1
2080 T3=(9/4)*T1+32
2090 T4=1.25*T1+273
2100 RETURN
2110 REM * K -> C,F,R BEREKENING *
2120 T2=T1-273
2130 T3=9/5*(T1-273)+32
2140 T4=0.8*(T1-273)
2150 RETURN
2160 REM * WACHT-ROUTINE *
2170 HO=6:VE=23:GOSUB110
2180 PRINT"TIK EEN TOETS VOOR VERVOLG";
2190 GOSUB210:X=0:GOSUB100
2200 RETURN
2210 GOSUB100
2220 PRINT"  INSTRUCTIES VOOR HET GEBRUIK VAN HET"
2230 PRINT"        TEMPERATUREN-PROGRAMMA."
2240 PRINT
2250 RETURN
2260 GOSUB2210
2270 PRINT"DE VOOR HET PROGRAMMA NODIGE INVOER"
2280 PRINT"BESTAAT UIT EEN GETAL EN EEN LETTER."
2290 PRINT"TUSSEN GETAL EN LETTER STAAT GEEN"
2300 PRINT"SPATIE. HET GETAL IS DE OM TE REKENEN"
2310 PRINT"TEMPERATUUR. DE LETTER IS DE SCHAAL "
2320 PRINT"WAARIN DE TEMPERATUUR OPGEGEVEN IS."
2330 PRINT"GEBRUIK C VOOR CELCIUS,F VOOR FAHREN-"
2340 PRINT"HEIT,R VOOR REAUMUR EN K VOOR KELVIN."
2350 PRINT"DUS BIJV. 20C,-50F,80.2R,270K."
2360 PRINT"ALLE GETALLEN,ZOWEL INVOER ALS UITVOER,"
2370 PRINT"WORDEN OP EEN DECIMAAL AFGEROND."
2380 PRINT"BIJ INTOETSEN VAN ANDERE LETTERS IS HET"
2390 PRINT"RESULTAAT ONVOORSPELBAAR EN AFHANKELIJK"
2400 PRINT"VAN UW COMPUTER."
2410 PRINT"WANNEER U 'LYST' INTYPT KUNT U EEN"
2420 PRINT"LYST SAMENSTELLEN WAARIN BINNEN ZELF TE"
2430 PRINT"BEPALEN GRENZEN EEN VERGELIJKENDE LIJST"
2440 GOSUB2160
2450 GOSUB2210
2460 PRINT"AFGEDRUKT WORDT.ER WORDT EERST GEVRAAGD"
2470 PRINT"WELKE SCHAAL LINKS OP HET SCHERM MOET"
2480 PRINT"KOMEN TE STAAN. DIT IS TEVENS DE SCHAAL"
2490 PRINT"WAARIN DE GRENZEN OPGEGEVEN DIENEN TE"
2500 PRINT"WORDEN. BIJ HET INVOEREN VAN DE GRENZEN"
2510 PRINT"VAN DE LIJST DIENT DUS -> GEEN <-"
2520 PRINT"SCHAAL OPGEGEVEN TE WORDEN."
2530 PRINT"BIJVOORBEELD: INDIEN U ACHTEREENVOLGENS"
2540 PRINT"INVOERT C , 10 , 20 EN 1 BESLAAT DE "
2550 PRINT"LIJST DE BEREKENINGEN VAN 10 T/M 20"
2560 PRINT"GRADEN CELCIUS IN STAPPEN VAN 1 GRAAD."
2570 PRINT"INDIEN ALS INVOER HET WOORD 'STOP' "
2580 PRINT"INGETYPT WORDT,VERLAAT U HET PROGRAMMA."
2590 PRINT
2600 PRINT"TIK EEN TOETS VOOR DE EERSTE BEREKENING."
2610 GOSUB210
2620 RETURN
2630 GOSUB100:FORX=1 TO5:PRINT:NEXTX
2640 PRINTST$:FORX=1 TO12:PRINTSP$:NEXTX:PRINTST$
2650 VE=7:HO=10:GOSUB110:RETURN
30000 REM DIT TEMPERATUREN-PROGRAMMA
30010 REM IS GEMAAKT OP EEN APPLE II
30020 REM VOLGENS DE BASICODE-2 NORM
30030 REM DOOR VICTOR VERMAAS
30040 REM MET DANK AAN WIM ZONNEVELD
30050 REM VOOR ZIJN SUGGESTIES EN DE
30060 REM VERLEENDE MEDEWERKING
30070 REM
30080 REM COPYRIGHT
30090 REM NOS HILVERSUM


1000 A=1200:GOTO20:REM ** AUTOKOSTEN **
1010 DIMA(110),E$(11):P$="":K=0:CT=9:CN=2:CT=9:GOSUB20000
1020 GOSUB100:J=0:K=K+1
1030 PRINT:PRINT"       AUTOKOSTENBEREKENING.";" NR.";K
1040 PRINT"       ====================  ******"
1050 PRINT:PRINT:PRINT
1060 PRINT" BEREKENT DE KOSTEN PER KM."
1070 PRINT:PRINT" KOSTEN PER WEEK,"
1080 PRINT:PRINT" KOSTEN PER MAAND,"
1090 PRINT:PRINT" KOSTEN PER JAAR EN"
1100 PRINT:PRINT" AANTAL JAREN VAN AFSCHRIJVING ENZ."
1110 PRINT:PRINT" MAX.10 BEREKENINGEN KOMEN IN GEHEUGEN"
1120 PRINT:FORI=1 TO40:PRINT"=";:NEXTI:
1130 PRINT:PRINT:PRINT"      DRUK OP EEN TOETS VOOR VERDER";
1140 GOSUB210
1150 GOSUB100
1160 VE=1:HO=2:GOSUB110
1170 PRINT"KENMERK B.V. MERK AUTO : ";:INPUTE$(K)
1180 PRINT:PRINT"  AANSCHAFKOSTEN VAN AUTO: ";:INPUTAN
1190 PRINT:PRINT"  AFSCHRIJVINGSJAREN     : ";:INPUTFF
1200 FF=INT(FF+.5)
1210 PRINT:PRINT"  DE RESTWAARDE          : ";:INPUTRE
1220 PRINT:PRINT"  DE RENTESTAND          : ";:INPUTRS
1230 RS=INT((RS*100)+.5)/100
1240 PRINT:PRINT"  GEM. AANTAL KM/JAAR    : ";:INPUTMM
1250 PRINT:PRINT"  GEM. VERBRUIK  1 OP ?  : ";:INPUTVB
1260 PRINT:PRINT"  BRANDSTOF LITERPRIJS   : ";:INPUTLP
1270 PRINT:PRINT"  ONDERHOUDSKOSTEN P/JAAR: ";:INPUTKS
1280 PRINT:PRINT"  VERZEKERING P/JAAR     : ";:INPUTVZ
1290 PRINT:PRINT"  WEGENBELASTING P/JAAR  : ";:INPUTWB
1300 PRINT:PRINT"  GEGEVENS CORRECT (J/N) :";
1310 GOSUB210:IF(IN$="N") OR(IN$="n") THENK=K-1:GOTO1020
1320 GOSUB5180:GOSUB100
1330 PRINT
1340 PRINT"            KOSTEN 1E JAAR."
1350 PRINT"            ============== "
1360 PRINT:PRINT:PRINT
1370 RE=RE*100
1380 LP=LP*100
1390 KS=KS*100
1400 VZ=VZ*100
1410 WB=WB*100
1420 AN=AN*100
1430 AF=INT((AN-RE)/FF)
1440 RT=INT((AN/100)*RS)
1450 TK=INT(((MM/VB)*LP)+RT+KS+VZ+WB+AF)
1460 LT=INT((MM/VB)*100)
1470 BK=INT((LT*LP)/100+.5)
1480 KP=INT(TK/12+.5)
1490 KW=INT(TK/52+.5)
1500 HP=INT((TK/MM)+.5)
1510 IFJ=0 THENDW=AN-AF
1520 IFP$="P" THEN9520
1530 GOTO2000
1540 REM****RENTE MINUS AFSCHRIJVING****
1550 RB=INT(AN-(AF*J)+.5)
1560 GOTO1580
1570 RB=INT(RE)
1580 RT=INT((RB/100)*RS+.5)
1590 IFJ=FF THENDW=RB:AF=0:GOTO1450
1600 DW=RB-AF
1610 GOTO1450
2000 X=DW:GOSUB5000:VE=3:HO=1:GOSUB110
2010 PRINT"DE DAGWAARDE IS        : ";"FL";
2020 VE=3:HO=27+T:GOSUB110:IFJ=FF THENW$="????"
2030 PRINTW$:DW=VAL(W$)
2040 X=AN:GOSUB5000
2050 VE=5:HO=1:GOSUB110
2060 PRINT"AANSCHAFPRIJS WAS      : ";"FL";
2070 VE=5:HO=27+T:GOSUB110
2080 PRINTW$
2090 AN=VAL(W$)
2100 X=AF:GOSUB5000
2110 VE=7:HO=1:GOSUB110
2120 PRINT"AFSCHRYVING DIT JAAR IS: ";"FL";
2130 VE=7:HO=27+T:GOSUB110
2140 PRINTW$
2150 AF=VAL(W$)
2160 X=RT:GOSUB5000
2170 VE=9:HO=1:GOSUB110
2180 PRINT"RENTE PERCENTAGE ";RS;"%      ";:VE=9
2190 HO=26:GOSUB110:PRINT"FL";
2200 VE=9:HO=27+T:GOSUB110
2210 PRINTW$
2220 RT=VAL(W$)
2230 X=TK:GOSUB5000
2240 VE=11:HO=1:GOSUB110
2250 PRINT"TOTALE KOSTEN P/JAAR IS: ";"FL";
2260 VE=11:HO=27+T:GOSUB110
2270 PRINTW$
2280 TK=VAL(W$)
2290 X=LT:GOSUB5000
2300 VE=13:HO=1:GOSUB110
2310 PRINT"LITERS BRANDSTOF P/JAAR: ";"  ";
2320 VE=13:HO=27+T:GOSUB110
2330 PRINTW$
2340 LT=VAL(W$)
2350 X=BK:GOSUB5000
2360 VE=15:HO=1:GOSUB110
2370 PRINT"BRANDSTOFTOTAAL P/JAAR : ";"FL";
2380 VE=15:HO=27+T:GOSUB110
2390 PRINTW$
2400 BK=VAL(W$)
2410 X=KP:GOSUB5000
2420 VE=17:HO=1:GOSUB110
2430 PRINT"KOSTEN PER/MAAND       : ";"FL";
2440 VE=17:HO=27+T:GOSUB110
2450 PRINTW$
2460 KP=VAL(W$)
2470 X=KW:GOSUB5000
2480 VE=19:HO=1:GOSUB110
2490 PRINT"KOSTEN PER/WEEK        : ";"FL";
2500 VE=19:HO=27+T:GOSUB110
2510 PRINTW$
2520 KW=VAL(W$)
2530 X=HP:GOSUB5000
2540 VE=21:HO=1:GOSUB110
2550 PRINT"KOSTEN PER/KM          : ";"FL";
2560 VE=21:HO=27+T:GOSUB110
2570 PRINTW$
2580 HP=VAL(W$)
2590 SR=J+1:GOSUB300
2600 SR$=SR$+"E"
2610 VE=23:HO=1:GOSUB110
2620 IFVAL(SR$)=FF+1 THEN2650
2630 PRINT"DIT IS ";SR$;" JAAR VAN AFSCHRIJVING.";
2640 GOTO2660
2650 PRINT"ALLES IS NU AFGESCHREVEN.";:GOSUB250
2660 GOSUB210
2670 IFJ=FF THEN2690
2680 GOTO2870
2690 GOSUB100:VE=5:HO=5:GOSUB110
2700 PRINT"H = BEREKENING HERHALEN OP SCHERM."
2710 VE=8:HO=5:GOSUB110
2720 PRINT"P = EEN BEREKENING UITPRINTEN."
2730 VE=11:HO=5:GOSUB110
2740 PRINT"N = EEN NIEUWE BEREKENING MAKEN."
2750 VE=14:HO=5:GOSUB110
2760 PRINT"E = STOPPEN VAN PROGRAMMA."
2770 VE=17:HO=9:GOSUB110
2780 PRINT"UW KEUZE.";:GOSUB210:PRINT:PRINT
2790 IF(IN$="H") OR(IN$="h") THEN8000
2800 IF(IN$="P") OR(IN$="p") THEN9000
2810 IF(IN$="N") OR(IN$="n") THEN1020
2820 IF(IN$<>"E") AND(IN$<>"e") THEN2840
2830 PRINT"   SUCCES MET HET PROGRAMMA.":END
2840 PRINT"         KIES H,P,N,E":FORI=1 TO3:GOSUB250
2850 NEXTI:GOTO2690
2860 GOSUB100
2870 GOSUB100
2880 PRINT:PRINT"       KOSTEN HIEROPVOLGENDE JAAR."
2890 PRINT"       =========================="
2900 AN=AN*100:AF=AF*100
2910 IFJ=FF THEN1570
2920 J=J+1
2930 GOTO1540
3000 REM ***OMREKENING VOOR PRINTER***
3010 IFJ=FF THENAF=0:RT=(RE/100)*RS:GOTO3040
3020 AF=(AN-RE)/FF
3030 RT=((AN-AF*J)/100)*RS
3040 TK=((MM/VB)*LP)+RT+KS+VZ+WB+AF
3050 LT=(MM/VB)
3060 BK=(LT*LP)
3070 IFJ=FF THENDW=RE:GOTO9520
3080 DW=AN-(AF*(J+1))
3090 GOTO9520
4000 REM***INPUT GEGEVENS PRINTEN*******
4010 GOSUB100:IFP$="P" THEN9080
4020 VE=1:HO=1:GOSUB110
4030 PRINT"      INGEVOERDE GEGEVENS."
4040 PRINT"       ==================="
4050 VE=3:HO=1:GOSUB110
4060 PRINT"KENMERK BEREKENING     : ";F$
4070 VE=5:HO=1:GOSUB110
4080 X=AN*100:GOSUB5000
4090 PRINT"AANSCHAFKOSTEN         : ";"FL";
4100 VE=5:HO=27+T:GOSUB110
4110 PRINTW$
4120 VE=7:HO=1:GOSUB110
4130 X=INT((FF*100)+.5):GOSUB5000
4140 PRINT"AFSCHRIJVINGSJAREN     : ";
4150 VE=7:HO=27+T:GOSUB110
4160 PRINTW$
4170 VE=9:HO=1:GOSUB110
4180 X=RE*100:GOSUB5000
4190 PRINT"RESTWAARDE             : ";"FL";
4200 VE=9:HO=27+T:GOSUB110
4210 PRINTW$
4220 VE=11:HO=1:GOSUB110
4230 X=INT((RS*100)+.5):GOSUB5000
4240 PRINT"RENTE STAND IN %       : ";
4250 VE=11:HO=27+T:GOSUB110
4260 PRINTW$+"%"
4270 VE=13:HO=1:GOSUB110
4280 X=INT((MM*100)+.5):GOSUB5000
4290 PRINT"GEM. AANTAL KM/JAAR    : ";
4300 VE=13:HO=27+T:GOSUB110
4310 PRINTW$
4320 VE=15:HO=1:GOSUB110
4330 PRINT"GEM.BRANDSTOFVERBRUIK  : ";"1 OP......";
4340 X=INT((VB*100)+.5):GOSUB5000
4350 VE=15:HO=27+T:GOSUB110
4360 PRINTW$
4370 VE=17:HO=1:GOSUB110
4380 X=INT((LP*100)+0.5):GOSUB5000
4390 PRINT"PRIJS PER/LITER        : ";"FL";
4400 VE=17:HO=27+T:GOSUB110
4410 PRINTW$
4420 VE=19:HO=1:GOSUB110
4430 PRINT"ONDERHOUDSKOSTEN P/JAAR: ";"FL";
4440 X=INT((KS*100)+.5):GOSUB5000
4450 VE=19:HO=27+T:GOSUB110
4460 PRINTW$
4470 VE=21:HO=1:GOSUB110
4480 PRINT"VERZEKERING            : ";"FL";
4490 X=INT((VZ*100)+.5):GOSUB5000
4500 VE=21:HO=27+T:GOSUB110
4510 PRINTW$
4520 VE=23:HO=1:GOSUB110
4530 PRINT"WEGENBELASTING         : ";"FL";
4540 X=INT((WB*100)+.5):GOSUB5000
4550 VE=23:HO=27+T:GOSUB110
4560 PRINTW$
4570 U=U-9:GOSUB6000
4580 GOSUB210:GOTO1320
5000 SR=X:GOSUB300
5010 W$=SR$
5020 LE=LEN(SR$)
5030 IFLE=1 THENW$="0,0"+W$:GOTO5160
5040 IFLE=2 THENW$="0,"+W$:GOTO5160
5050 IFLE<6 GOTO5070
5060 GOTO5090
5070 W$=LEFT$(W$,LE-2)+","+RIGHT$(W$,2)
5080 GOTO5160
5090 IFLE<9 THEN5110
5100 GOTO5130
5110 W$=LEFT$(W$,LE-5)+" "+MID$(W$,LE-4,3)+","+RIGHT$(W$,2)
5120 GOTO5160
5130 W$=LEFT$(W$,LE-8)+" "+MID$(W$,LE-7,3)+" "
5140 GOTO5160
5150 W$=W$+MID$(W$,LE-4,3)+","+RIGHT$(W$,2)
5160 T=12-LEN(W$)
5170 RETURN
5180 IFK=1 THENU=1:GOTO7000
5190 IFK=2 THENU=11:GOTO7000
5200 IFK=3 THENU=21:GOTO7000
5210 IFK=4 THENU=31:GOTO7000
5220 IFK=5 THENU=41:GOTO7000
5230 IFK=6 THENU=51:GOTO7000
5240 IFK=7 THENU=61:GOTO7000
5250 IFK=8 THENU=71:GOTO7000
5260 IFK=9 THENU=81:GOTO7000
5270 IFK=10 THENU=91:GOTO7000
5280 GOSUB100:PRINT:PRINT
5290 PRINT" GEHEUGEN VAN 10 BEREKENINGEN IS VOL."
5300 PRINT:PRINT" NIEUWE BEREKENINGEN BLIJVEN"
5310 PRINT:PRINT" ECHTER WEL MOGELIJK.
5320 FORI=1 TO3000:NEXTI:K=10
5330 RETURN
6000 REM***UITLEZEN VAN INPUTGEGEVENS***
6010 AN=A(U):U=U+1
6020 FF=A(U):U=U+1
6030 RE=A(U):U=U+1
6040 RS=A(U):U=U+1
6050 MM=A(U):U=U+1
6060 VB=A(U):U=U+1
6070 LP=A(U):U=U+1
6080 KS=A(U):U=U+1
6090 VZ=A(U):U=U+1
6100 WB=A(U)
6110 J=0
6120 RETURN
7000 REM ***BEWAREN VAN INPUTGEGEVENS***
7010 A(U)=AN:U=U+1
7020 A(U)=FF:U=U+1
7030 A(U)=RE:U=U+1
7040 A(U)=RS:U=U+1
7050 A(U)=MM:U=U+1
7060 A(U)=VB:U=U+1
7070 A(U)=LP:U=U+1
7080 A(U)=KS:U=U+1
7090 A(U)=VZ:U=U+1
7100 A(U)=WB:RETURN
8000 GOSUB100
8010 PRINT:PRINT"          AANTAL BEREKENINGEN."
8020 FORI=1 TO39:PRINT"=";:NEXTI
8030 FORI=1 TOK:PRINT
8040 PRINT"    ";I;"E.  BEREKENING. ";E$(I)
8050 NEXTI
8060 FORI=1 TO39:PRINT"=";:NEXTI:PRINT
8070 PRINT"          UW KEUZE ";:INPUTIN$
8080 IFVAL(IN$)>K THEN8000
8090 IFIN$="1" THENU=1:F$=E$(1):GOSUB6000:GOTO4000
8100 IFIN$="2" THENU=11:F$=E$(2):GOSUB6000:GOTO4000
8110 IFIN$="3" THENU=21:F$=E$(3):GOSUB6000:GOTO4000
8120 IFIN$="4" THENU=31:F$=E$(4):GOSUB6000:GOTO4000
8130 IFIN$="5" THENU=41:F$=E$(5):GOSUB6000:GOTO4000
8140 IFIN$="6" THENU=51:F$=E$(6):GOSUB6000:GOTO4000
8150 IFIN$="7" THENU=61:F$=E$(7):GOSUB6000:GOTO4000
8160 IFIN$="8" THENU=71:F$=E$(8):GOSUB6000:GOTO4000
8170 IFIN$="9" THENU=81:F$=E$(9):GOSUB6000:GOTO4000
8180 IFIN$="10" THENU=91:F$=E$(10):GOSUB6000:GOTO4000
8190 GOTO8000
9000 REM **BEREKENINGEN NAAR PRINTER**
9010 GOSUB100
9020 VE=10:HO=2:GOSUB110
9030 PRINT"MAAK KEUZE WAT NAAR PRINTER MOET."
9040 VE=13:HO=10:GOSUB110
9050 PRINT"DRUK OP TOETS"
9060 P$="P":GOSUB210
9070 GOTO8000
9080 Q$="                  ":FORI=1 TO3:GOSUB360:NEXTI
9090 SR$=Q$+"INGEVOERDE GEGEVENS BIJ DEZE BEREKENING."
9100 GOSUB350:GOSUB360
9110 SR$=Q$+"========================================"
9120 GOSUB350:GOSUB360
9130 SR$=Q$+"KENMERK BEREKENING ......."+F$
9140 GOSUB350:GOSUB360
9150 SR$=Q$+"----------------------------------------"
9160 GOSUB350:GOSUB360
9170 SR=AN:GOSUB310
9180 SR$=Q$+"AANSCHAFKOSTEN............FL."+SR$
9190 GOSUB350:GOSUB360
9200 SR=FF:GOSUB310
9210 SR$=Q$+"JAREN VAN AFSCHRIJVING....   "+SR$
9220 GOSUB350:GOSUB360
9230 SR=RE:GOSUB310
9240 SR$=Q$+"REST-WAARDE...............FL."+SR$
9250 GOSUB350:GOSUB360
9260 SR=RS:GOSUB310
9270 SR$=Q$+"RENTE-STAND IS............   "+SR$+" %"
9280 GOSUB350:GOSUB360
9290 SR=MM:GOSUB310
9300 SR$=Q$+"GEM. AANTAL KM/JAAR ......   "+SR$
9310 GOSUB350:GOSUB360
9320 SR=VB:GOSUB310
9330 SR$=Q$+"GEM.VERBRUIK   1 OP ......   "+SR$+" KM"
9340 GOSUB350:GOSUB360
9350 SR=LP:GOSUB310
9360 SR$=Q$+"BRANDSTOFPRIJS P/LITER ...FL."+SR$
9370 GOSUB350:GOSUB360
9380 SR=KS:GOSUB310
9390 SR$=Q$+"ONDERHOUSDSKOSTEN P/JAAR..FL."+SR$
9400 GOSUB350:GOSUB360
9410 SR=VZ:GOSUB310
9420 SR$=Q$+"VERZEKERING P/JAAR .......FL."+SR$
9430 GOSUB350:GOSUB360
9440 SR=WB:GOSUB310
9450 SR$=Q$+"WEGENBELASTING P/JAAR ....FL."+SR$
9460 GOSUB350:GOSUB360
9470 FORI=1 TO3:GOSUB360:NEXTI
9480 SR$=Q$+"----------------------------------------"
9490 GOSUB350:GOSUB360
9500 FORI=1 TO2:GOSUB360:NEXTI
9510 GOTO3000
9520 REM ***PRINTEN VAN BEREKENINGEN***
9530 SR$=Q$+"UITKOMSTEN VAN DE GEMAAKTE BEREKENINGEN "
9540 GOSUB350:GOSUB360
9550 SR$=Q$+"======================================="
9560 GOSUB350:GOSUB360
9570 IFJ=FF THEN9610
9580 SR=J+1:GOSUB300:SR$=SR$+"E"
9590 SR$=Q$+"DIT IS HET "+SR$+" JAAR VAN AFSCHRIJVING."
9600 GOTO9620
9610 SR$=Q$+"ALLES IN DEZE BEREKENING AFGESCHREVEN."
9620 GOSUB350:GOSUB360:GOSUB360
9630 SR=DW:GOSUB310:IFJ=FF THENSR$="     ????"
9640 SR$=Q$+"DE DAGWAARDE IS: .........FL."+SR$
9650 GOSUB350:GOSUB360
9660 SR=AN:GOSUB310
9670 SR$=Q$+"AANSCHAFPRIJS WAS ........FL."+SR$
9680 GOSUB350:GOSUB360
9690 SR=AF:GOSUB310
9700 SR$=Q$+"AFSCHRIJVEN DIT JAAR .....FL."+SR$
9710 GOSUB350:GOSUB360
9720 SR=RT:GOSUB310
9730 SR$=Q$+"RENTE VERLIES IS: ........FL."+SR$
9740 GOSUB350:GOSUB360
9750 SR=TK:GOSUB310
9760 SR$=Q$+"TOTALE KOSTEN PER/JAAR ...FL."+SR$
9770 GOSUB350:GOSUB360
9780 SR=LT:GOSUB310
9790 SR$=Q$+"TOTAAL LITERS BRANDST.P/J.FL."+SR$
9800 GOSUB350:GOSUB360
9810 SR=BK:GOSUB310
9820 SR$=Q$+"TOTAALBEDRAG BRANDSTOF ...FL."+SR$
9830 GOSUB350:GOSUB360
9840 SR=TK/12:GOSUB310
9850 SR$=Q$+"TOTAAL KOSTEN P/MAAND ....FL."+SR$
9860 GOSUB350:GOSUB360
9870 SR=TK/52:GOSUB310
9880 SR$=Q$+"TOTAAL KOSTEN P/WEEK .....FL."+SR$
9890 GOSUB350:GOSUB360
9900 SR=TK/MM:GOSUB310
9910 SR$=Q$+"DE KOSTEN PER KM. ........FL."+SR$
9920 GOSUB350:GOSUB360
9930 IFJ=FF THEN9990
9940 FORI=1 TO2:GOSUB360:NEXTI
9950 SR$=Q$+"----------------------------------------"
9960 GOSUB350:GOSUB360
9970 FORI=1 TO2:GOSUB360:NEXTI
9980 J=J+1:GOTO3000
9990 FORI=1 TO4:GOSUB360:NEXTI
10000 P$="":J=0:GOTO2690
10010 REM -------------------------------
20000 REM  FILE OP OP CASSETTE OF DISK
20010 REM  BEWAREN.
20020 REM  TEL AANTAL BEREKENINGEN
20030 REM  DIT IS DE VARIABLE K
20040 REM  WRITE FILE K
20100 REM  WRITE VERVOLGENS E$(K)  B.V.
20110 REM  FOR I=1 TO K
20120 REM  WRITE E$(I)
20130 REM  NEXT I
20140 REM  DE KENMERKEN OF DE VARIABLE
20150 REM  NAMEN ZIJN GESCHREVEN
20160 REM  NU DE INPUTBEDRAGEN NOG.
20170 REM  DIT IS :    A(U)    B.V.
20180 REM  U=K*10
20190 REM  FORI=1 TO U
20200 REM  WRITE A(I)
20210 REM  NEXT I
20220 REM  FILE GESCHREVEN. CLOSE FILE.
20230 REM  FILE LEZEN VAN DISK-CASSETTE
20240 REM  AANTAL BEREKENINGEN IS K
20250 REM  READ K
20260 REM  READ VERVOLGENS E$(K)   B.V.
20270 REM  FOR I=1 TO K
20280 REM  READ E$(K)
20290 REM  NEXT I
20300 REM  DE KENMERKEN EN VARIABLE
20310 REM  ZIJN NU GELEZEN.
20320 REM  NU DE INPUTBEDRAGEN NOG.
20330 REM  DIT IS WEER :  A(U)
20340 REM  U=K*10
20350 REM  FOR I=1 TO U
20360 REM  READ A(I)
20370 REM  NEXT I
20380 REM  FILE IS NU INGELEZEN. CLOSE
20390 REM  ----------------------------
20400 RETURN
30000 REM *****************************
30010 REM *                           *
30020 REM *   A U T O K O S T E N     *
30030 REM *                           *
30040 REM *   B E R E K E N E N .     *
30050 REM *                           *
30060 REM *                           *
30070 REM *   GEMAAKT OP EEN          *
30080 REM *                           *
30090 REM *   COMMODORE  64.          *
30100 REM *                           *
30110 REM *   DOOR.........           *
30120 REM *                           *
30130 REM *   O.M.  PA3-CMB. NED.     *
30140 REM *                           *
30150 REM *   HARRY FEITSMA           *
30160 REM *   KEEGENSTERWEG 10.       *
30170 REM *   9851AS BURUM (FR.)      *
30180 REM *                           *
30190 REM *   COPYRIGHT
30200 REM *   NOS HILVERSUM
30210 REM *
30220 REM ***************************** 


1000 A=200:GOTO20:REM STEM  BURO
1010 GOSUB100:GOSUB19500:HP=HM/2:VE=0:SR=0
1015 HO=HP-7:GOSUB110:PRINT"Beste Docent(e)":GOSUB10000
1020 PRINT"Een  TUSSENRESULTAAT  verschijnt door na";:PRINT
1025 PRINT"een vraag niet J of N maar 't WACHTWOORD";
1030 PRINT:PRINT"in te typen. (Laatste DATA!)"
1035 VE=VM:GOSUB110:GOSUB11000
1040 RESTORE:AP=0:AK=0:CN=0:CT=3
1050 READA$:IFA$="." THEN1080
1060 IFLEFT$(A$,1)="." THENAP=AP+1
1070 AK=AK+1:GOTO1050
1080 READPR,KG,ZE,WW$:DIMPK(AK):FORI=0 TOAK:PK(I)=0:NEXTI
1090 DIMP$(4*AP):AK=AK-2*AP
1100 RESTORE
1110 READA$:IFA$="." THEN1500
1120 IFLEFT$(A$,1)<>"." THEN1110
1130 READA$:P$(SR*4)=A$:SR=SR+1:GOTO1110
1500 REM INLEIDING
1505 IFPK(0)=KG THEN3300
1510 GOSUB100:VE=0:HO=HP-10:GOSUB110
1520 HO=HP-7:GOSUB110:PRINT"S T E M B U R O":GOSUB10000
1530 PRINT"Geef straks aan welke PARTIJ  je kiest.":PRINT
1540 PRINT"Daarna kun je EEN van de KANDIDATEN van":PRINT
1550 PRINT"de partij kiezen. Men kiest  meestal de":PRINT
1560 PRINT"'LIJSTAANVOERDER', de eerste.  Maar een":PRINT
1570 PRINT"ander kiezen mag ook. Men  spreekt  dan":PRINT
1580 PRINT"van een VOORKEURSTEM."
1590 GOSUB10000
1600 PRINT"Klaar om te stemmen";:GOSUB13000
1610 ONJN GOTO2000,1500,3300
2000 REM KIES PARTIJ
2010 GOSUB100:PRINT" NR PARTIJ";:HO=HP+1:VE=0:SR=0:GOSUB110
2020 PRINT"AANVOERDER";:GOSUB10000
2030 FORSR=1 TOAP:GOSUB310
2040 GOSUB110:GOSUB11000:PRINTSR$+" "+P$((SR-1)*4);
2050 RESTORE
2060 READA$:IFA$<>P$((SR-1)*4) THEN2060
2070 READA$:HO=HP:GOSUB110:PRINT" ";A$;:VE=VE+2:HO=0
2080 NEXTSR
2090 GOSUB10000:PRINT"Welke PARTIJ kies je":PRINT"(nummer)";
2100 GOSUB19000:PA=VAL(A$)
2110 IF(PA>0) AND(PA<AP+1) THEN2140
2120 PRINT"Van 1 tot ";AP;"graag!"
2130 GOTO2100
2140 GOSUB100:PRINT"Je kiest:":PRINT
2150 PRINT"* ";P$((PA-1)*4):PRINT:PRINT"Is dat goed";
2160 GOSUB13000:ONJN GOTO3000,2000,3300
3000 REM KIES KANDIDAAT
3010 GOSUB100:PRINT" NR KANDIDAAT";
3015 S=-1:SR=0:GOSUB10000:RESTORE
3020 S=S+1:READA$:IFA$<>P$((PA-1)*4) THEN3020
3030 SR=SR+1:READA$:IFLEFT$(A$,1)="." THEN3050
3040 GOSUB310:GOSUB11000:PRINTSR$;" ";A$:GOTO3030
3050 GOSUB10000
3060 GOSUB110:PRINT"Welk nummer kies je";
3070 GOSUB19000:KA=VAL(A$)
3080 IF(KA>0) AND(KA<SR) THEN3110
3090 PRINT"Van 1 tot ";SR-1;"graag!";
3100 GOTO3060
3110 RESTORE:FORI=0 TOS+KA:READA$:NEXTI
3120 GOSUB100:PRINT"Je kiest:":PRINT
3130 PRINT"* ";P$((PA-1)*4):PRINT:PRINT"* ";A$
3140 PRINT:PRINT"Bedoelde je dat";
3190 GOSUB13000:ONJN GOTO3200,2000,3300
3200 GOSUB100:PRINT"Bedankt voor je stem.":PRINT
3210 PRINT"Volgende kiezer graag!":W=2:GOSUB19600
3220 GOSUB11000:PK(0)=PK(0)+1:PK(S+KA)=PK(S+KA)+1
3290 GOTO1500
3300 REMUITSLAG
3310 GOSUB100:IFPK(0)>=ZE THEN3340
3320 PRINT"Nog geen ";ZE;"stemmen":PRINT"uitgebracht."
3330 W=2:GOSUB19600:GOTO1500
3340 GOSUB100:VE=0:HO=HP-10:GOSUB110
3350 PRINT"U I T S L A G E N":GOSUB10000:SR=KG:GOSUB310
3360 PRINTSR$;"  Kiesgerechtigden en ";
3370 SR=PK(0):GOSUB310
3380 PRINTSR$;" kwamen  ook";
3390 PRINT:SR=PK(0)/KG*100:GOSUB310
3400 PRINT"stemmen. Dat is ";SR$;"%. OPKOMST-PERCENTAGE";
3410 PRINT:PRINT"noemen we dat.    Van de ";
3420 SR=AK:GOSUB310
3430 PRINTSR$;"  kandidaten";:PRINT:PRINT"kunnen er ";
3440 SR=ZE:GOSUB310:PRINTSR$;
3450 PRINT" gekozen worden. We zeggen:";:PRINT
3452 PRINT"Er zijn ";SR$;" zetels te verdelen.";
3455 KD=INT(PK(0)/ZE+0.9):SR=PK(0):GOSUB310:PRINT:PRINT
3460 PRINT"Om gekozen te worden  heb je  (afgerond)";:PRINT
3470 PRINTSR$;:SR=ZE:GOSUB310:PRINT" :";SR$;" =";:SR=KD
3480 GOSUB310:PRINTSR$;
3485 PRINT" stemmen nodig.  Dat aantal";
3490 PRINT:PRINT"heet: DE KIESDELER."
3500 GOSUB10000
3530 VE=VM:GOSUB110:GOSUB11000
3600 REMZETELS+RESTZ
3610 GOSUB100:VE=0:HO=HP-10:GOSUB110
3620 PRINT"ZETELS EN RESTZETELS":GOSUB10000:PRINT
3630 PRINT"Na het delen  van  de  stemmen  door ";
3640 SR=KD:GOSUB310:PRINTSR$;
3650 PRINT:PRINT"(kiesdeler) blijven er RESTSTEMMEN over."
3660 GOSUB10000
3670 PRINT"PARTIJ";:GOSUB120:HO=HP-8:GOSUB110
3680 PRINT" STEMMEN ZETELS REST";:GOSUB10000
3700 GOSUB120:REM VOORL BEREKENING
3710 TL=0:RESTORE:READA$:FORI=1 TOAP
3720 J=(I-1)*4:READA$
3730 HO=0:GOSUB110:SM=0:TL=TL+1
3740 READA$:TL=TL+1:IFLEFT$(A$,1)="." THEN3760
3750 SM=SM+PK(TL):GOTO3740
3760 L=LEN(P$(J)):IFL<HP-6 THENSR$=P$(J):GOTO3780
3770 SR$=LEFT$(P$(J),HP-6)
3780 PRINTSR$;:SR=SM:GOSUB310:P$(J+1)=SR$
3790 HO=HP-5:GOSUB110:PRINTP$(J+1);
3800 SR=INT(SM/KD):GOSUB310:P$(J+2)=SR$
3810 Z=Z+SR:HO=HP+2:GOSUB110:PRINTP$(J+2);
3820 SR=SM-KD*SR:GOSUB310:P$(J+3)=SR$
3830 HO=HP+7:GOSUB110:PRINTP$(J+3);
3840 VE=VE+2:NEXTI
3900 GOSUB10000:VE=VM:GOSUB110:GOSUB11000
3910 PRINT"Restzetels:";ZE-Z:GOSUB10000
3920 REM RESTZETEL VERDELING
3930 IFZ>=ZE THEN5000
3940 KD=KD-1
3950 FORI=1 TOAP:J=(I-1)*4
3960 IF(VAL(P$(J+3))<>KD) OR(VAL(P$(J+1))=0) THEN3990
3965 SR=VAL(P$(J+2))+1:GOSUB310:P$(J+2)=SR$:Z=Z+1
3980 IFZ=ZE THENI=AP
3990 NEXTI:IFZ<ZE THEN3910
5000 REM UITSLAG
5010 GOSUB100:PRINT"Uitslag op de printer";
5020 P=0:HH=HP:GOSUB13000
5030 IFJN=1 THENP=1:HH=PR/2
5100 GOSUB100:PRINT"Maak je keuze:":GOSUB10000
5110 PRINT"A totaaloverzicht":PRINT
5120 PRINT"B uitslag per partij":PRINT
5130 PRINT"C verder stemmen":PRINT
5140 PRINT"D stoppen"
5150 GOSUB10000
5160 PRINT"Typ een letter";
5170 GOSUB19000:A=ASC(A$)-64
5180 IF(A<1) OR(A>4) THENPRINT"A,B,C of D GRAAG!":GOTO5170
5190 ONA GOTO5200,5500,1500,9999
5200 REM TOTAALOVERZICHT
5210 GOSUB100:SR$="TOTAALOVERZICHT"
5215 FORI=0 TOHH-9:SR$=" "+SR$:NEXTI:GOSUB12000
5220 SR$="-":GOSUB12000:SR$=""
5230 FORI=0 TOHH-5:SR$=SR$+" ":NEXTI
5240 SR$=SR$+"STEMMEN % ZETELS":GOSUB12000:SR$="-":GOSUB12000
5350 VE=5:FORI=1 TOAP:J=(I-1)*4
5360 GOSUB110:GOSUB11000:HO=0:A$=P$(J):L=LEN(A$)
5370 IFL<HH-5 THEN5390
5380 A$=LEFT$(A$,HH-5)
5390 L=LEN(A$):FORK=0 TOHH-4-L:A$=A$+" ":NEXTK
5400 SR=VAL(P$(J+1))/PK(0)*100:GOSUB310
5420 SR$=A$+P$(J+1)+"  "+SR$+" "+P$(J+2)
5430 GOSUB12000
5460 VE=VE+2:HO=0:NEXTI:SR$="-":GOSUB12000
5470 VE=VM:GOSUB110:GOSUB11000
5480 GOTO5000
5500 REM PER PARTIJ
5520 GOSUB100
5530 PRINT"UITSLAG PER PARTIJ":GOSUB10000
5540 FORSR=1 TOAP
5550 GOSUB11000:GOSUB310:SR$=SR$+" "+P$((SR-1)*4):PRINTSR$
5570 NEXTSR
5580 GOSUB10000
5590 PRINT"Van welke partij wil je de uitslag";
5620 GOSUB19000:PA=VAL(A$)
5640 IF(PA>0) AND(PA<AP+1) THEN5700
5650 PRINT"Van 1 tot ";AP;"graag!";
5660 GOTO5620
5700 GOSUB100:SR$="-":GOSUB12000:J=(PA-1)*4
5705 SR$="Uitslag van:":GOSUB12000:J=(PA-1)*4
5710 SR$=P$(J):GOSUB12000:SR$="-":GOSUB12000
5720 SR$="Aantal stemmen:"+P$(J+1):GOSUB12000
5730 SR=VAL(P$(J+1))/PK(0)*100:GOSUB310
5740 SR$="In   procenten:"+SR$:GOSUB12000
5750 SR$="Aantal  zetels:"+P$(J+2):GOSUB12000
5760 SR$="-":GOSUB12000
5770 SR$="stemmen":IFHH<20 THENHH=HM
5780 FORI=0 TOHH-7:SR$=" "+SR$:NEXTI:GOSUB12000
5800 RESTORE:TL=-1
5810 READA$:TL=TL+1:IFA$<>P$(J) THEN5810
5820 READSR$:TL=TL+1:IFLEFT$(SR$,1)="." THEN5890
5830 GOSUB11000
5840 L=LEN(SR$):IFL<HH-4 THEN5860
5850 SR$=LEFT$(SR$,HH-4):L=HH-4
5860 FORI=1 TOHH-L-3:SR$=SR$+" ":NEXTI
5870 A$=SR$:SR=PK(TL):GOSUB310:SR$=A$+SR$:GOSUB12000
5880 GOTO5820
5890 SR$="-":GOSUB12000
5900 VE=VM:GOSUB110:GOSUB11000:GOTO5000
9999 END
10000 REM ONDERSTREEP
10010 GOSUB120:V=VE:IFHO>0 THENVE=VE+1
10020 HO=0:GOSUB110:FORQ=0 TOHM:PRINT"-";
10030 NEXTQ:GOSUB120
10040 HO=0:GOSUB110:RETURN
10170 .
11000 REM-ANTI-SCROLL
11010 GOSUB120:IFVE<VM-3 THEN11060
11020 HO=0:VE=VM:GOSUB110:PRINT"Druk op INVOERtoets";
11030 GOSUB200:IFIN$<>"" THEN11050
11040 GOSUB110:PRINT"    ";:GOTO11020
11050 GOSUB100:GOSUB120
11060 RETURN
12000 REM-PRINTER-OF-SCHERM
12010 IFSR$="-" THEN12070
12020 IFP=1 THENGOSUB350:GOSUB360:GOTO12090
12030 PRINTSR$:GOTO12090
12070 IFP=0 THENGOSUB10000:GOTO12090
12080 FORI=1 TO80:GOSUB350:NEXTI:GOSUB360
12090 RETURN
13000 REM-J-OF-N-OF-WACHTWOORD
13010 GOSUB19000
13020 JN=0:IF(A$="J") OR(A$="j") THENJN=1
13030 IF(A$="N") OR(A$="n") THENJN=2
13040 IFA$=WW$ THENJN=3
13050 IFJN=0 THENPRINT"J of N graag!":GOTO13000
13060 RETURN
19000 PRINT:INPUTA$:RETURN
19500 REM-SCHERM-OPMETEN
19510 GOSUB100:PRINT"Even geduld!";
19520 HO=21:VE=15
19530 HO=HO+1:HM=HO:GOSUB110:GOSUB120:IFHM=HO THEN19530
19540 HM=HM-1:HO=0
19550 VE=VE+1:VM=VE:GOSUB110:GOSUB120:IFVM=VE THEN19550
19560 VM=VM-1:GOSUB100:GOSUB120
19570 RETURN
19600 REM WACHT
19610 FORFR=1 TOW*800
19620 NEXTFR
19630 RETURN
25000 REM  PARTIJEN EN KANDIDATEN
25050 DATA".P1","Kinderpartij"
25070 DATA"Ingrid van Dongen","Marta de Wit","Kris Zwart"
25080 DATA"Monique Bakker","Jan Gerritsen","Peter de Boer"
25085 DATA"Karel Meyer"
25090 DATA".P2","Jeugdzaken"
25100 DATA"Mark van Wezel","Frits de Bruin","Marga Welschen"
25120 DATA"Joke Barends","Hans van Dongen"
25140 DATA".P3","Speelpartij"
25150 DATA"Carolien Lammers","Frits Klaassen","Ger Arens"
25170 DATA"Klara Goedkoop","Yvonne Glastra","Tienus Bartels"
25180 DATA"Andre van de Lee","Andrea Huiskens","Piet Kwast"
25200 DATA".P4","Hobbykring"
25210 DATA"Maria van Meurs","Paul Fransen","Inge Karels"
29000 DATA"."
29010 REM-AANTAL KARAKTERS PRINTERREGEL
29020 DATA 80
29030 REM-KIESGERECHTIGDEN,ZETELS,WACHTWOORD
29040 DATA 26,6,"UITSLAG"
30000 REM VOOR PRINTERREGEL ZIE 29020
30020 REM PK( )=STEMMEN PER KANDIDAAT
30030 REM PK(0)=TOTAAL STEMMEN
30040 REM P$(0)=NAAM   )
30050 REM P$(1)=STEMMEN)PARTIJ 1
30060 REM P$(2)=ZETELS )
30070 REM P$(3)=RESTST.)
30080 REM P$(4)=NAAM   )PARTIJ 2 ENZ...
30090 REM OOK VOOR KLEINE SCHERMEN!
30100 REM   -STEM  BURO-
30110 REM NAAR EEN IDEA VAN
30120 REM    LOUIS NOUS
30130 REM
30140 REM  RIEN VAN DONGEN
30150 REM  TEL: 079 410625
30160 REM    10-01-1984
30170 REM
30180 REM COPYRIGHT
30190 REM NOS HILVERSUM

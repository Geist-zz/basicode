
1000 GOSUB3500:GOTO20:REM JAAR-OVERZICHT-1
1010 N1$="INTEGRALE HUISHOUD-REKENING"
1020 N2$="JAAR-OVERZICHT NO.1"
1030 GOSUB3500:GOSUB3600
1040 DIMN$(NR)
1050 IFCR=1 THENDIMA(NR,15)
1060 DIMB(RB,12)
1800 GOSUB8000:GOTO4000
2150 GOSUB100:N$=N1$:VE=0:GOSUB3000
2160 N$=N2$:VE=2:GOSUB3000:RETURN
2170 GOSUB100:N$=N1$:VE=0:GOSUB3000
2180 N$=N3$:VE=2:GOSUB3000:RETURN
2200 REM DATUM CONVERSIE
2210 DD=VAL(LEFT$(L$,2))
2220 MM=VAL(MID$(L$,3,2))
2230 JJ=VAL(RIGHT$(L$,2))
2240 SS=365*JJ+DD:IFMM<3 THEN2260
2250 SS=SS-INT(MM*.4+2.3):JJ=JJ+1
2260 SS=SS+INT(MM*31+((JJ-1)/4)):RETURN
3000 REM KOPPEN CENTREREN
3010 X$="---------------------------------------"
3020 X=LEN(N$):HO=INT((40-X)/2):X$=LEFT$(X$,X)
3030 GOSUB110:PRINTN$
3035 IFM=5 THENM=0:RETURN
3040 VE=VE+1:GOSUB110:PRINTX$:RETURN
3050 REM STREEP ER ONDER
3060 X$="---------------------------------------"
3070 X$=LEFT$(X$,L):IFM=5 THENM=0:RETURN
3080 PRINTX$:RETURN
3100 GOSUB250:HO=0:VE=23:GOSUB110
3110 PRINT"Druk een toets in ...";
3120 GOSUB210:PRINT:RETURN
3200 REM STRING N$(I) UITEENHALEN
3210 M1$=MID$(L$,1,6):REM DATUM
3220 M2$=MID$(L$,7,4):REM CRDGN
3230 M3$=MID$(L$,11,4):REM K/B/C/P
3240 M4$=MID$(L$,15,1):REM TEKEN
3250 M5$=MID$(L$,16,2):REM RUBRIEK
3260 M6$=MID$(L$,18,9):REM BEDRAG
3270 M7$=RIGHT$(L$,LEN(L$)-26):REM OMSCHR.
3280 RETURN
3500 REM BEREKENING 'A' IN 'CLEAR A'
3510 RESTORE:READNR:READRB
3520 A=(45+3)*NR:REM POSTEN
3530 RETURN
3600 READN$:GOSUB100:GOSUB15000
3610 GOSUB100:N$="INTEGRALE HUISHOUD-REKENING":VE=0
3620 GOSUB3000:GOSUB250:HO=3:VE=14:GOSUB110
3630 PRINT"Moet U een bestand !! NUMERIEK !!"
3640 VE=16:GOSUB110:PRINT"opslaan? (J/N): ";
3650 GOSUB210:PRINT:GOSUB23000
3660 CR=0:IFIN$="J" THENCR=1
3670 RETURN
4000 GOSUB2150:GOSUB250:HO=7:VE=10:GOSUB110
4010 PRINT"Voor welke JAAR wilt U een":VE=12:GOSUB110
4020 PRINT"JAAR-OVERZICHT? (19..): ";:INPUTJ$
4030 IF(LEN(J$)=2) OR(LEN(J$)=4) THEN4050
4040 GOTO4000
4050 IFLEN(J$)=4 THENJ$=RIGHT$(J$,2)
4060 GOTO5000
4200 IFLEN(SR$)<4 THENSR$=" "+SR$:GOTO4200
4210 RETURN
4300 B2=B2+VAL(M6$):AP=AP+1:RETURN
4400 REM DATA-BEGINSALDI-AANTALLEN BIJSTELLEN
4410 IFT=2 THEN4500
4420 IFT=3 THEN4570
4430 L$=EC$:GOSUB2200:EC=SS
4440 IFTE<EC THENEC$=TE$:EC=TE:SC$=TS$
4450 L$=LC$:GOSUB2200:LC=SS
4460 IFTL>LC THENLC$=TL$:LC=TL
4470 L$=DC$:GOSUB2200:DC=SS
4480 IFTD>DC THENDC$=TD$:DC=TD
4490 GOTO4630
4500 L$=EB$:GOSUB2200:EB=SS
4510 IFTE<EB THENEB$=TE$:EB=TE:SB$=TS$
4520 L$=LB$:GOSUB2200:LB=SS
4530 IFTL>LB THENLB$=TL$:LB=TL
4540 L$=DB$:GOSUB2200:DB=SS
4550 IFTD>DB THENDB$=TD$:DB=TD
4560 GOTO4630
4570 L$=EK$:GOSUB2200:EK=SS
4580 IFTE<EK THENEK$=TE$:EK=TE:SK$=TS$
4590 L$=LK$:GOSUB2200:LK=SS
4600 IFTL>LK THENLK$=TL$:LK=TL
4610 L$=DK$:GOSUB2200:DK=SS
4620 IFTD>DK THENDK$=TD$:DK=TD
4630 SR=AK:GOSUB300:GOSUB4200:AK$=SR$
4640 SR=AB+AP:GOSUB300:GOSUB4200:AB$=SR$
4650 SR=AP:GOSUB300:GOSUB4200:AP$=SR$
4660 SR=AC+AD:GOSUB300:GOSUB4200:AC$=SR$
4670 SR=AD:GOSUB300:GOSUB4200:AD$=SR$
4680 RETURN
4800 REM DATA EINDRAPPORT BIJSTELLEN
4810 IFJB>EC THENEC$=JB$
4820 IFJB>EB THENEB$=JB$
4830 IFJB>EK THENEK$=JB$
4840 IFJE<LC THENLC$=JE$
4850 IFJE<LB THENLB$=JE$
4860 IFJE<LK THENLK$=JE$
4870 IFJE<DC THENDC$=JE$
4880 IFJE<DB THENDB$=JE$
4890 IFJE<DK THENDK$=JE$
4895 RETURN
4900 REM POSTEN IN VOORGAAND JAAR
4910 PB=PB+1:SR=PB:GOSUB300:GOSUB4200:PB$=SR$
4920 FORK=1 TO3:GOSUB250:NEXTK:HO=0:VE=21
4930 GOSUB110:PRINT"Begin-saldo bijgesteld"
4940 HO=35:GOSUB110:PRINTPB$
4950 SR=VAL(TS$)-VAL(M6$):GOSUB310:TS$=SR$
4960 HO=25:VE=19:GOSUB110:PRINT"         "
4970 GOSUB110:PRINTTS$:RETURN
5000 N2$="Jaar-Overzicht 19"+J$
5010 GOSUB2150:FORI=1 TO3:GOSUB250:NEXTI
5020 HO=8:VE=12:GOSUB110:PRINT"Nu eerst INITIALISEREN!"
5030 SR=0:CN=2:CT=9:GOSUB310
5040 SK$=SR$:K1=0
5050 SB$=SR$:B1=0:B2=0
5060 SC$=SR$:C1=0:C2=0
5070 DK$="0101"+J$:DB$=DK$:DC$=DK$
5080 LK$=DK$:LB$=DK$:LC$=DK$
5090 JB$=DK$:L$=JB$:GOSUB2200:JB=SS
5100 EK$="3112"+J$:EB$=EK$:EC$=EK$
5110 JE$=EK$:L$=JE$:GOSUB2200:JE=SS
5120 AK=0:AB=0:AP=0:AC=0:AD=0
5130 N=0:M=0:FP=0:PB=0
5140 FORR=0 TORB:FORK=1 TO12
5150 B(R,K)=0:NEXTK:NEXTR
5160 GOSUB20000:IFN=0 THEN10000
5170 GOSUB2150:PRINT:PRINT"STAND NU"
5180 PRINT"--------                 Begin-    Aant"
5190 PRINT"           vanaf  t/m        saldo Post"
5200 L=39:GOSUB3050:PRINT"Kasboek"
5210 PRINT"Bankrek/FP":PRINT"Chequeboek":GOSUB3050
5220 GOSUB4630:HO=11:VE=9:GOSUB110
5230 PRINTEK$;" ";LK$;" ";SK$;" ";AK$
5240 VE=10:GOSUB110:PRINTEB$;" ";LB$;" ";SB$;" ";AB$
5250 VE=11:GOSUB110:PRINTEC$;" ";LC$;" ";SC$;" ";AC$
5260 HO=0:VE=14:GOSUB110
5270 PRINT"Er worden nu ";N;" posten geanalyseerd."
5280 PRINT:GOSUB3050:PRINT"Bijdrage van dit boek:"
5290 GOSUB3050:PRINTN3$:GOSUB3050
5300 HO=11:VE=19:GOSUB110
5310 PRINTTE$;" ";TL$;" ";TS$;" ":TT=0:PB=0
5320 FORI=1 TON:L$=N$(I):GOSUB3200
5330 L$=M1$:GOSUB2200:IFSS>JE THEN5460
5340 IFSS<JB THENGOSUB4900:GOTO5460
5350 IFM4$="0" THENC1=C1+VAL(M6$):AC=AC+1
5360 IFM4$="1" THENC2=C2+VAL(M6$):AD=AD+1
5370 IFM4$="2" THENB1=B1+VAL(M6$):AB=AB+1
5380 IFM4$="3" THENK1=K1+VAL(M6$):AK=AK+1
5390 IF(M4$="4") AND(FP=1) THENGOSUB4300
5400 IF(M4$="4") AND(FP=2) THEN5460
5410 R=VAL(M5$):K=VAL(MID$(M1$,3,2))
5420 B(R,K)=B(R,K)+VAL(M6$)
5430 SR=B(R,K):GOSUB310:B(R,K)=VAL(SR$)
5440 TT=TT+1:SR=TT:GOSUB300:GOSUB4200
5450 HO=35:VE=19:GOSUB110:PRINTSR$
5460 N$(I)="":NEXTI:GOSUB270:GOSUB3100:GOSUB4400
5470 GOSUB2150:GOSUB250:HO=0:VE=10:GOSUB110
5480 PRINT"Wilt U nog een bestand inlezen?":PRINT
5490 PRINT" (J/N): ";:GOSUB210:PRINT:GOSUB23000
5500 IFIN$="J" THEN5160
5510 SR=VAL(AK$)+VAL(AB$)+VAL(AC$)
5520 GOSUB300:GOSUB4200:TT$=SR$
5530 SR=VAL(AB$)-VAL(AP$):GOSUB300:GOSUB4200:AB$=SR$
5540 SR=VAL(AC$)-VAL(AD$):GOSUB300:GOSUB4200:AC$=SR$
5550 B(0,1)=VAL(J$):GOSUB21000
6000 GOSUB2150:PRINT
6010 PRINT"Wilt U een overzicht van de TOTALEN"
6020 PRINT"per BOEK? :":PRINT
6030 PRINT" 1=Video-Prints (op scherm)"
6040 PRINT" 2=Print-outs (met printer)":PRINT
6050 PRINT" 3=Einde":PRINT:PRINT
6060 GOSUB250:PRINT"Uw keuze: ";:GOSUB210:PRINTIN$
6070 W=VAL(IN$):IF(W<1) OR(W>3) THEN6000
6080 IFW=3 THEN10000
6090 SR=C1:GOSUB310:C1$=SR$
6100 SR=C2:GOSUB310:C2$=SR$
6110 SR=B1:GOSUB310:B1$=SR$
6120 SR=B2:GOSUB310:B2$=SR$
6130 SR=K1:GOSUB310:K1$=SR$
6140 GOSUB4800:N3$="Overzicht TOTALEN per BOEK"
6150 ONW GOTO6400,7200
6200 L=TB+LEN(L$)
6210 IFLEN(L$)<L THENL$=" "+L$:GOTO6210
6300 IFPT=5 THEN6320
6310 PRINTL$:L$="":RETURN
6320 SR$=L$:GOSUB350:GOSUB360
6330 SR$="":L$="":RETURN
6350 IFLEN(L$)<L THENL$=" "+L$:GOTO6350
6360 RETURN
6400 GOSUB2170:PRINT:PRINT:PT=0:GOSUB250
6410 PRINT"Aantal karakters op een regel: ";:INPUTX1
6420 PRINT"Aantal regels op scherm/blad : ";:INPUTX2
6430 IF(X1<40) OR(X2<24) THEN7000
6440 IF(PT=5) AND(X2<40) THEN7000
6450 GOSUB2170:PRINT:TB=(X1-40)/2:L=39:M=5
6460 GOSUB3050:L4$=X$:X$=""
6470 IFPT=5 THENGOSUB7400:GOSUB7600
6480 L$=L4$:GOSUB6200
6490 L$="           Bijgew. Eerste Laatste Aantl":GOSUB6200
6500 L$="Boek         t/m    post    post  postn":GOSUB6200
6510 L$=L4$:GOSUB6200
6520 L$="Kasboek    "+DK$+"  "+EK$+"  "
6530 L$=L$+LK$+"  "+AK$:GOSUB6200:GOSUB6300
6540 L$="Bankrek/FP "+DB$+"  "+EB$+"  "
6550 L$=L$+LB$:GOSUB6200:L$=AB$:L=31
6560 GOSUB6350:L$="-Bankrek"+L$:GOSUB6200
6570 L$=AP$:L=31:GOSUB6350
6580 L$="-Finplan"+L$:GOSUB6200:GOSUB6300
6590 L$="Chequeboek "+DC$+"  "+EC$+"  "
6600 L$=L$+LC$:GOSUB6200:L$=AC$:L=29
6610 GOSUB6350:L$="te verrek."+L$:GOSUB6200
6620 L$=AD$:L=29:GOSUB6350
6630 L$="verrekend "+L$:GOSUB6200
6640 L$=L4$:GOSUB6200:L$=TT$:L=33:GOSUB6350
6650 L$="Totaal"+L$:GOSUB6200
6660 L$=L4$:GOSUB6200
6700 IFPT=0 THENGOSUB3100:GOSUB2170:PRINT
6710 IFPT=5 THENGOSUB6300:GOSUB6300
6720 L$=L4$:GOSUB6200
6730 L$="         Begin-    Inkomsten Eind-":GOSUB6200
6740 L$="Boek         saldo +Uitgaven     saldo"
6750 GOSUB6200:L$=L4$:GOSUB6200
6760 SR=VAL(SK$)-VAL(K1$):GOSUB310:T1=SR
6770 L$="Kasboek  "+SK$+" "+K1$+" "+SR$
6780 GOSUB6200:GOSUB6300
6790 L$="Bankrek  "+SB$:GOSUB6200:L$=B1$:L=19
6800 GOSUB6350:L$="-opdr.   "+L$:GOSUB6200
6810 SR=VAL(SB$)-VAL(B1$)-VAL(C2$):GOSUB310
6820 L$=C2$+" "+SR$:T1=T1+SR:L=29:GOSUB6350
6830 L$="-cheq's  "+L$:GOSUB6200:GOSUB6300
6840 L$="Chequebk "+SC$:GOSUB6200
6850 SR=VAL(SC$)-VAL(C1$):GOSUB310
6860 L$=C1$+" "+SR$:T1=T1+SR:L=29:GOSUB6350
6870 L$="-te verr."+L$:GOSUB6200:GOSUB6300
6880 SR=-VAL(B2$):GOSUB310
6890 L$=B2$+" "+SR$:T1=T1+SR:L=29:GOSUB6350
6900 L$="Finplan  "+L$:GOSUB6200:L$=L4$
6910 GOSUB6200:SR=VAL(SK$)+VAL(SB$)+VAL(SC$):GOSUB310
6920 T1$=SR$:SR=VAL(K1$)+VAL(B1$)+VAL(B2$)
6930 SR=SR+VAL(C1$)+VAL(C2$):GOSUB310:T2$=SR$
6940 SR=T1:GOSUB310
6950 L$="Totaal   "+T1$+" "+T2$+" "+SR$
6960 GOSUB6200:L$=L4$:GOSUB6200:IFPT=5 THEN6000
6970 IFPT=0 THENGOSUB3100:GOTO6000
7000 FORI=1 TO3:GOSUB250:NEXTI:PRINT:PRINT
7010 PRINT"Voor  dit  overzicht  zijn 40 karakters"
7020 PRINT"op een regel nodig":PRINT
7030 PRINT"en 24 regels op het scherm":PRINT
7040 PRINT"of 40 regels op een blad!"
7050 GOSUB3100:GOTO6000
7200 GOSUB2170:PT=5:GOSUB250:HO=5:VE=8:GOSUB110
7210 PRINT"Hebt U een printer? (J/N): ";
7220 GOSUB210:PRINT:GOSUB23000
7230 IFIN$="N" THEN6000
7240 GOSUB250:HO=9:VE=10:GOSUB110
7250 PRINT"Zet deze dan NU aan!":GOSUB3100
7260 GOSUB2170:PRINT:PRINT:GOSUB250:GOTO6410
7400 GOSUB250:HO=13:VE=12:GOSUB110
7410 PRINT"Ik ben bezig!":RETURN
7600 L$=N3$:X=LEN(L$):L1=(X1+X)/2:L=L1
7610 GOSUB6210:L=X:M=5:GOSUB3050:L$=X$:L=L1
7620 GOSUB6210:RETURN
8000 GOSUB2150:GOSUB250:HO=0:VE=10:GOSUB110
8010 PRINT"Wilt U een aanvullende toelichting?":PRINT
8020 PRINT" (J/N): ";:GOSUB210:PRINT:GOSUB23000
8030 IFIN$="J" THEN8050
8040 RETURN
8050 GOSUB2150:PRINT
8060 PRINT"In dit programma kunt U al uw bestanden"
8070 PRINT"inlezen, behalve integrale bestanden.":PRINT
8080 PRINT"Na  het  inlezen van een bestand worden"
8090 PRINT"de bedragen gecumuleerd naar rubriek en"
8100 PRINT"maand in de numerieke array B(R,K).":PRINT
8110 PRINT"Tegelijkertijd wordt de oorspronkelijke"
8120 PRINT"string-array  leeg  gemaakt  en  is  uw"
8130 PRINT"computer gereed om het volgende bestand"
8140 PRINT"te ontvangen.":PRINT
8150 PRINT"Tenslotte  wordt  B(R,K)  weggeschreven"
8160 PRINT"als INPUT in  'JAAR-OVERZICHT NO.2'  en"
8170 PRINT"kunt  U  een  overzicht  krijgen van de"
8180 PRINT"TOTALEN per BOEK."
8190 GOSUB3100:GOSUB2150:PRINT
8200 PRINT"Posten  die  NIET  bij  het betreffende"
8210 PRINT"jaar behoren, worden NIET uitgeteld!":PRINT
8220 PRINT"Voor posten UIT VOORGAANDE JAREN  wordt"
8230 PRINT"het  BEGIN-SALDO  van  het  betreffende"
8240 PRINT"boek bijgesteld."
8250 PRINT"U krijgt hiervan een mededeling op  het"
8260 PRINT"scherm.":PRINT
8270 PRINT"Ook kunt U uw bestanden in willekeurige"
8280 PRINT"volgorde inlezen.":PRINT:PRINT
8290 PRINT"Er is ruimte voor 9999 posten per jaar."
8300 PRINT"(goed voor 450.000 bytes aan bestanden)":PRINT
8310 GOSUB3100:GOSUB2150:PRINT
8320 PRINT"De enige beperking is:"
8330 PRINT"Cumulatieve bedragen - ook de SOM  over"
8340 PRINT"alle bedragen - mogen +/- 99999.99 NIET"
8350 PRINT"overschrijden!":PRINT:PRINT
8360 PRINT"Niet-verrekende  cheques  worden geacht"
8370 PRINT"te zijn afgerekend op 'DATUM AFGIFTE'.":PRINT
8380 PRINT"Na  het  inlezen  van  een  bestand van"
8390 PRINT"'BANKREK/FP'  kunt  U  eenmalig  kiezen"
8400 PRINT"voor: UITTELLEN met of zonder  FINPLAN-"
8410 PRINT"posten.":GOSUB3100:GOSUB2150:PRINT
8420 L=39:GOSUB3050
8430 PRINT"Inlezen            Wegschrijven":GOSUB3050
8440 PRINT"String-array       Numerieke array":GOSUB3050
8450 PRINT" INPUT N            FOR R=0 TO RB"
8460 PRINT" FOR I=1 TO N       FOR K=1 TO 12"
8470 PRINT" INPUT N$(I)        PRINT B(R,K)"
8480 PRINT" NEXT I             NEXT K:NEXT R":GOSUB3050
8490 PRINT"                    B(0,1) = JAAR"
8500 GOSUB3100:RETURN
10000 GOSUB2150:FORI=1 TO5:GOSUB250:NEXTI
10010 HO=9:VE=12:GOSUB110
10020 PRINT"Tot de volgende keer!"
10030 END
15000 X$="":GOSUB250:HO=12:VE=12:GOSUB110
15010 PRINT"Uw code: ";:FORI=1 TOLEN(N$)
15020 GOSUB210:X$=X$+IN$:NEXTI:PRINT
15030 IFX$=N$ THENRETURN
15040 GOTO10000
20000 GOSUB100:N$="Bestand Inlezen":VE=0:GOSUB3000
20010 N$=N2$:VE=2:GOSUB3000
20100 GOSUB3100
20600 IFN=0 THENRETURN
20610 T=VAL(RIGHT$(N$(N),1))
20620 TE$=LEFT$(N$(1),6):L$=TE$:GOSUB2200:TE=SS
20630 TD$=LEFT$(N$(N),6):L$=TD$:GOSUB2200:TD=SS
20640 IFT=1 THENN3$="Chequeboek":GOTO20700
20650 IFT=2 THENN3$="Bankrek/FP":GOTO20710
20660 IFT=3 THENN3$="Kasboek":GOTO20710
20670 GOSUB2150:HO=5:VE=12:GOSUB110
20680 PRINT"Dit was een verkeerd bestand!"
20690 FORI=1 TO5:GOSUB250:NEXTI:GOSUB3100:GOTO10000
20700 TS$="     0.00":GOTO20810
20710 TS$=MID$(N$(N),7,9):IFT=2 THEN20730
20720 GOTO20810
20730 IFFP<>0 THEN20800
20740 GOSUB2150:PRINT:PRINT
20750 PRINT"Wilt U het jaar-overzicht:":PRINT
20760 PRINT" 1=met    FINPLAN-posten":PRINT
20770 PRINT" 2=zonder FINPLAN-posten":PRINT:PRINT
20780 GOSUB250:PRINT"Uw keuze: ";:GOSUB210:PRINTIN$
20790 FP=VAL(IN$):IF(FP<1) OR(FP>2) THEN20740
20800 IFFP=2 THENGOSUB20830:GOTO20820
20810 TL$=LEFT$(N$(N-1),6):L$=TL$:GOSUB2200:TL=SS
20820 N$(N)="":N=N-1:RETURN
20830 FL=0:FORI=1 TON-1:IFFL=1 THEN20860
20840 L$=N$(I):GOSUB3200
20850 IFM4$="4" THENP=I-1:FL=1
20860 NEXTI:IFFL=1 THENFL=0:GOTO20880
20870 P=N-1
20880 TL$=LEFT$(N$(P),6):RETURN
20990 RETURN
21000 GOSUB100:N$="Bestand Wegschrijven":VE=0
21010 GOSUB3000:N$=N2$:VE=2:GOSUB3000
21100 GOSUB3100
21600 RETURN
21990 RETURN
23000 REM   HL ROUTINE
23010 REM   ----------
23020 HL=ASC("a")-ASC("A")
23030 IFHL<0 THEN23200
23040 SR$="":FORZ=1 TOLEN(IN$)
23050 SR=ASC(MID$(IN$,Z,1))
23060 IF(SR>96) AND(SR<123) THENSR=SR-HL
23070 SR$=SR$+CHR$(SR)
23080 NEXTZ:IN$=SR$:RETURN
23200 HL=ABS(HL)
23210 SR$="":FORZ=1 TOLEN(IN$)
23220 SR=ASC(MID$(IN$,Z,1))
23230 IF(SR>64) AND(SR<91) THENSR=SR+HL
23240 SR$=SR$+CHR$(SR)
23250 NEXTZ:IN$=SR$:RETURN
25000 DATA101
25010 DATA20
25020 DATA"1234"
30000 REM
30010 REM HOBBYSCOOP-BASICODE2
30020 REM *                  *
30030 REM * INT.HH-REKENING  *
30040 REM * JAAR-OVERZICHT-1 *
30050 REM                    *
30060 REM P INGEZONDEN DOOR
30070 REM O                  1
30080 REM S  B.P.M.RINTJEMA  2
30090 REM T  TUINSTRAAT 13   0
30100 REM B  5802 AD VENRAY  0
30110 REM U
30120 REM S  EIGEN ONTWERP   *
30130 REM   FEB.84/ITT-2020  *
30140 REM * DEC.84/APPLE IIE *
30150 REM *                  *
30160 REM **** HILVERSUM *****
30170 REM
30180 REM COPYRIGHT  NOS - HILVERSUM

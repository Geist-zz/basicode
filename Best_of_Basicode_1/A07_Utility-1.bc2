
1000 GOSUB3500:GOTO20:REM UTILITY-1
1010 N1$="INTEGRALE HUISHOUD-REKENING"
1020 N2$="UTILITY NO.1"
1030 GOSUB3500:GOSUB3600
1040 DIMN$(NR)
1050 IFCR=1 THENDIMA(NR,15)
1060 DIMZZ(NR)
1200 CN=2:CT=9
1800 GOTO4000
2000 GOSUB250:PRINT:PRINT"- Sorteren":M=R
2010 M=INT(M/2):IFM=0 THEN2130
2020 J=1:K=R-M
2030 H=J
2040 V=H+M
2050 IFZZ(H)<=ZZ(V) THEN2110
2060 ZZ=ZZ(H):ZZ(H)=ZZ(V):ZZ(V)=ZZ
2070 Z$=N$(H):N$(H)=N$(V):N$(V)=Z$
2090 H=H-M:IFH<1 THEN2110
2100 GOTO2040
2110 J=J+1:IFJ>K THEN2010
2120 GOTO2030
2130 GOSUB250:PRINT
2135 PRINT"Gereed! - NU: GARBAGE COLLECTION"
2140 GOSUB270:RETURN
2150 GOSUB100:N$=N1$:VE=0:GOSUB3000
2160 N$=N2$:VE=2:GOSUB3000:RETURN
2170 GOSUB100:N$=N4$:VE=0:GOSUB3000
2180 N$=N3$:VE=2:GOSUB3000:RETURN
2200 REM DATUM CONTROLE+CONVERSIE
2210 IFLEN(L$)<>6 THENM=1:GOTO2830
2220 DD=VAL(LEFT$(L$,2))
2230 IF(DD<1) OR(DD>31) THENM=1:GOTO2830
2240 MM=VAL(MID$(L$,3,2))
2250 IF(MM<1) OR(MM>12) THENM=1:GOTO2830
2260 IF(MM=2) AND(DD>29) THENM=1:GOTO2830
2270 IF(MM=4) AND(DD>30) THENM=1:GOTO2830
2280 IF(MM=6) AND(DD>30) THENM=1:GOTO2830
2290 IF(MM=9) AND(DD>30) THENM=1:GOTO2830
2300 IF(MM=11) AND(DD>30) THENM=1:GOTO2830
2310 JJ=VAL(RIGHT$(L$,2))
2320 IF(JJ<70) THENM=1:GOTO2840
2330 IF((JJ/4)<>INT(JJ/4)) THEN2370
2340 SS=365*JJ+DD:IFMM<3 THEN2360
2350 SS=SS-INT(MM*.4+2.3):JJ=JJ+1
2360 SS=SS+INT(MM*31+((JJ-1)/4)):RETURN
2370 IF(MM=2) AND(DD=29) THENM=1:GOTO2830
2380 GOTO2340
2500 REM CONTROLE BEDRAG
2510 IFL$="" THENM=1:GOTO2810
2520 IFVAL(L$)<-99999.99 THENM=1:GOTO2870
2530 IFVAL(L$)>99999.99 THENM=1:GOTO2870
2540 SR=VAL(L$):GOSUB310
2550 IFLEFT$(SR$,2)="**" THENM=1:GOTO2890
2560 RETURN
2800 REM FOUTMELDINGEN
2810 GOSUB250
2820 PRINT"Ook dit gegeven hoort er bij!":GOTO4400
2830 GOSUB250:PRINT"Datum niet correct!":GOTO4400
2840 GOSUB250
2850 PRINT"Graag een datum na '010170'":GOTO4400
2860 GOSUB250:PRINT"Graag een datum ertussen!":GOTO4400
2870 GOSUB250
2880 PRINT"Bedrag tussen -99999.99 en +99999.99":GOTO4400
2890 GOSUB250:PRINT"U deed iets verkeerd!":GOTO4400
3000 REM KOPPEN CENTREREN
3010 X$="---------------------------------------"
3020 X=LEN(N$):HO=INT((40-X)/2):X$=LEFT$(X$,X)
3030 GOSUB110:PRINTN$
3040 VE=VE+1:GOSUB110:PRINTX$:RETURN
3050 REM STREEP ER ONDER
3060 X$="---------------------------------------"
3070 X$=LEFT$(X$,L):IFM=5 THENM=0:RETURN
3080 PRINTX$:RETURN
3100 GOSUB250:HO=0:VE=23:GOSUB110
3110 PRINT"Druk een toets in ...";
3120 GOSUB210:PRINT:RETURN
3200 REM STRING N$(I) UITEENHALEN
3210 M1$=MID$(L$,1,6):REM DATUM
3220 M4$=MID$(L$,15,1):REM TEKEN
3230 M6$=MID$(L$,18,9):REM BEDRAG
3240 RETURN
3500 REM BEREKENING 'A' IN 'CLEAR A'
3510 RESTORE:READNR
3520 A=(45+3)*NR:REM POSTEN
3530 RETURN
3600 READN$:GOSUB100:GOSUB15000
3610 GOSUB100:N$="INTEGRALE HUISHOUD-REKENING":VE=0
3620 GOSUB3000:GOSUB250:HO=3:VE=14:GOSUB110
3630 PRINT"Moet U een bestand !! NUMERIEK !!"
3640 VE=16:GOSUB110:PRINT"opslaan? (J/N): ";
3650 GOSUB210:PRINT:GOSUB23000
3660 CR=0:IFIN$="J" THENCR=1
3670 RETURN
4000 GOSUB2150:PRINT
4010 PRINT" 1=Aanvullende Toelichting":PRINT
4020 PRINT" 2=Corr. BEGIN-saldo Kasboek/Bankrek/FP":PRINT
4025 PRINT" 3=Corr. Totaal-Bedragen Bankrek/FP":PRINT
4030 PRINT" 4=Delen (splitsen) van bestanden":PRINT
4040 PRINT" 5=Samenvoegen van bestanden":PRINT
4050 PRINT" 6=Einde":PRINT:PRINT
4060 GOSUB250:PRINT"Uw keuze: ";:GOSUB210:PRINTIN$
4070 W=VAL(IN$):IF(W<1) OR(W>6) THEN4000
4080 ONW GOTO8000,5000,5500,6000,7000,10000
4400 REM CORRECTIE
4410 FORI=1 TO1000:NEXTI:HO=0:GOSUB110:GOSUB4440
4420 VE=VE+1:GOSUB110:GOSUB4440:RETURN
4440 PRINT"                                       "
4450 RETURN
4600 HO=0:FORI=T1 TOT2:VE=I:GOSUB110
4610 GOSUB4440:NEXTI:RETURN
5000 N4$="Correctie Begin-Saldo":GOSUB5400
5010 GOSUB20000:IFN=0 THEN4000
5020 GOSUB2170:GOSUB250:HO=0:VE=6:GOSUB110
5030 PRINT"Bijgewerkt t/m ...........: ";TD$
5040 GOSUB250:VE=8:GOSUB110
5050 PRINT"Begin-saldo NU ...........: ";TS$
5060 GOSUB250:VE=12:GOSUB110
5070 PRINT"Geef juiste BEGIN-saldo ..: ";:INPUTSB$
5080 L$=SB$:GOSUB2500:IFM=1 THENM=0:GOTO5060
5090 SB$=SR$:SB=VAL(SB$)
5100 IFT=2 THENSB=SB-VAL(TV$)
5110 GOSUB250:PRINT
5120 PRINT"EIND-saldo NU ............: ";
5130 GOSUB5300:PRINTSE$
5140 IFT=3 THEN5170
5150 PRINT"Hierin verrekende cheques : ";TV$
5160 PRINT:PRINT"Nog te verrekenen cheques : ";TU$
5170 GOSUB250:PRINT:PRINT"Is dit correct? (J/N): ";
5180 GOSUB210:PRINT:GOSUB23000
5190 IFIN$="J" THEN5210
5200 T1=10:T2=20:GOSUB4600:GOTO5060
5210 SR=T:GOSUB300:N=N+1
5220 IFT=2 THENN$(N)=TD$+SB$+TU$+TV$+SR$
5230 IFT=3 THENN$(N)=TD$+SB$+SR$
5240 GOSUB21000:GOSUB5400:GOTO4000
5300 REM EIND-SALDO
5310 FORI=1 TON:L$=N$(I):GOSUB3200
5320 IF(T=2) AND(M4$="2") THENGOSUB5360
5330 IF(T=3) AND(M4$="3") THENGOSUB5360
5340 IF(T=4) THENGOSUB5360
5350 NEXTI:SR=SB:GOSUB310:SE$=SR$:RETURN
5360 SB=SB-VAL(M6$):RETURN
5400 REM BESTAND LEEG
5410 FORI=1 TONR:N$(I)="":NEXTI
5420 N=0:A=0:P=0:T=0:GOSUB270:RETURN
5500 N4$="Correctie Totaal-bedragen":GOSUB5400
5510 SC=0:SD=0
5520 GOSUB2150:GOSUB250:HO=7:VE=12:GOSUB110
5530 PRINT"Eerst een bestand van":HO=8:VE=14
5540 GOSUB110:PRINT"CHEQUEBOEK ... inlezen!"
5550 GOSUB3100:GOSUB20000:IFN=0 THEN4000
5560 IF(T>1) THENGOSUB5400:GOTO5520
5570 FORI=1 TON:L$=N$(I):GOSUB3200
5580 IF(M4$="0") THENGOSUB5620
5590 IF(M4$="1") THENGOSUB5630
5600 NEXTI:SR=SC:GOSUB310:SC$=SR$
5610 SR=SD:GOSUB310:SD$=SR$:GOSUB5400:GOTO5700
5620 SC=SC+VAL(M6$):RETURN
5630 SD=SD+VAL(M6$):RETURN
5700 GOSUB2150:GOSUB250:HO=0:VE=12:GOSUB110
5710 PRINT"Nu het bestand van BANKREK/FINPLAN over"
5720 PRINT:PRINT"de gelijklopende periode inlezen."
5730 GOSUB3100:GOSUB20000:IFN=0 THEN4000
5740 IF(T=1) OR(T>2) THENGOSUB5400:GOTO5700
5750 N=N+1:N$(N)=TD$+TS$+SC$+SD$+"2"
5760 GOSUB21000:GOSUB5400:GOTO4000
6000 N4$="Delen van bestanden":GOSUB5400
6010 SC=0:SD=0:SR=0:GOSUB310:SC$=SR$:SD$=SR$
6020 GOSUB20000:IFN=0 THEN4000
6100 GOSUB2170:HO=0:VE=6:GOSUB110
6110 PRINT"Datum EERSTE  post: ";TE$:PRINT
6120 PRINT"Datum LAATSTE post: ";TL$
6130 GOSUB250:HO=0:VE=10:GOSUB110
6140 PRINT"Geef een datum (DDMMJJ): ";:INPUTDT$
6150 L$=DT$:GOSUB2200:IFM=1 THENM=0:GOTO6130
6160 IF(SS<=TE) OR(SS>=TL) THENGOSUB2860:GOTO6130
6170 DT=SS:FORI=1 TO3:GOSUB250:NEXTI
6180 PRINT:PRINT"Even geduld svp ..."
6190 FL=0:FORI=1 TON:IFFL=1 THEN6220
6200 M1$=LEFT$(N$(I),6):L$=M1$:GOSUB2200
6210 IFSS>=DT THENP=I-1:FL=1
6220 NEXTI:FL=0:GOSUB2170:PRINT
6300 PRINT"Totaal aantal posten: ";N:PRINT
6310 PRINT"Eerste boek:":L=12:GOSUB3050
6320 PRINT"Aantal posten ....: ";P
6330 PRINT"Datum eerste  post: ";TE$
6340 M1$=LEFT$(N$(P),6)
6350 PRINT"Datum laatste post: ";M1$
6360 PRINT:PRINT
6370 PRINT"Tweede boek:":L=12:GOSUB3050
6380 PRINT"Aantal posten ....: ";N-P
6390 M1$=LEFT$(N$(P+1),6)
6400 PRINT"Datum eerste  post: ";M1$
6410 PRINT"Datum laatste post: ";TL$:PRINT
6420 GOSUB250:PRINT:PRINT"Accoord? (J/N): ";
6430 GOSUB210:PRINT:GOSUB23000
6440 IFIN$="J" THEN6500
6450 GOTO6100
6500 NN=N:N=P+1:M$=N$(N):SR=T:GOSUB300
6510 IFT=1 THENN$(N)=DT$+SR$:GOTO6540
6520 IFT=2 THENGOSUB6800:GOTO6540
6530 N$(N)=DT$+TS$+SR$
6540 N$="Eerste bestand":GOSUB100:GOSUB250
6550 VE=12:GOSUB3000:FORI=1 TO1000:NEXTI
6560 GOSUB21000:N$(N)=M$:N=N-1:IFT=1 THEN6610
6600 SB=VAL(TS$):GOSUB5300
6610 FORI=1 TON:N$(I)="":NEXT
6620 FORI=1 TONN-N:N$(I)=N$(N+I):NEXTI
6630 SR=T:GOSUB300:N=NN-N+1
6640 IFT=1 THENN$(N)=TD$+SR$:GOTO6670
6650 IFT=2 THENGOSUB6810:GOTO6670
6660 N$(N)=TD$+SE$+SR$
6670 N$="Tweede bestand":GOSUB100:GOSUB250
6680 VE=12:GOSUB3000:FORI=1 TO1000:NEXTI
6690 GOSUB21000:GOSUB5400:GOTO4000
6800 N$(N)=DT$+TS$+SC$+SD$+SR$:RETURN
6810 N$(N)=TD$+SE$+SC$+SD$+SR$:RETURN
7000 N4$="Bestanden samenvoegen":GOSUB5400
7010 T1=0:SK=0:SB=0:SC=0:SD=0:SI=0
7020 SR=0:GOSUB310:SC$=SR$:SD$=SR$
7030 GOSUB20000:IFN=0 THEN4000
7040 IF(T=4) AND(T1=0) THENT1=4
7050 IF(T1=4) AND(T<>4) THENGOSUB20680:GOTO4000
7060 IF(T=1) AND(SC=0) THENGOSUB7800
7070 IF(T=2) AND(SB=0) THENGOSUB7820
7080 IF(T=3) AND(SK=0) THENGOSUB7840
7090 IF(T=4) AND(SI=0) THENGOSUB7860
7100 ONT GOSUB7600,7630,7660,7690
7200 GOSUB100:N$=N4$:VE=0:GOSUB3000
7210 FORI=1 TO3:GOSUB250:NEXTI:HO=0:VE=8:GOSUB110
7220 PRINT"Er zijn nu ";N;" posten in het geheugen"
7230 L=37:GOSUB3050
7240 PRINT"Nog ";NR-N-1;" posten kunnen worden opgenomen"
7250 HO=0:VE=12:GOSUB110
7260 PRINT"Wilt U nog een bestand inlezen?":PRINT
7270 PRINT" (J/N): ";:GOSUB210:PRINT:GOSUB23000
7280 IFIN$="J" THEN7030
7290 IFT=4 THENM$=DI$+SI$+"4":GOTO7400
7300 IF(SC<>0) AND((SB<>0) OR(SK<>0)) THENT=4
7310 IF(SK<>0) AND(SB<>0) THENT=4
7320 IFT=4 THEN7500
7330 IFT=1 THENM$=DC$+"1"
7340 IFT=2 THENM$=DB$+SB$+SC$+SD$+"2"
7350 IFT=3 THENM$=DK$+SK$+"3"
7400 GOSUB100:N$="Sorteren met SHELL-SORT"
7410 VE=0:GOSUB3000:PRINT:GOSUB250
7420 PRINT"- Datum converteren en in index zetten,"
7430 FORI=1 TON:L$=LEFT$(N$(I),6)
7440 GOSUB2200:ZZ(I)=SS:NEXTI
7450 R=N:GOSUB2000:N=N+1:N$(N)=M$
7460 GOSUB21000:GOSUB5400:GOTO4000
7500 N3$="Integraal Bestand":DI$=TE$:DI=TE
7510 IFSK<>0 THENSI=SI+SK
7520 IFSB<>0 THENSI=SI+SB
7530 SR=SI:GOSUB310:SI$=SR$
7540 IFDC>DI THENDI$=DC$:DI=DC
7550 IFDB>DI THENDI$=DB$:DI=DB
7560 IFDK>DI THENDI$=DK$:DI=DK
7570 M$=DI$+SI$+"4":GOTO7400
7600 IFTE<EC THENEC=TE
7610 IFTL>LC THENLC=TL:DC$=TD$:DC=TD
7620 RETURN
7630 IFTE<EB THENEB=TE:SB$=TS$:SB=VAL(SB$)
7640 IFTL>LB THENLB=TL:DB$=TD$:DB=TD
7650 RETURN
7660 IFTE<EK THENEK=TE:SK$=TS$:SK=VAL(SK$)
7670 IFTL>LK THENLK=TL:DK$=TD$:DK=TD
7680 RETURN
7690 IFTE<EI THENEI=TE:SI$=TS$:SI=VAL(SI$)
7700 IFTL>LI THENLI=TL:DI$=TD$:DI=TD
7710 RETURN
7800 EC$=TE$:LC$=TL$:DC$=TD$
7810 EC=TE:LC=TL:DC=TD:SC=1:RETURN
7820 EB$=TE$:LB$=TL$:DB$=TD$:SB$=TS$
7830 EB=TE:LB=TL:DB=TD:SB=VAL(SB$):RETURN
7840 EK$=TE$:LK$=TL$:DK$=TD$:SK$=TS$
7850 EK=TE:LK=TL:DK=TD:SK=VAL(SK$):RETURN
7860 EI$=TE$:LI$=TL$:DI$=TD$:SI$=TS$
7870 EI=TE:LI=TL:DI=TD:SI=VAL(SI$):RETURN
8000 N3$="Aanvullende Toelichting"
8010 N4$=N2$:GOSUB2170:PRINT
8020 HO=0:VE=10:GOSUB110
8030 PRINT"Met  dit  programma  kunt U wijzigingen"
8040 PRINT"aanbrengen in uw  bestanden  waarin  de"
8050 PRINT"overige programma's niet voorzien."
8060 GOSUB3100:GOSUB2170:PRINT
8070 PRINT"1.Correctie BEGIN-SALDO":L=23:GOSUB3050
8080 PRINT"  van Kasboek of Bankrek/Finplan"
8090 HO=2:VE=8:L=30:GOSUB110:GOSUB3050
8100 PRINT"  -lees het betreffende bestand in,"
8110 PRINT"  -geef het juiste BEGIN-saldo,"
8120 PRINT"  -controleer het EIND-saldo.":PRINT
8130 PRINT"Als U het daarmee eens bent, wordt  het"
8140 PRINT"bestand weggeschreven.":PRINT
8150 PRINT"Bij Bankrek/Finplan  wordt  het  bedrag"
8160 PRINT"AFGEREKENDE cheques verrekend."
8170 PRINT"Het  EIND-saldo  moet  dus overeenkomen"
8180 PRINT"met dat van uw Bank-AFREKENING."
8190 GOSUB3100:GOSUB2170:PRINT
8200 PRINT"2.Correctie Totaal-bedragen"
8210 L=27:GOSUB3050
8220 PRINT"In  de  laatste  post - N$(N) - van een"
8230 PRINT"bestand  van  BANKREK/FINPLAN  zijn  de"
8240 PRINT"Totaal-bedragen   van   het  CHEQUEBOEK"
8250 PRINT"opgenomen.":PRINT
8260 PRINT"Na  het  delen  cq samenvoegen van deze"
8270 PRINT"bestanden zijn deze Totaal-bedragen  op"
8280 PRINT"'nul' gezet."
8290 PRINT"U kunt dit als volgt herstellen:":PRINT
8300 PRINT"-lees het bestand van CHEQUEBOEK in,"
8310 PRINT"-lees het bestand van BANKREK/FP in.":PRINT
8320 PRINT"Daarna wordt het bestand weggeschreven."
8330 GOSUB3100:GOSUB2170:PRINT
8340 PRINT"3.Delen van bestanden":L=21:GOSUB3050
8350 PRINT"  -lees het betreffende bestand in,"
8360 PRINT"  -geef datum (DDMMJJ) waar  de  deling"
8370 PRINT"   moet komen, dwz:"
8380 PRINT"   EERSTE boek LOOPT TOT die datum of"
8390 PRINT"   eindigt eerder."
8400 PRINT"   TWEEDE boek BEGINT OP die datum of"
8410 PRINT"   later."
8420 PRINT"  -controleer of U het eens bent met de"
8430 PRINT"   getoonde informatie.":PRINT
8440 PRINT"Daarna   worden   de  bestanden  wegge-"
8450 PRINT"schreven.":PRINT
8460 PRINT"Het BEGIN-saldo van het TWEEDE boek  is"
8470 PRINT"het EIND-saldo van het EERSTE boek."
8480 GOSUB3100:GOSUB2170:PRINT
8490 PRINT"3.Delen van bestanden (vervolg)"
8500 L=21:GOSUB3050
8510 PRINT"Als   U  bestanden  van  Bankrek/FP  en":PRINT
8520 PRINT"Chequeboek splitst  over  gelijklopende":PRINT
8530 PRINT"perioden,  dan  moet  U het Begin-saldo":PRINT
8540 PRINT"van Bankrek/FP No.2 corrigeren!"
8550 GOSUB3100:GOSUB2170:PRINT
8560 PRINT"4.Samenvoegen van bestanden"
8570 L=27:GOSUB3050
8580 PRINT"  -lees de (sub-)bestanden in,"
8590 PRINT"  -vervolgens   worden  de  posten  ge-"
8600 PRINT"   sorteerd op datum."
8610 PRINT"Daarna wordt het bestand weggeschreven.":PRINT
8620 PRINT"Int.bestand + int.bestand is mogelijk,"
8630 PRINT"int.bestand + ander bestand niet!":PRINT
8640 PRINT"Bij een INTEGRAAL bestand is de laatste"
8650 PRINT"post een samenvoeging van:"
8660 PRINT"-de  laatste  datum  van de data dat de"
8670 PRINT" boeken werden bijgewerkt,"
8680 PRINT"-de som van de begin-saldi als dat  van"
8690 PRINT" toepassing is,"
8700 PRINT"-het teken '4'."
8710 GOSUB3100:GOSUB2170:PRINT
8720 PRINT"U  kunt  aldus  uw  bestanden groter of"
8730 PRINT"kleiner maken.":PRINT
8740 PRINT"Door   samenvoegen  van  bestanden  van"
8750 PRINT"verschillende categorie ontstaan  inte-"
8760 PRINT"grale bestanden!"
8770 PRINT"Iedere combinatie van  boeken  is  toe-"
8780 PRINT"gestaan.":PRINT
8790 PRINT"Als U voor het aanmaken  van  integrale"
8800 PRINT"bestanden  uw  bestanden moet splitsen,"
8810 PRINT"zorg dan voor een zekere gelijkloop van"
8820 PRINT"de   sub-bestanden  qua  datum  van  de"
8830 PRINT"eerste post en die van de laatste post.":PRINT
8840 PRINT"Integrale  bestanden  moeten  op elkaar"
8850 PRINT"aansluiten!"
8860 GOSUB3100:GOSUB2170:PRINT
8870 PRINT"Evenals al uw overige bestanden  kunnen"
8880 PRINT"integrale bestanden worden ingelezen in"
8890 PRINT"het 'UNIVERSELE PRINT/PRINTER-"
8900 PRINT"PROGRAMMA'.":PRINT
8910 PRINT"De ingelezen bestanden worden daar  20%"
8920 PRINT"groter  gemaakt  door  het tussenvoegen"
8930 PRINT"van het saldo na iedere post.":PRINT
8940 PRINT"Uiteraard moeten de vergrote  bestanden"
8950 PRINT"passen in uw computer!"
8960 GOSUB3100:GOSUB2170:PRINT
8970 L=39:GOSUB3050
8980 PRINT"Inlezen            Wegschrijven":GOSUB3050
8990 PRINT"20100-20500        21100-21500":GOSUB3050
9000 PRINT"INPUT A            PRINT N"
9010 PRINT"FOR I=N+1 TO N+A   FOR I=1 TO N"
9020 PRINT"INPUT N$(I)        PRINT N$(I)"
9030 PRINT"NEXT I             NEXT I":GOSUB3050
9040 PRINT"Lengte van een post: MAX. 45 kar.":PRINT
9050 PRINT:PRINT"Denk aan max. aantal posten!"
9060 PRINT"(Regel 25000 DATA ";NR;")"
9070 GOSUB3100:GOTO4000
10000 GOSUB2150:FORI=1 TO5:GOSUB250:NEXTI
10010 HO=9:VE=12:GOSUB110
10020 PRINT"Tot de volgende keer!"
10030 END
15000 X$="":GOSUB250:HO=12:VE=12:GOSUB110
15010 PRINT"Uw code: ";:FORI=1 TOLEN(N$)
15020 GOSUB210:X$=X$+IN$:NEXTI:PRINT
15030 IFX$=N$ THENRETURN
15040 GOTO10000
20000 GOSUB100:N$="Bestand Inlezen":VE=0:GOSUB3000
20100 GOSUB3100
20600 N=N+A:IFN=0 THENRETURN
20610 T=VAL(RIGHT$(N$(N),1))
20620 IFW=2 THEN20660
20630 IFW=3 THEN20650
20640 IFT=4 THENN3$="Integraal Bestand":GOTO20740
20650 IFT=1 THENN3$="Chequeboek":GOTO20750
20660 IFT=2 THENN3$="Bankrek/Finplan":GOTO20730
20670 IFT=3 THENN3$="Kasboek":GOTO20740
20680 GOSUB2150:FORI=1 TO3:GOSUB250:NEXTI
20690 HO=0:VE=12:GOSUB110
20700 PRINT"U hebt het verkeerde bestand ingelezen!"
20710 FORI=1 TON:N$(I)="":NEXTI:N=0
20720 GOSUB3100:GOSUB270:RETURN
20730 TU$=MID$(N$(N),16,9):TV$=MID$(N$(N),25,9)
20740 TS$=MID$(N$(N),7,9)
20750 TD$=LEFT$(N$(N),6)
20760 L$=TD$:GOSUB2200:TD=SS
20770 TE$=LEFT$(N$(N-A+1),6)
20780 L$=TE$:GOSUB2200:TE=SS
20790 TL$=LEFT$(N$(N-1),6)
20800 L$=TL$:GOSUB2200:TL=SS
20810 N$(N)="":N=N-1:A=0
20820 RETURN
20990 RETURN
21000 GOSUB100:N$="Bestand Wegschrijven"
21010 VE=0:GOSUB3000:N$=N3$:VE=2:GOSUB3000
21100 GOSUB3100
21600 RETURN
23000 REM   HL ROUTINE
23010 REM   ----------
23020 HL=ASC("a")-ASC("A")
23030 IFHL<0 THEN23200
23040 SR$="":FORZ=1 TOLEN(IN$)
23050 SR=ASC(MID$(IN$,Z,1))
23060 IF(SR>96) AND(SR<123) THENSR=SR-HL
23070 SR$=SR$+CHR$(SR)
23080 NEXTZ:IN$=SR$:RETURN
23200 HL=ABS(HL)
23210 SR$="":FORZ=1 TOLEN(IN$)
23220 SR=ASC(MID$(IN$,Z,1))
23230 IF(SR>64) AND(SR<91) THENSR=SR+HL
23240 SR$=SR$+CHR$(SR)
23250 NEXTZ:IN$=SR$:RETURN
25000 DATA101
25010 DATA"1234"
30000 REM
30010 REM HOBBYSCOOP-BASICODE2
30020 REM *                  *
30030 REM * INT.HH-REKENING  *
30040 REM *   UTILITY NO.1   *
30050 REM                    *
30060 REM P INGEZONDEN DOOR
30070 REM O                  1
30080 REM S  B.P.M.RINTJEMA  2
30090 REM T  TUINSTRAAT 13   0
30100 REM B  5802 AD VENRAY  0
30110 REM U
30120 REM S  EIGEN ONTWERP   *
30130 REM   FEB.84/ITT-2020  *
30140 REM * DEC.84/APPLE IIE *
30150 REM *                  *
30160 REM **** HILVERSUM *****
30170 REM
30180 REM COPYRIGHT  NOS - HILVERSUM

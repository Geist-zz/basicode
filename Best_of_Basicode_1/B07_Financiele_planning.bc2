
1000 GOSUB3500:GOTO20:REM FINANC.PLANNING
1010 N1$="INTEGRALE HUISHOUD-REKENING"
1020 N2$="FINANCIELE PLANNING":GOSUB3500
1040 DIMN$(NR)
1045 DIMRB$(RB)
1050 DIMZZ(NR),Z(NR)
1055 GOSUB3600
1060 IFCR=1 THENDIMA(NR,15)
1070 DIMA$(12)
1200 CN=2:CT=9
1800 M=0:N=0:GOTO3800
2000 GOSUB250:PRINT:PRINT"- Sorteren":M=R
2010 M=INT(M/2):IFM=0 THEN2130
2020 J=1:K=R-M
2030 H=J
2040 V=H+M
2050 IFZZ(H)<=ZZ(V) THEN2110
2060 ZZ(0)=ZZ(H):ZZ(H)=ZZ(V):ZZ(V)=ZZ(0)
2070 IFCR=4 THENZ$=N$(H):N$(H)=N$(V):N$(V)=Z$
2080 IFCR=5 THENZ(0)=Z(H):Z(H)=Z(V):Z(V)=Z(0)
2090 H=H-M:IFH<1 THEN2110
2100 GOTO2040
2110 J=J+1:IFJ>K THEN2010
2120 GOTO2030
2130 GOSUB250:PRINT
2135 PRINT"Gereed! - NU: GARBAGE COLLECTION"
2140 GOSUB270:RETURN
2150 GOSUB100:N$=N1$:VE=0:GOSUB3000
2160 N$=N2$:VE=2:GOSUB3000:RETURN
2170 GOSUB100:N$=N2$:VE=0:GOSUB3000
2180 N$=N3$:VE=2:GOSUB3000:RETURN
2200 REM DATUM CONTROLE + CONVERSIE
2210 IFLEN(L$)<>6 THENM=1:GOTO2810
2220 DD=VAL(LEFT$(L$,2))
2230 IF(DD<1) OR(DD>31) THENM=1:GOTO2830
2240 MM=VAL(MID$(L$,3,2))
2250 IF(MM<1) OR(MM>12) THENM=1:GOTO2830
2260 IF(MM=2) AND(DD>29) THENM=1:GOTO2830
2270 IF(MM=4) AND(DD>30) THENM=1:GOTO2830
2280 IF(MM=6) AND(DD>30) THENM=1:GOTO2830
2290 IF(MM=9) AND(DD>30) THENM=1:GOTO2830
2300 IF(MM=11) AND(DD>30) THENM=1:GOTO2830
2310 JJ=VAL(RIGHT$(L$,2))
2320 IF(JJ<70) THENM=1:GOTO2840
2330 IF((JJ/4)<>INT(JJ/4)) THEN2370
2340 SS=365*JJ+DD:IFMM<3 THEN2360
2350 SS=SS-INT(MM*.4+2.3):JJ=JJ+1
2360 SS=SS+INT(MM*31+((JJ-1)/4)):RETURN
2370 IF(MM=2) AND(DD=29) THENM=1:GOTO2830
2380 GOTO2340
2400 REM CONTROLE RUBRIEKNUMMERS
2410 IF(VAL(L$)<1) OR(VAL(L$)>RB) THENM=1:GOTO2860
2420 IFL$="19" THENM=1:GOTO2880
2430 IFLEN(L$)=1 THENL$="0"+L$
2440 RETURN
2500 REM CONTROLE BEDRAG
2510 IFL$="" THENM=1:GOTO2810
2520 IFVAL(L$)<-99999.99 THENM=1:GOTO2900
2530 IFVAL(L$)>99999.99 THENM=1:GOTO2900
2540 SR=VAL(L$):GOSUB310
2550 IFLEFT$(SR$,2)="**" THENM=1:GOTO2920
2560 RETURN
2600 REM CONTROLE OMSCHRIJVING
2610 IFL$="" THENM=1:GOTO2810
2620 IFLEN(L$)>L THENM=1:GOTO2930
2630 RETURN
2700 REM CONTROLE PRIOR.NO.
2710 IFL$="" THENM=1:GOTO2810
2720 IF(VAL(L$)<1) OR(VAL(L$)>9) THENM=1:GOTO2860
2730 RETURN
2800 REM FOUTMELDINGEN
2810 GOSUB250
2820 PRINT"Ook dit gegeven hoort er bij!":GOTO4400
2830 GOSUB250:PRINT"Datum niet correct!":GOTO4400
2840 GOSUB250
2850 PRINT"Graag een datum na '010170'":GOTO4400
2860 GOSUB250
2870 PRINT"Buiten toegestane reeks!":GOTO4400
2880 GOSUB250
2890 PRINT"Dit rubrieknummer kan hier niet!":GOTO4400
2900 GOSUB250
2910 PRINT"Bedrag tussen -99999.99 en +99999.99":GOTO4400
2920 GOSUB250:PRINT"U deed iets verkeerd!":GOTO4400
2930 GOSUB250:PRINT"Omschr. MAX.";L;" kar.!":GOTO4400
2940 GOSUB250:PRINT"Moet in het hetzelfde jaar!":GOTO4400
2950 GOSUB250
2960 PRINT"Niet plannen in het verleden!":GOTO4400
2970 GOSUB250
2980 PRINT"Dit is een 'EENMALIGE' post!":GOTO4400
3000 REM KOPPEN CENTREREN
3010 X$="---------------------------------------"
3020 X=LEN(N$):HO=INT((40-X)/2):X$=LEFT$(X$,X)
3030 GOSUB110:PRINTN$
3035 IFM=5 THENM=0:RETURN
3040 VE=VE+1:GOSUB110:PRINTX$:RETURN
3050 REM STREEP ER ONDER
3060 X$="---------------------------------------"
3070 X$=LEFT$(X$,L):IFM=5 THENM=0:RETURN
3080 PRINTX$:RETURN
3100 GOSUB250:HO=0:VE=23:GOSUB110
3110 PRINT"Druk een toets in ...";
3120 GOSUB210:PRINT:RETURN
3150 GOSUB250:HO=0:VE=23:GOSUB110
3160 PRINT"T=Terug/S=Stoppen/V=Verder ...";
3170 GOSUB210:PRINT:GOSUB23000:RETURN
3200 REM STRING UITEENRAFELEN
3210 M1$=MID$(L$,1,6):REM DATUM
3220 M2$=MID$(L$,7,4):REM CRDGN
3230 M3$=MID$(L$,11,4):REM K/B/C/P
3240 M4$=MID$(L$,15,1):REM TEKEN
3250 M5$=MID$(L$,16,2):REM RUBRIEK
3260 M6$=MID$(L$,18,9):REM BEDRAG
3270 M7$=RIGHT$(L$,LEN(L$)-26):REM OMSCHR.
3280 RETURN
3300 REM PRINT RUBRIEKEN
3310 L=39:GOSUB3050:D=RB
3320 IF(D<>2*INT(D/2)) THEND=D+1
3330 D=.5*D:FORI=1 TOD:SR=I:GOSUB300
3340 M1$=SR$:IFLEN(M1$)=1 THENM1$=" "+M1$
3350 SR=I+D:GOSUB300:M2$=SR$
3360 IFLEN(M2$)=1 THENM2$=" "+M2$
3370 IFLEN(RB$(I))<16 THEN3430
3380 L$=M1$+"="+RB$(I):IF(I+D)>RB THEN3400
3390 L$=L$+M2$+"="+RB$(I+D)
3400 PRINTL$:NEXTI:GOSUB3050
3410 PRINT"UITGAVEN: +        INKOMSTEN: -"
3420 GOSUB3050:GOSUB270:RETURN
3430 RB$(I)=RB$(I)+" ":GOTO3370
3500 REM BEREKENING 'A' IN 'CLEAR A'
3510 RESTORE:READNR:READRB
3520 A=(45+3)*NR:REM POSTEN
3530 A=A+(16+3)*RB:REM RUBRIEKEN
3540 A=A+(45+3)*12:REM PERIOD.OVERSCHR.
3550 RETURN
3600 READN$:GOSUB100:GOSUB15000
3610 GOSUB2150:GOSUB250:HO=3:VE=14:GOSUB110
3620 PRINT"Moet U een bestand !! NUMERIEK !!"
3630 VE=16:GOSUB110:PRINT"opslaan? (J/N): ";
3640 GOSUB210:PRINT:GOSUB23000
3650 CR=0:IFIN$="J" THENCR=1
3660 RETURN
3800 REM RUBRIEKEN INLEZEN UIT DATA-REGELS
3810 FORI=1 TORB:READRB$(I):NEXTI
4000 GOSUB2150:PRINT
4010 PRINT" 1=Aanvullende Toelichting":PRINT
4020 PRINT" 2=Periodieke Overschrijvingen":PRINT
4030 PRINT" 3=Financiele Planning":PRINT
4040 PRINT" 4=Overzicht Rubrieken":PRINT
4050 PRINT" 5=Einde":PRINT:PRINT
4060 GOSUB250:PRINT"Uw keuze: ";:GOSUB210:PRINTIN$
4070 W=VAL(IN$):IF(W<1) OR(W>5) THEN4000
4080 ONW GOTO8000,5000,6000,7000,10000
4400 REM CORRECTIE
4410 FORK=1 TO1000:NEXTK:HO=0:GOSUB110:GOSUB4450
4420 VE=VE+1:GOSUB110:GOSUB4450
4430 VE=VE+1:GOSUB110:GOSUB4450
4440 VE=VE-3:RETURN
4450 PRINT"                                       ":RETURN
4500 REM SCHERM SCHOON
4510 HO=0:FORK=T1 TOT2:VE=K:GOSUB110:GOSUB4450
4520 NEXTK:RETURN
4800 GOSUB2170:FORK=1 TO3:GOSUB250:NEXTK
4810 HO=5:VE=12:GOSUB110:PRINT"Hiermee overschrijdt U"
4820 VE=14:GOSUB110:PRINT"het max. aantal posten."
4830 GOSUB3100:GOSUB5960:GOTO5020
4900 REM TERUG NAAR HOOFD-MENU
4910 IFBW=1 THENGOSUB21000
4920 GOTO5860
5000 N3$="Periodieke Overschrijvingen"
5010 BW=0:N=0:GOSUB20000:IFN=0 THEN4000
5020 GOSUB2170:PRINT
5030 PRINT"Bankrek/Finplan bijgewerkt t/m ";DB$
5040 L=39:GOSUB3050
5050 PRINT"Er zijn nu ";N;" posten in het bestand"
5060 GOSUB3050
5070 PRINT"Nog ";NR-N-1;" posten kunnen worden opgenomen"
5080 GOSUB3050:PRINT
5090 L$=DB$:GOSUB2200:DB=SS
5100 JJ$=RIGHT$(DB$,2):A=0
5110 PRINT" 1=per maand     - niet na ";"3011"+JJ$
5120 PRINT" 2=per 2 maanden - niet na ";"3110"+JJ$
5130 PRINT" 3=per 3 maanden - niet na ";"3009"+JJ$
5140 PRINT" 4=per 4 maanden - niet na ";"3108"+JJ$
5150 PRINT" 5=per half jaar - niet na ";"3006"+JJ$
5160 PRINT:PRINT" 6=terug naar HOOFD-MENU":PRINT
5170 GOSUB250:PRINT"Uw keuze: ";:GOSUB210:PRINTIN$
5180 W=VAL(IN$):IF(W<1) OR(W>6) THEN5020
5190 IFW=1 THENSP=1:DA$="3011"+JJ$
5200 IFW=2 THENSP=2:DA$="3110"+JJ$
5210 IFW=3 THENSP=3:DA$="3009"+JJ$
5220 IFW=4 THENSP=4:DA$="3108"+JJ$
5230 IFW=5 THENSP=6:DA$="3006"+JJ$
5240 IFW=6 THEN4900
5250 L$=DA$:GOSUB2200:DA=SS
5260 GOSUB100:N$=N3$:VE=0:M=5:GOSUB3000:GOSUB3300
5270 GOSUB250:HO=0:VE=16:GOSUB110
5280 PRINT"Plandatum (DDMMJJ) : ";:INPUTL1$
5290 L$=L1$:GOSUB2200:IFM=1 THENM=0:GOTO5270
5300 DP=SS:J$=RIGHT$(L1$,2):IFJ$=JJ$ THEN5320
5310 GOSUB2940:GOTO5270
5320 IFDP<DB THENGOSUB2950:GOTO5270
5330 IFDP>(DA) THENGOSUB2970:GOTO5020
5340 GOSUB250:L=19:HO=0:VE=17:GOSUB110
5350 PRINT"Omschr. (MAX.19 k.): ";:INPUTL7$
5360 L$=L7$:GOSUB2600:IFM=1 THENM=0:GOTO5340
5370 GOSUB250:HO=0:VE=18:GOSUB110
5380 PRINT"Rubriek No. (1-20) : ";:INPUTL5$
5390 L$=L5$:GOSUB2400:IFM=1 THENM=0:GOTO5370
5400 L5$=L$
5410 GOSUB250:HO=0:VE=19:GOSUB110
5420 PRINT"Bedrag ............: ";:INPUTL6$
5430 L$=L6$:GOSUB2500:IFM=1 THENM=0:GOTO5410
5440 L6$=SR$
5450 GOSUB250:HO=0:VE=20:GOSUB110
5460 PRINT"Prioriteit (1-9) ..: ";:INPUTL3$
5470 L$=L3$:GOSUB2700:IFM=1 THENM=0:GOTO5450
5480 L3$="PLA"+L3$
5490 GOSUB250:PRINT:PRINT"Is dit correct? (J/N): ";
5500 GOSUB210:PRINT:GOSUB23000
5510 IFIN$="J" THENDP$=L1$:GOTO5530
5520 GOSUB5910:GOSUB5920:GOTO5270
5530 GOSUB2170:L=39:HO=0:VE=14:GOSUB110
5540 GOSUB3050:VE=18:GOSUB5940:HO=0:VE=8:GOSUB110
5550 A$="+000"+L3$+"4"+L5$+L6$+L7$
5560 A=A+1:IF(N+A)>NR THEN4800
5570 A$(A)=L1$+A$
5580 DD$=LEFT$(DP$,2)
5590 MM$=MID$(DP$,3,2)
5600 SR=VAL(MM$)+SP:IFSR>12 THEN5670
5610 GOSUB300:MM$=SR$
5620 IFLEN(MM$)=1 THENMM$="0"+MM$
5630 DP$=DD$+MM$+JJ$:L1$=DP$
5640 L$=L1$:GOSUB2200
5650 IFM=1 THENM=0:GOSUB5870:GOTO5640
5660 GOTO5560
5670 GOSUB2170:L=39:HO=0:VT=4+12/SP:VE=VT
5680 GOSUB110:GOSUB3050
5690 HO=0:VE=4:GOSUB110:FORI=1 TOA
5700 DP$=A$(I):IFLEN(DP$)>39 THENDP$=LEFT$(DP$,39)
5710 PRINTDP$:NEXTI:GOSUB250
5720 VE=VT+2:GOSUB110:PRINT"Accoord? (J/N): ";
5730 GOSUB210:PRINT:GOSUB23000
5740 IFIN$="J" THEN5760
5750 GOSUB5960:GOTO5260
5760 REM BESTANDEN SAMENVOEGEN
5770 FORI=1 TOA
5780 N$(N+I)=A$(I):A$(I)=""
5790 NEXTI:N=N+A:A=0
5800 GOSUB100:N$="Sorteren met SHELL-sort"
5810 VE=0:GOSUB3000:PRINT:GOSUB250
5820 PRINT"- Datum converteren en in index zetten,"
5830 FORI=1 TON:L$=LEFT$(N$(I),6)
5840 GOSUB2200:ZZ(I)=SS:NEXTI
5850 CR=4:R=N:GOSUB2000:BW=1:GOTO5020
5860 FORI=1 TON:N$(I)="":NEXTI:N=0:GOTO4000
5870 SR=VAL(DD$)-1:GOSUB300:DD$=SR$
5880 IFLEN(DD$)=1 THENDD$="0"+DD$
5890 L1$=DD$+MM$+JJ$:HO=0:VE=8:GOSUB110
5900 RETURN
5910 T1=15:T2=22:GOSUB4500:RETURN
5920 HO=11:VE=15:GOSUB110
5930 PRINT"*** Herhaling ***":RETURN
5940 FORK=1 TO3:GOSUB250:NEXTK:HO=13
5950 GOSUB110:PRINT"Ik ben bezig!":RETURN
5960 FORI=1 TOA:A$(I)="":NEXTI:A=0:RETURN
6000 N3$="Financiele Planning"
6010 N=0:GOSUB20000:IFN=0 THEN4000
6020 L$=DB$:GOSUB2200:DB=SS
6030 SB=VAL(SB$)-VAL(SC$)-VAL(SD$):BS=SB
6100 GOSUB2150:GOSUB250:HO=0:VE=10:GOSUB110
6110 PRINT"Geef DATUM (DDMMJJ) t/m waar":PRINT
6120 PRINT"moet worden geselecteerd: ";:INPUTDS$
6130 L$=DS$:GOSUB2200:IFM=1 THENM=0:GOTO6100
6140 DS=SS:P=0:Q=0
6200 GOSUB2150:GOSUB250:HO=0:VE=12:GOSUB110
6210 PRINT"Moet  er  rekening  worden gehouden met"
6220 PRINT:PRINT"prioriteiten? (J/N): ";
6230 GOSUB210:PRINTIN$:GOSUB23000
6240 SK=2:IFIN$="J" THENSK=1
6300 GOSUB2150:VE=12:GOSUB5940:FORI=1 TON
6310 L$=N$(I):GOSUB3200:L$=M1$:GOSUB2200
6320 IFSS<DB THENGOSUB6350
6330 IFSS<=DS THENQ=I
6340 NEXTI:P=P+1:GOTO6360
6350 BS=BS-VAL(M6$):P=I:RETURN
6360 R=0:BL=0:BT=INT((Q-P+1)/17)
6370 IF17*BT<>(Q-P+1) THENBT=BT+1
6380 GOSUB6800
6390 IFSK=2 THEN6610
6500 REM SELECTIE OP PRIORITEIT
6510 FORI=1 TO9:SR=I:GOSUB300:I$=SR$
6520 FORJ=P TOQ:L$=N$(J):GOSUB3200
6530 M8$=RIGHT$(M3$,1):IFM8$=I$ THENGOSUB6900
6540 NEXTJ:NEXTI
6550 L=39:GOSUB3050:GOSUB3100:GOTO6700
6600 REM ZONDER PRIORITEITEN
6610 FORI=P TOQ:L$=N$(I):GOSUB3200
6620 M8$=" ":GOSUB6900
6630 NEXTI:L=39:GOSUB3050:GOSUB3100
6700 GOSUB2150:HO=0:VE=12:GOSUB110
6710 PRINT"Wilt U nog een selectie zien?"
6720 GOSUB250:PRINT:PRINT" (J/N): ";
6730 GOSUB210:PRINT:GOSUB23000
6740 IFIN$="J" THEN6020
6750 GOTO5860
6800 REM KOP PRINTEN
6810 GOSUB100:BL=BL+1
6820 SR=BL:GOSUB300:L1$=SR$
6830 SR=BT:GOSUB300:L2$=SR$
6840 N$="FINPLAN":L$="Blad:"+L1$+" van:"+L2$
6850 IFLEN(L$)<(39-LEN(N$)) THENL$=" "+L$:GOTO6850
6860 L$=N$+L$:PRINTL$:L=39:GOSUB3050
6870 PRINT"Datum  P   Bedrag     Saldo   Omschr."
6880 GOSUB3050:RETURN
6900 REM POSTEN PRINTEN
6910 BS=BS-VAL(M6$):SR=BS:GOSUB310:BS$=SR$
6920 IFLEN(M7$)>10 THENM7$=LEFT$(M7$,10)
6930 L$=M1$+" "+M8$+" "+M6$+" "+BS$+" "+M7$
6940 PRINTL$:R=R+1:IFR=17 THENR=0:GOTO6960
6950 RETURN
6960 L=39:GOSUB3050:GOSUB3100
6970 GOSUB6800:RETURN
7000 GOSUB100:N$="Overzicht Rubrieken":VE=0
7010 GOSUB3000:GOSUB3300:GOSUB3100
7020 GOSUB270:GOTO4000
8000 N3$="Aanvullende Toelichting"
8010 GOSUB2170:PRINT
8020 PRINT"In  dit  programma  kunt  U  alleen een"
8030 PRINT"bestand van 'BANKREK/FINPLAN' inlezen.":PRINT
8040 PRINT"Als  U  PERIODIEK  een VAST BEDRAG laat"
8050 PRINT"overschrijven, of ontvangt, kunt U  die"
8060 PRINT"posten hier inbrengen.":PRINT
8070 PRINT"Uw  'EENMALIGE'  posten kunt U kwijt in"
8080 PRINT"het progr. 'BANKREK/FINPLAN'.":PRINT
8090 PRINT"Mogelijk zijn: per maand/2 maanden/"
8100 PRINT"3 maanden/4 maanden/half jaar.":PRINT
8110 PRINT"De   posten   worden   gegenereerd  t/m"
8120 PRINT"december van het lopende jaar."
8130 GOSUB3100:GOSUB2170:PRINT
8140 PRINT"De   datum  van  de  eerste  post  moet"
8150 PRINT"zijn NA de 'DATUM VANDAAG'!":PRINT
8160 PRINT"Iedere datum is toegestaan, ook bijv.:"
8170 PRINT" 310184  -  de  data  voor  de volgende"
8180 PRINT"maanden worden dan evt. gecorrigeerd.":PRINT
8190 PRINT"Echter: 290284  leidt  tot  posten  die"
8200 PRINT"worden gedateerd de 29ste van de maand!":PRINT
8210 PRINT"Zo ook bijv. 300484.":PRINT:PRINT
8220 PRINT"De aangemaakte posten (max.12 per keer)"
8230 PRINT"moeten   uiteraard  wel  passen  in  uw"
8240 PRINT"computer!":GOSUB3100:GOSUB2170:PRINT
8250 PRINT"In   'FINANCIELE PLANNING'   worden  de"
8260 PRINT"FINPLAN-posten geselecteerd:"
8270 PRINT" -in volgorde van prioriteit of niet,"
8280 PRINT" -tussen de 'DATUM VANDAAG' en een door"
8290 PRINT"  U op te geven datum.":PRINT
8300 PRINT"Na het inlezen van het  bestand  worden"
8310 PRINT"de  nog te verrekenen cheques verrekend"
8320 PRINT"met het BEGIN-saldo.":PRINT
8330 PRINT"Daar behoeft U dus geen  rekening  meer"
8340 PRINT"mee te houden!"
8350 GOSUB3100:GOSUB2170:PRINT
8360 PRINT"Getoond wordt het saldo na iedere post.":PRINT
8370 PRINT"Als dit saldo de nullijn onderschrijdt,"
8380 PRINT"zult U een beslissing moeten nemen:":PRINT
8390 PRINT"(Weer)   Eens  een  keer  'IN HET ROOD'"
8400 PRINT"staan, OF de betaling uitstellen?!"
8410 GOSUB3100:GOSUB2170:PRINT
8420 L=39:GOSUB3050
8430 PRINT"Inlezen            Wegschrijven":GOSUB3050
8440 PRINT"20100-20500        21100-21500":GOSUB3050
8450 PRINT" INPUT N            PRINT N"
8460 PRINT" FOR I=1 TO N       FOR I=1 TO N"
8470 PRINT" INPUT N$(I)        PRINT N$(I)"
8480 PRINT" NEXT I             NEXT I":GOSUB3050
8490 PRINT"Lengte van een post: MAX. 45 kar.":PRINT
8500 PRINT:PRINT"Denk aan MAX. aantal posten!"
8510 PRINT" (Regel 25000 DATA ";NR;")"
8520 GOSUB3100:GOTO4000
10000 GOSUB2150:FORI=1 TO5:GOSUB250:NEXTI
10010 HO=9:VE=12:GOSUB110
10020 PRINT"Tot de volgende keer!"
10030 END
15000 X$="":GOSUB250:HO=12:VE=12:GOSUB110
15010 PRINT"Uw code: ";:FORI=1 TOLEN(N$)
15020 GOSUB210:X$=X$+IN$:NEXTI:PRINT
15030 IFX$=N$ THENRETURN
15040 GOTO10000
20000 GOSUB100:N$="Bestand Inlezen":VE=0:GOSUB3000
20010 N$="Bankrek/Finplan":VE=2:GOSUB3000
20100 GOSUB3100
20600 IFN=0 THENRETURN
20610 T1=VAL(RIGHT$(N$(N),1)):T2=2
20620 IFT1<>T2 THEN20680
20630 DB$=LEFT$(N$(N),6)
20640 SB$=MID$(N$(N),7,9)
20650 SC$=MID$(N$(N),16,9)
20660 SD$=MID$(N$(N),25,9)
20670 N$(N)="":N=N-1:RETURN
20680 GOSUB2150:HO=0:VE=12:GOSUB110
20690 PRINT"U hebt het verkeerde bestand ingelezen!"
20700 FORI=1 TO5:GOSUB250:NEXTI
20710 GOSUB3100:GOTO10000
20990 RETURN
21000 GOSUB100:N$="Bestand Wegschrijven":VE=0
21010 GOSUB3000:N$="Bankrek/Finplan"
21020 VE=2:GOSUB3000
21030 N=N+1
21040 N$(N)=DB$+SB$+SC$+SD$+"2"
21100 GOSUB3100
21600 N$(N)="":N=N-1:RETURN
23000 REM   HL ROUTINE
23010 REM   ----------
23020 HL=ASC("a")-ASC("A")
23030 IFHL<0 THEN23200
23040 SR$="":FORZ=1 TOLEN(IN$)
23050 SR=ASC(MID$(IN$,Z,1))
23060 IF(SR>96) AND(SR<123) THENSR=SR-HL
23070 SR$=SR$+CHR$(SR)
23080 NEXTZ:IN$=SR$:RETURN
23200 HL=ABS(HL)
23210 SR$="":FORZ=1 TOLEN(IN$)
23220 SR=ASC(MID$(IN$,Z,1))
23230 IF(SR>64) AND(SR<91) THENSR=SR+HL
23240 SR$=SR$+CHR$(SR)
23250 NEXTZ:IN$=SR$:RETURN
25000 DATA101
25010 DATA20
25020 DATA"1234"
25030 REM LENGTE RUBR.NAMEN MAX.11 KAR.
25040 DATA"HUUR","INRICHTING"
25050 DATA"VERZEKERING","GENEESK.BEH"
25060 DATA"LEVENSMIDD.","KLEDING"
25070 DATA"DRANK/SIG.","KR-TIJDSCHR"
25080 DATA"STUDIE","VAK/UITGAAN"
25090 DATA"HOBBY","ONVOORZIEN"
25100 DATA"TELEFOON","GAS/WAT/EL"
25110 DATA"VERVOER","AUTO"
25120 DATA"BELASTINGEN","INKOMSTEN"
25130 DATA"KRUISP.K/CH","KRUISP.K/B"
30000 REM
30010 REM HOBBYSCOOP-BASICODE2
30020 REM *                  *
30030 REM * INT.HH-REKENING  *
30040 REM * FINANC.PLANNING  *
30050 REM                    *
30060 REM P INGEZONDEN DOOR
30070 REM O                  1
30080 REM S  B.P.M.RINTJEMA  2
30090 REM T  TUINSTRAAT 13   0
30100 REM B  5802 AD VENRAY  0
30110 REM U
30120 REM S  EIGEN ONTWERP   *
30130 REM   FEB.84/ITT-2020  *
30140 REM * DEC.84/APPLE IIE *
30150 REM *                  *
30160 REM **** HILVERSUM *****
30170 REM
30180 REM COPYRIGHT  NOS - HILVERSUM

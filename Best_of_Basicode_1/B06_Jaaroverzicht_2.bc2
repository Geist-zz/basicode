
1000 GOSUB3500:GOTO20:REM JAAR-OVERZICHT-2
1010 N1$="INTEGRALE HUISHOUD-REKENING"
1020 N2$="JAAR-OVERZICHT NO.2"
1030 GOSUB3500
1040 DIMB(RB,19)
1050 DIMRB$(RB)
1060 DIMS$(13)
1800 GOTO3700
2150 GOSUB100:N$=N1$:VE=0:GOSUB3000
2160 N$=N2$:VE=2:GOSUB3000:RETURN
2170 GOSUB100:N$=N2$:VE=0:GOSUB3000
2180 N$=N3$:VE=2:GOSUB3000:RETURN
3000 REM KOPPEN CENTREREN
3010 X$="---------------------------------------"
3020 X=LEN(N$):HO=INT((40-X)/2):X$=LEFT$(X$,X)
3030 GOSUB110:PRINTN$
3035 IFM=5 THENM=0:RETURN
3040 VE=VE+1:GOSUB110:PRINTX$:RETURN
3050 REM STREEP ER ONDER
3060 X$="---------------------------------------"
3070 X$=LEFT$(X$,L):IFM=5 THENM=0:RETURN
3080 PRINTX$:RETURN
3100 GOSUB250:HO=0:VE=23:GOSUB110
3110 PRINT"Druk een toets in ...";
3120 GOSUB210:PRINT:RETURN
3300 REM PRINT RUBRIEKEN
3310 L=39:GOSUB3050:D=RB
3320 IF(D<>2*INT(D/2)) THEND=D+1
3330 D=.5*D:FORI=1 TOD:SR=I:GOSUB300
3340 M1$=SR$:IFLEN(M1$)=1 THENM1$=" "+M1$
3350 SR=I+D:GOSUB300:M2$=SR$
3360 IFLEN(M2$)=1 THENM2$=" "+M2$
3370 IFLEN(RB$(I))<16 THEN3440
3380 L$=M1$+"="+RB$(I)
3390 IF(I+D)>RB THEN3410
3400 L$=L$+M2$+"="+RB$(I+D)
3410 PRINTL$:NEXTI:GOSUB3050
3420 PRINT"UITGAVEN: +        INKOMSTEN: -"
3430 GOSUB3050:GOSUB270:RETURN
3440 RB$(I)=RB$(I)+" ":GOTO3370
3500 REM BEREKENING 'A' IN 'CLEAR A'
3510 RESTORE:READRB
3520 A=(16+3)*RB:REM RUBRIEKEN
3530 A=A+(3+3)*13:REM SPIL
3540 RETURN
3700 READN$:GOSUB100:GOSUB15000
3710 REM RUBRIEKEN INLEZEN UIT DATA-REGELS
3720 FORI=1 TORB:READRB$(I):NEXTI
3730 GOSUB2150:GOSUB250:HO=0:VE=12:GOSUB110
3740 PRINT"Wilt U een aanvullende toelichting?":PRINT
3750 PRINT" (J/N): ";:GOSUB210:PRINT:GOSUB23000
3760 IFIN$="J" THEN3780
3770 GOTO4000
3780 GOSUB2150:PRINT
3790 PRINT"Dit  programma  is  het vervolg van het"
3800 PRINT"progr. 'Jaar-Overzicht No.1'.":PRINT
3810 PRINT"De daar weggeschreven  numerieke  array"
3820 PRINT"wordt  hier ingelezen als onderdeel van"
3830 PRINT"een array met 21 rijen en 20 kolommen.":PRINT
3840 PRINT"De bedragen in de  resterende  kolommen"
3850 PRINT"worden  berekend  bij  het samenstellen"
3860 PRINT"van het Jaar-Overzicht."
3870 GOSUB3100:GOSUB2150:PRINT
3880 L=39:GOSUB3050:PRINT"Bestand Inlezen":GOSUB3050
3890 PRINT"Numerieke array":GOSUB3050
3900 PRINT" FOR R=0 TO RB":PRINT" FOR K=1 TO 12"
3910 PRINT" INPUT B(R,K)":PRINT" NEXT K:NEXT R"
3920 GOSUB3050:PRINT" B(0,1) = JAAR":GOSUB3100
4000 GOSUB2150:PRINT
4010 PRINT:PRINT" 1=Jaar-Overzicht"
4020 PRINT:PRINT" 2=Overzicht Rubrieken"
4030 PRINT:PRINT" 3=Einde":PRINT:PRINT
4040 GOSUB250:PRINT"Uw keuze: ";:GOSUB210:PRINTIN$
4050 W=VAL(IN$):IF(W<1) OR(W>3) THEN4000
4060 ONW GOTO5000,4200,10000
4200 GOSUB100:N$="Overzicht Rubrieken":VE=0:GOSUB3000
4210 PRINT:GOSUB3300:GOSUB3100
4220 FORI=1 TORB
4230 IFRIGHT$(RB$(I),1)=" " THENGOSUB4250:GOTO4230
4240 NEXTI:GOSUB270:GOTO4000
4250 RB$(I)=LEFT$(RB$(I),LEN(RB$(I))-1)
4260 RETURN
5000 GOSUB2150:FORI=1 TO3:GOSUB250:NEXTI
5010 HO=8:VE=12:GOSUB110:PRINT"Nu eerst INITIALISEREN!"
5020 M=0:N=0:CN=2:CT=9
5030 FORR=0 TORB:FORK=0 TO19
5040 B(R,K)=0:NEXTK:NEXTR
5050 GOSUB20000:IFM=1 THENM=0:GOTO10000
5060 SR=B(0,1):GOSUB300:J$=SR$:B(0,1)=0
5070 N2$="Jaar-Overzicht 19"+J$
5100 GOSUB2150:PRINT:GOSUB250
5110 PRINT:PRINT"Som per kwartaal per rubriek"
5120 FORR=1 TORB
5130 FORK=1 TO3:B(R,13)=B(R,13)+B(R,K):NEXTK
5140 FORK=4 TO6:B(R,14)=B(R,14)+B(R,K):NEXTK
5150 FORK=7 TO9:B(R,15)=B(R,15)+B(R,K):NEXTK
5160 FORK=10 TO12:B(R,16)=B(R,16)+B(R,K):NEXTK
5170 NEXTR:GOSUB270:GOSUB250
5180 PRINT:PRINT"Som per halfjaar per rubriek"
5190 FORR=1 TORB
5200 FORK=13 TO14:B(R,17)=B(R,17)+B(R,K):NEXTK
5210 FORK=15 TO16:B(R,18)=B(R,18)+B(R,K):NEXTK
5220 NEXTR:GOSUB270:GOSUB250
5230 PRINT:PRINT"Som per jaar per rubriek"
5240 FORR=1 TORB:FORK=17 TO18
5250 B(R,0)=B(R,0)+B(R,K)
5260 NEXTK:NEXTR:GOSUB270:GOSUB250
5270 PRINT:PRINT"Som over alle kolommen"
5280 FORK=0 TO18:FORR=1 TORB
5290 B(0,K)=B(0,K)+B(R,K)
5300 NEXTR:NEXTK:GOSUB270:GOTO5500
5310 REM SUB-TOTAAL (?)
5320 REM KOLOM SUB-TOT OP '0' ZETTEN
5330 FORR=0 TORB:B(R,19)=0:NEXTR
5340 REM CONTROLE+SOM KOLOM SUB-TOT
5350 T=0:FORI=1 TOSK:S$=S$(I)
5360 IF(S$="TOT") OR(S$="SBT") THEN5420
5370 S$=RIGHT$(S$,2)
5380 IF(S$="KW") OR(S$="HJ") THEN5420
5390 T=T+1:S=VAL(S$(I))
5400 FORR=1 TORB
5410 B(R,19)=B(R,19)+B(R,S):NEXTR
5420 NEXTI:IFT<=1 THENGOSUB5600:M=5:RETURN
5430 REM SOM OVER KOLOM SUB-TOT
5440 FORR=1 TORB:B(0,19)=B(0,19)+B(R,19)
5450 NEXTR:RETURN
5500 GOSUB100:N$=N1$:VE=0:GOSUB3000
5510 N$=N2$:VE=2:GOSUB3000:PRINT
5520 PRINT:PRINT" 1=Video-Prints (op scherm)"
5530 PRINT:PRINT" 2=Print-outs (met printer)"
5540 PRINT:PRINT" 3=Terug naar HOOFD-MENU"
5550 PRINT:PRINT
5560 GOSUB250:PRINT"Uw keuze: ";:GOSUB210:PRINTIN$
5570 W=VAL(IN$):IF(W<1) OR(W>3) THEN5500
5580 ONW GOTO5700,9000,4000
5600 GOSUB250:HO=0:VE=21:GOSUB110
5610 PRINT"Geen SUB-TOTAAL bij 0 of 1 maand!"
5620 GOSUB3100:GOSUB270:RETURN
5650 GOSUB250:HO=8:VE=21:GOSUB110
5660 PRINT"Geen DUBBELE kenmerken!"
5670 GOSUB3100:GOSUB270:RETURN
5700 N3$="Video-Prints":PT=0
5710 GOSUB2170:GOSUB250:HO=5:VE=6:GOSUB110
5720 PRINT"De tekst stelt U zelf samen!":PRINT:GOSUB250
5730 PRINT"Aantal karakters op een regel  : ";:INPUTX1
5740 IFX1<20 THENPRINT"Minstens 20 kar.":GOTO5730
5750 PRINT"Aantal regels op uw scherm/blad: ";:INPUTX2
5760 IFX2<10 THENPRINT"Minstens 10 regels":GOTO5750
5770 IFM=6 THENM=0:GOTO8200
5780 PRINT:PRINT"U kiest welke gegevens in kolommen op"
5790 PRINT"het scherm/blad komen.":PRINT
5800 PRINT"Met 1 spatie tussen de kolommen en aan"
5810 PRINT"het einde is rekening gehouden, voor"
5820 PRINT"zover de bedragen niet groter zijn dan"
5830 PRINT"8 karakters.":PRINT
5840 PRINT"De kolommen worden gelijkmatig over"
5850 PRINT"het scherm/blad verdeeld.":GOSUB3100
5900 GOSUB2170:PRINT
5910 S1=INT((X1-3)/9):IFS1>13 THENS1=13
5920 S2=INT((X1-12)/9):IFS2>13 THENS2=13
5930 PRINT"Bij een scherm/blad-breedte van ";X1;" kar."
5940 PRINT"krijgt U:":PRINT
5950 PRINT"1.met rubriek-nummers: MAX.";S1;" kolommen"
5960 PRINT"2.met rubriek-namen  : MAX.";S2;" kolommen"
5970 GOSUB250:PRINT:PRINT"Wat wilt U? (1 of 2): ";
5980 GOSUB210:PRINTIN$:W=VAL(IN$)
5990 IF(W<1) OR(W>2) THEN5900
6000 IFW=1 THENSK=S1:RN=3
6010 IFW=2 THENSK=S2:RN=12
6020 GOSUB250:PRINT:PRINT"Hoeveel kolommen wilt U?"
6030 PRINT:PRINT"Geef een getal (1-";SK;"): ";
6040 INPUTIN$:W=VAL(IN$)
6050 IF(W<1) OR(W>SK) THEN5900
6100 GOSUB2170:PRINT:SK=W:GOSUB250
6110 PRINT"U koos voor ";SK;" kolommen."
6120 L=24:GOSUB3050
6130 PRINT"U geeft aan:"
6140 PRINT"- maanden   met hun nummer: 1-12"
6150 PRINT"- kwartalen idem, bijv. 1KW-2KW-enz"
6160 PRINT"- halfjaren idem, bijv. 1HJ-2HJ"
6170 PRINT"- TOT=Totaal Generaal"
6180 PRINT"- SBT=Sub-Totaal van gevraagde maanden."
6190 L=39:GOSUB3050
6200 PRINT"Nu de volgorde van links naar rechts:"
6210 K$="":K1$="":K2$="":K1=0:K2=0
6220 K1=SK:IFSK>9 THENK1=8:K2=SK-8
6230 TH=16:TC=INT((40-4*K1)/2):TB=TC
6240 IFSK>9 THENTD=INT((40-4*K2)/2)
6250 FL=0:FORI=1 TOSK:IFFL=1 THEN6390
6260 GOSUB250:HO=0:VE=21:GOSUB110
6270 PRINT"Kolom No.";I;": ";
6280 INPUTIN$:GOSUB23000:S$(I)=IN$:S$=IN$
6290 IF(S$="TOT") OR(S$="SBT") THEN6350
6300 IF(S$="1KW") OR(S$="2KW") THEN6350
6310 IF(S$="3KW") OR(S$="4KW") THEN6350
6320 IF(S$="1HJ") OR(S$="2HJ") THEN6350
6330 IF(VAL(S$)>0) AND(VAL(S$)<13) THEN6420
6340 FL=1:GOTO6390
6350 GOSUB110:GOSUB6440
6360 IFLEN(S$)<3 THENS$=" "+S$:GOTO6360
6370 K$=K$+S$:HO=TB:VE=TH:GOSUB110:PRINTK$:K$=K$+"-"
6380 IF(I=8) AND(SK>9) THENGOSUB6450
6390 NEXTI:IFFL=1 THENFL=0:GOTO6100
6400 IFSK<=9 THENK1$=K$:K$="":GOTO6470
6410 K2$=K$:K$="":GOTO6470
6420 IFLEN(S$(I))=1 THENS$(I)="0"+S$(I)
6430 GOTO6350
6440 PRINT"                                       ":RETURN
6450 TB=TD:TH=TH+2:K1$=K$:K$=""
6460 K1$=LEFT$(K1$,LEN(K1$)-1):RETURN
6470 IFSK=1 THEN6520
6480 FL=0:FORI=1 TOSK-1:IFFL=1 THEN6510
6490 FORJ=I+1 TOSK:IFS$(I)=S$(J) THENFL=1
6500 NEXTJ
6510 NEXTI:IFFL=1 THENFL=0:GOSUB5650:GOTO6100
6520 FL=0:FORI=1 TOSK:IFFL=1 THEN6540
6530 IFS$(I)="SBT" THENFL=1
6540 NEXTI:IFFL=1 THENFL=0:GOSUB5310
6550 IFM=5 THENM=0:GOTO6100
6600 RT$="":RT=INT((X1-SK*9-RN)/(SK+2))
6610 RL=RN+SK*(9+RT)
6620 IF(RL=X1) AND(RT>=1) THENRT=RT-1:GOTO6610
6630 IFLEN(RT$)<RT THENRT$=RT$+" ":GOTO6630
6640 C=1:BL=0:BT=INT(RB/(X2-7))
6650 IF(X2-7)*BT<>RB THENBT=BT+1
6660 T=1:GOSUB6900
6670 D=C+(X2-8):IFD>RB THEND=RB
6680 T=2:FORI=C TOD:GOSUB7030
6690 NEXTI:L$=L4$:GOSUB7300
6700 C=I:IFC>RB THEN6730
6710 IFPT=5 THENGOSUB9300:GOTO6660
6720 GOSUB3100:GOSUB270:GOTO6660
6730 IFRN=3 THENL$=RT$+"Tot"+RT$
6740 IFRN=12 THENL$=RT$+"Totaal      "+RT$
6750 I=0:GOSUB7070:L$=L4$:GOSUB7300:IFPT=0 THENGOSUB3100
6760 GOSUB270:GOTO5500
6900 IFPT=0 THENGOSUB100:REM KOPREGELS
6910 IFPT=5 THENGOSUB9400
6920 BL=BL+1:SR=BL:GOSUB300:L1$=SR$
6930 SR=BT:GOSUB300:L2$=SR$
6940 L$="Blad:"+L1$+" van:"+L2$
6950 IFPT=0 THENL=RL-LEN(N2$):GOSUB7400:L$=N2$+L$
6960 IF(PT=0) AND(X1<34) THENGOSUB7200:GOTO7000
6970 IFPT=5 THENL=RL-LEN(DT$):GOSUB7400:L$=DT$+L$
6980 IF(RL<32) AND(PT=0) THENGOSUB7200:GOTO7000
6990 L=RT+RL:GOSUB7400:GOSUB7300
7000 IFPT=5 THENGOSUB9200
7010 L$="-":L=RL:GOSUB7440
7020 L$=RT$+L$:L4$=L$:GOSUB7300
7030 IF(T=1) AND(RN=3) THENL$=RT$+"RB "+RT$
7040 IF(T=1) AND(RN=12) THENL$=RT$+"Rubriek     "+RT$
7050 IF(T=2) AND(RN=3) THENGOSUB7220
7060 IF(T=2) AND(RN=12) THENGOSUB7240
7070 FORJ=1 TOSK:S$=S$(J)
7080 IFT=1 THENGOSUB7500
7090 IFT=2 THENGOSUB7800
7100 IFJ=SK THENL$=L$+W$:GOTO7120
7110 L$=L$+W$+RT$
7120 NEXTJ:GOSUB7300
7130 IFT=1 THENL$=L4$:GOSUB7300
7140 RETURN
7200 L$=N2$:L1=LEN(N2$):L2=(RL-LEN(N2$))/2
7210 L=RT+L1+L2:GOSUB7400:GOSUB7300:RETURN
7220 SR=I:GOSUB300:L$=SR$:IFLEN(L$)=1 THENL$="0"+L$
7230 L$=RT$+L$+" "+RT$:RETURN
7240 L$=RB$(I):L=RN:GOSUB7420
7250 L$=RT$+L$+RT$:RETURN
7300 IFPT=5 THEN7320
7310 PRINTL$:L$="":RETURN
7320 SR$=L$:GOSUB350:GOSUB360
7330 SR$="":L$="":RETURN
7400 IFLEN(L$)<L THENL$=" "+L$:GOTO7400
7410 RETURN
7420 IFLEN(L$)<L THENL$=L$+" ":GOTO7420
7430 RETURN
7440 IFLEN(L$)<L THENL$=L$+"-":GOTO7440
7450 RETURN
7500 IFS$="01" THENW$="Jan."
7510 IFS$="02" THENW$="Febr."
7520 IFS$="03" THENW$="Maart"
7530 IFS$="04" THENW$="April"
7540 IFS$="05" THENW$="Mei"
7550 IFS$="06" THENW$="Juni"
7560 IFS$="07" THENW$="Juli"
7570 IFS$="08" THENW$="Aug."
7580 IFS$="09" THENW$="Sept."
7590 IFS$="10" THENW$="Okt."
7600 IFS$="11" THENW$="Nov."
7610 IFS$="12" THENW$="Dec."
7620 IFS$="1KW" THENW$="1.Kwart."
7630 IFS$="2KW" THENW$="2.Kwart."
7640 IFS$="3KW" THENW$="3.Kwart."
7650 IFS$="4KW" THENW$="4.Kwart."
7660 IFS$="1HJ" THENW$="1.Halfjr"
7670 IFS$="2HJ" THENW$="2.Halfjr"
7680 IFS$="SBT" THENW$="Sub-Tot"
7690 IFS$="TOT" THENW$="Tot-Gen"
7700 IFLEN(W$)<9 THENW$=" "+W$:GOTO7700
7710 RETURN
7800 IFS$="1KW" THENSR=B(I,13):GOTO7890
7810 IFS$="2KW" THENSR=B(I,14):GOTO7890
7820 IFS$="3KW" THENSR=B(I,15):GOTO7890
7830 IFS$="4KW" THENSR=B(I,16):GOTO7890
7840 IFS$="1HJ" THENSR=B(I,17):GOTO7890
7850 IFS$="2HJ" THENSR=B(I,18):GOTO7890
7860 IFS$="SBT" THENSR=B(I,19):GOTO7890
7870 IFS$="TOT" THENSR=B(I,0):GOTO7890
7880 S=VAL(S$):SR=B(I,S)
7890 GOSUB310:W$=SR$:RETURN
9000 N3$="Print-outs":GOSUB2170:PRINT:PT=5
9010 GOSUB250:HO=5:VE=8:GOSUB110
9020 PRINT"Hebt U een printer? (J/N): ";
9030 GOSUB210:PRINT:GOSUB23000
9040 IFIN$="N" THEN5500
9050 GOSUB250:HO=9:VE=10:GOSUB110
9060 PRINT"Zet deze dan NU aan!"
9070 IFM=6 THENGOSUB3100:GOTO9110
9080 GOSUB250:HO=5:VE=12:GOSUB110
9090 PRINT"Geef DATUM VANDAAG (voluit):"
9100 GOSUB250:HO=11:VE=14:GOSUB110:INPUTDT$
9110 GOTO5710
9200 L$=N2$:L1=LEN(L$):L2=(RL-LEN(L$))/2
9210 L=RT+L1+L2:GOSUB7400:GOSUB7300
9220 L$="-":L=L1:GOSUB7440:L=RT+L1+L2
9230 GOSUB7400:GOSUB7300:GOSUB7300:RETURN
9300 GOSUB2170:FORK=1 TO3:GOSUB250:NEXTK
9310 HO=0:VE=12:GOSUB110
9320 PRINT"U kunt nu een nieuw blad instellen!":GOSUB3100
9330 RETURN
9400 GOSUB2170:HO=0:VE=12:GOSUB110
9410 L=39:GOSUB3050:N$=K1$:M=5:VE=16:GOSUB3000
9420 IFK2$="" THEN9440
9430 N$=K2$:M=5:VE=18:GOSUB3000
9440 GOSUB250:HO=12:VE=6:GOSUB110
9450 PRINT"Ik ben bezig!":RETURN
9500 L$=N3$:X=LEN(L$):L1=(X1+LEN(L$))/2:L=L1
9510 GOSUB7400:GOSUB7300:L$="-":L=X:GOSUB7440
9520 L=L1:GOSUB7400:GOSUB7300
9530 L$="-":L=39:GOSUB7440:L=TB+39:GOSUB7400
9540 L4$=L$:GOSUB7300:RETURN
10000 GOSUB2150:FORI=1 TO3:GOSUB250:NEXTI
10010 HO=9:VE=12:GOSUB110
10020 PRINT"Tot de volgende keer!"
10030 END
15000 X$="":GOSUB250:HO=12:VE=12:GOSUB110
15010 PRINT"Uw code: ";:FORI=1 TOLEN(N$)
15020 GOSUB210:X$=X$+IN$:NEXTI:PRINT
15030 IFX$=N$ THENRETURN
15040 GOTO10000
20000 GOSUB100:N$="Bestand Inlezen":VE=0:GOSUB3000
20010 N$=N1$:VE=2:GOSUB3000
20100 GOSUB3100
20600 IFB(0,1)=0 THENM=1
20610 RETURN
20990 RETURN
23000 REM   HL ROUTINE
23010 REM   ----------
23020 HL=ASC("a")-ASC("A")
23030 IFHL<0 THEN23200
23040 SR$="":FORZ=1 TOLEN(IN$)
23050 SR=ASC(MID$(IN$,Z,1))
23060 IF(SR>96) AND(SR<123) THENSR=SR-HL
23070 SR$=SR$+CHR$(SR)
23080 NEXTZ:IN$=SR$:RETURN
23200 HL=ABS(HL)
23210 SR$="":FORZ=1 TOLEN(IN$)
23220 SR=ASC(MID$(IN$,Z,1))
23230 IF(SR>64) AND(SR<91) THENSR=SR+HL
23240 SR$=SR$+CHR$(SR)
23250 NEXTZ:IN$=SR$:RETURN
25000 DATA20
25010 DATA"1234"
25020 REM LENGTE RUBR.NAMEN MAX.11 KAR.
25030 DATA"HUUR","INRICHTING"
25040 DATA"VERZEKERING","GENEESK.BEH"
25050 DATA"LEVENSMIDD.","KLEDING"
25060 DATA"DRANK/SIG.","KR-TIJDSCHR"
25070 DATA"STUDIE","VAK/UITGAAN"
25080 DATA"HOBBY","ONVOORZIEN"
25090 DATA"TELEFOON","GAS/WAT/EL"
25100 DATA"VERVOER","AUTO"
25110 DATA"BELASTINGEN","INKOMSTEN"
25120 DATA"KRUISP.K/CH","KRUISP.K/B"
30000 REM
30010 REM HOBBYSCOOP-BASICODE2
30020 REM *                  *
30030 REM * INT.HH-REKENING  *
30040 REM * JAAR-OVERZICHT-2 *
30050 REM                    *
30060 REM P INGEZONDEN DOOR
30070 REM O                  1
30080 REM S  B.P.M.RINTJEMA  2
30090 REM T  TUINSTRAAT 13   0
30100 REM B  5802 AD VENRAY  0
30110 REM U
30120 REM S  EIGEN ONTWERP   *
30130 REM   FEB.84/ITT-2020  *
30140 REM * DEC.84/APPLE IIE *
30150 REM *                  *
30160 REM **** HILVERSUM *****
30170 REM
30180 REM COPYRIGHT  NOS - HILVERSUM

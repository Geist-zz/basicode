
1000 GOSUB3500:GOTO20:REM BANKREK/FINPLAN
1010 N1$="INTEGRALE HUISHOUD-REKENING"
1020 N2$="BANKREK/FINPLAN":GOSUB3500
1040 DIMN$(NR),RB$(RB)
1050 DIMZZ(NR),Z(NR)
1055 GOSUB3600
1060 IFCR=1 THENDIMA(NR,15)
1200 SR=0:CN=2:CT=9:GOSUB310
1210 SB$=SR$:SC$=SR$:SD$=SR$
1220 DB$="010170"
1790 M=0:N=0:BW=0
1800 GOTO3700
2000 GOSUB250:PRINT:PRINT"- Sorteren":M=R
2010 M=INT(M/2):IFM=0 THEN2130
2020 J=1:K=R-M
2030 H=J
2040 V=H+M
2050 IFZZ(H)<=ZZ(V) THEN2110
2060 ZZ(0)=ZZ(H):ZZ(H)=ZZ(V):ZZ(V)=ZZ(0)
2070 IFCR=4 THENZ$=N$(H):N$(H)=N$(V):N$(V)=Z$
2080 IFCR=5 THENZ(0)=Z(H):Z(H)=Z(V):Z(V)=Z(0)
2090 H=H-M:IFH<1 THEN2110
2100 GOTO2040
2110 J=J+1:IFJ>K THEN2010
2120 GOTO2030
2130 GOSUB250:PRINT
2135 PRINT"Gereed! - NU: GARBAGE COLLECTION"
2140 GOSUB270:RETURN
2150 GOSUB100:N$=N1$:VE=0:GOSUB3000
2160 N$=N2$:VE=2:GOSUB3000:RETURN
2170 GOSUB100:N$=N2$:VE=0:GOSUB3000
2180 N$=N3$:VE=2:GOSUB3000:RETURN
2200 REM DATUM CONTROLE + CONVERSIE
2210 IFLEN(L$)<>6 THENM=1:GOTO2810
2220 DD=VAL(LEFT$(L$,2))
2230 IF(DD<1) OR(DD>31) THENM=1:GOTO2830
2240 MM=VAL(MID$(L$,3,2))
2250 IF(MM<1) OR(MM>12) THENM=1:GOTO2830
2260 IF(MM=2) AND(DD>29) THENM=1:GOTO2830
2270 IF(MM=4) AND(DD>30) THENM=1:GOTO2830
2280 IF(MM=6) AND(DD>30) THENM=1:GOTO2830
2290 IF(MM=9) AND(DD>30) THENM=1:GOTO2830
2300 IF(MM=11) AND(DD>30) THENM=1:GOTO2830
2310 JJ=VAL(RIGHT$(L$,2))
2320 IF(JJ<70) THENM=1:GOTO2840
2330 IF((JJ/4)<>INT(JJ/4)) THEN2370
2340 SS=365*JJ+DD:IFMM<3 THEN2360
2350 SS=SS-INT(MM*.4+2.3):JJ=JJ+1
2360 SS=SS+INT(MM*31+((JJ-1)/4)):RETURN
2370 IF(MM=2) AND(DD=29) THENM=1:GOTO2830
2380 GOTO2340
2400 REM CONTROLE RUBRIEKNUMMERS
2410 IF(VAL(L$)<1) OR(VAL(L$)>RB) THENM=1:GOTO2860
2420 IFL$="19" THENM=1:GOTO2880
2430 IFLEN(L$)=1 THENL$="0"+L$
2440 RETURN
2500 REM CONTROLE BEDRAG
2510 IFL$="" THENM=1:GOTO2810
2520 IFVAL(L$)<-99999.99 THENM=1:GOTO2900
2530 IFVAL(L$)>99999.99 THENM=1:GOTO2900
2540 SR=VAL(L$):GOSUB310
2550 IFLEFT$(SR$,2)="**" THENM=1:GOTO2920
2560 RETURN
2600 REM CONTROLE OMSCHRIJVING
2610 IFL$="" THENM=1:GOTO2810
2620 IFLEN(L$)>L THENM=1:GOTO2930
2630 RETURN
2650 REM CONTROLE AFREK.NUMMER
2660 IFL$="" THENM=1:GOTO2810
2670 IF(VAL(L$)<1) OR(VAL(L$)>999) THENM=1:GOTO2860
2680 IFLEN(L$)<3 THENL$="0"+L$:GOTO2680
2690 RETURN
2700 REM CONTROLE PRIOR.NO.
2710 IFL$="" THENM=1:GOTO2810
2720 IF(VAL(L$)<1) OR(VAL(L$)>9) THENM=1:GOTO2860
2730 RETURN
2800 REM FOUTMELDINGEN
2810 GOSUB250
2820 PRINT"Ook dit gegeven hoort er bij!":GOTO4400
2830 GOSUB250:PRINT"Datum niet correct!":GOTO4400
2840 GOSUB250
2850 PRINT"Graag een datum na '010170'":GOTO4400
2860 GOSUB250
2870 PRINT"Buiten toegestane reeks!":GOTO4400
2880 GOSUB250
2890 PRINT"Dit rubrieknummer kan hier niet!":GOTO4400
2900 GOSUB250
2910 PRINT"Bedrag tussen -99999.99 en +99999.99":GOTO4400
2920 GOSUB250:PRINT"U deed iets verkeerd!":GOTO4400
2930 GOSUB250:PRINT"Omschr. max.";L;" kar.!":GOTO4400
2940 GOSUB250:PRINT
2950 PRINT"We plannen niet in het verleden!":GOTO4400
3000 REM KOPPEN CENTREREN
3010 X$="---------------------------------------"
3020 X=LEN(N$):HO=INT((40-X)/2):X$=LEFT$(X$,X)
3030 GOSUB110:PRINTN$
3035 IFM=5 THENM=0:RETURN
3040 VE=VE+1:GOSUB110:PRINTX$:RETURN
3050 REM STREEP ER ONDER
3060 X$="---------------------------------------"
3070 X$=LEFT$(X$,L)
3080 PRINTX$:RETURN
3100 GOSUB250:HO=0:VE=23:GOSUB110
3110 PRINT"Druk een toets in ...";
3120 GOSUB210:PRINT:RETURN
3150 GOSUB250:HO=0:VE=23:GOSUB110
3160 PRINT"T=Terug/S=Stoppen/V=Verder ...";
3170 GOSUB210:PRINT:GOSUB23000:RETURN
3200 REM STRING UITEENRAFELEN
3210 M1$=MID$(L$,1,6):REM DATUM
3220 M2$=MID$(L$,7,4):REM CRDGN
3230 M3$=MID$(L$,11,4):REM K/B/C/P
3240 M4$=MID$(L$,15,1):REM TEKEN
3250 M5$=MID$(L$,16,2):REM RUBRIEK
3260 M6$=MID$(L$,18,9):REM BEDRAG
3270 M7$=RIGHT$(L$,LEN(L$)-26):REM OMSCHR.
3280 RETURN
3300 REM PRINT RUBRIEKEN
3310 L=39:GOSUB3050:D=RB
3320 IF(D<>2*INT(D/2)) THEND=D+1
3330 D=.5*D:FORI=1 TOD:SR=I:GOSUB300
3340 M1$=SR$:IFLEN(M1$)=1 THENM1$=" "+M1$
3350 SR=I+D:GOSUB300:M2$=SR$
3360 IFLEN(M2$)=1 THENM2$=" "+M2$
3370 IFLEN(RB$(I))<16 THEN3440
3380 L$=M1$+"="+RB$(I)
3390 IF(I+D)>RB THEN3410
3400 L$=L$+M2$+"="+RB$(I+D)
3410 PRINTL$:NEXTI:GOSUB3050
3420 PRINT"UITGAVEN: +        INKOMSTEN: -"
3430 GOSUB3050:RETURN
3440 RB$(I)=RB$(I)+" ":GOTO3370
3500 REM BEREKENING 'A' IN 'CLEAR A'
3510 RESTORE:READNR:READRB
3520 A=(45+3)*NR:REM POSTEN
3530 A=A+(16+3)*RB:REM RUBRIEKEN
3540 RETURN
3600 READN$:GOSUB100:GOSUB15000
3610 GOSUB100:N$="INTEGRALE HUISHOUD-REKENING":VE=0
3620 GOSUB3000:GOSUB250:HO=3:VE=14:GOSUB110
3630 PRINT"Moet U een bestand !! NUMERIEK !!"
3640 VE=16:GOSUB110:PRINT"opslaan? (J/N): ";
3650 GOSUB210:PRINT:GOSUB23000
3660 CR=0:IFIN$="J" THENCR=1
3670 RETURN
3700 REM RUBRIEKEN INLEZEN UIT DATA-REGELS
3710 FORI=1 TORB:READRB$(I):NEXTI
3720 GOSUB2150:PRINT:GOSUB250
3730 PRINT"Hebt U al een bestand aangemaakt?"
3740 PRINT:PRINT" (J/N): ";:GOSUB210:PRINT:GOSUB23000
3750 IFIN$="J" THENGOSUB20000
3760 SB=VAL(SB$):IFSB=0 THENGOSUB2150:GOSUB3800
3770 GOSUB2150:GOSUB250:PRINT
3780 PRINT"INFORMATIE VORIGE KEER:":L=23:GOSUB3050
3790 GOSUB5600:GOSUB4700:GOTO3840
3800 GOSUB250:HO=0:VE=10:GOSUB110
3810 PRINT"Begin-saldo BANK ........:FL.";:INPUTSB$
3820 L$=SB$:GOSUB2500:IFM=1 THENM=0:GOTO3800
3830 SB$=SR$:RETURN
3840 GOSUB2150:N$="PROCEDURE":VE=7:GOSUB3000:PRINT
3850 PRINT"- Eerst in FINPLAN uw posten inbrengen,":PRINT
3860 PRINT"- daarna via BANKREKENING deze posten"
3870 PRINT"  afrekenen.":PRINT
3880 PRINT"Er is GEEN DIRECTE INGANG tot de BANK-"
3890 PRINT"REKENING!":GOSUB3100
4000 GOSUB2150:PRINT
4010 PRINT"BANKREK/FINPLAN bijgewerkt t/m ";DB$
4020 L=37:GOSUB3050:PRINT
4030 PRINT" 1=FINPLAN/Posten inbrengen"
4040 PRINT" 2=BANKREK/Posten afrekenen":PRINT
4050 PRINT" 3=Posten afvoeren":PRINT
4060 PRINT" 4=Overzicht posten"
4070 PRINT" 5=Overzicht rubrieken":PRINT
4080 PRINT" 6=Einde":PRINT:PRINT
4090 PRINT"Er zijn nu ";N;" posten in het bestand"
4100 L=36:GOSUB3050
4110 PRINT"Nog ";NR-N-1;" posten kunnen worden opgenomen"
4120 GOSUB250:PRINT:PRINT"Uw keuze: ";:GOSUB210:PRINT
4130 W=VAL(IN$):IF(W<1) OR(W>6) THEN4000
4140 IFW=1 THENN3$="Posten inbrengen"
4150 IFW=2 THENN3$="Posten afrekenen"
4160 IFW=3 THENN3$="Posten afvoeren"
4170 IFW=4 THENN3$="Overzicht posten"
4180 IFW=5 THENN3$="Overzicht rubrieken"
4190 ONW GOTO7200,7400,8000,9000,8500,4200
4200 IFBW=1 THENBW=0:GOSUB21000
4210 GOTO10000
4400 REM CORRECTIE
4410 FORK=1 TO1000:NEXTK:HO=0:GOSUB110:GOSUB4440
4420 VE=VE+1:GOSUB110:GOSUB4440
4430 VE=VE+1:GOSUB110:GOSUB4440:VE=VE-3:RETURN
4440 PRINT"                                       ":RETURN
4500 REM ONDERKANT SCHERM SCHOON
4510 GOSUB120:T2=VE:HO=0
4520 FORK=16 TOT2:VE=K-1:GOSUB110:GOSUB4440
4530 NEXTK:RETURN
4600 REM SCHERM SCHOON
4610 HO=0:FORK=T1 TOT2:VE=K:GOSUB110:GOSUB4440
4620 NEXTK:RETURN
4700 REM INPUT BEDRAGEN CHEQUES
4710 PRINT:PRINT:PRINT"INFORMATIE NU:":L=14:GOSUB3050
4720 GOSUB250:HO=0:VE=16:GOSUB110
4730 PRINT"Reeds verrekende  cheques:FL.";:INPUTSD$
4740 L$=SD$:GOSUB2500:IFM=1 THENM=0:GOTO4720
4750 SD$=SR$
4760 GOSUB250:VE=17:GOSUB110
4770 PRINT"Nog te verrekenen cheques:FL.";:INPUTSC$
4780 L$=SC$:GOSUB2500:IFM=1 THENM=0:GOTO4760
4790 SC$=SR$:RETURN
4800 GOSUB250:HO=0:VE=18:GOSUB110
4810 PRINT"U overschrijdt nu het maximum aantal"
4820 PRINT:PRINT"posten per bestand!":GOSUB3100
4830 IFBW=1 THEN7800
4840 GOSUB270:GOTO4000
5000 GOSUB250:HO=0:VE=9:GOSUB110
5010 PRINT"Bijgewerkt t/m ........:  ";DN$
5020 GOSUB250:VE=11:GOSUB110
5030 PRINT"Datum vandaag? (DDMMJJ): ";:INPUTL1$
5040 L$=L1$:GOSUB2200:IFM=1 THENM=0:GOTO5020
5050 DN$=L1$:RETURN
5100 GOSUB250:HO=0:VE=16:GOSUB110
5110 PRINT"Plan-datum (DDMMJJ): ";:INPUTL1$
5120 L$=L1$:GOSUB2200:IFM=1 THENM=0:GOTO5100
5130 L2=SS:IFL2<L1 THENGOSUB2940:GOTO5100
5140 RETURN
5150 GOSUB250:HO=0:VE=16:GOSUB110
5160 PRINT"Datum afrekening (DDMMJJ): ";:INPUTL1$
5170 L$=L1$:GOSUB2200:IFM=1 THENM=0:GOTO5150
5180 RETURN
5200 GOSUB250:VE=VE+1:GOSUB110
5210 PRINT"Afrek.no. (1-999) :";:INPUTL8$
5220 L$=L8$:GOSUB2650:IFM=1 THENM=0:GOTO5200
5230 L8$=L$:RETURN
5250 GOSUB250:VE=VE+1:GOSUB110
5260 PRINT"Prioriteit (1-9) .: ";:INPUTL8$
5270 L$=L8$:GOSUB2700:IFM=1 THENM=0:GOTO5250
5280 L3$="PLA"+L8$:L8$="":RETURN
5300 L=19:GOSUB250:VE=VE+1:GOSUB110
5310 PRINT"Omschr.(MAX.";L;" k.): ";:INPUTL7$
5320 L$=L7$:GOSUB2600:IFM=1 THENM=0:GOTO5300
5330 RETURN
5350 L=19:GOSUB250:VE=VE+1:GOSUB110
5360 PRINT"Omschrijving .....: ";M7$
5370 PRINT"(MAX.";L;" k.) of '=':";:INPUTL7$
5380 IFL7$="=" THENL7$=M7$
5385 L$=L7$:GOSUB2600:IFM=1 THENM=0:GOTO5350
5390 GOSUB4420:VE=VE+1:GOSUB110
5395 PRINT"Omschrijving .....: ";L7$:RETURN
5400 GOSUB250:VE=VE+1:GOSUB110
5410 PRINT"Rubriek no. (1-";RB;"): ";:INPUTL5$
5420 L$=L5$:GOSUB2400:IFM=1 THENM=0:GOTO5400
5430 L5$=L$:RETURN
5450 GOSUB250:VE=VE+1:GOSUB110
5460 PRINT"Rubriek no. ......: ";M5$
5470 PRINT"(1-";RB;") of '='     :";:INPUTL5$
5480 IFL5$="=" THENL5$=M5$
5485 L$=L5$:GOSUB2400:IFM=1 THENM=0:GOTO5450
5490 L5$=L$:GOSUB4420:VE=VE+1:GOSUB110
5495 PRINT"Rubriek no. ......: ";L5$:RETURN
5500 GOSUB250:VE=VE+1:GOSUB110
5510 PRINT"Bedrag ...........: ";:INPUTL6$:L$=L6$
5520 GOSUB2500:IFM=1 THENM=0:GOTO5500
5530 L6$=SR$:RETURN
5550 GOSUB250:VE=VE+1:GOSUB110
5560 PRINT"Bedrag ...........: ";M6$
5570 PRINT"MAX.+/-99999.99/= :";:INPUTL6$
5580 IFL6$="=" THENL6$=M6$
5585 L$=L6$:GOSUB2500:IFM=1 THENM=0:GOTO5550
5590 L6$=SR$:GOSUB4420:VE=VE+1:GOSUB110
5595 PRINT"Bedrag ...........: ";L6$:RETURN
5600 PRINT"Eind-saldo BANK .........:FL.";
5610 SB=VAL(SB$):SP=0:IFN=0 THEN5660
5620 FORI=1 TON:L$=N$(I):GOSUB3200
5630 IFM4$="2" THENSB=SB-VAL(M6$)
5640 IFM4$="4" THENSP=SP+VAL(M6$)
5650 NEXTI
5660 SR=SB-VAL(SD$):GOSUB310:PRINTSR$
5670 PRINT"Hierin verrekende cheques:FL.";SD$
5680 L=39:GOSUB3050
5690 PRINT"Nog te verrekenen cheques:FL.";SC$
5700 PRINT"Totaal posten FINPLAN ...:FL.";
5710 SR=SP:GOSUB310:PRINTSR$:RETURN
5800 IFN=0 THENRETURN
5810 FORI=1 TO5:GOSUB250:NEXTI
5820 PRINT:PRINT"Oude posten FINPLAN bijstellen!"
5830 FORI=1 TON:L$=N$(I):GOSUB3200
5840 IFM4$="4" THENGOSUB5860
5850 NEXTI:RETURN
5860 L$=M1$:GOSUB2200:L3=SS:IFL3<L1 THENGOSUB5880
5870 RETURN
5880 N$(I)=DB$+RIGHT$(N$(I),LEN(N$(I))-6)
5890 RETURN
6000 GOSUB100:N$=N3$:M=5:VE=0:GOSUB3000
6010 HO=0:VE=14:GOSUB110:L=39:GOSUB3050
6020 T=0:FL=0:VE=1:GOSUB110:GOSUB3050
6030 PRINT"Pno Datum  KBCP RB  Bedrag   Omschr."
6040 GOSUB3050:FORI=FP TON:IFFL=1 THEN6060
6050 L$=N$(I):GOSUB3200:IFM4$="4" THENGOSUB6100
6060 NEXTI:IFFL=1 THENFL=0:GOTO6230
6070 IFT>0 THENT=0:GOSUB6170
6080 IFFL=1 THENFL=0:GOTO6230
6090 M=1:RETURN
6100 IFFP=1 THENFP=I
6110 M7$=LEFT$(M7$,10):SR=I:GOSUB300:L$=SR$
6120 IFLEN(L$)<3 THENL$=" "+L$:GOTO6120
6130 L$=L$+" "+M1$+" "+M3$+" "+M5$
6140 L$=L$+" "+M6$+" "+M7$:PRINTL$
6150 T=T+1:IFT=10 THENT=0:GOTO6170
6160 RETURN
6170 GOSUB250:HO=0:VE=17:GOSUB110
6180 PRINT"Is de bewuste post aanwezig? (J/N): ";
6190 GOSUB210:PRINT:GOSUB23000
6200 IFIN$="J" THENFL=1:RETURN
6210 T1=4:T2=13:GOSUB4600:T1=16:T2=18:GOSUB4600
6220 VE=4:GOSUB110:RETURN
6230 GOSUB250:VE=19:GOSUB110
6240 PRINT"Geef postnummer: ";:INPUTP$:P=VAL(P$)
6250 IF(P<1) OR(P>N) THENGOSUB6280:GOTO6230
6260 L$=N$(P):GOSUB3200:IFM4$="2" THEN6290
6270 RETURN
6280 T1=19:T2=20:GOSUB4600:RETURN
6290 T1=15:T2=20:GOSUB4600:VE=18:GOSUB110
6300 PRINT"Eerst uw FINPLAN bijwerken!":GOSUB3100
6310 M=1:RETURN
7000 REM HOOFD-PROGRAMMA'S
7010 GOSUB2150:PRINT:DN$=DB$:GOSUB5000:DB$=DN$
7020 L1=SS:GOSUB5800:RETURN
7030 GOSUB100:N$=N3$:M=5:VE=0:GOSUB3000
7040 GOSUB3300:RETURN
7070 N=N+1
7080 N$(N)=L1$+L2$+L3$+L4$+L5$+L6$+L7$:RETURN
7100 N$(P)=L1$+L2$+L3$+L4$+L5$+L6$+L7$:RETURN
7200 GOSUB7010:L2$="+000":L4$="4":GOSUB7030
7210 IF(N+1)>(NR-1) THEN4800
7220 GOSUB5100:GOSUB5300:GOSUB5400:GOSUB5500:GOSUB5250
7230 GOSUB7600:IFM=1 THENM=0:GOTO7210
7240 IFM=2 THENM=0:GOTO7260
7250 GOSUB4500:GOTO7210
7260 HO=0:VE=16:GOSUB110:GOSUB5600:GOSUB3100:GOTO7800
7400 IFN=0 THENGOSUB2170:GOTO9800
7410 GOSUB7010:L2$="+000":L4$="2":FP=1
7420 GOSUB6000:IF(M=1) AND(BW=0) THENM=0:GOTO4000
7430 IF(M=1) AND(BW=1) THENM=0:GOSUB4500:GOTO7260
7440 GOSUB7030:L2$="+000":L4$="2"
7450 GOSUB5150:GOSUB5200:GOSUB5350:GOSUB5450
7460 GOSUB5550:L3$="B"+L8$:GOSUB7600
7470 IFM=1 THENM=0:GOTO7450
7480 IFM=2 THENM=0:GOTO7260
7490 GOTO7420
7600 GOSUB250:VE=VE+2:GOSUB110
7610 PRINT"Is dit correct? (J/N): ";
7620 GOSUB210:PRINT:GOSUB23000
7630 IF(IN$="J") AND(L4$="4") THENGOSUB7070:GOTO7660
7640 IFIN$="J" THENGOSUB7100:GOTO7660
7650 M=1:GOSUB4500:GOSUB7860:RETURN
7660 BW=1:GOSUB4500:GOSUB250:VE=18:GOSUB110
7670 PRINT"Wilt U nog meer ";N3$;"?"
7680 PRINT:PRINT" (J/N): ";:GOSUB210:PRINT:GOSUB23000
7690 IFIN$="J" THENRETURN
7700 M=2:GOSUB4500:RETURN
7800 GOSUB100:N$="Sorteren met SHELL-SORT"
7810 VE=0:GOSUB3000:PRINT:GOSUB250
7820 PRINT"- Datum converteren en in index zetten,"
7830 FORI=1 TON:L$=LEFT$(N$(I),6)
7840 GOSUB2200:ZZ(I)=SS:NEXTI
7850 CR=4:R=N:GOSUB2000:GOSUB270:GOTO4000
7860 HO=12:VE=15:GOSUB110
7870 PRINT"** Herhaling **":HO=0:RETURN
8000 GOSUB2170:PRINT:IFN=0 THEN9800
8010 PRINT"Om een post af te voeren moet U het"
8020 PRINT"postnummer kennen!":GOSUB250
8030 PRINT:PRINT"Kent U het postnummer? (J/N): ";
8040 GOSUB210:PRINT:GOSUB23000
8050 IFIN$="J" THEN8130
8060 M=5:GOSUB9100:GOSUB250
8070 GOSUB100:N$=N1$:VE=0:GOSUB3000
8080 N$=N2$:VE=2:GOSUB3000:PRINT:GOSUB250
8090 PRINT:PRINT"Is de bewuste post aanwezig? (J/N): ";
8100 GOSUB210:PRINT:GOSUB23000
8110 IFIN$="J" THEN8130
8120 GOSUB270:GOTO4000
8130 PRINT:PRINT"Geef het postnummer: ";:INPUTIN$
8140 P=VAL(IN$)
8150 IF(P<1) OR(P>N) THENGOSUB250:GOTO8030
8160 PRINT:PRINT"Is dit de bewuste post? :":PRINT
8170 L$=N$(P):GOSUB3200:PRINT"Datum :";M1$
8180 PRINT"KBCP  :";M3$:PRINT"Teken :";M4$
8190 PRINT"Rubr. :";M5$:PRINT"Bedrag:";M6$
8200 PRINT"Omschr:";M7$
8210 PRINT:PRINT" (J/N): ";:GOSUB210:PRINT:GOSUB23000
8220 IFIN$="J" THEN8235
8230 GOSUB2170:GOTO8030
8235 IFP=N THEN8250
8240 FORI=P TON-1:N$(I)=N$(I+1):NEXTI
8250 N$(N)="":N=N-1:BW=1
8260 GOSUB100:GOSUB250:HO=15:VE=12:GOSUB110
8270 PRINT"Afgevoerd!":FORI=1 TO1000:NEXTI
8280 GOSUB270:IFN=0 THEN8330
8290 GOSUB250:PRINT
8300 PRINT"Wilt U nog meer posten afvoeren?"
8310 PRINT" (J/N): ";:GOSUB210:PRINT:GOSUB23000
8320 IFIN$="J" THENGOSUB250:GOTO8030
8330 GOSUB250:HO=0:VE=17:GOSUB110:GOSUB5600
8340 GOSUB3100:GOSUB270:GOTO4000
8500 GOSUB100:N$=N3$:VE=0:GOSUB3000:PRINT
8510 GOSUB3300:GOSUB3100:GOTO4000
9000 GOSUB2170:PT=0:PRINT:IFN=0 THEN9800
9010 PRINT"In volgorde van:":L=16:GOSUB3050
9020 PRINT" 1=Datum (volgens bestand)"
9030 PRINT" 2=Rubrieken"
9040 PRINT" 3=Bedragen":PRINT
9050 PRINT" 4=Terug naar HOOFD-MENU":PRINT
9060 GOSUB250:PRINT:PRINT"Uw keuze: ";
9070 GOSUB210:PRINTIN$:W=VAL(IN$)
9080 IF(W<1) OR(W>4) THEN9000
9090 ONW GOTO9100,9120,9120,4000
9100 FORI=1 TON:Z(I)=I:NEXTI:GOTO9300
9120 FORI=1 TON:L$=N$(I):GOSUB3200
9140 IFW=2 THENZZ(I)=VAL(M5$)
9160 IFW=3 THENZZ(I)=VAL(M6$)
9180 Z(I)=I:NEXTI:CR=5:R=N:GOSUB2000
9300 C=1:BL=0:BT=INT(N/17)
9310 IF17*BT<>N THENBT=BT+1
9320 GOSUB9600:H=C:D=C+16:IFD>N THEND=N
9330 FORI=C TOD:L$=N$(Z(I)):GOSUB3200
9340 SR=Z(I):GOSUB300:L$=SR$
9350 IFLEN(L$)<3 THENL$=" "+L$:GOTO9350
9360 M7$=LEFT$(M7$,10)
9370 IFLEN(M7$)<10 THENM7$=M7$+" ":GOTO9370
9380 L$=L$+" "+M1$+" "+M3$+" "+M5$
9390 L$=L$+" "+M6$+" "+M7$:PRINTL$
9400 NEXTI:C=I:L=39:GOSUB3050
9410 GOSUB3150
9420 IF(IN$="S") AND(M=5) THENM=0:GOSUB270:RETURN
9430 IFIN$="S" THENGOSUB270:GOTO9680
9440 IFIN$="T" THEN9500
9450 IFIN$="V" THEN9550
9460 GOTO9410
9500 REM VORIG BLAD
9510 C=H-17
9520 IFC<0 THENC=1:BL=0:GOTO9320
9530 BL=BL-2:GOTO9320
9550 REM VOLGEND BLAD
9560 IFC>N THENC=H:BL=BL-1
9570 GOTO9320
9600 GOSUB100:BL=BL+1:REM KOPREGELS
9610 SR=BL:GOSUB300:L1$=SR$
9620 SR=BT:GOSUB300:L2$=SR$
9630 L$="Blad:"+L1$+" van:"+L2$
9640 IFLEN(L$)<(39-LEN(N$)) THENL$=" "+L$:GOTO9640
9650 L$=N$+L$:PRINTL$:L=39:GOSUB3050
9660 PRINT"Pno Datum  KBCP RB  Bedrag   Omschr."
9670 L=39:GOSUB3050:RETURN
9680 GOSUB2150:HO=0:VE=10:GOSUB110
9690 GOSUB5600:GOSUB3100:GOTO9000
9800 HO=6:VE=12:GOSUB110
9810 PRINT"Geen posten in het bestand!"
9820 GOSUB3100:GOSUB270:GOTO4000
10000 GOSUB2150:FORI=1 TO5:GOSUB250:NEXTI
10010 HO=9:VE=12:GOSUB110
10020 PRINT"Tot de volgende keer!"
10030 END
15000 X$="":GOSUB250:HO=12:VE=12:GOSUB110
15010 PRINT"Uw code: ";:FORI=1 TOLEN(N$)
15020 GOSUB210:X$=X$+IN$:NEXTI:PRINT
15030 IFX$=N$ THENRETURN
15040 GOTO10000
20000 GOSUB100:N$="Bestand Inlezen":VE=0:GOSUB3000
20010 N$=N2$:VE=2:GOSUB3000
20100 GOSUB3100
20600 IFN=0 THENRETURN
20610 T1=VAL(RIGHT$(N$(N),1)):T2=2
20620 IFT1<>T2 THEN20680
20630 DB$=LEFT$(N$(N),6)
20640 SB$=MID$(N$(N),7,9)
20650 SC$=MID$(N$(N),16,9)
20660 SD$=MID$(N$(N),25,9)
20670 N$(N)="":N=N-1:RETURN
20680 GOSUB2150:HO=0:VE=12:GOSUB110
20690 PRINT"U hebt het verkeerde bestand ingelezen!"
20700 FORI=1 TO5:GOSUB250:NEXTI
20710 GOSUB3100:GOTO10000
20990 RETURN
21000 GOSUB100:N$="Bestand wegschrijven":VE=0
21010 GOSUB3000:N$=N2$:VE=2:GOSUB3000
21020 N=N+1
21030 N$(N)=DB$+SB$+SC$+SD$+"2"
21100 GOSUB3100
21600 N$(N)="":N=N-1:RETURN
23000 REM   HL ROUTINE
23010 REM   ----------
23020 HL=ASC("a")-ASC("A")
23030 IFHL<0 THEN23200
23040 SR$="":FORZ=1 TOLEN(IN$)
23050 SR=ASC(MID$(IN$,Z,1))
23060 IF(SR>96) AND(SR<123) THENSR=SR-HL
23070 SR$=SR$+CHR$(SR)
23080 NEXTZ:IN$=SR$:RETURN
23200 HL=ABS(HL)
23210 SR$="":FORZ=1 TOLEN(IN$)
23220 SR=ASC(MID$(IN$,Z,1))
23230 IF(SR>64) AND(SR<91) THENSR=SR+HL
23240 SR$=SR$+CHR$(SR)
23250 NEXTZ:IN$=SR$:RETURN
25000 DATA101
25010 DATA20
25020 DATA"1234"
25030 REM LENGTE RUBR.NAMEN MAX.11 KAR.
25040 DATA"HUUR","INRICHTING"
25050 DATA"VERZEKERING","GENEESK.BEH"
25060 DATA"LEVENSMIDD.","KLEDING"
25070 DATA"DRANK/SIG.","KR-TIJDSCHR"
25080 DATA"STUDIE","VAK/UITGAAN"
25090 DATA"HOBBY","ONVOORZIEN"
25100 DATA"TELEFOON","GAS/WAT/EL"
25110 DATA"VERVOER","AUTO"
25120 DATA"BELASTINGEN","INKOMSTEN"
25130 DATA"KRUISP.K/CH","KRUISP.K/B"
30000 REM
30010 REM HOBBYSCOOP-BASICODE2
30020 REM *                  *
30030 REM * INT.HH-REKENING  *
30040 REM * BANKREK/FINPLAN  *
30050 REM                    *
30060 REM P INGEZONDEN DOOR
30070 REM O                  1
30080 REM S  B.P.M.RINTJEMA  2
30090 REM T  TUINSTRAAT 13   0
30100 REM B  5802 AD VENRAY  0
30110 REM U
30120 REM S  EIGEN ONTWERP   *
30130 REM   FEB.84/ITT-2020  *
30140 REM * DEC.84/APPLE IIE *
30150 REM *                  *
30160 REM **** HILVERSUM *****
30170 REM
30180 REM COPYRIGHT  NOS - HILVERSUM

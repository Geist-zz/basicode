
1000 GOSUB3500:GOTO20:REM KASBOEK
1010 N1$="INTEGRALE HUISHOUD-REKENING"
1020 N2$="KASBOEK":GOSUB3500
1030 DIMN$(NR),RB$(RB)
1040 DIMZZ(NR),Z(NR)
1050 GOSUB3600
1060 IFCR=1 THENDIMA(NR,15)
1200 SR=0:CN=2:CT=9:GOSUB310:SK$=SR$
1220 DK$="010170"
1790 M=0:N=0:BW=0
1800 GOTO3800
2000 GOSUB250:PRINT:PRINT"- Sorteren":M=R
2010 M=INT(M/2):IFM=0 THEN2130
2020 J=1:K=R-M
2030 H=J
2040 V=H+M
2050 IFZZ(H)<=ZZ(V) THEN2110
2060 ZZ(0)=ZZ(H):ZZ(H)=ZZ(V):ZZ(V)=ZZ(0)
2070 IFCR=4 THENZ$=N$(H):N$(H)=N$(V):N$(V)=Z$
2080 IFCR=5 THENZ(0)=Z(H):Z(H)=Z(V):Z(V)=Z(0)
2090 H=H-M:IFH<1 THEN2110
2100 GOTO2040
2110 J=J+1:IFJ>K THEN2010
2120 GOTO2030
2130 GOSUB250:PRINT
2135 PRINT"Gereed! - NU: GARBAGE COLLECTION"
2140 GOSUB270:RETURN
2150 GOSUB100:N$=N1$:VE=0:GOSUB3000
2160 N$=N2$:VE=2:GOSUB3000:RETURN
2170 GOSUB100:N$=N2$:VE=0:GOSUB3000
2180 N$=N3$:VE=2:GOSUB3000:RETURN
2200 REM DATUM CONTROLE + CONVERSIE
2210 IFLEN(L$)<>6 THENM=1:GOTO2830
2220 DD=VAL(LEFT$(L$,2))
2230 IF(DD<1) OR(DD>31) THENM=1:GOTO2830
2240 MM=VAL(MID$(L$,3,2))
2250 IF(MM<1) OR(MM>12) THENM=1:GOTO2830
2260 IF(MM=2) AND(DD>29) THENM=1:GOTO2830
2270 IF(MM=4) AND(DD>30) THENM=1:GOTO2830
2280 IF(MM=6) AND(DD>30) THENM=1:GOTO2830
2290 IF(MM=9) AND(DD>30) THENM=1:GOTO2830
2300 IF(MM=11) AND(DD>30) THENM=1:GOTO2830
2310 JJ=VAL(RIGHT$(L$,2))
2320 IF(JJ<70) THENM=1:GOTO2840
2330 IF((JJ/4)<>INT(JJ/4)) THEN2370
2340 SS=365*JJ+DD:IFMM<3 THEN2360
2350 SS=SS-INT(MM*.4+2.3):JJ=JJ+1
2360 SS=SS+INT(MM*31+((JJ-1)/4)):RETURN
2370 IF(MM=2) AND(DD=29) THENM=1:GOTO2830
2380 GOTO2340
2400 REM CONTROLE RUBRIEKNUMMERS
2410 IF(VAL(L$)<1) OR(VAL(L$)>RB) THENM=1:GOTO2860
2420 IFLEN(L$)=1 THENL$="0"+L$
2430 RETURN
2500 REM CONTROLE BEDRAG
2510 IFL$="" THENM=1:GOTO2810
2520 IFVAL(L$)<-99999.99 THENM=1:GOTO2880
2530 IFVAL(L$)>99999.99 THENM=1:GOTO2880
2540 SR=VAL(L$):GOSUB310
2550 IFLEFT$(SR$,2)="**" THENM=1:GOTO2900
2560 RETURN
2600 REM CONTROLE OMSCHRIJVING
2610 IFL$="" THENM=1:GOTO2810
2620 IFLEN(L$)>L THENM=1:GOTO2910
2630 RETURN
2800 REM FOUTMELDINGEN
2810 GOSUB250
2820 PRINT"Ook dit gegeven hoort er bij!":GOTO4400
2830 GOSUB250:PRINT"Datum niet correct!":GOTO4400
2840 GOSUB250
2850 PRINT"Graag een datum NA '010170'":GOTO4400
2860 GOSUB250
2870 PRINT"Buiten toegestane reeks!":GOTO4400
2880 GOSUB250
2890 PRINT"Bedrag tussen -99999.99 en +99999.99":GOTO4400
2900 GOSUB250:PRINT"U deed iets verkeerd!":GOTO4400
2910 GOSUB250:PRINT"Omschr. max.";L;" kar.!":GOTO4400
3000 REM KOPPEN CENTREREN
3010 X$="---------------------------------------"
3020 X=LEN(N$):HO=INT((40-X)/2):X$=LEFT$(X$,X)
3030 GOSUB110:PRINTN$
3035 IFM=5 THENM=0:RETURN
3040 VE=VE+1:GOSUB110:PRINTX$:RETURN
3050 REM STREEP ER ONDER
3060 X$="---------------------------------------"
3070 X$=LEFT$(X$,L)
3080 PRINTX$:RETURN
3100 GOSUB250:HO=0:VE=23:GOSUB110
3110 PRINT"Druk een toets in ...";
3120 GOSUB210:PRINT:RETURN
3150 GOSUB250:HO=0:VE=23:GOSUB110
3160 PRINT"T=Terug/S=Stoppen/V=Verder ...";
3170 GOSUB210:PRINT:GOSUB23000:RETURN
3200 REM STRING UITEENRAFELEN
3210 M1$=MID$(L$,1,6):REM DATUM
3220 M2$=MID$(L$,7,4):REM CRDGN
3230 M3$=MID$(L$,11,4):REM K/B/C/P
3240 M4$=MID$(L$,15,1):REM TEKEN
3250 M5$=MID$(L$,16,2):REM RUBRIEK
3260 M6$=MID$(L$,18,9):REM BEDRAG
3270 M7$=RIGHT$(L$,LEN(L$)-26):REM OMSCHR.
3280 RETURN
3300 REM PRINT RUBRIEKEN
3310 L=39:GOSUB3050:D=RB
3320 IF(D<>2*INT(D/2)) THEND=D+1
3330 D=.5*D:FORI=1 TOD:SR=I:GOSUB300
3340 M1$=SR$:IFLEN(M1$)=1 THENM1$=" "+M1$
3350 SR=I+D:GOSUB300:M2$=SR$
3360 IFLEN(M2$)=1 THENM2$=" "+M2$
3370 IFLEN(RB$(I))<16 THEN3440
3380 L$=M1$+"="+RB$(I)
3390 IF(I+D)>RB THEN3410
3400 L$=L$+M2$+"="+RB$(I+D)
3410 PRINTL$:NEXTI:GOSUB3050
3420 PRINT"UITGAVEN: +        INKOMSTEN: -"
3430 GOSUB3050:RETURN
3440 RB$(I)=RB$(I)+" ":GOTO3370
3500 REM BEREKENING 'A' IN 'CLEAR A'
3510 RESTORE:READNR:READRB
3520 A=(45+3)*NR:REM POSTEN
3530 A=A+(16+3)*RB:REM RUBRIEKEN
3540 RETURN
3600 READN$:GOSUB100:GOSUB15000
3610 GOSUB100:N$="INTEGRALE HUISHOUD-REKENING":VE=0
3620 GOSUB3000:GOSUB250:HO=3:VE=14:GOSUB110
3630 PRINT"Moet U een bestand !! NUMERIEK !!"
3640 VE=16:GOSUB110:PRINT"opslaan? (J/N): ";
3650 GOSUB210:PRINT:GOSUB23000
3660 CR=0:IFIN$="J" THENCR=1
3670 RETURN
3800 REM RUBRIEKEN INLEZEN UIT DATA-REGELS
3810 FORI=1 TORB:READRB$(I):NEXTI
3820 GOSUB2150:PRINT:GOSUB250
3830 PRINT"Hebt U al een bestand aangemaakt?"
3840 PRINT:PRINT" (J/N): ";:GOSUB210:PRINT:GOSUB23000
3850 IFIN$="J" THENGOSUB20000
3860 SK=VAL(SK$):IFSK=0 THENGOSUB2150:GOSUB3880
3870 GOSUB2150:GOSUB250:HO=0:VE=12:GOSUB110
3875 GOSUB5600:GOTO4000
3880 GOSUB250:HO=0:VE=12:GOSUB110
3890 PRINT"Begin-saldo KAS: FL. ";:INPUTSK$
3900 L$=SK$:GOSUB2500:IFM=1 THENM=0:GOTO3880
3910 SK$=SR$:RETURN
4000 GOSUB2150:PRINT
4010 PRINT"KASBOEK bijgewerkt t/m ";DK$
4020 L=29:GOSUB3050:PRINT
4030 PRINT" 1=posten inbrengen"
4040 PRINT" 2=posten afvoeren":PRINT
4050 PRINT" 3=overzicht posten"
4060 PRINT" 4=overzicht rubrieken":PRINT
4070 PRINT" 5=einde":PRINT:PRINT
4080 PRINT"Er zijn nu ";N;" posten in het bestand"
4090 L=36:GOSUB3050
4100 PRINT"Nog ";NR-N-1;" posten kunnen worden opgenomen"
4110 GOSUB250:PRINT:PRINT"Uw keuze: ";:GOSUB210:PRINTIN$
4120 W=VAL(IN$):IF(W<1) OR(W>5) THEN4000
4130 IFW=1 THENN3$="Posten inbrengen"
4140 IFW=2 THENN3$="Posten afvoeren"
4150 IFW=3 THENN3$="Overzicht posten"
4160 IFW=4 THENN3$="Overzicht rubrieken"
4170 ONW GOTO7200,8000,9000,8500,4200
4200 IFBW=1 THENGOSUB21000
4210 GOTO10000
4400 REM CORRECTIE
4410 FORI=1 TO1000:NEXTI:HO=0:GOSUB110:GOSUB4440
4420 VE=VE+1:GOSUB110:GOSUB4440
4430 VE=VE+1:GOSUB110:GOSUB4440:VE=VE-3:RETURN
4440 PRINT"                                       ":RETURN
4500 REM ONDERKANT SCHERM SCHOON
4510 GOSUB120:T2=VE:HO=0
4520 FORI=16 TOT2:VE=I-1:GOSUB110:GOSUB4440
4530 NEXTI:RETURN
4600 GOSUB250:HO=0:VE=18:GOSUB110
4610 PRINT"U overschrijdt nu het maximum aantal"
4620 PRINT:PRINT"posten per bestand!":GOSUB3100
4630 IFBW=1 THEN7500
4640 GOSUB270:GOTO4000
5000 GOSUB250:HO=0:VE=9:GOSUB110
5010 PRINT"Bijgewerkt t/m ........:  ";DN$
5020 GOSUB250:VE=11:GOSUB110
5030 PRINT"Datum vandaag? (DDMMJJ): ";:INPUTL1$
5040 L$=L1$:GOSUB2200:IFM=1 THENM=0:GOTO5020
5050 DN$=L1$:RETURN
5100 GOSUB250:HO=0:VE=16:GOSUB110
5110 PRINT"Uitg./ontv.datum (DDMMJJ): ";:INPUTL1$
5120 L$=L1$:GOSUB2200:IFM=1 THENM=0:GOTO5100
5130 RETURN
5150 L=19:GOSUB250:VE=VE+1:GOSUB110
5160 PRINT"Omschr.(MAX.";L;" k.): ";:INPUTL7$
5170 L$=L7$:GOSUB2600:IFM=1 THENM=0:GOTO5150
5180 RETURN
5200 GOSUB250:VE=VE+1:GOSUB110
5210 PRINT"Rubriek no. (1-";RB;"): ";:INPUTL5$
5220 L$=L5$:GOSUB2400:IFM=1 THENM=0:GOTO5200
5230 L5$=L$:RETURN
5250 GOSUB250:VE=VE+1:GOSUB110
5260 PRINT"Bedrag ...........: ";:INPUTL6$:L$=L6$
5270 GOSUB2500:IFM=1 THENM=0:GOTO5250
5280 L6$=SR$:RETURN
5600 PRINT"Eind-saldo KAS NU: FL. ";
5610 SK=VAL(SK$):IFN=0 THEN5640
5620 FORI=1 TON:L$=N$(I):GOSUB3200
5630 SK=SK-VAL(M6$):NEXTI
5640 SR=SK:GOSUB310:PRINTSR$
5650 GOSUB3100:RETURN
7000 REM HOOFD-PROGRAMMA
7010 GOSUB2170:PRINT:DN$=DK$:GOSUB5000:DK$=DN$:RETURN
7020 GOSUB100:N$=N2$+"-"+N3$:M=5:VE=0:GOSUB3000
7030 GOSUB3300:RETURN
7060 N=N+1
7070 N$(N)=L1$+L2$+L3$+L4$+L5$+L6$+L7$:RETURN
7200 GOSUB7010:L2$="+000":L3$="KAS ":L4$="3":GOSUB7020
7210 IF(N+1)>(NR-1) THEN4600
7220 GOSUB5100:GOSUB5150:GOSUB5200:GOSUB5250
7230 GOSUB7400:IFM=1 THENM=0:GOSUB4500:GOTO7250
7240 GOTO7210
7250 GOSUB250:HO=6:VE=18:GOSUB110
7260 GOSUB5600:GOTO7500
7400 GOSUB250:VE=VE+2:GOSUB110
7410 PRINT"Is dit correct? (J/N): ";
7420 GOSUB210:PRINT:GOSUB23000
7430 IFIN$="J" THENBW=1:GOSUB7060:GOTO7450
7440 GOSUB4500:GOSUB7560:RETURN
7450 GOSUB4500:GOSUB250:VE=18:GOSUB110
7460 PRINT"Wilt U nog meer posten inbrengen?"
7470 PRINT:PRINT" (J/N): ";:GOSUB210:PRINT:GOSUB23000
7480 IFIN$="J" THENGOSUB4500:RETURN
7490 M=1:RETURN
7500 GOSUB100:N$="Sorteren met SHELL-SORT"
7510 VE=0:GOSUB3000:PRINT:GOSUB250
7520 PRINT"- Datum converteren en in index zetten,"
7530 FORI=1 TON:L$=LEFT$(N$(I),6)
7540 GOSUB2200:ZZ(I)=SS:NEXTI
7550 R=N:CR=4:GOSUB2000:GOTO4000
7560 HO=12:VE=15:GOSUB110
7570 PRINT"** Herhaling **":HO=0:RETURN
8000 GOSUB2170:PRINT:IFN=0 THEN9800
8010 PRINT"Om een post af te voeren moet U het"
8020 PRINT"POSTNUMMER kennen!"
8030 PRINT:PRINT"Kent U het postnummer? (J/N): ";
8040 GOSUB210:PRINT:GOSUB23000
8050 IFIN$="J" THEN8130
8060 M=5:GOSUB9150:GOSUB250
8070 GOSUB100:N$=N2$:VE=0:GOSUB3000
8080 N$=N3$:VE=2:GOSUB3000:PRINT:GOSUB250
8090 PRINT:PRINT"Is de bewuste post aanwezig? (J/N): ";
8100 GOSUB210:PRINT:GOSUB23000
8110 IFIN$="J" THEN8130
8120 GOSUB270:GOTO4000
8130 GOSUB250:PRINT:PRINT"Geef het postnummer: ";
8140 INPUTIN$:P=VAL(IN$)
8150 IF(P<1) OR(P>N) THENGOSUB250:GOTO8030
8160 PRINT:PRINT"Is dit de bewuste post? :":PRINT
8170 L$=N$(P):GOSUB3200:PRINT"Datum :";M1$
8180 PRINT"Rubr. :";M5$:PRINT"Bedrag:";M6$
8190 PRINT"Omschr:";M7$
8200 PRINT:PRINT" (J/N): ";
8210 GOSUB210:PRINT:GOSUB23000
8220 IFIN$="J" THEN8235
8230 GOSUB2170:GOTO8030
8235 IFP=N THEN8250
8240 FORI=P TON-1:N$(I)=N$(I+1):NEXTI
8250 N$(N)="":N=N-1:BW=1
8260 GOSUB100:GOSUB250:HO=15:VE=12:GOSUB110
8270 PRINT"Afgevoerd!":FORI=1 TO1000:NEXTI
8280 GOSUB270:IFN=0 THEN8330
8290 GOSUB250:PRINT
8300 PRINT"Wilt U nog meer posten afvoeren?"
8310 PRINT" (J/N): ";:GOSUB210:PRINT:GOSUB23000
8320 IFIN$="J" THENGOSUB250:GOTO8030
8330 GOSUB250:HO=6:VE=18:GOSUB110:GOSUB5600
8340 GOSUB270:GOTO4000
8500 GOSUB100:N$=N3$:VE=0:GOSUB3000:PRINT
8510 GOSUB3300:GOSUB3100:GOTO4000
9000 GOSUB2170:PRINT:IFN=0 THEN9800
9010 PRINT"In volgorde van:":L=16:GOSUB3050:PRINT
9020 PRINT" 1=Datum (volgens bestand)"
9030 PRINT" 2=Rubrieknummer"
9040 PRINT" 3=Bedragen":PRINT
9050 PRINT" 4=Terug naar HOOFD-MENU"
9060 PRINT:PRINT
9070 GOSUB250:PRINT"Uw keuze: ";:GOSUB210:PRINTIN$
9080 W=VAL(IN$):IF(W<1) OR(W>4) THEN9000
9090 ONW GOTO9150,9160,9160,4000
9150 FORI=1 TON:Z(I)=I:NEXTI:GOTO9300
9160 FORI=1 TON:L$=N$(I):GOSUB3200
9170 IFW=2 THENZZ(I)=VAL(M5$)
9180 IFW=3 THENZZ(I)=VAL(M6$)
9190 Z(I)=I:NEXTI:CR=5:R=N:GOSUB2000
9300 C=1:BL=0:BT=INT(N/17)
9310 IF17*BT<>N THENBT=BT+1
9320 GOSUB9600:H=C:D=C+16:IFD>N THEND=N
9330 FORI=C TOD:L$=N$(Z(I)):GOSUB3200
9340 SR=Z(I):GOSUB300:L$=SR$
9350 IFLEN(L$)<3 THENL$=" "+L$:GOTO9350
9360 M7$=LEFT$(M7$,15)
9370 IFLEN(M7$)<15 THENM7$=M7$+" ":GOTO9370
9380 L$=L$+" "+M1$+" "+M5$+" "+M7$
9390 L$=L$+" "+M6$:PRINTL$
9400 NEXTI:C=I:L=39:GOSUB3050
9410 GOSUB3150
9420 IF(IN$="S") AND(M=5) THENM=0:GOSUB270:RETURN
9430 IFIN$="S" THENGOSUB270:GOTO9680
9440 IFIN$="T" THEN9500
9450 IFIN$="V" THEN9560
9460 GOTO9410
9500 REM VORIG BLAD
9510 C=H-17:IFC<0 THENC=1:BL=0:GOTO9320
9520 BL=BL-2:GOTO9320
9550 REM VOLGEND BLAD
9560 IFC>N THENC=H:BL=BL-1
9570 GOTO9320
9600 GOSUB100:BL=BL+1:REM KOPREGELS
9610 SR=BL:GOSUB300:L1$=SR$
9620 SR=BT:GOSUB300:L2$=SR$
9630 L$="Blad:"+L1$+" van:"+L2$
9640 IFLEN(L$)<(39-LEN(N$)) THENL$=" "+L$:GOTO9640
9650 L$=N$+L$:PRINTL$:L=39:GOSUB3050
9660 PRINT"Pno Datum  RB Omschr.(15 kar)  Bedrag"
9670 L=39:GOSUB3050:RETURN
9680 GOSUB2150:HO=0:VE=12:GOSUB110
9690 GOSUB5600:GOTO9000
9800 HO=6:VE=12:GOSUB110
9810 PRINT"Geen posten in het bestand!"
9820 GOSUB3100:GOSUB270:GOTO4000
10000 GOSUB2150:FORI=1 TO5:GOSUB250:NEXTI
10010 HO=9:VE=12:GOSUB110
10020 PRINT"Tot de volgende keer!"
10030 END
15000 X$="":GOSUB250:HO=12:VE=12:GOSUB110
15010 PRINT"Uw code: ";:FORI=1 TOLEN(N$)
15020 GOSUB210:X$=X$+IN$:NEXTI:PRINT
15030 IFX$=N$ THENRETURN
15040 GOTO10000
20000 GOSUB100:N$="Bestand Inlezen":VE=0:GOSUB3000
20010 N$=N2$:VE=2:GOSUB3000
20100 GOSUB3100
20600 IFN=0 THENRETURN
20610 T1=VAL(RIGHT$(N$(N),1)):T2=3
20620 IFT1<>T2 THEN20660
20630 DK$=LEFT$(N$(N),6)
20640 SK$=MID$(N$(N),7,9)
20650 N$(N)="":N=N-1:RETURN
20660 GOSUB2150:HO=0:VE=12:GOSUB110
20670 PRINT"U hebt het verkeerde bestand ingelezen!"
20680 FORI=1 TO5:GOSUB250:NEXTI
20690 GOSUB3100:GOTO10000
20990 RETURN
21000 GOSUB100:N$="Bestand Wegschrijven":VE=0
21010 GOSUB3000:N$=N2$:VE=2:GOSUB3000
21020 N=N+1
21030 N$(N)=DK$+SK$+"3"
21100 GOSUB3100
21600 N$(N)="":N=N-1:RETURN
23000 REM   HL ROUTINE
23010 REM   ----------
23020 HL=ASC("a")-ASC("A")
23030 IFHL<0 THEN23200
23040 SR$="":FORZ=1 TOLEN(IN$)
23050 SR=ASC(MID$(IN$,Z,1))
23060 IF(SR>96) AND(SR<123) THENSR=SR-HL
23070 SR$=SR$+CHR$(SR)
23080 NEXTZ:IN$=SR$:RETURN
23200 HL=ABS(HL)
23210 SR$="":FORZ=1 TOLEN(IN$)
23220 SR=ASC(MID$(IN$,Z,1))
23230 IF(SR>64) AND(SR<91) THENSR=SR+HL
23240 SR$=SR$+CHR$(SR)
23250 NEXTZ:IN$=SR$:RETURN
25000 DATA101
25010 DATA20
25020 DATA"1234"
25030 REM LENGTE RUBR.NAMEN MAX.11 KAR.
25040 DATA"HUUR","INRICHTING"
25050 DATA"VERZEKERING","GENEESK.BEH"
25060 DATA"LEVENSMIDD.","KLEDING"
25070 DATA"DRANK/SIG.","KR-TIJDSCHR"
25080 DATA"STUDIE","VAK/UITGAAN"
25090 DATA"HOBBY","ONVOORZIEN"
25100 DATA"TELEFOON","GAS/WAT/EL"
25110 DATA"VERVOER","AUTO"
25120 DATA"BELASTINGEN","INKOMSTEN"
25130 DATA"KRUISP.K/CH","KRUISP.K/B"
30000 REM
30010 REM HOBBYSCOOP-BASICODE2
30020 REM *                  *
30030 REM * INT.HH-REKENING  *
30040 REM *     KASBOEK      *
30050 REM                    *
30060 REM P INGEZONDEN DOOR
30070 REM O                  1
30080 REM S  B.P.M.RINTJEMA  2
30090 REM T  TUINSTRAAT 13   0
30100 REM B  5802 AD VENRAY  0
30110 REM U 
30120 REM S  EIGEN ONTWERP   *
30130 REM   FEB.84/ITT-2020  *
30140 REM * DEC.84/APPLE IIE *
30150 REM *                  *
30160 REM **** HILVERSUM *****
30170 REM
30180 REM COPYRIGHT  NOS - HILVERSUM


1000 GOSUB3500:GOTO20:REM UTILITY-2
1010 N1$="INTEGRALE HUISHOUD-REKENING"
1020 N2$="UTILITY NO.2":GOSUB3500
1040 DIMN$(NR),RB$(RB)
1045 GOSUB3600
1050 IFCR=1 THENDIMA(NR,15)
1200 CN=2:CT=9
1800 GOTO4000
2150 GOSUB100:N$=N1$:VE=0:GOSUB3000
2160 N$=N2$:VE=2:GOSUB3000:RETURN
2170 GOSUB100:N$=N4$:VE=0:GOSUB3000
2180 N$=N3$:VE=2:GOSUB3000:RETURN
2200 REM DATUM CONTROLE+CONVERSIE
2210 IFLEN(L$)<>6 THENM=1:GOTO2830
2220 DD=VAL(LEFT$(L$,2))
2230 IF(DD<1) OR(DD>31) THENM=1:GOTO2830
2240 MM=VAL(MID$(L$,3,2))
2250 IF(MM<1) OR(MM>12) THENM=1:GOTO2830
2260 IF(MM=2) AND(DD>29) THENM=1:GOTO2830
2270 IF(MM=4) AND(DD>30) THENM=1:GOTO2830
2280 IF(MM=6) AND(DD>30) THENM=1:GOTO2830
2290 IF(MM=9) AND(DD>30) THENM=1:GOTO2830
2300 IF(MM=11) AND(DD>30) THENM=1:GOTO2830
2310 JJ=VAL(RIGHT$(L$,2))
2320 IF(JJ<70) THENM=1:GOTO2840
2330 IF((JJ/4)<>INT(JJ/4)) THEN2370
2340 SS=365*JJ+DD:IFMM<3 THEN2360
2350 SS=SS-INT(MM*.4+2.3):JJ=JJ+1
2360 SS=SS+INT(MM*31+((JJ-1)/4)):RETURN
2370 IF(MM=2) AND(DD=29) THENM=1:GOTO2830
2380 GOTO2340
2400 REM CONTROLE RUBRIEKNUMMERS
2410 IF(VAL(L$)<1) OR(VAL(L$)>20) THENM=1:GOTO2850
2420 IF(T=1) AND(L$="20") THENM=1:GOTO2860
2430 IF(T=2) AND(L$="19") THENM=1:GOTO2860
2440 IFLEN(L$)=1 THENL$="0"+L$
2450 RETURN
2500 REM CONTROLE OMSCHRIJVING
2510 IFL$="" THENM=1:GOTO2810
2520 IFLEN(L$)>L THENM=1:GOTO2880
2530 RETURN
2600 REM CONTROLE PRIORITEIT
2610 IFL$="" THENM=1:GOTO2810
2620 IF(VAL(L$)<1) OR(VAL(L$)>9) THENM=1:GOTO2850
2630 RETURN
2800 REM FOUTMELDINGEN
2810 GOSUB250
2820 PRINT"Ook dit gegeven hoort er bij!":GOTO4400
2830 GOSUB250:PRINT"Datum niet correct!":GOTO4400
2840 GOSUB250
2845 PRINT"Graag een datum na '010170'":GOTO4400
2850 GOSUB250:PRINT"Buiten toegestane reeks!":GOTO4400
2860 GOSUB250
2870 PRINT"Dit rubrieknummer kan hier niet!":GOTO4400
2880 GOSUB250:PRINT"Omschr. MAX.";L;" kar.!":GOTO4400
2900 GOSUB250:PRINT"Tenminste 39 kar.!":GOTO4400
2920 GOSUB250:PRINT"Tenminste 20 regels!":GOTO4400
3000 REM KOPPEN CENTREREN
3010 X$="---------------------------------------"
3020 X=LEN(N$):HO=INT((40-X)/2):X$=LEFT$(X$,X)
3030 GOSUB110:PRINTN$
3035 IFM=5 THENM=0:RETURN
3040 VE=VE+1:GOSUB110:PRINTX$:RETURN
3050 REM STREEP ER ONDER
3060 X$="---------------------------------------"
3070 X$=LEFT$(X$,L):IFM=5 THENM=0:RETURN
3080 PRINTX$:RETURN
3100 GOSUB250:HO=0:VE=23:GOSUB110
3110 PRINT"Druk een toets in ...";
3120 GOSUB210:PRINT:RETURN
3200 REM STRINGS N$(I) UITEENHALEN
3210 M1$=MID$(L$,1,6):REM DATUM
3220 M2$=MID$(L$,7,4):REM CRDG
3230 M3$=MID$(L$,11,4):REM KBCP
3240 M4$=MID$(L$,15,1):REM TEKEN
3250 M5$=MID$(L$,16,2):REM RUBRIEK
3260 M6$=MID$(L$,18,9):REM BEDRAG
3270 M7$=RIGHT$(L$,LEN(L$)-26):REM OMSCHR.
3280 IFT=1 THENGOSUB3310
3290 IFT=2 THENGOSUB3360
3300 RETURN
3310 IFM4$="1" THEN3330
3320 RETURN
3330 M8$=LEFT$(M7$,5):REM CHEQUENO + BLANK
3340 M7$=RIGHT$(M7$,LEN(M7$)-5):REM OMSCHR.
3350 RETURN
3360 IFM4$="2" THENM8$=" "
3370 IFM4$="4" THENM8$=RIGHT$(M3$,1)
3380 RETURN
3500 REM BEREKENING 'A' IN 'CLEAR A'
3510 RESTORE:READNR:READRB
3520 A=(45+3)*NR:REM POSTEN
3530 A=A+(16+3)*RB:REM RUBRIEKEN
3540 RETURN
3600 READN$:GOSUB100:GOSUB15000
3610 FORI=1 TORB:READRB$(I):NEXTI
3620 GOSUB2150:GOSUB250:HO=3:VE=14:GOSUB110
3630 PRINT"Moet U een bestand !! NUMERIEK !!"
3640 VE=16:GOSUB110:PRINT"opslaan? (J/N): ";
3650 GOSUB210:PRINT:GOSUB23000
3660 CR=0:IFIN$="J" THENCR=1
3670 RETURN
3700 REM PRINT RUBRIEKEN
3710 L=39:GOSUB3050:D=RB
3720 IF(D<>2*INT(D/2)) THEND=D+1
3730 D=.5*D:FORI=1 TOD:SR=I:GOSUB300
3740 M1$=SR$:IFLEN(M1$)=1 THENM1$=" "+M1$
3750 SR=I+D:GOSUB300:M2$=SR$
3760 IFLEN(M2$)=1 THENM2$=" "+M2$
3770 IFLEN(RB$(I))<16 THEN3840
3780 L$=M1$+"="+RB$(I)
3790 IF(I+D)>RB THEN3810
3800 L$=L$+M2$+"="+RB$(I+D)
3810 PRINTL$:NEXTI:GOSUB3050
3820 PRINT"UITGAVEN: +        INKOMSTEN: -"
3830 GOSUB3050:RETURN
3840 RB$(I)=RB$(I)+" ":GOTO3770
4000 GOSUB2150:PRINT
4010 PRINT" 1=Aanvullende Toelichting":PRINT
4020 PRINT" 2=Wijzigen van:"
4030 PRINT"     -Rubrieknummer"
4040 PRINT"     -Omschrijving"
4050 PRINT"     -Prioriteit (BANKREK/FINPLAN)":PRINT
4060 PRINT" 3=Selecteren op RUBRIEK en OMSCHR.":PRINT
4070 PRINT" 4=Overzicht Rubrieken":PRINT
4075 PRINT" 5=Einde":PRINT:PRINT
4080 GOSUB250:PRINT"Uw keuze: ";:GOSUB210:PRINTIN$
4090 W=VAL(IN$):IF(W<1) OR(W>5) THEN4000
4100 ONW GOTO8000,5000,6000,4800,10000
4400 REM CORRECTIE
4410 FORK=1 TO1000:NEXTK:HO=0:GOSUB110:GOSUB4440
4420 VE=VE+1:GOSUB110:GOSUB4440
4430 VE=VE+1:GOSUB110:GOSUB4440:RETURN
4440 PRINT"                                       ":RETURN
4500 REM ONDERKANT SCHERM SCHOON
4510 GOSUB120:T2=VE:FORK=16 TOT2:VE=K-1
4520 GOSUB110:GOSUB4440:NEXTK:RETURN
4600 REM SCHERM SCHOON
4610 HO=0:FORK=T1 TOT2:VE=K:GOSUB110
4620 GOSUB4440:NEXTK:RETURN
4800 GOSUB100:N$="Overzicht Rubrieken":VE=0
4810 GOSUB3000:GOSUB3700:GOSUB3100:GOTO4000
5000 N4$="Wijzigen in bestanden":GOSUB100
5010 N$=N4$:VE=0:GOSUB3000
5020 N$="Instructie":VE=2:GOSUB3000:PRINT
5030 PRINT"1.Moet een gegeven worden gewijzigd,"
5040 PRINT"  dan het nieuwe gegeven inbrengen,":PRINT
5050 PRINT"2.Moet een gegeven gelijk blijven, dan"
5060 PRINT"  '=' inbrengen,":PRINT
5070 PRINT"3.Als niets wordt ingebracht, dan wordt"
5080 PRINT"  /blijft het gegeven blank - als dat"
5090 PRINT"  is toegestaan!":GOSUB3100
5100 M=0:N=0:BW=0:GOSUB20000:IFN=0 THEN4000
5110 GOSUB100:N$=N3$:M=5:VE=0:GOSUB3000
5120 L=39:HO=0:VE=14:GOSUB110:GOSUB3050
5130 R=0:FL=0:VE=1:GOSUB110:GOSUB3050
5140 IFT=2 THENGOSUB5230:GOTO5160
5150 PRINT"Pno Datum  RB   Omschrijving"
5160 GOSUB3050:FORI=1 TON:IFFL=1 THEN5180
5170 L$=N$(I):GOSUB3200:GOSUB5250
5180 NEXTI:IFFL=1 THENFL=0:GOTO5390
5190 IFR>0 THENR=0:GOSUB5330
5200 IFFL=1 THENFL=0:GOTO5390
5210 IFBW=1 THENBW=0:GOTO5770
5220 GOTO5780
5230 PRINT"Pno Datum  P RB   Omschrijving"
5240 RETURN
5250 SR=I:GOSUB300:L$=SR$
5260 IFLEN(L$)<3 THENL$=" "+L$:GOTO5260
5270 IFT=2 THENGOSUB5310:GOTO5290
5280 L$=L$+" "+M1$+" "+M5$+" "+M7$
5290 PRINTL$:R=R+1:IFR=10 THENR=0:GOTO5330
5300 RETURN
5310 L$=L$+" "+M1$+" "+M8$+" "+M5$+" "+M7$
5320 RETURN
5330 GOSUB250:HO=0:VE=16:GOSUB110
5340 PRINT"Is de bewuste post aanwezig? (J/N): ";
5350 GOSUB210:PRINT:GOSUB23000
5360 IFIN$="J" THENFL=1:RETURN
5370 T1=4:T2=13:GOSUB4600:T1=15:T2=19:GOSUB4600
5380 VE=4:GOSUB110:RETURN
5390 GOSUB250:VE=18:GOSUB110
5400 PRINT"Geef POSTNUMMER: ";:INPUTP$:P=VAL(P$)
5410 IF(P<1) OR(P>N) THEN5430
5420 GOSUB5460:L$=N$(P):GOSUB3200:GOTO5470
5430 GOSUB250:PRINT"Nog maar eens!"
5440 FORK=1 TO1000:NEXTK
5450 T1=18:T2=20:GOSUB4600:GOTO5390
5460 T1=15:T2=22:GOSUB4600:RETURN
5470 HO=0:VE=16:GOSUB110
5480 PRINT"Wat moet het worden? :"
5490 GOSUB250:HO=0:VE=18:GOSUB110
5500 PRINT"Rubriek No.(1-20): ";:INPUTL5$
5510 IFL5$="=" THENL5$=M5$
5520 L$=L5$:GOSUB2400:IFM=1 THENM=0:GOTO5490
5530 L5$=L$
5540 L=19:IFT=1 THENL=14
5550 GOSUB250:HO=0:VE=19:GOSUB110
5560 PRINT"Omschr.(<";L+1;" kar.): ";:INPUTL7$
5570 IFL7$="=" THENL7$=M7$
5580 L$=L7$:GOSUB2500:IFM=1 THENM=0:GOTO5540
5590 IF(T=1) OR(T=3) THEN5640
5595 IFM8$=" " THEN5640
5600 GOSUB250:HO=0:VE=20:GOSUB110
5610 PRINT"Prioriteit (1-20): ";:INPUTL8$
5620 IFL8$="=" THENL8$=M8$
5630 L$=L8$:GOSUB2600:IFM=1 THENM=0:GOTO5600
5640 GOSUB250:PRINT:PRINT"Is dit correct? (J/N): ";
5650 GOSUB210:PRINT:GOSUB23000
5660 IFIN$="J" THENBW=1:GOSUB5460:GOTO5680
5670 GOSUB5460:GOSUB5910:GOTO5470
5680 IF(T=1) AND(M4$="0") THENGOSUB5810
5690 IF(T=1) AND(M4$="1") THENGOSUB5830
5700 IF(T=2) AND(M4$="2") THENGOSUB5850
5710 IF(T=2) AND(M4$="4") THENGOSUB5870
5720 IFT=3 THENGOSUB5890
5730 GOSUB250:HO=0:VE=16:GOSUB110
5740 PRINT"Wilt U nog meer posten wijzigen?"
5750 PRINT:PRINT" (J/N): ";:GOSUB210:PRINT:GOSUB23000
5760 IFIN$="J" THEN5110
5770 N=N+1:N$(N)=T$:GOSUB21000
5780 REM BESTAND LEEG
5790 FORI=1 TON:N$(I)="":NEXTI:N=0:P=0
5800 GOSUB270:GOTO4000
5810 N$(P)=M1$+M2$+M3$+M4$+L5$+M6$+L7$
5820 RETURN
5830 N$(P)=M1$+M2$+M3$+M4$+L5$+M6$+M8$+L7$
5840 RETURN
5850 N$(P)=M1$+M2$+M3$+M4$+L5$+M6$+L7$
5860 RETURN
5870 N$(P)=M1$+M2$+"PLA"+L8$+M4$+L5$+M6$+L7$
5880 RETURN
5890 N$(P)=M1$+M2$+M3$+M4$+L5$+M6$+L7$
5900 RETURN
5910 GOSUB250:HO=11:VE=15:GOSUB110
5920 PRINT"*** Herhaling ***":RETURN
6000 N4$="Selecteren"
6010 M=0:N=0:GOSUB20000:IFN=0 THEN4000
6020 GOSUB2170:PRINT
6030 PRINT" 1=Video-prints (op scherm)":PRINT
6040 PRINT" 2=Print-outs (met printer)":PRINT
6050 PRINT" 3=Terug naar HOOFD-MENU":PRINT:PRINT
6060 GOSUB250:PRINT"Uw keuze: ";:GOSUB210:PRINTIN$
6070 W=VAL(IN$):IF(W<1) OR(W>3) THEN6020
6080 X1=39:IFW=1 THENPT=0:RL=39:X2=20
6090 IN$="":IFW=2 THENPT=5:GOSUB7000
6100 IFW=3 THEN5780
6110 IFIN$="N" THEN6020
6120 GOSUB2170:GOSUB250:HO=0:VE=8:GOSUB110
6130 PRINT"Rubrieknummer: ";:INPUTRB$
6140 L$=RB$:GOSUB2400:IFM=1 THENM=0:GOTO6090
6150 RB$=L$
6160 L=19:IFT=1 THENL=14
6170 GOSUB250:HO=0:VE=12:GOSUB110
6180 PRINT"Omschrijving (of deel hiervan):"
6190 INPUTIN$:GOSUB23000:Q$=IN$
6200 Q=LEN(Q$):L$=Q$:GOSUB2500
6210 IFM=1 THENM=0:GOTO6160
6220 REM INSTELLEN
6230 RT$="":RT=INT((RL-X1)/2):BL=0:R=0
6240 IFLEN(RT$)<RT THENRT$=RT$+" ":GOTO6240
6250 IFPT=0 THENGOSUB6810
6260 IFPT=5 THENGOSUB7850:GOSUB7210
6270 REM SELECTIE RUBR.+OMSCHR.
6280 TE=0:FORI=1 TON:L$=N$(I):GOSUB3200
6290 IFM5$=RB$ THEN6310
6300 GOTO6350
6310 IN$=M7$:GOSUB23000:QL$=IN$
6320 FL=0:FORJ=1 TOLEN(QL$):IFFL=1 THEN6340
6330 IFMID$(QL$,J,Q)=Q$ THENFL=1:GOSUB6470
6340 NEXTJ:FL=0
6350 NEXTI:IFTE=0 THEN6390
6360 L$=L4$:R=0:GOSUB7610
6370 IFPT=0 THENGOSUB3100
6380 GOTO6020
6390 L1$="geen informatie in bestand":L$=""
6400 IFPT=5 THEN6440
6410 FORI=1 TO3:GOSUB250:NEXTI
6420 HO=6:VE=12:GOSUB110:PRINTL1$
6430 GOSUB3100:GOTO6020
6440 GOSUB6460:L$=L1$:GOSUB7300:GOSUB6460
6450 L$=L4$:GOSUB7610:GOTO6020
6460 FORI=1 TO4:GOSUB7610:NEXTI:RETURN
6470 TE=TE+1:IFT=1 THENGOSUB6500
6480 IF(T=2) OR(T=3) THENGOSUB6530
6490 RETURN
6500 IFM4$="1" THENM3$=LEFT$(M8$,4)
6510 L$=M1$+" "+M3$+" "+M5$+" "+M6$+" "+M7$
6520 L$=RT$+L$:GOSUB7610:RETURN
6530 L$=M1$+" "+M5$+" "+M6$+" "+M7$
6540 L$=RT$+L$:GOSUB7610:RETURN
6800 REM KOP SCHERM-PRINT
6810 GOSUB100:N$=RB$+" "+Q$
6820 BL=BL+1:SR=BL:GOSUB300:L$="Blad:"+SR$
6830 L=X1-LEN(N$):GOSUB7510:L$=N$+L$:GOSUB7610
6840 L$="-":L=X1:GOSUB7550:L4$=L$:GOSUB7610
6850 IFT=1 THENGOSUB6900
6860 IF(T=2) OR(T=3) THENGOSUB6880
6870 GOSUB7610:L$=L4$:GOSUB7610:RETURN
6880 L$="Datum  RB   Bedrag  Omschrijving"
6890 RETURN
6900 L$="Datum  CHno RB   Bedrag  Omschrijving"
6910 RETURN
7000 REM PRINT-OUTS
7010 GOSUB2170:GOSUB250:HO=5:VE=8:GOSUB110
7020 PRINT"Hebt U een printer? (J/N): ";
7030 GOSUB210:PRINT:GOSUB23000
7040 IFIN$="N" THENRETURN
7050 GOSUB250:HO=9:VE=10:GOSUB110
7060 PRINT"Zet deze dan nu aan!"
7070 GOSUB250:HO=5:VE=12:GOSUB110
7080 PRINT"Geef datum vandaag (voluit):"
7090 GOSUB250:HO=11:VE=14:GOSUB110:INPUTDT$
7100 GOSUB2170:GOSUB250:HO=0:VE=10:GOSUB110
7110 PRINT"Aantal karakters op een regel: ";:INPUTRL
7120 IFRL<39 THENGOSUB2900:GOTO7100
7130 GOSUB250:VE=12:GOSUB110
7140 PRINT"Aantal regels op een blad ...: ";:INPUTX2
7150 IFX2<20 THENGOSUB2920:GOTO7130
7160 RETURN
7200 REM KOP PRINT-OUTS
7210 L$=N1$:GOSUB7300:L$=N2$:GOSUB7300
7220 BL=BL+1:SR=BL:GOSUB300:L1$="Blad:"+SR$
7230 GOSUB7400:GOSUB7610:L$=Q$:L=X1-LEN(RB$)
7240 GOSUB7510:L$=RT$+RB$+L$:GOSUB7610
7250 L=X1:GOSUB7550:L$=RT$+L$:L4$=L$:GOSUB7610
7260 IFT=1 THENGOSUB6900
7270 IF(T=2) OR(T=3) THENGOSUB6880
7280 L$=RT$+L$:GOSUB7610:L$=L4$:GOSUB7610
7290 RETURN
7300 REM CENTREER-ROUTINE
7310 L1=LEN(L$):L2=(RL-L1)/2:L=L1+L2
7320 GOSUB7510:GOSUB7610:L=L1:GOSUB7550
7330 L=L1+L2:GOSUB7510:GOSUB7610:RETURN
7400 REM 3 KOPPEN OP EEN REGEL
7410 L1=LEN(N3$):L2=INT((RL-L1)/2):L3=L1+L2
7420 L=L3-LEN(DT$):L$=N3$:GOSUB7510
7430 L$=DT$+L$:L=RL-LEN(L1$):GOSUB7530
7440 L$=L$+L1$:GOSUB7610:L=LEN(N3$):GOSUB7550
7450 L=L3:GOSUB7510:GOSUB7610:RETURN
7500 REM OP LENGTE BRENGEN
7510 IFLEN(L$)<L THENL$=" "+L$:GOTO7510
7520 RETURN
7530 IFLEN(L$)<L THENL$=L$+" ":GOTO7530
7540 RETURN
7550 IFLEN(L$)<L THENL$=L$+"-":GOTO7550
7560 RETURN
7600 REM PRINT-ROUTINE
7610 IFPT=5 THEN7630
7620 PRINTL$:L$="":GOTO7650
7630 SR$=L$:GOSUB350:GOSUB360
7640 SR$="":L$=""
7650 R=R+1:IFR=X2 THENR=0:GOTO7670
7660 RETURN
7670 IFPT=0 THENPRINTL4$:GOSUB3100
7680 IFPT=5 THENSR$=L4$:GOSUB350:GOSUB360:SR$=""
7690 IFI>=N THENRETURN
7700 IFPT=0 THENGOSUB6800
7710 IFPT=5 THENGOSUB7810:GOSUB7850:GOSUB7200
7720 RETURN
7800 REM TUSSENTIJDS
7810 GOSUB2170:FORK=1 TO3:GOSUB250:NEXTK
7820 HO=0:VE=12:GOSUB110
7830 PRINT"U kunt nu een nieuw blad instellen!"
7840 GOSUB3100:RETURN
7850 GOSUB2170:FORK=1 TO3:GOSUB250:NEXTK
7860 HO=12:VE=12:GOSUB110:PRINT"Ik ben bezig!"
7870 RETURN
8000 GOSUB2150:PRINT
8010 PRINT"Soms  is  het  wel  eens  nuttig om een"
8020 PRINT"uittreksel te maken  per  rubriek  naar"
8030 PRINT"een deel van de omschrijving."
8040 PRINT"Door hierin een code onder te  brengen,"
8050 PRINT"kan  het  aantal  rubrieken  belangrijk"
8060 PRINT"worden uitgebreid!"
8070 PRINT"bijv. A-omschr. / B-omschr. / enz.":PRINT
8080 PRINT"Om dat te bereiken moet de omschrijving"
8090 PRINT"daarop zijn ingesteld.":PRINT
8100 PRINT"Met dit programma kunt U wijzigen:"
8110 PRINT" - het RUBRIEKNUMMER"
8120 PRINT" - de OMSCHRIJVING.":PRINT
8130 PRINT"Dat  kan  voor  alle bestanden, behalve"
8140 PRINT"voor INTEGRALE bestanden."
8150 GOSUB3100:GOSUB2150:PRINT
8160 PRINT"Als  een  bestand  van  BANKREK/FINPLAN"
8170 PRINT"wordt ingelezen, kan voor  de  FINPLAN-"
8180 PRINT"posten  ook de PRIORITEIT worden gewij-"
8190 PRINT"zigd.":PRINT:PRINT
8200 PRINT"U  krijgt  een  eenvoudige lijst met de"
8210 PRINT"meest relevante informatie,":PRINT
8220 PRINT"zowel op het scherm als op een printer."
8230 GOSUB3100:GOSUB2150:PRINT
8240 L=39:GOSUB3050
8250 PRINT"Inlezen            Wegschrijven":GOSUB3050
8260 PRINT"20100-20500        21100-21500":GOSUB3050
8270 PRINT" INPUT N            PRINT N"
8280 PRINT" FOR I=1 TO N       FOR I=1 TO N"
8290 PRINT" INPUT N$(I)        PRINT N$(I)"
8300 PRINT" NEXT I             NEXT I":GOSUB3050
8310 PRINT"Lengte van een post: MAX. 45 kar.":PRINT
8320 PRINT:PRINT"Denk aan max. aantal posten!"
8330 PRINT" (Regel 25000 DATA ";NR;")"
8340 GOSUB3100:GOTO4000
10000 GOSUB2150:FORI=1 TO5:GOSUB250:NEXTI
10010 HO=9:VE=12:GOSUB110
10020 PRINT"Tot de volgende keer!"
10030 END
15000 X$="":GOSUB250:HO=12:VE=12:GOSUB110
15010 PRINT"Uw code: ";:FORI=1 TOLEN(N$)
15020 GOSUB210:X$=X$+IN$:NEXTI:PRINT
15030 IFX$=N$ THENRETURN
15040 GOTO10000
20000 GOSUB100:N$="Bestand Inlezen":VE=0:GOSUB3000
20100 GOSUB3100
20600 IFN=0 THENRETURN
20610 T=VAL(RIGHT$(N$(N),1))
20620 IFT=1 THENN3$="Chequeboek":GOTO20700
20630 IFT=2 THENN3$="Bankrek/Finplan":GOTO20700
20640 IFT=3 THENN3$="Kasboek":GOTO20700
20650 GOSUB2150:FORI=1 TO3:GOSUB250:NEXTI
20660 HO=0:VE=12:GOSUB110
20670 PRINT"U hebt het verkeerde bestand ingelezen!"
20680 FORI=1 TON:N$(I)="":NEXTI:N=0
20690 GOSUB3100:GOTO10000
20700 T$=N$(N):N$(N)="":N=N-1:RETURN
20990 RETURN
21000 GOSUB100:N$="Bestand Wegschrijven"
21010 VE=0:GOSUB3000:N$=N3$:VE=2:GOSUB3000
21100 GOSUB3100
21600 RETURN
23000 REM   HL ROUTINE
23010 REM   ----------
23020 HL=ASC("a")-ASC("A")
23030 IFHL<0 THEN23200
23040 SR$="":FORZ=1 TOLEN(IN$)
23050 SR=ASC(MID$(IN$,Z,1))
23060 IF(SR>96) AND(SR<123) THENSR=SR-HL
23070 SR$=SR$+CHR$(SR)
23080 NEXTZ:IN$=SR$:RETURN
23200 HL=ABS(HL)
23210 SR$="":FORZ=1 TOLEN(IN$)
23220 SR=ASC(MID$(IN$,Z,1))
23230 IF(SR>64) AND(SR<91) THENSR=SR+HL
23240 SR$=SR$+CHR$(SR)
23250 NEXTZ:IN$=SR$:RETURN
25000 DATA101
25010 DATA20
25020 DATA"1234"
25030 REM LENGTE RUBR.NAMEN MAX.11 KAR.
25040 DATA"HUUR","INRICHTING"
25050 DATA"VERZEKERING","GENEESK.BEH"
25060 DATA"LEVENSMIDD.","KLEDING"
25070 DATA"DRANK/SIG.","KR-TIJDSCHR"
25080 DATA"STUDIE","VAK/UITGAAN"
25090 DATA"HOBBY","ONVOORZIEN"
25100 DATA"TELEFOON","GAS/WAT/EL"
25110 DATA"VERVOER","AUTO"
25120 DATA"BELASTINGEN","INKOMSTEN"
25130 DATA"KRUISP.K/CH","KRUISP.K/B"
30000 REM
30010 REM HOBBYSCOOP-BASICODE2
30020 REM *                  *
30030 REM * INT.HH-REKENING  *
30040 REM *   UTILITY NO.2   *
30050 REM                    *
30060 REM P INGEZONDEN DOOR
30070 REM O                  1
30080 REM S  B.P.M.RINTJEMA  2
30090 REM T  TUINSTRAAT 13   0
30100 REM B  5802 AD VENRAY  0
30110 REM U
30120 REM S  EIGEN ONTWERP   *
30130 REM   FEB.84/ITT-2020  *
30140 REM * DEC.84/APPLE IIE *
30150 REM *                  *
30160 REM **** HILVERSUM *****
30170 REM
30180 REM COPYRIGHT  NOS - HILVERSUM

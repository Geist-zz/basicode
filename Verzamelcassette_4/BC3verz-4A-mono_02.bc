
1000 A=500:GOTO20:REM Escott's schuifpuzzel
1010 DIMC(11,5),C1(11,5),FL(10),A(9),S$(10),RI(4)
1020 SO$="636211546441522134532263642443730292811174"
1030 SO$=SO$+"412221513263545323430392731383821174044121"
1040 SO$=SO$+"31625273040304038384838444432124932212"
1050 HT=HO:VT=VE:VD=0
1060 A(0)=-4:A(1)=-2:A(2)=-1:A(3)=1:A(4)=2:A(5)=4
1070 A(6)=-2:A(7)=-1:A(8)=1:A(9)=2
1080 REM stukjes inlezen
1090 FORI=1 TO10:READS$(I):NEXTI
1100 REM speelveld voorbereiden
1110 FORVP=0 TO5:FORHP=0 TO11
1120 C(HP,VP)=-1:NEXTHP:NEXTVP
1130 REM stukjes in beginstand plaatsen
1140 SR$="344500777733550077::22110066991111006889"
1150 FORVP=1 TO4:FORHP=1 TO10
1160 C(HP,VP)=ASC(MID$(SR$,HP+10*VP-10,1))-48
1170 NEXTHP:NEXTVP
1180 REM eenheidsstappen H/V bepalen
1190 AH=INT((HG*.9375*.958-2)/10):AV=INT((VG*.958-2)/8)
1200 IFVD=0 THEN GOSUB6010:REM uitleg
1210 GOSUB600:CN=0
1220 REM teken raster
1230 FORY=1 TO1+8*AV STEPAV
1240 VE=(Y+.1)/VG
1250 FORX=1 TO1+10*AH STEPAH
1260 HO=(X+.1)/HG:GOSUB620
1270 NEXTX
1280 NEXTY
1290 REM teken buitenkader
1300 HO=0:VE=0:GOSUB620
1310 HO=(2.1+10*AH)/HG:GOSUB630
1320 VE=(2.1+8*AV)/VG:GOSUB630
1330 HO=0:GOSUB630:VE=0:GOSUB630
1340 REM teken blokken
1350 FORI=0 TO10:FL(I)=0:NEXTI:REM 0=nog niet getekend
1360 FORHP=1 TO10
1370 FORVP=1 TO4
1380 IFFL(C(HP,VP))=1 THEN1420:REM reeds getekend
1390 FL(C(HP,VP))=1
1400 IFC(HP,VP)=0 THEN1420
1410 TB=0:GOSUB4020:REM teken blok
1420 NEXTVP
1430 NEXTHP
1440 ZT=0
1450 VE=.9584:HO=0
1460 CN=1:GOSUB620:CN=0
1470 IFVD=0 THEN1580:REM niet voordoen
1480 SR=ZT:GOSUB300:SR$="Voordoen; Zet "+SR$
1490 CN=1:GOSUB650
1500 IF2*ZT<LEN(SO$) THEN1530
1510 CN=0:HO=.5:SR$="Klaar, druk toets"
1520 GOSUB650:GOSUB210:GOTO950
1530 ZT=ZT+1:ZZ$=MID$(SO$,2*ZT-1,2)
1540 SR=ZT:GOSUB300:SR$="Voordoen; Zet "+SR$
1550 CN=0:GOSUB650
1560 IN$="":BL=VAL(LEFT$(ZZ$,1)):IFBL=0 THENBL=10
1570 GOTO1640
1580 SR$="Welk blok ? ":GOSUB650
1590 GOSUB210:BL=IN-64
1600 IFIN=83 THEN950:REM stoppen
1610 IFIN=86 THENVD=1:ZT=0:GOTO1110:REM voordoen
1620 IF(BL<1)OR(BL>10) THEN GOSUB250:GOTO1590
1630 CN=1:GOSUB650
1640 FORI=0 TO3:RI(I)=0:NEXTI:BH=0
1650 FORHP=1 TO10:FORVP=1 TO4
1660 IFC(HP,VP)<>BL THEN1730:REM ander stukje
1670 IFBH=0 THENBH=HP:BV=VP
1680 REM kijk welke richtingen kunnen
1690 IF(C(HP+1,VP)<>BL)AND(C(HP+1,VP)<>0) THENRI(0)=1
1700 IF(C(HP,VP-1)<>BL)AND(C(HP,VP-1)<>0) THENRI(1)=1
1710 IF(C(HP-1,VP)<>BL)AND(C(HP-1,VP)<>0) THENRI(2)=1
1720 IF(C(HP,VP+1)<>BL)AND(C(HP,VP+1)<>0) THENRI(3)=1
1730 NEXTVP:NEXTHP
1740 CN=1:GOSUB620:CN=0
1750 SR$="Blok "+IN$+", Welke richting ":S1$="("
1760 RI(4)=0
1770 FORI=0 TO3
1780 IFRI(I)=1 THEN1830
1790 IFRI(4)>0 THENRI(4)=-1:GOTO1820
1800 REM mogelijke richting melden
1810 IFRI(4)=0 THENRI(4)=I+1
1820 SR$=SR$+S1$+MID$("ONWZ",I+1,1):S1$=","
1830 NEXTI
1840 IFVD=1 THENRI(4)=VAL(RIGHT$(ZZ$,1)):REM voordoen
1850 IFRI(4)=0 THEN GOSUB250:GOTO1450:REM kan niet
1860 IFRI(4)>0 THEN1960
1870 SR$=SR$+")":GOSUB650
1880 GOSUB210:REM vraag richting
1890 IF(IN=79)AND(RI(0))=0 THENRI(4)=1:GOTO1940:REM O
1900 IF(IN=78)AND(RI(1))=0 THENRI(4)=2:GOTO1940:REM N
1910 IF(IN=87)AND(RI(2))=0 THENRI(4)=3:GOTO1940:REM W
1920 IF(IN=90)AND(RI(3))=0 THENRI(4)=4:GOTO1940:REM Z
1930 GOSUB250:GOTO1880
1940 CN=1:GOSUB650:REM wis regel
1950 REM wis oud blok
1960 HP=BH:VP=BV:TB=1:GOSUB4020
1970 REM ga bewegen
1980 FORHP=1 TO10:FORVP=1 TO4
1990 C1(HP,VP)=C(HP,VP):NEXTVP:NEXTHP:KN=0
2000 FORHP=1 TO10:FORVP=1 TO4
2010 C(HP,VP)=C1(HP,VP):NEXTVP:NEXTHP
2020 IFRI(4)<>1 THEN2100:REM niet oost
2030 REM beweeg in oostelijke richting
2040 FORHP=11 TO2 STEP-1:FORVP=1 TO4
2050 IFC(HP-1,VP)<>BL THEN2090:REM deze niet
2060 IFC(HP,VP)=BL THEN2080:REM kan
2070 IFC(HP,VP)<>0 THENKN=1:GOTO2090:REM kan niet
2080 C1(HP,VP)=BL:C1(HP-1,VP)=0
2090 NEXTVP:NEXTHP:IFKN=0 THENBH=BH+1:GOTO2000
2100 IFRI(4)<>2 THEN2170:REM niet noord
2110 FORHP=1 TO10:FORVP=0 TO3
2120 IFC(HP,VP+1)<>BL THEN2160:REM deze niet
2130 IFC(HP,VP)=BL THEN2150
2140 IFC(HP,VP)<>0 THENKN=1:GOTO2160:REM kan niet
2150 C1(HP,VP)=BL:C1(HP,VP+1)=0
2160 NEXTVP:NEXTHP:IFKN=0 THENBV=BV-1:GOTO2000
2170 IFRI(4)<>3 THEN2240:REM niet west
2180 FORHP=0 TO9:FORVP=1 TO4
2190 IFC(HP+1,VP)<>BL THEN2230:REM deze niet
2200 IFC(HP,VP)=BL THEN2220
2210 IFC(HP,VP)<>0 THENKN=1:GOTO2230:REM kan niet
2220 C1(HP,VP)=BL:C1(HP+1,VP)=0
2230 NEXTVP:NEXTHP:IFKN=0 THENBH=BH-1:GOTO2000
2240 IFRI(4)<>4 THEN2320:REM niet zuid
2250 FORHP=1 TO10:FORVP=5 TO2 STEP-1
2260 IFC(HP,VP-1)<>BL THEN2300:REM deze niet
2270 IFC(HP,VP)=BL THEN2290
2280 IFC(HP,VP)<>0 THENKN=1:GOTO2300:REM kan niet
2290 C1(HP,VP)=BL:C1(HP,VP-1)=0
2300 NEXTVP:NEXTHP:IFKN=0 THENBV=BV+1:GOTO2000
2310 REM bewogen
2320 HP=BH:VP=BV:TB=0:GOSUB4020:REM teken nieuw
2330 GOTO1450
4000 REM SR teken of wis blok
4010 REM draw block C(HP,VP) from position HP,VP
4020 PH=2.1+(HP-1)*AH:PV=2.1+(VP-1)*2*AV
4030 HO=(PH+AH/2-4)/HG:VE=(PV+AV/2-4)/VG
4040 CN=1:GOSUB620:CN=TB:SR$=CHR$(64+C(HP,VP)):GOSUB650
4050 HO=PH/HG:VE=PV/VG:GOSUB620
4060 FORI=1 TOLEN(S$(C(HP,VP)))
4070 SR=VAL(MID$(S$(C(HP,VP)),I,1))
4080 IFI=2*INT(I/2) THEN4120:REM I=even=HP
4090 REM I=oneven=VP
4100 IFSR<6 THENPV=PV-2*SGN(A(SR))
4110 PV=PV+A(SR)*AV:GOTO4140
4120 IFSR<6 THENPH=PH-2*SGN(A(SR))
4130 PH=PH+A(SR)*AH
4140 HO=PH/HG:VE=PV/VG:GOSUB630
4150 NEXTI
4160 RETURN
6000 REM uitleg
6010 SR$="Schuifpuzzel van ESCOTT":GOSUB150
6020 PRINT:PRINT
6030 PRINT"Deze puzzel bestaat  uit een speelbord"
6040 PRINT"met daarop een aantal stukjes, die met"
6050 PRINT"de letters A tot en met J  zijn aange-"
6060 PRINT"duid.":PRINT
6070 PRINT"Er zijn drie vierkante stukjes  en zes"
6080 PRINT"L-vormige.":PRINT
6090 PRINT"Begin met de opstelling na te tekenen."
6100 GOSUB6460
6110 PRINT"Het is dan de bedoeling dat u met deze"
6120 PRINT"stukjes gaat schuiven. Daartoe geeft u"
6130 PRINT"mij telkens eerst de letter op van het"
6140 PRINT"te verschuiven stuk. Indien nodig moet"
6150 PRINT"u mij tevens opgeven in welke richting"
6160 PRINT"(N)oord, (O)ost, (Z)uid of (W)est  het"
6170 PRINT"stukje moet worden verschoven.":PRINT
6180 PRINT"Het doel van het spel is, het vierkant"
6190 PRINT"gevormd door de blokken  A en B  op de"
6200 PRINT"plaats te krijgen van de blokken G + J"
6210 PRINT"en het paar  G en J  op de oude plaats"
6220 PRINT"van A en B te krijgen.  Waar de andere"
6230 PRINT"blokken terecht komen doet er niet toe."
6240 GOSUB6460
6250 PRINT"Het kan (volgens Martin Gardner) in 63"
6260 PRINT"verplaatsingen en het is knap moeilijk"
6270 PRINT"om er uit te komen."
6280 PRINT
6290 PRINT"N.B.: Nadat dit programma was uitgezon-"
6300 PRINT"stuurde Jan de Geus, een verwoed verza-"
6310 PRINT"melaar van wiskundige puzzels,  ons een"
6320 PRINT"zeer uitgebreide reaktie waarin hij een"
6330 PRINT"oplossing beschreef die onlangs in 1986"
6340 PRINT"werd gepubliceerd door Edward Hordern."
6350 PRINT"Daarbij zijn slechts 61 zetten nodig !"
6360 PRINT"Deze oplossing is nu in dit programma"
6370 PRINT"ingebouwd om in noodgevallen voor te"
6380 PRINT"doen. Bedankt Jan!"
6390 PRINT
6400 PRINT"Als u het opgeeft, druk dan op de S en"
6410 PRINT"als u vindt  dat ik het maar voor moet"
6420 PRINT"doen, druk dan op de V."
6430 PRINT
6440 PRINT"Veel plezier!"
6450 REM spatie voor verder
6460 HO=0:VE=VT:GOSUB110
6470 SR$="<spatie> voor verder":GOSUB150
6480 GOSUB210:IFIN<>32 THEN6480
6490 GOSUB100:RETURN
25000 REM vormen en maten van de stukjes
25010 DATA"450196","4411","541762","4411","440297"
25020 DATA"536811","546910","4411","489301","4411"
25030 REM
30000 REM Edward Brind Escott publiceerde deze
30010 REM puzzel in 1938 in het toen bestaande blad
30020 REM 'Games Digest'. De bekende puzzelaar
30030 REM Martin Gardner vond dit de moeilijkste
30040 REM schuifpuzzel die hij ooit gezien had.
30050 REM Dat maakt de uitdaging alleen maar
30060 REM groter, niet waar?
30070 REM De door Hordern gepubliceerde oplossing
30080 REM staat in het boek 'SLIDING PIECE PUZZLES'
30090 REM
30100 REM #########################
30110 REM #                       #
30120 REM #  Dit programma is op  #
30130 REM #  21-12-1987 gemaakt   #
30140 REM #   en op 20-4-1988     #
30150 REM #     uitgebreid        #
30160 REM #        door:          #
30170 REM #                       #
32000 REM #  J.G.A. Haubrich      #
32010 REM #      Eindhoven        #
32020 REM #                       #
32030 REM #  (C)opyright 1987/'88 #
32040 REM #                       #
32050 REM #########################
32060 REM TROS-RADIO dd 880127

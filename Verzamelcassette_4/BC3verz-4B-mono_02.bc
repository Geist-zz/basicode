
1000 A=200:GOTO20:REM * REVERSI *
1010 N1$="* R E V E R S I *"
1020 N2$="================="
1030 AK$="abcdefgh":AL$="ABCDEFGH"
1040 S$="":P1=0:P2=0:V1$="":V2$=""
1050 DIMB$(8,8),RX(8),RY(8)
1060 B=4:P=0:S1$="o":S2$="x":H=.47:V=.35
1070 RX(1)=1:RX(2)=0:RX(3)=-1:RX(4)=-1
1080 RY(1)=1:RY(2)=1:RY(3)=1:RY(4)=0
1090 RX(5)=-1:RX(6)=0:RX(7)=1:RX(8)=1
1100 RY(5)=-1:RY(6)=-1:RY(7)=-1:RY(8)=0
1110 FORI=1 TO8:FORJ=1 TO8
1120 B$(I,J)="":NEXTJ:NEXTI
1130 B$(4,4)=S1$:B$(4,5)=S2$
1140 B$(5,5)=S1$:B$(5,4)=S2$
1150 REM * HOOFD-MENU *
1160 VE=0:HO=11:GOSUB100:GOSUB110:PRINTN1$:VE=1:GOSUB110
1170 PRINTN2$:VE=6:HO=14:GOSUB110
1180 PRINT"Hoofd-menu":VE=7:GOSUB110
1190 PRINT"==========":VE=10:GOSUB110
1200 PRINT"1 = Uitleg":VE=12:GOSUB110
1210 PRINT"2 = Het spel":VE=14:GOSUB110
1220 PRINT"3 = Stoppen":VE=23:HO=28:GOSUB110
1230 PRINT"Uw keuze";
1240 GOSUB210:W=VAL(IN$):IF(W<1)OR(W>3) THEN1240
1250 ONW GOTO9010,1510,1260
1260 GOSUB100:GOTO950
1500 REM * HET SPEL *
1510 GOSUB100:HO=2:VE=8:GOSUB110:PRINT"Naam van speler 1";
1520 INPUTP1$
1530 VE=10:GOSUB110:PRINT"Naam van speler 2";:INPUTP2$
1540 VE=12:GOSUB110:PRINTP1$;" moet de kruisjes insluiten"
1550 VE=14:GOSUB110:PRINTP2$;" moet de nulletjes insluiten"
1560 VE=22:HO=30:GOSUB110:PRINT"<Toets>"
1570 GOSUB210:IFIN$="" THEN1570
1580 GOSUB3010:REM * TEKEN BORD *
1590 GOSUB7510:REM * PRINT DE STAND *
1600 S$=S1$:GOSUB3510
1610 IFF<>3 THENP=0:GOSUB7510:B=B+1
1620 IFF=3 THENP=P+1
1630 IF(B=64)OR(P=2)OR(S1=0)OR(S2=0) THEN2010
1640 S$=S2$:GOSUB3510
1650 IFF<>3 THENP=0:GOSUB7510:B=B+1
1660 IFF=3 THENP=P+1
1670 IF(B=64)OR(P=2)OR(S1=0)OR(S2=0) THEN2010
1680 GOTO1600
2000 REM * EINDE SPEL *
2010 HO=H:VE=V:IFS2=S1 THEN2060
2020 IFS1>S2 THENSR$=LEFT$(P1$,10)
2030 IFS2>S1 THENSR$=LEFT$(P2$,10)
2040 GOSUB650:HO=.47+(LEN(SR$)+1)*.025
2050 SR$=" wint!!":GOSUB650:GOTO2070
2060 SR$="Gelijk spel, allebei":GOSUB650
2070 HO=H:VE=.45:SR$="   GEFELICITEERD !":GOSUB650
2080 HO=H:VE=.55:SR$="Nog een spel  <j/n>":GOSUB650
2090 GOSUB250:GOSUB210
2100 IFIN$="j" THEN1000
2110 GOSUB100:HO=13:VE=10:GOSUB110:PRINT"TOT ZIENS!!!"
2120 GOTO950
2500 HO=H:VE=.45
2510 SR$="Druk een toets in...":GOSUB650
2520 IFCN=1 THENCN=0:RETURN
2530 GOSUB250:GOSUB210:IN$="":RETURN
2540 HO=0:VE=23:GOSUB110
2550 PRINT"Druk een <toets> in...";
2560 REM GOSUB250
2570 GOSUB210:PRINT:RETURN
3000 REM * TEKEN SPEELVELD *
3010 GOSUB100:GOSUB600:CN=0:C=.045
3020 FORHO=.05 TO(8*C+.051) STEPC
3030 VE=.2:GOSUB620:VE=.68:GOSUB630
3040 NEXTHO:C=.06
3050 FORVE=.2 TO(8*C+.201) STEPC
3060 HO=.05:GOSUB620:HO=.41:GOSUB630
3070 NEXTVE
3080 SR$=N1$:HO=.25:VE=.04:GOSUB650
3090 SR$=N2$:VE=.08:GOSUB650
3100 A$="87654321":A=1:C=.06
3110 HO=0:FORVE=.22 TO(8*C+.22) STEPC
3120 SR$=MID$(A$,A,1):GOSUB650:A=A+1:NEXTVE
3130 A=1:C=.045
3140 VE=.7:FORHO=.06 TO(9*C+.7) STEPC
3150 SR$=MID$(AK$,A,1):GOSUB650:A=A+1:NEXTHO
3160 SR$="Uw zet   Stand":HO=.63:VE=.75:GOSUB650
3170 VE=VE+.07:HO=.35
3180 SR$=LEFT$(P1$,10):GOSUB650:VE=VE+.07
3190 SR$=LEFT$(P2$,10):GOSUB650:RETURN
3500 REM * VRAAG ZET *
3510 F=1:CN=0:Q=0
3520 GOSUB3720:GOSUB6300
3530 GOSUB4500:CN=1:GOSUB3720:CN=0:GOSUB4960
3540 IF(IN$="P")OR(IN$="p") THENF=3:GOTO5010
3550 GOSUB4110:REM * IF <A OR >H *
3560 IFF=2 THENCN=1:GOSUB4960:GOSUB4800:GOTO3510
3570 REM
3580 F=1:Q=0:CN=0
3590 CN=1:GOSUB4960:CN=0:GOSUB3720:GOSUB6400
3600 GOSUB4560:CN=1:GOSUB3720:CN=0:GOSUB4960
3610 GOSUB4210:REM * IF <1 OR >8 *
3620 IFF=2 THENCN=1:GOSUB4960:GOSUB4900:GOTO3580
3630 B2=VAL(IN$)
3640 IFB$(B1,B2)<>"" THENCN=1:GOSUB4960:GOSUB4010
3650 IFB$(B1,B2)<>"" THEN GOSUB3720:GOTO3510
3660 GOSUB6010:REM * IS ZET LEGAAL *
3670 IFZZ=0 THENCN=1:GOSUB4960:GOSUB7000:GOSUB7110
3680 IFZZ=0 THEN GOSUB4310:GOTO3510
3690 IFF<>3 THENB$(B1,B2)=S$
3700 CN=1:GOSUB4960:CN=0:RETURN
3710 REM * MEDEDELING SPELER *
3720 Q1$=" (o)":Q2$=" (x)"
3730 HO=.47:VE=.35:SR$=LEFT$(P1$,10)+Q1$
3740 IFS$=S2$ THENSR$=LEFT$(P2$,10)+Q2$
3750 GOSUB650
3760 HO=.47+(LEN(SR$)+1)*.025
3770 SR$=" zet!":GOSUB650
3780 RETURN
4000 REM * VAK BEZET *
4010 GOSUB6500
4020 SR$="Dit vak is al bezet!":GOSUB650
4030 IFQ=2 THEN4050
4040 SD=8:GOSUB450:IFQ=1 THEN4010
4050 GOSUB7000:GOSUB7110:Q=0:CN=0:RETURN
4100 REM * TUSSEN A EN H *
4110 B1=0
4120 FORI=1 TO8
4130 IF(MID$(AK$,I,1)=IN$)OR(MID$(AL$,I,1)=IN$) THENB1=I
4140 NEXTI
4150 IFB1=0 THENF=2:RETURN
4160 F=1:RETURN
4200 REM * TUSSEN 1 EN 8 *
4210 IF(VAL(IN$)<1)OR(VAL(IN$)>8) THENF=2:RETURN
4220 F=1:RETURN
4300 REM * ONJUISTE ZET *
4310 GOSUB6500
4320 SR$="Onjuiste zet!!":GOSUB650
4330 IFQ=2 THEN4350
4340 SD=8:GOSUB450:IFQ=1 THEN4310
4350 IFS$=S1$ THENSR$=W3$:VE=.82
4360 IFS$=S2$ THENSR$=V3$:VE=.89
4370 GOSUB7230:CN=0:Q=0:RETURN
4500 HO=.7:VE=.82:IFS$=S2$ THENVE=.89
4510 GOSUB210:CN=1:GOSUB6300
4520 SR$=IN$:CN=0:GOSUB650
4530 IFS$=S1$ THENW1$=IN$
4540 IFS$=S2$ THENV1$=IN$
4550 RETURN
4560 HO=.727:VE=.82:IFS$=S2$ THENVE=.89
4570 GOSUB210:CN=1:GOSUB6400:HO=.727
4580 SR$=IN$:CN=0:GOSUB650
4590 IFS$=S1$ THENW2$=IN$
4600 IFS$=S2$ THENV2$=IN$
4610 W3$=W1$+W2$:V3$=V1$+V2$
4620 RETURN
4800 GOSUB6500
4810 SR$="Kolom van a tot h !!":GOSUB650
4820 IFQ=2 THEN4840
4830 SD=8:GOSUB450:IFQ=1 THEN4800
4840 GOSUB7000:Q=0:CN=0:RETURN
4900 GOSUB6500
4910 SR$="Rij van 1 tot 8 !!":GOSUB650
4920 IFQ=2 THEN4940
4930 SD=8:GOSUB450:IFQ=1 THEN4900
4940 GOSUB7110:Q=0:CN=0:RETURN
4950 REM
4960 HO=H:VE=V
4970 SR$="Ogenblikje a.u.b. ...":GOSUB650:RETURN
5000 REM * PASCONTROLE *
5010 Z=0
5020 FORK=1 TO8
5030 FORL=1 TO8
5040 IFZ=1 THEN5080
5050 B1=K:B2=L
5060 IFB$(B1,B2)="" THEN GOSUB6010
5070 IFZ=1 THENBX=B1:BY=B2
5080 NEXTL
5090 NEXTK
5100 IFZ=1 THEN5200
5110 IFZ=0 THEN5300
5120 GOTO3690
5200 F=1:CN=1:GOSUB4960
5210 GOSUB6500
5220 SR$="Onterecht gepast!!":GOSUB650:GOSUB5510
5230 GOSUB2500:IFQ=1 THEN5210
5240 GOSUB7000:GOTO3510
5300 F=3:CN=1:GOSUB4960
5310 GOSUB6500
5320 SR$="Terecht gepast!!":GOSUB650
5330 GOSUB2500:IFQ=1 THEN5310
5340 GOSUB7000:CN=0:Q=0:GOTO5120
5500 REM * MOGELIJKE ZET *
5510 HO=H:VE=.25:SR$="Goede zet b.v.:":GOSUB650
5520 HO=.9:SR$=MID$(AK$,BX,1):GOSUB650
5530 SR=BY:GOSUB300:HO=.925:GOSUB650
5540 RETURN
6000 REM * ZETCONTROLE *
6010 Z=0:ZZ=0:FORI=1 TO8
6020 T=0:IF(F=3)AND(Z=1) THEN6190
6030 X=B1+RX(I):Y=B2+RY(I)
6040 T=T+1
6050 IF(X<1)OR(X>8)OR(Y<1)OR(Y>8) THEN6180
6060 IF(B$(X,Y)="")OR(B$(X,Y)=S$) THEN6180
6070 X=X+RX(I):Y=Y+RY(I)
6080 T=T+1
6090 IF(X<1)OR(X>8)OR(Y<1)OR(Y>8) THEN6180
6100 IF(B$(X,Y)<>"")AND(B$(X,Y)<>S$) THEN6070
6110 IFB$(X,Y)="" THEN6180
6120 Z=1
6130 IFF=3 THEN6190
6140 FORJ=1 TO(T-1)
6150 X=X-RX(I):Y=Y-RY(I)
6160 B$(X,Y)=S$
6170 NEXTJ:ZZ=1:GOTO6190
6180 Z=0
6190 NEXTI
6200 RETURN
6300 HO=.7:VE=.82:IFS$=S2$ THENVE=.89
6310 SR$="?":GOTO6420
6400 HO=.7:VE=.82:IFS$=S2$ THENVE=.89
6410 SR$=" ":GOTO6420
6420 GOSUB650:SR$="?":HO=.727:GOSUB650:HO=.7:CN=0:RETURN
6500 HO=H:VE=V:CN=0:Q=Q+1
6510 IFQ=2 THENCN=1:RETURN
6520 RETURN
7000 CN=1:HO=.7:VE=.82:SR$=W1$
7010 IFS$=S2$ THENVE=.89:SR$=V1$
7020 GOSUB650:CN=0:RETURN
7100 REM
7110 CN=1:HO=.727:VE=.82:SR$=W2$
7120 IFS$=S2$ THENVE=.89:SR$=V2$
7130 GOSUB650:CN=0:RETURN
7140 REM
7150 CN=1:HO=.925:VE=.82:SR=P1:GOSUB300:GOSUB650
7160 VE=.89:SR=P2:GOSUB300:GOSUB650:CN=0:RETURN
7170 REM
7200 REM * WISSEN STAND *
7210 IFS$=S1$ THENVE=.89:SR$=V3$
7220 IFS$=S2$ THENVE=.82:SR$=W3$
7230 SQ$=SR$:SR$=LEFT$(SQ$,1):HO=.7:CN=1:GOSUB650
7240 HO=.727:SR$=RIGHT$(SQ$,1):GOSUB650:CN=0:RETURN
7500 REM * ZETTEN+STAND *
7510 S1=0:S2=0
7520 FORI=1 TO8:FORJ=1 TO8:C=.045:C1=.06
7530 HO=.061+((I-1)*C):VE=.63-((J-1)*C1)
7540 IFB$(I,J)="" THEN7620
7550 IF(S$="o")AND(B$(I,J)="o") THENSR$="x":CN=1:GOSUB650
7560 IF(S$="o")AND(B$(I,J)="o") THENCN=0:GOTO7590
7570 IF(S$="x")AND(B$(I,J)="x") THENSR$="o":CN=1:GOSUB650
7580 IF(S$="x")AND(B$(I,J)="x") THENCN=0:GOTO7590
7590 SR$=B$(I,J):GOSUB650
7600 IFSR$=S1$ THENS1=S1+1
7610 IFSR$=S2$ THENS2=S2+1
7620 NEXTJ:NEXTI:GOSUB7150
7630 HO=.925:VE=.82:SR=S1:GOSUB300:GOSUB650
7640 VE=.89:SR=S2:GOSUB300:GOSUB650
7650 P1=S1:P2=S2:P3=P3+1
7660 IFP3>=3 THEN GOSUB7210
7670 RETURN
9000 REM * UITLEG *
9010 GOSUB100:VE=0:HO=14:GOSUB110
9020 PRINT"* REVERSI *":VE=VE+1:GOSUB110
9030 PRINT"===========":HO=0:VE=2:GOSUB110
9040 PRINT"Uitleg.":VE=VE+1:GOSUB110
9050 PRINT"=======":HO=0:VE=6:GOSUB110
9060 PRINT"Dit is de computer-versie van het"
9070 PRINT"beroemde spel Reversi."
9080 PRINT
9090 PRINT"Er wordt gespeeld op een veld met 64"
9100 PRINT"vakjes. Bij het begin van het spel"
9110 PRINT"bezitten beide spelers twee vakjes."
9120 PRINT
9130 PRINT"Het gaat erom zoveel mogelijk vakjes"
9140 PRINT"te 'veroveren'."
9150 GOSUB2540:GOSUB100:VE=2:GOSUB110
9160 PRINT"Insluiten / Veroveren.":VE=3:GOSUB110
9170 PRINT"======================":VE=6:GOSUB110
9180 PRINT"Om beurten bezetten de spelers een leeg"
9190 PRINT"vakje en wel zo dat hierdoor een of"
9200 PRINT"meer, in een rechte lijn, aansluitende"
9210 PRINT"vakjes van de ander worden ingesloten."
9220 PRINT
9230 PRINT"De ingesloten vakjes verwisselen nu van"
9240 PRINT"eigenaar.(N.B.:Een vakje kan dus meer-"
9250 PRINT"dere keren van eigenaar wisselen!)."
9260 PRINT
9270 PRINT"Insluiten kan in alle richtingen,"
9280 PRINT"ook tegelijkertijd, gebeuren :"
9290 PRINT"vertikaal, horizontaal en diagonaal."
9300 PRINT"Een vakje bezetten wordt gedaan door"
9310 PRINT"een coordinaat in te typen bv.:'a1'."
9320 GOSUB2540:GOSUB100:VE=2:HO=0:GOSUB110
9330 PRINT"Passen.":VE=VE+1:GOSUB110
9340 PRINT"=======":VE=6:GOSUB110
9350 PRINT"Als een speler geen vakje kan bezetten"
9360 PRINT"waarbij een of meer vakjes van de"
9370 PRINT"tegenspeler worden ingesloten dan moet"
9380 PRINT"worden gepast. Dit kan door 'p' in te"
9390 PRINT"typen. De computer controleert dan of"
9400 PRINT"dit terecht wordt gedaan of niet."
9410 PRINT"Als terecht wordt gepast dan komt de"
9420 PRINT"andere speler aan de beurt."
9430 PRINT"Als onterecht wordt gepast dan wordt"
9440 PRINT"aangegeven welke zet toch mogelijk is,"
9450 PRINT"wat echter niet de meest profijtelijke"
9460 PRINT"hoeft te zijn. Het zal duidelijk zijn"
9470 PRINT"dat deze mogelijkheid kan helpen op een"
9480 PRINT"moment dat de speler niet direkt ziet"
9490 PRINT"wat een goede zet is."
9500 GOSUB2540:GOSUB100:VE=2:HO=0:GOSUB110
9510 PRINT"Einde spel.":VE=VE+1:GOSUB110
9520 PRINT"===========":VE=6:GOSUB110
9530 PRINT"Het spel is uit als alle vakjes bezet"
9540 PRINT"zijn of wanneer bij gebrek aan"
9550 PRINT"mogelijkheden tot insluiten geen vakje"
9560 PRINT"meer bezet kan worden en beide spelers"
9570 PRINT"dus moeten passen."
9580 PRINT
9590 PRINT"Wie de meeste vakjes bezet is winnaar !"
9600 PRINT
9610 PRINT"Succes !"
9620 PRINT
9630 GOSUB2540:GOTO1160
32000 REM
32010 REM
32020 REM ************************
32030 REM *                      *
32040 REM *     * REVERSI *      *
32050 REM *                      *
32060 REM * Een programma van:   *
32070 REM *                      *
32080 REM * J.S. ROSMAN          *
32090 REM * Ch.Th.STORKSTRAAT 88 *
32100 REM * 3119 VE  SCHIEDAM    *
32110 REM *                      *
32120 REM * Versie 2 : Feb 1987  *
32130 REM *                      *
32140 REM * Aangepast aan        *
32150 REM * B A S I C O D E - 3  *
32160 REM * door:                *
32170 REM *                      *
32180 REM *   P.A.KESTELOO       *
32190 REM *   REEDE 34           *
32200 REM *   3232 CV  BRIELLE   *
32210 REM *   Tel. 01810-15569   *
32220 REM *                      *
32230 REM *     14 Nov 1987      *
32240 REM *     COMMODORE 64     *
32250 REM *                      *
32260 REM ************************
32270 REM
32280 REM TROS-RADIO dd 880518
32290 REM incl.corr. dd 880922

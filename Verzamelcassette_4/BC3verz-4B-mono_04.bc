
1000 A=500:GOTO20:REM PARABOOLONDERZOEK
1010 REM*CONSTANTEN INITIEREN*
1020 DIMA(3),V$(3),A$(6),I$(3)
1030 R2$="":R3$="":R4$=""
1040 REM MAX/MIN X-AS
1050 MX=10:SR=MX:GOSUB300:MX$=SR$
1060 XM=-10:SR=XM:GOSUB300:XM$=SR$
1070 REM OORSPRONG
1080 X0=144:Y0=75:XA=288:YA=200
1090 REM MAX/MIN Y-AS
1100 MY=50:SR=MY:GOSUB300:MY$=SR$
1110 YM=-50:SR=YM:GOSUB300:YM$=SR$
1120 REM VERDELINGSFACTOR Y-AS
1130 SY=2*Y0/ABS(YM-MY)
1140 REM VERDELINGSFACTOR X-AS
1150 SX=2*X0/ABS(XM-MX)
1160 V$(1)=" a wordt:":V$(2)=" b wordt:":V$(3)=" c wordt:"
1170 REM TITELPAGINA AANROEPEN
1180 GOSUB5010
1190 REM*PROGRAMMA TEKST*
1200 REM SCHERM SCHOON
1210 GOSUB100
1220 PRINT"parabool - variabelen - onderzoek"
1230 PRINT:PRINT
1240 P$="parabool : f(x) = a.x^2 + b.x + c"
1250 PRINTP$
1260 PRINT:PRINT"met dit programma kun je gaan onderzoe-"
1270 PRINT"ken welke gevolgen het veranderen van "
1280 PRINT"de variabelen a,b en c hebben voor de "
1290 PRINT"vorm van de grafiek van de functie."
1300 PRINT"je begint met het invoeren van de waar-"
1310 PRINT"den a,b en c van de beginparabool."
1320 PRINT"je kunt daarna deze waarden veranderen"
1330 PRINT"en de grafiek opnieuw laten tekenen."
1340 PRINT"de grafiek wordt steeds voor het inter-"
1350 PRINT"val -10 tot 10 getekend."
1360 PRINT"veel succes met je onderzoek!"
1370 PRINT:PRINT"doorgaan <return>"
1380 GOSUB210:IFIN$<>CHR$(13) THEN1380
1390 REM BEGININVOER
1400 REM SCHERM SCHOON
1410 GOSUB100
1420 PRINTP$:PRINT
1430 PRINT"welke waarde krijgt a ";:INPUTA0:PRINT
1440 SR=A0:GOSUB300:A0$=SR$
1450 PRINT"welke waarde krijgt b ";:INPUTB0:PRINT
1460 SR=B0:GOSUB300:B0$=SR$
1470 PRINT"welke waarde krijgt c ";:INPUTC0:PRINT
1480 SR=C0:GOSUB300:C0$=SR$
1490 PRINT:PRINT:PRINT"doorgaan <return>"
1500 GOSUB210:IFIN<>13 THEN1500
1510 REM*HOOFDPROGRAMMA*
1520 REM GRAFISCH BEDRIJF
1530 GOSUB600
1540 REM*X-AS+VERDELING*
1550 CN=0:HO=0:VE=Y0/YA:GOSUB620
1560 HO=(2*X0-1)/XA:GOSUB630
1570 FORX=0 TO2*X0 STEPSX
1580 CN=0:HO=X/XA:VE=(Y0-2)/YA:GOSUB620
1590 VE=(Y0+2)/YA:GOSUB630
1600 NEXTX
1610 REM TEKST X-AS
1620 SR$="x":HO=(2*X0-8)/XA:VE=(Y0+5)/YA:GOSUB650
1630 REM*Y-AS+VERDELING*
1640 CN=0:HO=X0/XA:VE=0:GOSUB620
1650 VE=2*Y0/YA:GOSUB630
1660 FORY=0 TO2*Y0 STEP10*SY
1670 CN=0:HO=(X0-2)/XA:VE=Y/YA:GOSUB620
1680 HO=(X0+2)/XA:GOSUB630
1690 NEXTY
1700 SR$="y":HO=(X0-8)/XA:VE=0:GOSUB650
1710 SR$=MY$:HO=X0/XA:VE=0:GOSUB650
1720 SR$=YM$:HO=X0/XA:VE=2*Y0/YA:GOSUB650
1730 REM*BEGINTEKST+PARABOOL*
1740 A$(0)=A0$+" x^2 + "+B0$+" x +"+C0$
1750 A$(0)="begin "+A$(0)
1760 CN=0:SR$=A$(0):HO=0:VE=160/YA:GOSUB650
1770 A=A0:B=B0:C=C0
1780 REM FUNCTIE-SUBROUTINE AANROEPEN
1790 GOSUB2010
1800 REM KEUZE-SUBROUTINE SPRINGEN
1810 GOTO3010
2000 REM FUNCTIE-SUBROUTINE
2010 X1=0:Y1=A*-10*-10+B*-10+C
2020 IFY1>MY THENY1=MY
2030 IFY1<YM THENY1=YM
2040 Y1=Y0-Y1*SY
2050 FORX=XM+1 TOMX
2060 Y=(A*X+B)*X+C
2070 IFY>MY THENY=MY
2080 IFY<YM THENY=YM
2090 X2=X0+X*SX
2100 Y2=Y0-Y*SY:IF(Y1=0)OR(Y1=150) THENY1=Y2:X1=X2
2110 REM LIJN TEKENEN
2120 CN=0:HO=(X1-1)/XA:VE=Y1/YA:IFHO<0 THENHO=0
2130 GOSUB620
2140 HO=(X2-1)/XA:VE=Y2/YA:GOSUB630
2150 X1=X2:Y1=Y2
2160 NEXTX
2170 RETURN
3000 REM KEUZE-SUBROUTINE
3010 CN=1:SR$=R3$:HO=0:VE=180/YA:GOSUB650
3020 CN=1:SR$=R4$:HO=0:VE=190/YA:GOSUB650
3030 R3$="(v)eranderen"
3040 CN=0:SR$=R3$:VE=180/YA:GOSUB650
3050 R4$="(s)toppen":
3060 CN=0:SR$=R4$:VE=190/YA:GOSUB650
3070 GOSUB210
3080 IF(IN<>83)AND(IN<>86) THEN3070
3090 IFIN=86 THEN4010:REM V
3100 GOSUB100:PRINT"andere beginparabool (j/n)"
3110 GOSUB210
3120 IF(IN<>74)AND(IN<>78) THEN3110
3130 IFIN=74 THEN1410:REM J
3140 GOTO950
4000 REM VERANDEREN SUBROUTINE
4010 CN=1:SR$=R3$:HO=0:VE=180/YA:GOSUB650
4020 CN=1:SR$=R4$:HO=0:VE=190/YA:GOSUB650
4030 CN=1:SR$=R2$:HO=0:VE=170/YA:GOSUB650
4040 R2$="f(x)= a.x^2 + b.x + c"
4050 CN=0:SR$=R2$:HO=0:VE=170/YA:GOSUB650
4060 FORV=1 TO3
4070 R3$=V$(V):CN=0
4080 SR$=R3$:HO=0:VE=180/YA:GOSUB650
4090 GOSUB210
4100 IFIN=13 THEN4120
4110 I$(V)=I$(V)+IN$
4120 A(V)=VAL(I$(V))
4130 R5$=I$(V)
4140 CN=0:SR$=R5$:HO=90/XA:VE=180/YA:GOSUB650
4150 IFIN<>13 THEN4090
4160 REM TEKST WISSEN
4170 CN=1:SR$=R3$:HO=0:VE=180/YA:GOSUB650
4180 CN=1:SR$=R5$:HO=90/XA:VE=180/YA:GOSUB650
4190 NEXTV
4200 CN=1:SR$=R2$:HO=0:VE=170/YA:GOSUB650
4210 R2$=I$(1)+" x^2 + "+I$(2)+" x + "+I$(3)
4220 CN=0:SR$=R2$:HO=0:VE=170/YA:GOSUB650
4230 A=A(1):B=A(2):C=A(3)
4240 REM FUNCTIE SUBROUTINE AANROEPEN
4250 GOSUB2010
4260 FORI=1 TO3:I$(I)="":NEXTI
4270 GOTO3010:REM NIEUWE KEUZE
5000 REM TITELPAGINA SUBROUTINE
5010 A$(1)=" w i s k u n d e "
5020 A$(2)=" paraboolonderzoek"
5030 A$(3)=" door: c.brokken"
5040 A$(4)=" wijk bij duurstede"
5050 A$(5)=" 03435 - 75030"
5060 A$(6)=" doorgaan < return >"
5070 REM GRAFISCH BEDRIJF
5080 GOSUB600
5090 FORI=1 TO6
5100 CN=0:HO=0:VE=I*20/YA:SR$=A$(I)
5110 GOSUB650
5120 NEXTI
5130 GOSUB210:IFIN=0 THEN5130
5140 RETURN
32000 REM*PARABOOL ONDERZOEK *
32010 REM*DOOR C.BROKKEN*
32020 REM*ANTONIUS MAVO WIJK BIJ DUURSTEDE*
32030 REM*TEL. 03435-75030
32040 REM*VOOR BASICODE-3 AANGEPAST*
32050 REM*OP 8-4-1988*
32060 REM
32070 REM TROS-RADIO dd 880622

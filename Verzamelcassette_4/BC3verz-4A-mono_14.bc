
1000 A=500:GOTO20:REM"10-VINGER-TYPEN"
1010 H$="1 2 3 4 5 6 7 8 9 0 - ^ \"
1020 I$=" Q W E R T Y U I O P [ ]"
1030 J$="  A S D F G H J K L ; ' ` RT"
1040 K$="SH Z X C V B N M , . / SH"
1050 L$="     ==============="
1060 M$="  A S D F     J K L ;"
1070 N$="  A S D F-G H-J K L ;"
1080 NO$=" Q W E R     U I O P"
1090 P$=" Q W E R-T Y-U I O P"
1100 Q$="  2 3 4 5     8 9 0 -"
1110 R$="1-2 3 4 5-6 7-8 9 0 -"
1120 S$="   Z X C V     M , . /"
1130 T$="   Z X C V-B N-M , . /"
1140 U$="SH-Z X C V-B N-M , . /-SH"
1150 V$="  A S D F-G H-J K L ; : ] RT"
1160 X$="BN2345890167=:()+-$+/<>.,;?"
1170 W$="ASDFJKLGHQWERUIOPTYZXCVM"+X$
1180 GOSUB100:GOSUB4590
1190 GOSUB210
1200 IF(IN<49)OR(IN>57) THEN1190
1210 IN=IN-48
1220 ONIN GOTO1240,1800,2250,2630,2940,3420,3670,3860,4040
1230 REM === LES 1===
1240 A$=H$:B$=I$:C$=J$:D$=K$:E$=L$
1250 TK=9:Z$="1.1":GOSUB4490
1260 A1$="LEER BLIND TYPEN MET 10 VINGERS!"
1270 A2$="Het is de moeite waard, want het"
1280 A3$="maakt het  typen  gemakkelijker."
1290 A4$="":A5$="":A6$="":A7$=""
1300 GOSUB4240:GOSUB4540
1310 Z$="1.2":GOSUB4490
1320 A1$="Het principe is heel eenvoudig:"
1330 A2$="Elke  vinger  krijgt een  vaste"
1340 A3$="plaats  op  het  toetsenbord en"
1350 A4$="van  daar uit  vinden  we  elke"
1360 A5$="toets zonder te kijken!"
1370 GOSUB4240:GOSUB4540
1380 Z$="1.3":GOSUB4490
1390 A1$="Voor sukses is maar een ding nodig"
1400 A2$="OEFENEN, want oefening baart kunst"
1410 A3$="DAAR GAAN WE !":A4$="":A5$=""
1420 GOSUB4240:GOSUB4540
1430 A$="":B$="":C$=M$:D$="":E$=L$
1440 Z$="1.4":GOSUB4490
1450 A1$="De vingers van de  LINKER HAND"
1460 A2$="staan losjes op de  A S D en F"
1470 A3$="De vingers van de RECHTER HAND"
1480 A4$="staan losjes op de  J K L en ;"
1490 A5$="De DUIMEN boven de  SPATIEBALK"
1500 A6$=">> DIT ZIJN DE RUST-TOETSEN <<"
1510 A7$=">> NU EN ALTIJD: ONTHOUDEN! <<"
1520 GOSUB4240:GOSUB4540
1530 Z$="1.5":GOSUB4490
1540 EO$="FJ FJ JF FF JJ FJFJ JFJF"
1550 GOSUB4290
1560 EO$="AF AS DAS DAAS JA KAS ALS"
1570 GOSUB4290
1580 EO$="KASSA LAS KLAAS SLAK JAS"
1590 GOSUB4290
1600 EO$="KAAS SLA JAJA KAAL DAL"
1610 GOSUB4290
1620 Z$="1.6":C$=N$:GOSUB4490
1630 A1$="We nemen nu de G en de H er bij"
1640 A2$="De LINKER WIJSVINGER  doet F en G"
1650 A3$=">> De rust-toets BLIJFT F <<"
1660 A4$="De RECHTER  WIJSVINGER doet H en J"
1670 A5$=">> De rust-toets BLIJFT J <<"
1680 A6$="":A7$="":GOSUB4240:GOSUB4540
1690 Z$="1.7":GOSUB4490
1700 EO$="GH HG FGHJ HJ FJ GH FJHG"
1710 GOSUB4290
1720 EO$="HAAK GLAD GAS AFDAK HAL"
1730 GOSUB4290
1740 EO$="GAAF LAAG HAF HALS ALG"
1750 GOSUB4290
1760 EO$="JAAG HALF LAKS GAL HAAG"
1770 GOSUB4290
1780 Z$="1.8":GOSUB4490:GOTO4060
1790 REM === LES 2 ===
1800 A$="":B$=NO$:C$=N$:D$="":E$=L$
1810 Z$="2.1":TK=19:GOSUB4490
1820 A1$="We nemen links en rechts vier"
1830 A2$="nieuwe letters er bij."
1840 A3$="Ze worden getikt met de vingers"
1850 A4$="die er rechts-onder staan."
1860 A5$="Die vingers mogen daarvoor telkens"
1870 A6$="EVEN van hun rust-toets komen,"
1880 A7$="ze moeten daarna METEEN weer terug!"
1890 GOSUB4240:GOSUB4540
1900 Z$="2.2":GOSUB4490
1910 EO$="HIJ WAS HIER EERDER"
1920 GOSUB4290
1930 EO$="IK WAS DAAR DE DERDE DAG"
1940 GOSUB4290
1950 EO$="LEO WAS ER OOK AL"
1960 GOSUB4290
1970 EO$="DE POELIER WAS HEEL AARDIG"
1980 GOSUB4290
1990 EO$="DE KIP LIEP HARD WEG"
2000 GOSUB4290
2010 EO$="IK KOOP ER OOK DRIE"
2020 GOSUB4290
2030 Z$="2.3":B$=P$:GOSUB4490
2040 A1$="De wijsvingers krijgen er elk nog"
2050 A2$="een letter bij:"
2060 A3$="De LINKER WIJSVINGER neemt de T"
2070 A4$="De RECHTER WIJSVINGER neemt de Y"
2080 A5$="Denk rustig na en maak geen haast,"
2090 A6$="de snelheid komt later wel!"
2100 A7$="KIJK NIET NAAR DE VINGERS !"
2110 GOSUB4240:GOSUB4540
2120 Z$="2.4":GOSUB4490
2130 EO$="HET IS NIET DE EERSTE KEER"
2140 GOSUB4290
2150 EO$="HIJ HEEFT DAT WEG GEHAALD"
2160 GOSUB4290
2170 EO$="IK WEET WELK TYPE HIJ HAD"
2180 GOSUB4290
2190 EO$="AQUA DESTILLATA IS WATER"
2200 GOSUB4290
2210 EO$="DE POLITIEAUTO REED ERG HARD"
2220 GOSUB4290
2230 Z$="2.5":GOSUB4490:GOTO4060
2240 REM=== LES 3 ===
2250 A$="":B$=P$:C$=N$:D$=S$:E$=L$
2260 TK=26:Z$="3.1":GOSUB4490:GOSUB4490
2270 A1$="We nemen nu de onderste toetsen"
2280 A2$="erbij, eerst weer twee maal vier."
2290 A3$="Die worden getikt met de vingers"
2300 A4$="die er links-boven staan."
2310 A5$="DIE VINGERS BLIJVEN OP HUN PLAATS"
2320 A6$="Ze mogen er alleen EVEN af om een"
2330 A7$="letter te tikken."
2340 GOSUB4240:GOSUB4540
2350 Z$="3.2":GOSUB4490
2360 EO$="ZE WAS ECHT MOE GEWEEST"
2370 GOSUB4290
2380 EO$="HIJ VAART MEE OP DAT SCHIP"
2390 GOSUB4290
2400 EO$="IK MAAK HET EXTRA MOOI"
2410 GOSUB4290
2420 EO$="LACH JIJ MAAR LEKKER HOOR"
2430 GOSUB4290
2440 Z$="3.3":D$=T$:GOSUB4490
2450 A1$="De wijsvingers krijgen weer elk"
2460 A2$="een letter erbij."
2470 A3$="De LINKER  WIJSVINGER krijgt de B"
2480 A4$="De RECHTER WIJSVINGER krijgt de N"
2490 A5$="De RUST-TOETSEN VERANDEREN NIET!"
2500 A6$="Nu hebben we alle letters!"
2510 A7$="":GOSUB4240:GOSUB4540
2520 Z$="3.4":GOSUB4490
2530 EO$="JE MOET NOG VEEL DOEN"
2540 GOSUB4290
2550 EO$="ZO ZIE JE MAAR WEER"
2560 GOSUB4290
2570 EO$="OEFENING BAART KUNST"
2580 GOSUB4290
2590 EO$="WAT EEN EXQUISE LEKKERNIJ"
2600 GOSUB4290
2610 Z$="3.5":GOSUB4490:GOTO4060
2620 REM=== LES 4 ===
2630 A$=Q$:B$=P$:C$=N$:D$=T$:E$=L$
2640 Z$="4.1":TK=36:GOSUB4490
2650 A1$="We nemen nu ook CIJFERS"
2660 A2$="De LINKERHAND krijgt 2,3,4 en 5"
2670 A3$="De RECHTERHAND krijgt 8,9,0 en -"
2680 A4$="We letten nog even niet op de"
2690 A5$="tekens die boven de cijfers staan."
2700 A6$="":A7$="":GOSUB4240:GOSUB4540
2710 Z$="4.2":GOSUB4490
2720 EO$="2 3 4 5 8 9 0"
2730 GOSUB4290
2740 EO$="9 2 8 5 3 4 8 0"
2750 GOSUB4290
2760 A$=R$:Z$="4.3":GOSUB4490
2770 A1$="We nemen nu alle cijfers."
2780 A2$="De LINKER PINK doet de 1 erbij."
2790 A3$="De LINKER WIJSVINGER doet de 6 erbij."
2800 A4$="De RECHTER WIJSVINGER doet de 7 erbij."
2810 A5$="De RUST-TOETSEN VERANDEREN NIET!"
2820 GOSUB4240:GOSUB4540
2830 Z$="4.4":GOSUB4490
2840 EO$="1234567890 0987654321"
2850 GOSUB4290
2860 EO$="IK TIK 26 LETTERS EN 10 CIJFERS"
2870 GOSUB4290
2880 EO$="EEN JAAR IS 365 DAGEN"
2890 GOSUB4290
2900 EO$="IN 24 UUR LEGDE HIJ 18000 KM AF"
2910 GOSUB4290
2920 Z$="4.5":GOSUB4490:GOTO4060
2930 REM=== LES 5 ===
2940 TK=42:A$=R$:B$=P$:C$=N$:D$=U$:E$=L$
2950 Z$="5.1":GOSUB4490
2960 A1$="Naast de LETTERS en de CIJFERS"
2970 A2$="zijn er de 'OVERIGE TEKENS'"
2980 A3$="Sommige daarvan staan boven een"
2990 A4$="cijfer, andere staan samen op"
3000 A5$="een toets."
3010 A6$="WE GAAN DE SHIFT-TOETS GEBRUIKEN."
3020 A7$="":GOSUB4240:GOSUB4540
3030 Z$="5.2":GOSUB4490
3040 A1$="De SH-toetsen worden ingedrukt met"
3050 A2$="de PINK VAN DE HAND DIE NIET TIKT"
3060 A3$="Dus het teken dat boven de 9 staat"
3070 A4$="tikken we met de RECHTERHAND"
3080 A5$="terwijl de LINKER PINK de"
3090 A6$="SHIFT-toets INGEDRUKT HOUDT."
3100 GOSUB4240:GOSUB4540
3110 Z$="5.3":GOSUB4490
3120 A1$="De plaats van de 'overige tekens'"
3130 A2$="is niet op iedere computer gelijk."
3140 A3$="We kijken daarom voor elke"
3150 A4$="oefening waar de tekens staan en"
3160 A5$="prenten ons die plaats in."
3170 A6$="KIJK EN ONTHOUD WAAR = en : STAAN"
3180 GOSUB4240:GOSUB4540
3190 Z$="5.4":GOSUB4490
3200 EO$="A=22:B=23:C=24:D=25"
3210 GOSUB4290
3220 EO$="Z=800:K=431:PP=0"
3230 GOSUB4290
3240 Z$="5.5":GOSUB4490
3250 PRINT"KIJK EN ONTHOUD WAAR ( en ) STAAN"
3260 GOSUB4540
3270 Z$="5.6":GOSUB4490
3280 EO$="A(12)=B(22):G=INT(A(12))"
3290 GOSUB4290
3300 EO$="K(A)=A(22):GOSUB 210"
3310 GOSUB4290
3320 Z$="5.7":GOSUB4490
3330 PRINT"KIJK EN ONTHOUD WAAR + en - STAAN"
3340 GOSUB4540
3350 Z$="5.8":GOSUB4490
3360 EO$="A=B+22:Z(12)=A-Z(3)"
3370 GOSUB4290
3380 EO$="Q=ABS(A-B):T=T+1:RETURN"
3390 GOSUB4290
3400 Z$="5.9":GOSUB4490:GOTO4060
3410 REM=== LES 6 ===
3420 A$=R$:B$=P$:C$=N$:D$=U$:E$=L$
3430 Z$="6.1":TK=47:GOSUB4490
3440 PRINT"KIJK EN ONTHOUD WAAR $ en ";CHR$(34);" STAAN"
3450 GOSUB4540
3460 Z$="6.2":GOSUB4490
3470 EO$="A$="+CHR$(34)+"HALLO"+CHR$(34)+":PRINT TAB(33)A$"
3480 GOSUB4290
3490 Z$="6.3":GOSUB4490
3500 PRINT"KIJK EN ONTHOUD WAAR * en / STAAN"
3510 GOSUB4540
3520 Z$="6.4":GOSUB4490
3530 EO$="A$=B$:T=T/4*P:GOSUB1290"
3540 GOSUB4290
3550 EO$="A(4)=B(4)/3*A*B:RETURN"
3560 GOSUB4290
3570 Z$="6.5":GOSUB4490
3580 PRINT"KIJK EN ONTHOUD WAAR < en > STAAN"
3590 GOSUB4540
3600 Z$="6.6":GOSUB4490
3610 EO$="IF A>6 THEN B=C/8:NEXT X"
3620 GOSUB4290
3630 EO$="IF A$<>B$ THEN D=600"
3640 GOSUB4290
3650 Z$="6.7":GOSUB4490:GOTO4060
3660 REM=== LES 7 ===
3670 A$=R$:B$=P$:C$=N$:D$=U$:E$=L$
3680 Z$="7.1":TK=51:GOSUB4490
3690 PRINT"KIJK EN ONTHOUD WAAR . en , STAAN"
3700 GOSUB4540
3710 Z$="7.2":GOSUB4490
3720 EO$="DIM A$(10),B$(10):X=8.22"
3730 GOSUB4290
3740 EO$="READ A$,B$:GOTO 1280"
3750 GOSUB4290
3760 Z$="7.3":GOSUB4490
3770 PRINT"KIJK EN ONTHOUD WAAR ; en ? STAAN"
3780 GOSUB4540
3790 Z$="7.4":GOSUB4490
3800 EO$="PRINT A;B$;:GOTO 1200"
3810 GOSUB4290
3820 EO$="1340 REM IS A<100 ?"
3830 GOSUB4290
3840 Z$="7.5":GOSUB4490:GOTO4060
3850 REM=== LES 8 ===
3860 A$=R$:B$=P$:C$=V$:D$=U$:E$=L$
3870 Z$="8.1":TK=51:GOSUB4490
3880 A1$="ELKE INGETIKTE REGEL moet worden"
3890 A2$="afgesloten met de RETURN-toets."
3900 A3$="Deze toets wordt getikt met de"
3910 A4$="PINK van de RECHTERHAND."
3920 A5$="Tik RETURN na het laatste teken"
3930 A6$="van elke regel.":A7$=""
3940 GOSUB4240:GOSUB4540
3950 Z$="8.2":GOSUB4490
3960 EO$="ALLE BEGIN IS MOEILIJK"+CHR$(13)
3970 GOSUB4290
3980 EO$="TAXI PERFECT TELEFOON 18579"+CHR$(13)
3990 GOSUB4290
4000 EO$="ALI BABA EN DE 40 ROVERS"+CHR$(13)
4010 GOSUB4290
4020 Z$="8.3":GOSUB4490:GOTO4060
4030 REM=== LES 9 ===
4040 TK=51:A$=R$:B$=P$:C$=V$:D$=U$:E$=L$
4050 Z$="9.0":GOSUB4490
4060 PRINT"VRIJ SPEL OP HET SCHERM:"
4070 PRINT:PRINT"PROBEER HIER HOE GOED HET AL GAAT!"
4080 PRINT:PRINT"(% = TIKVOORBEELDEN,  # = STOPPEN)":PRINT
4090 GOSUB210:IFIN$=CHR$(13) THENPRINT
4100 IFIN$="%" THEN GOSUB4750
4110 IFIN$="#" THEN4140
4120 IFASC(IN$)<32 THEN4090
4130 PRINTIN$;:GOTO4090
4140 IFZ$<>"9.0" THEN GOSUB4160:GOSUB210:GOTO950
4150 REM=== EINDE ===
4160 GOSUB100
4170 PRINT:PRINT"DIT IS HET EINDE VAN DEZE LES -"
4180 PRINT:PRINT"WE ZIJN OP DE GOEDE WEG, EN"
4190 PRINT:PRINT"DE AANHOUDER WINT !"
4200 PRINT:PRINT
4210 PRINT:PRINT"TOT ZIENS !"
4220 SD=100:GOSUB450:GOTO950
4230 REM=== INSTRUCTIES ===
4240 PRINTA1$:PRINT:PRINTA2$:PRINT
4250 PRINTA3$:PRINT:PRINTA4$:PRINT
4260 PRINTA5$:PRINT:PRINTA6$:PRINT
4270 PRINTA7$:RETURN
4280 REM=== TIKCONTROLE ===
4290 GOSUB4490:PRINT
4300 PRINT"Vingers OP DE RUST-TOETSEN"
4310 GOSUB4340:IFTP$<>EO$ THEN4290
4320 RETURN
4330 REM=== TIK-OEFENING ===
4340 PRINT
4350 PRINT"Tik nu heel precies na"
4360 PRINT
4370 PRINT"ZONDER NAAR DE VINGERS TE KIJKEN:":PRINT
4380 PRINT:PRINT:PRINTEO$:TP$=""
4390 PRINT:PRINT;
4400 FORN=1 TOLEN(EO$)
4410 GOSUB210:IFIN$="%" THENTP$=EO$:N=LEN(EO$):GOTO4430
4420 TP$=TP$+IN$:PRINTIN$;
4430 NEXTN:PRINT
4440 IFTP$<>EO$ THENPRINT:PRINT"Dit doen we nog eens."
4450 IFIN$="%" THEN4470
4460 IFTP$=EO$ THENPRINT:PRINT"GOED GEDAAN!"
4470 GOSUB4540:RETURN
4480 REM=== TEKEN KEYBOARD ===
4490 GOSUB100:PRINT
4500 PRINTA$:PRINTB$:PRINTC$:PRINTD$:PRINTE$:PRINT
4510 PRINT"LES ";Z$:PRINT
4520 RETURN
4530 REM=== TIK 'N TOETS ===
4540 PRINT:PRINT"---> TIK OP 'N TOETS (V='VRIJ')";
4550 GOSUB210:IF(IN$<>"V")AND(IN$<>"v") THEN4570
4560 GOSUB4490:GOSUB4060:GOSUB4160:GOTO950
4570 RETURN
4580 REM=== TITEL ===
4590 PRINT:PRINT
4600 PRINT"XXXXX X   X XXXX  XXXXX X   X"
4610 PRINT"X X X X   X X   X X     XX  X"
4620 PRINT"  X    X X  X   X X     X X X"
4630 PRINT"  X     X   XXXX  XXXX  X  XX"
4640 PRINT"  X     X   X     X     X   X"
4650 PRINT"  X     X   X     X     X   X"
4660 PRINT" XXX   XXX XXX    XXXXX X   X"
4670 PRINT:PRINT"CURSUS BLIND 10-VINGERSYSTEEM"
4680 PRINT:PRINT"VOOR  ALLE COMPUTERGEBRUIKERS"
4690 PRINT:PRINT"LES 1 T/M 8 =  INSTRUCTIES EN"
4700 PRINT:PRINT"DAARBIJ HORENDE TIKOEFENINGEN"
4710 PRINT:PRINT"LES 9 VRIJ SPEL OP HET SCHERM"
4720 PRINT:PRINT"KIES HET LESNUMMER (1......9)";
4730 RETURN
4740 REM=== VOORBEELDEN IN LES 9 ===
4750 TK$="":FORN=1 TO10:GOSUB260
4760 LR=1+RV*TK:TK$=TK$+MID$(W$,LR,1)
4770 NEXTN
4780 HO=10:VE=20:GOSUB110:PRINTTK$
4790 VE=22:GOSUB110:PRINT"          "
4800 GOSUB110:CP$="":FORN=1 TO10
4810 GOSUB210:IFIN$=CHR$(13) THEN4810
4820 IFIN$="#" THENN=10:GOTO4840
4830 PRINTIN$;:CP$=CP$+IN$
4840 NEXTN:IFIN$="#" THENRETURN
4850 IFCP$<>TK$ THEN4790
4860 GOTO4750
30000 REM==============================
30010 REM EEN  TIK-OEFENING  KAN WORDEN
30020 REM OVERGESLAGEN DOOR % TE TIKKEN
30030 REM==============================
30040 REM GEMAAKT OP EEN BBC B COMPUTER
30050 REM DOOR
32000 REM Ch.W.Brederode
32010 REM Von Bonninghausenlaan 33
32020 REM 2161 ES  LISSE
32030 REM
32040 REM TROS-RADIO dd 880420

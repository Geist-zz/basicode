
1000 A=500:GOTO20:REM ZET DE HUISJES OP EEN RIJ
1010 DIMC(11,5),C1(11,5),FL(10),A(9),S$(10),RI(4)
1020 SO$="9161542132334253636293847421626141443112"
1040 HT=HO:VT=VE:VD=0:HU=0
1050 A(0)=-4:A(1)=-2:A(2)=-1:A(3)=1:A(4)=2:A(5)=4
1060 A(6)=-2:A(7)=-1:A(8)=1:A(9)=2
1070 REM STUKJES INLEZEN
1080 FORI=1 TO9:READS$(I):NEXTI
1090 REM SPEELVELD VOORBEREIDEN
1100 FORVP=0 TO4:FORHP=0 TO11
1110 C(HP,VP)=-1:NEXTHP:NEXTVP
1120 REM STUKJES IN BEGINSTAND PLAATSEN
1130 SR$="002255777711335588881144669900"
1140 FORVP=1 TO3:FORHP=1 TO10
1150 C(HP,VP)=ASC(MID$(SR$,HP+10*VP-10,1))-48
1160 NEXTHP:NEXTVP
1170 REM EENHEIDSSTAPPEN H/V BEPALEN
1180 AH=INT((HG*.9375*.958-2)/10):AV=INT((VG*.958-2)/8)
1190 IFVD=0 THEN GOSUB6010:REM UITLEG
1200 GOSUB600:CN=0
1210 REM TEKEN BUITENKADER
1220 HO=0:VE=0:GOSUB620
1230 HO=(2.1+10*AH)/HG:GOSUB630
1240 VE=(2.1+6*AV)/VG:GOSUB630
1250 HO=0:GOSUB630:VE=0:GOSUB630
1260 REM TEKEN BLOKKEN
1270 FORI=0 TO10:FL(I)=0:NEXTI:REM 0=NOG NIET GETEKEND
1280 FORHP=1 TO10
1290 FORVP=1 TO3
1300 IFFL(C(HP,VP))=1 THEN1340:REM REEDS GETEKEND
1310 FL(C(HP,VP))=1
1320 IFC(HP,VP)=0 THEN1340
1330 TB=0:GOSUB4020:REM TEKEN BLOK
1340 NEXTVP
1350 NEXTHP
1360 ZT=0
1370 VE=.7584:HO=0
1380 CN=1:GOSUB620:CN=0
1390 IFVD=0 THEN1500:REM NIET VOORDOEN
1400 SR=ZT:GOSUB300:SR$="Voordoen; Zet "+SR$
1410 CN=1:GOSUB650
1420 IF2*ZT<LEN(SO$) THEN1450
1430 CN=0:HO=.5:SR$="Klaar, druk toets"
1440 GOSUB650:GOSUB210:GOSUB100:GOTO950
1450 ZT=ZT+1:ZZ$=MID$(SO$,2*ZT-1,2)
1460 SR=ZT:GOSUB300:SR$="Voordoen; Zet "+SR$
1470 CN=0:GOSUB650
1480 IN$="":BL=VAL(LEFT$(ZZ$,1)):IFBL=0 THENBL=9
1490 GOTO1560
1500 SR$="Welk blok ? ":GOSUB650
1510 GOSUB210:BL=IN-64
1520 IFIN=83 THEN GOSUB100:GOTO950:REM STOPPEN
1530 IFIN=86 THENVD=1:ZT=0:GOTO1100:REM VOORDOEN
1540 IF(BL<1)OR(BL>9) THEN GOSUB250:GOTO1510
1550 CN=1:GOSUB650
1560 FORI=0 TO3:RI(I)=0:NEXTI:BH=0
1570 FORHP=1 TO10:FORVP=1 TO4
1580 IFC(HP,VP)<>BL THEN1650:REM ANDER STUKJE
1590 IFBH=0 THENBH=HP:BV=VP
1600 REM KIJK WELKE RICHTINGEN KUNNEN
1610 IF(C(HP+1,VP)<>BL)AND(C(HP+1,VP)<>0) THENRI(0)=1
1620 IF(C(HP,VP-1)<>BL)AND(C(HP,VP-1)<>0) THENRI(1)=1
1630 IF(C(HP-1,VP)<>BL)AND(C(HP-1,VP)<>0) THENRI(2)=1
1640 IF(C(HP,VP+1)<>BL)AND(C(HP,VP+1)<>0) THENRI(3)=1
1650 NEXTVP:NEXTHP
1660 CN=1:GOSUB620:CN=0
1670 SR$="Blok "+IN$+", Welke richting ":S1$="("
1680 RI(4)=0
1690 FORI=0 TO3
1700 IFRI(I)=1 THEN1750
1710 IFRI(4)>0 THENRI(4)=-1:GOTO1740
1720 REM MOGELIJKE RICHTING MELDEN
1730 IFRI(4)=0 THENRI(4)=I+1
1740 SR$=SR$+S1$+MID$("ONWZ",I+1,1):S1$=","
1750 NEXTI
1760 IFVD=1 THENRI(4)=VAL(RIGHT$(ZZ$,1)):REM VOORDOEN
1770 IFRI(4)=0 THEN GOSUB250:GOTO1370:REM KAN NIET
1780 IFRI(4)>0 THEN1880
1790 SR$=SR$+")":GOSUB650
1800 GOSUB210:REM VRAAG RICHTING
1810 IF(IN=79)AND(RI(0))=0 THENRI(4)=1:GOTO1860:REM O
1820 IF(IN=78)AND(RI(1))=0 THENRI(4)=2:GOTO1860:REM N
1830 IF(IN=87)AND(RI(2))=0 THENRI(4)=3:GOTO1860:REM W
1840 IF(IN=90)AND(RI(3))=0 THENRI(4)=4:GOTO1860:REM Z
1850 GOSUB250:GOTO1800
1860 CN=1:GOSUB650:REM WIS REGEL
1870 REM WIS OUD BLOK
1880 HP=BH:VP=BV:TB=1:GOSUB4020
1890 REM GA BEWEGEN
1900 FORHP=1 TO10:FORVP=1 TO3
1910 C1(HP,VP)=C(HP,VP):NEXTVP:NEXTHP:KN=0
1920 FORHP=1 TO10:FORVP=1 TO3
1930 C(HP,VP)=C1(HP,VP):NEXTVP:NEXTHP
1940 IFRI(4)<>1 THEN2020:REM NIET OOST
1950 REM BEWEEG IN OOSTELIJKE RICHTING
1960 FORHP=11 TO2 STEP-1:FORVP=1 TO3
1970 IFC(HP-1,VP)<>BL THEN2010:REM DEZE NIET
1980 IFC(HP,VP)=BL THEN2000:REM KAN
1990 IFC(HP,VP)<>0 THENKN=1:GOTO2010:REM KAN NIET
2000 C1(HP,VP)=BL:C1(HP-1,VP)=0
2010 NEXTVP:NEXTHP:IFKN=0 THENBH=BH+1:GOTO1920
2020 IFRI(4)<>2 THEN2090:REM NIET NOORD
2030 FORHP=1 TO10:FORVP=0 TO3
2040 IFC(HP,VP+1)<>BL THEN2080:REM DEZE NIET
2050 IFC(HP,VP)=BL THEN2070
2060 IFC(HP,VP)<>0 THENKN=1:GOTO2080:REM KAN NIET
2070 C1(HP,VP)=BL:C1(HP,VP+1)=0
2080 NEXTVP:NEXTHP:IFKN=0 THENBV=BV-1:GOTO1920
2090 IFRI(4)<>3 THEN2160:REM NIET WEST
2100 FORHP=0 TO9:FORVP=1 TO4
2110 IFC(HP+1,VP)<>BL THEN2150:REM DEZE NIET
2120 IFC(HP,VP)=BL THEN2140
2130 IFC(HP,VP)<>0 THENKN=1:GOTO2150:REM KAN NIET
2140 C1(HP,VP)=BL:C1(HP+1,VP)=0
2150 NEXTVP:NEXTHP:IFKN=0 THENBH=BH-1:GOTO1920
2160 IFRI(4)<>4 THEN2240:REM NIET ZUID
2170 FORHP=1 TO10:FORVP=5 TO2 STEP-1
2180 IFC(HP,VP-1)<>BL THEN2220:REM DEZE NIET
2190 IFC(HP,VP)=BL THEN2210
2200 IFC(HP,VP)<>0 THENKN=1:GOTO2220:REM KAN NIET
2210 C1(HP,VP)=BL:C1(HP,VP-1)=0
2220 NEXTVP:NEXTHP:IFKN=0 THENBV=BV+1:GOTO1920
2230 REM BEWOGEN
2240 HP=BH:VP=BV:TB=0:GOSUB4020:REM TEKEN NIEUW
2250 GOTO1370
4000 REM SR TEKEN OF WIS BLOK
4010 REM DRAW BLOCK C(HP,VP) FROM POSITION HP,VP
4020 PH=2.1+(HP-1)*AH:PV=2.1+(VP-1)*2*AV
4030 HO=(PH+AH/2-6)/HG:VE=(PV+AV/2-6)/VG
4040 CN=1:GOSUB620:CN=TB:SR$=CHR$(64+C(HP,VP)):GOSUB650
4050 HO=PH/HG:VE=PV/VG:GOSUB620
4060 FORI=1 TOLEN(S$(C(HP,VP)))
4070 SR=VAL(MID$(S$(C(HP,VP)),I,1))
4080 IFI=2*INT(I/2) THEN4120:REM I=EVEN=HP
4090 REM I=ONEVEN=VP
4100 IFSR<6 THENPV=PV-2*SGN(A(SR))
4110 PV=PV+A(SR)*AV:GOTO4140
4120 IFSR<6 THENPH=PH-2*SGN(A(SR))
4130 PH=PH+A(SR)*AH
4140 HO=PH/HG:VE=PV/VG:GOSUB630
4150 NEXTI
4160 GOSUB330
4170 IF(SR$="A")OR(SR$="D") THEN GOSUB5010
4180 IF(SR$="E")OR(SR$="G") THEN GOSUB5010
4190 RETURN
5000 REM * HUISJES *
5010 HO=(PH+AH/2-4)/HG:VE=(PV+AV/2-4)/VG
5020 HO=HO+.03:IF(SR$="G")AND(HU=1) THENHO=HO+.17
5030 VE=VE+.075:IFSR$="A" THENVE=VE+.23
5040 GOSUB620:HO=HO+.05:GOSUB630
5050 VE=VE+.06:GOSUB630:HO=HO-.05:GOSUB630:VE=VE-.06
5060 GOSUB630:HO=HO+.025:VE=VE-.06:GOSUB630:HO=HO+.025
5070 VE=VE+.06:GOSUB630:VE=VE+.02
5080 GOSUB620:HO=HO-.02:GOSUB630:VE=VE+.04:GOSUB630
5090 HO=HO-.01:VE=VE-.02:GOSUB620:HO=HO-.01:GOSUB630
5100 VE=VE-.015
5110 GOSUB630:HO=HO+.01:GOSUB630:VE=VE+.015:GOSUB630
5120 HO=HO-.015
5130 VE=VE-.065:GOSUB620:VE=VE-.03:GOSUB630:HO=HO+.01
5140 GOSUB630:VE=VE+.015:GOSUB630
5150 HO=PH/HG:VE=PV/VG
5160 IFSR$="G" THENHU=HU+1
5170 IFHU=1 THEN5010
5180 HU=0
5190 RETURN
6000 REM UITLEG
6010 SR$="Zet de HUISJES op een rij":GOSUB150
6020 PRINT:PRINT
6030 PRINT"Deze puzzel bestaat  uit een"
6040 PRINT"speelbord met daarop een aantal"
6050 PRINT"stukjes,die met de letters A tot"
6060 PRINT"en met I zijn aangeduid.":PRINT
6070 PRINT"Er zijn vijf vierkante stukjes"
6080 PRINT"en vier rechthoek-vormige.":PRINT
6090 PRINT"Begin met de opstelling na te tekenen."
6100 GOSUB6350
6110 PRINT"Het is dan de bedoeling dat u met"
6120 PRINT"deze stukjes gaat schuiven."
6130 PRINT"Daartoe geeft u mij telkens eerst"
6140 PRINT"de letter op van het te verschuiven"
6150 PRINT"stuk. Indien nodig moet u mij"
6160 PRINT"tevens opgeven in welke richting"
6170 PRINT"(N)oord, (O)ost, (Z)uid of (W)est"
6180 PRINT"het stukje moet worden verschoven.":PRINT
6190 PRINT"Het doel van het spel is, om te"
6200 PRINT"zorgen dat de  HUISJES  op de"
6210 PRINT"MIDDELSTE rij naast elkaar komen"
6220 PRINT"te staan.":PRINT
6230 PRINT"Waar dan andere blokjes terecht"
6240 PRINT"komen doet er niet toe."
6250 GOSUB6350
6260 PRINT"Het kan in 20 verplaatsingen en"
6270 PRINT"het is knap moeilijk om er uit"
6280 PRINT"te komen.":PRINT
6290 PRINT"Als u het opgeeft, druk dan op"
6300 PRINT"de S en als u vindt  dat ik het"
6310 PRINT"maar voor moet doen, druk dan op"
6320 PRINT"de V.":PRINT
6330 PRINT"Veel plezier!"
6340 REM SPATIE VOOR VERDER
6350 HO=0:VE=VT:GOSUB110
6360 SR$="<spatie> voor verder":GOSUB150
6370 GOSUB210:IFIN<>32 THEN6370
6380 GOSUB100:RETURN
25000 REM VORMEN EN MATEN VAN DE STUKJES
25010 DATA"5401","4411","4411","4411","5401"
25020 DATA"4411","4510","4510","4411"
25030 REM
30000 REM DEZE PUZZEL WERD IN 1934 GEMAAKT
30010 REM DOOR RICHARD W. FATIGUANT OM DE
30020 REM GEBOORTE TE VIEREN VAN DE EERSTE
30030 REM VIJFLING TER WERELD DIE IN LEVEN
30040 REM BLEEF, DE DIONNE-VIJFLING.
30050 REM
32000 REM #########################
32010 REM #                       #
32020 REM #  DIT PROGRAMMA IS OP  #
32030 REM #  09-05-1988 GEMAAKT   #
32040 REM #        DOOR:          #
32050 REM #                       #
32060 REM #    P.A. KESTELOO      #
32070 REM #    REEDE 34           #
32080 REM #    3232 CV BRIELLE    #
32090 REM #                       #
32100 REM #  NAAR AANLEIDING VAN  #
32110 REM #    ESCOT'S PUZZEL     #
32120 REM #         VAN           #
32130 REM #      J.HAUBRICH       #
32140 REM #                       #
32150 REM #########################
32160 REM
32170 REM TROS-RADIO dd 880810
32180 REM incl corr. dd 880821


1000 A=500:GOTO20:REM MEMORY
1010 DIMNE$(14),EN$(14),FR$(14),DU$(14),LA$(14)
1020 P=0
1030 HS=100:HS$="niemand":BS=0:BS$="niemand"
1040 FORQ=0 TO14:READNE$(Q):NEXTQ
1050 FORQ=0 TO14:READEN$(Q):NEXTQ
1060 FORQ=0 TO14:READFR$(Q):NEXTQ
1070 FORQ=0 TO14:READDU$(Q):NEXTQ
1080 FORQ=0 TO14:READLA$(Q):NEXTQ
1090 HL=HO:VL=VE:GOTO6010:REM MENU
2000 GOSUB600
2010 CN=0
2020 FORHO=0 TO.99 STEP.16666
2030 VE=0:GOSUB620
2040 VE=.8:GOSUB630
2050 NEXTHO
2060 VE=0:HO=.999:GOSUB620:VE=.8:GOSUB630
2070 FORVE=0 TO.81 STEP.16
2080 HO=0:GOSUB620
2090 HO=.999:GOSUB630
2100 NEXTVE
2110 FORSR=1 TO30
2120 SR=SR-1:VE=INT(SR/6+1)*.16+.01-.16:SR=SR+1
2130 SR=SR-1:HO=(SR-((INT(SR/6))*6))*.166666+.01:SR=SR+1
2140 GOSUB300:GOSUB650
2150 NEXTSR
2160 RETURN
2500 REM KAARTEN SCHUDDEN
2510 B$=".aabbccddeeffgghhiijjkkllmmnnoo.":B=0
2520 C$=".000000000000000000000000000000."
2530 GOSUB260:RV=INT(RV*30+1)
2540 B=B+1
2550 IFB>30 THENRETURN
2560 C$=MID$(C$,1,B)+MID$(B$,RV+1,1)+MID$(C$,B+2,32-B)
2570 IFMID$(B$,RV+1,1)<>"-" THEN2600
2580 RV=RV+1:IFRV>30 THENRV=RV-30
2590 GOTO2560
2600 B$=MID$(B$,1,RV)+"-"+MID$(B$,RV+2,32-RV)
2610 GOTO2530
3000 REM **TROS**
3010 GOSUB620:HO=HO+.03:GOSUB630:HO=HO-.015:GOSUB620
3020 VE=VE+.07:GOSUB630:HO=HO+.023:GOSUB620:VE=VE-.07
3030 GOSUB630:HO=HO+.03:GOSUB630:VE=VE+0.035:GOSUB630
3040 HO=HO-.03:GOSUB630:HO=HO+.03:VE=VE+0.035:GOSUB630
3050 HO=HO+.01:GOSUB620:VE=VE-.07:GOSUB630:HO=HO+.03
3060 GOSUB630:VE=VE+.07:GOSUB630:HO=HO-.03:GOSUB630
3070 HO=HO+.04:GOSUB620:HO=HO+.03:GOSUB630:VE=VE-.035
3080 GOSUB630:HO=HO-.03:GOSUB630:VE=VE-.035:GOSUB630
3090 HO=HO+.03:GOSUB630
3100 RETURN
3200 REM **DOOS**
3210 HO=HO+.03:VE=VE+.02:GOSUB620:HO=HO+.06:GOSUB630
3220 VE=VE+.04:GOSUB630:HO=HO-.06:GOSUB630:VE=VE-.04
3230 GOSUB630:VE=VE-.02:HO=HO+.02:GOSUB630:HO=HO+.06
3240 GOSUB630:VE=VE+.02:HO=HO-.02:GOSUB630:VE=VE-.02
3250 HO=HO+.02:GOSUB620:VE=VE+.04:GOSUB630:VE=VE+.02
3260 HO=HO-.02:GOSUB630
3270 HO=HO+.01:GOSUB630
3280 RETURN
3400 REM **BOEK**
3410 HO=HO+.04:VE=VE+.01:GOSUB620:VE=VE+.05:GOSUB630
3420 HO=HO+.03:VE=VE+.02:GOSUB630:VE=VE-.05:GOSUB630
3430 VE=VE-.02:HO=HO+.03:GOSUB630:VE=VE+.05:GOSUB630
3440 VE=VE+.02:HO=HO-.03:GOSUB630:VE=VE-.06:GOSUB630
3450 VE=VE+.01:GOSUB620:HO=HO-.03:VE=VE-.02:GOSUB630
3460 HO=HO+.01:VE=VE-.01:GOSUB630:HO=HO+.02:VE=VE+.02
3470 GOSUB630:HO=HO+.02:VE=VE-.02:GOSUB630:VE=VE+.01
3480 HO=HO+.01:GOSUB630
3490 RETURN
3600 REM **TV**
3610 HO=HO+.03:GOSUB620:HO=HO+.1:GOSUB630
3620 VE=VE+.08:GOSUB630:HO=HO-.1:GOSUB630:VE=VE-.08
3630 GOSUB630:HO=HO+.01:VE=VE+.01:GOSUB620:HO=HO+.06
3640 GOSUB630:VE=VE+.06:GOSUB630:HO=HO-.06:GOSUB630
3650 VE=VE-.06:GOSUB630:HO=HO+.07:GOSUB620:HO=HO+.01
3660 GOSUB620:VE=VE+.01:GOSUB620:HO=HO-.01:GOSUB620
3670 VE=VE+.01:GOSUB620:HO=HO+.01:GOSUB620:HO=HO-.03
3680 VE=VE-.03:GOSUB620:HO=HO+.02:VE=VE-.04:GOSUB630
3690 HO=HO-.06:GOSUB620:VE=VE+.04:HO=HO+.02:GOSUB630
3700 RETURN
3800 REM **HUIS**
3810 HO=HO+.05:VE=VE+.02:GOSUB620:HO=HO+.05:GOSUB630
3820 VE=VE+.06:GOSUB630:HO=HO-.05:GOSUB630:VE=VE-.06
3830 GOSUB630:HO=HO+.025:VE=VE-.06:GOSUB630:HO=HO+.025
3840 VE=VE+.06:GOSUB630:VE=VE+.02
3850 GOSUB620:HO=HO-.02:GOSUB630:VE=VE+.04:GOSUB630
3860 HO=HO-.01:VE=VE-.02:GOSUB620:HO=HO-.01:GOSUB630
3870 VE=VE-.015
3880 GOSUB630:HO=HO+.01:GOSUB630:VE=VE+.015:GOSUB630
3890 HO=HO-.015
3900 VE=VE-.065:GOSUB620:VE=VE-.03:GOSUB630:HO=HO+.01
3910 GOSUB630:VE=VE+.015:GOSUB630
3920 RETURN
4000 REM **SCHOEN**
4010 HO=HO+.04:GOSUB620:VE=VE+.07:GOSUB630:HO=HO+.08
4020 GOSUB630:VE=VE-.01:HO=HO+.01:GOSUB630:VE=VE-.01
4030 GOSUB630:HO=HO-.01:VE=VE-.01:GOSUB630:HO=HO-.04
4040 GOSUB630:VE=VE-.04:GOSUB630:HO=HO-.04:GOSUB630
4050 RETURN
4200 REM **POTLOOD**
4210 HO=HO+.02:VE=VE+.02:GOSUB620:HO=HO+.08:GOSUB630
4220 HO=HO+.03:VE=VE+.02:GOSUB630:VE=VE+.02:HO=HO-.03
4230 GOSUB630:HO=HO-.08:GOSUB630:VE=VE-.04:GOSUB630
4240 RETURN
4400 REM **GLAS**
4410 HO=HO+.02:VE=VE+.01:GOSUB620:VE=VE+.06:HO=HO+.02
4420 GOSUB630:HO=HO+.03:GOSUB630:VE=VE-.06:HO=HO+.02
4430 GOSUB630:HO=HO-.05:VE=VE+.06:GOSUB620:HO=HO+.06
4440 VE=VE-.08:GOSUB630:HO=HO+.02:GOSUB630:VE=VE-.01
4450 GOSUB620:HO=HO-.025:GOSUB630:HO=HO-.06
4460 VE=VE+.08:GOSUB630
4470 RETURN
4600 REM **TAFEL**
4610 HO=HO+.03:VE=VE+.03:GOSUB620:HO=HO+.07:GOSUB630
4620 VE=VE-.03:HO=HO+.02:GOSUB630:HO=HO-.07:GOSUB630
4630 HO=HO-.02:VE=VE+.03:GOSUB630:VE=VE+.05:GOSUB630
4640 HO=HO+.07:GOSUB620:VE=VE-.05:GOSUB630:VE=VE-.03
4650 HO=HO+.02:GOSUB620:VE=VE+.05:GOSUB630
4660 RETURN
4800 REM **STOEL**
4810 HO=HO+.05:GOSUB620:VE=VE+.08:GOSUB630:VE=VE-.04
4820 GOSUB620:HO=HO+.04:GOSUB630:VE=VE+.04:GOSUB630
4830 RETURN
5000 REM **PIJL BOVEN**
5010 HO=HO+.05:VE=VE+.07:GOSUB620:HO=HO+.04:GOSUB630
5020 VE=VE-.07:GOSUB630:HO=HO+.03:VE=VE+.03:GOSUB630
5030 HO=HO-.05:VE=VE-.07:GOSUB630:HO=HO-.05:VE=VE+.07
5040 GOSUB630:VE=VE-.03:HO=HO+.03
5050 GOSUB630:VE=VE+.07:GOSUB630
5060 RETURN
5200 REM **PIJL ONDER**
5210 HO=HO+.05:VE=VE-.04:GOSUB620:HO=HO+.04:GOSUB630
5220 VE=VE+.07:GOSUB630:HO=HO+.03:VE=VE-.03:GOSUB630
5230 HO=HO-.05:VE=VE+.07:GOSUB630:HO=HO-.05:VE=VE-.07
5240 GOSUB630:HO=HO+.03:VE=VE+.03:GOSUB630:VE=VE-.07
5250 GOSUB630
5260 RETURN
5400 REM **SLEUTEL**
5410 HO=HO+.01:VE=VE+.03:GOSUB620:VE=VE+.04:GOSUB630
5420 HO=HO+.04:GOSUB630:VE=VE-.01:GOSUB630:HO=HO+.09
5430 GOSUB630:VE=VE-.02:GOSUB630
5440 FORZ=1 TO2:HO=HO-.01:GOSUB630:VE=VE-.01:GOSUB630
5450 HO=HO-.01:GOSUB630:VE=VE+.01:GOSUB630:NEXTZ
5460 HO=HO-.05:GOSUB630:VE=VE-.01:GOSUB630:HO=HO-.04
5470 GOSUB630
5480 RETURN
5490 REM **STER**
5500 HO=HO+.04:VE=VE+.03:GOSUB620:HO=HO+.08:GOSUB630
5510 HO=HO-.04:VE=VE-.04:GOSUB620:VE=VE+.08:GOSUB630
5520 HO=HO+.03:VE=VE-.01:GOSUB620:HO=HO-.06:VE=VE-.06
5530 GOSUB630:HO=HO+.06:GOSUB620
5540 HO=HO-.06:VE=VE+.06:GOSUB630:
5550 RETURN
5600 REM **FIGUUR**
5610 HO=HO+.03:VE=VE+.02:GOSUB620:HO=HO+.05:VE=VE+.05
5620 GOSUB630:HO=HO+.05:VE=VE-.05:GOSUB630:HO=HO-.05
5630 VE=VE-.05:GOSUB630:VE=VE+.05:HO=HO-.05:GOSUB630
5640 VE=VE+.025:HO=HO+.025:GOSUB620:HO=HO+.05:GOSUB630
5650 VE=VE-.05:GOSUB630:HO=HO-.05
5660 GOSUB630:VE=VE+.05:GOSUB630:HO=HO+.025:GOSUB620
5670 HO=HO+.025
5680 VE=VE-.025:GOSUB630:HO=HO-.025:VE=VE-.025:GOSUB630
5690 VE=VE+.025:HO=HO-.025:GOSUB630:HO=HO+.025:VE=VE+.025
5700 GOSUB630
5710 RETURN
6000 REM **MENU**
6010 GOSUB100
6020 HO=INT(HL/2)-7:VE=1:GOSUB110:SR$="MEMORY":GOSUB150
6030 HO=HO+4:VE=3:GOSUB110:PRINT"MENU":HO=HO-5:PRINT
6040 PRINTTAB(HO);"1 - 1 speler":PRINT
6050 PRINTTAB(HO);"2 - 2 spelers":PRINT
6060 PRINTTAB(HO);"3 - uitleg":PRINT
6070 PRINTTAB(HO);"4 - stoppen":PRINT
6080 HO=1:VE=VL-1:GOSUB110:PRINT"Maak uw keuze";
6090 GOSUB210
6100 IFIN$="1" THENNS=1:GOTO6510
6110 IFIN$="2" THENNS=2:GOTO6510
6120 IFIN$="3" THEN10010
6130 IFIN$="4" THEN950
6140 GOTO6090
6500 REM **NAMEN INTYPEN**
6510 GOSUB100
6520 PRINT"Speler 1 wat is je naam";:INPUTN1$
6530 IFNS=1 THEN6560
6540 PRINT:PRINT"Speler 2 wat is je naam";:INPUTN2$
6550 REM **PLAATJES OF WOORDEN**
6560 GOSUB100
6570 PRINT"Wat wilt u : 1 - Plaatjes"
6580 PRINT"             2 - Woorden"
6590 GOSUB210
6600 IFIN$="1" THENPW=20:GOTO7000
6610 IFIN$="2" THEN6640
6620 GOTO6590
6630 REM **WELKE TAAL**
6640 GOSUB100
6650 PRINT"Wat wilt u : 1 - Nederlands"
6660 PRINT"             2 - Engels"
6670 PRINT"             3 - Frans"
6680 PRINT"             4 - Duits"
6690 PRINT"             5 - Latijn"
6700 PRINT"             6 - Nederlands en Engels"
6710 PRINT"             7 - Nederlands en Frans"
6720 PRINT"             8 - Nederlands en Duits"
6730 PRINT"             9 - Nederlands en Latijn"
6740 PRINT"            10 - Engels en Frans"
6750 PRINT"            11 - Engels en Duits"
6760 PRINT"            12 - Engels en Latijn"
6770 PRINT"            13 - Frans en Duits"
6780 PRINT"            14 - Frans en Latijn"
6790 PRINT"            15 - Duits en Latijn"
6800 PRINT:INPUTPW:IF(PW<1)OR(PW>15) THEN6640
7000 GOSUB2000:REM HOKJES TEKENEN
7010 AF=0:SR$="Even wachten,kaarten schudden":HO=.1:VE=.81
7020 GOSUB650:GOSUB2510:REM KAARTEN SCHUDDEN
7030 IF(PW>5)AND(PW<16) THEN GOSUB11000
7040 CN=1:VE=.81:HO=.1:GOSUB650
7050 CN=0:T1=0:T2=0:W=1
7060 CN=0:HO=.1:VE=.82
7070 IF(W=1)AND(NS=2) THENSR$=N1$+" kies twee getallen"
7080 IF(W=1)AND(NS=2) THENW=2:GOTO7110
7090 IFW=1 THENSR$=N1$+" kies twee getallen"
7100 IFW=2 THENW=1:SR$=N2$+" kies twee getallen"
7110 GOSUB650
7120 SR$="nr 1":HO=.01:VE=.87:GOSUB650
7500 NR$="":GOSUB7800:N1=VAL(NR$)
7510 GOSUB9500
7520 SR$="nr 2":HO=.01:VE=.92:GOSUB650
7530 NR$="":P=1:GOSUB7800:P=0:N2=VAL(NR$)
7540 GOSUB9500
7550 SR$="nr 1":HO=.01:VE=.87:CN=1:GOSUB650
7560 SR$="nr 2":HO=.01:VE=.92:GOSUB650
7570 SR=N2:GOSUB300:SR$=MID$(SR$,1,1):HO=.2:GOSUB650
7580 SR=N2:GOSUB300:SR$=MID$(SR$,2,1):HO=.25:GOSUB650
7590 SR=N1:GOSUB300:SR$=MID$(SR$,1,1):HO=.2:VE=.87:GOSUB650
7600 SR=N1:GOSUB300:SR$=MID$(SR$,2,1):HO=.25:GOSUB650:CN=0
7610 IF(N1=N2)OR(N1<1)OR(N1>30)OR(N2<1)OR(N2>30) THEN7120
7620 IF(MID$(C$,N1+1,1)="*")OR(MID$(C$,N2+1,1)="*") THEN7120
7630 GOSUB8500
7640 IFAF=1 THEN9000
7650 IFNS=1 THENT1=T1+1
7660 GOTO7060
7800 GOSUB210
7810 IFIN<>127 THEN8000
7820 IFLEN(NR$)=1 THENSR$=NR$:HO=.2:CN=1:GOSUB650:CN=0
7830 IFLEN(NR$)=1 THENNR$="":GOTO7800
7840 IFLEN(NR$)=2 THENSR$=MID$(NR$,2,1):CN=1:HO=.25:GOSUB650
7850 IFLEN(NR$)=2 THENNR$=MID$(NR$,1,1):CN=0:GOTO7800
7860 GOTO7800
8000 IF(IN=13)AND(LEN(NR$)>0) THEN8060
8010 IF(IN<ASC("0"))OR(IN>ASC("9")) THEN7800
8020 IFLEN(NR$)<2 THENNR$=NR$+IN$:SR$=IN$
8030 IFLEN(NR$)=1 THENHO=.2:GOSUB650:GOTO7800
8040 IFLEN(NR$)=2 THENHO=.25:GOSUB650:GOTO7800
8050 GOTO7800
8060 NR=VAL(NR$)
8070 IF(NR>30)OR(NR<1)OR(MID$(C$,NR+1,1))="*" THEN8010
8080 IFP=1 THENIFNR=N1 THEN8010
8090 RETURN
8500 VE=.82:HO=.1
8510 IF(W=1)AND(NS=2) THENSR$=N2$+" kies twee getallen":CN=1
8520 IF(W=1)AND(NS=2) THEN GOSUB650:CN=0:GOTO8550
8530 SR$=N1$+" kies twee getallen":CN=1:GOSUB650:CN=0
8540 GOTO8550
8550 IFMID$(C$,N1+1,1)=MID$(C$,N2+1,1) THEN8620
8560 SR$="Jammer,mis gegokt":VE=.87:HO=.2:GOSUB650
8570 SV=7:SP=50:SD=10:GOSUB400:REM WACHTEN
8580 HO=.2:CN=1:GOSUB650:CN=0
8590 NR=N1:CN=1:GOSUB9500
8600 NR=N2:GOSUB9500:CN=0
8610 GOTO8700
8620 SR$="Goed Zo !":VE=.87
8630 FORQ=1 TO3:SV=7:SP=75:SD=2:GOSUB400:CN=0:GOSUB650
8640 SP=90:GOSUB400:CN=1:GOSUB650:NEXTQ
8650 C$=MID$(C$,1,N1)+"*"+MID$(C$,N1+2,32-N1)
8660 C$=MID$(C$,1,N2)+"*"+MID$(C$,N2+2,32-N2)
8670 IF(NS=2)AND(W=1) THENT2=T2+1:W=2:GOTO8690
8680 IF(NS=2)AND(W=2) THENT1=T1+1:W=1
8690 IFC$=".******************************." THENAF=1:RETURN
8700 RETURN
9000 GOSUB100
9010 FORR=0 TOHL-1:PRINT"*";:NEXTR
9020 VE=VL-1:HO=0:GOSUB110:FORR=0 TOHL-2:PRINT"*";:NEXTR
9030 HO=0:FORR=1 TOVL-2:VE=R:GOSUB110:PRINT"*";:NEXTR
9040 HO=HL-1:FORR=1 TOVL-2:VE=R:GOSUB110:PRINT"*";:NEXTR
9050 IFNS=2 THEN9200
9060 VE=5:HO=4:GOSUB110
9070 PRINT"Je hebt alle plaatjes gevonden,"
9080 HO=INT((HL/2)-(LEN(N1$)/2)):VE=7:GOSUB110:PRINTN1$
9090 SR=T1:GOSUB300
9100 HO=7:VE=10:GOSUB110:SR$="Aantal beurten :"+SR$
9110 GOSUB150
9120 IFT1<HS THENHS=T1:HS$=N1$
9130 HO=3:VE=15:GOSUB110:SR=HS:GOSUB300
9140 PRINT"hoogste score:";HS$;" ";SR$;" beurten"
9150 HO=2:VE=VL-2:GOSUB110:PRINT"Druk op een knop ";
9160 GOSUB210:GOTO6010
9200 HO=3:VE=5:SR=T1:GOSUB300:SR$=N1$+" :"+SR$+" punten"
9210 GOSUB110:GOSUB150
9220 HO=3:VE=7:SR=T2:GOSUB300:SR$=N2$+" :"+SR$+" punten"
9230 GOSUB110:GOSUB150
9240 HO=5:VE=13:GOSUB110:PRINT"De winnaar is :"
9250 IFT1>T2 THENSR$=N1$:GOTO9270
9260 SR$=N2$
9270 HO=INT((HL/2)-((LEN(SR$)+6)/2)):VE=15:GOSUB110
9280 GOSUB150
9290 IF(T1>T2)AND(T1>BS) THENBS=T1:BS$=N1$:GOTO9310
9300 IFT2>BS THENBS=T2:BS$=N2$
9310 SR=BS:GOSUB300
9320 HO=3:VE=18:GOSUB110
9330 PRINT"hoogste score:";BS$;" ";SR$;" punten"
9340 GOTO9150
9500 NR=NR-1:VE=INT(NR/6+1)*.16+.07-.16
9510 HO=(NR-((INT(NR/6))*6))*.166666+.01:NR=NR+1
9520 Y$=MID$(C$,NR+1,1):Y=ASC(Y$)-ASC("a")+1
9530 IFPW<>20 THEN9600
9540 IFY>9 THEN9570
9550 ONY GOSUB3010,3210,3410,3610,3810,4010,4210,4410,4610
9560 RETURN
9570 Y=Y-9
9580 ONY GOSUB4810,5010,5210,5410,5500,5610
9590 Y=Y+9:RETURN
9600 Y=Y-1:IFPW>5 THEN9670
9610 IFPW=1 THENSR$=NE$(Y)
9620 IFPW=2 THENSR$=EN$(Y)
9630 IFPW=3 THENSR$=FR$(Y)
9640 IFPW=4 THENSR$=DU$(Y)
9650 IFPW=5 THENSR$=LA$(Y)
9660 GOSUB650:RETURN
9670 IFPW>9 THEN9740
9680 IFMID$(D$,NR+1,1)="1" THENSR$=NE$(Y):GOTO9860
9690 IFPW=6 THENSR$=EN$(Y)
9700 IFPW=7 THENSR$=FR$(Y)
9710 IFPW=8 THENSR$=DU$(Y)
9720 IFPW=9 THENSR$=LA$(Y)
9730 GOTO9860
9740 IFPW>12 THEN9800
9750 IFMID$(D$,NR+1,1)="1" THENSR$=EN$(Y):GOTO9860
9760 IFPW=10 THENSR$=FR$(Y)
9770 IFPW=11 THENSR$=DU$(Y)
9780 IFPW=12 THENSR$=LA$(Y)
9790 GOTO9860
9800 IFPW>14 THEN9840
9810 IFMID$(D$,NR+1,1)="1" THENSR$=FR$(Y):GOTO9860
9820 IFPW=13 THENSR$=DU$(Y)
9830 IFPW=14 THENSR$=LA$(Y)
9840 IFMID$(D$,NR+1,1)="1" THENSR$=DU$(NR):GOTO9860
9850 SR$=LA$(Y)
9860 GOSUB650:RETURN
10000 REM **UITLEG**
10010 GOSUB100:PRINTTAB(10);"MEMORY"
10020 PRINT:PRINT"De bedoeling van dit spel is om twee"
10030 PRINT"dezelfde plaatjes bij elkaar te"
10040 PRINT"zoeken. Dit spel kunt u alleen of met"
10050 PRINT"z'n tweeen spelen. U kunt kiezen of u"
10060 PRINT"plaatjes of woorden wilt. De woorden"
10070 PRINT"zijn Nederlands,Engels,Frans,Duits"
10080 PRINT"of Latijn(of twee van deze talen door"
10090 PRINT"elkaar zodat u twee woorden met"
10100 PRINT"dezelfde betekenis bij elkaar moet"
10110 PRINT"zoeken.)"
10120 PRINT"Als u alleen speelt is het de"
10130 PRINT"bedoeling om zo vlug mogelijk alle"
10140 PRINT"plaatjes bij elkaar te zoeken."
10150 PRINT"Met z'n tweeen moet ieder voor zich"
10160 PRINT"proberen om zoveel mogelijk plaatjes"
10170 PRINT"bij elkaar te zoeken.(nb.degene die"
10180 PRINT"twee plaatjes bij elkaar gevonden"
10190 PRINT"heeft,krijgt een extra beurt)"
10200 HO=HL-15:PRINTTAB(HO);"Edwin Van Waes"
10210 VE=VL-1:HO=0:GOSUB110:PRINT"Druk op een knop";
10220 GOSUB210:GOTO6010
11000 D$=".000000000000000000000000000000."
11010 FORR=1 TO15
11020 GOSUB260:RV=INT(RV*30+1)
11030 IFMID$(D$,RV+1,1)="0" THEN11060
11040 RV=RV+1:IFRV>30 THENRV=1:GOTO11030
11050 GOTO11030
11060 D$=MID$(D$,1,RV)+"1"+MID$(D$,RV+2,32-RV)
11070 X$=MID$(C$,RV+1,1)
11080 RV=RV+1:IFRV=31 THENRV=1
11090 IFMID$(C$,RV+1,1)=X$ THEN11110
11100 GOTO11080
11110 D$=MID$(D$,1,RV)+"2"+MID$(D$,RV+2,32-RV)
11120 NEXTR
11130 RETURN
25000 DATA"man","hond","vuur","hoofd","tafel","dag","dood"
25010 DATA"jaar","bos","goud","hand","god","deur","boom"
25020 DATA"zee"
25030 DATA"man","dog","fire","head","table","day","dead"
25040 DATA"year","wood","gold","hand","god","door","tree"
25050 DATA"sea"
25060 DATA"homme","chien","feu","tete","table","jour"
25070 DATA"mort","annee","bois","or","main","dieu","porte"
25080 DATA"arbre","mer"
25090 DATA"Mann","Hund","Feuer","Kopf","Tisch"
25100 DATA"Tag","Tod","Jahr","Wald","Gold","Hand","Gott"
25110 DATA"Tuer","Baum","Meer"
25120 DATA"vir","canis","ignis","caput","mensa","dies"
25130 DATA"mors","annus","silva","aurum","palma","deus"
25140 DATA"ianua","arbor","mare"
32000 REM******************************
32010 REM*          MEMORY            *
32020 REM*         --------           *
32030 REM* GEMAAKT OP EEN CBM-64 DOOR *
32040 REM*                            *
32050 REM*  EDWIN VAN WAES (13 JAAR)  *
32060 REM*                            *
32070 REM* EDWIN VAN WAES             *
32080 REM* STAPELEN 27                *
32090 REM* 5653 KP EINDHOVEN          *
32100 REM* 040-520257         23-1-88 *
32110 REM******************************
32120 REM TROS-RADIO dd 880406
32130 REM incl corr. dd 880609

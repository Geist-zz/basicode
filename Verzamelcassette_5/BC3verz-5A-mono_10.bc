
1000 A=400:GOTO20:REMComputerhandel
1010 REM------- HOOFDPROGRAMMA ------
1030 GOSUB11010:REMtitel
1040 GOSUB13010:REMwachtlus
1050 GOSUB13010:REMwachtlus
1060 GOSUB100
1070 HO=3:VE=5:GOSUB110
1080 PRINT"Wilt u de toelichting ?  (j/n)":GOSUB250
1090 GOSUB200
1100 IFIN=74 THEN1130:REMJ
1110 IFIN=78 THEN1140:REMN
1120 GOTO1090
1130 GOSUB2310:REMtoelichting
1140 DIMA(21,16),D(21,16),K(21,16),T(2),C(2)
1160 GOSUB2210:REMvariabelen vastleggen
1170 GOSUB3410:REMmoeilijkheidsgraad
1180 CP=10*DF
1190 GOSUB2010:REMarray structuur
1200 GOSUB7010:REMspeelveld printen
1210 GOSUB7410:REMverkoop grond
1500 REM***spelloop***
1510 GOSUB260:RF=1+INT(RV*(12-DF))
1520 RX=1
1530 IF(T>10)AND(RF<6) THEN GOSUB8510
1540 HO=3:VE=21:GOSUB110
1550 SR$="Prijzen computers verhoogd"
1560 HO=3:VE=22:GOSUB110
1570 SR$="Winst gehalveerd"
1580 IF(T>10)AND(RF<6) THENPRINTSR$
1590 IF(T>10)AND(RF<6) THEN GOSUB13010:GOSUB13030
1600 IF(RF<6) THENRX=0.5
1610 GOSUB9510:REMbepalen tijdstip ellende
1620 IF(CP>INT(4*NA)/DF) THEN9810:REMik win
1630 IF(NA>CP)AND(NA>200)AND(LA>80) THEN9610
1640 LA=LA-(INT(MX/2)/10):T=T+1
1650 IF(T=2)OR(T=8)OR(T=15)OR(T=20) THEN7100
1660 IF(T=30)OR(T=40)OR(T=60) THEN7100
1670 IF(T=80)OR(T=100)OR(T=150) THEN7100
1680 IF(T=200)OR(T=300)OR(T=400)OR(T=1000) THEN7100
1690 LA=LA+(PX*RX)
1700 GOSUB260:TX=2+INT(RV*6)
1710 IF(RF=1) THEN GOSUB8510:REMprint telex
1720 HO=0:VE=21:GOSUB110
1730 IF(RF=1) THENPRINT"Regering verhoogt omzetbelasting"
1740 HO=0:VE=22:GOSUB110
1750 IF(RF=1) THENPRINT"Kosten f";:PRINTTX;:PRINT"m."
1760 IF(RF=1) THENLA=LA-TX:GOSUB8010:REMprint financ
1770 HO=1:VE=21:GOSUB110
1780 SR$="Activiteiten op locatie"
1790 IF(T>10)AND(RF=3)AND(WX>0) THENPRINTSR$
1800 HO=1:VE=22:GOSUB110
1810 SR$="worden gestaakt"
1820 IF(T>10)AND(RF=3)AND(WX>0) THENPRINTSR$
1830 GOSUB13010:REMwachtlus
1840 IF(T>10)AND(RF=3)AND(WX>0) THEN GOSUB9210
1850 IF(RF<=3) THEN GOSUB13030:REMwis tekst
1860 GOSUB13030:REMwis tekst
1870 HO=0:VE=22:GOSUB110:GOSUB250
1880 PRINT"Bouw Lenen Uit Vestigen verGun Markt";
1890 GOSUB200
1900 IFIN=66 THEN4510:REMbouwen
1910 IFIN=76 THEN4010:REMlening
1920 IFIN=71 THEN4210:REMvergunning
1930 IFIN=86 THEN5210:REMvestigen
1940 IFIN=77 THEN3610:REMmarktonderzoek
1950 IFIN=85 THEN6010:REMuitbreiden
1960 GOTO1890
2000 REM***array structuur***
2010 HO=10:VE=6:GOSUB110
2020 PRINT"Even geduld a.u.b."
2030 FORX=3 TO21
2040 FORY=3 TO16
2050 GOSUB260:A(X,Y)=1+INT(RV*2)
2060 GOSUB260
2070 IFA(X,Y)>=2 THENA(X,Y)=A(X,Y)+INT(RV*2)
2080 NEXTY
2090 NEXTX
2100 GOSUB100
2110 RETURN
2200 REM***variabelen vastleggen***
2210 T=0
2220 CX=0:WX=0:BX=0:CY=0:LA=10*10:PZ=0
2230 SW=LA:PY=0:PZ=0:MX=0:PX=0:T(1)=4
2240 RETURN
2300 REM***toelichting***
2310 GOSUB100
2320 SR$="C O M P U T E R H A N D E L":GOSUB150
2330 PRINT:PRINT
2340 PRINT"In deze simulatie bent u de directeur"
2350 PRINT"van een computergroothandel."
2360 PRINT"De bedoeling is dat u uw onderneming"
2370 PRINT"zo goed mogelijk leidt, d.w.z. goede"
2380 PRINT"investeringen doen en de juiste"
2390 PRINT"beslissingen nemen."
2400 PRINT:PRINT"Uw concurrent is Datachain."
2410 PRINT
2420 PRINT"Uw opdracht is Datachain over te nemen."
2430 GOSUB13010:REMwachtlus
2440 GOSUB10010:REMblz
2450 SR$="KOPEN VAN GROND":GOSUB150:PRINT:PRINT
2460 PRINT"In het begin van het spel worden er"
2470 PRINT"percelen grond te koop aangeboden."
2480 PRINT"De bedoeling is er een aantal te "
2490 PRINT"kopen om er later een vestiging te"
2500 PRINT"bouwen."
2510 PRINT"De prijzen liggen tussen 1 en 10"
2515 PRINT"miljoen gulden."
2520 PRINT"Het is echter verstandig in het"
2530 PRINT"begin hoog te bieden, omdat u anders"
2540 PRINT"enige tijd werkeloos blijft zitten."
2550 PRINT"Zonder grond kunt u eigenlijk niets"
2560 PRINT"anders doen dan marktonderzoek."
2570 GOSUB10010:REMblz
2580 SR$="MARKTONDERZOEK":GOSUB150:PRINT:PRINT
2590 PRINT"Een marktonderzoek kunt u doen door de"
2600 PRINT"M in te duwen."
2610 PRINT"Een marktonderzoek kost 0.2 miljoen."
2620 PRINT"Ook als u grond gekocht hebt, kunt u"
2630 PRINT"op die locatie marktonderzoek doen."
2640 PRINT"Dan kost een onderzoek u echter 1.2"
2650 PRINT"miljoen."
2660 GOSUB10010:REMblz
2670 SR$="VERGUNNING":GOSUB150:PRINT:PRINT
2680 PRINT"Als u een stuk grond gekocht hebt,"
2690 PRINT"kunt u daarop een vestiging bouwen."
2700 PRINT"Daarvoor moet u echter eerst een "
2710 PRINT"vergunning aanvragen. Dit doet u door"
2720 PRINT"op de G te drukken."
2725 PRINT:SR$="BOUWEN":GOSUB150:PRINT:PRINT
2730 PRINT"Om te bouwen drukt u de B in."
2740 PRINT"Dit kost ook weer geld en"
2750 PRINT"u krijgt een mededeling over hoe de"
2760 PRINT"bouw vordert. U moet blijven bouwen,"
2770 PRINT"totdat het gebouw klaar is."
2780 GOSUB10010:REMblz
2790 SR$="VESTIGING":GOSUB150:PRINT:PRINT
2800 PRINT"Als het gebouw klaar is kunt u zich op"
2810 PRINT"die locatie vestigen. Dit doet u door"
2820 PRINT"op de V te duwen. Ook dit kost weer"
2830 PRINT"geld."
2840 GOSUB10010:REMblz
2850 SR$="UITBREIDEN":GOSUB150:PRINT:PRINT
2860 PRINT"u kunt de opbrengst van een vestiging"
2870 PRINT"nog verhogen door uit te breiden."
2880 PRINT"Dit doet u door de U in te drukken."
2890 GOSUB10010:REMblz
2900 PRINT"De letter die steeds op een locatie"
2910 PRINT"staat geeft aan:"
2920 PRINT
2930 PRINT"- dat die locatie van u is."
2940 PRINT"- in welk stadium uw activiteiten"
2950 PRINT"  op die locatie zijn."
2960 PRINT"- wat de activiteit is welke u op"
2970 PRINT"  deze locatie kunt ondernemen."
2980 PRINT:PRINT
2990 PRINT"Onder in beeld vindt u een regel met"
3000 PRINT"alle opties, die u per beurt hebt."
3010 PRINT:PRINT"Deze ziet er als volgt uit:"
3020 PRINT:PRINT:PRINT
3030 PRINT"Bouw Lenen Uit Vestigen verGun Markt"
3040 PRINT
3050 GOSUB10010:REMblz
3060 PRINT:PRINT"Bouw Lenen Uit Vestigen verGun Markt"
3070 PRINT:PRINT"Als er dus b.v. een G op een locatie"
3080 PRINT"staat betekent dit:"
3090 PRINT:PRINT"deze locatie is van u en u moet hier"
3100 PRINT"een vergunning aanvragen"
3110 PRINT"De enige andere optie is marktonder-"
3120 PRINT"zoek doen. Deze optie kunt u altijd"
3130 PRINT"kiezen."
3140 PRINT:PRINT
3150 PRINT"Als er dus b.v. een V op een locatie"
3160 PRINT"staat betekent dit:"
3170 PRINT"op deze locatie moet u nu een"
3180 PRINT"filiaal plaatsen."
3190 GOSUB10010:REMblz
3200 PRINT"Af en toe krijgt u telexmededelingen"
3210 PRINT"over omzetbelastingen of een lagere"
3220 PRINT"winst."
3230 PRINT
3240 PRINT"Soms doen er zich ongelukken of rampen"
3250 PRINT"voor en dit gebeurt eerder, wanneer u"
3260 PRINT"veel geld geleend hebt. Geld kunt u"
3270 PRINT"lenen door op L te duwen."
3280 GOSUB10010:REMblz
3290 PRINT"Wilt u de handleiding nog een keer"
3300 PRINT"doornemen ? (j/n)":GOSUB250
3310 GOSUB200
3320 IFIN=74 THEN2310:REMJ
3330 IFIN=78 THEN3350:REMN
3340 GOTO3310
3350 GOSUB100
3360 HO=7:VE=10:GOSUB110
3370 PRINT"VEEL SUCCES !!!"
3380 GOSUB13010:REMwachtlus
3390 RETURN
3400 REM***moeilijkheidsgraad***
3410 GOSUB100
3420 HO=0:VE=15:GOSUB110
3430 PRINT"Moeilijkheidsgraad"
3440 PRINT"1. makkelijk"
3450 PRINT"2. moeilijk"
3460 PRINT"3. zeer moeilijk"
3470 PRINT
3480 PRINT"moeilijkheidsgraad?":GOSUB250
3490 GOSUB210
3500 DF=IN-48
3510 IF(DF=1)OR(DF=2)OR(DF=3) THEN3530
3520 GOTO3490
3530 GOSUB100
3540 RETURN
3600 REM***markt-onderzoek***
3610 GOSUB13030:REMwis tekst
3620 HO=0:VE=21:GOSUB110
3630 PRINT"Markt onderzoek"
3640 GOSUB13010:REMwachtlus
3650 GOSUB8610:REMplaatsbepaling/invoer
3660 HO=2:VE=22:GOSUB110
3670 PRINT"Vestigingsplaats is ";
3680 IFA(X,Y)=1 THENPRINT"ongunstig"
3690 IFA(X,Y)=2 THENPRINT"geschikt"
3700 IFA(X,Y)=3 THENPRINT"zeer geschikt"
3710 IF(A(X,Y)>3)AND(A(X,Y)<=30) THEN GOSUB3850
3720 LA=LA-0.2
3730 GOSUB13010:REMwachtlus
3740 GOSUB13030:REMwis tekst
3750 HO=0:VE=19:GOSUB110
3760 PRINT"Nog een markt-onderzoek? (j/n)":GOSUB250
3770 GOSUB210
3790 IFIN=78 THEN3820:REMN
3800 IFIN=74 THEN3610:REMJ
3810 GOTO3770
3820 GOSUB8010:REMprint financ
3830 GOSUB13030:REMwis tekst
3840 GOTO1510:REMspelloop
3850 HO=0:VE=19:GOSUB110
3860 PRINT"Markt-onderzoek kost $1.2 m."
3870 LA=LA-1
3880 HO=23:VE=22:GOSUB110
3890 IFA(X,Y)=10 THENPRINT"ongunstig"
3910 IFA(X,Y)=20 THENPRINT"geschikt"
3930 IFA(X,Y)=30 THENPRINT"zeer geschikt"
3940 RETURN
4000 REM***lening***
4010 GOSUB13030:REMwis tekst
4020 HO=0:VE=19:GOSUB110
4030 PRINT"Lening ?"
4040 PRINT"Geef bedrag in miljoenen guldens";:GOSUB250
4050 INPUTJ$
4060 BE=VAL(J$)
4070 IF(BE<1)OR(BE>=30) THEN4020
4080 PRINT"f ";J$;" miljoen gevraagd"
4090 GOSUB260
4100 LS=1+(INT(RV*2)*BE)
4110 IFLS>40 THENPRINT"Lening niet gegeven":GOTO4140
4120 IFLS<40 THENPRINT"Lening gegeven"
4130 IFLS<40 THENLA=LA+BE:MX=MX+BE
4140 GOSUB8010:REMprint financ
4150 IFLS>=40 THEN GOSUB13010:REMwis regel
4160 GOSUB13010:REMwachtlus
4170 GOSUB13030:REMwis tekst
4180 GOTO1510:REMspelloop
4200 REM***aanvraag vergunning***
4210 GOSUB13030:REMwis tekst
4220 HO=0:VE=19:GOSUB110
4230 PRINT"Waar wilt u een vergunning aanvragen "
4240 GOSUB8620:REMplaatsbepaling/invoer
4250 IFA(X,Y)<10 THEN4370
4260 LD=ABS(X-2):RW=ABS(Y-20)
4270 IFRW>LD THEN4290
4280 IFLD>RW THENLA=LA-LD*0.2:GOTO4310
4290 IFY<=6 THENRW=RW*2
4300 LA=LA-RW*0.2
4310 LA=LA-LD*0.2
4320 A(X,Y)=A(X,Y)*10
4330 HO=X-1:VE=Y-1:GOSUB110
4340 PRINT"B"
4350 GOSUB8010:REMprint financ
4360 GOTO1510:REMspelloop
4370 HO=0:VE=20:GOSUB110
4380 PRINT"Deze grond is niet van u"
4390 GOSUB13010:REMwachtlus
4400 GOTO1510:REMspelloop
4500 REM***bouwen***
4510 GOSUB13030:REMwis tekst
4520 HO=0:VE=19:GOSUB110
4530 PRINT"Bouwplaats"
4540 GOSUB8620:REMplaatsbepaling/invoer
4550 IFA(X,Y)<100 THEN5010
4560 GOSUB260
4570 T(1)=T(1)+1
4580 D(X,Y)=D(X,Y)+INT(RV*6)+1
4590 HO=0:VE=19:GOSUB110
4600 PRINT"Hoogte gebouw nu : ";D(X,Y);" meter"
4610 C(1)=INT(D(X,Y)/3)+1:LA=LA-C(1)
4620 PRINT"Bouwkosten = ";C(1);" miljoen gulden"
4630 IFD(X,Y)>20 THENPRINT"Gebouw ingestort"
4640 IFD(X,Y)>20 THEN GOSUB9210:GOTO4930
4650 GOSUB260
4660 PO=RV*A(X,Y)*D(X,Y)
4670 PRINT"Vordering bouw :  ";
4680 IFPO>200*(1+T(1)/10) THEN4740
4690 UX=200*(1+T(1)/10):UY=140*(1+T(1)/20)
4700 IF(PO<UX)AND(PO>UY) THENPRINT" redelijk";
4710 IFPO<=140*(1+T(1)/10) THENPRINT" snel"
4720 GOSUB13010:REMwachtlus
4730 GOTO4930
4740 GOSUB13010
4750 FORZ=1 TO5
4760 GOSUB13030
4770 HO=3:VE=21:GOSUB110
4780 PRINT"Gebouw klaar"
4790 NEXTZ
4800 A(X,Y)=A(X,Y)*10
4810 HO=X-1:VE=Y-1:GOSUB110
4820 PRINT"V"
4830 GOSUB260
4840 RF=1+INT(RV*9)
4850 GOSUB260
4860 AC=1+INT(RV*20)/10
4870 HO=0:VE=19:GOSUB110
4880 IFRF=1 THENPRINT"Vervanging bouwkraan f";
4890 IFRF=1 THENPRINTAC/2;"m.":LA=LA-AC/2
4900 IFRF=2 THENPRINT"Ongeluk op bouwplaats kosten f";
4910 IFRF=2 THENPRINTAC;"m.":LA=LA-AC
4920 IFRF=4 THEN5110
4930 GOSUB8010:REMprint financ
4940 GOSUB13030:REMwis tekst
4950 GOTO1510:REMspelloop
5000 REM***geen vergunning***
5010 HO=1:VE=20:GOSUB110
5020 PRINT"Geen vergunning verleend"
5030 GOSUB13010:REMwachtlus
5040 GOTO1510:REMspelloop
5100 REM***ontploffing***
5110 HO=1:VE=22:GOSUB110
5120 A(X,Y)=A(X,Y)/10:LA=LA-20
5130 GOSUB13010:REMwachtlus
5140 GOTO1510:REMspelloop
5200 REM***vestigen verkooppunt***
5210 GOSUB13030:REMwis tekst
5220 HO=1:VE=19:GOSUB110
5230 PRINT"Vestiging verkooppunt"
5240 GOSUB8620:REMplaatsbepaling/invoer
5250 IFA(X,Y)<1000 THEN5710
5260 PP=20:IFY<=6 THENPP=30
5270 HO=1:VE=21:GOSUB110
5280 IFLA<PP THENPRINT"Onvoldoende geld":GOTO5890
5290 LA=LA-PP
5300 GOSUB260
5310 AC=1+INT(RV*20)/10
5320 GOSUB260
5330 RF=1+INT(RV*9)
5340 IFRF=1 THEN5810
5350 HO=0:VE=21:GOSUB110
5360 IFRF=2 THENPRINT"Technisch probleem-grondwater"
5370 IFRF=2 THENPRINT"Kosten ";AC;
5380 IFRF=2 THENPRINT"miljoen gulden"
5390 IFRF=2 THENLA=LA-AC
5400 IFRF=3 THENPRINT"Brand beschadigt bouw kosten f";
5410 IFRF=3 THENPRINTAC*4;" miljoen":LA=LA-AC*4
5420 IF(RF=2)OR(RF=3) THEN GOSUB13010:GOSUB13030
5430 GOSUB260
5440 BU=25+INT(RV*200)
5450 GOSUB260
5460 RY=BU*INT(30+RV*15)/1000
5470 HO=1:VE=20:GOSUB110
5480 PRINT"Kosten f";PP;" miljoen":PX=PX+RY
5490 HO=1:VE=21:GOSUB110
5500 PRINT"Opslagruimte voor ";(BU*1000);" computers"
5510 HO=1:VE=22:GOSUB110
5520 PRINT"Opbrengst f";INT(RY*10)/10;
5530 PRINT" m.":BX=BX+BU:WX=WX+1:A(X,Y)=A(X,Y)*10
5540 HO=X-1:VE=Y-1:GOSUB110
5550 PRINT"U"
5560 PZ=PZ+PP
5570 GOSUB8010:REMprint financ
5580 GOSUB13010:REMwachtlus
5590 GOSUB13030:REMwis tekst
5600 GOTO1510:REMspelloop
5700 REM***Bouw nog niet voltooid***
5710 HO=1:VE=21:GOSUB110
5720 PRINT"Bouw nog niet voltooid"
5730 GOSUB13010:REMwachtlus
5740 GOSUB13030:REMwis tekst
5750 GOTO1510:REMspelloop
5800 REM***Gebouw verwoest***
5810 FORZ=1 TO16
5820 HO=1:VE=21:GOSUB110
5830 PRINT"               "
5840 GOSUB110
5850 PRINT"Ontploffing"
5860 NEXTZ
5870 HO=1:VE=22:GOSUB110
5880 PRINT"Gebouw verwoest":LA=LA-20
5890 GOSUB8010:REMprint financ
5900 GOSUB13030:REMwis tekst
5910 GOTO1510:REMspelloop
6000 REM***uitbreiden***
6010 GOSUB13030:REMwis tekst
6020 HO=1:VE=21:GOSUB110
6030 PRINT"Vestiging uitbreiden"
6040 GOSUB8620:REMplaatsbepaling/invoer
6050 IFA(X,Y)<9999 THEN6410
6060 LD=ABS(X-2):RW=ABS(Y-20)
6070 IFRW>LD THEN6090
6080 IFLD>RW THENPL=LD*0.4:GOTO6110
6090 IFY<=6 THENRW=RW*2
6100 PL=RW*0.4
6110 HO=1:VE=21:GOSUB110
6120 IFLA<PL THENPRINT"Onvoldoende geld":GOTO6580
6130 LA=LA-PL
6140 GOSUB260
6150 RF=1+INT(RV*6)
6160 IFRF=2 THEN6710
6170 HO=1:VE=21:GOSUB110
6180 SR$="Uitbreiding 1.5 m duurder als begroot"
6190 IFRF=3 THENPRINTSR$
6200 IFRF=3 THENLA=LA-1.5:GOSUB13010:GOSUB13030
6210 GOSUB260
6220 BU=25+INT(RV*100)
6230 GOSUB260
6240 RY=BU*INT(30+RV*15)/1000
6250 PY=PY+PL:BX=BX+BU:PX=PX+RY
6260 HO=1:VE=20:GOSUB110
6270 PRINT"Kosten uitbreiding f";PL;" m."
6280 HO=1:VE=21:GOSUB110
6290 PRINT"Toename opslagruimte ";BU*100;" computers"
6300 HO=1:VE=22:GOSUB110
6310 PRINT"Inkomsten f";INT(RY*10)/10;" m."
6320 HO=X-1:VE=Y-1:GOSUB110
6330 PRINT"U"
6340 GOSUB8010:REMprint financ
6350 GOSUB13030:REMwis tekst
6360 GOTO1510:REMspelloop
6400 REM***nog geen vestiging***
6410 HO=1:VE=21:GOSUB110
6420 PRINT"Nog geen vestiging"
6430 GOSUB13010:REMwachtlus
6440 GOSUB13030:REMwis tekst
6450 GOTO1510:REMspelloop
6500 REM***vandalisme***
6510 FORZ=1 TO16
6520 HO=1:VE=21:GOSUB110
6530 PRINT"        "
6540 GOSUB110
6550 PRINT"Vandalisme"
6560 NEXTZ
6570 HO=1:VE=22:GOSUB110
6580 GOSUB13010:REMwachtlus
6590 GOSUB13030:REMwis tekst
6600 GOTO1510:REMspelloop
6700 REM***storm***
6710 FORZ=1 TO16
6720 HO=2:VE=21:GOSUB110
6730 PRINT"                         "
6740 GOSUB110
6750 PRINT"Er heeft een storm gewoed"
6760 NEXTZ
6770 HO=2:VE=22:GOSUB110
6780 PRINT"Vestiging is totaal vernield"
6790 GOSUB13010:REMwachtlus
6800 IFPZ>30 THENPZ=PZ-20
6810 IFPZ<=30 THENPZ=0
6820 A(X,Y)=A(X,Y)/10
6830 GOSUB9410:REMopgeven vestiging
6840 HO=X-1:VE=Y-1:GOSUB110
6850 PRINT"B"
6860 GOSUB8010:REM print financ
6870 GOSUB13030:REMwis tekst
6880 GOTO1510:REMspelloop
7000 REM***speelveld printen***
7010 HO=2:VE=0:GOSUB110
7020 PRINT"ABCDEFGHIJKLMNOPQRS"
7030 FORQ=3 TO16
7040 HO=0:VE=Q-1:GOSUB110
7050 PRINTCHR$(ASC("a")-3+Q)
7060 NEXTQ
7070 PRINT
7080 GOSUB8010:REMprint financ
7090 RETURN
7100 GOSUB13030:REMwis tekst
7110 IFT<10 THEN7410:REMverkoop grond
7200 REM***exploitatieoverzicht***
7210 HO=1:VE=18:GOSUB110
7220 PRINT"EXPLOITATIEOVERZICHT"
7230 PRINT"Aantal percelen ";CX
7240 PRINT"Aantal vestigingen";WX
7250 GOSUB8010:REM print financ
7260 HO=0:VE=21:GOSUB110
7270 PRINT"Opslagruimte voor ";BX*1000;
7280 PRINT" computers"
7290 PRINT"Activa Datachain f";CP;" miljoen"
7300 SD=10:GOSUB450
7310 GOSUB13010:REMwachtlus
7320 GOSUB13030:REMwis tekst
7330 GOTO1510:REMspelloop
7400 REM***verkoop grond***
7410 GOSUB13030:REMwis tekst
7420 HO=0:VE=19:GOSUB110
7430 PRINT"Verkoop van grond"
7440 GOSUB260
7450 UU=1+RV*(4-DF)
7460 FORU=1 TOUU
7470 GOSUB260
7480 Y=3+INT(RV*13)
7490 GOSUB260
7500 X=3+INT(RV*18)
7510 IFK(X,Y)=1 THEN7480
7520 FORF=1 TO40
7530 HO=X-1:VE=Y-1
7540 GOSUB110:PRINT" "
7550 GOSUB110:PRINT"*"
7570 NEXTF
7580 HO=1:VE=20:GOSUB110
7590 PRINT"Je bod";:GOSUB250
7600 INPUTV
7610 PRINTV;" m."
7620 GOSUB260
7630 CB=INT(A(X,Y)*(1+RV*4))
7640 HO=1:VE=22:GOSUB110
7650 IFV>LA THENPRINT"Onvoldoende geld"
7660 IFV>=CB THEN7720
7670 IFV<CB THENPRINT"Bod te laag";
7680 HO=X-1:VE=Y-1:GOSUB110
7690 IF(V<CB)OR(V>LA) THENPRINT" "
7700 IF(V<CB)OR(V>LA) THEN GOSUB13010:REMwachtlus
7710 GOTO7800
7720 HO=1:VE=22:GOSUB110
7730 PRINT"Geslaagd bod"
7740 CX=CX+1:A(X,Y)=A(X,Y)*10
7750 HO=X-1:VE=Y-1:GOSUB110
7760 PRINT"G"
7770 K(X,Y)=1
7780 LA=LA-V:CY=CY+V
7790 GOSUB8010:REMprint financ
7800 GOSUB13030:REMwis tekst
7810 NEXTU
7820 GOTO1510:REMspelloop
8000 REM***print financ***
8010 HO=22:VE=0:GOSUB110
8020 PRINT"Financieel"
8030 VE=1:GOSUB110
8040 PRINT"overzicht."
8050 VE=2:GOSUB110
8060 PRINT"in miljoenen"
8070 VE=4:GOSUB110
8080 PRINT"ACTIVA"
8090 VE=5:GOSUB110
8100 PRINT"Percelen   ";
8110 VV=CY:SR=VV:GOSUB300
8120 CT=6:CN=2:GOSUB310:PRINTSR$
8130 VE=6:GOSUB110
8140 PRINT"Filialen   ";
8150 VV=PY:SR=VV:GOSUB300
8160 GOSUB310:PRINTSR$
8170 VE=7:GOSUB110
8180 PRINT"Vestigingen";
8190 VV=PZ:SR=VV:GOSUB300
8200 GOSUB310:PRINTSR$
8210 VE=8:GOSUB110
8220 PRINT"Contant  ";
8230 VV=LA:SR=VV:GOSUB300
8240 CT=8:GOSUB310:PRINTSR$
8250 TX=LA+PZ+PY+CY
8260 VE=9:GOSUB110
8270 PRINT"Leningen  -";
8280 VV=MX:SR=VV:GOSUB300
8290 CT=6:GOSUB310:PRINTSR$
8300 NA=TX-MX
8310 VE=11:GOSUB110
8320 PRINT"TOTAAL:  ";
8330 VV=NA:SR=VV:GOSUB300
8340 CT=8:GOSUB310:PRINTSR$
8350 VE=13:GOSUB110
8360 PRINT"Balans "
8370 VE=14:GOSUB110
8380 PRINT"Kapitaal ";
8390 VV=SW:SR=VV:GOSUB300
8400 GOSUB310:PRINTSR$
8410 VE=15:GOSUB110
8420 PRINT"Winst    ";
8430 EN=NA-SW:VV=EN:SR=VV:GOSUB300
8440 GOSUB310:PRINTSR$
8450 VE=16:GOSUB110
8460 PRINT"TOTAAL:  ";
8470 VV=NA:SR=VV:GOSUB300
8480 GOSUB310:PRINTSR$
8490 RETURN
8500 REM***telex***
8510 FORTT=1 TO16
8520 HO=3:VE=21:GOSUB110
8530 PRINT"Telex mededeling":GOSUB250
8540 GOSUB110
8550 PRINT"                "
8560 NEXTTT
8570 RETURN
8600 REM***plaatsbepaling***
8610 GOSUB13030:REMwis tekst
8620 HO=1:VE=22:GOSUB110:GOSUB250
8630 PRINT"Geef lijn no. ";
8640 GOSUB210:Y=IN
8660 IFY<97 THENY=Y+32
8670 Y=Y-94
8680 IF(Y<3)OR(Y>16) THEN8640
8690 HO=16:VE=22:GOSUB110
8700 PRINTIN$
8710 HO=18:GOSUB110
8720 PRINT"geef kolom no.";:GOSUB250
8730 GOSUB210:X=IN
8750 IFX<97 THENX=X+32
8760 X=X-94
8770 IF(X<3)OR(X>21) THEN8730
8780 HO=33:VE=22:GOSUB110
8790 PRINTIN$
8800 FORZ=1 TO1500:NEXTZ
8810 GOSUB13030:REMwis tekst
8820 RETURN
8900 REM***allerlei ellende***
8910 GOSUB260
8920 RF=1+INT(RV*3)
8930 HO=0:VE=22:GOSUB110
8940 SR$="Grote partij computers gedumpd"
8950 IFRF=1 THENPRINTSR$
8960 IFRF=1 THENPRINT"weinig winst deze maand";:RX=4
8970 IFRF=2 THENPRINT"ongunstige Dollarkoers"
8980 IFRF=2 THENPRINT"opbrengst een derde";:PX=PX/3
8990 IFRF=3 THENPRINT"stijging  rente kosten f";
9000 IFRF=3 THENPRINTINT(MX/4)/10;
9010 IFRF=3 THENPRINT"m.":LA=LA-(INT(MX/4)/10)
9020 GOSUB13010:REMwachtlus
9030 GOSUB13030:REMwis tekst
9040 RETURN
9100 REM***nog meer ellende***
9110 HO=0:VE=19:GOSUB110
9120 PRINT"Gemeente onteigent grond"
9130 PRINT"Opbrengst :";TX*3;" m.":LA=LA+(TX*3)
9140 GOSUB9210:REMwaarden velden
9150 GOTO1510:REMspelloop
9200 REM***waarden velden***
9210 IF(A(X,Y)>=10)AND(CY>=8) THENCY=CY-8
9220 IF(D(X,Y)>20)AND(CY>=8) THENCY=CY-8
9230 IF(A(X,Y)>=10)AND(CY<8) THENCY=0
9240 IF(D(X,Y)>20)AND(CY<8) THENCY=0
9250 IF(A(X,Y)>=10000)AND(PZ>=30) THENPZ=PZ-20
9260 IF(A(X,Y)>=10000)AND(PZ<30) THENPZ=0
9270 IF(A(X,Y)>=100000)AND(PY>=2) THENPY=PY-2
9280 IF(A(X,Y)>=100000)AND(PY<2) THENPY=0
9290 IF(A(X,Y)>=10000) THENWX=WX-1
9300 IF(D(X,Y)>20)OR(A(X,Y)>=10) THENCX=CX-1
9310 A(X,Y)=1
9320 GOSUB9410:REMopgeven veld
9330 GOSUB8010:REMprint financ
9340 GOSUB13030:REMwis tekst
9350 RETURN
9400 REM***opgeven veld***
9410 FORZ=1 TO40
9420 HO=X-1:VE=Y-1:GOSUB110
9430 PRINT"*"
9440 GOSUB110
9450 PRINT" "
9460 NEXTZ
9470 RETURN
9500 REM***bepalen tijdstip ellende***
9510 GOSUB260
9520 CP=CP+INT(RV*CP/5)
9530 IFMX>20 THEN GOSUB8910
9540 IFCP>INT(4*NA)/DF THEN9810:REMik win
9550 RETURN
9600 REM***jij wint***
9610 GOSUB100
9620 GOSUB8510:REMprint telex
9630 PRINT"U hebt Datachain overgenomen."
9640 PRINT"Netto activa ten bedrage van f";
9650 PRINTINT(CP);" m."
9660 SD=10:GOSUB450
9670 GOSUB13010:REMwachtlus
9680 NA=NA+CP:SD=10:GOSUB450
9700 REM***nog een keer?***
9710 GOSUB100
9720 PRINT"U had er ";T;" maanden voor nodig"
9730 PRINT"Moeilijkheidsgraad ";DF
9740 PRINT"Uw totale activa f";INT(NA);" m."
9750 PRINT"Wilt u nog een keer spelen? (j/n)":GOSUB250
9760 GOSUB210:IFIN=74 THEN1000
9770 IFIN<>78 THEN9760
9780 PRINT"Doei ...":SD=10:GOSUB450:GOTO950
9800 REM***ik win***
9810 GOSUB8510
9820 PRINT"Datachain heeft uw bedrijf overgenomen"
9830 FORZ=1 TO1000:NEXTZ
9840 GOSUB13010:GOSUB13010:GOSUB100
9850 PRINT"Datachain activa f";INT(CP);" m."
9860 GOTO9710
10000 REM***bladzijde omslaan***
10010 HO=0:VE=22:GOSUB110
10020 PRINT"Druk een toets in ...";
10030 IN$="":HO=36:GOSUB110:PRINT"=>";
10040 FORT=1 TO100:NEXTT
10050 GOSUB200:IFIN<>0 THEN GOSUB100:RETURN
10060 GOSUB110:PRINT"  ";
10070 FORT=1 TO100:NEXTT:GOTO10010
11000 REM***TITEL***
11010 GOSUB600:CN=0
11020 HO=0.2:VE=0.1:GOSUB620
11030 HO=0.8:GOSUB630:VE=0.6:GOSUB630
11040 HO=0.2:GOSUB630:VE=0.1:GOSUB630
11050 HO=0.13:VE=0.04:GOSUB620
11060 HO=0.87:GOSUB630:VE=0.66:GOSUB630
11070 HO=0.13:GOSUB630:VE=0.04:GOSUB630
11080 HO=0.13:VE=0.66:GOSUB620
11090 VE=0.9:HO=0.02:GOSUB630
11100 HO=0.98:GOSUB630:HO=0.87:VE=0.66:GOSUB630
11110 SR$="O O O O O O O O   O O O"
11120 HO=0.17:VE=0.72:GOSUB650
11130 SR$="O O O O O O O O O"
11140 HO=0.15:VE=0.77:GOSUB650
11150 SR$="O O O"
11160 HO=0.7:GOSUB650
11170 SR$="O O O O O O O O O O O O"
11180 HO=0.17:VE=0.82:GOSUB650
11190 SR$="Computerhandel"
11200 HO=0.3:VE=0.3:GOSUB650
11210 SR$="Sjef Simons 1988"
11220 HO=0.27:VE=0.4:GOSUB650
11230 RETURN
13000 REM***Wachtlus***
13010 SD=25:GOSUB450:RETURN
13020 REM***wis tekst***
13030 SR$="                                       "
13040 HO=0:VE=18:GOSUB110:PRINTSR$
13050 PRINTSR$
13060 PRINTSR$
13070 PRINTSR$
13080 PRINTSR$:PRINTSR$;
13090 RETURN
30000 REM
30010 REM************************
30020 REM*                      *
30030 REM*  COMPUTERHANDEL      *
30040 REM*                      *
30050 REM*  GESCHREVEN OP EEN   *
30060 REM*  SCHNEIDER CPC 464   *
30070 REM*  AUGUSTUS 1985       *
30080 REM*  OMGEWERKT NAAR      *
30090 REM*  BASICODE-3          *
30100 REM*  APRIL 1988          *
30110 REM*                      *
30120 REM************************
30130 REM*                      *
32000 REM*  AUTEUR:             *
32010 REM*                      *
32020 REM*  J.P.M. SIMONS       *
32030 REM*  STATIONSSTRAAT 11   *
32040 REM*  4872 TA ETTEN-LEUR  *
32050 REM*                      *
32060 REM************************
32070 REM
32080 REM TROS-RADIO dd 881214
32090 REM incl corr. dd 881228

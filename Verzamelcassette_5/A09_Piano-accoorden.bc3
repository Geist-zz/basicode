
1000 A=20:GOTO20:REM *** ACCOORDEN SPELEN ***
1010 VT=VE:REM schermhoogte
1020 REM *** UITLEG ***
1030 GOSUB4010:PRINT:PRINT
1040 PRINT:PRINT"Met dit programma kunt u een accoord"
1050 PRINT:PRINT"leren spelen m.b.v. de afbeelding"
1060 PRINT:PRINT"van pianotoetsen."
1070 GOSUB6010
2000 GOSUB100:REM *** MENU ***
2010 PRINT"Maak een keuze:"
2020 PRINT:PRINT"====================================="
2030 PRINT:PRINT"        1 ........ majeur"
2040 PRINT:PRINT"        2 ........ mineur"
2050 PRINT:PRINT"        3 ........ majeur-7"
2060 PRINT:PRINT"        4 ........ mineur-7"
2070 PRINT:PRINT"        5 ........ majeur-6"
2080 PRINT:PRINT"        6 ........ mineur-6"
2090 PRINT:PRINT"        7 ........ verminderd"
2100 PRINT:PRINT"        8 ........ overmatig"
2110 PRINT:PRINT"====================================="
2120 PRINT:PRINT"KEUZE =>";:INPUTKZ
2130 IF(KZ<1)OR(KZ>8) THEN GOSUB250:GOTO2000
2140 ONKZ GOSUB3020,3030,3040,3050,3060,3070,3080,3090
2150 VE=22:HO=0:GOSUB110:PRINT"TOONSOORT:";
2160 INPUTSR$:GOSUB330:IN$=SR$
2170 REM *** TOONSOORTBEPALING ***
2180 T=0
2190 IF(IN$="C")OR(IN$="BIS") THENT=1
2200 IF(IN$="CIS")OR(IN$="DES") THENT=2
2210 IFIN$="D" THENT=3
2220 IF(IN$="ES")OR(IN$="DIS") THENT=4
2230 IF(IN$="E")OR(IN$="FES") THENT=5
2240 IF(IN$="F")OR(IN$="EIS") THENT=6
2250 IF(IN$="FIS")OR(IN$="GES") THENT=7
2260 IFIN$="G" THENT=8
2270 IF(IN$="GIS")OR(IN$="AS") THENT=9
2280 IF(IN$="A") THENT=10
2290 IF(IN$="AIS")OR(IN$="BES") THENT=11
2300 IF(IN$="B")OR(IN$="CES") THENT=12
2310 IFT<>0 THEN2510
2320 GOSUB250:VE=VT:HO=0:GOSUB110
2330 PRINT"DIE TOONSOORT BESTAAT NIET !!!";
2340 SD=200:GOSUB450
2350 GOSUB6010:GOTO2000
2500 REM ** ACCOORDEN OP TOETSEN **
2510 GOSUB100:GOSUB4010
2520 HO=A+T:GOSUB5010
2530 HO=B+T:GOSUB5010
2540 HO=C+T:GOSUB5010
2550 HO=D+T:GOSUB5010
2560 VE=15:HO=0:GOSUB110
2570 PRINT"Druk op een toets om verder te gaan";:GOSUB200
2580 IFIN=0 THEN2520
2590 VE=18:HO=0:GOSUB110
2600 PRINT"         DOORGAAN = D"
2610 PRINT
2620 PRINT"         STOPPEN  = S";
2630 GOSUB210
2640 IFIN=68 THEN2000
2650 IFIN<>83 THEN GOSUB250:GOTO2630
2660 GOSUB100:GOSUB4010
2670 VE=10:HO=0:GOSUB110
2680 PRINT"*************************************"
2690 PRINT"* Dit programma werd ontworpen door *"
2700 PRINT"* Walther van den Elshout; Maart'88 *"
2710 PRINT"* en vertaald naar basicode-3 op    *"
2720 PRINT"* 23 oktober 1988                   *"
2730 PRINT"*************************************"
2740 GOSUB210:GOTO950
3000 REM * ACCOORDENBEPALING OP *
3010 REM * VOLGORDE VAN HET MENU *
3020 A=1:B=5:C=8:D=13:RETURN
3030 A=1:B=4:C=8:D=13:RETURN
3040 A=1:B=5:C=8:D=11:RETURN
3050 A=1:B=4:C=8:D=11:RETURN
3060 A=1:B=5:C=8:D=10:RETURN
3070 A=1:B=4:C=8:D=10:RETURN
3080 A=1:B=4:C=7:D=10:RETURN
3090 A=1:B=5:C=9:D=13:RETURN
4000 REM *** PIANOKLAVIER ***
4010 PRINT"+-%-%-+-%-%-%-+-%-%-+-%-%-%-+ #########"
4020 PRINT"! % % ! % % % ! % % ! % % % ! ACCOORDEN"
4030 PRINT"! % % ! % % % ! % % ! % % % !  SPELEN"
4040 PRINT"! % % ! % % % ! % % ! % % % !    OP"
4050 PRINT"! ! ! ! ! ! ! ! ! ! ! ! ! ! !   PIANO"
4060 PRINT"! ! ! ! ! ! ! ! ! ! ! ! ! ! !  TOETSEN"
4070 PRINT"+---------------------------+ #########"
4080 RETURN
5000 REM *** TOETSEN CORRECTIE ***
5010 IFHO>6 THENHO=HO+1
5020 IFHO>14 THENHO=HO+1
5030 IFHO>20 THENHO=HO+1
5040 IFHO>28 THENHO=HO+1
5050 REM *** HELE/HALVE NOTEN ***
5060 VE=5
5070 IFHO<>2*INT(HO/2) THENVE=3
5080 HO=HO-1
5090 GOSUB110:PRINT"*"
5100 VE=7:GOSUB110:PRINT"^"
5110 SD=10:GOSUB450
5120 GOSUB110:PRINT"."
5130 REM *** TOON LATEN HOREN ***
5140 SD=5:SV=15
5150 IFHO>5 THENHO=HO-1
5160 IFHO>13 THENHO=HO-1
5170 IFHO>18 THENHO=HO-1
5180 SP=HO+47:GOSUB400
5190 SP=HO+59:GOSUB400:RETURN
6000 REM *** SPATIEBALK ***
6010 VE=VT:HO=0:GOSUB110
6020 PRINT"============= SPATIEBALK ============";
6030 GOSUB210:IFIN=32 THENRETURN
6040 GOSUB250:GOTO6030
30000 REM ***************************
30010 REM * ACCOORDEN OP PIANOTOETS *
30020 REM * EERSTE ONTWERP OP AQUA- *
30030 REM * RIUS 32K IN BASICODE-2  *
30040 REM * HUIDIGE ONTWERP OP COM- *
30050 REM * MODORE 64 IN BASICODE-3 *
30060 REM ***************************
30070 REM
32000 REM ***************************
32010 REM * WALTHER VAN DEN ELSHOUT *
32020 REM * GEN.WICHERSLAAN 14      *
32030 REM * 5623 GS  EINDHOVEN      *
32040 REM * TEL: 040 - 451098       *
32050 REM ***************************
32060 REM
32070 REM TROS-RADIO dd 881130

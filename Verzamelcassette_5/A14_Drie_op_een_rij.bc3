
1000 A=250:GOTO20:REM *** 3 OP EEN RIJ ***
1010 DIMC(3),N(40),M(40),VV(40),HH(40),G(36)
1020 N3$="...........":N4$=" zet een "
1030 KR$="KRUISJE  op:":VI$="VIERKANT op:"
1040 GOSUB100
1050 HO=13:VE=10:GOSUB110:PRINT"Uitleg  <J/N>"
1060 GOSUB210:IFIN=74 THEN GOSUB8000:GOTO1080
1070 IFIN<>78 THEN1050
1080 GOSUB100:HO=0:VE=6:GOSUB110
1090 N1$=" Speler 1":N2$=" Speler 2"
1100 INPUT"Voornaam speler 1 ";N1$:N1$=LEFT$(N1$+N3$,11)
1110 PRINT:INPUT"Voornaam speler 2 ";N2$
1120 N2$=LEFT$(N2$+N3$,11)
1130 GOSUB100:HO=7:VE=6:GOSUB110
1140 PRINT"1 - Getallen op volgorde"
1150 HO=7:VE=8:GOSUB110
1160 PRINT"2 - Getallen door elkaar"
1170 HO=14:VE=12:GOSUB110
1180 PRINT"Kies 1 of 2"
1190 GOSUB210:IF(IN<49)OR(IN>50) THEN1170
1200 AA=IN-48
1210 P=0:KR=-1:GOSUB100
1220 CN=0:GOSUB600
1230 VE=0:HO=.21:SR$="*** 3 OP EEN RIJ ***":GOSUB650
1240 VE=.04:SR$="====================":GOSUB650
1250 HO=.07:V2=.15+3*.089:H2=.07+12*.0695
1260 FORX=0 TO13
1270 VE=.15:GOSUB620:VE=V2:GOSUB630
1280 H1=X*.0695
1290 HO=.07+H1:NEXTX
1300 VE=.15
1310 FORX=0 TO4
1320 HO=.07:GOSUB620:HO=H2:GOSUB630
1330 V1=X*.09:VE=.15+V1:NEXTX
1340 VE=.18:HO=.075:A=0
1350 FORX=1 TO36:G(X)=X:NEXTX
1360 FORW=1 TO36:IFAA=1 THENQ=W:GOTO1400
1370 GOSUB260:Q=INT(RV*36)+1
1380 IFG(Q)=0 THEN1370
1390 G(Q)=0
1400 SR=Q:GOSUB300:GOSUB650
1410 VV(Q)=VE:HH(Q)=HO:N(W)=Q:M(W)=Q
1420 A=A+1:VE=VE+.0924
1430 IFA=3 THENHO=HO+.07:VE=.18:A=0
1440 NEXTW
1450 VE=.4725:HO=.2275
1460 FORX=1 TO3:GOSUB620
1470 HO=HO+.08:GOSUB630:VE=VE+.1:GOSUB630
1480 HO=HO-.08:GOSUB630:VE=VE-.1:GOSUB630
1490 HO=HO+.22:NEXTX
1500 GOSUB260:SP=INT(RV*2)
1510 V=0:PP=0:CN=0
1520 FORX=1 TO3:GOSUB260:C(X)=INT(RV*6)+1:NEXTX
1530 A=C(1):B=C(2):C=C(3)
1540 VE=.5:HO=.25
1550 SR=A:GOSUB300:GOSUB650:HO=HO+.225
1560 SR=B:GOSUB300:GOSUB650:HO=HO+.2225
1570 SR=C:GOSUB300:GOSUB650
1580 VE=.6375:HO=0:D=0
1590 SR$=N1$+N4$+VI$
1600 IFSP=1 THENSR$=N2$+N4$+KR$
1610 GOSUB650:SR$="":SD=900:REM 1.5 MINUUT
1620 GOSUB450
1630 IFSD=0 THENCN=1:GOSUB5040:GOTO1580
1640 VE=.6375:HO=.9:IFIN=13 THEN1680
1650 IF(IN-48<0)OR(IN-48>9) THEN1620
1660 SR$=SR$+IN$:GOSUB650
1670 GOTO1620
1680 G=VAL(SR$)
1690 IFG=0 THENCN=1:GOSUB5000:GOTO1510
1700 IFG>36 THEN GOSUB3000:SD=20:GOSUB450:CN=1
1710 IFG>36 THEN GOSUB5000:CN=1:GOSUB3000:GOTO1510
1720 IFN(G)=37 THEN GOSUB3200:SD=20:GOSUB450:CN=1
1730 IFN(G)=37 THEN GOSUB3200:GOSUB5000:GOTO1510
1740 GOSUB2000:IFD=-1 THEN GOSUB3100:SD=20:GOSUB450
1750 IFD=-1 THENCN=1:GOSUB3100:GOSUB5000:GOTO1510
1760 P=P+1:N(G)=37
1770 FORX=1 TO36
1780 IF(M(X)=G)AND(SP=0) THENM(X)=-1
1790 IF(M(X)=G)AND(SP=1) THENM(X)=-2
1800 NEXTX
1810 GOSUB6000:GOSUB4000
1820 IFPP=1 THEN GOSUB7000:GOTO1080
1830 GOSUB5000
1840 IFP=36 THEN GOSUB7050:GOTO1210
1850 GOTO1510
1860 D=0:V=V+1:IFN(V)=37 THEN1860
1870 IFV=37 THEN GOSUB3000:SD=10:GOSUB450
1880 IFV=37 THENCN=1:G=0:GOSUB3000:GOSUB5000:GOTO1510
1890 G=V:GOSUB2000
1900 IFD=-1 THEN1860
1910 GOSUB3100:SD=10:GOSUB450:CN=1:GOSUB3100
1920 REM
2000 AB=A*B:BC=B*C:AC=A*C:AG=A*G:BG=B*G:CG=C*G
2010 IF(G=C-A+B)OR(G=A+B+C)OR(G=A+B-C) THENRETURN
2020 IF(G*C=A+B)OR(G=AC+BC)OR(AG+BG=C) THENRETURN
2030 IF(G=A-B+C)OR(G=C-A+B)OR(G=A-B-C) THENRETURN
2040 IF(G=-A+B-C)OR(G=AC-BC)OR(CG=B-A) THENRETURN
2050 IF(BG=AC)OR(G=-AC+BC)OR(CG=A-B) THENRETURN
2060 IF(CG=B-AC)OR(C=AG-BG)OR(C=BG-AG) THENRETURN
2070 IF(G=C+AB)OR(G=AB*C)OR(G=C-AB) THENRETURN
2080 IF(G=AB-C)OR(CG=AB)OR(G*AB=C) THENRETURN
2090 IF(AG=C+AB)OR(BG=C+AB)OR(AG=C-AB) THENRETURN
2100 IF(BG=C-AB)OR(AG=AB-C)OR(BG=AB-C) THENRETURN
2110 IF(G=A+BC)OR(G=A-BC)OR(G=BC-A) THENRETURN
2120 IF(AG=BC)OR(G*BC=A)OR(CG=A+BC) THENRETURN
2130 IF(BG=A+BC)OR(BG=A-BC)OR(CG=A-BC) THENRETURN
2140 IF(BG=BC-A)OR(CG=BC-A) THENRETURN
2150 IF(G=B+AC)OR(G=B-AC)OR(G=AC-B) THENRETURN
2160 IF(G*AC=B)OR(CG=B+AC)OR(AG=B+AC) THENRETURN
2170 IF(AG=B-AC)OR(CG=AC-B)OR(AG=AC-B) THENRETURN
2180 IF(G=AB-BC)OR(G=AB-AC)OR(G=BC-AB) THENRETURN
2190 IF(G=AC-AB)OR(BG=A+C)OR(AG=B+C) THENRETURN
2200 IF(BG=A-C)OR(AG=B-C)OR(BG=C-A) THENRETURN
2210 IF(AG=C-B)OR(G=AB+BC)OR(G=AB+AC) THENRETURN
2220 IF(AG+CG=B)OR(BG+CG=A)OR(B=CG-AG) THENRETURN
2230 IF(B=AG-CG)OR(A=BG-CG)OR(A=CG-BG) THENRETURN
2240 D=-1:RETURN
2250 REM
3000 HO=.225:VE=.9:SR$="Niet groter dan 36!":GOSUB650
3010 RETURN
3020 REM
3100 HO=.3:VE=.9:SR$="Dat kan niet!":GOSUB650
3110 RETURN
3120 REM
3200 HO=.3:VE=.9:SR$="Is al geweest!":GOSUB650
3210 RETURN
3220 REM
4000 IFSP=1 THENKR=-2
4010 PP=0:X=1
4020 IFM(X)<>KR THEN4040
4030 IF(M(X+1)=KR)AND(M(X+2)=KR) THENX=34:PP=1:RETURN
4040 X=X+3:IFX<35 THEN4020
4050 X=1
4060 IFM(X)<>KR THEN4090
4070 IF(M(X+3)=KR)AND(M(X+6)=KR) THENX=28:PP=1:RETURN
4080 IF(M(X+4)=KR)AND(M(X+8)=KR) THENX=28:PP=1:RETURN
4090 X=X+3:IFX<29 THEN4060
4100 X=2
4110 IFM(X)<>KR THEN4130
4120 IF(M(X+3)=KR)AND(M(X+6)=KR) THENX=29:PP=1:RETURN
4130 X=X+3:IFX<30 THEN4110
4140 X=3
4150 IFM(X)<>KR THEN4180
4160 IF(M(X+3)=KR)AND(M(X+6)=KR) THENX=29:PP=1:RETURN
4170 IF(M(X+2)=KR)AND(M(X+4)=KR) THENX=29:PP=1:RETURN
4180 X=X+3:IFX<31 THEN4150
4190 RETURN
4200 REM
5000 VE=.5:HO=.25:CN=1
5010 SR=A:GOSUB300:GOSUB650:HO=HO+.225
5020 SR=B:GOSUB300:GOSUB650:HO=HO+.2225
5030 SR=C:GOSUB300:GOSUB650
5040 VE=.6375:HO=0
5050 SR$=N1$+N4$+VI$
5060 IFSP=1 THENSR$=N2$+N4$+KR$
5070 GOSUB650
5080 HO=.9:SR=G:GOSUB300:GOSUB650
5090 SP=SP+1:IFSP=2 THENSP=0:KR=-1
5100 CN=0:RETURN
5110 REM
6000 CN=1:VE=VV(G):HO=HH(G)
6010 SR=G:GOSUB300:GOSUB650
6020 CN=0:HO=HO+.0025
6030 VE=VE-.015
6040 IFSP=1 THEN6090
6050 GOSUB620:HO=HO+.05:GOSUB630
6060 VE=VE+.06:GOSUB630:HO=HO-.05:GOSUB630
6070 VE=VE-.06:GOSUB630
6080 RETURN
6090 GOSUB620:HO=HO+.05:VE=VE+.06:GOSUB630
6100 HO=HO-.05:GOSUB620:HO=HO+.05:VE=VE-.06:GOSUB630
6110 RETURN
6120 REM
7000 HO=.235:VE=.85:CN=0
7010 FORX=1 TO4:GOSUB250
7020 SR$="DRIE OP EEN RIJ!!!":GOSUB650:GOSUB250:CN=1
7030 SR$="DRIE OP EEN RIJ!!!":GOSUB650:CN=0
7040 NEXTX
7050 SR$="Nog een spel <J/N>":HO=.45:VE=.9
7060 IFP=36 THENSR$="Vol! GEEN winnaar. Nog een spel <J/N>"
7070 IFP=36 THENHO=0:GOSUB250
7080 GOSUB650:GOSUB210:IFIN=78 THEN950
7090 IFIN<>74 THEN7080
7100 RETURN
7110 REM
8000 GOSUB100
8010 HO=7:VE=0:GOSUB110
8020 SR$="* DRIE OP EEN RIJ *":GOSUB150
8030 HO=0:VE=3:GOSUB110
8040 PRINT:PRINT"De computer gooit drie dobbelstenen."
8050 PRINT:PRINT"Met de drie cijfers hiervan moet je"
8060 PRINT"een sommetje maken."
8070 PRINT:PRINT"Je mag optellen, aftrekken, vermenig-"
8080 PRINT"vuldigen en delen."
8090 PRINT:PRINT"Typ het antwoord in en druk <RETURN>"
8100 PRINT:PRINT"Wie het eerst DRIE OP EEN RIJ heeft"
8110 PRINT"is winnaar."
8120 PRINT:PRINT"Denk je dat er geen som te maken is,"
8130 PRINT"typ dan 0 in."
8140 PRINT:PRINT"Bedenktijd per som 90 seconden."
8150 HO=28:VE=23:GOSUB110
8160 PRINT"<TOETS>";
8170 GOSUB210:GOSUB100
8180 RETURN
30000 REM
30010 REM
30020 REM *** TROS BASICODE-3 ****
30030 REM *                      *
30040 REM *  *DRIE OP EEN RIJ*   *
30050 REM                        *
30060 REM P   INGEZONDEN DOOR :  *
30070 REM O
32000 REM S   P.A.KESTELOO       4
32010 REM T   REEDE 34           5
32020 REM B   3232 CV BRIELLE    0
32030 REM U
32040 REM S   TEL.01810-15569    *
32050 REM                        *
32060 REM *     20 OKT 1988      *
32070 REM *                      *
32080 REM *     COMMODORE 64     *
32090 REM *                      *
32100 REM ****** HILVERSUM *******
32110 REM
32120 REM TROS-RADIO dd. 890111


1000 A=600:GOTO20:REM Vugraf Bridge
1010 DIMV$(13),A$(14),U(14),C$(18),CM$(11),EX$(7),LI$(20)
1020 DIMRE$(2),DB$(2),UI$(2),KA(4),KB(4),H(4),V(4)
1030 DIMNA$(8),WE$(2),SP$(4)
1040 DIMB$(4),CO$(2),KA$(2),PO(2),VL(7),TR(4),SC(2)
1050 RL=HO:SH=VE:H(1)=19:H(2)=22:H(3)=19:H(4)=16
1060 V(1)=6:V(2)=8:V(3)=10:V(4)=8:U=0
1070 SP$(1)="noord":SP$(2)="oost"
1071 SP$(3)="zuid":SP$(4)="west"
1080 GOSUB100:PRINT"indien kwetsbaar * achter naam."
1090 PRINT:PRINT"met welk spel wilt u beginnen?"
1100 PRINT:PRINT"kies in dit geval spel 1 ";
1110 INPUTAB
1120 AC=AB:VL(6)=0
1170 IFAB>4 THEN GOSUB14000:GOTO1200
1180 ONAB GOSUB10000,11000,12000,13000
1200 IFAC>4 THEN GOSUB14000:GOTO1230
1210 ONAC GOSUB10000,11000,12000,13000
1215 GOTO1230
1220 GOSUB14000
1230 GOSUB4030
1240 GOSUB2600
1250 GOSUB1390
1260 GOSUB2180
1270 GOSUB3840
1280 GOSUB2790
1290 GOSUB3980
1300 IFVL(2)=1 THEN1350
1310 GOSUB3190
1320 GOSUB3490:SL=SL+1
1330 GOSUB4010
1340 WOTO1310
1350 GOSUB1540
1360 HO=3:VE=2:GOSUB110
1370 PRINT"wacht even s.v.p":AB=AB+1:GOTO1120
1380 GOTO1550
1390 IFLEFT$(CO$(2),1)="x" THENVL(4)=1:RETURN
1400 IFLEFT$(CO$(2),1)="*" THENVL(5)=1:RETURN
1410 VL(1)=1:GOSUB2180:GOSUB3840
1420 K=10:GOSUB2820:VL(1)=0
1430 HO=1:VE=13:GOSUB110:PRINT"closed    :";CO$(2);DB$(2)
1440 SP=U2-1:IFSP=0 THENSP=4
1450 PRINT" door    :";SP$(SP);" "
1460 PRINT" result    :";RE$(2)
1470 HO=9:VE=SH-3:GOSUB110:PRINT"de'leads':";LD$;
1480 PRINT"                             "
1490 HO=1:VE=SH-1:GOSUB110:PRINT"voor bieden ";
1500 PRINT"en spel open room:return";
1510 GOSUB210
1520 IFIN<>13 THEN1510
1530 RETURN
1540 HO=1:VE=15:GOSUB110:PRINT"                "
1550 VL(2)=0:PRINT" result  :";RE$(1);"   "
1560 HO=4:VE=SH-3:GOSUB110
1570 PRINT"voor score en commentaar:return";
1580 GOSUB210
1590 IFIN<>13 THEN1580
1600 IFLEFT$(UI$(1),1)="n" THENCQ$=" tegen ":GOTO1630
1610 IFLEFT$(UI$(1),1)="z" THENCQ$=" tegen ":GOTO1630
1620 CQ$=""
1630 HX$=HO$:HO=0:VE=0:GOSUB100:PRINT"spel";AB;":"
1640 IFAD>0 THENHO=0:VE=0:GOSUB110:PRINT"spel ";AD
1650 IFVL(5)=1 THENVL(5)=0:GOTO1940
1660 IFVL(4)=1 THENVL(4)=0:GOTO1940
1670 FORI=1 TO(12-LEN(HO$)):HO$=HO$+" ":NEXTI
1680 IV$=VI$:FORI=1 TO(12-LEN(VI$)):VI$=VI$+" ":NEXTI
1690 PRINT:PRINTHO$;" speelde ";CQ$;CO$(1);DB$(1);
1700 PRINT" (open) en het   resultaat was:";
1710 PRINTRE$(1);" dat is ";PO(1);" pts"
1720 IFLEFT$(UI$(2),1)="n" THENCQ$=" tegen ":GOTO1750
1730 IFLEFT$(UI$(2),1)="z" THENCQ$=" tegen ":GOTO1750
1740 CQ$=""
1750 PRINT:PRINTVI$;" speelde ";CQ$;CO$(2);DB$(2);
1760 PRINT" (closed) en het resultaat was:";RE$(2);
1770 PRINT" dat is ";PO(2);" pts"
1780 PRINT
1790 IFSC>=0 THEN1800
1795 PRINTHO$;" verliest ";SC;" scorepunten(imps)"
1796 GOTO1810
1800 IFSC>=0 THENPRINTHO$;" wint ";SC;" scorepunten(imps)"
1810 HO=0:VE=10:GOSUB110
1820 PRINT"de score was   ";HO$:VE=10:HO=28:GOSUB110
1830 PRINT":";SC(1):VE=11:HO=15:GOSUB110:PRINTVI$
1840 VE=11:HO=28:GOSUB110:PRINT":";SC(2)
1850 IFSC>=0 THENSC(1)=SC(1)+SC
1860 IFSC<=0 THENSC(2)=SC(2)+ABS(SC)
1870 PRINT"de score wordt ";HO$:VE=12:HO=28:GOSUB110
1880 PRINT":";SC(1):VE=13:HO=15:GOSUB110:PRINTVI$
1890 VE=13:HO=28:GOSUB110:PRINT":";SC(2)
1900 PRINT:HO$=HX$:VI$=IV$
1910 HO=8:VE=SH-5:GOSUB110
1911 PRINT"voor commentaar druk return";
1920 GOSUB210
1930 IFIN<>13 THEN1920
1940 GOSUB100
1950 PL=0:AL=0:LM=0
1960 K=1
1970 BK=1:LI$=CM$(K):IFK=11 THENLI$(0)="":GOTO2100
1980 AK=0:IFBK>LEN(LI$) THENK=K+1:GOTO1970
1990 AK=AK+1:IFBK+AK>LEN(LI$) THEN2010
2000 IFMID$(LI$,BK+AK,1)<>" " THEN1990
2010 WO$=MID$(LI$,BK,AK):BK=BK+LEN(WO$)+1
2020 IFWO$="." THENAL=AL+1:LI$(AL)="":PRINT:PL=LM:GOTO1980
2030 IFPL+LEN(WO$)>=RL THENAL=AL+1:LI$(AL)="":PRINT:PL=LM
2040 GOSUB2070:PL=PL+LEN(WO$)+1
2050 IF(PL<RL-1) THENWO$=" ":GOSUB2070:GOTO1980
2060 LI$(AL)=LI$(AL)+" ":GOTO1980
2070 SR$=WO$
2080 LI$(AL)=LI$(AL)+SR$
2090 PRINTWO$;:RETURN
2100 PRINT:PRINT:PRINT"voor volgend spel   :v"
2110 PRINT"voor zelfde spel    :z"
2120 PRINT"voor een ander spel :a";
2130 GOSUB210
2140 IFIN=86 THEN GOSUB100:RETURN
2150 IFIN=90 THENAB=AB-1:RETURN
2160 IFIN<>65 THEN2130
2170 GOSUB100:PRINT"welk spel";:INPUTAB:GOTO1120
2180 GOSUB100
2190 HO=0:VE=SH-6:GOSUB110
2200 PRINT"wedstrijd:";WE$(1)
2210 PRINT"          ";WE$(2)
2220 IF(RE$(1)="?")OR(RE$(2)="?") THEN2240
2230 GOTO2280
2240 HO=0:VE=SH-6:GOSUB110:PRINT"   bridgeprobleem ";
2250 PRINT"of vragenbus          "
2260 HO=10:VE=SH-5
2270 GOSUB110:PRINT"                            "
2280 HO=0:VE=0:GOSUB110:PRINTSP$:PRINT"gever: ";GE$
2290 IFVL(1)=0 THENPRINT"open room":GOSUB120
2300 IFVL(5)<>1 THEN2310
2305 VE=VE-1:GOSUB110:PRINT:PRINT"viertallen   "
2310 IFVL(4)=1 THENVE=VE-1:GOSUB110:PRINT"paren    "
2320 IFVL(1)=1 THENPRINT"closed room"
2330 AD=VAL(EX$(7))
2340 IFAD>0 THENHO=0:VE=0:GOSUB110:PRINT"spel ";AD
2350 FORX=1 TO4:K=1
2360 IFX=1 THENHO=18:VE=1:LK=HO:Q$=N$
2370 IF(X=1)AND(LEFT$(N$,1)="x") THEN2560
2380 IFX=2 THENHO=28:VE=6:LK=HO:Q$=QO$
2390 IF(X=2)AND(LEFT$(QO$,1)="x") THEN2560
2400 IFX=3 THENHO=18:VE=12:LK=HO:Q$=Z$
2410 IF(X=3)AND(LEFT$(Z$,1)="x") THEN2560
2420 IFX=4 THENHO=8:VE=6:LK=HO:Q$=WE$
2430 IF(X=4)AND(LEFT$(WE$,1)="x") THEN2560
2440 FORI=1 TO17
2450 W$=MID$(Q$,I,1)
2460 IFW$="+" THENSR$="S":HO=LK-1:GOSUB110
2470 IFW$="+" THEN GOSUB330:PRINTSR$:GOTO2550
2480 HO=LK+K:K=K+1
2490 IF(W$="-")OR(W$=".") THENVE=VE+1:K=1:HO=LK-1:GOSUB110
2500 IFW$="/" THENVE=VE+1:K=1:HO=LK-1:GOSUB110
2510 IFW$="-" THENSR$="H":GOSUB330:PRINTSR$:GOTO2550
2520 IFW$="." THENSR$="R":GOSUB330:PRINTSR$:GOTO2550
2530 IFW$="/" THENSR$="K":GOSUB330:PRINTSR$:GOTO2550
2540 GOSUB110:PRINTW$
2550 NEXTI
2560 NEXTX
2570 VA$="23456789tbvha"
2580 FORI=1 TO13:V$(I)=MID$(VA$,I,1):NEXTI
2590 RETURN
2600 IFLEFT$(GE$,1)="x" THEN2620
2610 GOTO2700
2620 IF(RE$(1)="?")OR(RE$(2)="?") THEN GOSUB1940
2630 IF(RE$(1)="?")OR(RE$(2)="?") THENAB=AB+1:GOTO1120
2640 GOSUB100:PRINT"dit spel";AB;"is niet aanwezig";
2650 PRINT",ik zoek de  uitslag."
2660 IFLEFT$(GE$,2)="xx" THENPRINT"er is ook geen ";
2670 IFLEFT$(GE$,2)="xx" THENPRINT"uitslag op dit spel."
2680 IFLEFT$(GE$,2)="xx" THENAB=AB+1:GOSUB2100:GOTO1120
2690 GOSUB1810:AB=AB+1:GOTO1120
2700 IFLEFT$(A$(01),1)<>"x" THEN2770
2710 VL(2)=1:GOSUB100:PRINT:PRINT
2720 VL(2)=1:PRINT"afspel is niet interessant op dit spel"
2730 PRINT" of dit is een bridgeprobleem of een";
2740 PRINT"     vraag uit de vragenbus!"
2750 PRINT"  druk een toets ";
2760 GOSUB210
2770 RETURN
2780 GOSUB3020
2790 IFVL(6)=1 THEN GOSUB3040:GOTO1310
2800 K=1:GOSUB210
2810 IFIN<>32 THEN2790
2820 HO=20:VE=6:GOSUB110:GOSUB3990:PRINTMID$(C$(K),1,3)
2830 GOSUB3020
2840 GOSUB210
2850 IFIN<>32 THEN2840
2860 HO=23:VE=8:GOSUB110:GOSUB3990:PRINTMID$(C$(K),4,3)
2870 GOSUB3020
2880 GOSUB210
2890 IFIN<>32 THEN2880
2900 HO=20:VE=10:GOSUB110:GOSUB3990:PRINTMID$(C$(K),7,3)
2910 GOSUB3020
2920 GOSUB210
2930 IFIN<>32 THEN2920
2940 HO=17:VE=8:GOSUB110:GOSUB3990:PRINTMID$(C$(K),10,3)
2950 GOSUB3020
2960 GOSUB210
2970 IFIN<>32 THEN2960
2980 IFVL(7)=1 THENVL(7)=0:C$(K)="":GOTO3030
2990 K=K+1:IF(LEN(C$(K))<2)AND(LEN(C$(K-1))>2) THEN3010
3000 GOTO2820
3010 C$(K)="            ":VL(7)=1:GOTO2820
3020 VE=SH-3:HO=15:GOSUB110:PRINT"druk op spatie";:RETURN
3030 IFVL(1)=1 THENRETURN
3040 HO=1:VE=12:GOSUB110:PRINT"contract:";CO$(1);DB$(1)
3050 SP=U1-1:IFSP=0 THENSP=4
3060 PRINT" door    :";SP$(SP);" "
3070 PRINT" uitkomst:";UI$(1)
3080 IFVL(6)=1 THENRETURN
3090 IFVL(5)=1 THEN3130
3100 IFVL(4)=1 THEN3130
3110 PRINT" closed  :";CO$(2);DB$(2)
3120 PRINT" result  :";RE$(2)
3130 HO=10:VE=SH-3:GOSUB110:PRINT"*bieding opnieuw ?j/n";
3140 GOSUB210
3150 IFIN=74 THENNZ=0:WO=0:SL=1:U=U1:GOTO1260
3160 IFIN<>78 THEN3140
3170 HO=10:VE=SH-3:GOSUB110:PRINT"                     ";
3180 RETURN
3190 K=1:FORI=1 TO4:B$(I)=MID$(A$(SL),K,2):K=K+2:NEXTI
3200 RETURN
3210 IFTR$="nt" THEN3360
3220 IO=0:R=1
3230 FORI=1 TO4
3240 KL$=LEFT$(B$(I),1)
3250 IFKL$=TR$ THENTR(R)=I:R=R+1:IO=1
3260 NEXTI
3270 IFIO=0 THEN3360
3280 B=0:FORI=1 TO(R-1)
3290 WA$=RIGHT$(B$(TR(I)),1)
3300 FORX=1 TO13
3310 IFV$(X)=WA$ THENA=X:X=13
3320 NEXTX
3330 IFA>B THENB=A:Y=TR(I):U=Y:U(SL)=U
3340 NEXTI
3350 GOTO3440
3360 B=0:FORI=1 TO4:KL$=LEFT$(B$(I),1)
3370 IFKL$<>LEFT$(B$(U),1) THEN3430
3380 WA$=RIGHT$(B$(I),1)
3390 FORX=1 TO13
3400 IFV$(X)=WA$ THENA=X:X=13
3410 NEXTX
3420 IFA>B THENB=A:Y=I:U=I:U(SL)=U
3430 NEXTI
3440 IFY/2=INT(Y/2) THENWO=WO+1:Y1=Y:HO=30:VE=15
3450 IFY/2=INT(Y/2) THEN GOSUB110:PRINT"ow=";WO:GOTO3470
3460 Y1=Y:HO=30:VE=14:NZ=NZ+1:GOSUB110:PRINT"nz=";NZ
3470 HO=H(Y1)+1:VE=V(Y1)+1:GOSUB110:PRINT"-"
3480 RETURN
3490 Y=U:FORI=1 TO4
3500 HU$=LEFT$(B$(U),1)
3510 IFHU$="+" THENHU$="s"
3520 IFHU$="-" THENHU$="h"
3530 IFHU$="." THENHU$="r"
3540 IFHU$="/" THENHU$="k"
3550 HO=H(U):VE=V(U):GOSUB110:PRINTHU$RIGHT$(B$(U),1)
3560 IFI=4 THEN3580
3570 GOSUB3020:GOSUB210:IFIN<>32 THEN3570
3580 Y=Y+1:IFY>4 THENU=U-4:Y=1
3590 U=U+1
3600 NEXTI
3610 GOSUB3210
3620 GOSUB3020:GOSUB210:IFIN<>32 THEN3620
3630 FORI=1 TO4
3640 HO=H(I):VE=V(I):GOSUB220:KA(I)=IN
3650 HO=HO+1:GOSUB220:KB(I)=IN
3660 HO=H(I):VE=V(I):GOSUB220:IFIN=83 THENTE=0
3670 HO=H(I):VE=V(I):GOSUB220:IFIN=72 THENTE=1
3680 HO=H(I):VE=V(I):GOSUB220:IFIN=82 THENTE=2
3690 HO=H(I):VE=V(I):GOSUB220:IFIN=75 THENTE=3
3700 FORZ=0 TO8
3710 IFI=1 THENHO=19+Z:VE=1+TE:GOSUB220:LK=HO:LR=VE
3720 IFI=2 THENHO=29+Z:VE=6+TE:GOSUB220:LK=HO:LR=VE
3730 IFI=3 THENHO=19+Z:VE=12+TE:GOSUB220:LK=HO:LR=VE
3740 IFI=4 THENHO=9+Z:VE=6+TE:GOSUB220:LK=HO:LR=VE
3750 IFIN=KB(I) THENHO=LK:VE=LR:GOSUB110:PRINT"."
3760 NEXTZ:NEXTI
3770 HO=H(Y1)+1:VE=V(Y1)+1:GOSUB110:PRINT" "
3780 FORI=1 TO4:HO=H(I):VE=V(I):GOSUB110:PRINT"  ":NEXTI
3790 HO=10:VE=SH-3:GOSUB110:PRINT"*spel opnieuw ?    j  ";
3800 GOSUB210
3810 HO=10:VE=SH-3:GOSUB110:PRINT"                    "
3820 IFIN=74 THENNZ=0:WO=0:SL=1:U=U1:VL(6)=1:GOTO1260
3830 RETURN
3840 IJ=1:IFVL(1)=1 THENIJ=IJ+4
3850 FORI=1 TO4
3860 IFI=1 THENHO=17:VE=0
3870 IFI=2 THENHO=27:VE=5
3880 IFI=3 THENHO=17:VE=16
3890 IFI=4 THENHO=7:VE=5
3900 GOSUB110:SR$=NA$(IJ):PRINTSR$;
3910 GOSUB330
3920 IF(LEFT$(KW$,2)="nz")AND(I/2<>INT(I/2)) THENPRINT" *"
3930 IF(RIGHT$(KW$,2)="ow")AND(I/2=INT(I/2)) THENPRINT" *"
3940 IJ=IJ+1
3950 NEXTI
3960 GOSUB3020
3970 RETURN
3980 PRINT"   ";:SD=1:GOSUB450
3990 PRINT"   ";:SD=1:GOSUB450
4000 GOSUB110:RETURN
4010 IFLEN(A$(SL))<>8 THEN1350
4020 RETURN
4030 CP$=RIGHT$(CO$(1),1)
4040 IFCP$="s" THENTR$="+"
4050 IFCP$="h" THENTR$="-"
4060 IF(CP$="r")OR(CP$="d") THENTR$="."
4070 IF(CP$="k")OR(CP$="c") THENTR$="/"
4080 IF(CP$="t")OR(CP$="a") THENTR$="nt"
4090 UJ$=LEFT$(UI$(1),1)
4100 UK$=LEFT$(UI$(2),1)
4110 IFUJ$="n" THENU=1:U1=1
4120 IFUK$="n" THENU2=1
4130 IF(UJ$="o")OR(UJ$="e") THENU=2:U1=2
4140 IF(UK$="o")OR(UK$="e") THENU2=2
4150 IF(UJ$="z")OR(UJ$="s") THENU=3:U1=3
4160 IF(UK$="z")OR(UK$="s") THENU2=3
4170 IFUJ$="w" THENU=4:U1=4
4180 IFUK$="w" THENU2=4
4190 SL=1:U(SL)=U:NZ=0:WO=0:CQ$=""
4200 IFLEN(EX$(5))<=1 THEN4210
4205 WE$(1)=EX$(5):WE$(2)=EX$(6):GOTO4240
4210 WE$(1)="* nederland-frankrijk *"
4220 WE$(2)="* maart'88 den haag   *"
4230 HO$="nederland":VI$="frankrijk "
4240 IFLEN(EX$(1))<=1 THEN4260
4250 FORI=1 TO4:NA$(I)=EX$(I):NEXTI:GOTO4340
4260 NA$(1)="JAN NOORD"
4270 NA$(2)="JAQUES"
4280 NA$(3)="PIET ZUID"
4290 NA$(4)="PIERRE"
4300 NA$(5)="LOUIS"
4310 NA$(6)="WIM OOST"
4320 NA$(7)="BERNARD"
4330 NA$(8)="HENK WEST"
4340 KA$(1)="open room":KA$(2)="closed room"
4350 RETURN
10000 GOSUB100:PRINT:PRINT"   wacht even s.v.p"
10010 SP$="spel 1"
10020 GE$="w"
10030 KW$="nzow"
10040 CO$(1)="4h":DB$(1)="."
10050 CO$(2)="4h":DB$(2)="."
10060 UI$(1)="ws6"
10070 UI$(2)="wk2"
10080 LD$="k2-h2-ha-s3-kv"
10090 RE$(1)="="
10100 RE$(2)="-1"
10110 PO(1)=620
10120 PO(2)=+100
10130 SC=12
10140 C$(00)="n  o  z  w  "
10150 C$(01)=".  .  .  p  "
10160 C$(02)="1r p  1h p  "
10170 C$(03)="1s p  4h p  "
10180 C$(04)="p  p  .  .  "
10190 C$(05)=""
10200 C$(06)=""
10210 C$(07)=""
10220 C$(08)=""
10230 C$(09)=""
10240 N$="+hb97-2.aht92/a74"
10250 QO$="+avt8-b973.b5/vbt"
10260 Z$="+3-ahvt865.73/653"
10270 WE$="+6542-4.v864/h982"
10280 A$(00)="n o z w "
10290 A$(01)="+b+v+3+6"
10300 A$(02)="/a/v/3/2"
10310 A$(03)="+7+8-5+2"
10320 A$(04)="-2-3-a-4"
10330 A$(05)=".2-7-h+4"
10340 A$(06)=".a.5.3.4"
10350 A$(07)="+9+t-6+5"
10360 A$(08)=".h.b.7.6"
10370 A$(09)="+h+a-8/8"
10380 A$(10)="/4/t/6/9"
10390 A$(11)="/7/b/5/h"
10400 A$(12)=".t-9-t.8"
10410 A$(13)=""
10420 A$(14)=""
10430 C$(00)="n  o  z  w  "
10440 C$(10)=".  .  .  p  "
10450 C$(11)="1r p  1h p  "
10460 C$(12)="1s p  4h p  "
10470 C$(13)="p  p  .  .  "
10480 C$(14)=""
10490 C$(15)=""
10500 C$(16)=""
10510 C$(17)=""
10520 C$(18)=""
10530 CM$(1)="in dit spel ziet u een viertallen wedstrijd."
10540 CM$(2)="eerst komt hierin de zogenaamde gesloten"
10550 CM$(3)="kamer en daarna de open kamer. in de open"
10560 CM$(4)="kamer krijgen de noord-zuid (nz) spelers"
10570 CM$(5)="dezelfde kaarten als de oost-west (ow)"
10580 CM$(6)="spelers in de gesloten kamer en de ow spelers"
10590 CM$(7)="de nz kaarten. hierdoor kunnen de resultaten"
10600 CM$(8)="van beide teams goed met elkaar worden"
10610 CM$(9)="vergeleken."
10620 CM$(10)=""
10630 EX$(1)=""
10640 EX$(2)=""
10650 EX$(3)=""
10660 EX$(4)=""
10670 EX$(5)=""
10680 EX$(6)=""
10685 EX$(7)=""
10690 RETURN
11000 GOSUB100:PRINT:PRINT"   wacht even s.v.p"
11010 SP$="spel 2"
11020 GE$="z"
11030 KW$="ow"
11040 CO$(1)="5r":DB$(1)="."
11050 CO$(2)="x4r":DB$(2)="."
11060 UI$(1)="wka"
11070 UI$(2)="wka"
11080 LD$="kh-ka-kv-ra-rh"
11090 RE$(1)="="
11100 RE$(2)="+1"
11110 PO(1)=400
11120 PO(2)=-150
11130 SC=+6
11140 C$(0)="n  o  z  w  "
11150 C$(1)=".  .  2r 3k "
11160 C$(2)="p  p  4r p  "
11170 C$(3)="5r p  p  p  "
11180 C$(4)=""
11190 C$(5)=""
11200 C$(6)=""
11210 C$(7)=""
11220 C$(8)=""
11230 C$(9)=""
11240 N$="+hb65-7642.42/t98"
11250 QO$="+v432-vbt83.765/3"
11260 Z$="+a9-ah9.ahvbt9/76"
11270 WE$="+t87-5.83/ahvb542"
11280 A$(00)="n o z w "
11290 A$(01)="/8/3/6/a"
11300 A$(02)="/9-3/7/h"
11310 A$(03)="/t-8.9/v"
11320 A$(04)=".4.7.a.8"
11330 A$(05)=".2.6.h.3"
11340 A$(06)="+5.5.v/2"
11350 A$(07)="-2+2.b/4"
11360 A$(08)="-4-t-a-5"
11370 A$(09)="-6-b-h/5"
11380 A$(10)="-7+3.t/b"
11390 A$(11)="+6+4+a+7"
11400 A$(12)="+h+v+9+8"
11410 A$(13)="+b-v-9+t"
11420 A$(14)=""
11430 C$(00)="n  o  z  w  "
11440 C$(10)=".  .  2k p  "
11450 C$(11)="2r p  3r p  "
11460 C$(12)="3h p  3s p  "
11470 C$(13)="4r p  p  p  "
11480 C$(14)=""
11490 C$(15)=""
11500 C$(16)=""
11510 C$(17)=""
11520 C$(18)=""
11530 CM$(1)="Dit is een voorbeeld van een parenwedstrijd."
11540 CM$(2)="Hierin ziet u slechts vier spelers."
11550 CM$(3)="Het resultaat van de twee paren"
11560 CM$(4)="die u op het scherm ziet moet vaak vergeleken"
11570 CM$(5)="worden met de score van vele andere paren."
11580 CM$(6)="Het resultaat van alle paren komt onder meer"
11590 CM$(7)="tot uiting in een gemiddelde score"
11600 CM$(8)=""
11610 CM$(9)=""
11620 CM$(10)=""
11630 EX$(1)=""
11640 EX$(2)=""
11650 EX$(3)=""
11660 EX$(4)=""
11670 EX$(5)=""
11680 EX$(6)=""
11685 EX$(7)=""
11690 RETURN
12000 GE$="x":RE$(1)="?"
12530 CM$(1)="Behalve voor het afspelen van viertallen en"
12540 CM$(2)="parenwedstrijden op een vugraf-achtige manier"
12550 CM$(3)="kan men met dit programma ook bridgevragen"
12560 CM$(4)="en bridgeproblemen weergeven. Tevens kan men"
12570 CM$(5)="de kaarten van de tegenpartij en/of partner"
12580 CM$(6)="van het scherm laten verdwijnen zodat een"
12590 CM$(7)="situatie wordt nagebootst zoals in een"
12600 CM$(8)="echte wedstrijd voorkomt."
12610 CM$(9)=""
12620 CM$(10)=""
12700 RETURN
13000 GE$="x":RE$(1)="?"
13520 CM$(1)="Voor bestellen van dergelijke bridge "
13530 CM$(2)="programmas (alleen materiaal en verzend"
13540 CM$(3)="kosten) voor meer informatie en voor"
13550 CM$(4)="handleiding- . *************************** . "
13560 CM$(5)="Th.M.van Leeuwen . Raadhuisweg "
13570 CM$(6)="18 . 1921 AB Akersloot . "
13580 CM$(7)="tel 02513 - 12680 . "
13590 CM$(8)="*************************** . "
13600 CM$(9)="Vermeld s.v.p. welk computermerk u bezit en"
13610 CM$(10)="of u een diskdrive heeft. . ** einde **"
13690 RETURN
14000 GE$="x":RE$(1)="?"
14520 CM$(1)="normaal bevat dit programma 11 spellen. deze"
14530 CM$(2)="korte demo bevat slechts 4 spellen . kies 1-4"
14540 FORI=3 TO10:CM$(I)="":NEXTI
14690 RETURN
32000 REM Vugraf Bridge
32010 REM
32020 REM Auteur: Th.M. van Leeuwen
32030 REM         Raadhuisweg 18
32040 REM         1921 AB Akersloot
32050 REM         tel 02513 - 12680
32060 REM
32070 REM TROS-RADIO dd 880927

10 GOTO 1000:REM Basicode-2 subroutines
20 CLEAR:IN$=" ":SR=0:SR$="":CT=0:CN=0:OHT%=80:OVT%=24
22 KEY OFF:SCREEN 0:WIDTH OHT%:COLOR 7,0,0:CLS:RANDOMIZE TIMER*100
29 GOTO 1010
100 CLS:RETURN
110 GOSUB 111:LOCATE OVE%+1,OHO%+1:RETURN
111 OVE%=INT(VE):OHO%=INT(HO)
112 IF OVE%<0 THEN OVE%=0 ELSE IF OVE%>=OVT% THEN OVE%=OVT%-1
113 IF OHO%<0 THEN OHO%=0 ELSE IF OHO%>=OHT% THEN OHO%=OHT%-1
114 RETURN
120 HO=POS(0)-1:VE=CSRLIN-1:RETURN
200 IN$=INKEY$:RETURN
210 IN$=INPUT$(1):RETURN
250 BEEP:RETURN
260 RV=RND:RETURN
270 FR=FRE(""):RETURN
300 SR$=MID$(STR$(ABS(SR)),2):IF LEFT$(SR$,1)="." THEN SR$="0"+SR$
301 IF SR<0 THEN SR$="-"+SR$
302 RETURN
310 O#=ABS(SR)+.5*10^-CN:O!=INT(O#):O#=1+O#-O!:O$=""
311 IF CN THEN O$="."+STRING$(CN,48):IF O#>1 THEN MID$(O$,1)=MID$(STR$(O#),3)
312 O$=MID$(STR$(O!),2)+O$:IF VAL(O$)>0 AND SR<0 THEN O$="-"+O$
313 IF LEN(O$)<=CT THEN SR$=SPACE$(CT-LEN(O$))+O$:RETURN
314 SR$=STRING$(CT,42):RETURN
350 LPRINT SR$;:RETURN
360 LPRINT:RETURN
1000 A = 500: GOTO 20: REM Sportuitslagen
1010 DIM TM$(20), TW(20), WN(20, 20), DV(20, 20), DT(20, 20)
1012 DIM PL(20), AW(20), W(20), G(20), V(20), P(20), T(20)
1014 DIM PC(20), GV(20), GT(20), SD(20), HP(20)
1016 GOSUB 6000: REM Initialiseren
1020 GOSUB 6100: REM Inlezen data
1030 GOSUB 1300: REM Hoofdmenu
1040 GOSUB 270
1050 ON KE GOTO 2100, 2800, 3300, 4400, 6800, 1100
1090 REM Stoppen
1100 IF(K1 = 1) AND(K5 = 0) THEN 1120
1110 GOSUB 100: END
1120 GOSUB 100: SR$ = "WAARSCHUWING!"
1130 HO = 13: VE = 1: GOSUB 110: PRINT SR$
1140 PRINT: PRINT: PRINT: PRINT
1150 PRINT: PRINT"De inhoud van het bestand is gewijzigd"
1160 PRINT: PRINT"en nog niet weggeschreven naar diskette"
1170 PRINT: PRINT"of cassette."
1180 SR$ = "Wilt u echt stoppen (J/N) ? ": GOSUB 1550
1190 GOSUB 5100
1200 IF(IN$ = "J") OR(IN$ = "j") THEN 1110
1210 IF(IN$ <> "N") AND(IN$ <> "n") THEN 1190
1220 GOTO 1030
1290 REM Hoofdmenu
1300 GOSUB 100: SR$ = SB$ + " COMPETITIE"
1310 HO = INT((39 - LEN(SR$)) / 2): VE = 1
1320 GOSUB 110: PRINT SR$
1330 HO = INT((39 - LEN(KL$)) / 2): VE = VE + 2
1340 GOSUB 110: PRINT KL$
1350 HO = 10: VE = VE + 2: GOSUB 110
1360 PRINT"SEIZOEN 19"; JR$;
1370 SR = VAL("19" + JR$) + 1: GOSUB 300
1380 PRINT" - "; SR$: PRINT: PRINT
1390 PRINT: PRINT"    <1> Uitslagen invullen/wijzigen"
1400 PRINT: PRINT"    <2> Resultaten per team"
1410 PRINT: PRINT"    <3> Competitiestand"
1420 IF K4 = 0 THEN 1440
1430 PRINT: PRINT"    <4> Uitslagen voorspellen"
1440 PRINT: PRINT"    <5> Gegevens wegschrijven"
1450 PRINT: PRINT"    <6> Stoppen"
1460 SR$ = "Wat kies je ?": GOSUB 1550
1470 GOSUB 5100: KE = VAL(IN$)
1480 IF(KE < 1) OR(KE > 6) THEN 1470
1490 IF(KE = 4) AND(K4 = 0) THEN 1470
1500 RETURN
1540 REM Laatste regel
1550 HO = 0: VE = 23: GOSUB 110: PRINT BR$;
1560 HO = INT((39 - LEN(SR$)) / 2): GOSUB 110
1570 PRINT SR$;
1580 RETURN
1590 REM Invoer cijfers
1600 I$ = "": GOSUB 110: PRINT"?"; : GOSUB 110
1610 GOSUB 5100
1620 IF IN$ = CHR$(13) THEN 1680
1630 IF IN$ = CHR$(32) THEN 1730
1640 IF(IN$ < "0") OR(IN$ > "9") THEN 1610
1650 IF LEN(I$) < CT THEN PRINT IN$; : I$ = I$ + IN$
1660 GOTO 1610
1670 REM Controleer getal
1680 IF VAL(I$) <= GW THEN 1780
1690 HO = HM: GOSUB 110
1700 FOR I = 1 TO CT: PRINT" "; : NEXT I
1710 GOSUB 110: GOSUB 250: GOTO 1600
1720 REM Wis laatste cijfer
1730 GOSUB 120: HO = HO - 1
1740 IF HO < HM THEN HO = HM
1750 GOSUB 110: PRINT" ";
1760 GOSUB 110: IF LEN(I$) < 2 THEN 1600
1770 I$ = LEFT$(I$, LEN(I$) - 1): GOTO 1610
1780 RETURN
1790 REM Teams op scherm
1800 HO = 30: VE = 0: GOSUB 110
1810 PRINT"TEAMS"
1820 HO = 25: VE = VE + 2: CT = 2: CN = 0
1830 FOR I = 1 TO AV
1840 SR = I: GOSUB 310: GOSUB 110
1850 PRINT SR$; " "; TM$(I);
1860 VE = VE + 1: NEXT I
1870 RETURN
1890 REM Schermdeel wissen
1900 HO = 0: FOR VE = 0 TO 23
1910 GOSUB 110: PRINT BL$;
1920 NEXT VE
1930 RETURN
1940 REM Score invullen
1950 PRINT SR$; " "; TM$(T2);
1960 HO = 16: GOSUB 110: HM = HO: GW = 99: GOSUB 1600
1970 DV(T1, T2) = VAL(I$)
1980 SR = DV(T1, T2): GOSUB 310: HO = 16
1990 GOSUB 110: PRINT SR$; "-";
2000 HO = 19: HM = HO: GOSUB 1600
2010 DT(T1, T2) = VAL(I$)
2020 SR = DT(T1, T2): GOSUB 310: HO = 19
2030 GOSUB 110: PRINT SR$;
2040 K1 = 1: K3 = 0: K4 = 0
2050 RETURN
2090 REM Uitslagen
2100 GOSUB 100: GOSUB 1800
2110 SR$ = "Kies nr. THUISclub (0=Stop): ": GOSUB 1550
2120 GOSUB 120: HM = HO: CT = 2: GW = AV: GOSUB 1600
2130 T1 = VAL(I$): IF T1 = 0 THEN 1030
2140 SR$ = "": GOSUB 1550
2150 GOSUB 1900
2160 HO = INT((21 - LEN(TM$(T1))) / 2): VE = 0
2170 GOSUB 110: PRINT TM$(T1)
2180 HO = 0: VE = VE + 2: GOSUB 110
2190 PRINT"R  Tegenstander Score"
2200 PRINT"====================="
2210 VE = VE + 3: IF TW(T1) = 0 THEN 2320
2220 FOR I = 1 TO TW(T1)
2230 HO = 0: CT = 2: SR = I: GOSUB 310
2240 T2 = WN(T1, I): GOSUB 110
2250 PRINT SR$; " "; TM$(T2);
2260 HO = 16: GOSUB 110
2270 SR = DV(T1, T2): GOSUB 310
2280 PRINT SR$; "-";
2290 SR = DT(T1, T2): GOSUB 310
2300 PRINT SR$; : VE = VE + 1
2310 NEXT I
2320 SR$ = "I=Invullen     W=Wijzigen": GOSUB 1550
2330 GOSUB 5100
2340 IF(IN$ = "W") OR(IN$ = "w") THEN 2510
2350 IF(IN$ <> "I") AND(IN$ <> "i") THEN 2330
2360 REM Invullen
2370 SR$ = "Kies nr. BEZOEKERS (0=Stop): ": GOSUB 1550
2380 GOSUB 120: HM = HO: GW = AV: GOSUB 1600
2390 T2 = VAL(I$): IF T2 = T1 THEN GOSUB 250: GOTO 2370
2400 IF T2 = 0 THEN GOSUB 1900: GOTO 2110
2405 IF TW(T1) = 0 THEN 2440
2410 FOR I = 1 TO TW(T1)
2420 IF T2 = WN(T1, I) THEN GOSUB 250: T2 = 0: I = TW(T1)
2430 NEXT I: IF T2 = 0 THEN 2370
2440 SR$ = "": GOSUB 1550
2450 HO = 0: VE = 5 + TW(T1): GOSUB 110
2460 SR = TW(T1) + 1: GOSUB 310
2470 GOSUB 1950
2480 TW(T1) = TW(T1) + 1: WN(T1, TW(T1)) = T2
2490 GOTO 2370
2500 REM Wijzigen
2510 SR$ = "Kies nr. WEDSTRIJD (0=Stop): ": GOSUB 1550
2520 GOSUB 120: HM = HO: GW = TW(T1): GOSUB 1600
2530 W = VAL(I$): IF W = 0 THEN 1030
2540 HO = 0: VE = 4 + W: GOSUB 110: PRINT BL$
2550 TN = WN(T1, W): WN(T1, W) = 0
2560 SR$ = "Kies nr. BEZOEKERS: ": GOSUB 1550
2570 GOSUB 120: HM = HO: GW = AV: GOSUB 1600
2580 T2 = VAL(I$): IF(T2 = T1) OR(T2 = 0) THEN GOSUB 250: GOTO 2560
2590 FOR I = 1 TO TW(T1)
2600 IF T2 = WN(T1, I) THEN GOSUB 250: T2 = 0: I = TW(T1)
2610 NEXT I
2620 IF T2 = 0 THEN 2560
2630 SR$ = "": GOSUB 1550
2640 DV(T1, TN) = 0: DT(T1, TN) = 0
2650 WN(T1, W) = T2
2660 HO = 0: VE = 4 + W: GOSUB 110
2670 SR = W: GOSUB 310: GOSUB 1950
2680 SR$ = "Nog meer wijzigen (J/N)?": GOSUB 1550
2690 GOSUB 5100
2700 IF(IN$ = "J") OR(IN$ = "j") THEN 2510
2710 IF(IN$ <> "N") AND(IN$ <> "n") THEN 2690
2720 GOTO 1030
2790 REM Resultaten per team
2800 GOSUB 100: GOSUB 1800
2810 SR$ = "Kies nr. van TEAM (0=Stop): ": GOSUB 1550
2820 GOSUB 120: HM = HO: GW = AV: GOSUB 1600
2830 T1 = VAL(I$): IF T1 = 0 THEN 1030
2840 GOSUB 100
2850 SR$ = "Resultaten van " + TM$(T1)
2860 HO = INT((39 - LEN(SR$)) / 2): VE = 0: GOSUB 110: PRINT SR$
2870 VE = VE + 1: GOSUB 110
2880 FOR I = 1 TO LEN(SR$): PRINT"="; : NEXT I
2890 VE = 3
2900 FOR I = 1 TO AV
2910 T2 = WN(T1, I): IF T2 = 0 THEN 2980
2920 HO = 3: VE = VE + 1: IF VE > 21 THEN GOSUB 3150
2930 GOSUB 110: PRINT TM$(T1);
2940 HO = 15: GOSUB 110: PRINT" - "; TM$(T2);
2950 SR = DV(T1, T2): CT = 2: GOSUB 310
2960 HO = 32: GOSUB 110: PRINT SR$; "-";
2970 SR = DT(T1, T2): GOSUB 310: PRINT SR$
2980 FOR J = 1 TO AV: T2 = 0
2990 IF WN(J, I) = T1 THEN T2 = J
3000 IF T2 = 0 THEN 3070
3010 HO = 3: VE = VE + 1: IF VE > 21 THEN GOSUB 3150
3020 GOSUB 110: PRINT TM$(T2);
3030 HO = 15: GOSUB 110: PRINT" - "; TM$(T1);
3040 SR = DV(T2, T1): CT = 2: GOSUB 310
3050 HO = 32: GOSUB 110: PRINT SR$; "-";
3060 SR = DT(T2, T1): GOSUB 310: PRINT SR$
3070 NEXT J
3080 NEXT I
3090 SR$ = "Nog 'n team zien (J/N)?": GOSUB 1550
3100 GOSUB 5100
3110 IF(IN$ = "J") OR(IN$ = "j") THEN 2800
3120 IF(IN$ <> "N") AND(IN$ <> "n") THEN 3100
3130 GOTO 1030
3140 REM Scherm vol
3150 SR$ = "V=Verder ": GOSUB 1550
3160 GOSUB 5100
3170 IF(IN$ <> "V") AND(IN$ <> "v") THEN 3160
3180 FOR VE = 3 TO 22: GOSUB 110: PRINT BR$; : NEXT VE
3190 HO = 3: VE = 4
3200 RETURN
3240 REM Verwissel
3250 H = PL(I): PL(I) = PL(J): PL(J) = H
3260 RETURN
3290 REM Competitiestand
3300 GOSUB 100: IF K3 = 1 THEN 3860
3310 SR$ = "COMPETITIESTAND"
3320 HO = 12: VE = 1: GOSUB 110: PRINT SR$
3330 HO = 0: VE = 5: GOSUB 110
3340 PRINT: PRINT"Afhankelijk van het merk computer kan"
3350 PRINT: PRINT"het rekenwerk best wat tijd in beslag"
3360 PRINT: PRINT"nemen."
3370 PRINT: PRINT"Tijd voor een kopje koffie misschien?"
3380 PRINT: PRINT"Of hoort u tot de mensen die hun"
3390 PRINT: PRINT"nieuwsgierigheid geen twee minuten"
3400 PRINT: PRINT"kunnen bedwingen?"
3410 FOR I = 1 TO AV
3420 AW(I) = 0: W(I) = 0: G(I) = 0: V(I) = 0: P(I) = 0: HP(I) = 0
3430 GV(I) = 0: GT(I) = 0: SD(I) = 0: PC(I) = 0: PL(I) = I
3440 NEXT I
3450 REM Totalen per team
3460 FOR I = 1 TO AV: FOR J = 1 TO AV
3470 T2 = WN(I, J): IF T2 = 0 THEN 3530
3480 AW(I) = AW(I) + 1
3490 IF DV(I, T2) > DT(I, T2) THEN W(I) = W(I) + 1: P(I) = P(I) + 2
3500 IF DV(I, T2) = DT(I, T2) THEN G(I) = G(I) + 1: P(I) = P(I) + 1
3510 IF DV(I, T2) < DT(I, T2) THEN V(I) = V(I) + 1
3520 GV(I) = GV(I) + DV(I, T2): GT(I) = GT(I) + DT(I, T2)
3530 NEXT J
3540 IF AW(I) = 0 THEN 3560
3550 HP(I) = (P(I) / 2) / AW(I)
3560 NEXT I
3570 REM Totaal uitwedstrijden
3580 FOR I = 1 TO AV: FOR J = 1 TO AV
3590 IF I = J THEN 3700
3600 FOR K = 1 TO AV: T2 = WN(J, K)
3610 IF T2 = 0 THEN K = AV
3620 IF T2 = I THEN T2 = J: K = AV
3630 NEXT K
3640 IF T2 = 0 THEN 3700
3650 AW(I) = AW(I) + 1
3660 IF DV(T2, I) < DT(T2, I) THEN W(I) = W(I) + 1: P(I) = P(I) + 2
3670 IF DV(T2, I) = DT(T2, I) THEN G(I) = G(I) + 1: P(I) = P(I) + 1
3680 IF DV(T2, I) > DT(T2, I) THEN V(I) = V(I) + 1
3690 GV(I) = GV(I) + DT(T2, I): GT(I) = GT(I) + DV(T2, I)
3700 NEXT J
3710 SD(I) = GV(I) - GT(I): IF AW(I) = 0 THEN 3730
3720 PC(I) = (P(I) / 2) / AW(I)
3730 NEXT I
3740 REM Sorteren
3750 FOR I = 1 TO AV - 1
3760 FOR J = I + 1 TO AV
3770 IF P(PL(I)) < P(PL(J)) THEN GOSUB 3250: GOTO 3840
3780 IF P(PL(I)) > P(PL(J)) THEN 3840
3790 IF AW(PL(I)) > AW(PL(J)) THEN GOSUB 3250: GOTO 3840
3800 IF AW(PL(I)) < AW(PL(J)) THEN 3840
3810 IF SD(PL(I)) < SD(PL(J)) THEN GOSUB 3250: GOTO 3840
3820 IF SD(PL(I)) > SD(PL(J)) THEN 3840
3830 IF GV(PL(I)) < GV(PL(J)) THEN GOSUB 3250
3840 NEXT J: NEXT I
3850 REM Resultaten afdrukken
3860 GOSUB 100: GOSUB 270
3870 SR$ = "Stand " + KL$
3880 HO = INT((39 - LEN(SR$)) / 2): VE = 0
3890 GOSUB 110: IF LP = 0 THEN PRINT SR$
3900 IF LP = 1 THEN GOSUB 350: GOSUB 360
3910 VE = VE + 1: GOSUB 110
3920 IF LP = 0 THEN FOR I = 1 TO LEN(SR$): PRINT"="; : NEXT I
3930 VE = VE + 1
3940 IF LP = 1 THEN GOSUB 360
3950 FOR I = 1 TO AV: N = PL(I)
3960 HO = 0: VE = VE + 1: GOSUB 110
3970 SR$ = TM$(N): IF LP = 0 THEN PRINT SR$;
3980 IF LP = 1 THEN SR$ = SR$ + LEFT$(B$, 14 - LEN(SR$)): GOSUB 350
3990 HO = 13: GOSUB 110: CT = 2: CN = 0
4000 SR = AW(N): GOSUB 310: IF LP = 0 THEN PRINT SR$;
4010 IF LP = 1 THEN SR$ = SR$ + " ": GOSUB 350
4020 HO = 16: GOSUB 110
4030 SR = W(N): GOSUB 310: IF LP = 0 THEN PRINT SR$;
4040 IF LP = 1 THEN SR$ = SR$ + " ": GOSUB 350
4050 HO = 19: GOSUB 110
4060 SR = G(N): GOSUB 310: IF LP = 0 THEN PRINT SR$;
4070 IF LP = 1 THEN SR$ = SR$ + " ": GOSUB 350
4080 HO = 22: GOSUB 110
4090 SR = V(N): GOSUB 310: IF LP = 0 THEN PRINT SR$;
4100 IF LP = 1 THEN SR$ = SR$ + " ": GOSUB 350
4110 HO = 25: GOSUB 110
4120 SR = P(N): GOSUB 310: IF LP = 0 THEN PRINT SR$;
4130 IF LP = 1 THEN SR$ = SR$ + " ": GOSUB 350
4140 HO = 28: GOSUB 110: CT = 3
4150 SR = GV(N): GOSUB 310: IF LP = 0 THEN PRINT SR$; "-";
4160 IF LP = 1 THEN SR$ = SR$ + "-": GOSUB 350
4170 HO = 32: GOSUB 110
4180 SR = GT(N): GOSUB 310: IF LP = 0 THEN PRINT SR$;
4190 IF LP = 1 THEN SR$ = SR$ + " ": GOSUB 350
4200 HO = 36: GOSUB 110
4210 SR = SD(N): GOSUB 310: IF LP = 0 THEN PRINT SR$;
4220 IF LP = 1 THEN GOSUB 350: GOSUB 360
4230 NEXT I: K3 = 1: K4 = 1: LP = 0
4240 SR$ = "M = Menu     P = Printer": GOSUB 1550
4250 GOSUB 5100
4260 IF(IN$ = "M") OR(IN$ = "m") THEN 1030
4270 IF(IN$ <> "P") AND(IN$ <> "p") THEN 4250
4280 SR$ = "Staat de printer klaar (J/N) ?": GOSUB 1550
4290 GOSUB 5100
4300 IF(IN$ <> "J") AND(IN$ <> "j") THEN 4240
4310 LP = 1: GOTO 3860
4390 REM Uitslagen voorspellen
4400 GOSUB 100: GOSUB 1800: N = 0
4410 FOR I = 1 TO AV: T(I) = 0: NEXT I
4420 SR$ = "Kies nr. THUISclub (0=Stop): ": GOSUB 1550
4430 GOSUB 120: HM = HO: CT = 2: GW = AV: GOSUB 1600
4440 T1 = VAL(I$): IF(T1 = 0) AND(N = 0) THEN 1030
4450 IF T1 = 0 THEN 4660
4460 FOR I = 1 TO AV
4470 IF T1 = T(I) THEN GOSUB 250: T1 = 0: I = AV
4480 NEXT I
4490 IF T1 = 0 THEN 4420
4500 N = N + 1: IF N > AV - 1 THEN 4660
4510 HO = 25: VE = T1 + 1: GOSUB 110: PRINT B$;
4520 HO = 0: VE = N + 1: GOSUB 110
4530 PRINT TM$(T1); : T(N) = T1
4540 SR$ = "Kies nr. BEZOEKERS: ": GOSUB 1550
4550 GOSUB 120: HM = HO: GW = AV: GOSUB 1600
4560 T2 = VAL(I$): IF(T2 = T1) OR(T2 = 0) THEN GOSUB 250: GOTO 4540
4570 FOR I = 1 TO AV
4580 IF T2 = T(I) THEN GOSUB 250: T2 = 0: I = AV
4590 NEXT I
4600 IF T2 = 0 THEN 4540
4610 N = N + 1
4620 HO = 25: VE = T2 + 1: GOSUB 110: PRINT B$;
4630 HO = 12: VE = N: GOSUB 110
4640 PRINT"-"; TM$(T2); : T(N) = T2
4650 GOTO 4420
4660 SR$ = "Is dit korrekt (J/N) ? ": GOSUB 1550
4670 GOSUB 5100
4680 IF(IN$ = "N") OR(IN$ = "n") THEN 4400
4690 IF(IN$ <> "J") AND(IN$ <> "j") THEN 4670
4700 GOSUB 100
4710 VE = 0: HO = 12
4720 SR$ = "PROGNOSE"
4730 GOSUB 110: IF LP = 0 THEN PRINT SR$
4740 IF LP = 1 THEN GOSUB 350: GOSUB 360: GOSUB 360
4750 FOR I = 1 TO INT(N / 2) * 2 STEP 2
4760 HO = 0: VE = VE + 2: GOSUB 110
4770 SR$ = TM$(T(I)): IF LP = 0 THEN PRINT SR$;
4780 IF LP = 1 THEN SR$ = SR$ + LEFT$(B$, 13 - LEN(SR$)) + "-": GOSUB 350
4790 HO = 12: GOSUB 110: IF LP = 0 THEN PRINT"-";
4800 SR$ = TM$(T(I + 1)): IF LP = 0 THEN PRINT SR$;
4810 IF LP = 1 THEN SR$ = SR$ + LEFT$(B$, 14 - LEN(SR$)): GOSUB 350
4820 R1 = (HP(T(I)) + PC(T(I))) / 2
4830 R2 = (3 * PC(T(I + 1)) - HP(T(I + 1))) / 2
4840 HO = 26: GOSUB 110
4850 SR = R1 - R2
4860 FOR K = 1 TO 5: J = K / 20
4870 SR$ = "1 "
4880 IF(SR >= J) AND(LP = 0) THEN PRINT SR$;
4890 IF(SR >= J) AND(LP = 1) THEN GOSUB 350
4900 SR$ = "2 "
4910 IF(SR <= -J) AND(LP = 0) THEN PRINT SR$;
4920 IF(SR <= -J) AND(LP = 1) THEN GOSUB 350
4930 SR$ = "3 "
4940 IF(SR > -J) AND(SR < J) AND(LP = 0) THEN PRINT SR$;
4950 IF(SR > -J) AND(SR < J) AND(LP = 1) THEN GOSUB 350
4960 NEXT K
4970 IF LP = 1 THEN GOSUB 360
4980 NEXT I
4990 LP = 0
5000 SR$ = "M = Menu     P = Printer": GOSUB 1550
5010 GOSUB 5100
5020 IF(IN$ = "M") OR(IN$ = "m") THEN 1030
5030 IF(IN$ <> "P") AND(IN$ <> "p") THEN 5010
5040 SR$ = "Staat de printer klaar (J/N) ?": GOSUB 1550
5050 GOSUB 5100
5060 IF(IN$ <> "J") AND(IN$ <> "j") THEN 5000
5070 LP = 1: GOTO 4700
5090 REM Buffer leegmaken
5100 GOSUB 200: IF IN$ <> "" THEN 5100
5110 GOSUB 210
5120 RETURN
6000 REM Initialiseren
6030 SB$ = "KNVB": KL$ = "EREDIVISIE": JR$ = "88"
6040 K1 = 0: K3 = 0: K4 = 0: K5 = 0: LP = 0
6050 BL$ = "                         "
6060 BR$ = "                                       "
6070 B$ = "               "
6080 RETURN
6090 REM Inlezen data
6100 GOSUB 100: SR$ = "GEGEVENS INLEZEN"
6110 HO = 11: VE = 1: GOSUB 110
6120 PRINT SR$
6130 PRINT: PRINT
6140 PRINT: PRINT
6150 PRINT: PRINT"    <1> van cassette"
6160 PRINT: PRINT"    <2> van diskette"
6170 PRINT: PRINT"    <3> van DATAregels in programma"
6180 SR$ = "Wat kies je ? ": GOSUB 1550
6190 GOSUB 5100: KE = VAL(IN$)
6200 IF(KE < 1) OR(KE > 3) THEN 6190
6210 IF KE = 3 THEN 6600
6220 NF$ = SB$ + JR$ + "." + LEFT$(KL$, 3)
6230 GOSUB 100: HO = 5: VE = 10: GOSUB 110
6240 PRINT"Bestandsnaam: "; NF$
6260 SR$ = "Is dit korrekt (J/N) ? ": GOSUB 1550
6270 GOSUB 5100
6280 IF(IN$ = "J") OR(IN$ = "j") THEN 6330
6290 IF(IN$ <> "N") AND(IN$ <> "n") THEN 6270
6300 HO = 0: VE = 23: GOSUB 110: PRINT BR$;
6310 HO = 6: GOSUB 110: PRINT"Nieuwe naam: ";
6320 INPUT NF$: GOTO 6230
6330 SR$ = "Even geduld ...": GOSUB 1550
6580 RETURN
6590 REM Read data
6600 SR$ = "Even geduld ...": GOSUB 1550
6610 READ AV, SB$, KL$, JR$
6620 FOR I = 1 TO AV
6630 READ TM$(I), TW(I)
6640 IF TW(I) = 0 THEN 6690
6650 FOR J = 1 TO TW(I)
6660 READ WN(I, J): T2 = WN(I, J)
6670 READ DV(I, T2), DT(I, T2)
6680 NEXT J
6690 NEXT I: RETURN
6790 REM Data wegschrijven
6800 GOSUB 100: SR$ = "GEGEVENS WEGSCHRIJVEN"
6810 HO = 9: VE = 1: GOSUB 110: PRINT SR$
6820 PRINT: PRINT
6830 PRINT: PRINT
6840 PRINT: PRINT"    <1> naar cassette"
6850 PRINT: PRINT"    <2> naar diskette"
6860 PRINT: PRINT"    <3> terug naar hoofdmenu"
6870 SR$ = "Wat kies je ? ": GOSUB 1550
6880 GOSUB 5100: KE = VAL(IN$)
6890 IF(KE < 1) OR(KE > 3) THEN 6880
6900 IF KE = 3 THEN 1030
6910 NF$ = SB$ + JR$ + "." + LEFT$(KL$, 3)
6920 GOSUB 100: HO = 5: VE = 10: GOSUB 110
6930 PRINT"Bestandsnaam: "; NF$
6950 SR$ = "Is dit korrekt (J/N) ? ": GOSUB 1550
6960 GOSUB 5100
6970 IF(IN$ = "J") OR(IN$ = "j") THEN 7020
6980 IF(IN$ <> "N") AND(IN$ <> "n") THEN 6960
6990 HO = 0: VE = 23: GOSUB 110: PRINT BR$;
7000 HO = 6: GOSUB 110: PRINT"Nieuwe naam: ";
7010 INPUT NF$: GOTO 6920
7020 SR$ = "Even geduld ...": GOSUB 1550
7290 K5 = 1: GOTO 1030
25000 DATA 18,"KNVB","EREDIVISIE","88"
25010 DATA"Ajax",0,"Den Bosch",0
25020 DATA"Feyenoord",0,"Fortuna SC",0
25030 DATA"Groningen",0,"Haarlem",0
25040 DATA"MVV",0,"PEC Zwolle",0
25050 DATA"PSV",0,"RKC",0
25060 DATA"Roda JC",0,"Sparta",0
25070 DATA"Twente",0,"Utrecht",0
25080 DATA"Veendam",0,"Volendam",0
25090 DATA"VVV",0,"Willem II",0
30000 REM ATTENTIE !!!
30010 REM Lees eerst de extra beeldkrant
30020 REM bij dit programma.
32000 REM**********************
32010 REM*                    *
32020 REM*    Harry Engel     *
32030 REM* Jan Amentstraat 30 *
32040 REM*  6041 BE Roermond  *
32050 REM*  Tel. 04750-19943  *
32060 REM*                    *
32070 REM* Geschreven op een: *
32080 REM*   Bull Micral 60   *
32090 REM*   PC/AT computer   *
32100 REM*                    *
32110 REM*    januari 1989    *
32120 REM*                    *
32130 REM**********************
32140 REM Copyright NOS Hilversum

10 GOTO 1000:REM Basicode-2 subroutines
20 CLEAR:IN$=" ":SR=0:SR$="":CT=0:CN=0:OHT%=80:OVT%=24
22 KEY OFF:SCREEN 0:WIDTH OHT%:COLOR 7,0,0:CLS:RANDOMIZE TIMER*100
29 GOTO 1010
100 CLS:RETURN
110 GOSUB 111:LOCATE OVE%+1,OHO%+1:RETURN
111 OVE%=INT(VE):OHO%=INT(HO)
112 IF OVE%<0 THEN OVE%=0 ELSE IF OVE%>=OVT% THEN OVE%=OVT%-1
113 IF OHO%<0 THEN OHO%=0 ELSE IF OHO%>=OHT% THEN OHO%=OHT%-1
114 RETURN
120 HO=POS(0)-1:VE=CSRLIN-1:RETURN
200 IN$=INKEY$:RETURN
210 IN$=INPUT$(1):RETURN
250 BEEP:RETURN
260 RV=RND:RETURN
270 FR=FRE(""):RETURN
300 SR$=MID$(STR$(ABS(SR)),2):IF LEFT$(SR$,1)="." THEN SR$="0"+SR$
301 IF SR<0 THEN SR$="-"+SR$
302 RETURN
310 O#=ABS(SR)+.5*10^-CN:O!=INT(O#):O#=1+O#-O!:O$=""
311 IF CN THEN O$="."+STRING$(CN,48):IF O#>1 THEN MID$(O$,1)=MID$(STR$(O#),3)
312 O$=MID$(STR$(O!),2)+O$:IF VAL(O$)>0 AND SR<0 THEN O$="-"+O$
313 IF LEN(O$)<=CT THEN SR$=SPACE$(CT-LEN(O$))+O$:RETURN
314 SR$=STRING$(CT,42):RETURN
350 LPRINT SR$;:RETURN
360 LPRINT:RETURN
 1000 A=1500:GOTO 20:REM FIDO-NOS SIMULATOR V2.2
 1010 GOSUB 100:HO=8:VE=3:GOSUB 110
 1020 PRINT"FIDO-NOS SIMULATOR V2.2"
 1030 HO=10:VE=5:GOSUB 110
 1040 PRINT"P.J. van der Mespel"
 1050 HO=12:VE=15:GOSUB 110
 1060 PRINT"Moment, a.u.b..."
 1062 DIM M$(8),F$(9),MF$(3),HL$(3),C$(10),EA(3)
 1064 DIM HD(3,5),MS$(100),AB(3),BR(3),RI$(2)
 1070 GOSUB 21190:REM INITIALISE
 1080 REM INPUT TIME + SPECIAL FUNCTIONS
 1090 GOSUB 100:PRINT
 1100 PRINT"Voer de volgende toetsen in i.v.m."
 1110 PRINT"de verschillende codes die de computers"
 1120 PRINT"voor deze letters en functie-toetsen"
 1130 PRINT"gebruiken."
 1140 PRINT:PRINT
 1150 PRINT"Download:":GOSUB 210:F1=ASC(IN$)
 1160 PRINT"Afbreken:":GOSUB 210:CC=ASC(IN$)
 1170 PRINT"Pauze   :":GOSUB 210:CS=ASC(IN$)
 1180 PRINT"Delete (wis)-toets :":GOSUB 210:WI=ASC(IN$)
 1190 PRINT"Datum (DDMMJJ):";:LL=6:GOSUB 20000
 1200 IF LEN(I$)<6 THEN 1190
 1210 MO=VAL(MID$(I$,3,2)):IF(MO<1) OR(MO>12) THEN 1190
 1220 DA=VAL(LEFT$(I$,2)):IF(DA<1) OR(DA>31) THEN 1190
 1230 JJ=VAL(RIGHT$(I$,2))
 1240 DA$=LEFT$(I$,2)+" "
 1250 DA$=DA$+MID$(MO$,(MO-1)*3+1,3)
 1260 DA$=DA$+" "+RIGHT$(I$,2)
 1270 SR=DA:G=0:GOSUB 21140:DM$=SR$+"-"
 1280 SR=MO:G=0:GOSUB 21140:DM$=DM$+SR$+"-"
 1290 SR=JJ:G=0:GOSUB 21140:DM$=DM$+"19"+SR$
 1300 PRINT"Tijd  (UUMMSS):";:LL=6:GOSUB 20000
 1310 IF LEN(I$)<6 THEN 1300
 1320 U$=LEFT$(I$,2)+":"+MID$(I$,3,2)+":"+RIGHT$(I$,2)
 1330 TD$=LEFT$(U$,5)+" uur"
 1340 REM START X-MODEM
 1350 GOSUB 100
 1360 HO=16:VE=3:GOSUB 110:PRINT"X-MODEM"
 1370 HO=10:VE=8:GOSUB 110
 1380 PRINT"Druk op een toets":GOSUB 210
 1390 HO=4:GOSUB 110:PRINT"U belt nu FIDO-NOS (035-45395)..."
 1400 HO=15:VE=16:GOSUB 110
 1410 T$="035-45395"
 1420 FOR P=1 TO LEN(T$)
 1430 PRINT MID$(T$,P,1);
 1440 FOR W=1 TO 150:NEXT W
 1450 NEXT P
 2000 REM LOGON
 2010 GOSUB 100:PC=1
 2020 E$="inlog1":GOSUB 20480
 2030 T$=".Your FIRST Name : #":GOSUB 20310
 2040 LL=20:GOSUB 20000:IF I$="" THEN 2030
 2050 NA$=I$:GOSUB 21530:IF PS>0 THEN 2090
 2060 T$="Your  LAST Name : #":GOSUB 20310
 2070 LL=20:GOSUB 20000:IF I$="" THEN 2060
 2080 NA$=NA$+" "+I$
 2090 T$=NA$:GOSUB 20240:IF Y=0 THEN 2030
 2100 T$="Wait ...":GOSUB 20310
 2110 W1$=NA$:W2$=NM$:FC=1:GOSUB 21580
 2120 IF SA=1 THEN NA$=NM$:PL$=WP$:GOTO 2290
 2130 REM NEW MEMBER
 2140 E$="nieuw1":GOSUB 20480
 2150 T$=".Where are you calling from? #":GOSUB 20310
 2160 LL=20:GOSUB 20000:PL$=I$:IF PL$="" THEN 2150
 2170 T$=I$:GOSUB 20240:IF Y=0 THEN 2150
 2180 T$="Choose a password: #":GOSUB 20310
 2190 LL=15:GOSUB 20000:IF I$="" THEN 2180
 2200 GOSUB 21530:IF PS=0 THEN 2230
 2210 T$=".must be 1 word":GOSUB 20310
 2220 GOTO 2180
 2230 PV$=I$
 2240 T$=I$:GOSUB 20240:IF Y=0 THEN 2180
 2250 E$="nieuw2":GOSUB 20480
 2260 T$=".En dan komt nu het uur van de waarheid:"
 2270 GOSUB 20310:T$="(weet je um nog ?)":GOSUB 20310
 2280 PRINT:PRINT
 2290 T$=".Password: #":GOSUB 20310
 2300 LL=15:PW=1:GOSUB 20000
 2310 W1$=I$:W2$=PV$:FC=1:GOSUB 21580
 2315 IF SA=1 THEN 2350
 2320 T$=".Wrong!":GOSUB 20310:PRINT
 2330 PC=PC+1:IF PC>3 THEN 7400
 2340 GOTO 2290
 2350 E$="inlog2":GOSUB 20480
 2360 REM MAILING LIST
 2365 PRINT
 2370 T$=".Lijst met nog openstaande post op "+DM$
 2380 T$=T$+", "+TD$:GOSUB 20310:PRINT
 2390 T$=NA$+"  1:1 2:1 3:1":GOSUB 20310
 2400 T$=".Einde lijst met nog openstaande post op "
 2410 T$=T$+DM$+", "+TD$:GOSUB 20310
 2420 E$="murphy":GOSUB 20480
 3000 REM MAIN MENU
 3010 PRINT:PRINT
 3020 K$="mfbsclvg":IF J<CM THEN 3090
 3030 T$="MAIN Section:":GOSUB 20310
 3040 M$(1)="M)sg-Section% 09F)ile-Section"
 3050 M$(2)="B)ulletins% 11S)tatistics"
 3060 M$(3)="C)hange% 14L)ist-Callers"
 3070 M$(4)="V)ersion% 13G)oodbye"
 3080 I=4:GOSUB 20550:IF CM=0 THEN 3000
 3090 J=J+1:GOSUB 20950
 3100 IF(K<1) OR(K>8) THEN J=0:CM=0:GOTO 3000
 3110 ON K GOTO 4000,7000,3120,3300,3330,3820,3790,7400
 3120 REM BULLETINS
 3130 K$="12345q":IF J<CM THEN 3260
 3140 T$=". 1) Uitleg bij de lijst met nog open"
 3150 T$=T$+"staande post":GOSUB 20310
 3160 T$=" 2) Lijst met nog openstaande post op "
 3170 T$=T$+DM$+", "+TD$:GOSUB 20310
 3180 T$=" 3) Nieuwe bestanden op deze Fido per "+DM$
 3190 GOSUB 20310
 3200 T$=" 4) Gebruiksaanwijzing Fido-Nos"
 3210 GOSUB 20310
 3220 T$=" 5) Bereikbaarheid van dit BBS"
 3230 GOSUB 20310
 3240 T$="Choose one: [Q=quit] #":GOSUB 20310:BT=1
 3250 I=1:GOSUB 20550:IF CM=0 THEN 3120
 3260 J=J+1:GOSUB 20950
 3270 IF(K<1) OR(K>6) THEN J=0:CM=0:GOTO 3120
 3280 IF K=6 THEN 3000
 3290 GOSUB 21840:GOTO 3120
 3300 REM STATISTICS
 3310 T$=DA$+"  "+U$:GOSUB 20310
 3320 E$="statistics":GOSUB 20480:GOTO 3000
 3330 REM CHANGE
 3340 K$="taphwlmfq":IF J<CM THEN 3520
 3350 T$=".---- Personal Info ----":GOSUB 20310
 3360 T$="Name% 14: "+NA$:GOSUB 20310
 3370 T$="Address% 11: "+PL$:GOSUB 20310
 3380 T$="Password% 10: "+PV$:GOSUB 20310
 3390 T$="Help Level% 08: "+HL$(HL):GOSUB 20310
 3400 T$=".----- Your System -----":GOSUB 20310
 3410 T$="Number of Columns: "+CL$:GOSUB 20310
 3420 T$="Number of Lines  : "+LI$:GOSUB 20310
 3430 T$="'More[Y,n]'      : OFF":GOSUB 20310
 3440 T$="Tab Expansion    : ON":GOSUB 20310
 3450 T$="Filler Nulls     : 0":GOSUB 20310:PRINT
 3460 M$(1)=".T)ab% 16A)ddress"
 3470 M$(2)="P)assword% 11H)elp-Level"
 3480 M$(3)="W)idth% 14L)ength"
 3490 M$(4)="M)ore% 15F)iller-Nulls"
 3500 M$(5)="Q)uit"
 3510 I=5:GOSUB 20550:IF CM=0 THEN 3330
 3520 J=J+1:GOSUB 20950
 3530 IF(K<1) OR(K>9) THEN J=0:CM=0:GOTO 3330
 3540 IF K>4 THEN 3560
 3550 ON K GOTO 3770,3570,3600,3660
 3560 ON K-4 GOTO 3720,3770,3770,3770,3000
 3570 T$="New address ["+PL$+"]: #"
 3580 LL=20:GOSUB 21020:IF I$="" THEN 3330
 3590 PL$=I$:GOTO 3330
 3600 T$="New password, 1 word ["+PV$+"]: #"
 3610 LL=15:GOSUB 21020:IF I$="" THEN 3330
 3620 GOSUB 21530:IF PS=0 THEN 3650
 3630 T$=".must be 1 word":GOSUB 20310
 3640 GOTO 3600
 3650 PV$=I$:GOTO 3330
 3660 REM HELP-LEVEL
 3670 T$="Help Level: 1=None, 2=Some, 3=All: ["
 3680 SR=HL:GOSUB 300:T$=T$+SR$+"]: #"
 3690 LL=1:GOSUB 21020:IF I$="" THEN 3330
 3700 U=VAL(I$):IF(U<1) OR(U>3) THEN 3780
 3710 HL=U:GOTO 3330
 3720 T$=".Number of columns on your display: 16 - 127"
 3730 T$=T$+" ["+CL$+"]: #"
 3740 LL=3:GOSUB 21020:IF I$="" THEN 3330
 3750 U=VAL(I$):IF(U<16) OR(U>127) THEN 3780
 3760 CL=U:CL$=I$:GOTO 3330
 3770 GOSUB 21840:GOTO 3330
 3780 J=0:CM=0:GOTO 3330
 3790 REM VERSION
 3800 T$="Fido Version 12p":GOSUB 20310
 3810 GOTO 3000
 3820 REM USERLIST
 3830 T$="Name or part of name to match (CR=all): #"
 3840 LL=30:GOSUB 21020:IF I$="" THEN 3880
 3850 W1$=I$:W2$=NM$
 3860 FC=0:GOSUB 21580:IF SA=1 THEN 3880
 3870 T$="Not found":GOSUB 20310:GOTO 3000
 3880 T$=NM$+"% 1422 Jun 91  16:23:25 "+WP$
 3890 GOSUB 20310:GOTO 3000
 4000 REM MSG-SECTION
 4010 MG=1:CR=0:K$="npeaclsgm12345<>.+-"+CHR$(13)
 4020 IF J<CM THEN 4190
 4030 PRINT
 4040 SR=MF:GOSUB 300
 4050 T$="Message Area   "+SR$+": "+MF$(MF):GOSUB 20310
 4060 IF EA(MF)=1 THEN 4130
 4070 SR=AB(MF):GOSUB 300
 4080 T$="Total messages:     "+SR$:GOSUB 20310
 4090 T$="Highest message:    "+SR$:GOSUB 20310
 4100 SR=BR(MF):GOSUB 300
 4110 T$="Highest read:       "+SR$:GOSUB 20310
 4120 EA(MF)=1
 4130 M$(1)=".N)ext% 09P)revious"
 4140 M$(2)="E)nter% 08A)rea-Change"
 4150 M$(3)="C)ontinuous   L)ist"
 4160 M$(4)="S)earch% 07G)oodbye"
 4170 M$(5)="M)ain-Menu"
 4180 I=5:GOSUB 20550:IF CM=0 THEN 4000
 4190 MG=0:J=J+1:IF VAL(C$(J))>5 THEN 4310
 4200 GOSUB 20950:IF(K<1) OR(K>20) THEN J=0:CM=0:GOTO 4000
 4210 H=INT(K/7)+1
 4220 ON H GOTO 4230,4240,4250
 4230 ON K GOTO 5850,5870,4600,4360,5890,4500
 4240 ON K-6 GOTO 5960,7400,4270,4290,4290,4290,4290
 4250 ON K-13 GOTO 4290,4320,4340,5660,4000,4000,5882
 4270 REM MAIN MENU
 4280 EA(MF)=0:RI=2:GOTO 3000
 4290 REM MESSAGE NUMBER
 4300 IF(K-9<=AB(MF)) THEN BR(MF)=K-9:GOTO 5660
 4310 T$=".No such message":GOSUB 20310
 4315 GOTO 4000
 4320 REM<
 4330 BR(MF)=1:RI=2:GOTO 4000
 4340 REM>
 4350 BR(MF)=AB(MF):RI=1:GOTO 4000
 4360 REM MSG-AREA CHANGE
 4370 IF J<CM THEN J=J+1:I$=C$(J):GOTO 4460
 4380 T$=".----- Message Areas -----":GOSUB 20310
 4390 FOR A=1 TO 3
 4400 SR=A:GOSUB 300
 4410 T$="    "+SR$+") "+MF$(A):GOSUB 20310
 4420 NEXT A
 4430 SR=MF:GOSUB 300
 4440 T$="Message Area ["+SR$+"]: #":GOSUB 20310
 4450 LL=1:GOSUB 20000
 4460 IF(I$="q") OR(I$="Q") OR(I$="") THEN 4000
 4470 U=VAL(I$):IF U=MF THEN 4000
 4480 IF(U>0) AND(U<4) THEN EA(MF)=0:MF=U:RI=2:GOTO 4000
 4490 J=0:CM=0:GOTO 4430
 4500 REM LIST HEADERS
 4505 BB=BR(MF)
 4510 SR=BR(MF):BW=HD(MF,SR):GOSUB 300
 4520 T$="#"+SR$+"   "+DA$+"  "+U$:GOSUB 20310
 4530 T$="From: "+MS$(BW):GOSUB 20310
 4540 T$="To:   "+MS$(BW+1):GOSUB 20310
 4550 T$="Subj: "+MS$(BW+2):GOSUB 20310
 4560 PRINT:IF N=1 THEN 4590
 4570 ON RI GOSUB 21780,21720
 4580 IF ED=0 THEN 4510
 4590 BR(MF)=BB:GOTO 4000
 4600 REM ENTER
 4650 IF AB(MF)<5 THEN 4680
 4660 T$=".-MAXIMAAL AANTAL MESSAGES (5) BEREIKT-":GOSUB 20310
 4670 GOTO 4000
 4680 IF MS<=71 THEN 4710
 4690 T$=".-MAXIMALE GEHEUGENRUIMTE BEREIKT-":GOSUB 20310
 4700 GOTO 4000
 4710 MN=AB(MF)+1:HD(MF,MN)=MS:SR=MN:GOSUB 300
 4720 T$="This will be message #"+SR$:GOSUB 20310
 4730 T$="From: "+NA$:GOSUB 20310
 4740 MS$(MS)=NA$
 4750 T$="To: #":LL=30:GOSUB 21020
 4760 IF I$="" THEN 4000
 4770 MS$(MS+1)=I$
 4780 T$="Subject: #":GOSUB 20310
 4790 LL=30:GOSUB 20000
 4800 IF I$="" THEN 4000
 4810 MS$(MS+2)=I$
 4820 T$=".Maximum message length, lines: 25":GOSUB 20310
 4830 T$="Enter your message, blank line to end"
 4840 GOSUB 20310
 4850 T$="Words will wrap automatically":GOSUB 20310
 4860 PRINT:RN=1:J=0:CM=0:Y$=""
 4870 SR=RN:G=1:GOSUB 21140:PRINT SR$;": ";Y$;
 4880 LL=CL-5:GOSUB 20000
 4890 IF I$="" THEN 4990
 4900 IF IN=13 THEN 4970
 4910 GOSUB 21530:IF PS=0 THEN 4970
 4920 GOSUB 120:FOR A=LEN(I$) TO 1 STEP-1
 4930 HO=HO-1:GOSUB 110:PRINT" ";
 4940 IF MID$(I$,A,1)=" " THEN Q1=A-1:Q2=A+1:A=1
 4950 NEXT A:Q$=I$:GOSUB 21980:I$=Q1$
 4960 GOSUB 22010:Y$=Q2$+IN$:PRINT
 4970 MS$(MS+2+RN)=I$:RN=RN+1
 4980 IF RN<=25 THEN 4870
 4982 T$=".Message is full":GOSUB 20310:J=0:CM=0:Y$=""
 4990 REM SUB-MENU ENTER
 5000 K$="scaledih":IF J<CM THEN 5050
 5010 PRINT
 5020 M$(1)="S)ave C)ontinue-adding A)bort L)ist E)dit-Line"
 5030 M$(2)="D)el-Line I)ns-Line H)eader"
 5040 I=2:GOSUB 20550:IF CM=0 THEN 4990
 5050 J=J+1:GOSUB 20950
 5060 IF(K<1) OR(K>8) THEN J=0:CM=0:GOTO 4990
 5070 ON K GOTO 5080,4980,5120,5650,5190,5400,5460,5550
 5080 REM SAVE.ENTER
 5090 T$="Saving your message":GOSUB 20310
 5100 AB(MF)=MN:PRINT:MS$(MS+2+RN)="$$$$$"
 5110 MS=MS+RN+3:RN=0:GOTO 4000
 5120 REM ABORT.ENTER
 5130 T$="Throw away the message":GOSUB 20240
 5140 IF Y=0 THEN 4990
 5150 FOR A=MS TO MS+2+RN:MS$(A)="":NEXT A
 5160 HD(MF,MN)=0
 5170 T$="Message aborted":GOSUB 20310
 5180 GOTO 4000
 5190 REM EDIT.ENTER
 5200 T$="Edit line number: #"
 5210 LL=2:GOSUB 21020
 5220 LZ=VAL(I$):IF(LZ<1) OR(LZ>RN-1) THEN 4990
 5230 SR=LZ:G=1:GOSUB 21140:LZ=LZ+2:Z$=SR$:W$=MS$(MS+LZ)
 5240 T$="."+Z$+": "+W$:GOSUB 20310
 5250 T$="Old string: #"
 5260 LL=20:GOSUB 21020:IF I$="" THEN 4990
 5270 W1$=I$:W2$=W$:T$="New string: #"
 5280 LL=20:GOSUB 21020:N$=I$
 5310 FC=0:GOSUB 21580:IF SA=1 THEN 5340
 5320 T$=". Cannot find '"+W1$+"'"
 5330 GOSUB 20310:GOTO 4990
 5340 Q$=W$:Q1=S-1:GOSUB 21980
 5360 Q2=S+LEN(W1$):GOSUB 22010
 5370 MS$(MS+LZ)=Q1$+N$+Q2$
 5380 T$="."+Z$+": "+MS$(MS+LZ):GOSUB 20310
 5390 GOTO 4990
 5400 REM DELETE.ENTER
 5410 T$="Delete line number: #"
 5420 LL=2:GOSUB 21020
 5430 DL=VAL(I$):IF(DL<1) OR(DL>RN-1) THEN 4990
 5440 FOR C=DL TO RN:MS$(MS+2+C)=MS$(MS+3+C):NEXT C
 5450 MS$(MS+2+RN)="":RN=RN-1:PRINT:GOTO 4990
 5460 REM INSERT.ENTER
 5465 IF RN>25 THEN 4982
 5470 T$=".Insert new line before line number: #"
 5480 LL=2:GOSUB 21020
 5490 DL=VAL(I$):IF(DL<1) OR(DL>RN-1) THEN 4990
 5500 T$="Enter new line now:":GOSUB 20310
 5510 SR=DL:G=1:GOSUB 21140:T$="."+SR$+": #":GOSUB 20310
 5520 LL=CL-5:GOSUB 20000
 5530 FOR C=RN TO DL STEP-1:MS$(MS+3+C)=MS$(MS+2+C):NEXT C
 5540 MS$(MS+2+DL)=I$:RN=RN+1:PRINT:GOTO 4990
 5550 REM HEADER.ENTER
 5560 T$="From: "+NA$:GOSUB 20310
 5570 T$="To: ["+MS$(MS+1)+"] #"
 5580 LL=30:GOSUB 21020:IF I$="" THEN 5600
 5590 MS$(MS+1)=I$
 5600 T$="Subject: ["+MS$(MS+2)+"] #":GOSUB 20310
 5610 LL=30:GOSUB 20000:IF I$="" THEN 4990
 5620 MS$(MS+2)=I$
 5630 GOTO 4990
 5640 REM LIST.ENTER
 5650 LE=1:MS$(MS+2+RN)="$$$$$"
 5660 REM READ MESSAGE
 5670 PRINT:PRINT
 5675 IF LE=1 THEN SR=MN:BW=MS:GOTO 5690
 5680 SR=BR(MF):BW=HD(MF,SR)
 5690 GOSUB 300
 5700 T$="#"+SR$+"   "+DA$+"  "+U$:GOSUB 20310
 5710 T$="From: "+MS$(BW):GOSUB 20310
 5720 T$="To:   "+MS$(BW+1):GOSUB 20310
 5730 T$="Subj: "+MS$(BW+2):GOSUB 20310
 5740 TL=1:PRINT
 5750 T$="":IF MS$(BW+2+TL)="$$$$$" THEN 5800
 5760 IF LE=1 THEN SR=TL:G=1:GOSUB 21140:T$=SR$+": "
 5770 T$=T$+MS$(BW+2+TL):GOSUB 20310
 5780 IF N=0 THEN TL=TL+1:GOTO 5750
 5790 CR=0
 5800 IF LE=1 THEN LE=0:GOTO 4990
 5810 IF CR=0 THEN 4000
 5820 ON RI GOSUB 21780,21720
 5830 IF ED=0 THEN 5660
 5840 GOTO 4000
 5850 REM NEXT
 5860 RI=2:GOTO 5882
 5870 REM PREVIOUS
 5880 RI=1
 5882 REM DEFAULT
 5884 LE=0:CR=0:GOTO 5820
 5890 REM CONTINUOUS
 5900 T$=".Start now, or Control-C to abort":GOSUB 20310
 5910 T$="Control-S to pause":GOSUB 20310
 5920 T$="Strike any key to start ...":GOSUB 20310
 5930 GOSUB 210:IN=ASC(IN$)
 5940 IF IN=CC THEN 4000
 5950 CR=1:GOTO 5660
 5960 REM SEARCH
 5970 PRINT
 5980 T$="Pattern to search for "
 5990 IF SP$<>"" THEN T$=T$+"["+SP$+"]"
 6000 T$=T$+": #":LT=LEN(T$)-2
 6010 LL=30:GOSUB 21020:IF I$="" THEN I$=SP$
 6020 IF LEN(I$)>13 THEN 6120
 6030 IF I$="" THEN 4000
 6040 W1$=I$:SP$=I$:BB=BR(MF):GOTO 6090
 6050 FOR C=0 TO 2
 6060 W2$=MS$(HD(MF,BR(MF))+C)
 6070 FC=0:GOSUB 21580:IF SA=1 THEN C=2
 6080 NEXT C:IF SA=1 THEN 5660
 6090 ON RI GOSUB 21780,21720
 6100 IF ED=0 THEN 6050
 6110 T$="Not found":GOSUB 20310:PRINT:BR(MF)=BB:GOTO 4000
 6120 T$="Too wide!":GOSUB 20310
 6130 SR=LT:GOSUB 300:T$="% "+SR$+"!%-13!":GOSUB 20310
 6140 J=0:CM=0:GOTO 5980
 7000 REM FILE-SECTION
 7010 K$="fdatgm":IF J<CM THEN 7090
 7020 PRINT
 7030 SR=F:GOSUB 300
 7040 T$="File Area   "+SR$+": "+F$(F):GOSUB 20310
 7050 M$(1)="F)iles% 14D)ownload"
 7060 M$(2)="A)rea-Change% 08T)ype"
 7070 M$(3)="G)oodbye% 12M)ain-Menu"
 7080 I=3:GOSUB 20550:IF CM=0 THEN 7000
 7090 J=J+1:GOSUB 20950
 7100 IF(K<1) OR(K>6) THEN J=0:CM=0:GOTO 7000
 7110 ON K GOTO 7270,7440,7120,7320,7400,3000
 7120 REM CHANGE FILE-AREA
 7130 IF J<CM THEN J=J+1:I$=C$(J):GOTO 7220
 7140 T$=".----- File Areas -----":GOSUB 20310
 7150 FOR A=1 TO 9:IF F$(A)="" THEN 7180
 7160 SR=A:GOSUB 300
 7170 T$="    "+SR$+") "+F$(A):GOSUB 20310
 7180 NEXT A
 7190 SR=F:GOSUB 300
 7200 T$="File Area ["+SR$+"]: #":GOSUB 20310
 7210 LL=1:GOSUB 20000
 7220 IF(I$="q") OR(I$="Q") OR(I$="") THEN 7000
 7230 U=VAL(I$)
 7240 IF(U>0) AND(U<4) THEN F=U:GOTO 7000
 7250 IF((U>4) AND(U<8)) OR(U=9) THEN F=U:GOTO 7000
 7260 J=0:CM=0:GOTO 7190
 7270 REM FILES
 7280 PRINT
 7290 SR=F:GOSUB 300
 7300 E$="area"+SR$:GOSUB 20480
 7310 PRINT:GOTO 7000
 7320 REM TYPE
 7330 T$=".File: #"
 7340 LL=16:GOSUB 21020
 7350 IF I$="" THEN 7000
 7360 GOSUB 21050
 7370 IF FO=1 THEN 7390
 7380 T$=".Can't find '"+I$+"'":GOSUB 20310:GOTO 7000
 7390 GOSUB 21840:GOTO 7000
 7400 REM GOODBYE
 7410 T$=".Logging "+NA$+" off at "+DA$+" "+U$
 7420 GOSUB 20310
 7430 END
 7440 REM DOWNLOAD
 7450 IF J<CM THEN J=J+1:I$=C$(J):GOTO 7500
 7460 T$=".A)scii, K)ermit, X)modem, Z)modem,"
 7470 T$=T$+" T)elink, ? for help":GOSUB 20310
 7480 T$="add C for CRC, ie.XC, etc:#":GOSUB 20310
 7490 LL=1:GOSUB 20000
 7500 IF(I$="x") OR(I$="X") THEN 7530
 7510 T$=".OPMERKING: U kunt alleen met XMODEM":GOSUB 20310
 7520 T$="downloaden!":GOSUB 20310:GOTO 7440
 7530 T$="XMODEM transfer":GOSUB 20310
 7540 T$=".File(s): #":LL=60:GOSUB 21020:IF I$="" THEN 7000
 7550 GOSUB 21530:IF PS=0 THEN 7570
 7560 T$=".You can only do one file at a time!":GOTO 7585
 7570 GOSUB 21050:IF FO=1 THEN 7590
 7580 T$=".Can't find '"+I$+"'"
 7585 GOSUB 20310:GOTO 7000
 7590 BL=INT(VAL(MID$(F$,17,5))/128+.5)
 7600 TT=INT(BL*1.448):TM=INT(TT/60):TS=TT-TM*60
 7610 T$=".Ready to send '"+I$+"'":GOSUB 20310
 7620 SR=TM:G=0:GOSUB 21140:MI$=SR$
 7630 SR=TS:G=0:GOSUB 21140:SE$=SR$
 7640 SR=VAL(MID$(F$,17,5)):GOSUB 300
 7650 T$=SR$+" Bytes total, "+MI$+":"+SE$+" transfer time"
 7660 GOSUB 20310
 7670 T$="Start now, or five Control-X's to abort"
 7680 GOSUB 20310:CX=0
 7690 GOSUB 210:IF IN$=CHR$(F1) THEN 7730
 7700 IF IN$=CHR$(CC) THEN CX=CX+1:IF CX>4 THEN 7720
 7710 GOTO 7690
 7720 T$="One or more files not sent":GOSUB 20310:GOTO 7000
 7730 T$=".Verricht verder de handelingen die bij":GOSUB 20310
 7740 T$="het door U gebruikte communicatie-":GOSUB 20310
 7750 T$="programma nodig zijn om een file van":GOSUB 20310
 7760 T$="Fido te downloaden.":GOSUB 20310
 7770 PRINT:GOTO 7000
20000 REM INPUT
20010 I$="":L=0
20020 IF RN>0 THEN I$=Y$:L=LEN(Y$):Y$=""
20030 GOSUB 210:IN=ASC(IN$)
20040 IF(IN>=A1) AND(IN<=Z1) THEN 20100
20050 IF(IN>=A2) AND(IN<=Z2) THEN 20100
20060 IF(IN>=32) AND(IN<=63) THEN 20100
20070 IF IN=13 THEN PRINT:PW=0:RETURN
20075 IF L=0 THEN 20030
20080 IF IN=WI THEN W=1:GOSUB 20170
20085 IF IN=CC THEN W=L:GOSUB 20170
20090 GOTO 20030
20100 L=L+1
20110 IF(L>LL) AND(RN>0) THEN RETURN
20120 IF L>LL THEN L=LL:GOTO 20030
20130 I$=I$+IN$
20140 IF PW=1 THEN PRINT".";:GOTO 20030
20150 PRINT IN$;
20160 GOTO 20030
20170 L=L-W:IF L<0 THEN L=0:RETURN
20180 GOSUB 120
20190 IF L=0 THEN I$="":GOTO 20210
20200 I$=LEFT$(I$,L)
20210 FOR TW=1 TO W
20215 HO=HO-1:IF HO<0 THEN HO=CL-1:VE=VE-1
20220 GOSUB 110:PRINT" ";
20230 GOSUB 110
20235 NEXT TW:RETURN
20240 REM YES/NO
20250 T$=T$+"? [Y,n]: #"
20260 GOSUB 20310:LL=1:GOSUB 20000
20270 IF I$="" THEN Y=1:RETURN
20280 IF(I$="y") OR(I$="Y") THEN Y=1:RETURN
20290 IF(I$="n") OR(I$="N") THEN Y=0:RETURN
20300 GOTO 20260
20310 REM PRINT TEXT
20320 Z=LEN(T$):N=0
20330 D=0:IF RIGHT$(T$,1)="#" THEN D=1
20340 IF LEFT$(T$,1)="." THEN PRINT:Z=Z-1:T$=RIGHT$(T$,Z)
20350 FOR P=1 TO(Z-D):HH=0
20360 IF MID$(T$,P,1)<>"%" THEN 20390
20370 HH=VAL(MID$(T$,P+2,2)):P=P+1
20380 FOR TL=1 TO HH
20390 PRINT MID$(T$,P,1);
20400 GOSUB 200
20410 IF IN$=CHR$(CS) THEN GOSUB 210
20420 IF IN$=CHR$(CC) THEN N=1:J=0:CM=0:P=Z-D:TL=HH
20430 IF HH=0 THEN 20450
20440 NEXT TL:HH=0:P=P+2
20450 NEXT P
20460 IF D=0 THEN PRINT
20470 RETURN
20480 REM READ DATA
20490 N=0:RESTORE
20500 READ F$:IF F$=E$ THEN 20520
20510 GOTO 20500
20520 READ T$:IF T$="$$$$$" THEN RETURN
20530 GOSUB 20310:IF N=1 THEN RETURN
20540 GOTO 20520
20550 REM PRINT MENU + INPUT
20560 FOR A=0 TO 10:C$(A)="":NEXT A
20570 IF(BT=1) THEN BT=0:GOTO 20740
20580 IF HL<3 THEN 20600
20590 FOR M=1 TO I:T$=M$(M):GOSUB 20310:NEXT M:PRINT
20600 T$="Command ":IF HL<>2 THEN 20670
20610 T$=T$+": "
20620 FOR B=1 TO LEN(K$)
20630 AC=ASC(MID$(K$,B,1))
20640 IF(AC<A1) OR(AC>Z1) THEN 20660
20650 T$=T$+CHR$(AC-A1+A2)+" "
20660 NEXT B
20670 IF MG=0 THEN 20730
20680 IF HL=1 THEN 20710
20690 IF HL=2 THEN GOSUB 20310:T$=""
20700 T$=T$+"or Message Number "
20710 SR=AB(MF):GOSUB 300:T$=T$+"1-"+SR$
20720 SR=BR(MF):GOSUB 300:T$=T$+" ["+SR$+" "+RI$(RI)+"]"
20730 T$=T$+": #":GOSUB 20310
20740 LL=60:GOSUB 20000
20750 IF(I$="") AND(MG=1) THEN I$=CHR$(13):GOTO 20770
20760 IF I$="" THEN CM=0:GOTO 20840
20770 CM=1:LW=0:Q=0
20780 FOR MC=1 TO LEN(I$)
20790 S$=MID$(I$,MC,1)
20800 IF(S$=" ") OR(S$=";") THEN 20850
20810 C$(CM)=C$(CM)+S$:V=1
20820 NEXT MC:IF V=0 THEN CM=CM-1:GOTO 20840
20830 IF LEN(C$(CM))>1 THEN LW=LW+1:GOSUB 20890
20840 J=0:RETURN
20850 IF V=0 THEN 20820
20860 GOSUB 20880:IF LW=1 THEN Q=CM
20870 V=0:CM=CM+1:GOTO 20820
20880 IF LEN(C$(CM))>1 THEN LW=LW+1:RETURN
20890 IF LW<2 THEN LW=0:Q=0:RETURN
20900 C$="":FOR C=1 TO LW
20910 C$=C$+C$(Q+C-1)+" "
20920 NEXT C:C$(Q)=LEFT$(C$,LEN(C$)-1):LW=0
20930 IF MC>LEN(I$) THEN CM=Q:RETURN
20940 C$(Q+1)=C$(CM):C$(CM)="":CM=Q+1:RETURN
20950 REM CHECK COMMANDS
20960 IF C$(J)="" THEN K=0:RETURN
20970 W1$=LEFT$(C$(J),1):W2$=K$
20980 FC=0:GOSUB 21580:K=S
20990 IF K>0 THEN RETURN
21000 T$=".'"+W1$+"' is not a command":GOSUB 20310
21010 RETURN
21020 REM PRINT TEXT + INPUT + CHECK
21030 IF J<CM THEN J=J+1:I$=C$(J):RETURN
21040 GOSUB 20310:GOSUB 20000:RETURN
21050 REM SEARCH FOR STRING IN DATA
21070 H$=I$:X$=I$:GOSUB 21910
21080 FO=1:SR=F:GOSUB 300:FA$="area"+SR$:RESTORE
21090 READ F$:IF F$<>FA$ THEN 21090
21100 READ F$:IF F$="$$$$$" THEN FO=0:GOTO 21130
21110 I$=F$:GOSUB 21530:IF PS<2 THEN 21100
21120 IF X$<>LEFT$(F$,PS-1) THEN 21100
21130 I$=H$:RETURN
21140 REM FORMAT NUMBER
21150 GOSUB 300:IF SR>9 THEN 21180
21160 IF G=1 THEN SR$=" "+SR$:GOTO 21180
21170 SR$="0"+SR$
21180 G=0:RETURN
21190 REM INITIALISE
21220 F=1:MF=1:M=0:G=0:CL=40:BT=0:LE=0
21230 PW=0:J=0:CM=0:RN=0:HL=3:MG=0:V=0
21240 EA(1)=0:EA(2)=0:EA(3)=0
21250 AB(1)=1:AB(2)=1:AB(3)=1
21260 BR(1)=1:BR(2)=1:BR(3)=1
21270 HL$(1)="None":HL$(2)="Some":HL$(3)="All"
21280 RI$(1)="P":RI$(2)="N"
21290 CL$="40":LI$="24":Y$="":RI=2:SA=0
21300 MO$="JanFebMarAprMayJunJulAugSepOctNovDec":SP$=""
21310 A1=ASC("a"):Z1=ASC("z"):A2=ASC("A"):Z2=ASC("Z")
21320 FOR A=1 TO 3:FOR B=1 TO 5:HD(A,B)=0:NEXT B:NEXT A
21330 FOR C=1 TO 100:MS$(C)="":NEXT C
21340 NM$="Peter.jan Van.der.mespel"
21350 PV$="test":WP$="Den Haag"
21360 F$(1)="NOS-Scoop Beeldkranten"
21370 F$(2)="Radio Netherlands files"
21380 F$(3)="Basicode programma gebied"
21390 F$(5)="Algemeen Up- en Download gebied"
21400 F$(6)="Fido - Ned. Net Nieuws - Utilities"
21410 F$(7)="HCC Nieuwsbrief":F$(4)="":F$(8)=""
21420 F$(9)="Files voor luister- en zendamateurs"
21430 MF$(1)="NOS-Scoop radio berichtengebied"
21440 MF$(2)="Radio Netherlands message area"
21450 MF$(3)="Berichten van en aan de sysop"
21460 T=1:RESTORE
21470 READ F$:IF F$<>"message" THEN 21470
21480 READ F$
21490 MS$(T)=F$:T=T+1:IF F$<>"$$$$$" THEN 21480
21500 MS=T
21510 FOR A=1 TO 3:HD(A,1)=1:NEXT A
21520 RETURN
21530 REM CHECK FOR SPACE
21540 PS=0:FOR A=1 TO LEN(I$)
21550 IF MID$(I$,A,1)=" " THEN PS=A:A=LEN(I$)
21560 NEXT A
21570 RETURN
21580 REM COMPARE STRINGS
21590 SA=0:S=0
21600 W1=LEN(W1$):W2=LEN(W2$)
21610 IF(W1=0) OR(W2=0) THEN RETURN
21620 IF W1$=W2$ THEN SA=1:S=1:RETURN
21630 IF W1>W2 THEN RETURN
21640 IF(FC=1) AND(W1<W2) THEN RETURN
21650 X$=W1$:GOSUB 21910:W3$=X$
21660 X$=W2$:GOSUB 21910
21670 AK=W2-W1+1
21680 FOR D=1 TO AK
21690 IF W3$=MID$(X$,D,W1) THEN SA=1:S=D:D=AK
21700 NEXT D
21710 RETURN
21720 REM NEXT MESSAGE
21730 ED=0:BR(MF)=BR(MF)+1
21740 IF BR(MF)>AB(MF) THEN BR(MF)=AB(MF):GOTO 21760
21750 RETURN
21760 T$=".Highest message":GOSUB 20310
21770 ED=1:RETURN
21780 REM PREVIOUS MESSAGE
21790 ED=0:BR(MF)=BR(MF)-1
21800 IF BR(MF)<1 THEN BR(MF)=1:GOTO 21820
21810 RETURN
21820 T$=".Lowest message":GOSUB 20310
21830 ED=1:RETURN
21840 REM WARNING
21850 T$=".Deze funktie/tekst is niet in de":GOSUB 20310
21860 T$="simulator aanwezig...":GOSUB 20310
21870 RETURN
21880 REM CONVERT CHARACTER TO UPPER CASE
21890 IF(X>=A1) AND(X<=Z1) THEN X=X-A1+A2
21900 RETURN
21910 REM CONVERT STRING TO UPPER CASE
21920 XX$="":IF X$="" THEN RETURN
21930 FOR LX=1 TO LEN(X$)
21940 X=ASC(MID$(X$,LX,1)):GOSUB 21880
21950 XX$=XX$+CHR$(X)
21960 NEXT LX:X$=XX$
21970 RETURN
21980 REM Q1$=LEFT$(Q$,Q1)
21990 IF Q1<1 THEN Q1$="":RETURN
22000 Q1$=LEFT$(Q$,Q1):RETURN
22010 REM Q2$=MID$(Q$,Q2)
22020 LQ=LEN(Q$):IF LQ<Q2 THEN Q2$="":RETURN
22030 Q2$=RIGHT$(Q$,LQ-Q2+1):RETURN
25000 DATA"inlog1"
25010 DATA"Fido v12p"
25020 DATA"FidoNet 2:500/202"
25030 DATA"% 22>--<      ["
25040 DATA" N O S - Scoop% 07/!oo )    [["
25050 DATA" Postbus 1200% 07( !  / )    [["
25060 DATA" 1200 BE Hilversum   / /   ]     [["
25070 DATA"% 20! hcc ! ]     ))"
25080 DATA"%*18  ! (&) !  ]   //"
25090 DATA"**     FIDO     **  ! nos ! /  ]//"
25100 DATA"**   net  500   **   [//!!  ]   )"
25110 DATA"**  node  202   **  ( /( !(    / (jm)"
25120 DATA"%*18  300/300,1200/75"
25130 DATA"% 161200/1200, 2400/2400"
25140 DATA"Sysops : Hans Janssen  /  Joop Engels","%=37"
25150 DATA"BELANGRIJK!!  Als  er  straks  om  je"
25160 DATA"naam gevraagd wordt, toets dan achter"
25170 DATA"'FIRST name'  je VOORNAAM  en  achter"
25180 DATA"' LAST name'  je ACHTERNAAM in."
25190 DATA"Dubbele  namen  of  voorvoegsels  svp"
25200 DATA"verbinden met een punt."
25210 DATA"Voorbeeld:  Jan.willem  Van.der.vlist"
25220 DATA"%=37","$$$$$"
25230 DATA"inlog2"
25240 DATA".Hallo, welkom op ons systeem..."
25250 DATA"Dit bulletin board is bestemd voor"
25260 DATA"radioluisteraars en HCC-leden in"
25270 DATA"regio 't Gooi. Vragen en mededelingen"
25280 DATA"mbt NOS-Scoop & Basicode richten aan:"
25290 DATA"HANS JANSSEN in berichten gebied 1"
25300 DATA"%-12","Hello,welcome to our system. This"
25310 DATA"bulletin board is open 21 hrs a day."
25320 DATA"Our speciality is broadcasting."
25330 DATA"Please address your messages concerning"
25340 DATA"Radio Netherlands (Hilversum) to:"
25350 DATA"ROCUS DE.JOODE in message area 2"
25360 DATA"%-14","$$$$$"
25370 DATA"statistics"
25380 DATA"% 18Total   Limit   Remaining"
25390 DATA"Total Calls:      59409"
25400 DATA"Your Calls:% 111"
25410 DATA"Connect Time:% 091% 0530% 0729"
25420 DATA"24 Hr Time Limit:% 1160% 0760"
25430 DATA"Credit:% 10f0,00% 12f0,00"
25440 DATA"$$$$$"
25450 DATA"murphy"
25460 DATA".Pas nadat een onderdeel goed is vastgezet#"
25470 DATA" zal blijken dat"
25480 DATA"men de tussenringetjes vergeten heeft."
25490 DATA"25e STELLING VAN MURPHY"
25500 DATA"$$$$$"
25510 DATA"area1"," %*74"
25520 DATA" *    F I L E  A R E A  1  :  N O S  -  #"
25530 DATA"S C O O P  T E X T - F I L E S    *"
25535 DATA" %*74"," "
25540 DATA"BOB-1.CAT         320 Index Best of#"
25550 DATA" Basicode no. 1"
25560 DATA"BOB-2.CAT         709 Index Best of#"
25570 DATA" Basicode no. 2"
25580 DATA"BOB-3.CAT         649 Index Best of#"
25581 DATA" Basicode no. 3"
25582 DATA"BOB-5.CAT        9140 Index Best of#"
25584 DATA" Basicode no. 5"," "
25600 DATA"AGENDA.BKR        745 Landelijke actuele#"
25610 DATA" computer-agenda uit de Beeldkrant"," "
25620 DATA"BLDKRANT.427     9959 Beeldkranten januari#"
25630 DATA" - juni / Gewetensvraag (15-7-91)"
25640 DATA"BLDKRANT.426    10765 MIR / Zonsverduistering#"
25650 DATA" / Microfoons deel 3 (15-7-91)"
25660 DATA"$$$$$"
25670 DATA"area2","%-61"
25680 DATA"    R A D I O   N E T H E R L A N D S   #"
25690 DATA"F I L E   A R E A","%-61"," "
25700 DATA"SCHEDULE.RNW    15872 Radio Netherlands#"
25710 DATA" Broadcasting Schedule (04/07/91)"
25720 DATA"PATHFIND.TXT    20580 Pathfinder: NASA#"
25730 DATA" Space program"
25740 DATA"SHUTTLE.FRQ      1755 Shortwave frequencies#"
25750 DATA" to monitor Space Shuttle"
25760 DATA"$$$$$"
25770 DATA"area3","%-78"
25780 DATA"    N O S  -  S C O O P      B A S I C O D E#"
25790 DATA"% 07P R O G R A M M A ' S","%-78"," "
25800 DATA"SPEURPUZ.BSC    19456 Speurpuzzel-oplosser#"
25810 DATA" (P.J. van der Mespel)"
25820 DATA"VALTABEL.BSC     4736 Valuta-tabel voor#"
25830 DATA" vakantiegangers (Chris Bouws)"
25840 DATA"BRIEFKRT.BSC     6400 Schrijf een #"
25850 DATA"briefkaart mbv uw printer (Chris Bouws)"
25860 DATA"$$$$$"
25870 DATA"area5"
25880 DATA"  Algemeen up- en download gebied% 16#"
25890 DATA"NOS-Scoop","%-78"
25900 DATA"  Geen gelicenseerde programma's uploaden,#"
25910 DATA" we zullen ze onherroepelijk"
25920 DATA"  verwijderen. U riskeert dan dat wij U #"
25930 DATA"de toegang tot dit systeem"
25940 DATA"  moeten ontzeggen !","%-78"," "
25950 DATA"FIDOUSER.NED    27008 Gebruiksaanwijzing #"
25955 DATA"Fido versie 11"
25960 DATA"ETIKET.BAS      31031 Etiketten aanmaken #"
25970 DATA"(demo programma in GW-Basic)"
25980 DATA"SUPERSOM.COM    12288 Cijferwerk voor lager#"
25990 DATA" onderwijs (I.B.M.)"
26000 DATA"$$$$$"
26010 DATA"area7","%-37"
26015 DATA" Files-gebied van de HCC Nieuwsbrief"
26017 DATA"%-37"," "
26020 DATA"RECENSIE.DOC      934 #"
26030 DATA"Instructie voor schrijven van recensie"
26040 DATA"VIDISAFE.ARC    71877 #"
26050 DATA"Software en documentatie"
26060 DATA"DEMO.ARC        51131 #"
26070 DATA"Voorbeelden en aanvullende informatie"
26080 DATA"$$$$$"
26090 DATA"area9","%-39","  FILES VOOR LUISTER- EN #"
26095 DATA"ZENDAMATEURS","%-39"," "
26100 DATA"MORSE.MSX        3968 #"
26110 DATA"Oefenprogramma Morse in MSX-Basic"
26120 DATA"IBMCW1.BAS       5120 #"
26130 DATA"Morsepgm voor IBM-PC en compatibles nr 1"
26140 DATA"IBMCW2.BAS       5120 #"
26150 DATA"Morsepgm voor IBM-PC en compatibles nr 2"
26160 DATA"$$$$$"
26170 DATA"message"
26180 DATA"Peter.jan Van.der.mespel","All","Message Areas"
26190 DATA"Deze brief staat niet in Fido-Nos, maar"
26200 DATA"dient alleen om U een beetje vertrouwd"
26210 DATA"te maken met het berichten-gebied van"
26220 DATA"Fido-Nos, vandaar dat ook de echte"
26230 DATA"berichten niet in dit programma zijn","verwerkt."
26240 DATA"De vriendelijke groeten van:"
26250 DATA"P.J. van der Mespel"
26260 DATA"$$$$$"
26270 DATA"area6","%-70"
26280 DATA"  Wekelijkse rapportage van het gebruik #"
26290 DATA"van deze Fido","%-70"
26300 DATA".  Hier vindt U de overzichten van het #"
26310 DATA"gebruik van deze Fidonode van de"
26320 DATA"  afgelopen week. Deze worden door Fido#"
26330 DATA" elke week automatisch"
26340 DATA"  gegenereerd."
26350 DATA" "
26360 DATA"DOWNLOG.LWK      9740 Lijst van door de#"
26370 DATA" gebruikers opgevraagde files"
26380 DATA"UTILLOG.LWK      3277 Statistisch over#"
26390 DATA"zicht van het gebruik van deze Fido"
26400 DATA"$$$$$"
26410 DATA"nieuw1"
26420 DATA".HOERA, een nieuwe op het systeem !!!!!"
26430 DATA"Welkom, welkom !!!"
26440 DATA"Denk je nu bij jezelf, achter het oor"
26450 DATA"krabbelend: 'Hoe kan dat nu?"
26460 DATA"Ik ben NIET nieuw op deze Fido ?'"
26470 DATA"Dan zijn er 2 mogelijkheden:"
26480 DATA"- Het is langer dan 2 maanden geleden,"
26490 DATA"  dat je voor het laatst ingebeld hebt"
26500 DATA"  Dan ben je eruit gewipt en word je"
26510 DATA"  weer beschouwd als 'nieuw'."
26520 DATA"- Je hebt je naam niet goed opgegeven."
26530 DATA"  Dan moet je nu de verbinding"
26540 DATA"  verbreken en opnieuw inbellen."
26550 DATA".Ben je wel nieuw, toets dan zodadelijk"
26560 DATA"je WOONPLAATS in. Kies daarna een"
26570 DATA"PASSWORD (wachtwoord) en onthoud dit"
26580 DATA"goed !!  (Je hebt 'm nog nodig....)"
26590 DATA"$$$$$"
26600 DATA"nieuw2"
26610 DATA".Okee, de formaliteiten hebben we gehad!"
26620 DATA"Vergeet vooral je 'password' niet,"
26630 DATA"anders kom je er de volgende keer niet"
26640 DATA"meer in!"
26850 DATA".Rest ons nog, je veel plezier toe te"
26860 DATA"wensen op deze Fido en voor vragen,"
26870 DATA"kommentaar en opmerkingen kan je"
26880 DATA"altijd een bericht naar ons toesturen!"
26890 DATA".Hans Janssen en Joop Engels, sysops","%-35"
26900 DATA"$$$$$"
30000 REM***********************
30010 REM*                     *
30020 REM* FIDO-SIMULATOR V2.2 *
30030 REM* =================== *
30040 REM*                     *
30050 REM* P.J. VAN DER MESPEL *
30060 REM*   ROSSINILAAN 491   *
30070 REM*  2551 MZ DEN HAAG   *
30080 REM*                     *
30090 REM*    14 JULI 1991     *
30100 REM***********************
30110 REM COPYRIGHT NOS HILVERSUM

10 GOTO 1000:REM Basicode-2 subroutines
20 CLEAR:IN$=" ":SR=0:SR$="":CT=0:CN=0:OHT%=80:OVT%=24
22 KEY OFF:SCREEN 0:WIDTH OHT%:COLOR 7,0,0:CLS:RANDOMIZE TIMER*100
29 GOTO 1010
100 CLS:RETURN
110 GOSUB 111:LOCATE OVE%+1,OHO%+1:RETURN
111 OVE%=INT(VE):OHO%=INT(HO)
112 IF OVE%<0 THEN OVE%=0 ELSE IF OVE%>=OVT% THEN OVE%=OVT%-1
113 IF OHO%<0 THEN OHO%=0 ELSE IF OHO%>=OHT% THEN OHO%=OHT%-1
114 RETURN
120 HO=POS(0)-1:VE=CSRLIN-1:RETURN
200 IN$=INKEY$:RETURN
210 IN$=INPUT$(1):RETURN
250 BEEP:RETURN
260 RV=RND:RETURN
270 FR=FRE(""):RETURN
300 SR$=MID$(STR$(ABS(SR)),2):IF LEFT$(SR$,1)="." THEN SR$="0"+SR$
301 IF SR<0 THEN SR$="-"+SR$
302 RETURN
310 O#=ABS(SR)+.5*10^-CN:O!=INT(O#):O#=1+O#-O!:O$=""
311 IF CN THEN O$="."+STRING$(CN,48):IF O#>1 THEN MID$(O$,1)=MID$(STR$(O#),3)
312 O$=MID$(STR$(O!),2)+O$:IF VAL(O$)>0 AND SR<0 THEN O$="-"+O$
313 IF LEN(O$)<=CT THEN SR$=SPACE$(CT-LEN(O$))+O$:RETURN
314 SR$=STRING$(CT,42):RETURN
350 LPRINT SR$;:RETURN
360 LPRINT:RETURN
 1000 A=500:GOTO 20:REM** TOP PARABOOL
 1010 GOSUB 100:GOSUB 19200:GOSUB 10000
 1020 GOSUB 100:A=1:B=4:C=0:X=-2:Y=-4:GOSUB 18500:GOSUB 10000
 1030 GOSUB 100:A=-1:B=4:C=1:X=2:Y=5:GOSUB 18500:GOSUB 10000
 1040 GOSUB 19000:GOSUB 10000
 1090 P=0:A=0:B=0:C=0:X=0:Y=0:E=0:F=0:H=0
 1100 GOSUB 100
 1110 PRINT:PRINT"Welke vorm komt met jouw vergelijking"
 1120 PRINT:PRINT"overeen ?    Kies dat nummer !!":PRINT
 1130 PRINT"----------------------------------------":PRINT
 1140 PRINT" 1    Y = A * ";:GOSUB 16000
 1150 PRINT" + B * X + C":PRINT:PRINT
 1160 PRINT" 2    Y = A * ";:GOSUB 16000
 1170 PRINT" + B * X":PRINT:PRINT
 1180 PRINT" 3    Y = A * ";:GOSUB 16000
 1190 PRINT" + C":PRINT:PRINT
 1200 PRINT" 4    Y = A * ";:GOSUB 16000:PRINT:PRINT
 1210 PRINT"----------------------------------------"
 1220 GOSUB 210
 1230 P=VAL(IN$)
 1240 IF(P<1) OR(P>4) THEN 1220
 1250 GOSUB 100
 1260 ON P GOTO 1300,2500,3000,3300
 1290 REM FUNCTIEVORM 1
 1300 GOSUB 16600
 1310 PRINT:PRINT"         Y = A * ";:GOSUB 16000
 1320 PRINT" + B * X + C":PRINT
 1330 GOSUB 16650
 1340 GOSUB 100:PRINT:PRINT"         Y = A * ";:GOSUB 16000
 1350 PRINT" + B * X + C":PRINT
 1360 PRINT"We gaan nu voor A, B en C getallen in":PRINT
 1370 PRINT"de vergelijking substitueren.":PRINT
 1380 PRINT"Opm: A kan ook 1 of -1, maar geen 0 zijn":PRINT
 1390 PRINT"Druk na elk getal op return":PRINT:PRINT" A= ";
 1395 INPUT A:IF A=0 THEN GOSUB 18400:GOTO 1090
 1400 PRINT:PRINT" B= ";:INPUT B
 1410 PRINT:PRINT" C= ";:INPUT C:PRINT
 1420 PRINT"Zijn de getallen zo goed ? (j/n)"
 1430 GOSUB 210:IF(IN$="n") OR(IN$="N") THEN 1340
 1440 GOSUB 16400:GOSUB 17000
 1450 GOSUB 210:IF(IN$="n") OR(IN$="N") THEN 1340
 1460 GOSUB 18100
 1470 GOSUB 10000:GOSUB 16400:GOSUB 18200
 1480 IF X=-(INT(100*B/(2*A)+.5)/100) THEN GOSUB 18300:GOTO 1500
 1490 GOSUB 18000:GOTO 1470
 1500 GOSUB 10000:GOSUB 16400:GOSUB 18360
 1510 PRINT"We gaan nu de waarde van X substitueren"
 1520 PRINT"in de vergelijking van de parabool!":PRINT:PRINT
 1530 PRINT"Bereken: A * ";:GOSUB 16000:PRINT:PRINT
 1540 PRINT"Met max 3 cijfers achter de punt";:INPUT E:PRINT
 1550 IF(E<(A*X*X-1E-03)) OR(E>(A*X*X+1E-03)) THEN 1570
 1560 GOSUB 18300:GOSUB 10000:GOSUB 16400:GOSUB 18360:GOTO 1580
 1570 GOSUB 18000:E=0:GOTO 1500
 1580 PRINT:PRINT"        A * ";:GOSUB 16000:PRINT"=";E
 1590 PRINT:PRINT:PRINT"Bereken    B * X":PRINT:H=B*X
 1600 INPUT F:IF(F<(H-1E-03)) OR(F>(H+1E-03)) THEN 1620
 1610 GOSUB 18300:GOSUB 10000:GOSUB 100:GOTO 1630
 1620 GOSUB 18000:GOSUB 10000:GOSUB 16400:GOSUB 18360:GOTO 1580
 1630 PRINT:PRINT"         Y = A * ";:GOSUB 16000
 1640 PRINT"+ B * X + C":PRINT:PRINT
 1650 PRINT"         A * ";:GOSUB 16000:PRINT"=";E;:PRINT
 1660 PRINT:PRINT"         B * X =";F;:PRINT:PRINT
 1670 PRINT"             C =";C;:PRINT
 1680 PRINT"        ------------+"
 1690 PRINT"             Y ="
 1700 INPUT Y:PRINT
 1710 IF(Y>(E+F+C-1E-03)) AND(Y<(E+F+C+1E-03)) THEN 1725
 1720 GOSUB 18000:GOSUB 10000:GOSUB 100:GOTO 1630
 1725 GOSUB 18300
 1730 PRINT"De coordinaten van de top zijn:":PRINT
 1740 PRINT"  (";X;",";Y;")":GOSUB 10000
 1750 IF(X>6) OR(X<-6) THEN 1780
 1760 IF(Y>5) OR(Y<-5) THEN 1780
 1770 IF(X=INT(X)) AND(Y=INT(Y)) THEN 1820
 1780 GOSUB 100:GOSUB 14900
 1800 IF(IN$="j") OR(IN$="J") THEN 1090
 1810 PRINT"    VEEL SUCCES BIJ DE REPETITIE !!":GOTO 9999
 1820 GOSUB 100:GOSUB 18500
 1830 GOSUB 10000:GOSUB 100:GOSUB 15000:GOTO 1800
 2490 REM FUNCTIEVORM 2
 2500 PRINT:GOSUB 16600:PRINT"           Y = A * ";
 2505 GOSUB 16000
 2510 PRINT"+ B * X":PRINT:GOSUB 16650:GOSUB 100
 2520 PRINT:PRINT"        Y = A * ";:GOSUB 16000
 2530 PRINT"+ B * X":PRINT
 2540 PRINT"We gaan nu voor A en B getallen in de":PRINT
 2550 PRINT"vergelijking substitueren.":PRINT
 2560 PRINT"Opm: A kan ook 1 of -1, maar geen 0 zijn":PRINT
 2570 PRINT"Druk na elk getal op return":PRINT
 2580 PRINT" A = ";:INPUT A:IF A=0 THEN GOSUB 18400:GOTO 1090
 2590 PRINT:PRINT" B = ";:INPUT B
 2600 PRINT:PRINT"Zijn de getallen zo goed ? (j/n)"
 2610 GOSUB 210:IF(IN$="n") OR(IN$="N") THEN GOSUB 100:GOTO 2520
 2620 GOSUB 16400:GOSUB 17000
 2630 GOSUB 210:IF(IN$="n") OR(IN$="N") THEN 2500
 2640 GOSUB 18100
 2650 GOSUB 10000:GOSUB 16400:GOSUB 18280
 2660 IF X=-(INT(100*B/(2*A)+.5)/100) THEN GOSUB 18300:GOTO 2680
 2670 GOSUB 18000:GOTO 2650
 2680 GOSUB 10000:GOSUB 16400:GOSUB 18340
 2690 PRINT"We gaan nu de waarde van X substitueren":PRINT
 2700 PRINT"in de vergelijking van de parabool !":PRINT:PRINT
 2710 PRINT"Bereken: A * ";:GOSUB 16000:PRINT:PRINT
 2720 PRINT"Met max 3 cijfers achter de punt";:INPUT E:PRINT
 2730 IF(E<(A*X*X-1E-03)) OR(E>(A*X*X+1E-03)) THEN 2750
 2740 GOSUB 18300:GOSUB 10000:GOSUB 16400:GOSUB 18340:GOTO 2760
 2750 GOSUB 18000:GOSUB 10000:E=0:GOTO 2680
 2760 PRINT:PRINT"        A * ";:GOSUB 16000:PRINT"=";E
 2770 PRINT:PRINT:PRINT"Bereken    B * X":PRINT:INPUT F
 2780 PRINT:IF(F>(B*X-1E-03)) AND(F<(B*X+1E-03)) THEN 2800
 2790 GOSUB 18000:GOSUB 10000:GOSUB 16400:GOSUB 18340:GOTO 2760
 2800 GOSUB 18300:GOSUB 10000:GOSUB 100
 2810 PRINT:PRINT"         Y = A * ";:GOSUB 16000
 2820 PRINT"+ B * X":PRINT:PRINT
 2830 PRINT"         A * ";:GOSUB 16000:PRINT"=";E;:PRINT
 2840 PRINT:PRINT"         B * X =";F;:PRINT:PRINT
 2850 PRINT"       -------------+"
 2860 PRINT"             Y ="
 2870 INPUT Y:PRINT
 2880 IF(Y>(E+F-1E-03)) AND(Y<(E+F+1E-03)) THEN 2895
 2890 GOSUB 18000:GOSUB 10000:GOSUB 100:GOTO 2810
 2895 GOSUB 18300
 2900 PRINT"De coordinaten van de top zijn:":PRINT
 2910 PRINT"   (";X;",";Y;")":GOSUB 10000:GOTO 1750
 2990 REM FUNCTIEVORM 3
 3000 PRINT:GOSUB 16600:PRINT"             Y = A * ";
 3010 GOSUB 16000:PRINT" + C":PRINT
 3020 PRINT"Je hebt nu een eenvoudige vergelijking":PRINT
 3030 PRINT"van een parabool !":PRINT
 3040 PRINT"Bij dit soort vergelijking is de":PRINT
 3050 PRINT"X-coordinaat van de top altijd 0 (nul)":PRINT
 3060 X=0
 3070 PRINT"De Y-coordinaat van de top heeft de":PRINT
 3080 PRINT"waarde van de letter C.":GOSUB 10000
 3090 GOSUB 100:PRINT:PRINT"            Y = A * ";
 3100 GOSUB 16000:PRINT" + C":PRINT
 3110 PRINT"We gaan nu voor A en C getallen in de":PRINT
 3120 PRINT"vergelijking substitueren.":PRINT
 3130 PRINT"Denk er om: A kan ook 1 of -1 zijn":PRINT
 3140 PRINT"Druk na elk getal op return":PRINT
 3150 PRINT" A = ";:INPUT A:IF A=0 THEN GOSUB 18400:GOTO 1090
 3160 PRINT:PRINT" C = ";:INPUT C:Y=C
 3170 PRINT:PRINT"Zijn de getallen zo goed ? (j/n)"
 3180 GOSUB 210:IF(IN$="n") OR(IN$="N") THEN 3090
 3190 PRINT:PRINT"De coordinaten van de top zijn:":PRINT
 3200 PRINT"   ( 0,";Y;")":GOSUB 10000:GOTO 1750
 3290 REM FUNCTIEVORM 4
 3300 PRINT:GOSUB 16600
 3310 PRINT"           Y = A * ";:GOSUB 16000:PRINT:PRINT
 3320 PRINT"Je hebt nu de eenvoudigste vergelijking":PRINT
 3330 PRINT"van een parabool !":PRINT:PRINT
 3340 PRINT"Bij dit soort vergelijking heeft de top":PRINT
 3350 PRINT"altijd de coordinaten ( 0, 0 )":PRINT:PRINT
 3360 GOSUB 10000
 3370 GOSUB 100:PRINT:PRINT"            Y = A * ";
 3380 GOSUB 16000:PRINT:PRINT
 3390 PRINT"We gaan nu voor A een getal in de":PRINT
 3400 PRINT"vergelijking substitueren.":PRINT
 3410 PRINT"Denk er om: A kan ook 1 of -1 zijn":PRINT:PRINT
 3420 PRINT"   A = ";:INPUT A:IF A=0 THEN GOSUB 18400:GOTO 1090
 3430 X=0:Y=0:PRINT:PRINT"Is het getal A juist ? (j/n)"
 3440 GOSUB 210:IF(IN$="n") OR(IN$="N") THEN 3370
 3450 PRINT:PRINT"De coordinaten van de top zijn:":PRINT
 3460 PRINT:PRINT"      ( 0, 0 )":GOSUB 10000:GOTO 1750
 9999 END
10000 VE=23:HO=0:GOSUB 110:REM* POSITIE
10010 PRINT"Druk voor vervolg op de spatiebalk !";
10020 GOSUB 210:IF IN$<>" " THEN 10020
10030 RETURN
14900 PRINT:PRINT"        Jammer, deze parabool":PRINT
14910 PRINT"         kan ik niet tekenen":PRINT
15000 PRINT"Wil je nog een TOP berekenen ? (j/n)":PRINT
15010 GOSUB 210:PRINT:RETURN
16000 REM** KWADRAAT AFDRUK **
16010 PRINT"X";:GOSUB 16100
16020 PRINT"2";:GOSUB 16200
16030 RETURN
16100 REM** CURSOR OMHOOG **
16110 GOSUB 120
16120 VE=VE-1
16130 GOSUB 110
16140 RETURN
16200 REM** CURSOR OMLAAG **
16210 VE=VE+1:HO=HO+1
16220 GOSUB 110
16230 RETURN
16400 GOSUB 100:REM** VERGELIJKINGEN **
16410 IF(B<0) AND(C<0) THEN 16510
16420 IF(B<0) AND(C=0) THEN 16490
16430 IF(B<0) AND(C>0) THEN 16530
16440 IF(B>0) AND(C=0) THEN 16590
16450 IF(B>0) AND(C<0) THEN 16550
16470 GOTO 16570
16490 PRINT:PRINT"        Y =";A;"* ";:GOSUB 16000
16500 PRINT" ";B;"* X":PRINT:PRINT:RETURN
16510 PRINT:PRINT"        Y =";A;"* ";:GOSUB 16000
16520 PRINT" ";B;"* X  ";C;:PRINT:PRINT:RETURN
16530 PRINT:PRINT"        Y =";A;"* ";:GOSUB 16000
16540 PRINT" ";B;"* X  +";C;:PRINT:PRINT:RETURN
16550 PRINT:PRINT"        Y =";A;"* ";:GOSUB 16000
16560 PRINT"+";B;"* X  ";C;:PRINT:PRINT:RETURN
16570 PRINT:PRINT"        Y =";A;"* ";:GOSUB 16000
16580 PRINT"+";B;"* X  +";C;:PRINT:PRINT:RETURN
16590 PRINT:PRINT"        Y =";A;"* ";:GOSUB 16000
16595 PRINT" +";B;"* X":PRINT:PRINT:RETURN
16600 PRINT"De vergelijking van de parabool ziet er":PRINT
16610 PRINT"dus zo uit:":PRINT:RETURN
16650 PRINT"Het is mogelijk dat er op de plaats van":PRINT
16660 PRINT"de letter A geen getal staat! Neem dan:":PRINT
16670 PRINT"                A = 1":PRINT
16680 PRINT"Het is ook mogelijk dat er voor de term":PRINT
16690 PRINT"** ";:GOSUB 16000:PRINT" ** een - teken staat,"
16700 PRINT:PRINT"dan is:"
16710 PRINT"                A = -1":PRINT
16720 PRINT:PRINT"Maar denk er om, A mag geen 0 (nul) zijn"
16730 GOSUB 10000:RETURN
17000 PRINT"Ziet de vergelijking er net zo uit als":PRINT
17010 PRINT"in je boek ?  Tik j(ja) of n(nee) in!":PRINT
17020 RETURN
18000 PRINT"Helaas FOUT ! Probeer het nog eens !":PRINT
18020 RETURN
18100 PRINT"We gaan nu de coordinaten van de top":PRINT
18110 PRINT"van de parabool berekenen:":PRINT
18120 PRINT"De X coordinaat van de top is te":PRINT
18130 PRINT"berekenen met de formule:":PRINT
18140 PRINT"          **   -B    **"
18150 PRINT"          **  -----  **"
18160 PRINT"          **  (2*a)  **":PRINT:RETURN
18200 PRINT"        Y = A * ";:GOSUB 16000
18210 PRINT"+ B * X  + C":PRINT:PRINT
18220 PRINT"          X = -B/(2*A)":PRINT
18230 PRINT"Bereken X en tik het antwoord in.":PRINT
18240 PRINT"Mocht het antwoord een breuk bevatten":PRINT
18250 PRINT"gebruik dan een punt, bijv. 1.5 en niet":PRINT
18260 PRINT"1,5 en maar 1 cijfer achter de punt !!!":PRINT
18270 INPUT X:PRINT:RETURN
18280 PRINT"        Y = A * ";:GOSUB 16000
18290 PRINT" + B * X":PRINT:GOSUB 18220:RETURN
18300 PRINT:PRINT"Dit antwoord is GOED !":PRINT:PRINT:RETURN
18340 PRINT"        Y = A * ";:GOSUB 16000
18350 PRINT" + B * X":PRINT:GOTO 18380
18360 PRINT"        Y = A * ";:GOSUB 16000
18370 PRINT"+ B * X + C":PRINT
18380 PRINT"        X =";X;:PRINT:PRINT:PRINT:RETURN
18400 REM A mag niet 0 zijn
18410 PRINT"Je mag voor A geen nul gebruiken."
18420 PRINT:PRINT"Zo krijg je geen parabool.":GOSUB 10000
18430 RETURN
18500 PRINT"                   +5"
18520 PRINT"                   !"
18530 PRINT"                   +4"
18540 PRINT"                   !"
18550 PRINT"                   +3"
18560 PRINT"                   !"
18570 PRINT"                   +2"
18580 PRINT"                   !"
18590 PRINT"                   +1"
18600 PRINT"                   !"
18610 PRINT"-+--+--+--+--+--+--0--+--+--+--+--+--+-"
18620 PRINT"-6 -5 -4 -3 -2 -1  !  1  2  3  4  5  6"
18630 PRINT"                   +-1"
18640 PRINT"                   !"
18650 PRINT"                   +-2"
18660 PRINT"                   !"
18679 PRINT"                   +-3"
18680 PRINT"                   !"
18690 PRINT"                   +-4"
18700 PRINT"                   !"
18710 PRINT"                   +-5"
18715 VE=21:HO=0:GOSUB 110:PRINT"Top (";X;",";Y;")"
18720 HO=20:VE=11:GOSUB 110
18730 HO=((HO+3*X)-1):VE=((VE-2*Y)-1):GOSUB 110
18740 PRINT"o":GOSUB 10000
18750 FOR K=(X-6) TO(X+6)
18760 HO=20:VE=11:GOSUB 110
18770 L=0:IF(K<-6) OR(K>6) THEN 18820
18780 L=(((A*K*K)+(B*K))+C)
18790 IF(L<-5) OR(L>5) THEN 18820
18800 HO=((HO+3*K)-1):VE=((VE-2*L)-1)
18810 GOSUB 110:PRINT"*"
18820 NEXT K
18830 VE=22:HO=7:GOSUB 110
18840 PRINT"parabool Y =";A;"* ";:GOSUB 16000
18850 PRINT"+";B;"* X +";C
18860 RETURN
19000 REM* TITEL *
19010 GOSUB 100
19020 PRINT"Dit programma helpt je bij het vinden":PRINT
19030 PRINT"van de top van een parabool.":PRINT:PRINT
19040 PRINT"De vergelijking van de parabool kan":PRINT
19050 PRINT"diverse vormen hebben, daarom moet":PRINT
19060 PRINT"je uit de 4 mogelijkheden die op het":PRINT
19070 PRINT"scherm verschijnen, die vergelijking":PRINT
19080 PRINT"kiezen, die met jouw vergelijking":PRINT
19090 PRINT"overeenkomt!"
19100 RETURN
19200 HO=0:VE=6:GOSUB 110
19210 PRINT"****"
19220 PRINT"*  *"
19230 PRINT"*  *"
19240 PRINT"****"
19250 PRINT"*":PRINT"*":PRINT"*"
19260 HO=5:VE=6:GOSUB 110
19270 PRINT" ** "
19280 HO=5:VE=7:GOSUB 110
19290 PRINT"*  *"
19300 HO=5:VE=8:GOSUB 110
19310 PRINT"*  *"
19320 HO=5:VE=9:GOSUB 110
19330 PRINT"****"
19340 HO=5:VE=10:GOSUB 110
19350 PRINT"*  *"
19360 HO=5:VE=11:GOSUB 110
19370 PRINT"*  *"
19380 HO=5:VE=12:GOSUB 110
19390 PRINT"*  *"
19400 HO=10:VE=6:GOSUB 110:PRINT"****"
19410 HO=10:VE=7:GOSUB 110:PRINT"*  *"
19420 HO=10:VE=8:GOSUB 110:PRINT"*  *"
19430 HO=10:VE=9:GOSUB 110:PRINT"****"
19440 HO=10:VE=10:GOSUB 110:PRINT"*  *"
19450 HO=10:VE=11:GOSUB 110:PRINT"*   *"
19460 HO=10:VE=12:GOSUB 110:PRINT"*   *"
19470 HO=16:VE=6:GOSUB 110:PRINT" ** "
19480 HO=16:VE=7:GOSUB 110:PRINT"*  *"
19490 HO=16:VE=8:GOSUB 110:PRINT"*  *"
19500 HO=16:VE=9:GOSUB 110:PRINT"****"
19510 HO=16:VE=10:GOSUB 110:PRINT"*  *"
19520 HO=16:VE=11:GOSUB 110:PRINT"*  *"
19530 HO=16:VE=12:GOSUB 110:PRINT"*  *"
19540 HO=21:VE=6:GOSUB 110:PRINT"*** "
19550 HO=21:VE=7:GOSUB 110:PRINT"*  *"
19560 HO=21:VE=8:GOSUB 110:PRINT"*  *"
19570 HO=21:VE=9:GOSUB 110:PRINT"*** "
19580 HO=21:VE=10:GOSUB 110:PRINT"*  *"
19590 HO=21:VE=11:GOSUB 110:PRINT"*  *"
19600 HO=21:VE=12:GOSUB 110:PRINT"*** "
19610 HO=26:VE=6:GOSUB 110:PRINT" ** "
19620 HO=26:VE=7:GOSUB 110:PRINT"*  *"
19630 HO=26:VE=8:GOSUB 110:PRINT"*  *"
19640 HO=26:VE=9:GOSUB 110:PRINT"*  *"
19650 HO=26:VE=10:GOSUB 110:PRINT"*  *"
19660 HO=26:VE=11:GOSUB 110:PRINT"*  *"
19670 HO=26:VE=12:GOSUB 110:PRINT" ** "
19680 HO=31:VE=6:GOSUB 110:PRINT" ** "
19690 HO=31:VE=7:GOSUB 110:PRINT"*  *"
19700 HO=31:VE=8:GOSUB 110:PRINT"*  *"
19710 HO=31:VE=9:GOSUB 110:PRINT"*  *"
19720 HO=31:VE=10:GOSUB 110:PRINT"*  *"
19730 HO=31:VE=11:GOSUB 110:PRINT"*  *"
19740 HO=31:VE=12:GOSUB 110:PRINT" ** "
19750 HO=36:FOR VE=6 TO 11:GOSUB 110:PRINT"*":NEXT VE
19760 HO=36:VE=12:GOSUB 110:PRINT"****"
19770 HO=8:VE=16:GOSUB 110
19780 PRINT"Eerst twee voorbeelden:"
19990 RETURN
30000 REM*****************************
30010 REM*     27 DECEMBER 1988      *
30020 REM*    NAAR EEN IDEE VAN      *
30030 REM*    G.J. SCHEPPINK EN      *
30040 REM*      N.BOSSCHAERT         *
30050 REM* BEGONNEN OP EEN CBM 3000  *
30060 REM*  UITGEBREID OP EEN P 2000 *
30070 REM*                           *
30080 REM*       OVERGEZET IN        *
30090 REM*        BASICODE 2         *
30100 REM*    OP EEN COMMODORE 64    *
30110 REM*    DOOR G. HOOGENDOORN    *
30120 REM*         SOERENSEBEEK 5    *
30130 REM*         8033 DL ZWOLLE    *
30140 REM*         TEL 038-538262    *
30150 REM*****************************
30160 REM
30170 REM COPYRIGHT N.O.S. HILVERSUM

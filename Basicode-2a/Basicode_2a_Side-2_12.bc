
1000 A=100:GOTO20:REM RADIOAKTIEF VERVAL
1010 LETXM=40:LETYM=23
1020 LETZM=3:LETC=XM*YM
1030 LETE=1:LETN=0
1040 DIMA(XM-E,YM-E)
1050 LETP$=".":LETS$=""
1060 FORX=E TOXM
1070 LETS$=S$+"*":NEXTX
1090 REM
1100 REM*** MENU ***
1110 GOSUB100
1120 PRINT"          RADIOAKTIEF VERVAL"
1130 PRINT"          ==================":PRINT
1140 PRINT"MAKER:   E. IVENS, DOETINCHEM":PRINT:PRINT
1150 PRINT"U KUNT KIEZEN UIT:":PRINT
1160 PRINT"1. UITVOERING VAN DE 'PROEF'":PRINT
1170 PRINT"2. UITLEG VOOR DE LEERLING":PRINT
1180 PRINT"3. UITLEG VOOR DE LERAAR":PRINT
1190 PRINT"4. UITLEG VOOR DE PROGRAMMEUR":PRINT
1200 PRINT"5. STOPPEN.":PRINT:PRINT
1210 PRINT"WELK NUMMER KIEST U ? ";
1220 GOSUB210:REMHAAL TOETS
1230 LETA=VAL(IN$)
1240 IFA<1 ORA>5 THEN1260
1250 ONA GOSUB1500,2000,3000,4000,5000
1260 LETHO=N:LETVE=19
1270 GOSUB110
1280 PRINT"U MOET EEN 1, 2, 3, 4 OF 5 INDRUKKEN !":PRINT
1290 GOTO1210
1490 REM
1500 REM*** PROEF ***
1510 GOSUB100
1520 FORY=N TOYM-E
1530 LETHO=0:LETVE=Y
1540 GOSUB110
1550 PRINTS$;
1560 NEXTY
1570 LETHO=ZM:LETVE=YM
1580 GOSUB110
1590 PRINT"EVEN WACHTEN !";
1600 FORX=N TOXM-E
1610 FORY=N TOYM-E
1620 LETA(X,Y)=E
1630 NEXTY
1640 NEXTX
1650 LETHO=ZM:LETVE=YM
1660 GOSUB110
1670 PRINTC;"  DRUK EEN TOETS IN ";
1680 GOSUB210:REMHAAL TOETS
1690 LETHO=ZM:LETVE=YM
1700 GOSUB110
1710 PRINTC;"  AFBREKEN MET 'RETURN'";
1720 GOSUB260:REMRANDOM
1730 LETHO=INT(XM*RV)
1740 GOSUB260:REMRANDOM
1750 LETVE=INT(YM*RV)
1760 IFA(HO,VE)=N THEN1780
1770 LETC=C-E
1780 IFA(HO,VE)=E THEN1800
1790 LETC=C-N
1800 LETA(HO,VE)=N
1810 GOSUB110:PRINTP$;
1820 LETHO=ZM:LETVE=YM
1830 GOSUB110:PRINTC;" ";
1840 IFC=N THEN1920
1850 GOSUB200
1860 IFIN$="" THEN1720
1870 IFIN$<>CHR$(13) THEN1720
1880 LETHO=ZM:LETVE=YM
1890 GOSUB110
1900 PRINTC;"                       ";
1910 GOTO1950
1920 LETHO=N:LETVE=YM
1930 GOSUB110
1940 PRINT"ALLE ATOMEN ZIJN VERVALLEN.  ";
1950 GOSUB6000
1960 LETC=XM*YM
1970 GOTO1100
1990 REM
2000 REM*** LEERLING ***
2010 GOSUB100
2020 PRINT"UITLEG VOOR DE LEERLING."
2030 PRINT"========================":PRINT
2040 PRINT"BIJ HET BEGIN VAN DE PROEF VERTOONT HET":PRINT
2050 PRINT"SCHERM ";C;" STERRETJES. ELK STERRETJE":PRINT
2060 PRINT"STELT EEN RADIOAKTIEF ATOOM VOOR DAT ZAL"
2070 PRINT"VERVALLEN TOT EEN ANDER ATOOM (VOORGE-":PRINT
2080 PRINT"STELD DOOR EEN PUNTJE). WANNEER DAT":PRINT
2090 PRINT"VERVAL OPTREEDT IS NIET VAN TE VOREN TE":PRINT
2100 PRINT"ZEGGEN.":PRINT
2110 PRINT"WEL KAN AANGETOOND WORDEN DAT NA EEN":PRINT
2120 PRINT"ZEKERE TIJD (DE HALFWAARDETIJD) GEMID-":PRINT
2130 PRINT"DELD NOG DE HELFT VAN HET OORSPRONKE-":PRINT
2140 PRINT"LIJKE AANTAL OVER IS.";
2150 GOSUB6000
2190 REM
2200 GOSUB100
2210 PRINT"ALS U DE PROEF DOET KUNT U EEN AANTAL":PRINT
2220 PRINT"MALEN DE HALFWAARDETIJD METEN. BIJV. VAN"
2230 PRINT"900 NAAR 450 ATOMEN, DAARNA VAN 400-200,"
2240 PRINT"VAN 180-90 EN VAN 80-40. MAAK GEBRUIK":PRINT
2250 PRINT"VAN DE TELLER ONDER IN HET BEELD, DIE":PRINT
2260 PRINT"LAAT ZIEN HOEVEEL RADIOAKTIEVE KERNEN":PRINT
2270 PRINT"NOG OVER ZIJN.":PRINT:PRINT
2280 PRINT"SUCCES MET DE PROEF !"
2290 GOSUB6000
2300 GOTO1100
2900 REM
3000 REM*** LERAAR ***
3010 GOSUB100
3020 PRINT"UITLEG VOOR DE LERAAR."
3030 PRINT"======================":PRINT
3040 PRINT"DIT PROGRAMMA IS BRUIKBAAR VOOR DE":PRINT
3050 PRINT"KLASSIKALE BEHANDELING VAN DE BEGRIPPEN":PRINT
3060 PRINT"VERVAL EN HALFWAARDETIJD. U KUNT HET OOK"
3070 PRINT"GEBRUIKEN ALS VERVANGEND PRAKTIKUM VOOR":PRINT
3080 PRINT"DE METING VAN DE HALFWAARDETIJD.":PRINT
3090 PRINT"ALS U DE 'PROEF' ENIGE MALEN HERHAALT":PRINT
3100 PRINT"MAAKT HET WEL UIT OF UW KOMPUTER ECHTE":PRINT
3110 PRINT"RANDOM-NUMMERS GEEFT, OF PSEUDO-RANDOM.":PRINT
3120 PRINT"IN HET LAATSTE GEVAL WORDT DE PROEF ELKE"
3130 PRINT"KEER IDENTIEK HERHAALT, WAT MINDER":PRINT
3140 PRINT"WENSELIJK IS.";
3150 GOSUB6000
3190 REM
3200 GOSUB100
3210 PRINT"DE HALFWAARDETIJD VAN DIT PROGRAMMA IS":PRINT
3220 PRINT"STERK AFHANKELIJK VAN DE GEBRUIKTE KOM-":PRINT
3230 PRINT"PUTER. OP DE APPLE IS HET CIRCA  86 S.":PRINT
3240 PRINT"EEN KORTERE (KONSTANTE) HALFWAARDETIJD":PRINT
3250 PRINT"IS MET GEBRUIK VAN BASICODE NIET MOGE-":PRINT
3260 PRINT"LIJK. EEN LANGERE WEL.":PRINT:PRINT
3270 PRINT"ZIE VERDER 'UITLEG VOOR DE PROGRAMMEUR'"
3280 GOSUB6000
3290 GOTO1100
3900 REM
4000 REM*** PROGRAMMEUR ***
4010 GOSUB100
4020 PRINT"UITLEG VOOR DE PROGRAMMEUR."
4030 PRINT"===========================":PRINT
4040 PRINT"U KUNT DE HALFWAARDETIJD KORTER MAKEN":PRINT
4050 PRINT"DOOR IN REGEL 1810+1830 DE 'GOSUB 110'":PRINT
4060 PRINT"TE VERVANGEN DOOR 'KOMPUTER-EIGEN' OP-":PRINT
4070 PRINT"DRACHTEN. REGEL 1820 VERVALT DAN.":PRINT
4080 PRINT"OOK DE 'GOSUB 260' IN REGELS 1720 EN":PRINT
4090 PRINT"1740 KAN VERMOEDELIJK KORTER. VERDER":PRINT
4100 PRINT"KUNT U 1850 WIJZIGEN IN 'GOTO 1730'.":PRINT
4110 PRINT"LANGERE HALFWAARDETIJDEN ZULLEN GEEN":PRINT
4120 PRINT"PROBLEEM GEVEN: EEN WACHTLUSJE OP BIJV.":PRINT
4130 PRINT"REGEL 1835 (EVENTUEEL MET RANDOM WACHT-":PRINT
4140 PRINT"TIJD PER PROEF).";
4150 GOSUB6000
4190 REM
4200 GOSUB100
4210 PRINT"U ZULT MISSCHIEN DE KONSTRUKTIE VAN":PRINT
4220 PRINT"REGEL 1760 TOT EN MET 1790 WAT OMSLACH-":PRINT
4230 PRINT"TIG VINDEN. DEZE METHODE IS ECHTER":PRINT
4240 PRINT"BEWUST GEVOLGD OM EEN HALFWAARDETIJD TE":PRINT
4250 PRINT"VERKRIJGEN DIE NIET STEEDS KORTER WORDT."
4260 PRINT:PRINT
4270 PRINT"INDIEN U WEINIG GEHEUGENRUIMTE IN DE":PRINT
4280 PRINT"KOMPUTER HEBT: ALLES VAN REGEL 2000 TOT":PRINT
4290 PRINT"5900 KAN GEMIST WORDEN ! ('PROEF' HEEFT":PRINT
4300 PRINT"HIERVAN NIETS NODIG).":PRINT
4310 PRINT"HET 'MENU' KAN DAN UITERAARD OOK WEG."
4320 GOSUB6000
4390 REM
4400 GOSUB100
4410 PRINT"DIT PROGRAMMA IS GEMAAKT VOOR EEN SCHERM"
4420 PRINT"VOLGENS BASICODE NORM: 40 TEKENS OP EEN":PRINT
4430 PRINT"REGEL, 24 REGELS OP HET SCHERM.":PRINT
4440 PRINT"INDIEN U MEER/MINDER TEKENS OF REGELS":PRINT
4450 PRINT"TOT UW BESCHIKKING HEBT KUNT/MOET U":PRINT
4460 PRINT"DE GETALLEN IN REGEL 1010 AANPASSEN:":PRINT
4480 PRINT"XM = AANTAL TEKENS PER REGEL,":PRINT
4490 PRINT"YM = AANTAL REGELS MIN 1."
4500 GOSUB6000
4510 GOTO1100
4900 REM
5000 REM*** STOPPEN ***
5010 GOSUB100
5020 PRINT"EINDE PROGRAMMA 'RADIOAKTIEF VERVAL'"
5030 END
5900 REM
6000 REM*** SUBROUTINE ***
6010 LETHO=XM-7:LETVE=YM+E
6020 GOSUB110
6030 PRINT"TOETS>";
6040 GOSUB210:REMHAAL TOETS
6050 RETURN
6900 REM
30000 REM-RADIOAKTIEF VERVAL-
30010 REMVERSIE 15 APRIL 1983
30020 REM
30030 REMSTAAT IN BASICODE-2
30040 REMGEMAAKT OP APPLE II
30050 REM
30060 REMHET PROGRAMMA OMVAT:
30070 REM(VANAF REGEL NUMMER)
30080 REMINITIALISATIE (1000)
30090 REMMENU          (1100)
30100 REMHOOFDPROGRAMMA(1500)
30110 REMUITLEG LEERL. (2000)
30120 REMUITLEG LERAAR (3000)
30130 REMUITLEG PROGR. (4000)
30140 REMSTOPPEN       (5000)
30150 REMSUBROUTINE    (6000)
30160 REM
30170 REME. IVENS,
30180 REMLERAAR NATUURKUNDE,
30190 REMST. LUDGERCOLLEGE,
30200 REMVONDELSTRAAT 5,
30210 REM7002 AN  DOETINCHEM.

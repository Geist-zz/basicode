
1000 A=1000:GOTO20:REM WELKOM OP KANT 2
1010 GOSUB100:PRINT"Even geduld aub..."
1020 GOSUB24500:REM SCHERM OPMETEN
1030 DD$=".TEKST":GOSUB24600
1040 END
24500 REM-SCHERM-OPMETEN
24510 HO=15:VE=15
24520 HO=HO+1:HH=HO:GOSUB110:GOSUB120:IFHH=HO THEN24520
24530 HO=0
24540 VE=VE+1:VV=VE:GOSUB110:GOSUB120:IFVV=VE THEN24540
24550 GOSUB100:RETURN
24600 REM-ZOEK-DATA-EN-ZET-OP-SCHERM
24610 RESTORE
24620 READSR$:IFSR$="." THEN24660
24630 IFSR$<>DD$ THEN24620
24640 READSR$:IFLEFT$(SR$,1)="." THEN24660
24650 GOSUB24700:GOTO24640
24660 RETURN
24700 REM-BASICODE-2-TEKST-VORMER
24710 IFSR$="" THENGOSUB24900:RETURN
24720 IFSR$<>"-" THEN24740
24730 FORII=4 TOHH:SR$=SR$+"-":NEXTII:VE=VE-1
24740 IFLEFT$(SR$,1)<>" " THEN24780
24750 HO=0:IFSR$="  " THENVE=VV:SR$=" "
24760 IFSR$=" " THENVE=VE+4:GOSUB110:RETURN
24770 VE=VE+2:GOSUB110:SR$=RIGHT$(SR$,LEN(SR$)-1)
24780 SR$=SR$+" ":LL=LEN(SR$):SP=1
24790 FORII=1 TOLL
24800 IFMID$(SR$,II,1)<>" " THEN24895
24810 SM$=MID$(SR$,SP,II-SP)
24820 LM=LEN(SM$):IFLM+HO<HH-1 THEN24840
24830 HO=0:VE=VE+2:GOSUB110
24840 GOSUB120:IFVE<VV-3 THEN24860
24850 GOSUB24900:GOSUB100:GOSUB120
24860 IFLM<HH THEN24890
24870 SM$=MID$(SR$,SP,HH-HO-2)
24880 II=SP+HH-HO-3
24890 PRINT" ";SM$;:GOSUB120:SP=II+1
24895 NEXTII:RETURN
24900 REM-VRAAG-INVOERTOETS
24910 VS=VE:HS=HO:VE=VV-1:HO=0
24920 GOSUB110
24930 PRINT"                    ";:FORI=0 TO200:NEXTI
24940 GOSUB200:IFIN$<>"" THEN24980
24950 GOSUB110
24960 PRINT"DRUK OP INVOER-TOETS";:FORI=0 TO200:NEXTI
24970 GOTO24920
24980 VE=VS:HO=HS:RETURN
25000 DATA".TEKST","Welkom!","-","Welkom bij de groeiende"
25010 DATA"club van Basicode-gebruikers!"," Op deze kant"
25020 DATA"van de cassette staan een aantal programma's"
25030 DATA"in Basicode-2 standaard. U kunt hiermee"
25040 DATA"oefenen en proberen of alles goed werkt,"
25050 DATA"maar U heeft ook meteen een aantal leuke en"
25060 DATA"nuttige programma's erbij."
25070 DATA" Al deze programma's voldoen aan het"
25080 DATA"Basicode-2 protocol zoals dat in hoofdstuk 3"
25090 DATA"van het Basicodeboek staat beschreven."
25100 DATA" Maar.... het Basicode-2 protocol houdt"
25110 DATA"rekening met een scherm van 24 regels met"
25120 DATA"ieder 40 karakters. Als het scherm van Uw"
25130 DATA"computer kleiner is, werken de meeste"
25140 DATA"programma's niet zonder ze aan te passen."
25150 DATA"Hoeveel werk dat aanpassen is, hangt af van"
25160 DATA"het programma en Uw schermafmetingen."
25170 DATA" Als het scherm van Uw computer groter is dan"
25180 DATA"24 bij 40, werken de programma's zonder ze"
25190 DATA"te wijzigen."," Er zijn echter ook"
25200 DATA"programma's die zelf opmeten hoe groot het"
25210 DATA"scherm is en zich daaraan aanpassen! (Dit"
25220 DATA"programma is daar een voorbeeld van!)","  "
25230 DATA"Zo, nu genoeg gepraat. We wensen U veel"
25240 DATA"plezier met Basicode-2 en de programma's op"
25250 DATA"dit bandje."," U wist toch dat Hobbyscoop elke"
25260 DATA"zondagavond een Basicodeprogramma uitzendt"
25270 DATA"via Hilversum 2 en 4?  (van 19.10 tot 19.45"
25280 DATA"uur)"," Luisteren en opnemen dus!"
25290 DATA"  ","We kunnen natuurlijk alleen programma's"
25300 DATA"blijven uitzenden als de luisteraars ze blijven"
25310 DATA"insturen!"," Dus.. als U wat leuks gemaakt"
25320 DATA"heeft, aarzel niet maar stuur het naar"
25330 DATA"Hobbyscoop zodat iedereen er plezier van heeft."
25340 DATA" Om het nog aantrekkelijker te maken wordt"
25350 DATA"iedere maand een kado-bon weggegeven voor het"
25360 DATA"beste (leukste, orgineelste) programma!"
25370 DATA"Het adres is:","-"," Hobbyscoop"
25380 DATA" Postbus 1200"," 1200 BE Hilversum","   ","-"
25390 DATA"."
30000 REM
30010 REM    WELKOM OP KANT 2
30020 REM
30030 REM    TEKST SUBROUTINE NAAR
30040 REM    NAAR EEN IDEE VAN 
30050 REM    RIEN VAN DONGEN
30060 REM    ZOETERMEER  


1000 A=1000:GOTO20:REM*KRETOLOGIE-GENERATOR*
1010 GOSUB100:REM***SCHERM OPMETEN***
1020 VE=0:FORI=0 TO80:HO=I:GOSUB110
1030 GOSUB120:IFHO<>I THENHP=I:I=80
1040 NEXTI
1050 HO=0:FORI=0 TO40:VE=I:GOSUB110
1060 GOSUB120:IFVE<>I THENVP=I:I=40
1070 NEXTI:CT=2:CN=0
1080 X1=2:X2=HP-2*X1:X3=X1+X2
1090 Y1=1:Y2=VP-2-2*Y1:Y3=Y1+Y2
1100 :
1110 REM***OPENINGSBEELD***
1120 VE=Y1
1130 FORHO=X1-1 TO(X3-1):GOSUB110:PRINT"*":NEXTHO
1140 FORVE=Y1 TOY3:GOSUB110:PRINT"*":NEXTVE
1150 FORHO=X3 TOX1 STEP-1
1155 GOSUB110:PRINT"*":NEXTHO
1160 FORVE=(Y3+1) TOY1 STEP-1
1165 GOSUB110:PRINT"*":NEXTVE
1170 X1=X1+1:X2=X2-2:X3=X1+X2
1180 Y1=Y1+1:Y2=Y2-2:Y3=Y1+Y2
1190 IFY1>=VP/2-5 THEN1300
1200 GOTO1120
1300 HO=HP/2-10:VE=VP/2-3:GOSUB110
1310 PRINT"KRETOLOGIE-GENERATOR"
1320 HO=HO+2:VE=VE+2:GOSUB110
1330 PRINT"by SoftTextHouse"
1340 HO=HO+1:VE=VE+2:GOSUB110
1350 PRINT"for HOBBYSCOPE"
1360 HO=HO+4:VE=VE+2:GOSUB110
1370 PRINT"@1987"
1500 :
1510 REM***WOORDEN TELLEN EN IN ARRAYS**
1520 RESTORE:T1=0:T2=0:T3=0
1530 READT$:IFT$="*" THEN1550
1540 T1=T1+1:GOTO1530
1550 READT$:IFT$="*" THEN1570
1560 T2=T2+1:GOTO1550
1570 READT$:IFT$="*" THEN1590
1580 T3=T3+1:GOTO1570
1590 DIMT1$(T1),T2$(T2),T3$(T3)
1600 :
1610 RESTORE
1620 FORT=0 TOT1:READT$:T1$(T)=T$:NEXTT
1630 FORT=0 TOT2:READT$:T2$(T)=T$:NEXTT
1640 FORT=0 TOT3:READT$:T3$(T)=T$:NEXTT
1650 FORT=1 TO5:NEXTT
1660 DIMD$(16):FORN=0 TO16:READD$
1670 D$(N)=D$:NEXTN
1680 DIME$(12):FORN=0 TO12:READE$
1690 E$(N)=E$:NEXTN
1700 :
1710 REM***KEUZEBEELD***
1720 GOSUB100:HO=HP/2-10:VE=VP/2-6:GOSUB110
1730 PRINT"KRETOLOGIE-GENERATOR":VE=VE+1:GOSUB110
1740 PRINT"--------------------":VE=VE+2:GOSUB110
1750 PRINT" 1  uitleg          ":VE=VE+1:GOSUB110
1760 PRINT" 2  generator       ":VE=VE+1:GOSUB110
1770 PRINT" 3  correspondentie ":VE=VE+1:GOSUB110
1780 PRINT" 4  toespraak       ":VE=VE+2:GOSUB110
1785 PRINT" 5  STOP            ":VE=VE+2:GOSUB110
1790 PRINT"MAAK UW KEUZE !     "
1800 GOSUB250:GOSUB210
1810 T=VAL(IN$):IF(T<1) OR(T>5) THEN1700
1820 ONT GOTO10000,4000,6000,8000,2000
2000 :
2010 REM***EINDBEELD***
2020 GOSUB100:HO=HP/2-10:VE=VP/2-5
2030 GOSUB110:GOSUB250
2040 PRINT"   *                 ":VE=VE+1:GOSUB110
2050 PRINT"  ***                ":VE=VE+1:GOSUB110
2060 PRINT" *****               ":VE=VE+1:GOSUB110
2080 PRINT"  *T*                ":VE=VE+1:GOSUB110
2090 PRINT"  *** SoftTextHouse  ":VE=VE+1:GOSUB110
2100 PRINT"                     ":VE=VE+1:GOSUB110
2110 PRINT"byebye and good luck!":VE=VE+1:GOSUB250
2120 VE=VE+2:GOSUB110:END
4000 :
4010 REM***GENERATORPROGRAMMA***
4020 KR=1
4030 GOSUB100:HO=HP/2-10:VE=VP/2-8:GOSUB110
4040 PRINT"KRETENGENERATOR":VE=VE+1
4050 GOSUB110:PRINT"==============="
4060 VE=VE+1:GOSUB110
4070 PRINT"Iedere toetsaanslag":VE=VE+1
4080 GOSUB110:PRINT"genereert een kreet.":VE=VE+1
4090 GOSUB110:PRINT"MENU: TOETS M."
4094 HO=HO/2-20:IFHO<0 THENHO=0
4097 VE=VE+2:GOSUB110
4100 SR=KR:GOSUB310:PRINTSR$;" ";:GOSUB20000
4110 VE=VE+2:GOSUB110:KR=KR+1
4120 GOSUB210:IF(IN$="M") OR(IN$="m") THEN1700
4130 IFIN$="" THEN4120
4140 IFVE>VP/2+7 THEN4030
4150 GOTO4100
6000 :
6010 REM***CORRESPONDENTIE***
6020 GOSUB100:HO=0:VE=0
6030 GOSUB110:PRINTD$(0)
6040 VE=VE+2:GOSUB110
6050 F$=D$(1)+D$(2):GOSUB20200
6060 GOSUB21000:F1$=A$:VE=VE+1
6070 GOSUB21000:F2$=A$:GOSUB110
6080 GOSUB21000:F3$=A$
6090 F$=D$(3)+F1$+D$(4)+F2$+D$(5)+F3$+D$(6):GOSUB20200
6100 GOSUB21000:F1$=A$:VE=VE+1
6110 GOSUB21000:F2$=A$:GOSUB110
6115 GOSUB22000
6120 F$=D$(7)+F3$+D$(8)+F1$+D$(9)+F2$+D$(10):GOSUB20200
6130 VE=VE+1:GOSUB110:GOSUB22000
6140 F$=D$(11)+B$+C$+D$(12):GOSUB20200
6150 VE=VE+2:GOSUB110:GOSUB22000
6160 F$=D$(13)+D$(14):GOSUB20200
6170 VE=VE+2:GOSUB110:GOSUB22000
6180 PRINTD$(15)
6190 VE=VE+1:GOSUB110:GOSUB22000
6200 PRINTD$(16)
6205 VE=VE+2:GOSUB110:GOSUB22000
6210 PRINT"MENU: DRUK EEN TOETS"
6220 GOSUB200:IFIN$="" THEN6220
6230 GOTO1700
8000 :
8010 REM***TOESPRAAK***
8020 GOSUB100:HO=0:VE=0
8030 GOSUB110:PRINTE$(0)
8040 VE=VE+2:GOSUB110
8050 F$=E$(1)+E$(2):GOSUB20200
8060 GOSUB21000:F1$=A$:VE=VE+1
8070 GOSUB21000:F2$=A$:GOSUB110
8080 GOSUB21000:F3$=A$
8090 F$=E$(3)+F1$+E$(4)+F2$+E$(5)+F3$+E$(6):GOSUB20200
8100 GOSUB21000:F1$=A$:VE=VE+1
8110 GOSUB21000:F2$=A$:GOSUB110
8115 GOSUB22000
8120 F$=E$(7)+F3$+E$(8)+F1$+E$(9)+F2$+E$(10):GOSUB20200
8130 VE=VE+1:GOSUB110:GOSUB22000
8140 F$=E$(11):GOSUB20200
8150 VE=VE+1:GOSUB110:GOSUB22000
8160 F$=E$(12):GOSUB20200
8170 VE=VE+3:GOSUB110:GOSUB22000
8210 PRINT"MENU: DRUK EEN TOETS"
8220 GOSUB200:IFIN$="" THEN8220
8230 GOTO1700
10000 :
10010 REM***UITLEG***
10020 GOSUB100:HO=HP/2-11:VE=VP/2-7:GOSUB110
10030 PRINT"--------UITLEG--------";:VE=VE+1:GOSUB110
10040 PRINT" KRETOLOGIE-GENERATOR ";:VE=VE+1:GOSUB110
10050 PRINT"----------------------";:VE=VE+1:GOSUB110
10060 PRINT"Ook U heeft vast wel  ";:VE=VE+1:GOSUB110
10070 PRINT"eens het gevoel, dat U";:VE=VE+1:GOSUB110
10080 PRINT"iemand hoort spreken  ";:VE=VE+1:GOSUB110
10090 PRINT"met imponerend gebruik";:VE=VE+1:GOSUB110
10100 PRINT"van woorden, maar dat ";:VE=VE+1:GOSUB110
10110 PRINT"U er geen snars van   ";:VE=VE+1:GOSUB110
10120 PRINT"begrijpt.":VE=VE+1:GOSUB110
10130 PRINT"Of U ontvangt een     ";:VE=VE+1:GOSUB110
10140 PRINT"brief waar geen zinnig";:VE=VE+1:GOSUB110
10150 PRINT"touw aan vast is te   ";:VE=VE+1:GOSUB110
10160 PRINT"knopen."
10170 FORT=1 TO5000:NEXTT
10180 GOSUB100:HO=HP/2-11:VE=VP/2-4:GOSUB110
10190 PRINT"Tien tegen een dat de ";:VE=VE+1:GOSUB110
10200 PRINT"spreker de beschikking";:VE=VE+1:GOSUB110
10210 PRINT"heeft over dit        ";:VE=VE+1:GOSUB110
10220 PRINT"BASICODE-2 programma  ";:VE=VE+1:GOSUB110
10230 PRINT"om zijn toespraken,   ";:VE=VE+1:GOSUB110
10240 PRINT"correspondentie en    ";:VE=VE+1:GOSUB110
10250 PRINT"dergelijke voor te    ";:VE=VE+1:GOSUB110
10260 PRINT"bereiden."
10270 FORT=1 TO5000:NEXTT
10280 GOSUB100:HO=HP/2-11:VE=VP/2-5:GOSUB110
10290 PRINT"Dank zij HOBBYSCOOP   ";:VE=VE+1:GOSUB110
10300 PRINT"kunt U nu ook zelf tot";:VE=VE+1:GOSUB110
10310 PRINT"de elitaire groep     ";:VE=VE+1:GOSUB110
10320 PRINT"Nederlanders behoren  ";:VE=VE+1:GOSUB110
10330 PRINT"die met indrukwekkende";:VE=VE+1:GOSUB110
10340 PRINT"termen in elke        ";:VE=VE+1:GOSUB110
10350 PRINT"situatie een air van  ";:VE=VE+1:GOSUB110
10360 PRINT"diepgaande kennis en  ";:VE=VE+1:GOSUB110
10370 PRINT"autoriteit opbouwen.  ";
10380 FORT=1 TO5000:NEXTT
10390 GOSUB100:HO=HP/2-11:VE=VP/2-5:GOSUB110
10400 PRINT"Wanneer U kiest voor: ";:VE=VE+2:GOSUB110
10410 PRINT"> 3 correspondentie of";:VE=VE+1:GOSUB110
10420 PRINT"> 4 toespraak         ";:VE=VE+2:GOSUB110
10430 PRINT"heeft U de beschikking";:VE=VE+1:GOSUB110
10440 PRINT"over een bijna        ";:VE=VE+1:GOSUB110
10450 PRINT"eindeloze reeks van   ";:VE=VE+1:GOSUB110
10460 PRINT"geschikte teksten.    ";
10470 FORT=1 TO5000:NEXTT
10480 GOSUB100:HO=HP/2-11:VE=VP/2-5:GOSUB110
10490 PRINT"Maar heeft U behoefte ";:VE=VE+1:GOSUB110
10500 PRINT"aan nog enige eigen   ";:VE=VE+1:GOSUB110
10510 PRINT"inbreng, kies dan:    ";:VE=VE+2:GOSUB110
10520 PRINT"> 2 generator         ";:VE=VE+2:GOSUB110
10530 PRINT"en voeg de aangeboden ";:VE=VE+1:GOSUB110
10540 PRINT"kreten in passende    ";:VE=VE+1:GOSUB110
10550 PRINT"zinnen bij elkaar.    ";
10560 FORT=1 TO5000:NEXTT
10570 GOTO1700
20000 :
20005 REM***GENERATORROUTINE***
20010 GOSUB260:SR=INT(RV*100)
20020 IFSR>T1-1 THEN20010
20030 A$=T1$(SR)
20040 GOSUB260:SR=INT(RV*100)
20050 IFSR>T2-1 THEN20040
20060 B$=" "+T2$(SR)
20070 GOSUB260:SR=INT(RV*100)
20080 IFSR>T3-1 THEN20070
20090 C$=T3$(SR)
20100 A$=A$+B$+C$
20110 IFLEN(A$)>HP-3 THEN20000
20120 PRINTA$
20130 RETURN
20200 :
20210 REM***PRINTROUTINE***
20220 R=HP-2
20230 R$=MID$(F$,R,1)
20240 IFR$<>CHR$(32) THENR=R-1
20250 IFR$<>CHR$(32) THEN20230
20260 PRINTLEFT$(F$,R)
20265 IFLEN(F$)=R THENF$="":GOTO20280
20270 F$=RIGHT$(F$,(LEN(F$)-R))
20280 IFF$="" THENRETURN
20290 VE=VE+1:GOSUB110:GOSUB22000
20300 GOTO20200
21000 :
21005 REM***GENERATORROUTINE TEKST***
21010 GOSUB260:SR=INT(RV*100)
21020 IFSR>T1-1 THEN21010
21030 A$=T1$(SR)
21040 GOSUB260:SR=INT(RV*100)
21050 IFSR>T2-1 THEN21040
21060 B$=" "+T2$(SR)
21070 GOSUB260:SR=INT(RV*100)
21080 IFSR>T3-1 THEN21070
21090 C$=T3$(SR)
21100 A$=A$+B$+C$
21110 IFLEN(A$)>HP-3 THEN21000
21130 RETURN
22000 :
22010 REM***BEELDFORMATTER***
22020 IFVE<(VP-2) THENRETURN
22030 GOSUB110
22040 PRINT"VERVOLG: DRUK TOETS";
22050 GOSUB200
22060 IFIN$<>"" THEN22080
22070 GOTO22050
22080 GOSUB100:VE=0:GOSUB110
22090 PRINT"(VERVOLG)"
22100 VE=VE+2:GOSUB110
22110 RETURN
25000 :
25010 REM***DATA WOORD 1***
25020 DATA"leidende","nivellerende"
25030 DATA"permanente","systematische"
25040 DATA"progressieve","functionele"
25050 DATA"georienteerde","synchrone"
25060 DATA"gekwalificeerde","ambivalente"
25070 DATA"geintegreerde","modale"
25080 DATA"gedifferentieerde","unieke"
25090 DATA"lineaire","gesubstitueerde"
25100 DATA"irreversibele","rationele"
25110 DATA"interdependente","nominale"
25120 DATA"subversieve","morbide"
25130 DATA"forfaitaire","complexe"
25140 DATA"quasi","reele","aperte"
25150 DATA"pertinente","stimulerende"
25160 DATA"*"
26000 :
26010 REM***DATA WOORD 2***
26020 DATA"concentratie","organisatie"
26030 DATA"identificatie","coalitie"
26040 DATA"vierde generatie","groei"
26050 DATA"fluctuatie","overgangs"
26060 DATA"gedrags","interpretatie"
26070 DATA"driefasen","tweesporen"
26080 DATA"ontwikkelings","nivellatie"
26090 DATA"deactiverings","beleids"
26100 DATA"*"
27000 :
27010 REM***DATA WOORD 3***
27020 DATA"structuur","flexibiliteit"
27030 DATA"integratie","tendens"
27040 DATA"programmering","conceptie"
27050 DATA"termijn","potentie"
27060 DATA"problematiek","contingentering"
27070 DATA"conjunctuur","methodiek"
27080 DATA"informatie","trend"
27090 DATA"coordinaat","variabele"
27100 DATA"curve","differentiatie"
27110 DATA"analyse","research"
27120 DATA"constellatie","penetratie"
27130 DATA"degradatie","permutatie"
27140 DATA"*"
27980 :
27990 REM***DATA TEKST CORRESPONDENTIE**
28000 DATA"Mijne Heren, "
28010 DATA"Hierbij bericht "
28020 DATA"ik U als volgt. "
28030 DATA"Door een "," en een "
28040 DATA" komt het bij mij tot een ",". "
28050 DATA"Deze "," impliceert een "
28060 DATA" met als gevolg een ",". "
28070 DATA"U begrijpt natuurlijk wat zo'n "
28080 DATA" betekent. ","Ik hoop U hiermee naar genoegen "
28090 DATA"te hebben geinformeerd. "
28100 DATA"Hoogachtend, ","  (naam)  "
28980 :
28990 REM***DATA TEKST TOESPRAAK***
29000 DATA"Dames en Heren, "
29010 DATA"Ik zou graag het "
29020 DATA"volgende willen zeggen. "
29030 DATA"Op grond van een "," gekoppeld aan een "
29040 DATA" hebben wij besloten tot een "
29050 DATA" zonder beperkingen. "
29060 DATA"Op basis van deze "," mogen we een "
29070 DATA" verwachten die resulteert in een ",". "
29080 DATA"En daar hebben we wellicht allemaal wat aan. "
29090 DATA"Ik dank U voor Uw welwillende aandacht. "
30000 REM***TOELICHTING****************
30005 REM
30010 REM DE KRETOLOGIE-GENERATOR IS ZO
30020 REM GEMAAKT, DAT HIJ ZICH AAN
30030 REM ELKE SCHERMGROOTTE AANPAST.
30040 REM ELKE KREET BESTAAT UIT DRIE
30050 REM DEELWOORDEN DIE RESP. TE
30060 REM VINDEN ZIJN IN DE DATAREGELS
30070 REM 25000, 26000, 27000 EN
30080 REM VOLGENDE. HET LAATSTE WOORD
30090 REM MOET ALTIJD EEN * ZIJN, OMDAT
30100 REM HET PROGRAMMA DE WOORDEN TELT
30110 REM EN IN EEN ARRAY PLAATST.
30120 REM
30130 REM***NOG EEN OPMERKING**********
30140 REM HET PROGRAMMA LOOPT DES TE
30150 REM TRAGER NAARMATE HET SCHERM
30160 REM KLEINER IS. DAT KOMT DOORDAT
30170 REM HET STEEDS MOEILIJKER WORDT
30180 REM OM KRETEN SAMEN TE STELLEN
30190 REM
31000 REM******************************
31010 REM*                            *
31020 REM*   *                        *
31030 REM*  ***                       *
31040 REM* *****                      *
31050 REM*  *T*  SoftTextHouse        *
31060 REM*  ***                       *
31070 REM* computerprogramming        *
31080 REM*                            *
31090 REM* is een van de Houses van   *
31100 REM*                            *
31110 REM* FJW van Let                *
31120 REM* Pelikaanweg 5              *
31130 REM* 3762 VA Soest              *
31140 REM*                            *
31150 REM*                            *
31160 REM**** KRETOLOGIE-GENERATOR ****
31170 REM*     is gemaakt op een      *
31180 REM*         SHARP 721          *
31190 REM*                            *
31200 REM* COPYRIGHT NOS HILVERSUM 87 *
31210 REM******************************

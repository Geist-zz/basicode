
1000 A=1000:GOTO20:REM * moleculaire genetica
1010 GOSUB100:REM scherm schoon
1020 REM * RNA en eiwit maken/mutaties
1030 DIMA$(250):DIMB$(250):DIMP$(90)
1040 PRINT"Moleculaire Genetica":PRINT:PRINT
1050 PRINT"U kunt straks een reeks van"
1060 PRINT"max 240 basen invoeren."
1070 PRINT"Deze basen vormen een DNA-streng."
1080 PRINT"U kunt het invoeren beeindigen"
1090 PRINT"door een * in te toetsen."
1100 PRINT"Tussentijds corrigeren is"
1110 PRINT"niet mogelijk.  Na het"
1120 PRINT"invoeren bestaat echter de"
1130 PRINT"mogelijkheid om mutaties"
1140 PRINT"uit te voeren  (invoegen,"
1150 PRINT"weghalen, vervangen)."
1160 PRINT
1170 PRINT"De definitieve DNA-streng kan worden"
1180 PRINT"aangevuld tot een dubbelstrengs"
1190 PRINT"DNA-ladder. Vervanging door de"
1200 PRINT"complementaire DNA-streng is"
1210 PRINT"mogelijk.":PRINT:PRINT
1220 PRINT"                   druk toets"
1230 GOSUB210:REM wacht op toets
1240 GOSUB100:REM scherm schoon
1250 PRINT
1260 PRINT"Vertalen naar eiwit is eveneens"
1270 PRINT"mogelijk (via RNA)."
1280 PRINT
1290 PRINT"Om de eiwitketen van boven naar"
1300 PRINT"beneden te kunnen lezen"
1310 PRINT"(amino- naar carboxylterminus)"
1320 PRINT"is het vereist dat de DNA-keten"
1330 PRINT"bij de 3' plaats begint."
1340 PRINT
1350 PRINT"U moet dus het DNA van 3' naar 5'"
1360 PRINT"intypen, zodat het RNA van 5'"
1370 PRINT"naar 3' zal lopen."
1380 PRINT"Een korte DNA-streng kan later"
1390 PRINT"worden aangevuld tot max 240 basen."
1400 PRINT
1410 PRINT"Het programma accepteert alleen"
1420 PRINT"HOOFDLETTERS. Controleer of de"
1430 PRINT"Caps Lock toets in werking is."
1440 PRINT:PRINT:PRINT
1450 PRINT"                  druk toets"
1460 GOSUB210:REM  wacht op toets
1470 GOSUB100:REM  scherm schoon
1480 PRINT"U kunt nu basen invoeren (A,T,C en G)."
1490 PRINT" * = klaar"
1500 I=0
1510 REM begin invoerlus
1520 I=I+1
1530 IFI>240 THENT=240:GOTO1660
1540 GOSUB200:REM pak toets
1550 ZZ$=IN$
1560 IF(ZZ$="*") AND(I>1) THENT=I-1:GOTO1660
1570 IF(ZZ$="A") THEN1620
1580 IF(ZZ$="T") THEN1620
1590 IF(ZZ$="C") THEN1620
1600 IF(ZZ$="G") THEN1620
1610 GOTO1540
1620 A$(I)=ZZ$
1630 PRINTI;:PRINTTAB(6);A$(I)
1640 GOTO1520
1650 REM einde invoerlus
1660 PRINT
1670 PRINT"Reeks voltooid"
1680 PRINT:PRINT:PRINT
1690 PRINT"                       druk toets"
1700 GOSUB210:REM wacht op toets
1710 GOSUB100:REM scherm schoon
1720 PRINT"Nu zijn er de volgende mogelijkheden:"
1730 PRINT
1740 PRINT" (C) Complementaire DNA streng maken"
1750 PRINT"     (deze kan evt. de oorspronkelijke"
1760 PRINT"        vervangen)"
1770 PRINT
1780 PRINT" (R) RNA en eiwit maken"
1790 PRINT
1800 PRINT" (M) Mutatie (vervangen, invoegen,"
1810 PRINT"                  weghalen)"
1820 PRINT
1830 PRINT" (S) De huidige streng laten zien"
1840 PRINT
1850 PRINT" (V) De huidige streng verlengen"
1860 PRINT
1870 PRINT" (Q) Stoppen"
1880 PRINT:PRINT
1890 PRINT"                      druk toets"
1900 GOSUB210:REM wacht op toets
1910 ZZ$=IN$
1920 IFZZ$="C" THEN1990
1930 IFZZ$="R" THEN2310
1940 IFZZ$="M" THEN5120
1950 IFZZ$="S" THEN6090
1960 IFZZ$="V" THEN6270
1970 IFZZ$="Q" THENGOSUB100:END
1980 GOTO1900
1990 GOSUB100:REM scherm schoon
2000 PRINT"druk voor start en stop een toets in"
2010 PRINT:PRINT
2020 GOSUB210:REM wacht op toets
2030 I=0
2040 I=I+1
2050 GOSUB200:ZZ$=IN$:IFZZ$="" THEN2070
2060 GOSUB210:ZZ$=IN$:REM haal toets
2070 IFI>=T+1 THEN2140
2080 IFA$(I)="A" THENB$(I)="T"
2090 IFA$(I)="T" THENB$(I)="A"
2100 IFA$(I)="C" THENB$(I)="G"
2110 IFA$(I)="G" THENB$(I)="C"
2120 PRINTI;:PRINTTAB(6);A$(I);:PRINTTAB(12);B$(I)
2130 GOTO2040
2140 PRINT
2150 PRINT"Einde van de streng"
2160 PRINT:PRINT:PRINT
2170 PRINT"Wilt U de oorspronkelijke streng"
2180 PRINT"vervangen door de complementaire? (J/N)";
2190 GOSUB210:REM wacht op toets
2200 ZZ$=IN$
2210 IFZZ$<>"J" THEN1710
2220 FORI=1 TOT
2230 A$(I)=B$(T-I+1)
2240 NEXTI
2250 GOSUB100:REM scherm schoon
2260 PRINT"Vervangen door de complementaire streng"
2270 PRINT:PRINT:PRINT
2280 PRINT"                       druk toets"
2290 GOSUB210:ZZ$=IN$:REM wacht op toets
2300 GOTO1710
2310 GOSUB100:REM scherm schoon
2320 PRINT"RNA en eiwit maken"
2330 PRINT"(transcriptie en translatie)"
2340 PRINT:PRINT
2350 PRINT"Druk voor start en stop een toets in"
2360 PRINT:PRINT
2370 GOSUB210:ZZ$=IN$:REM wacht op toets
2380 FORI=1 TOT-2 STEP3
2390 GOSUB200:ZZ$=IN$:IFZZ$="" THEN2410
2400 GOSUB210:ZZ$=IN$:REM wacht op toets
2410 H=(I+2)/3
2420 IFA$(I)<>"T" THEN2450
2430 B$(I)="A"
2440 GOTO2550:REM * BLOK T
2450 IFA$(I)<>"C" THEN2480
2460 B$(I)="G"
2470 GOTO2690:REM * BLOKC
2480 IFA$(I)<>"A" THEN2510
2490 B$(I)="U"
2500 GOTO2830:REM * BLOK A
2510 REM * A$(I)="G"
2520 B$(I)="C"
2530 GOTO2970:REM * BLOK G
2540 :
2550 REM * BLOK T .......
2560 IFA$(I+1)<>"T" THEN2590
2570 B$(I+1)="A"
2580 GOTO3110:REM * BLOK T.T
2590 IFA$(I+1)<>"C" THEN2620
2600 B$(I+1)="G"
2610 GOTO3250:REM * BLOK T.C
2620 IFA$(I+1)<>"A" THEN2650
2630 B$(I+1)="U"
2640 GOTO3390:REM * BLOK T.A
2650 REM * A$(I+1)="G"
2660 B$(I+1)="C"
2670 GOTO3530:REM *BLOK T.G
2680 :
2690 REM * BLOK C .......
2700 IFA$(I+1)<>"T" THEN2730
2710 B$(I+1)="A"
2720 GOTO3610:REM * BLOK C.T
2730 IFA$(I+1)<>"C" THEN2760
2740 B$(I+1)="G"
2750 GOTO3750:REM * BLOK C.C
2760 IFA$(I+1)<>"A" THEN2790
2770 B$(I+1)="U"
2780 GOTO3830:REM * BLOK C.A
2790 REM * A$(I+1)="G"
2800 B$(I+1)="C"
2810 GOTO3910:REM * BLOK C.G
2820 :
2830 REM * BLOK A .......
2840 IFA$(I+1)<>"T" THEN2870
2850 B$(I+1)="A"
2860 GOTO3990:REM * BLOK A.T
2870 IFA$(I+1)<>"C" THEN2900
2880 B$(I+1)="G"
2890 GOTO4130:REM * BLOK A.C
2900 IFA$(I+1)<>"A" THEN2930
2910 B$(I+1)="U"
2920 GOTO4270:REM * BLOK A.A
2930 REM * A$(I+1)="G"
2940 B$(I+1)="C"
2950 GOTO4410:REM * BLOK A.G
2960 :
2970 REM * BLOK G .......
2980 IFA$(I+1)<>"T" THEN3010
2990 B$(I+1)="A"
3000 GOTO4490:REM * BLOK G.T
3010 IFA$(I+1)<>"C" THEN3040
3020 B$(I+1)="G"
3030 GOTO4630:REM * BLOK G.C
3040 IFA$(I+1)<>"A" THEN3070
3050 B$(I+1)="U"
3060 GOTO4710:REM *BLOK G.A
3070 REM * A$(I+1)="G"
3080 B$(I+1)="C"
3090 GOTO4790:REM * BLOK G.G
3100 :
3110 REM T.T .......
3120 IFA$(I+2)<>"T" THEN3150
3130 B$(I+2)="A":P$(H)="Lys  K    Lysine"
3140 GOTO3230
3150 IFA$(I+2)<>"C" THEN3180
3160 B$(I+2)="G":P$(H)="Lys  K    Lysine"
3170 GOTO3230
3180 IFA$(I+2)<>"A" THEN3210
3190 B$(I+2)="U":P$(H)="Asn  N    Asparagine"
3200 GOTO3230
3210 IFA$(I+2)<>"G" THEN3230
3220 B$(I+2)="C":P$(H)="Asn  N    Asparagine"
3230 GOTO4850
3240 :
3250 REM T.C .......
3260 IFA$(I+2)<>"T" THEN3290
3270 B$(I+2)="A":P$(H)="Arg  R    Arginine"
3280 GOTO3370
3290 IFA$(I+2)<>"C" THEN3320
3300 B$(I+2)="G":P$(H)="Arg  R    Arginine"
3310 GOTO3370
3320 IFA$(I+2)<>"A" THEN3350
3330 B$(I+2)="U":P$(H)="Ser  S    Serine"
3340 GOTO3370
3350 IFA$(I+2)<>"G" THEN3370
3360 B$(I+2)="C":P$(H)="Ser  S    Serine"
3370 GOTO4850
3380 :
3390 REM T.A .......
3400 IFA$(I+2)<>"T" THEN3430
3410 B$(I+2)="A":P$(H)="Ile  I    Isoleucine"
3420 GOTO3510
3430 IFA$(I+2)<>"C" THEN3460
3440 B$(I+2)="G":P$(H)="Met  M    Methionine"
3450 GOTO3510
3460 IFA$(I+2)<>"A" THEN3490
3470 B$(I+2)="U":P$(H)="Ile  I    Isoleucine"
3480 GOTO3510
3490 IFA$(I+2)<>"G" THEN3510
3500 B$(I+2)="C":P$(H)="Ile  I    Isoleucine"
3510 GOTO4850
3520 :
3530 REM T.G .......
3540 IFA$(I+2)="T" THENB$(I+2)="A"
3550 IFA$(I+2)="C" THENB$(I+2)="G"
3560 IFA$(I+2)="A" THENB$(I+2)="U"
3570 IFA$(I+2)="G" THENB$(I+2)="C"
3580 P$(H)="Thr  T    Threonine"
3590 GOTO4850
3600 :
3610 REM C.T .......
3620 IFA$(I+2)<>"T" THEN3650
3630 B$(I+2)="A":P$(H)="Glu  E    Glutamaat"
3640 GOTO3730
3650 IFA$(I+2)<>"C" THEN3680
3660 B$(I+2)="G":P$(H)="Glu  E    Glutamaat"
3670 GOTO3730
3680 IFA$(I+2)<>"A" THEN3710
3690 B$(I+2)="U":P$(H)="Asp  D    Aspartaat"
3700 GOTO3730
3710 IFA$(I+2)<>"G" THEN3730
3720 B$(I+2)="C":P$(H)="Asp  D    Aspartaat"
3730 GOTO4850
3740 :
3750 REM C.C .......
3760 IFA$(I+2)="T" THENB$(I+2)="A"
3770 IFA$(I+2)="C" THENB$(I+2)="G"
3780 IFA$(I+2)="A" THENB$(I+2)="U"
3790 IFA$(I+2)="G" THENB$(I+2)="C"
3800 P$(H)="Gly  G    Glycine"
3810 GOTO4850
3820 :
3830 REM C.A .......
3840 IFA$(I+2)="T" THENB$(I+2)="A"
3850 IFA$(I+2)="C" THENB$(I+2)="G"
3860 IFA$(I+2)="A" THENB$(I+2)="U"
3870 IFA$(I+2)="G" THENB$(I+2)="C"
3880 P$(H)="Val  V    Valine"
3890 GOTO4850
3900 :
3910 REM C.G.......
3920 IFA$(I+2)="T" THENB$(I+2)="A"
3930 IFA$(I+2)="C" THENB$(I+2)="G"
3940 IFA$(I+2)="A" THENB$(I+2)="U"
3950 IFA$(I+2)="G" THENB$(I+2)="C"
3960 P$(H)="Ala  A    Alanine"
3970 GOTO4850
3980 :
3990 REM A.T .......
4000 IFA$(I+2)<>"T" THEN4030
4010 B$(I+2)="A":P$(H)="STOP"
4020 GOTO4110
4030 IFA$(I+2)<>"C" THEN4060
4040 B$(I+2)="G":P$(H)="STOP"
4050 GOTO4110
4060 IFA$(I+2)<>"A" THEN4090
4070 B$(I+2)="U":P$(H)="Tyr  Y    Tyrosine"
4080 GOTO4110
4090 IFA$(I+2)<>"G" THEN4110
4100 B$(I+2)="C":P$(H)="Tyr  Y    Tyrosine"
4110 GOTO4850
4120 :
4130 REM A.C .......
4140 IFA$(I+2)<>"T" THEN4170
4150 B$(I+2)="A":P$(H)="STOP"
4160 GOTO4250
4170 IFA$(I+2)<>"C" THEN4200
4180 B$(I+2)="G":P$(H)="Trp  W    Tryptofan"
4190 GOTO4250
4200 IFA$(I+2)<>"A" THEN4230
4210 B$(I+2)="U":P$(H)="Cys  C    Cysteine"
4220 GOTO4250
4230 IFA$(I+2)<>"G" THEN4250
4240 B$(I+2)="C":P$(H)="Cys  C    Cysteine"
4250 GOTO4850
4260 :
4270 REM A.A ......
4280 IFA$(I+2)<>"T" THEN4310
4290 B$(I+2)="A":P$(H)="Leu  L    Leucine"
4300 GOTO4390
4310 IFA$(I+2)<>"C" THEN4340
4320 B$(I+2)="G":P$(H)="Leu  L    Leucine"
4330 GOTO4390
4340 IFA$(I+2)<>"A" THEN4370
4350 B$(I+2)="U":P$(H)="Phe  F    Fenylalanine"
4360 GOTO4390
4370 IFA$(I+2)<>"G" THEN4390
4380 B$(I+2)="C":P$(H)="Phe  F    Fenylalanine"
4390 GOTO4850
4400 :
4410 REM A.G .......
4420 IFA$(I+2)="T" THENB$(I+2)="A"
4430 IFA$(I+2)="C" THENB$(I+2)="G"
4440 IFA$(I+2)="A" THENB$(I+2)="U"
4450 IFA$(I+2)="G" THENB$(I+2)="C"
4460 P$(H)="Ser  S    Serine"
4470 GOTO4850
4480 :
4490 REM G.T .......
4500 IFA$(I+2)<>"T" THEN4530
4510 B$(I+2)="A":P$(H)="Gln  Q    Glutamine"
4520 GOTO4610
4530 IFA$(I+2)<>"C" THEN4560
4540 B$(I+2)="G":P$(H)="Gln  Q    Glutamine"
4550 GOTO4610
4560 IFA$(I+2)<>"A" THEN4590
4570 B$(I+2)="U":P$(H)="His  H    Histidine"
4580 GOTO4610
4590 IFA$(I+2)<>"G" THEN4610
4600 B$(I+2)="C":P$(H)="His  H    Histidine"
4610 GOTO4850
4620 :
4630 REM G.C .......
4640 IFA$(I+2)="T" THENB$(I+2)="A"
4650 IFA$(I+2)="C" THENB$(I+2)="G"
4660 IFA$(I+2)="A" THENB$(I+2)="U"
4670 IFA$(I+2)="G" THENB$(I+2)="C"
4680 P$(H)="Arg  R    Arginine"
4690 GOTO4850
4700 :
4710 REM G.A .......
4720 IFA$(I+2)="T" THENB$(I+2)="A"
4730 IFA$(I+2)="C" THENB$(I+2)="G"
4740 IFA$(I+2)="A" THENB$(I+2)="U"
4750 IFA$(I+2)="G" THENB$(I+2)="C"
4760 P$(H)="Leu  L    Leucine"
4770 GOTO4850
4780 :
4790 REM G.G .......
4800 IFA$(I+2)="T" THENB$(I+2)="A"
4810 IFA$(I+2)="C" THENB$(I+2)="G"
4820 IFA$(I+2)="A" THENB$(I+2)="U"
4830 IFA$(I+2)="G" THENB$(I+2)="C"
4840 P$(H)="Pro  P    Proline"
4850 PRINTI;:PRINTTAB(6);A$(I);
4860 PRINTTAB(10);B$(I)
4870 PRINTI+1;:PRINTTAB(6);A$(I+1);
4880 PRINTTAB(10);B$(I+1)
4890 PRINTI+2;:PRINTTAB(6);A$(I+2);
4900 PRINTTAB(10);B$(I+2);:PRINTTAB(15);P$(H)
4910 NEXTI
4920 PRINT:PRINT
4930 PRINT"Wilt U het eiwit"
4940 PRINT"afzonderlijk zien? (J/N) ";
4950 GOSUB210:ZZ$=IN$:REM wacht op toets
4960 IFZZ$="J" THEN4980
4970 GOTO1710
4980 GOSUB100:REM scherm schoon
4990 PRINT"Druk voor start en stop een toets in"
5000 PRINT:PRINT:PRINT
5010 GOSUB210:ZZ$=IN$:REM wacht op toets
5020 FORI=1 TOH
5030 GOSUB200:ZZ$=IN$:IFZZ$="" THEN5050
5040 GOSUB210:ZZ$=IN$:REM wacht op toets
5050 PRINTI;:PRINTTAB(6);P$(I)
5060 NEXTI
5070 PRINT:PRINT:PRINT
5080 PRINT"                    druk toets"
5090 GOSUB210:ZZ$=IN$:REM wacht op toets
5100 GOTO1710
5110 :
5120 GOSUB100:REM scherm schoon
5130 PRINT"Mutatie "
5140 PRINT:PRINT:PRINT
5150 PRINT"   De mogelijkheden zijn:"
5160 PRINT:PRINT
5170 PRINT"     (V) Vervangen (transitie)"
5180 PRINT
5190 PRINT"     (I) Invoegen (insertie)"
5200 PRINT
5210 PRINT"     (W) Weghalen (deletie)"
5220 PRINT
5230 PRINT"     (*) Terug naar hoofdmenu"
5240 PRINT:PRINT:PRINT
5250 PRINT"            druk toets (V/I/W/*)"
5260 GOSUB210:ZZ$=IN$:REM wacht op toets
5270 IFZZ$="V" THEN5320
5280 IFZZ$="I" THEN5560
5290 IFZZ$="W" THEN5850
5300 IFZZ$="*" THEN1710
5310 GOTO5260
5320 GOSUB100:REM scherm schoon
5330 PRINT"Vervangen"
5340 PRINT:PRINT:PRINT
5350 PRINT"Wat is het nummer van de te muteren"
5360 PRINT"base ";
5370 INPUTZZ$
5380 Z=INT(VAL(ZZ$)):IF(Z<1) OR(Z>T) THEN5370
5390 PRINT:PRINT
5400 PRINTZ;:PRINTTAB(6);A$(Z)
5410 PRINT:PRINT
5420 PRINT"   Wat wordt deze (A,T,C,G) ?"
5430 GOSUB200:ZZ$=IN$:REM haal toets
5440 IF(ZZ$="A") THEN5490
5450 IF(ZZ$="T") THEN5490
5460 IF(ZZ$="G") THEN5490
5470 IF(ZZ$="C") THEN5490
5480 GOTO5430
5490 PRINT:PRINT
5500 PRINTZ;:PRINTTAB(6);A$(Z);" wordt ";ZZ$
5510 A$(Z)=ZZ$
5520 PRINT:PRINT:PRINT
5530 PRINT"                  druk toets"
5540 GOSUB210:ZZ$=IN$:REM wacht op toets
5550 GOTO5120
5560 GOSUB100:REM scherm schoon
5570 PRINT"Invoegen"
5580 IFT=240 THEN1710
5590 PRINT:PRINT:PRINT
5600 PRINT"Wat is het nummer van de invoeg- "
5610 PRINT"    plaats ";
5620 INPUTZZ$
5630 Z=INT(VAL(ZZ$)):IF(Z<1) OR(Z>T) THEN5620
5640 PRINT:PRINT
5650 PRINT"   Wat komt hier (A,T,C,G) ?"
5660 GOSUB200:ZZ$=IN$:REM haal toets
5670 IF(ZZ$="A") THEN5720
5680 IF(ZZ$="T") THEN5720
5690 IF(ZZ$="G") THEN5720
5700 IF(ZZ$="C") THEN5720
5710 GOTO5660
5720 PRINT:PRINT
5730 PRINTZ;:PRINT" invoegen van ";ZZ$
5740 FORI=T TOZ STEP-1
5750 A$(I+1)=A$(I)
5760 NEXTI
5770 A$(241)=""
5780 A$(Z)=ZZ$:T=T+1
5790 PRINT
5800 PRINT"  Invoegen gereed"
5810 PRINT:PRINT:PRINT
5820 PRINT"                   druk toets"
5830 GOSUB210:ZZ$=IN$:REM wacht op toets
5840 GOTO5120
5850 GOSUB100:REM scherm schoon
5860 PRINT"Weghalen"
5870 IFT=1 THEN1710
5880 PRINT:PRINT:PRINT
5890 PRINT"Wat is het nummer van de weg te halen"
5900 PRINT"      base ";
5910 INPUTZZ$
5920 PRINT:PRINT
5930 Z=INT(VAL(ZZ$)):IF(Z<1) OR(Z>T) THEN5910
5940 PRINT:PRINT
5950 PRINT"Op plaats ";Z;" stond een ";A$(Z)
5960 PRINT
5970 PRINT"        Deze vervalt"
5980 PRINT:PRINT
5990 FORI=Z TOT
6000 A$(I)=A$(I+1)
6010 NEXTI
6020 T=T-1
6030 PRINT
6040 PRINT"  Weghalen gereed"
6050 PRINT:PRINT:PRINT
6060 PRINT"                   druk toets"
6070 GOSUB210:ZZ$=IN$:REM wacht op toets
6080 GOTO5120
6090 GOSUB100:REM * Toon streng vanaf Z
6100 PRINT"Vanaf welke base wilt U de streng zien ";
6110 INPUTZZ$
6120 Z=INT(VAL(ZZ$))
6130 IF(Z<1) OR(Z>T) THEN6110
6140 PRINT:PRINT
6150 PRINT"Druk voor start en stop een toets in"
6160 PRINT:PRINT
6170 GOSUB210:ZZ$=IN$:REM wacht op toets
6180 FORI=Z TOT
6190 PRINTI;:PRINTTAB(6);A$(I)
6200 GOSUB200:ZZ$=IN$:IFZZ$="" THEN6220
6210 GOSUB210:ZZ$=IN$:IFZZ$="" THEN6210
6220 NEXTI
6230 PRINT:PRINT:PRINT
6240 PRINT"                      druk toets"
6250 GOSUB210:ZZ$=IN$:REM haal toets
6260 GOTO1710
6270 GOSUB100:REM scherm schoon
6280 PRINT"U kunt de aanwezige streng verlengen"
6290 PRINT"met basen A,T,C en G."
6300 PRINT" * = klaar"
6310 PRINT:PRINT
6320 IFT>5 THENQ=5
6330 IFT<=5 THENQ=T-1
6340 FORI=Q TO0 STEP-1
6350 PRINTT-I;:PRINTTAB(6);A$(T-I)
6360 NEXTI
6370 I=T
6380 I=I+1
6390 IFI>240 THENT=240:GOTO6500
6400 GOSUB200:ZZ$=IN$:REM haal toets
6410 IFZZ$="*" THENT=I-1:GOTO6500
6420 IF(ZZ$="A") THEN6470
6430 IF(ZZ$="T") THEN6470
6440 IF(ZZ$="G") THEN6470
6450 IF(ZZ$="C") THEN6470
6460 GOTO6400
6470 A$(I)=ZZ$
6480 PRINTI;:PRINTTAB(6);A$(I)
6490 GOTO6380
6500 PRINT
6510 PRINT"Reeks voltooid"
6520 PRINT:PRINT:PRINT
6530 PRINT"                       druk toets"
6540 GOSUB210:REM wacht op toets
6550 GOTO1710
6560 END
32000 REM
32010 REM Drs. K.G.L. Loehlefink
32020 REM Toplicht 127
32030 REM 9732 HH  GRONINGEN
32040 REM
32050 REM bewerking voor het tijdschrift
32055 REM Mens en Wetenschap 7/87
32057 REM (1 oktober-15 november)
32060 REM en Basicode-2:
32070 REM
32080 REM John Beek en
32090 REM
32100 REM Pim van Tend
32110 REM Veldheimwg 8
32120 REM 6871 CD  RENKUM
32130 REM 08373 - 15358
32140 REM
32150 REM copyright (c) 1987
32160 REM NOS HILVERSUM

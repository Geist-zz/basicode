1000 A=2500:GOTO20:REM ETIKETTEN PRINTEN
1010 GOTO2500:REM SCHERM OPMETEN
1020 READRE$
1030 IFRE$<>"***" THEN1070
1040 GOSUB1240
1050 IFAR<>0 THEN1040
1060 GOTO3000
1070 BK=1
1080 IFBK>LEN(RE$) THEN1020
1090 AK=0
1100 AK=AK+1
1110 IFBK+AK>LEN(RE$) THEN1130
1120 IFMID$(RE$,BK+AK,1)<>" " THEN1100
1130 WO$=MID$(RE$,BK,AK)
1140 BK=BK+LEN(WO$)+1
1150 IFPL+LEN(WO$)>=RL THENGOSUB1240
1160 IFWO$="&" THENWO$="":GOSUB1240:GOTO1200
1170 IFWO$<>"#" THEN1200
1180 GOSUB1240:IFAR<>0 THEN1180
1190 GOTO1080
1200 PRINTWO$;
1210 PL=PL+LEN(WO$)+1
1220 IFPL<RL-1 THENPRINT" ";
1230 GOTO1080
1240 PRINT
1250 AR=AR+1
1260 IFAR<SH-2 THEN1320
1270 PRINT
1280 PRINT"  VERVOLG: <return>";
1290 GOSUB210:IFIN$<>CHR$(13) THEN1290
1300 AR=0
1310 GOSUB100
1320 PL=0
1330 RETURN
1340 REM
2500 GOSUB100
2510 REM
2520 SH=14:VE=14:RL=16
2530 HO=RL:GOSUB110:GOSUB120
2540 IFHO=RL THENRL=RL+1:GOTO2530
2550 VE=SH:GOSUB110:GOSUB120
2560 IFVE=SH THENSH=SH+1:GOTO2550
2580 GOSUB100
2590 PL=0:AR=0
2600 GOTO1020
2610 REM
3000 REM HOOFDPROGRAMMA
3010 DIML$(25)
3160 GOSUB100:GOSUB250:HO=5:VE=5:GOSUB110
3170 PRINT"Hoeveel regels heeft het etiket":PRINT:PRINT
3180 INPUTVO:IFVO=0 THEN3160
3190 GOSUB100:GOSUB250:HO=10:VE=5:GOSUB110
3200 PRINT"Hoeveel items wilt U":PRINT:INPUTIT
3210 IFIT=0 THEN3190
3220 FORI=1 TOIT:GOSUB100:GOSUB250:HO=17:VE=5:GOSUB110
3230 PRINT"item ";I:PRINT:INPUTL$(I)
3240 NEXTI:IFIT=1 THEN3280
3250 GOSUB100:GOSUB250:HO=5:VE=5:GOSUB110
3260 PRINT"Hoeveel regels tussen items":PRINT:INPUTRE
3270 GOSUB3420
3280 GOSUB100:GOSUB250:HO=10:VE=5:GOSUB110
3290 PRINT"Hoeveel afdrukken":PRINT:INPUTHE
3300 IFHE=0 THEN3280
3310 GOSUB100:GOSUB250:PRINT"Ik ben nu bezig !!"
3320 FORH=1 TOHE:FORI=1 TOIT
3330 SR$=L$(I):GOSUB350:GOSUB360
3340 IFRE>0 THENGOSUB3400
3350 NEXTI
3360 A1=IT*(RE+1):A2=VO-A1
3370 FORA=1 TOA2:GOSUB360:NEXTA
3380 HO=10:VE=5:GOSUB110:PRINTH;" ETIKETTEN GEPRINT":NEXTH
3390 END
3400 FORX=1 TORE:GOSUB360:NEXTX
3410 RETURN
3420 IFVO>=IT*(RE+1) THENRETURN
3430 IFVO<=IT*(RE+1) THENGOSUB100:FORA=1 TO14
3440 H0=10:VE=5:GOSUB110:PRINT"TEVEEL REGELS":GOSUB250
3450 NEXTA:END
25000 DATA"     ETIKET PRINTER & &"
25010 DATA"& & & & Met dit programma kunt U door middel van"
25020 DATA"het ingeven van items zoveel etiketten"
25030 DATA"printen als U wilt."
25040 DATA"& & Dit programma is alleen geschikt met"
25050 DATA"etiketten op een kettingrol."
25060 DATA"& & & & & UITLEG OP VOLGENDE PAGINA"
25070 DATA"# & & U moet tijdens het programma ingeven: & &"
25080 DATA"& & 1 hoeveel regels een etiket heeft"
25090 DATA" (Als U dat niet weet plak dan een etiket"
25100 DATA" op een listing o.i.d. en tel de regels)"
25110 DATA"& & 2 hoeveel items U wilt hebben"
25120 DATA"& & 3 wat U per item geprint wilt hebben"
25130 DATA"& & 4 hoeveel lege regels er tussen de items"
25140 DATA" moeten staan"
25150 DATA"& & 5 hoeveel etiketten U geprint wilt hebben"
25160 DATA"# & & & & & &         SUCCES !!!!!!"
25170 DATA"***"
30000 REM ****************************
30010 REM *    geschreven door :     *
30020 REM *      Leo Teunissen       *
30030 REM *     Lange bunder 60      *
30040 REM *      4854MA Bavel        *
30050 REM * op MSX Philips VG-8020   *
30060 REM * voor MSX op 11-07-1986   *
30070 REM * aangepast voor basicode  *
30080 REM *       op 12-07-86        *
30090 REM ****************************
31000 REM
31010 REM COPYRIGHT NOS HILVERSUM


1000 A=1000:GOTO20
1010 REM  EMUF 1.7+ - EXTENDED EMUF
1020 B=0:R$="":GOSUB100
1030 PRINT"PROPAGATION FORECAST CALCULATION"
1040 PRINT"--------------------------------"
1050 PRINT:PRINT"Give your location in degrees (decimal)"
1060 PRINT:PRINT"Latitude (North +, South -): ";
1070 INPUTL5$:IFL5$="" THENL5$="52"
1080 L5=VAL(L5$)
1090 PRINT:PRINT"Longitude  (East +, West -): ";
1100 INPUTW5$:IFW5$="" THENW5$="5"
1110 W5=VAL(W5$)
1120 BO$="":FORI=1 TO4
1130 BO$=BO$+"-------------------":NEXTI:BO$=BO$+"-"
1140 DIMA1(24),B1(24),C1(24),L(1)
1150 DIMT1(1),T3(1),T4(1),C0(1),T9(1),M(1),M$(12)
1160 DIMG6(1),G7(1),G8(1),J(24),K(24),F(9),Q$(9,24):N1=0
1170 DIMA1$(24),B1$(24),C1$(24),N$(24)
1180 P1=3.14159:J9=P1/180
1190 FORI=1 TO9:READF(I):NEXTI
1200 FORI=1 TO12:READM$(I):NEXTI
1210 GOSUB100:HO=0:VE=5:GOSUB110
1220 PRINT"Other station  ";:INPUTL$
1230 PRINT"Latitude       ";:INPUTL6
1240 PRINT"Longitude      ";:INPUTW6
1250 PRINT"Month (1...12) ";:INPUTM0
1260 S0$=""
1270 PRINT"Sunspot number ";:INPUTS0$
1280 S0=VAL(S0$):IFS0<>0 THEN1330
1290 PRINT"Flux           ";:INPUTFX
1300 FORZ=1 TO300
1310 IFFX=INT(63.75+.728*Z+8.9E-04*Z*Z+.5) THENS0=Z:Z=300
1320 NEXTZ
1330 P$="S"
1340 IFABS(W5-W6)<120 THEN1380
1350 PRINT"Long or short path (L/S): ";:INPUTP$
1360 IFP$="l" THENP$="L"
1370 IFP$="s" THENP$="S"
1380 D0=15
1390 W1=-(W5-180*(1+SGN(W5-1E-03)))*J9
1400 W2=-(W6-180*(1+SGN(W6-1E-03)))*J9
1410 L1=L5*J9:L2=L6*J9:GOSUB100
1420 PRINT"Route...... ";L$:PRINT"Month...... ";M$(M0)
1430 PRINT"SSN........ ";S0
1440 PRINT"Flux....... ";INT(63.75+.728*S0+8.9E-04*S0*S0+.5)
1450 REM ** ROTATE LONGITUDES **
1460 W3=W2-W1+1E-03
1470 W3=P1*(1-SGN(W3))+W3
1480 REM ** PATH LENGTH **
1490 H1=SIN(L1)*SIN(L2)+COS(L1)*COS(L2)*COS(W3)
1500 G1=ATN(SQR(1-H1*H1)/H1)+P1/2*(1-SGN(H1))
1510 IFP$="L" THENG1=P1+P1-G1
1520 REM ** PATH LENGTH IN 4000 KM UNITS **
1530 H0=INT(1.59*G1)+1
1540 REM ** BEARING **
1550 H9=(SIN(L2)-H1*SIN(L1))/SIN(G1)/COS(L1)
1560 H9=ATN(SQR(1-H9*H9)/H9)+P1/2*(1-SGN(H9))
1570 H9=H9*SGN(W3-P1)*SGN(P1-G1)
1580 H9=H9+P1*(1-SGN(H9))
1590 BE=INT(H9/J9+.5)
1600 IFP$="S" THENPRINT"Short path. ";:GOTO1620
1610 PRINT"Long path.. ";
1620 PRINTH0;" hop";:IFH0>1 THENPRINT"s";
1630 PRINT:PRINT"Bearing.... ";BE;" degrees"
1640 PRINT"Distance... ";INT(G1*6368);" kilometers"
1650 HO=0:VE=8:GOSUB110
1660 PRINT"UTC 00  04  08  12  16  20  24"
1670 HO=0:VE=20:GOSUB110
1680 PRINT"UTC 00  04  08  12  16  20  24"
1690 PRINT"Add 30 minutes to each UTC."
1700 HO=5:VE=9:GOSUB110:PRINT"!...!...!...!...!...!...!"
1710 HO=5:VE=19:GOSUB110:PRINT"!...!...!...!...!...!...!"
1720 FORI=1 TO9
1730 SR=F(I)/10:GOSUB300
1740 IFLEN(SR$)<4 THENSR$=SR$+" ":GOTO1740
1750 HO=1:VE=I+9:GOSUB110:PRINTSR$
1760 HO=32:GOSUB110:PRINTSR$
1770 NEXTI
1780 Y6=ATN(1/TAN(G1/(H0+1))-.952/SIN(G1/(H0+1)))
1790 IFY6<.314 THENY6=.314
1800 Y6=1/SQR(1-.965*COS(Y6)^2)
1810 Y1=.0172*(10+(M0-1)*30.4+D0)
1820 Y2=.409*COS(Y1)
1830 Y1=.13*SIN(Y1)+.156*SIN(Y1+Y1)
1840 REM ** DIRECTION COSINE **
1850 H9=(SIN(L1)-COS(G1)*SIN(L2))/SIN(G1)/COS(L2)
1860 REM ** 'M' FACTOR **
1870 M9=SIN(2.5*G1/H0)
1880 M9=1+2.5*M9*SQR(M9)
1890 K1=1-.5/H0
1900 FORN=0 TO1
1910 REM ** CONTROL POINTS **
1920 L9=COS(G1*K1)*SIN(L2)+SIN(G1*K1)*COS(L2)*H9
1930 L0=P1/2-(ATN(SQR(1-L9*L9)/L9)+P1/2*(1-SGN(L9)))
1940 W0=(COS(G1*K1)-SIN(L2)*L9)/COS(L2)/COS(L0)
1950 W0=ATN(SQR(1-W0*W0+1E-04)/W0)+P1/2*(1-SGN(W0))
1960 W0=P1-SGN(P1-G1*K1)*(P1-W0)
1970 W0=W3+W0*SGN(W3-P1)*SGN(P1-G1)+W1-1E-03
1980 W0=W0-P1*(1-SGN(P1+P1-W0))
1990 REM ** LOCAL NOON **
2000 T0=3.82*W0+12+Y1
2010 T0=T0-12*(1+SGN(T0-24))*SGN(ABS(T0-24))
2020 IFCOS(L0+Y2)>-.26 THEN2060
2030 T1(N)=0
2040 GOTO2180
2050 REM ** DURATION OF SUNLIGHT **
2060 T1(N)=(-.26+SIN(Y2)*L9)/(COS(Y2)*COS(L0)+1E-03)
2070 T1(N)=12-ATN(T1(N)/SQR(ABS(1-T1(N)*T1(N))))*24/P1
2080 REM ** T(DAWN) **
2090 T7=T0-T1(N)/2
2100 T3(N)=T7+12*(1-SGN(T7))*SGN(ABS(T7))
2110 REM ** T(SUNSET) **
2120 T7=T0+T1(N)/2
2130 T4(N)=T7-12*(1+SGN(T7-24))*SGN(ABS(T7-24))
2140 C0(N)=ABS(COS(L0+Y2))
2150 REM ** RELAXATION TIME **
2160 T9(N)=9.7*(C0(N)^8)
2170 IFT9(N)<.1 THENT9(N)=.1
2180 K1=1-K1
2190 REM ** F0F2 MULTIPLIERS **
2200 M(N)=M9*.75*((12/T1(N)-1)*SGN(INT(12/T1(N)))+1)
2210 ZZ=(1+S0/100*(1-(T1(N)/12-1)*SGN(INT(T1(N)/12))))
2220 M(N)=M(N)*ZZ
2230 L9=ABS(L0+.21*SIN(W0+.35))
2240 G2=.5
2250 IFL9<P1/4 THEN2280
2260 M(N)=M(N)*(1+.25*COS(L9+L9))
2270 G2=.2
2280 L(N)=SIN(L9*4)*G2
2290 REM ** EFFECTIVE COS X PRESETS **
2300 G8(N)=P1*T9(N)/T1(N)
2310 T7=T1(N)/T9(N)
2320 IFT7>85 THENT7=85
2330 G7(N)=C0(N)*G8(N)*(EXP(-T7)+1)
2340 G6(N)=G7(N)*EXP((T1(N)-24)/2)
2350 NEXTN
2360 FORT5=.5 TO23.5 STEP1
2370 J9=100
2380 K9=0
2390 FORN=0 TO1
2400 G0=0:G3=P1/2
2410 IFT1(N)=0 THEN2630
2420 IFT4(N)<T3(N) THEN2520
2430 REM ** DAYTIME? **
2440 IF(T5-T3(N))*(T4(N)-T5)>0 THEN2530
2450 REM ** EFFECTIVE COS X (NIGHT) **
2460 T6=T5+12*(1+SGN(T4(N)-T5))*SGN(ABS(T4(N)-T5))
2470 G4=P1*(T6-T4(N))/(24-T1(N))
2480 G0=G7(N)*EXP((T4(N)-T6)/2)
2490 G3=G4+(P1-G4)/4*(1+SGN(L(N)))
2500 G4=0:GOTO2630
2510 REM ** NIGHT TIME? **
2520 IF(T5-T4(N))*(T3(N)-T5)>0 THEN2450
2530 REM ** EFFECTIVE COS X (DAY) **
2540 T6=T5+12*(1+SGN(T3(N)-T5))*SGN(ABS(T3(N)-T5))
2550 G4=P1*(T6-T3(N))/T1(N)
2560 T8=(T3(N)-T6)/T9(N)
2570 IFABS(T8)>85 THENT8=85*SGN(T8)
2580 G0=C0(N)*(SIN(G4)+G8(N)*(EXP(T8)-COS(G4)))
2590 G3=P1/2:IFT6-T3(N)>T1(N)/2+3 THEN2610
2600 G3=(T6-T3(N))/(T1(N)/2+3)*G3
2610 G3=G3*(1+SGN(L(N)))/2
2620 IFG0<G6(N) THENG0=G6(N)
2630 REM ** F0F2 **
2640 G2=SQR(7+45*SQR(G0/(1+G8(N)*G8(N))))
2650 REM ** HPF **
2660 G2=G2*M(N)*(1+SIN(G3)*L(N))
2670 IFG2<J9 THENJ9=G2
2680 REM ** E LAYER **
2690 Y8=.2:IFT1(N)*G4=0 THEN2780
2700 Y9=C0(N)*SIN(P1*(T6-T3(N))/T1(N))
2710 IFY9>.174 THEN2760
2720 REM F0(E) TWILIGHT
2730 Y8=(ATN(SQR(1-Y9*Y9)/Y9)*180/P1-76)^(-.4)
2740 GOTO2780
2750 REM F0(E) DAYTIME
2760 Y8=Y9^(.3)
2770 REM SCREENING FREQ
2780 Y9=(3.4+5.44E-03*S0)*Y8*Y6
2790 REM LUF
2800 IFY9>7 THENY9=(1.33*Y9-3.31)^(2)/7:GOTO2820
2810 Y9=.91*Y9-.37
2820 IFK9<Y9 THENK9=Y9
2830 NEXTN:K9=K9*1.1
2840 N1=N1+1
2850 J(N1)=J9
2860 K(N1)=K9
2870 J7=1.27
2880 K7=2.7-1.13*LOG(K9)/LOG(10)
2890 REM DISPLAY DIAGRAMME
2900 S$="-"
2910 FORB=1 TO9:VE=B+9:HO=N1+4:GOSUB110
2920 X=(F(B)*.1-J9)/(J9*J7-J9):GOSUB20000
2930 U=FF:IFU>99 THENU=99
2940 IFU<7 THENPRINT"-";:Q$(B,N1)=S$:GOTO3010
2950 X=(K9*K7-F(B)*.1)/(K9*K7-K9):GOSUB20000
2960 L=FF:IFL>99 THENL=99
2970 IFL<7 THENPRINT"-";:Q$(B,N1)=S$:GOTO3010
2980 SR=INT(U*L*1E-03):GOSUB300:R$=RIGHT$(SR$,1)
2990 Q$(B,N1)=R$
3000 PRINTR$;
3010 NEXTB:NEXTT5
3020 HO=0:VE=24:GOSUB110:PRINT"Press any key for table";
3030 GOSUB210
3040 GOSUB100:PRINTL$;"  ";M$(M0);"  SSN ";S0:PRINT
3050 PRINT:PRINT"UTC  MUF  OWF  LUF ! UTC  MUF  OWF  LUF"
3060 PRINT"-------------------!-------------------"
3070 FORN=1 TO12:M=N+12
3080 CT=5:CN=1
3090 A1(N)=INT(J(N)*12.7+.5):B1(N)=INT(J(N)*10+.5)
3100 SR=A1(N)/10:GOSUB310
3110 A1$(N)=SR$
3120 SR=B1(N)/10:GOSUB310
3130 B1$(N)=SR$
3140 C1(N)=INT(K(N)*10+.5)
3150 SR=C1(N)/10:GOSUB310
3160 C1$(N)=SR$
3170 A1(M)=INT(J(M)*12.7+.5):B1(M)=INT(J(M)*10+.5)
3180 SR=A1(M)/10:GOSUB310
3190 A1$(M)=SR$
3200 SR=B1(M)/10:GOSUB310
3210 B1$(M)=SR$
3220 C1(M)=INT(K(M)*10+.5)
3230 SR=C1(M)/10:GOSUB310
3240 C1$(M)=SR$
3250 SR=N-1:GOSUB300
3260 IFLEN(SR$)<3 THENSR$=" "+SR$:GOTO3260
3270 N$(N)=SR$
3280 SR=M-1:GOSUB300
3290 SR$=" "+SR$:N$(M)=SR$
3300 PRINTN$(N);A1$(N);B1$(N);C1$(N);
3310 PRINT" ! ";N$(M);A1$(M);B1$(M);C1$(M)
3320 NEXTN
3330 PRINT"-------------------!-------------------"
3340 PRINT:PRINT"Add 30 minutes to each UTC."
3350 PRINT:PRINT"(P)rintout, (C)ontinue or (Q)uit ?";
3360 GOSUB210
3370 IF(IN$="C") OR(IN$="c") THENRUN
3380 IF(IN$="Q") OR(IN$="q") THENEND
3390 IF(IN$<>"P") AND(IN$<>"p") THEN3360
3400 REM
3410 REM ***PRINTER ROUTINE
3420 REM
3430 GOSUB360
3440 SR$=BO$:GOSUB350:GOSUB360
3450 SR$="EMUF.V1.7+ FORECAST                    "
3460 SR$=SR$+"                        "
3470 SR$=SR$+"SM6CPI VERSION"
3480 GOSUB350:GOSUB360
3490 SR$=BO$:GOSUB350:GOSUB360
3500 BS$="                                      "
3510 SR$="Month:  "+M$(M0)+LEFT$(BS$,(26-LEN(M$(M0))))
3520 SR$=SR$+"To "+L$:GOSUB350:GOSUB360
3530 SR=S0:GOSUB300:S0$=SR$
3540 SR=INT(BE):GOSUB300:BE$=SR$
3550 SR=INT(G1*6368):GOSUB300:G1$=SR$
3560 SR$="Sunspot No. "+S0$
3570 SR$=SR$+LEFT$(BS$,(22-LEN(S0$)))+"Bearing "+BE$
3580 GOSUB350:GOSUB360
3590 SR=H0:GOSUB300:H0$=SR$
3600 SR$="Distance "+G1$+" km"+LEFT$(BS$,(22-LEN(G1$)))
3610 SR$=SR$+"No. of hops "+H0$
3620 GOSUB350:GOSUB360
3630 IFP$="S" THENSR$="Short path":GOTO3650
3640 SR$="Long path"
3650 GOSUB350:GOSUB360:GOSUB360
3660 SR$="UTC  00  04  08  12  16  20  24 "
3670 SR$=SR$+"  UTC  MUF  OWF  LUF  :  UTC  MUF  OWF  LUF"
3680 GOSUB350:GOSUB360
3690 SR$="     !...!...!...!...!...!...!"
3700 GOSUB350:GOSUB360
3710 FORB=1 TO9
3720 CT=3:CN=0
3730 IFB>7 THENCN=1
3740 SR=F(B)/10:GOSUB310
3750 SR$=SR$+"  "
3760 FORT=1 TO24
3770 SR$=SR$+Q$(B,T)
3780 NEXTT
3790 I=B:J=I+12
3800 SR$=SR$+"   ":GOSUB3990
3810 NEXTB
3820 SR$="     !...!...!...!...!...!...!  "
3830 I=10:J=22:GOSUB3990
3840 SR$="UTC  00  04  08  12  16  20  24 "
3850 I=11:J=23:GOSUB3990
3860 SR$="                                "
3870 I=12:J=24:GOSUB3990
3880 GOSUB360
3890 SR$="Add 30 minutes to each UTC.        "
3900 GOSUB350:GOSUB360
3910 SR$=BO$:GOSUB360:GOSUB350:GOSUB360
3920 FORI=1 TO4:GOSUB360:NEXTI
3930 GOSUB100:VE=5:HO=0:GOSUB110
3940 PRINT"(C)ontinue, (Q)uit ";
3950 GOSUB210
3960 IF(IN$="q") OR(IN$="Q") THENEND
3970 IF(IN$="c") OR(IN$="C") THENRUN
3980 GOTO3950
3990 RR$=SR$
4000 SR=I-1:CT=5:CN=0:GOSUB310
4010 RR$=RR$+SR$
4020 RR$=RR$+A1$(I)
4030 RR$=RR$+B1$(I)
4040 RR$=RR$+C1$(I)+"  :   "
4050 SR=J-1:GOSUB300
4060 RR$=RR$+SR$
4070 RR$=RR$+A1$(J)
4080 RR$=RR$+B1$(J)
4090 SR$=RR$+C1$(J)
4100 GOSUB350:GOSUB360
4110 RETURN
20000 FF=50+X*(-50.507+X*X*(11.741+X*X*(-1.397)))
20010 RETURN
25000 DATA280,240,210,180,140,100,70,35,18
25010 DATA"JANUARY","FEBRUARY","MARCH","APRIL","MAY","JUNE"
25020 DATA"JULY","AUGUST","SEPTEMBER","OCTOBER"
25030 DATA"NOVEMBER","DECEMBER"
30000 REM  EMUF 1.7+ - EXTENDED EMUF
30010 REM  VERSION 12/11/87
30020 REM BASED ON MINIMUF-3 PROGRAM DEVISED BY:
30030 REM P.H.LEVINE, R.B.ROSE & J.N.MARTIN 
30040 REM MODIFIED BY G3CCZ AND G4LEH
30050 REM PUBLISHED IN RADCOM MARCH 1983
30060 REM SM6CPI ADAPTED IT FOR COMMODORE BASIC IN 1983
30070 REM AND ADDED DISPLAY ROUTINE
30080 REM PLUS IMPROVED CALCULATION ROUTINE
30090 REM FROM A SINCLAIR VERSION BY G3CCZ.
30100 REM  ADAPTED FOR BASICODE-2 BY: 
30110 REM Dr Pieter Vijlbrief PA0DOK
30120 REM and revised for use with
30130 REM 'Sun-Flux' in november 1987
30140 REM
30150 REM COPYRIGHT NOS HILVERSUM

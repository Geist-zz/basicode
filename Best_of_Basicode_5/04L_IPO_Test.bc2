1000 A=800:GOTO20:REM ** TEST **
1010 A=0:B=0:Q=0:PA=0:PB=1:PT=0:X=0:WA=0:CR=0:SC=0:RT=0
1015 GOSUB100
1020 T=0:TT=0:I=0:K=0:J=0:TF=0:RD=0:P=0:R=0:L=0
1030 DIMCR$(10),MN$(10),CR(10),WA(10),SC(10),RT(10)
1040 GOSUB20390:VE=14:HO=6:GOSUB110
1050 PRINT"INDIVIDUEEL PRODUKT-ONDERZOEK"
1060 VE=15:GOSUB110:PRINT"-----------------------------"
1070 VE=17:HO=9:GOSUB110:PRINT"Wilt U uitleg ? (J/N)"
1080 HO=31:GOSUB110:GOSUB210
1090 IF(IN$="J") OR(IN$="j") THENGOSUB20020:GOTO1130
1100 IF(IN$="N") OR(IN$="n") THEN1130
1110 GOTO1080
1120 REM ** INVOER PAG. 1 **
1130 GOSUB20500:GOSUB20620
1140 VE=4:HO=4:GOSUB110
1150 PRINT"Welk produkt wilt U onderzoeken ?"
1160 VE=6:HO=10:GOSUB110:PRINT"...................."
1170 VE=14:HO=4:GOSUB110
1180 PRINT"Op hoeveel  verschillende punten"
1190 VE=15:HO=4:GOSUB110
1200 PRINT"wilt U het produkt vergelijken ?"
1210 VE=17:HO=7:GOSUB110:PRINT"(min. 2 - max. 9)- ."
1220 VE=9:HO=2:GOSUB110
1230 PRINT"Hoeveel produkten wilt U onderzoeken ?"
1240 VE=11:HO=7:GOSUB110:PRINT"(min. 2 - max. 9)- ."
1250 VE=6:L=10:R=30:GOSUB20720
1260 HO=10:GOSUB110:PRINT"...................."
1270 NA$=L$:L$="":VE=6:HO=20-((LEN(NA$)/2))
1280 GOSUB110:PRINTNA$
1290 GOSUB20640:VE=11:HO=26:GOSUB20860
1300 IF(L<2) THEN1290
1310 A=L:SR=A:GOSUB21240:HO=26:GOSUB110:PRINTSR$
1320 VE=17:HO=26:GOSUB20860
1330 IF(L<2) THEN1320
1340 B=L:SR=B:GOSUB21240:HO=26:GOSUB110:PRINTSR$
1350 REM ** INVOER PAG. 2 **
1360 GOSUB20500:GOSUB20620:VE=3:HO=3:GOSUB110
1370 PRINT"Omschrijf de verschillende punten."
1380 FORQ=1 TOB:VE=5+Q:HO=7:GOSUB110
1385 SR=Q:GOSUB21240:PRINTSR$;
1390 PRINT" .  ....................":NEXTQ
1400 FORQ=1 TOB:VE=5+Q:L=12:R=32:GOSUB20720
1410 HO=12:GOSUB110:PRINT"...................."
1420 CR$(Q)=L$:L$="":VE=5+Q:HO=32-(LEN(CR$(Q)))
1430 GOSUB110:PRINTCR$(Q):NEXTQ
1440 REM ** INVOER PAG. 3 **
1450 GOSUB20500:GOSUB20640
1460 VE=3:HO=3:GOSUB110
1470 PRINT"Bepaal de mate van belang per punt."
1480 VE=18:HO=2:GOSUB110
1490 PRINT"1=weinig       2=normaal       3=veel"
1500 FORQ=1 TOB:VE=5+Q:HO=5:GOSUB110
1505 SR=Q:GOSUB21240:PRINTSR$;
1510 PRINT" .":HO=30-(LEN(CR$(Q))):GOSUB110
1520 PRINTCR$(Q);:PRINT"-  .":NEXTQ
1530 FORQ=1 TOB:VE=5+Q:HO=33
1540 GOSUB20860:CR(Q)=L:L=0:RD=CR(Q)+RD
1550 IFCR(Q)>3 THEN1540
1555 SR=CR(Q):GOSUB21240
1560 HO=33:GOSUB110:PRINTSR$:NEXTQ
1570 REM ** INVOER PAG. 4 **
1580 GOSUB20500:GOSUB20620
1590 VE=3:HO=6:GOSUB110
1600 PRINT"Vul merknamen en evt. type in."
1610 FORQ=1 TOA:VE=5+Q:HO=7:GOSUB110
1615 SR=Q:GOSUB21240:PRINTSR$;
1620 PRINT" .  ....................":NEXTQ
1630 FORQ=1 TOA:VE=5+Q:L=12:R=32:GOSUB20720
1640 HO=12:GOSUB110:PRINT"...................."
1650 MN$(Q)=L$:L$="":VE=5+Q:HO=32-(LEN(MN$(Q)))
1660 GOSUB110:PRINTMN$(Q):NEXTQ
1670 REM ** INVOER PAG. 5 **
1680 PA=PA+1:FORQ=1 TOA
1690 GOSUB20510:GOSUB20640:VE=3:HO=2:GOSUB110
1700 PRINT"Waardering voor- ";:PRINTMN$(Q)
1710 VE=17:HO=1:GOSUB110
1720 PRINT" 1.slecht    2.matig       3.voldoende"
1730 VE=18:HO=1:GOSUB110
1740 PRINT" 4.goed      5.uitstekend"
1750 PB=PB+1:FORX=1 TOB
1760 VE=4+X:HO=5:GOSUB110
1765 SR=X:GOSUB21240:PRINTSR$;" .";
1770 HO=30-LEN(CR$(X)):GOSUB110:PRINTCR$(X);:PRINT"-  ."
1780 NEXTX
1790 FORX=1 TOB:VE=4+X:HO=33
1800 GOSUB20860:WA(X)=L:L=0
1810 IF(WA(X)>5) THEN1800
1815 SR=WA(X):GOSUB21240
1820 HO=33:GOSUB110:PRINTSR$
1830 T=(CR(X)*WA(X)):SC(Q)=(SC(Q)+T)
1840 NEXTX:TT=SC(Q)+TT:NEXTQ
1850 REM ** SORTEREN **
1860 FORJ=1 TOA:FORI=1 TOA
1870 IFSC(I)>SC(I+1) THEN1900
1880 K=SC(I):SC(I)=SC(I+1):SC(I+1)=K
1890 K$=MN$(I):MN$(I)=MN$(I+1):MN$(I+1)=K$
1900 NEXTI:NEXTJ:PB=1
1910 REM ** BEOORDELING PAG. 6 **
1920 GOSUB20500
1930 GOSUB20620:VE=20:HO=10:GOSUB110
1940 PRINT"----- V-VERDER ----- "
1950 VE=3:HO=2:GOSUB110:PRINT"Onderzoek naar - ";:PRINTNA$
1960 FORX=1 TOA
1970 VE=5+X:HO=5:GOSUB110
1975 SR=X:GOSUB21240:PRINTSR$;
1980 HO=28-LEN(MN$(X)):GOSUB110:PRINTMN$(X)
1990 HO=32:GOSUB110
1995 SR=SC(X):GOSUB21360:PRINTSR$
2000 NEXTX:TF=INT(TT/A)
2010 VE=17:HO=12:GOSUB110:PRINT"gemiddelde score";
2015 HO=32:GOSUB110
2020 SR=TF:GOSUB21360:PRINTSR$
2030 VE=20:HO=38:GOSUB110:GOSUB210
2040 IF(IN$="V") OR(IN$="v") THEN2070
2050 GOTO2030
2060 REM ** BEOORDELING PAG. 7 **
2070 GOSUB20500
2080 GOSUB20620:VE=20:HO=4:GOSUB110
2090 PRINT"-----   V-VERDER    T-TERUG  ----"
2100 VE=3:HO=2:GOSUB110:PRINT"De waardering -"
2110 VE=7:HO=5:GOSUB110:PRINT"Uitstekend";
2120 SR=INT((RD*B*4.5)/B):GOSUB21310
2130 VE=9:HO=11:GOSUB110:PRINT"Goed";
2140 SR=INT((RD*B*3.5)/B):GOSUB21310
2150 VE=11:HO=6:GOSUB110:PRINT"Voldoende";
2160 SR=INT((RD*B*2.5)/B):GOSUB21310
2170 VE=13:HO=10:GOSUB110:PRINT"Matig";
2180 SR=INT((RD*B*1.5)/B):GOSUB21310
2190 VE=15:HO=9:GOSUB110:PRINT"Slecht";
2195 SR=0:GOSUB21310
2200 VE=20:HO=38:GOSUB110:GOSUB210
2210 IF(IN$="V") OR(IN$="v") THEN2250
2220 IF(IN$="T") OR(IN$="t") THENPA=PA-2:GOTO1920
2230 GOTO2200
2240 REM ** BEOORDELING PAG. 8 **
2250 GOSUB20500
2260 GOSUB20620:VE=20:HO=4:GOSUB110
2270 PRINT"- S-STOPPEN  T-TERUG  P-PRINTEN -"
2280 VE=3:HO=2:GOSUB110:PRINT"Onderzoek naar - ";:PRINTNA$
2290 FORX=1 TOA:RT(X)=(SC(X)/RD)
2300 VE=5+X:HO=3:GOSUB110:PRINTMN$(X)
2310 HO=25:GOSUB110:GOSUB20980:PRINT"- ";
2320 PRINTBO$:NEXTX
2330 VE=20:HO=38:GOSUB110:GOSUB210
2340 IF(IN$="T") OR(IN$="t") THENPA=PA-2:GOTO2070
2350 IF(IN$="S") OR(IN$="s") THEN2770
2360 IF(IN$="P") OR(IN$="p") THENGOSUB2390
2370 GOTO2330
2380 REM ** PRINTEN **
2390 GOSUB21050:PT=1:REM resultaten naar printer
2400 SR$="De resultaten van het onderzoek naar - "
2410 GOSUB350:SR$=NA$:GOSUB350:GOSUB21050
2420 FORX=1 TOA:GOSUB360
2430 SR$="   ":GOSUB350
2440 SR=X:GOSUB21240:GOSUB350
2450 SR$=".":GOSUB350
2460 P=22-LEN(MN$(X)):FORZ=1 TOP:SR$=" ":GOSUB350:NEXTZ
2470 SR$=MN$(X):GOSUB350:SR$=" - ":GOSUB350
2475 SR=SC(X):GOSUB21360:GOSUB350
2480 SR$=" punten ---- ":GOSUB350
2490 GOSUB20980:SR$=BO$:GOSUB350
2500 NEXTX:GOSUB360:GOSUB360
2510 SR$="           gemiddelde score - ":GOSUB350
2515 SR=TF:GOSUB21360:GOSUB350
2520 SR$=" punten":GOSUB350
2530 GOSUB360:GOSUB360
2540 SR$="De waardering komt als volgt tot stand":GOSUB350
2550 GOSUB21050:SR$="   Uitstekend":GOSUB350
2560 SR=INT((RD*B*4.5)/B):GOSUB21310
2570 SR$=" Goed ":GOSUB350
2580 SR=INT((RD*B*3.5)/B):GOSUB21310
2590 GOSUB360
2600 SR$="   Voldoende ":GOSUB350
2610 SR=INT((RD*B*2.5)/B):GOSUB21310
2620 SR$=" Matig":GOSUB350
2630 SR=INT((RD*B*1.5)/B):GOSUB21310
2640 GOSUB360
2650 SR$="   Slecht    ":GOSUB350
2660 SR=0:GOSUB21310:GOSUB360:GOSUB360
2670 SR$="De produkten zijn onderzocht op de ":GOSUB350
2680 SR$="volgende punten":GOSUB350:GOSUB21050
2690 FORQ=1 TOB:SR$="   ":GOSUB350
2700 SR=Q:GOSUB21240:GOSUB350:SR$=". ":GOSUB350
2710 SR$=CR$(Q):GOSUB350:SR$=" ":GOSUB350
2720 P=23-LEN(CR$(Q)):FORZ=1 TOP:SR$=" ":GOSUB350:NEXTZ
2730 GOSUB21090:SR$=W$:GOSUB350
2740 SR$=" rekening mee houdend.":GOSUB350:GOSUB360:NEXTQ
2750 GOSUB21050:PT=0:RETURN
2760 REM ** TEKST EINDSCHERM **
2770 GOSUB100:GOSUB20390
2780 VE=2:HO=13:GOSUB110:PRINT"CANON  V-20 MSX"
2790 VE=14:HO=13:GOSUB110:PRINT"Maarten Leeuwis"
2800 VE=15:HO=12:GOSUB110:PRINT"Eksterstraat  124"
2810 VE=16:HO=13:GOSUB110:PRINT"3853 SK  Ermelo"
2820 VE=18:HO=14:GOSUB110:PRINT"januari  1986"
2830 END
20000 REM ** SUBROUTINES **
20010 REM ** UITLEG **
20020 GOSUB100
20030 PRINT" INLEIDING"
20040 PRINT" ---------":PRINT
20050 PRINT" Met dit programma kunt U produkten met"
20060 PRINT" elkaar vergelijken om, afhankelijk van"
20070 PRINT" uw  individuele belangen, te komen tot"
20080 PRINT" een beoordeling van deze produkten.":PRINT
20090 PRINT" Het individuele karakter van deze test"
20100 PRINT" komt als volgt tot stand-":PRINT
20110 PRINT" In het verloop van het programma wordt"
20120 PRINT" gevraagd op welke punten U het produkt"
20130 PRINT" wilt  vergelijken en  hoeveel belang U"
20140 PRINT" stelt in de diverse onderdelen.":PRINT
20150 PRINT" Het is dus mogelijk dat er op het eind"
20160 PRINT" een andere  beoordeling tot stand komt"
20170 PRINT" dan in de testen die in  verschillende"
20180 PRINT" tijdschriften worden vermeld."
20190 GOSUB21140
20200 GOSUB100
20210 PRINT" GEBRUIKSAANWIJZING"
20220 PRINT" ------------------":PRINT
20230 PRINT" Links boven op het  beeldscherm ziet U"
20240 PRINT" de paginanummering.  Op de pagina's  1"
20250 PRINT" t/m 5 vult U de gegevens in.  Onder op"
20260 PRINT" het scherm  ziet U of er  tekst of een"
20270 PRINT" getal ingevoerd moet worden.":PRINT
20280 PRINT" Wanneer U een fout heeft gemaakt in de"
20290 PRINT" tekst, druk dan op . en de cursor gaat"
20300 PRINT" naar links. Verder moet U aan het eind"
20310 PRINT" van de tekst RETURN indrukken.":PRINT
20320 PRINT" Bij het invoeren  van getallen heeft U"
20330 PRINT" geen correctiemogelijkheid.  Let er op"
20340 PRINT" dat de juiste toets wordt ingedrukt.":PRINT
20350 PRINT" Volgt U verder de  aanwijzingen op het"
20360 PRINT" scherm. Veel plezier...."
20370 GOSUB21140:GOSUB100:RETURN
20380 REM *** BEGIN/EIND SCHERM ***
20390 VE=4:HO=7:GOSUB110:PRINT"***************************"
20400 VE=5:GOSUB110:PRINT"*                         *"
20410 VE=6:GOSUB110:PRINT"* OOOOO OOOOO OOOOO OOOOO *"
20420 VE=7:GOSUB110:PRINT"*   O   O     O       O   *"
20430 VE=8:GOSUB110:PRINT"*   O   OOOO  OOOOO   O   *"
20440 VE=9:GOSUB110:PRINT"*   O   O         O   O   *"
20450 VE=10:GOSUB110:PRINT"*   O   OOOOO OOOOO   O   *"
20460 VE=11:GOSUB110:PRINT"*                         *"
20470 VE=12:GOSUB110:PRINT"***************************"
20480 RETURN
20490 REM ** KOPREGEL + PAGINA NR. **
20500 PA=PA+1
20510 GOSUB100:VE=1:HO=11:GOSUB110
20520 IFPA<=5 THENPRINT"- INVOER GEGEVENS -"
20530 IFPA>5 THENPRINT"-    BEOORDELING  -"
20540 VE=0:HO=1:GOSUB110
20550 PRINT"---------------------------------------"
20560 VE=2:HO=1:GOSUB110
20570 PRINT"---------------------------------------"
20580 VE=1:HO=2:GOSUB110
20585 SR=PA:GOSUB21240:PRINTSR$
20590 VE=1:HO=38:GOSUB110
20595 SR=PB:GOSUB21240:PRINTSR$
20600 RETURN
20610 REM ** VOETREGEL **
20620 VE=20:HO=11:GOSUB110
20630 PRINT"-  Voer TEKST in. -":GOTO20660
20640 VE=20:HO=11:GOSUB110
20650 PRINT"-  Voer GETAL in. -"
20660 VE=19:HO=1:GOSUB110
20670 PRINT"---------------------------------------"
20680 VE=21:GOSUB110
20690 PRINT"---------------------------------------"
20700 RETURN
20710 REM ** TEKST INVOER ROUTINE **
20720 HO=L:IFR<=L THENR=L+1
20730 L$=""
20740 GOSUB110:GOSUB210
20750 IFIN$=CHR$(13) THENRETURN
20760 IFIN$="." THEN20810
20770 IFHO=R THEN20740
20780 PRINTIN$;
20790 HO=HO+1:L$=L$+IN$
20800 GOTO20740
20810 IFHO>L THENHO=HO-1:GOSUB110:PRINT".";
20820 IFHO=L THEN20730
20830 L$=LEFT$(L$,LEN(L$)-1)
20840 GOTO20740
20850 REM ** GETAL INVOER ROUTINE **
20860 GOSUB110:GOSUB210
20870 IF(IN$="1") THENL=1:RETURN
20880 IF(IN$="2") THENL=2:RETURN
20890 IF(IN$="3") THENL=3:RETURN
20900 IF(IN$="4") THENL=4:RETURN
20910 IF(IN$="5") THENL=5:RETURN
20920 IF(IN$="6") THENL=6:RETURN
20930 IF(IN$="7") THENL=7:RETURN
20940 IF(IN$="8") THENL=8:RETURN
20950 IF(IN$="9") THENL=9:RETURN
20960 GOTO20860
20970 REM ** BEOORDELINGS ROUTINE **
20980 IF(RT(X)>=1) THENBO$="slecht"
20990 IF(RT(X)>=1.5) THENBO$="matig"
21000 IF(RT(X)>=2.5) THENBO$="voldoende"
21010 IF(RT(X)>=3.5) THENBO$="goed"
21020 IF(RT(X)>=4.5) THENBO$="uitstekend"
21030 RETURN
21040 REM ** PRINTER ROUTINE **
21050 GOSUB360:SR$="---------------------------------------"
21060 GOSUB350:SR$="--------------------":GOSUB350:GOSUB360
21070 RETURN
21080 REM ** BEOORDELINGS ROUTINE **
21090 IF(CR(Q)=1) THENW$=" weinig"
21100 IF(CR(Q)=2) THENW$="normaal"
21110 IF(CR(Q)=3) THENW$="   veel"
21120 RETURN
21130 REM ** EINDE UITLEG PAG. **
21140 HO=31:VE=22:GOSUB110:PRINT"RETURN-"
21150 HO=38:GOSUB110:GOSUB210
21160 IFIN$=CHR$(13) THENRETURN
21170 GOTO21150
21180 REM ** GETAL AFDRUK ROUTINE **
21190 REM Deze routine zet getal SR om
21200 REM in string SR$ en verwijdert
21210 REM alle spaties aan begin en
21220 REM eind. Deze routine behoort
21230 REM eigenlijk overbodig te zijn!
21240 GOSUB300
21250 IFLEFT$(SR$,1)=" " THENGOSUB21280:GOTO21250
21260 IFRIGHT$(SR$,1)=" " THENGOSUB21290:GOTO21260
21270 RETURN
21280 SR$=RIGHT$(SR$,LEN(SR$)-1):RETURN
21290 SR$=LEFT$(SR$,LEN(SR$)-1):RETURN
21300 REM ** ROUTINE VOOR AFDRUK RESULTATEN **
21310 GOSUB21360
21320 SR$=" vanaf -"+SR$+" punten."
21330 IFPT=1 THENGOSUB350:RETURN
21340 PRINTSR$:RETURN
21350 REM ** SR OMZETTEN NAAR SR$ EN **
21360 GOSUB21240
21370 REM ** SR$ VERLENGEN TOT 4 POSITIES **
21380 FORL=1 TO(4-LEN(SR$))
21390 SR$=" "+SR$:NEXTL
21400 RETURN
30000 REM ***********************
30010 REM **  Maarten Leeuwis  **
30020 REM ** Eksterstraat  124 **
30030 REM **  3853 SK  Ermelo  **
30040 REM **                   **
30050 REM **   februari 1987   **
30060 REM ***********************
30070 REM
30080 REM COPYRIGHT NOS HILVERSUM

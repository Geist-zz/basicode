1000 A=1000:GOTO20:REM OPPERVLAK 2
1010 GOSUB20100
1020 GA=0:REM GOED ANTWOORD
1030 FA=0:REM FOUT ANTWOORD
1100 REM ****> ONDERDELEN <****
1110 GOSUB1500:REM-INTRO LES
1120 GOSUB2000:REM-OPGAVE 1
1130 GOSUB2300:REM-OPGAVE 2
1140 GOSUB2600:REM-OPGAVE 3
1150 GOSUB2900:REM-OPGAVE 4
1160 GOSUB3200:REM-OPGAVE 5A
1170 GOSUB3500:REM-OPGAVE 5B
1180 GOSUB3800:REM-INTRO TWEEDE DEEL
1190 GOSUB4000:REM-VOORBEELD 1
1200 GOSUB4400:REM-VOORBEELD 2
1210 GOSUB4800:REM-VOORBEELD 3
1220 GOSUB5200:REM-DRIE OPGAVEN
1230 GOSUB5500:REM-ANTWOORDEN
1240 GOSUB5800:REM-EINDE LES
1250 END
1260 REM
1500 REM ******> INTRO <******
1510 GOSUB100
1520 PRINT"****************************************"
1530 PRINT"* HET OPPERVLAK VAN RECHTHOEKEN DEEL 2 *"
1540 PRINT"****************************************":PRINT
1550 PRINT"We beginnen deze les met het nakijken":PRINT
1560 PRINT"van de opgaven uit de vorige les.":PRINT:PRINT
1570 PRINT"Waar dat nodig is geven we nog enkele":PRINT
1580 PRINT"aanwijzingen."
1590 W=2:GOSUB10000
1600 GOSUB100
1610 PRINT"Daarna laten we zien hoe opgaven,":PRINT
1620 PRINT"waarin iets moet worden uitgelegd,":PRINT
1630 PRINT"opgelost dienen te worden."
1640 W=2:GOSUB10000
1650 RETURN
1660 REM
2000 REM *****> OPGAVE 1 <******
2010 GOSUB100
2020 PRINT"Druk de wis-toets eens in.":PRINT:PRINT
2030 GOSUB210:VB$=IN$
2040 PRINT"Hoeveel dm2 komt er bij jou uit":PRINT
2050 PRINT"opgave 1 ?":PRINT:PRINT
2060 GOSUB120:HA=HO:VA=VE
2070 GOSUB10400:PRINT:PRINT
2080 BW=VAL("37.5"):IFAW=BW THEN2130
2090 IFAW=3750 THENPRINT"Je neemt : 1 m = 100 dm."
2100 IFAW=.00375 THENPRINT"Je neemt : 1 m = 0.1 dm."
2110 FA=1:GOSUB10700
2120 GOTO2060
2130 IFFA=0 THENGA=GA+1:PRINT:PRINT
2140 GOSUB10800:PRINT:PRINT
2150 W=.5:GOSUB10000
2160 RETURN
2170 REM
2300 REM *****> OPGAVE 2 <******
2310 GOSUB100:FA=0
2320 PRINT
2330 PRINT"Hoeveel m2 komt er bij jou uit":PRINT
2340 PRINT"opgave 2 ?":PRINT:PRINT
2350 GOSUB120:HA=HO:VA=VE
2360 GOSUB10400:PRINT:PRINT
2370 BW=VAL(".33"):IFAW=BW THEN2420
2380 IFAW=33 THENPRINT"Je neemt : 1 cm = 0.1 m."
2390 IFAW=.0033 THENPRINT"Je neemt : 1 cm = 0.001 m."
2400 FA=1:GOSUB10700
2410 GOTO2350
2420 IFFA=0 THENGA=GA+1:PRINT:PRINT
2430 GOSUB10800:PRINT:PRINT
2440 W=.5:GOSUB10000
2450 RETURN
2460 REM
2600 REM *****> OPGAVE 3 <******
2610 GOSUB100:FA=0
2620 PRINT
2630 PRINT"Hoeveel cm2 komt er bij jou uit":PRINT
2640 PRINT"opgave 3 ?":PRINT:PRINT
2650 GOSUB120:HA=HO:VA=VE
2660 GOSUB10400:PRINT:PRINT
2670 BW=VAL("15756"):IFAW=BW THEN2720
2680 IFAW=157.56 THENPRINT"Je neemt : 1 m = 10 cm."
2690 IFAW=.015756 THENPRINT"Je neemt : 1 m =  0.1 cm."
2700 FA=1:GOSUB10700
2710 GOTO2650
2720 IFFA=0 THENGA=GA+1:PRINT:PRINT
2730 GOSUB10800:PRINT:PRINT
2740 W=.5:GOSUB10000
2750 RETURN
2760 REM
2900 REM *****> OPGAVE 4 <******
2910 GOSUB100:FA=0
2920 PRINT
2930 PRINT"Hoeveel m2 komt er bij jou uit":PRINT
2940 PRINT"opgave 4 ?":PRINT:PRINT
2950 GOSUB120:HA=HO:VA=VE
2960 GOSUB10400:PRINT:PRINT
2970 BW=VAL("9"):IFAW=BW THEN3010
2980 IFAW=900 THENPRINT"Je neemt : 10 cm = 1 m."
2990 FA=1:GOSUB10700
3000 GOTO2950
3010 IFFA=0 THENGA=GA+1:PRINT:PRINT
3020 GOSUB10800:PRINT
3030 IFGA=4 THENPRINT"Dat gaat goed zo"
3040 W=1:GOSUB10000
3050 RETURN
3060 REM
3200 REM *****> OPGAVE 5A <******
3210 GOSUB100:FA=0
3220 PRINT
3230 PRINT"Hoeveel cm2 komt er bij jou uit":PRINT
3240 PRINT"opgave 5a ?":PRINT:PRINT
3250 GOSUB120:HA=HO:VA=VE
3260 GOSUB10400:PRINT:PRINT
3270 BW=VAL("3600"):IFAW=BW THEN3300
3280 FA=1:GOSUB10700
3290 GOTO3250
3300 IFFA=0 THENGA=GA+1:PRINT:PRINT
3310 GOSUB10800:PRINT
3320 IFGA=5 THENPRINT"Knap werk. En nu de laatste nog."
3330 W=1:GOSUB10000
3340 RETURN
3350 REM
3500 REM *****> OPGAVE 5B <******
3510 GOSUB100:FA=0
3520 PRINT
3530 PRINT"Hoeveel m2 komt er bij jou uit":PRINT
3540 PRINT"opgave 5b ?":PRINT:PRINT
3550 GOSUB120:HA=HO:VA=VE
3560 GOSUB10400:PRINT:PRINT
3570 BW=VAL(".36"):IFAW=BW THEN3600
3580 FA=1:GOSUB10700
3590 GOTO3550
3600 IFFA=0 THENGA=GA+1:PRINT:PRINT
3610 GOSUB10800:PRINT:PRINT
3620 W=1:GOSUB10000
3630 IFGA=6 THENPRINT"Fantastisch werk geleverd."
3640 IFGA=5 THENPRINT"Bijna alles goed, knap."
3650 IFGA=4 THENPRINT"Je hebt het wel door."
3660 IFGA<4 THENGOSUB3700
3670 W=1:GOSUB10000
3680 RETURN
3690 REM
3700 REM ****> OVER DOEN <****
3710 GOSUB100
3720 PRINT"Je had er maar";GA;"direct goed.":PRINT
3730 PRINT"Je hebt of slordig gewerkt of het nog":PRINT
3740 PRINT"niet goed door.":PRINT:PRINT
3750 W=3:GOSUB10000
3760 PRINT"In het laatste geval is het beter dat":PRINT
3770 PRINT"je oppervlak 1 nog eens over doet."
3780 RETURN
3790 REM
3800 REM *****> UITLEG OPGAVEN <*****
3810 GOSUB100
3820 PRINT"Bij sommige opgaven moet je b.v. alleen":PRINT
3830 PRINT"dm2 omrekenen in m2 of in cm2."
3840 PRINT:PRINT
3850 W=3:GOSUB10000
3860 PRINT"Met drie voorbeelden laten we je zien":PRINT
3870 PRINT"hoe je dan te werk moet gaan.":PRINT
3880 W=2:GOSUB10000
3890 PRINT"Neem de vragen en de oplossingen over."
3900 W=1:GOSUB10000
3910 RETURN
3920 REM
4000 REM ******> VOORBEELD 1 <******
4010 GOSUB100
4020 PRINT"Eerst voorbeeld.":PRINT:PRINT
4030 PRINT"Gegeven   :Het oppervlak van een tafel"
4040 PRINT"           is 96 dm2.":PRINT
4050 PRINT"Gevraagd  :Leg uit hoeveel cm2 dit is.":PRINT
4060 PRINT:PRINT"Neem over.":PRINT:PRINT
4070 W=5:GOSUB10000
4080 PRINT"De 'oplossing' of de 'uitleg' dient er  "
4090 PRINT"als volgt uit te zien."
4100 W=2:GOSUB10000
4110 GOSUB100
4120 PRINT"Oplossing :1 dm2 = 1 dm * 1 dm":PRINT
4130 PRINT"           1 dm2 = 10 cm * 10 cm":PRINT
4140 PRINT"           1 dm2 = 100 cm2":PRINT
4150 PRINT"           96 dm2 = 96 * 100 cm2":PRINT
4160 PRINT"           96 dm2 = 9600 cm2.":PRINT:PRINT
4170 W=3:GOSUB10000
4180 PRINT"Natuurlijk wist je dat al lang.":PRINT
4190 PRINT"Maar staat er: 'bereken' of 'leg uit'":PRINT
4200 PRINT"of 'laat zien' dan moet je laten zien":PRINT
4210 PRINT"hoe je aan het antwoord komt.":PRINT
4220 W=3:GOSUB10000
4230 PRINT"Neem de oplossing over."
4240 W=5:GOSUB10000
4250 RETURN
4260 REM
4400 REM *******> VOORBEELD 2 <*******
4410 GOSUB100
4420 PRINT"Tweede voorbeeld.":PRINT:PRINT
4430 PRINT"Gegeven   :Het oppervlak van een vel"
4440 PRINT"           teken papier is 40 dm2":PRINT
4450 PRINT"Gevraagd  :Laat zien hoeveel m2 dit is."
4460 PRINT:PRINT
4470 W=3:GOSUB10000
4480 PRINT"Oplossing :1 dm2 = 1 dm * 1 dm":PRINT
4490 PRINT"           1 dm2 = 0.1 m * 0.1 m":PRINT
4500 PRINT"           1 dm2 = 0.01 m2":PRINT
4510 PRINT"           40 dm2 = 40 * 0.01 m2":PRINT
4520 PRINT"           40 dm2 = 0.40 m2.":PRINT:PRINT
4530 PRINT"Neem over."
4540 W=5:GOSUB10000
4550 RETURN
4560 REM
4800 REM *******> VOORBEELD 3 <*******
4810 GOSUB100
4820 PRINT"Derde voorbeeld.":PRINT:PRINT
4830 PRINT"Gevraagd  :Reken 0.25 m2 om in cm2.":PRINT:PRINT
4840 W=2:GOSUB10000
4850 PRINT"Oplossing :1 m2 = 1 m * 1 m":PRINT
4860 PRINT"           1 m2 = 100 cm * 100 cm":PRINT
4870 PRINT"           1 m2 = 10000 cm2":PRINT
4880 PRINT"           0.25 m2 = 0.25 * 10000 cm2":PRINT
4890 PRINT"           0.25 m2 = 2500 cm2.":PRINT:PRINT
4900 PRINT"Neem over."
4910 W=4:GOSUB10000
4920 RETURN
4930 REM
5200 REM ******> DRIE OPGAVEN <*****
5210 GOSUB100
5220 PRINT"Je krijgt nog drie opgaven.":PRINT
5230 PRINT"Neem ze eerst over en ga ze daarna":PRINT
5240 PRINT"oplossen.":PRINT:PRINT
5250 W=2:GOSUB10000
5260 PRINT"1 Gevraagd :Reken 248 cm2 om in dm2.":PRINT
5270 PRINT"2 Gevraagd :Reken 3.5 m2 om in dm2.":PRINT
5280 PRINT"3 Gevraagd :Reken 5600 cm2 om in m2.":PRINT:PRINT
5290 W=6:GOSUB10000
5300 PRINT"Eerst oplossen voor je verder gaat.":PRINT:PRINT
5310 W=8:GOSUB10000
5320 PRINT"Laat de oplossingen aan de leerkracht":PRINT
5330 PRINT"zien.":PRINT
5340 W=2:GOSUB10000
5350 RETURN
5360 REM
5500 REM ****> ANTWOORDEN <*****
5510 GOSUB100
5520 PRINT"Tot slot geven we nog de antwoorden van":PRINT
5530 PRINT"de drie opgaven.":PRINT:PRINT
5540 W=2:GOSUB10000
5550 PRINT"Opgave 1 :248 cm2 = 2.48 dm2.":PRINT
5560 PRINT"Opgave 2 :3.5 m2 = 350 dm2.":PRINT
5570 PRINT"Opgave 3 :5600 cm2 = 0.56 m2.":PRINT:PRINT
5580 PRINT"Kijk of je antwoorden goed zijn.":PRINT:PRINT
5590 W=4:GOSUB10000
5600 PRINT"Als je foute antwoorden hebt, ga dan na":PRINT
5610 PRINT"wat je nog niet goed weet en doe daar":PRINT
5620 PRINT"wat aan."
5630 W=2:GOSUB10000
5640 GOSUB100
5650 RETURN
5660 REM
5800 REM *******> EINDE LES <*******
5810 GOSUB100
5820 PRINT"De volgende les gaat over volume.":PRINT
5830 PRINT"Als je nog niet weet wat dat is, zoek":PRINT
5840 PRINT"het dan op in het natuurkunde boek."
5850 W=3:GOSUB10000
5860 PRINT:PRINT:PRINT
5870 PRINT"  ******> Tot de volgende keer <******"
5880 RETURN
5890 REM
10000 REM *****> SPATIEBALK <*****
10010 FORW1=1 TOW*10:NEXTW1
10020 GOSUB120:VI=VE
10030 VE=23:HO=0:GOSUB110
10040 PRINT"   ****> Druk op de spatiebalk <**** ";
10050 GOSUB20000
10060 GOSUB200:IFIN$="" THEN10060
10065 :IFIN$<>" " THEN10060
10070 VE=23:HO=0:GOSUB110
10080 PRINT"                                      ";
10090 VE=VI:GOSUB110
10100 RETURN
10200 REM *****> RUIMTE MAKEN <*****
10210 REM
10220 FORI=0 TOY2-Y1
10230 HO=0:VE=Y1+I:GOSUB110
10240 PRINTA$;
10250 NEXTI
10260 VE=Y1:GOSUB110
10270 RETURN
10400 REM *****> INVOER ANTWOORD <****
10410 PRINT"Voer het getal in :";
10420 AW$="":I=0:GOSUB120:HI=HO
10430 GOSUB210
10440 IFIN$=CHR$(44) THENIN$="."
10450 IFIN$=VB$ THEN10490
10460 IFIN$=CHR$(13) THENAW=VAL(AW$):GOTO10530
10470 HO=HI+I:GOSUB110:PRINTIN$;:I=I+1
10480 AW$=AW$+IN$:GOTO10430
10490 IFI=0 THEN10430
10500 L=LEN(AW$):AW$=LEFT$(AW$,(L-1)):I=I-1
10510 HO=HI+I:GOSUB110:PRINT" ";
10520 GOTO10430
10530 RETURN
10540 REM
10600 REM ***> HET GOEDE ANTWOORD <***
10610 GOSUB100
10620 PRINT"Dat is het goede antwoord.":PRINT
10630 PRINT"Je hebt dus ook staan :":PRINT
10640 RETURN
10700 REM *****> FOUT ANTWOORD <*****
10705 PRINT
10710 PRINT"Je antwoord is niet juist.":PRINT
10720 PRINT"Reken het nog eens na.":PRINT
10730 W=2:GOSUB10000
10740 Y1=VA:Y2=20
10745 A$="                                       "
10750 GOSUB10200
10760 HO=HA:VE=VA:GOSUB110
10770 RETURN
10780 REM
10800 REM **> REAKTIE GOED ANTWOORD <**
10810 IFFA=1 THEN10855
10815 IFGA=1 THENGA$="Dit is het eerste goede antwoord."
10820 IFGA=2 THENGA$="Dit is het tweede goede antwoord."
10825 IFGA=3 THENGA$="Dit is het derde goede antwoord."
10830 IFGA=4 THENGA$="Dit is het vierde goede antwoord."
10835 IFGA=5 THENGA$="Dit is het vijfde goede antwoord."
10840 IFGA=6 THENGA$="Dit is het zesde goede antwoord."
10845 IFGA=7 THENGA$="Al je antwoorden zijn goed."
10850 PRINTGA$:GOTO10865
10855 GA$="Dit antwoord is goed."
10860 PRINTGA$
10865 RETURN
10870 REM
20000 REM TOETSENBORD BUFFER LEEG
20010 REM ******> C-64 <*******
20020 REM POKE 198,0
20030 REM *****> P-2000T <*****
20040 REM POKE &H600C,0
20050 REM *****> IBM PC <******
20060 REM WHILE INKEY$<>"":WEND
20070 RETURN
20080 REM
20100 REM INSTELLEN MONOCHROOM MONITOR
20110 REM ******> C-64 <*******
20120 REM POKE 53280,0:POKE 53281,0
20130 REM *****> IBM PC <******
20140 REM COLOR 7,0,0
20150 REM KLEINE LETTERMODE C-64
20160 REM POKE 53272,23
20170 RETURN
20180 REM
30000 REM******************************
30010 REM*                            *
30020 REM*       OPPERVLAK 2          *
30030 REM*                            *
30040 REM* INGEZONDEN DOOR: J.WIELES  *
30050 REM* HOOFDWAL 68. 01830-30492   *
30060 REM* 4207 EE GORINCHEM          *
30070 REM*                            *
30080 REM* GEMAAKT OP EEN C-64        *
30090 REM* UITGEPROBEERD OP EEN HZN PC*
30100 REM* IKLL 1002 -IBM KLOON-      *
30110 REM*                            *
30120 REM* TOETSENBORD BUFFERS KUNNEN *
30130 REM* WORDEN LEEG GEMAAKT DOOR DE*
30140 REM* REM WEG TE HALEN BIJ REGEL *
30150 REM* 20020 VOOR C-64            *
30160 REM* 20040 VOOR P2000T          *
30170 REM* 20060 VOOR IBM PC          *
30180 REM*                            *
30190 REM* MONOCHROME MONITOR INSTEL- *
30200 REM* LEN DOOR REM WEG TE HALEN  *
30210 REM* BIJ REGEL                  *
30220 REM* 20120 VOOR C-64            *
30230 REM* 20150 VOOR IBM PC          *
30240 REM*                            *
30250 REM* KLEINE LETTERMODE VOOR     *
30260 REM* C-64 DOOR REM WEG TE HALEN *
30270 REM* BIJ REGEL 20160            *
30280 REM*                            *
30290 REM* LEESTIJD IN TE STELLEN DOOR*
30300 REM* FACTOR 10 IN REGEL 10100 TE*
30310 REM* VERANDEREN.                *
30320 REM*                            *
30330 REM* MET DANK AAN DE TWEEDE KLAS*
30340 REM* LEERLINGEN VAN DE OPENBARE *
30350 REM* SCHOOL VOOR MAVO TE        *
30360 REM* GORINCHEM.                 *
30370 REM*                            *
30380 REM******************************
30390 REM
30400 REM COPYRIGHT NOS HILVERSUM

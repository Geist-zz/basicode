1000 A=600:GOTO20
1010 GOSUB100:E1=1E-09:E2=.000001:E3=.001
1020 E4=1:E5=1000!:E6=1000000!:E7=1E+09:E8=1E+12
1025 NL=LOG(EXP(1)):IFABS(1-NL)<E2 THENNL=1
1030 HO=11:VE=2:GOSUB110:PRINT"N.O.S.HOBBYSCOOP":HO=8
1040 VE=6:GOSUB110:PRINT"+++++++++++++++++++++"
1050 VE=7:GOSUB110:PRINT"+ V A D E M E C U M +"
1060 VE=8:GOSUB110:PRINT"+++++++++++++++++++++"
1070 HO=1:VE=15:GOSUB110
1080 PRINT"EEN VERZAMELING NUTTIGE BEREKENINGEN"
1090 HO=7:VE=23:GOSUB110
1100 PRINT" * * DRUK EEN TOETS * *";:GOSUB210
1110 GOSUB100:PRINT"        * * *  M E N U  * * *":PRINT
1120 PRINT"1...Reactantie-Resonantie van'L'en'C'":PRINT
1130 PRINT"2...Netwerken met weerstanden":PRINT
1140 PRINT"3...Waarde van Condensatoren in serie":PRINT
1150 PRINT"4...Waarde van Weerstanden parallel":PRINT
1160 PRINT"5...Berekeningen volgens de wet van Ohm":PRINT
1170 PRINT"6...Machtsverheffen en worteltrekken":PRINT
1180 PRINT"7...Omrekeningen dB.en versterking":PRINT
1190 PRINT"8...Natuurlijk-en Log(10)-Logarithme":PRINT
1200 PRINT"9...Gebruiksaanwijzing":PRINT
1210 PRINT"0...Ophouden. (Terug naar Menu:'ENTER')"
1220 PRINT:PRINT"UW KEUZE ?... NR.INTYPEN";:GOSUB210
1230 M=VAL(IN$):IF(IN$<"0") OR(IN$>"9") THEN1110
1240 IF(M=0) THENGOSUB100:END
1250 ONM GOTO2150,2630,3300,3760,4220,4770,5000,5180,5470
1260 REM SORTEER DE EXPRESSIE
1270 IF(X<E1) THENY$="picoFARAD":A$="picoAMP."
1280 IF(X<E1) THENW$="picoWATT":X=X*E8:RETURN
1290 IF(X>=E1) AND(X<E2) THENY$="nanoFARAD"
1300 IF(X>=E1) AND(X<E2) THENA$="nanoAMP."
1310 IF(X>=E1) AND(X<E2) THENZ$="nanoHENRY"
1320 IF(X>=E1) AND(X<E2) THENW$="nanoWATT"
1330 IF(X>=E1) AND(X<E2) THENX=X*E7:RETURN
1340 IF(X>=E2) AND(X<E3) THENY$="microFARAD"
1350 IF(X>=E2) AND(X<E3) THENA$="micr0AMP."
1360 IF(X>=E2) AND(X<E3) THENZ$="microHENRY"
1370 IF(X>=E2) AND(X<E3) THENV$="microVOLT"
1380 IF(X>=E2) AND(X<E3) THENW$="microWATT"
1390 IF(X>=E2) AND(X<E3) THENX=X*E6:RETURN
1400 IF(X>=E3) AND(X<E4) THENY$="milliFARAD"
1410 IF(X>=E3) AND(X<E4) THENA$="milliAMP."
1420 IF(X>=E3) AND(X<E4) THENZ$="milliHENRY"
1430 IF(X>=E3) AND(X<E4) THENV$="milliVOLT"
1440 IF(X>=E3) AND(X<E4) THENX$="milliOHM"
1450 IF(X>=E3) AND(X<E4) THENW$="milliWATT"
1460 IF(X>=E3) AND(X<E4) THENX=X*E5:RETURN
1470 IF(X>=E4) AND(X<E5) THENZ$="HENRY":H$="HERZ"
1480 IF(X>=E4) AND(X<E5) THENV$="VOLT":W$="WATT"
1490 IF(X>=E4) AND(X<E5) THENA$="AMPERE":X$="OHM"
1500 IF(X>=E4) AND(X<E5) THENX=X:RETURN
1510 IF(X>=E5) AND(X<E6) THENX$="kiloOHM"
1520 IF(X>=E5) AND(X<E6) THENH$="kiloHERZ"
1530 IF(X>=E5) AND(X<E6) THENA$="kiloAMP."
1540 IF(X>=E5) AND(X<E6) THENV$="kiloVOLT"
1550 IF(X>=E5) AND(X<E6) THENW$="kiloWATT"
1560 IF(X>=E5) AND(X<E6) THENX=X/E5:RETURN
1570 IF(X>=E6) THENX$="meg.OHM":H$="megaHERZ"
1580 IF(X>=E6) THENW$="megaWATT":X=X/E6:RETURN
1590 REM HERLEIDING AMPERE
1600 IF(I$="A") OR(I$="a") THENI=I:V=1
1610 IF(I$="MA") OR(I$="ma") THENI=I/E5:V=1
1620 IF(I$="UA") OR(I$="ua") THENI=I/E6:V=1
1630 RETURN
1640 REM HERLEIDING VOLT
1650 IF(E$="V") OR(E$="v") THENE=E:V=1
1660 IF(E$="MV") OR(E$="mv") THENE=E/E5:V=1
1670 IF(E$="UV") OR(E$="uv") THENE=E/E6:V=1
1680 RETURN
1690 REM HERLEIDING HENRY
1700 IF(L$="H") OR(L$="h") THENL=L:V=1
1710 IF(L$="MH") OR(L$="mh") THENL=L/E5:V=1
1720 IF(L$="UH") OR(L$="uh") THENL=L/E6:V=1
1730 RETURN
1740 REM HERLEIDING FARAD
1750 IF(C$="UF") OR(C$="uf") THENC=C/E6:V=1
1760 IF(C$="NF") OR(C$="nf") THENC=C/E7:V=1
1770 IF(C$="PF") OR(C$="pf") THENC=C/E8:V=1
1780 RETURN
1790 REM HERLEIDING OHM
1800 IF(R$="O") OR(R$="o") THENR=R:V=1
1810 IF(R$="KO") OR(R$="ko") THENR=R*E5:V=1
1820 IF(R$="MO") OR(R$="mo") THENR=R*E6:V=1
1830 RETURN
1840 REM HERLEIDING HERZ
1850 IF(F$="H") OR(F$="h") THENF=F:V=1
1860 IF(F$="KH") OR(F$="kh") THENF=F*E5:V=1
1870 IF(F$="MH") OR(F$="mh") THENF=F*E6:V=1
1880 RETURN
1890 PRINT"* FOUTIEF UITGEDRUKT *":RETURN
1900 PRINT:PRINT"WAT WILT U BEREKENEN ? Nr.INTYPEN ";
1910 GOSUB210:K=VAL(IN$):RETURN
1920 HO=0:VE=23:GOSUB110
1930 PRINT"WILT U NOG WAT WETEN ? (J/N)";:GOSUB210
1940 IF(IN$="J") OR(IN$="j") THEN1250
1950 IF(IN$="N") OR(IN$="n") THENM=0:GOTO1110
1960 GOTO1920
1970 PRINT"De waarde van 'L'....... ";:INPUTL
1980 IF(L=0) THEN1970
1990 PRINT"Uitgedrukt in'H''MH''UH' ";:INPUTL$
2000 V=0:GOSUB1690
2010 IF(V=0) THENGOSUB1890:GOTO1990
2020 RETURN
2030 PRINT"De frequentie 'F'....... ";:INPUTF
2040 IF(F=0) THEN2030
2050 PRINT"Uitgedrukt in'H''KH''MH' ";:INPUTF$
2060 V=0:GOSUB1840
2070 IF(V=0) THENGOSUB1890:GOTO2050
2080 RETURN
2090 PRINT"De waarde van 'C'....... ";:INPUTC
2100 IF(C=0) THEN2090
2110 PRINT"Uitgedrukt in'UF''NF''PF'";:INPUTC$
2120 V=0:GOSUB1740
2130 IF(V=0) THENGOSUB1890:GOTO2110
2140 RETURN
2150 GOSUB100:K=0:L=0:C=0:F=0:L$=""
2160 C$="":F$="":PY=3.14159
2170 PRINT"REACTANTIE + RESONANTIE 'L'en'C'"
2180 FORN=1 TO32:PRINT"-";:NEXTN:PRINT
2190 PRINT"1..Reactantie 'L'"
2200 PRINT"2..Reactantie 'C'"
2210 PRINT"3..Reactantie 'L'en'C'in serie"
2220 PRINT"4..Reactantie 'L'en'C'parallel"
2230 PRINT"5..Resonantiefrequentie 'L'en'C'"
2240 PRINT"6..Resonantiekring,waarde'C'?"
2250 PRINT"7..Resonantiekring,waarde'L'?"
2260 GOSUB1900:IF(K=0) THEN1110
2270 IF(K<1) OR(K>7) THEN2150
2280 PRINT"*";K:PRINT
2290 ONK GOTO2300,2340,2380,2380,2480,2530,2580
2300 GOSUB1970:GOSUB2030
2310 Z=2*PY*F*L:X=Z:GOSUB1260:Z=INT(X*100)/100:PRINT
2320 SR=Z:GOSUB300:PRINT"DE REACTANTIE = ";SR$;" ";X$
2330 M=1:GOTO1920
2340 GOSUB2090:GOSUB2030
2350 Z=1/(2*PY*F*C):X=Z:GOSUB1260:Z=INT(X*100)/100:PRINT
2360 SR=Z:GOSUB300:PRINT"DE REACTANTIE = ";SR$;" ";X$
2370 M=1:GOTO1920
2380 GOSUB1970:GOSUB2090:GOSUB2030
2390 IF(K=3) THENZ=2*PY*F*L-(1/(2*PY*F*C))
2400 IF(K=4) THENZ=(2*PY*F*L)/(1-(2*PY*F)^2*L*C)
2410 S$="IND."
2420 IF(Z<0) THENS$="CAP.":Z=Z*-1
2430 X=Z:GOSUB1260:Z=INT(X*100)/100:PRINT
2440 FF=1/(2*PY*SQR(L*C))
2450 IF(FF>=F*98/100) AND(FF<=F*102/100) THENS$="RES."
2460 SR=Z:GOSUB300:PRINT"REACTANTIE = ";SR$;" ";X$;" ";S$
2470 M=1:GOTO1920
2480 GOSUB1970:GOSUB2090
2490 F=1/(2*PY*SQR(L*C)):X=F:GOSUB1260
2500 F=INT(X*1000)/1000:PRINT
2510 SR=F:GOSUB300:PRINT"RES.FREQUENTIE = ";SR$;" ";H$
2520 M=1:GOTO1920
2530 GOSUB2030:GOSUB1970
2540 C=1/(39.478355#*F^2*L)
2550 X=C:GOSUB1260:C=INT(X*10)/10:PRINT
2560 SR=C:GOSUB300:PRINT"CONDENSATOR = ";SR$;" ";Y$
2570 M=1:GOTO1920
2580 GOSUB2030:GOSUB2090
2590 L=1/(39.478355#*F^2*C)
2600 X=L:GOSUB1260:L=INT(X*10)/10:PRINT
2610 SR=L:GOSUB300:PRINT"ZELFINDUCTIE = ";SR$;" ";Z$
2620 M=1:GOTO1920
2630 GOSUB100:Z1=0:Z2=0:D=0:E=0
2640 PRINT"BEREKENINGEN VAN PASSIEVE NETWERKEN"
2650 FORN=1 TO35:PRINT"-";:NEXTN:PRINT:PRINT
2660 PRINT"1.Asymmetrisch T-netwerk"
2670 PRINT"2.Symmetrisch  H-netwerk"
2680 PRINT"3.Asymmetrisch PI-netwerk"
2690 PRINT"4.Symmetrisch  PI-netwerk"
2700 GOSUB1900
2710 IF(K=0) THEN1110
2720 IF(K<1) OR(K>4) THEN2630
2730 ONK GOSUB3040,3100,3160,3220
2740 PRINT:PRINT
2750 PRINT"Ingangsweerstand Z1 (Ohm)= ";:INPUTZ1:PRINT
2760 IF(Z1<=0) THENPRINT"Z1 > 0 svp.":GOTO2750
2770 PRINT"Uitgangsweerstand Z2 (Ohm)= ";:INPUTZ2:PRINT
2780 IF(Z2<=0) THENPRINT"Z1 > 0 svp.":GOTO2770
2790 V=Z1/Z2:IF(V<1) THENV=1/V
2800 E=4.3429448#*(LOG((SQR(V)+SQR(V-1))^2))/NL
2810 EE=INT(E*100)/100
2820 PRINT"Minimum demping = ";EE;" dB"
2830 PRINT:PRINT"Gewenste demping in dB ";:INPUTD:PRINT
2840 IF(D<=EE) THENPRINT"DEMPING >";EE;" dB.svp":GOTO2830
2850 Q=10^(D/10)+1:M=10^(D/10)-1:S=2*(10^(D/20))
2860 IF(K=1) OR(K=2) THEN2910
2870 R3=M/S*SQR(Z1*Z2):L=Q/M/Z1-(1/R3)
2880 R1=1/L:W=Q/M/Z2-(1/R3):R2=1/W
2890 IF(V<1) THENR1=1/W:R2=1/L
2900 GOTO2950
2910 R3=S/M*SQR(Z1*Z2)
2920 R1=Z1*Q/M-R3
2930 R2=Z2*Q/M-R3
2940 IF(V<1) THENR1=Z2*Q/M-R3:R2=Z1*Q/M-R3
2950 GOSUB100
2960 ONK GOSUB3040,3100,3160,3220
2970 SR=Z1:GOSUB300:PRINT"Z1 = ";SR$;" Ohm"
2980 SR=Z2:GOSUB300:PRINT"Z2 = ";SR$;" Ohm"
2990 SR=INT(R1*100)/100:GOSUB300:PRINT"R1 = ";SR$;" Ohm"
3000 SR=INT(R2*100)/100:GOSUB300:PRINT"R2 = ";SR$;" Ohm"
3010 SR=INT(R3*100)/100:GOSUB300:PRINT"R3 = ";SR$;" Ohm"
3020 SR=D:GOSUB300:PRINT"Demping = ";SR$;" dB"
3030 M=2:GOTO1920
3040 GOSUB100
3050 PRINT"0--R1--+--R2--0"
3060 PRINT;TAB(7);"!"
3070 PRINT"Z1";TAB(6);"R3";TAB(14);"Z2";TAB(20);"T-NETWERK"
3080 PRINT;TAB(7);"!";TAB(20);"========="
3090 PRINT"0------+------0":PRINT:RETURN
3100 GOSUB100
3110 PRINT"0--R1a---+---R2a--0"
3120 PRINT;TAB(9);"!"
3130 PRINT"Z1";TAB(9);"R3";TAB(18);"Z2";TAB(23);"H-NETWERK"
3140 PRINT;TAB(9);"!";TAB(23);"========="
3150 PRINT"0--R1b---+---R2b--0":PRINT:RETURN
3160 GOSUB100
3170 PRINT"0---+---R3---+---0"
3180 PRINT"    !        !"
3190 PRINT"Z1  R1      R2  Z2";TAB(23);"PI-NETWERK"
3200 PRINT"    !        !";TAB(23);"=========="
3210 PRINT"0---+--------+---0":PRINT:RETURN
3220 GOSUB100
3230 PRINT"0---+---R3a---+---0"
3240 PRINT"    !         !"
3250 PRINT"Z1  R1       R2  Z2";
3260 PRINT;TAB(23);"PI-DUBBELNETWERK"
3270 PRINT"    !         !";
3280 PRINT;TAB(23);"================"
3290 PRINT"0---+---R3b---+---0":PRINT:RETURN
3300 GOSUB100:I=0:C=0:C$=""
3310 PRINT"SERIE SCHAKELING VAN CONDENSATOREN"
3320 FORN=1 TO36:PRINT"-";:NEXTN:PRINT
3330 PRINT"1.Waarde van een serie schakeling"
3340 PRINT"2.Waarde van de serie condensator"
3350 GOSUB1900:IF(K=0) THEN1110
3360 IF(K<1) OR(K>2) THEN3300
3370 PRINT"*";K:PRINT:ONK GOTO3380,3590
3380 PRINT"Aantal condensatoren 2-3-4-5  ";:INPUTI
3390 IF(I<2) OR(I>5) THEN3380
3400 PRINT:FORN=1 TOI
3410 PRINT"De waarde van C ";N;"      ";:INPUTC
3420 IF(C=0) THEN3410
3430 PRINT"Uitgedrukt in'UF''NF''PF'";:INPUTC$
3440 V=0:GOSUB1740
3450 IF(V=0) THENGOSUB1890:GOTO3430
3460 IF(N=1) THENC1=C:C=0
3470 IF(N=2) THENC2=C:C=0
3480 IF(N=2) AND(I=2) THENS=1/(1/C1+1/C2)
3490 IF(N=3) THENC3=C:C=0
3500 IF(N=3) AND(I=3) THENS=1/(1/C1+1/C2+1/C3)
3510 IF(N=4) THENC4=C:C=0
3520 IF(N=4) AND(I=4) THENS=1/(1/C1+1/C2+1/C3+1/C4)
3530 IF(N=5) THENC5=C
3540 IF(N=5) AND(I=5) THENS=1/(1/C1+1/C2+1/C3+1/C4+1/C5)
3550 NEXTN
3560 X=S:GOSUB1260:S=INT(X*100.001)/100:PRINT
3570 SR=S:GOSUB300:PRINT"SERIE WAARDE = ";SR$;" "Y$
3580 M=3:GOTO1920
3590 PRINT"Waarde voorhandene condensator ";:INPUTC
3600 IF(C=0) THEN3590
3610 PRINT"Uitgedrukt in'UF''NF''PF'..... ";:INPUTC$
3620 V=0:GOSUB1740:C1=C
3630 IF(V=0) THENGOSUB1890:GOTO3610
3640 PRINT"Gewenste waarde in serie...... ";:INPUTC
3650 IF(C=0) THEN3640
3660 X=C1:GOSUB1260:C3=X
3670 PRINT"Uitgedrukt in'UF''NF''PF'..... ";:INPUTC$
3680 V=0:GOSUB1740:C2=C
3690 IF(V=0) THENGOSUB1890:GOTO3670
3700 IF(C2>=C1) THENPRINT"SVP.KLEINER ALS ";C3;" ";Y$
3710 IF(C2>=C1) THEN3640
3720 S=1/(1/C1-1/C2)
3730 X=S*-1:GOSUB1260:S=INT(X*100.001)/100:PRINT
3740 SR=S:GOSUB300:PRINT"SERIE CONDENSATOR = ";SR$;" ";Y$
3750 M=3:GOTO1920
3760 GOSUB100:I=0:R=0:R$=""
3770 PRINT"PARALLEL SCHAKELING VAN WEERSTANDEN"
3780 FORN=1 TO35:PRINT"-";:NEXTN:PRINT
3790 PRINT"1.Waarde van parallelschakelingen"
3800 PRINT"2.Waarde van de parallelweerstand"
3810 GOSUB1900:IF(K=0) THEN1110
3820 IF(K<1) OR(K>2) THEN3760
3830 PRINT"*";K:PRINT:ONK GOTO3840,4050
3840 PRINT"Aantal weerstanden 2-3-4-5  ";:INPUTI
3850 IF(I<2) OR(I>5) THEN3840
3860 PRINT:FORN=1 TOI
3870 PRINT"De waarde van R ";N;"     ";:INPUTR
3880 IF(R=0) THEN3870
3890 PRINT"Uitgedrukt in'O''KO''MO'";:INPUTR$
3900 V=0:GOSUB1790
3910 IF(V=0) THENGOSUB1890:GOTO3890
3920 IF(N=1) THENR1=R:R=0
3930 IF(N=2) THENR2=R:R=0
3940 IF(N=2) AND(I=2) THENP=1/(1/R1+1/R2)
3950 IF(N=3) THENR3=R:R=0
3960 IF(N=3) AND(I=3) THENP=1/(1/R1+1/R2+1/R3)
3970 IF(N=4) THENR4=R:R=0
3980 IF(N=4) AND(I=4) THENP=1/(1/R1+1/R2+1/R3+1/R4)
3990 IF(N=5) THENR5=R
4000 IF(N=5) AND(I=5) THENP=1/(1/R1+1/R2+1/R3+1/R4+1/R5)
4010 NEXTN
4020 X=P:GOSUB1260:P=INT(X*100.001)/100:PRINT
4030 SR=P:GOSUB300:PRINT"PARALLEL WAARDE = ";SR$;" ";X$
4040 M=4:GOTO1920
4050 PRINT"Waarde voorhandene weerstand ";:INPUTR
4060 IF(R=0) THEN4050
4070 PRINT"Uitgedrukt in'O''KO''MO'.... ";:INPUTR$
4080 V=0:GOSUB1790:R1=R
4090 IF(V=0) THENGOSUB1890:GOTO4070
4100 PRINT"Gewenste parallel waarde.... ";:INPUTR
4110 IF(R=0) THEN4100
4120 X=R1:GOSUB1260:R3=X
4130 PRINT"Uitgedrukt in'O''KO''MO'.... ";:INPUTR$
4140 V=0:GOSUB1790:R2=R
4150 IF(V=0) THENGOSUB1890:GOTO4130
4160 IF(R2>=R1) THENPRINT"SVP.KLEINER ALS ";R3;" ";X$
4170 IF(R2>=R1) THEN4100
4180 P=1/(1/R1-1/R2)
4190 X=P*-1:GOSUB1260:P=INT(X*100.001)/100:PRINT
4200 SR=P:GOSUB300:PRINT"PARALLEL WEERSTAND = ";SR$;" ";X$
4210 M=4:GOTO1920
4220 GOSUB100:K=0:I=0:E=0:R=0:I$="":E$="":R$=""
4230 PRINT"WET VAN OHM"
4240 PRINT"-----------":PRINT
4250 PRINT"1.Spanning over de weerstand + Vermogen"
4260 PRINT"2.Stroom door de weerstand.. + Vermogen"
4270 PRINT"3.De waarde van de weerstand + Vermogen"
4280 GOSUB1900:IF(K=0) THEN1110
4290 IF(K<1) OR(K>3) THEN4220
4300 PRINT"*";K:PRINT
4310 ONK GOTO4320,4470,4620
4320 PRINT"Stroom door weerstand...";:INPUTI
4330 IF(I=0) THEN4320
4340 PRINT"Uitgedrukt in'A''MA''UA'";:INPUTI$
4350 V=0:GOSUB1590
4360 IF(V=0) THENGOSUB1890:GOTO4340
4370 PRINT"Waarde van de weerstand ";:INPUTR
4380 IF(R=0) THEN4370
4390 PRINT"Uitgedrukt in'O''KO''MO'";:INPUTR$
4400 V=0:GOSUB1790
4410 IF(V=0) THENGOSUB1890:GOTO4390
4420 X=I*R:GOSUB1260:E=INT(X*100)/100:PRINT
4430 SR=E:GOSUB300:PRINT"DE SPANNING  E = ";SR$;" ";V$
4440 X=I^2*R:GOSUB1260:P=INT(X*100.001)/100:PRINT
4450 SR=P:GOSUB300:PRINT"HET VERMOGEN P = ";SR$;" ";W$
4460 M=5:GOTO1920
4470 PRINT"Spanning over weerstand ";:INPUTE
4480 IF(E=0) THEN4470
4490 PRINT"Uitgedrukt in'V''MV''UV'";:INPUTE$
4500 V=0:GOSUB1640
4510 IF(V=0) THENGOSUB1890:GOTO4490
4520 PRINT"De waarde van weerstand ";:INPUTR
4530 IF(R=0) THEN4520
4540 PRINT"Uitgedrukt in'O''KO''MO'";:INPUTR$
4550 V=0:GOSUB1790
4560 IF(V=0) THENGOSUB1890:GOTO4540
4570 X=E/R:GOSUB1260:I=INT(X*100.001)/100:PRINT
4580 SR=I:GOSUB300:PRINT"DE STROOM... I = ";SR$;" ";A$
4590 X=E^2/R:GOSUB1260:P=INT(X*100.001)/100:PRINT
4600 SR=P:GOSUB300:PRINT"HET VERMOGEN P = ";SR$;" ";W$
4610 M=5:GOTO1920
4620 PRINT"Spanning over weerstand ";:INPUTE
4630 IF(E=0) THEN4620
4640 PRINT"Uitgedrukt in'V''MV''UV'";:INPUTE$
4650 V=0:GOSUB1640
4660 IF(V=0) THENGOSUB1890:GOTO4640
4670 PRINT"Stroom door weerstand...";:INPUTI
4680 IF(I=0) THEN4670
4690 PRINT"Uitgedrukt in'A''MA''UA'";:INPUTI$
4700 V=0:GOSUB1590
4710 IF(V=0) THENGOSUB1890:GOTO4690
4720 R=E/I:X=R:GOSUB1260:R=INT(X*100)/100:PRINT
4730 SR=R:GOSUB300:PRINT"DE WEERSTAND R = ";SR$;" ";X$
4740 X=E*I:GOSUB1260:P=INT(X*100.001)/100:PRINT
4750 SR=P:GOSUB300:PRINT"HET VERMOGEN P = ";SR$;" ";W$
4760 M=5:GOTO1920
4770 GOSUB100:K=0:A=0:B=0
4780 PRINT"WORTELTREKKEN EN MACHTSVERHEFFEN"
4790 FORN=1 TO32:PRINT"-";:NEXTN:PRINT
4800 PRINT"1..Worteltrekken"
4810 PRINT"2..Machtsverheffen"
4820 GOSUB1900:IF(K=0) THEN1110
4830 IF(K<1) OR(K>2) THEN4770
4840 PRINT"*";K:PRINT
4850 ONK GOTO4860,4930
4860 PRINT"Welk getal kiest U.......";:INPUTA
4870 IF(A=0) THEN4860
4880 PRINT"Welke machts wortel......";:INPUTB
4890 IF(B=0) THEN4880
4900 C=INT(A^(1/B)*100)/100:PRINT
4910 PRINT"DE ";B;" e MACHTSWORTEL UIT ";A;" =";C
4920 M=6:GOTO1920
4930 PRINT"Welk getal kiest U.......";:INPUTA
4940 IF(A=0) THEN4930
4950 PRINT"Tot welke macht..........";:INPUTB
4960 IF(B=0) THEN4950
4970 C=INT(A^B*100)/100:PRINT
4980 PRINT"DE ";B;" e MACHT VAN ";A;" =";C
4990 M=6:GOTO1920
5000 GOSUB100:K=0:D=0:V=0
5010 PRINT"OMREKENING'DB'-VERSTERKINGSFACTOR"
5020 FORN=1 TO33:PRINT"-";:NEXTN:PRINT
5030 PRINT"1.Van'dB'naar versterkingsfactor"
5040 PRINT"2.Van versterkingsfactor naar'dB'"
5050 GOSUB1900:IF(K=0) THEN1110
5060 IF(K<1) OR(K>2) THEN5000
5070 PRINT"*";K:PRINT
5080 ONK GOTO5090,5130
5090 PRINT"Wat is de waarde in dB ";:INPUTD
5100 V=INT(10^(D/20)*1000)/1000:PRINT
5110 PRINT"DE VERSTERKINGSFACTOR V = ";V
5120 M=7:GOTO1920
5130 PRINT"De versterkingsfactor = ";:INPUTV
5140 IF(V<=0) THENPRINT"NIET TOEGESTAAN":GOTO5130
5150 D=INT(2000.001*LOG(V)/LOG(10))/100:PRINT
5160 PRINT"DE VERSTERKINGSFACTOR ";V;"= ";D;" dB."
5170 M=7:GOTO1920
5180 GOSUB100:K=0:X=0
5190 PRINT"NATUURLIJK-EN LOG(10)-LOGARITME"
5200 FORN=1 TO32:PRINT"-";:NEXTN:PRINT
5210 PRINT"1..Logarithme van getal'X'"
5220 PRINT"2..Logaritme log(10) van getal'X'"
5230 PRINT"3..Grondgetal van Logaritme"
5240 PRINT"4..Grondgetal van Logaritme log(10)":PRINT
5250 GOSUB1900:IF(K=0) THEN1110
5260 IF(K<1) OR(K>4) THEN5180
5270 PRINT"*";K:PRINT
5280 ONK GOTO5290,5340,5390,5430
5290 PRINT"Logaritme van... ";:INPUTX
5300 IF(X<=0) THENPRINT"LOGARITME <= 0 KAN NIET":GOTO5290
5310 L=LOG(X)/NL:PRINT
5320 PRINT"DE LOGARITHME VAN ";X;" =";L
5330 M=8:GOTO1920
5340 PRINT"Logaritme log(10) van ";:INPUTX
5350 IF(X<=0) THENPRINT"LOGARITME <= 0 KAN NIET":GOTO5340
5360 L=LOG(X)/LOG(10):PRINT
5370 PRINT"DE LOGARITME LOG(10) VAN ";X;" =";L
5380 M=8:GOTO1920
5390 PRINT"Grondgetal van logaritme ";:INPUTX
5400 G=EXP(X):PRINT
5410 PRINT"HET GRONDGETAL VAN ";X;" =";G
5420 M=8:GOTO1920
5430 PRINT"Grondgetal v.logaritme log(10)";:INPUTX
5440 G=10^X:PRINT
5450 PRINT"HET GRONDGETAL VAN ";X;" =";G
5460 M=8:GOTO1920
5470 GOSUB100:PRINT"       * GEBRUIKS-AANWIJZING *":PRINT
5480 PRINT"Om te vermijden dat U zelf bij diverse"
5490 PRINT"berekeningen eerst alles moeizaam moet"
5500 PRINT"herleiden naar de gebruikelijke eenheid"
5510 PRINT"voor formules,worden U de gegevens in"
5520 PRINT"twee delen gevraagd:"
5530 PRINT"Eerst de getalwaarde,dan de expressie"
5540 PRINT"B.V.:Weerstand 2 kiloOHM:2-ENTER-dan KO"
5550 PRINT"De gebruikte'variabelen'zijn duidelijk"
5560 PRINT"en worden steeds in de opgaven vermeld:"
5570 PRINT"Volt=V,MilliVolt=MV,MicroVolt=UV"
5580 PRINT"Amp.=A,MilliAmp.=MA,MicroAmp.=UA"
5590 PRINT"Henry=H,MilliHenry=MH,MicroHenry=UH"
5600 PRINT"MicroFarad=UF,NanoFarad=NF,PicoFarad=PF"
5610 PRINT"Ohm=O,Kilo-Ohm=KO,mega-Ohm=MO"
5620 PRINT"Herz=H,KiloHerz=KH,megaHerz=MH"
5630 PRINT"Bij berekening van'L'en'C',parallel of"
5640 PRINT"serie,krijgt U bij de reactantie ook"
5650 PRINT"vermeld:capacitief=CAP of inductief=IND"
5660 PRINT"In bereik van +/- 2% van de resonantie-"
5670 PRINT"frequentie wordt dit aangegeven met:RES"
5680 PRINT:PRINT"DRUK EEN TOETS VOOR HET MENU";
5690 M=0:GOSUB210:GOTO1110
30000 REM Vademecum:Diverse berekeningen
30010 REM Programma van PA0MAX-Max Wolff
30020 REM Luchthavenlaan 66
30030 REM 5042 TE TILBURG
30040 REM Computer PHILIPS P2000T/102
30050 REM Basic NL.Hobbyscoop Basicode 2
30060 REM COPYRIGHTS N.O.S.HOBBYSCOOP
30070 REM DATUM 24 NOVEMBER 1987 Versie 5

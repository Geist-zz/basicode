1000 A=2000:GOTO20
1010 REMCOMPUTERHANDEL
1020 REM-------- HOOFDPROGRAMMA -------
1030 GOSUB21000:REMTITEL
1040 GOSUB22000:REMWACHTLUS
1050 GOSUB22000:REMWACHTLUS
1060 GOSUB100
1070 HO=3:VE=5:GOSUB110
1080 PRINT"Wilt u de toelichting ?  (j/n)":GOSUB250
1090 GOSUB200
1100 IF(IN$="j") OR(IN$="J") THEN1130
1110 IF(IN$="n") OR(IN$="N") THEN1140
1120 GOTO1090
1130 GOSUB2300:REMTOELICHTING
1140 DIMA(21,16):DIMD(21,16)
1150 DIMK(21,16):DIMT(2):DIMC(2)
1160 GOSUB2200:REMVARIABELEN VASTLEGGEN
1170 GOSUB3400:REMMOEILIJKHEIDSGRAAD
1180 CP=10*DF
1190 GOSUB2000:REMARRAY STRUCTUUR
1200 GOSUB7000:REMSPEELVELD PRINTEN
1210 GOSUB7400:REMVERKOOP GROND
1500 REM***spelloop***
1510 GOSUB260:RF=1+INT(RV*(12-DF))
1520 RX=1
1530 IF(T>10) AND(RF<6) THENGOSUB8500
1540 HO=3:VE=21:GOSUB110
1550 SR$="Prijzen computers verhoogd"
1560 HO=3:VE=22:GOSUB110
1570 SR$="Winst gehalveerd"
1580 IF(T>10) AND(RF<6) THENPRINTSR$
1590 IF(T>10) AND(RF<6) THENGOSUB22000:GOSUB23000
1600 IF(RF<6) THENRX=.5
1610 GOSUB9500:REMBEPALEN TIJDSTIP ELLENDE  
1620 IF(CP>INT(4*NA)/DF) THEN9800:REMIK WIN
1630 IF(NA>CP) AND(NA>200) AND(LA>80) THEN9600
1640 LA=LA-(INT(MX/2)/10):T=T+1
1650 IF(T=2) OR(T=8) OR(T=15) OR(T=20) THEN7100
1660 IF(T=30) OR(T=40) OR(T=60) THEN7100
1670 IF(T=80) OR(T=100) OR(T=150) THEN7100
1680 IF(T=200) OR(T=300) OR(T=400) OR(T=1000) THEN7100
1690 LA=LA+(PX*RX)
1700 GOSUB260:TX=2+INT(RV*6)
1710 IF(RF=1) THENGOSUB8500:REMPRINT TELEX
1720 HO=0:VE=21:GOSUB110
1730 IF(RF=1) THENPRINT"Regering verhoogt omzetbelasting"
1740 HO=0:VE=22:GOSUB110
1750 IF(RF=1) THENPRINT"Kosten f";:PRINTTX;:PRINT" m."
1760 IF(RF=1) THENLA=LA-TX:GOSUB8000:REMPRINT FINANC
1770 HO=1:VE=21:GOSUB110
1780 SR$="Activiteiten op locatie"
1790 IF(T>10) AND(RF=3) AND(WX>0) THENPRINTSR$
1800 HO=1:VE=22:GOSUB110
1810 SR$="worden gestaakt"
1820 IF(T>10) AND(RF=3) AND(WX>0) THENPRINTSR$
1830 GOSUB22000:REMWACHTLUS
1840 IF(T>10) AND(RF=3) AND(WX>0) THENGOSUB9200
1850 IF(RF<=3) THENGOSUB23000:REMWIS TEKST 
1860 GOSUB23000:REMWIS TEKST
1870 HO=0:VE=22:GOSUB110:GOSUB250
1880 PRINT"Bouw Lenen Uit Vestigen verGun Markt";
1890 GOSUB200
1900 IF(IN$="b") OR(IN$="B") THEN4500:REMBOUWEN
1910 IF(IN$="l") OR(IN$="L") THEN4000:REMLENING
1920 IF(IN$="g") OR(IN$="G") THEN4200:REMVERGUNNING
1930 IF(IN$="v") OR(IN$="V") THEN5200:REMVESTIGEN
1940 IF(IN$="m") OR(IN$="M") THEN3600:REMMARKTONDERZOEK
1950 IF(IN$="u") OR(IN$="U") THEN6000:REMUITBREIDEN
1960 GOTO1890
2000 REM***array structuur***
2010 HO=10:VE=6:GOSUB110
2020 PRINT"Even geduld a.u.b."
2030 FORX=3 TO21
2040 FORY=3 TO16
2050 GOSUB260:A(X,Y)=1+INT(RV*2)
2060 GOSUB260
2070 IF(A(X,Y)>=2) THENA(X,Y)=A(X,Y)+INT(RV*2)
2080 NEXTY
2090 NEXTX
2100 GOSUB100
2110 RETURN
2200 REM***variabelen vastleggen***
2210 T=0
2220 CX=0:WX=0:BX=0:CY=0:LA=10*10:PZ=0
2230 SW=LA:PY=0:PZ=0:MX=0:PX=0:T(1)=4
2240 RETURN
2300 REM***toelichting***
2310 GOSUB100
2320 PRINT"C O M P U T E R H A N D E L"
2330 PRINT
2340 PRINT"In deze simulatie bent u de directeur"
2350 PRINT"van een computergroothandel."
2360 PRINT"De bedoeling is dat u uw onderneming"
2370 PRINT"zo goed mogelijk leidt, d.w.z. goede"
2380 PRINT"investeringen doen en de juiste"
2390 PRINT"beslissingen nemen."
2400 PRINT:PRINT"Uw concurrent is Datachain."
2410 PRINT
2420 PRINT"Uw opdracht is Datachain over te nemen."
2430 GOSUB22000:REMWACHTLUS  
2440 GOSUB20000:REMBLZ
2450 PRINT"KOPEN VAN GROND":PRINT
2460 PRINT"In het begin van het spel worden er"
2470 PRINT"percelen grond te koop aangeboden."
2480 PRINT"De bedoeling is er een aantal te "
2490 PRINT"kopen om er later een vestiging te"
2500 PRINT"bouwen."
2510 PRINT"De prijzen liggen tussen 1 en 10"
2515 PRINT"miljoen gulden."
2520 PRINT"Het is echter verstandig in het"
2530 PRINT"begin hoog te bieden, omdat u anders"
2540 PRINT"enige tijd werkeloos blijft zitten."
2550 PRINT"Zonder grond kunt u eigenlijk niets"
2560 PRINT"anders doen dan marktonderzoek."
2570 GOSUB20000:REMBLZ
2580 PRINT"MARKTONDERZOEK":PRINT
2590 PRINT"Een marktonderzoek kunt u doen door de"
2600 PRINT"M in te duwen."
2610 PRINT"Een marktonderzoek kost 0.2 miljoen."
2620 PRINT"Ook als u grond gekocht hebt, kunt u"
2630 PRINT"op die locatie marktonderzoek doen."
2640 PRINT"Dan kost een onderzoek u echter 1.2 "
2650 PRINT"miljoen."
2660 GOSUB20000:REMBLZ
2670 PRINT"VERGUNNING":PRINT
2680 PRINT"Als u een stuk grond gekocht hebt,"
2690 PRINT"kunt u daarop een vestiging bouwen."
2700 PRINT"Daarvoor moet u echter eerst een "
2710 PRINT"vergunning aanvragen. Dit doet u door"
2720 PRINT"op de G te drukken.":PRINT:PRINT"BOUWEN":PRINT
2730 PRINT"Om te bouwen drukt u de B in."
2740 PRINT"Dit kost ook weer geld en"
2750 PRINT"u krijgt een mededeling over hoe de"
2760 PRINT"bouw vordert. U moet blijven bouwen,"
2770 PRINT"totdat het gebouw klaar is."
2780 GOSUB20000:REMBLZ
2790 PRINT"VESTIGING":PRINT
2800 PRINT"Als het gebouw klaar is kunt u zich op"
2810 PRINT"die locatie vestigen. Dit doet u door"
2820 PRINT"op de V te duwen. Ook dit kost weer"
2830 PRINT"geld."
2840 GOSUB20000:REMBLZ   
2850 PRINT"UITBREIDEN":PRINT
2860 PRINT"u kunt de opbrengst van een vestiging"
2870 PRINT"nog verhogen door uit te breiden."
2880 PRINT"Dit doet u door de U in te drukken."
2890 GOSUB20000:REMBLZ   
2900 PRINT"De letter die steeds op een locatie"
2910 PRINT"staat geeft aan:"
2920 PRINT
2930 PRINT"- dat die locatie van u is."
2940 PRINT"- in welk stadium uw activiteiten"
2950 PRINT"  op die locatie zijn."
2960 PRINT"- wat de activiteit is welke u op"
2970 PRINT"  deze locatie kunt ondernemen."
2980 PRINT:PRINT
2990 PRINT"Onder in beeld vindt u een regel met"
3000 PRINT"alle opties, die u per beurt hebt."
3010 PRINT:PRINT"Deze ziet er als volgt uit:"
3020 PRINT:PRINT:PRINT
3030 PRINT"Bouw Lenen Uit Vestigen verGun Markt"
3040 PRINT
3050 GOSUB20000:REMBLZ  
3060 PRINT:PRINT"Bouw Lenen Uit Vestigen verGun Markt"
3070 PRINT:PRINT"Als er dus b.v. een G op een locatie"
3080 PRINT"staat betekent dit:"
3090 PRINT:PRINT"deze locatie is van u en u moet hier"
3100 PRINT"een vergunning aanvragen"
3110 PRINT"De enige andere optie is marktonder-"
3120 PRINT"zoek doen. Deze optie kunt u altijd"
3130 PRINT"kiezen."
3140 PRINT:PRINT
3150 PRINT"Als er dus b.v. een V op een locatie"
3160 PRINT"staat betekent dit:"
3170 PRINT"op deze locatie moet u nu een"
3180 PRINT"filiaal plaatsen."
3190 GOSUB20000:REMBLZ
3200 PRINT"Af en toe krijgt u telexmededelingen"
3210 PRINT"over omzetbelastingen of een lagere"
3220 PRINT"winst."
3230 PRINT
3240 PRINT"Soms doen er zich ongelukken of rampen"
3250 PRINT"voor en dit gebeurt eerder, wanneer u"
3260 PRINT"veel geld geleend hebt. Geld kunt u"
3270 PRINT"lenen door op L te duwen."
3280 GOSUB20000:REMBLZ
3290 PRINT"Wilt u de handleiding nog een keer"
3300 PRINT"doornemen ? (j/n)":GOSUB250
3310 GOSUB200
3320 IF(IN$="j") OR(IN$="J") THEN2300
3330 IF(IN$="n") OR(IN$="N") THEN3350
3340 GOTO3310
3350 GOSUB100
3360 HO=7:VE=10:GOSUB110
3370 PRINT"VEEL SUCCES !!!"
3380 GOSUB22000:REMWACHTLUS
3390 RETURN
3400 REM***moeilijkheidsgraad***
3410 GOSUB100
3420 HO=0:VE=15:GOSUB110
3430 PRINT"Moeilijkheidsgraad"
3440 PRINT"1. makkelijk"
3450 PRINT"2. moeilijk"
3460 PRINT"3. zeer moeilijk"
3470 PRINT
3480 PRINT"moeilijkheidsgraad?":GOSUB250
3490 GOSUB210
3500 DF=ASC(IN$)-48
3510 IF(DF=1) OR(DF=2) OR(DF=3) THEN3530
3520 GOTO3490
3530 GOSUB100
3540 RETURN
3600 REM***markt-onderzoek***
3610 GOSUB23000:REMWIS TEKST
3620 HO=0:VE=21:GOSUB110
3630 PRINT"Markt onderzoek"
3640 GOSUB22000:REMWACHTLUS
3650 GOSUB8600:REMPLAATSBEPALING/INVOER
3660 HO=2:VE=22:GOSUB110
3670 PRINT"Vestigingsplaats is ";
3680 IF(A(X,Y)=1) THENPRINT"ongunstig"
3690 IF(A(X,Y)=2) THENPRINT"geschikt"
3700 IF(A(X,Y)=3) THENPRINT"zeer geschikt"
3710 IF(A(X,Y)>3) AND(A(X,Y)<=30) THENGOSUB3850
3720 LA=LA-.2
3730 GOSUB22000:REMWACHTLUS
3740 GOSUB23000:REMWIS TEKST  
3750 HO=0:VE=19:GOSUB110
3760 PRINT"Nog een markt-onderzoek? (j/n)":GOSUB250
3770 GOSUB210
3780 IF(IN$="") THEN3770
3790 IF(IN$="n") OR(IN$="N") THEN3820
3800 IF(IN$="j") THEN3600
3810 GOTO3770
3820 GOSUB8000:REMPRINT FINANC       
3830 GOSUB23000:REMWIS TEKST   
3840 GOTO1500:REMSPELLOOP    
3850 HO=0:VE=19:GOSUB110
3860 PRINT"Markt-onderzoek kost $1.2 m."
3870 LA=LA-1
3880 HO=23:VE=22:GOSUB110
3890 IF(A(X,Y)=10) THENPRINT"ongunstig"
3900 HO=23:VE=22:GOSUB110
3910 IF(A(X,Y)=20) THENPRINT"geschikt"
3920 HO=23:VE=22:GOSUB110
3930 IF(A(X,Y)=30) THENPRINT"zeer geschikt"
3940 RETURN
4000 REM***lening***
4010 GOSUB23000:REMWIS TEKST
4020 HO=0:VE=19:GOSUB110
4030 PRINT"Lening ?"
4040 PRINT"Geef bedrag in miljoenen guldens";:GOSUB250
4050 INPUTJ$
4060 BE=VAL(J$)
4070 IF(BE<1) OR(BE>=30) THEN4040
4080 PRINT"f ";J$;" miljoen gevraagd"
4090 GOSUB260
4100 LS=1+(INT(RV*2)*BE)
4110 IF(LS>40) THENPRINT"Lening niet gegeven":GOTO4140
4120 IF(LS<40) THENPRINT"Lening gegeven"
4130 IF(LS<40) THENLA=LA+BE:MX=MX+BE
4140 GOSUB8000:REMPRINT FINANC
4150 IF(LS>=40) THENGOSUB22000:REMWIS REGEL   
4160 GOSUB22000:REMWACHTLUS
4170 GOSUB23000:REMWIS TEKST
4180 GOTO1500:REMSPELLOOP    
4200 REM***aanvraag vergunning***    
4210 GOSUB23000:REMWIS TEKST
4220 HO=0:VE=19:GOSUB110
4230 PRINT"Waar wilt u een vergunning aanvragen "
4240 GOSUB8620:REMPLAATSBEPALING/INVOER     
4250 IF(A(X,Y)<10) THEN4370
4260 LD=ABS(X-2):RW=ABS(Y-20)
4270 IF(RW>LD) THEN4290
4280 IF(LD>RW) THENLA=LA-(LD*.2):GOTO4310
4290 IF(Y<=6) THENRW=RW*2
4300 LA=LA-(RW*.2)
4310 LA=LA-(LD*.2)
4320 A(X,Y)=A(X,Y)*10
4330 HO=X-1:VE=Y-1:GOSUB110
4340 PRINT"B"
4350 GOSUB8000:REMPRINT FINANC
4360 GOTO1500:REMSPELLOOP  
4370 HO=0:VE=20:GOSUB110
4380 PRINT"Deze grond is niet van u"
4390 GOSUB22000:REMWACHTLUS
4400 GOTO1500:REMSPELLOOP
4500 REM***bouwen***
4510 GOSUB23000:REMWIS TEKST
4520 HO=0:VE=19:GOSUB110
4530 PRINT"Bouwplaats"
4540 GOSUB8620:REMPLAATSBEPALING/INVOER
4550 IF(A(X,Y)<100) THEN5000
4560 GOSUB260
4570 T(1)=T(1)+D
4580 D(X,Y)=D(X,Y)+INT(RV*6)+1
4590 HO=0:VE=19:GOSUB110
4600 PRINT"Hoogte gebouw nu : ";D(X,Y);" meter"
4610 C(1)=INT(D(X,Y)/3)+1:LA=LA-C(1)
4620 PRINT"Bouwkosten = ";C(1);" miljoen gulden"
4630 IF(D(X,Y)>20) THENPRINT"Gebouw ingestort"
4640 IF(D(X,Y)>20) THENGOSUB9200:GOTO4930
4650 GOSUB260
4660 PO=(RV*(A(X,Y)*D(X,Y)))
4670 PRINT"Vordering bouw :  ";
4680 IF(PO>(20*(1+(T(1)/10)))*10) THEN4740
4690 UX=(20*(1+(T(1)/10)))*10:UY=(14*(1+(T(1)/20)))*10
4700 IF(PO<UX) AND(PO>UY) THENPRINT" redelijk";
4710 IF(PO<=(14*(1+(T(1)/10)))*10) THENPRINT" snel"
4720 GOSUB22000:REMWACHTLUS
4730 GOTO4930
4740 GOSUB22000
4750 FORZ=1 TO5
4760 GOSUB23000
4770 HO=3:VE=21:GOSUB110
4780 PRINT"Gebouw klaar"
4790 NEXTZ
4800 A(X,Y)=A(X,Y)*10:IF(A(X,Y)<1000) THENA(X,Y)=1000
4810 HO=X-1:VE=Y-1:GOSUB110
4820 PRINT"V"
4830 GOSUB260
4840 RF=1+INT(RV*9)
4850 GOSUB260
4860 AC=1+(INT(RV*20)/10)
4870 HO=0:VE=19:GOSUB110
4880 IF(RF=1) THENPRINT"Vervanging bouwkraan f";
4890 IF(RF=1) THENPRINTAC/2;:PRINT"m.":LA=LA-(AC/2)
4900 IF(RF=2) THENPRINT"Ongeluk op bouwplaats kosten f";
4910 IF(RF=2) THENPRINTAC;:PRINT"m.":LA=LA-AC
4920 IF(RF=4) THEN5100
4930 GOSUB8000:REMPRINT FINANC
4940 GOSUB23000:REMWIS TEKST
4950 GOTO1500:REMSPELLOOP  
5000 REM***geen vergunning***    
5010 HO=1:VE=20:GOSUB110
5020 PRINT"Geen vergunning verleend"
5030 GOSUB22000:REMWACHTLUS  
5040 GOTO1500:REMSPELLOOP   
5100 REM***ontploffing***   
5110 HO=1:VE=22:GOSUB110
5120 A(X,Y)=A(X,Y)/10:LA=LA-20
5130 GOSUB22000:REMWACHTLUS
5140 GOTO1500:REMSPELLOOP
5200 REM***vestigen verkooppunt***   
5210 GOSUB23000:REMWIS TEKST  
5220 HO=1:VE=19:GOSUB110
5230 PRINT"Vestiging verkooppunt"
5240 GOSUB8620:REMPLAATSBEPALING/INVOER
5250 IF(A(X,Y)<1000) THEN5700
5260 PP=20:IF(Y<=6) THENPP=30
5270 HO=1:VE=21:GOSUB110
5280 IF(LA-PP<0) THENPRINT"Onvoldoende geld":GOTO5890
5290 LA=LA-PP
5300 GOSUB260
5310 AC=1+(INT(RV*20)/10)
5320 GOSUB260
5330 RF=1+INT(RV*9)
5340 IF(RF=1) THEN5800
5350 HO=0:VE=21:GOSUB110
5360 IF(RF=2) THENPRINT"Technisch probleem-grondwater"
5370 IF(RF=2) THENPRINT"Kosten ";:PRINTAC;
5380 IF(RF=2) THENPRINT"miljoen gulden"
5390 IF(RF=2) THENLA=LA-AC
5400 IF(RF=3) THENPRINT"Brand beschadigt bouw kosten f";
5410 IF(RF=3) THENPRINTAC*4;:PRINT"miljoen":LA=LA-(AC*4)
5420 IF(RF=2) OR(RF=3) THENGOSUB22000:GOSUB23000
5430 GOSUB260
5440 BU=25+INT(RV*200)
5450 GOSUB260
5460 RY=BU*INT(30+(RV*15))/1000
5470 HO=1:VE=20:GOSUB110
5480 PRINT"Kosten f";:PRINTPP;:PRINT"miljoen":PX=PX+RY
5490 HO=1:VE=21:GOSUB110
5500 PRINT"Opslagruimte voor ";(BU*1000);" computers"
5510 HO=1:VE=22:GOSUB110
5520 PRINT"Opbrengst f";:PRINTINT(RY*10)/10;
5530 PRINT" m.":BX=BX+BU:WX=WX+1:A(X,Y)=A(X,Y)*10
5540 HO=X-1:VE=Y-1:GOSUB110
5550 PRINT"U"
5560 PZ=PZ+PP
5570 GOSUB8000:REMPRINT FINANC
5580 GOSUB22000:REMWACHTLUS
5590 GOSUB23000:REMWIS TEKST
5600 GOTO1500:REMSPELLOOP   
5700 REM***Bouw nog niet voltooid***
5710 HO=1:VE=21:GOSUB110
5720 PRINT"Bouw nog niet voltooid"
5730 GOSUB22000:REMWACHTLUS
5740 GOSUB23000:REMWIS TEKST
5750 GOTO1500:REMSPELLOOP
5800 REM***Gebouw verwoest***   
5810 FORZ=1 TO16
5820 HO=1:VE=21:GOSUB110
5830 PRINT"               "
5840 HO=1:VE=21:GOSUB110
5850 PRINT"Ontploffing"
5860 NEXTZ
5870 HO=1:VE=22:GOSUB110
5880 PRINT"Gebouw verwoest":LA=LA-20
5890 GOSUB8000:REMPRINT FINANC
5900 GOSUB23000:REMWIS TEKST
5910 GOTO1500:REMSPELLOOP   
6000 REM***uitbreiden***   
6010 GOSUB23000:REMWIS TEKST
6020 HO=1:VE=21:GOSUB110
6030 PRINT"Vestiging uitbreiden"
6040 GOSUB8620:REMPLAATSBEPALING/INVOER
6050 IF(A(X,Y)<9999) THEN6400
6060 LD=ABS(X-2):RW=ABS(Y-20)
6070 IF(RW>LD) THEN6090
6080 IF(LD>RW) THENPL=LD*.4:GOTO6110
6090 IF(Y<=6) THENRW=RW*2
6100 PL=RW*.4
6110 HO=1:VE=21:GOSUB110
6120 IF(LA-PL<0) THENPRINT"Onvoldoende geld":GOTO6580
6130 LA=LA-PL
6140 GOSUB260
6150 RF=1+INT(RV*6)
6160 IF(RF=2) THEN6700
6170 HO=1:VE=21:GOSUB110
6180 SR$="Uitbreiding 1.5 m duurder als begroot"
6190 IF(RF=3) THENPRINTSR$
6200 IF(RF=3) THENLA=LA-1.5:GOSUB22000:GOSUB23000
6210 GOSUB260
6220 BU=25+INT(RV*100)
6230 GOSUB260
6240 RY=BU*INT(30+(RV*15))/1000
6250 PY=PY+PL:BX=BX+BU:PX=PX+RY
6260 HO=1:VE=20:GOSUB110
6270 PRINT"Kosten uitbreiding f";:PRINTPL;:PRINT"m."
6280 HO=1:VE=21:GOSUB110
6290 PRINT"Toename opslagruimte ";:PRINTBU*100;"computers"
6300 HO=1:VE=22:GOSUB110
6310 PRINT"Inkomsten f";:PRINTINT(RY*10)/10;:PRINT" m."
6320 HO=X-1:VE=Y-1:GOSUB110
6330 PRINT"U"
6340 GOSUB8000:REMPRINT FINANC
6350 GOSUB23000:REMWIS TEKST
6360 GOTO1500:REMSPELLOOP  
6400 REM***nog geen vestiging***
6410 HO=1:VE=21:GOSUB110
6420 PRINT"Nog geen vestiging"
6430 GOSUB22000:REMWACHTLUS
6440 GOSUB23000:REMWIS TEKST
6450 GOTO1500:REMSPELLOOP  
6500 REM***vandalisme***   
6510 FORZ=1 TO16
6520 HO=1:VE=21:GOSUB110
6530 PRINT"        "
6540 HO=1:VE=21:GOSUB110
6550 PRINT"Vandalisme"
6560 NEXTZ
6570 HO=1:VE=22:GOSUB110
6580 GOSUB22000:REMWACHTLUS
6590 GOSUB23000:REMWIS TEKST
6600 GOTO1500:REMSPELLOOP  
6700 REM***storm***
6710 FORZ=1 TO16
6720 HO=2:VE=21:GOSUB110
6730 PRINT"                         "
6740 HO=2:VE=21:GOSUB110
6750 PRINT"Er heeft een storm gewoed"
6760 NEXTZ
6770 HO=2:VE=22:GOSUB110
6780 PRINT"Vestiging is totaal vernield"
6790 GOSUB22000:REMWACHTLUS
6800 IF(PZ>30) THENPZ=PZ-20
6810 IF(PZ<=30) THENPZ=0
6820 A(X,Y)=A(X,Y)/10
6830 GOSUB9400:REMOPGEVEN VESTIGING
6840 HO=X-1:VE=Y-1:GOSUB110
6850 PRINT"B"
6860 GOSUB8000:REMREM PRINT FINANC
6870 GOSUB23000:REMWIS TEKST
6880 GOTO1500:REMSPELLOOP   
7000 REM***speelveld printen***
7010 HO=2:VE=0:GOSUB110
7020 PRINT"ABCDEFGHIJKLMNOPQRS"
7030 FORQ=3 TO16
7040 HO=0:VE=Q-1:GOSUB110
7050 PRINTCHR$(94+Q)
7060 NEXTQ
7070 PRINT
7080 GOSUB8000:REMPRINT FINANC
7090 RETURN
7100 GOSUB23000:REMWIS TEKST
7110 IF(T<10) THEN7400:REMVERKOOP GROND
7200 REM***exploitatieoverzicht***
7210 HO=1:VE=18:GOSUB110
7220 PRINT"EXPLOITATIEOVERZICHT"
7230 PRINT"Aantal percelen ";:PRINTCX
7240 PRINT"Aantal vestigingen";:PRINTWX
7250 GOSUB8000:REMREM PRINT FINANC
7260 HO=0:VE=21:GOSUB110
7270 PRINT"Opslagruimte voor ";:PRINTBX*1000;
7280 PRINT" computers"
7290 PRINT"Activa Datachain f";:PRINTCP;:PRINT"miljoen"
7300 FORZ=1 TO1000:NEXTZ
7310 GOSUB22000:REMWACHTLUS
7320 GOSUB23000:REMWIS TEKST
7330 GOTO1500:REMSPELLOOP   
7400 REM***verkoop grond***
7410 GOSUB23000:REMWIS TEKST   
7420 HO=0:VE=19:GOSUB110
7430 PRINT"Verkoop van grond"
7440 GOSUB260
7450 UU=1+(RV*(4-DF))
7460 FORU=1 TOUU
7470 GOSUB260
7480 Y=3+INT(RV*13)
7490 GOSUB260
7500 X=3+INT(RV*18)
7510 IF(K(X,Y)=1) THEN7480
7520 FORF=1 TO40
7530 HO=X-1:VE=Y-1:GOSUB110
7540 PRINT" "
7550 HO=X-1:VE=Y-1:GOSUB110
7560 PRINT"*"
7570 NEXTF
7580 HO=1:VE=20:GOSUB110
7590 PRINT"Je bod ";:GOSUB250
7600 INPUTV$:V=VAL(V$)
7610 PRINTV;:PRINT" m."
7620 GOSUB260
7630 CB=INT(A(X,Y)*(1+(RV*4)))
7640 HO=1:VE=22:GOSUB110
7650 IF(V>LA) THENPRINT"Onvoldoende geld"
7660 IF(V>=CB) THEN7720
7670 IF(V<CB) THENPRINT"Bod te laag"
7680 HO=X-1:VE=Y-1:GOSUB110
7690 IF(V<CB) OR(V>LA) THENPRINT" "
7700 IF(V<CB) OR(V>LA) THENGOSUB22000:REMWACHTLUS
7710 GOTO7800
7720 HO=1:VE=22:GOSUB110
7730 PRINT"Geslaagd bod"
7740 CX=CX+1:A(X,Y)=A(X,Y)*10
7750 HO=X-1:VE=Y-1:GOSUB110
7760 PRINT"G"
7770 K(X,Y)=1
7780 LA=LA-V:CY=CY+V
7790 GOSUB8000:REMPRINT FINANC
7800 GOSUB23000:REMWIS TEKST
7810 NEXTU
7820 GOTO1500:REMSPELLOOP   
8000 REM***print financ***
8010 HO=22:VE=0:GOSUB110
8020 PRINT"Financieel"
8030 HO=22:VE=1:GOSUB110
8040 PRINT"overzicht."
8050 HO=22:VE=2:GOSUB110
8060 PRINT"in miljoenen"
8070 HO=22:VE=4:GOSUB110
8080 PRINT"ACTIVA"
8090 HO=22:VE=5:GOSUB110
8100 PRINT"Percelen   ";
8110 VV=CY:SR=VV:GOSUB300
8120 CT=6:CN=2:GOSUB310:PRINTSR$
8130 HO=22:VE=6:GOSUB110
8140 PRINT"Filialen   ";
8150 VV=PY:SR=VV:GOSUB300
8160 CT=6:CN=2:GOSUB310:PRINTSR$
8170 HO=22:VE=7:GOSUB110
8180 PRINT"Vestigingen";
8190 VV=PZ:SR=VV:GOSUB300
8200 CT=6:CN=2:GOSUB310:PRINTSR$
8210 HO=22:VE=8:GOSUB110
8220 PRINT"Contant    ";
8230 VV=LA:SR=VV:GOSUB300
8240 CT=6:CN=2:GOSUB310:PRINTSR$
8250 TX=LA+PZ+PY+CY
8260 HO=22:VE=9:GOSUB110
8270 PRINT"Leningen  -";
8280 VV=MX:SR=VV:GOSUB300
8290 CT=6:CN=2:GOSUB310:PRINTSR$
8300 NA=TX-MX
8310 HO=22:VE=11:GOSUB110
8320 PRINT"TOTAAL:    ";
8330 VV=NA:SR=VV:GOSUB300
8340 CT=6:CN=2:GOSUB310:PRINTSR$
8350 HO=22:VE=13:GOSUB110
8360 PRINT"Balans "
8370 HO=22:VE=14:GOSUB110
8380 PRINT"Kapitaal   ";
8390 VV=SW:SR=VV:GOSUB300
8400 CT=6:CN=2:GOSUB310:PRINTSR$
8410 HO=22:VE=15:GOSUB110
8420 PRINT"Winst      ";
8430 EN=NA-SW:VV=EN:SR=VV:GOSUB300
8440 CT=6:CN=2:GOSUB310:PRINTSR$
8450 HO=22:VE=16:GOSUB110
8460 PRINT"TOTAAL:    ";
8470 VV=NA:SR=VV:GOSUB300
8480 CT=6:CN=2:GOSUB310:PRINTSR$
8490 RETURN
8500 REM***telex***
8510 FORTT=1 TO16
8520 HO=3:VE=21:GOSUB110
8530 PRINT"Telex mededeling":GOSUB250
8540 HO=3:VE=21:GOSUB110
8550 PRINT"                "
8560 NEXTTT
8570 RETURN
8600 REM***plaatsbepaling***
8610 GOSUB23000:REMWIS TEKST   
8620 HO=1:VE=22:GOSUB110:GOSUB250
8630 PRINT"Geef lijn no. ";
8640 GOSUB210
8650 Y=ASC(IN$)
8660 IF(Y<97) THENY=Y+32
8670 Y=Y-94
8680 IF(Y<3) OR(Y>16) THEN8640
8690 HO=16:VE=22:GOSUB110
8700 PRINTIN$
8710 HO=18:VE=22:GOSUB110
8720 PRINT"geef kolom no.";:GOSUB250
8730 GOSUB210
8740 X=ASC(IN$)
8750 IF(X<97) THENX=X+32
8760 X=X-94
8770 IF(X<3) OR(X>21) THEN8730
8780 HO=33:VE=22:GOSUB110
8790 PRINTIN$
8800 FORZ=1 TO1500:NEXTZ
8810 GOSUB23000:REMWIS TEKST
8820 RETURN
8900 REM***allerlei ellende***
8910 GOSUB260
8920 RF=1+INT(RV*3)
8930 HO=0:VE=22:GOSUB110
8940 SR$="Grote partij computers gedumpd"
8950 IF(RF=1) THENPRINTSR$
8960 IF(RF=1) THENPRINT"weinig winst deze maand":RX=4
8970 IF(RF=2) THENPRINT"ongunstige Dollarkoers"
8980 IF(RF=2) THENPRINT"opbrengst een derde":PX=PX/3
8990 IF(RF=3) THENPRINT"stijging  rente kosten f";
9000 IF(RF=3) THENPRINTINT(MX/4)/10;
9010 IF(RF=3) THENPRINT"m.":LA=LA-(INT(MX/4)/10)
9020 GOSUB22000:REMWACHTLUS
9030 GOSUB23000:REMWIS TEKST
9040 RETURN
9100 REM***nog meer ellende***
9110 HO=0:VE=19:GOSUB110
9120 PRINT"Gemeente onteigent grond"
9130 PRINT"Opbrengst :";TX*3;:PRINT"m.":LA=LA+(TX*3)
9140 GOSUB9200:REMWAARDEN VELDEN
9150 GOTO1500:REMSPELLOOP
9200 REM***waarden velden***
9210 IF(A(X,Y)>=10) AND(CY>=8) THENCY=CY-8
9220 IF(D(X,Y)>20) AND(CY>=8) THENCY=CY-8
9230 IF(A(X,Y)>=10) AND(CY<8) THENCY=0
9240 IF(D(X,Y)>20) AND(CY<8) THENCY=0
9250 IF(A(X,Y)>=10000) AND(PZ>=30) THENPZ=PZ-20
9260 IF(A(X,Y)>=10000) AND(PZ<30) THENPZ=0
9270 IF(A(X,Y)>=100000!) AND(PY>=2) THENPY=PY-2
9280 IF(A(X,Y)>=100000!) AND(PY<2) THENPY=0
9290 IF(A(X,Y)>=10000) THENWX=WX-1
9300 IF(D(X,Y)>20) OR(A(X,Y)>=10) THENCX=CX-1
9310 A(X,Y)=1
9320 GOSUB9400:REMOPGEVEN VELD
9330 GOSUB8000:REMPRINT FINANC
9340 GOSUB23000:REMWIS TEKST
9350 RETURN
9400 REM***opgeven veld***
9410 FORZ=1 TO40
9420 HO=X-1:VE=Y-1:GOSUB110
9430 PRINT"*"
9440 HO=X-1:VE=Y-1:GOSUB110
9450 PRINT" "
9460 NEXTZ
9470 RETURN
9500 REM***bepalen tijdstip ellende***
9510 GOSUB260
9520 CP=CP+INT(RV*(CP/5))
9530 IF(MX>20) THENGOSUB8900
9540 IF(CP>INT(4*NA)/DF) THEN9800:REMIK WIN   
9550 RETURN
9600 REM***jij wint***
9610 GOSUB100
9620 GOSUB8500:REMPRINT TELEX
9630 PRINT"U hebt Datachain overgenomen."
9640 PRINT"Netto activa ten bedrage van f";
9650 PRINTINT(CP);:PRINT"m."
9660 FORZ=1 TO1000:NEXTZ
9670 GOSUB22000:REMWACHTLUS
9680 NA=NA+CP:FORF=1 TO1000:NEXTF
9700 REM***nog een keer?***
9710 GOSUB100
9720 PRINT"U had er ";:PRINTT;:PRINT" maanden voor nodig"
9730 PRINT"Moeilijkheidsgraad ";:PRINTDF
9740 PRINT"Uw totale activa f";:PRINTINT(NA);:PRINT" m."
9750 PRINT"Wilt u nog een keer spelen? (j/n)":GOSUB250
9760 GOSUB210
9770 IF(IN$="j") OR(IN$="J") THENRUN
9780 IF(IN$="n") OR(IN$="N") THENPRINT"Jammer dan":END
9790 GOTO9760
9800 REM***ik win***
9810 GOSUB8500
9820 PRINT"Datachain heeft uw bedrijf overgenomen"
9830 FORZ=1 TO1000:NEXTZ
9840 GOSUB22000:GOSUB22000:GOSUB100
9850 PRINT"Datachain activa f";:PRINTINT(CP);:PRINT" m."
9860 GOTO9700
20000 REM***bladzijde omslaan***
20010 HO=0:VE=22:GOSUB110
20020 PRINT"Druk een toets in ...";
20030 IN$="":VE=22:HO=36:GOSUB110:PRINT"=>";
20040 FORT=1 TO100:NEXTT
20050 GOSUB200:IF(IN$<>"") THENGOSUB100:RETURN
20060 VE=22:HO=36:GOSUB110:PRINT"  ";
20070 FORT=1 TO100:NEXTT:GOTO20000
21000 REM***TITEL***
21010 GOSUB100
21020 HO=0:VE=1:GOSUB110
21030 PRINT"       **************************"
21040 PRINT"       *                        *"
21050 PRINT"       *  ********************  *"
21060 SR$="       *  *                  *  *"
21070 FORF=1 TO9
21080 PRINTSR$:NEXTF
21090 PRINT"       *  ********************  *"
21100 PRINT"       *                        *"
21110 PRINT"       **************************"
21120 PRINT"      *                          *"
21130 PRINT"     *  O O O O O O O O O O O O   *"
21140 PRINT"    *  O O O O O O O O O O O O O   *"
21150 PRINT"   *    O O O O O O O O O O O O     *"
21160 PRINT"  *                                  *"
21170 PRINT" **************************************"
21180 HO=13:VE=6:GOSUB110
21190 PRINT"Computerhandel"
21200 HO=12:VE=10:GOSUB110
21210 PRINT"Sjef Simons 1987"
21220 RETURN
22000 REM***Wachtlus***
22010 FORZ=1 TO3000:NEXTZ:RETURN
23000 REM***wis tekst***
23010 SR$="                                      "
23020 HO=0:VE=18:GOSUB110
23030 PRINTSR$;
23040 HO=0:VE=19:GOSUB110
23050 PRINTSR$;
23060 HO=0:VE=20:GOSUB110
23070 PRINTSR$;
23080 HO=0:VE=21:GOSUB110
23090 PRINTSR$;
23100 HO=0:VE=22:GOSUB110
23110 PRINTSR$;
23140 RETURN
29000 REM****************************
29010 REM
29020 REMDOOR DE WAARDE VAN Z IN  
29030 REMREGEL 22010 TE VERANDEREN 
29035 REMKUNT U DE SNELHEID VAN HET
29040 REMPROGRAMMA AANPASSEN AAN UW
29050 REMCOMPUTER.
29060 REM
29070 REM****************************
30000 REM
30010 REM************************
30020 REM*                      *
30030 REM*  COMPUTERHANDEL      *
30040 REM*                      *   
30050 REM*  AUTEUR:             * 
30060 REM*                      *   
30070 REM*  J.P.M. SIMONS       *
30080 REM*  STATIONSSTRAAT 11   *
30090 REM*  4872 TA ETTEN-LEUR  *
30100 REM*                      *   
30110 REM*  GESCHREVEN OP EEN   *  
30120 REM*  SCHNEIDER CPC 464   *
30130 REM*  AUGUSTUS 1985       *  
30140 REM*  OMGEWERKT NAAR      *
30150 REM*  BASICODE-2          *
30160 REM*  JANUARI 1987        *
30170 REM*                      *    
30180 REM************************
30190 REM
30200 REMCOPYRIGHT (C) - NOS HILVERSUM    

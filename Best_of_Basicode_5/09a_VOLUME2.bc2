1000 LETA=1000:GOTO20:REM VOLUME 2
1010 GOSUB20100
1020 GA=0:FA=0
1100 REM ***> ONDERDELEN <***
1110 GOSUB1200:REM-INLEIDING
1120 GOSUB1300:REM-OPGAVE 1
1130 GOSUB1500:REM-OPGAVE 2
1140 GOSUB1700:REM-OPGAVE 3A
1150 GOSUB1900:REM-OPGAVE 3B
1160 GOSUB2100:REM-OPGAVE 4A
1170 GOSUB2300:REM-OPGAVE 4B
1180 GOSUB2500:REM-OPGAVE 4C
1190 END
1200 REM ****> INLEIDING <****
1205 GOSUB100
1210 PRINT"           +++++++++++++++++"
1220 PRINT"           + volume deel 2 +"
1230 PRINT"           +++++++++++++++++":PRINT
1240 PRINT"Met dit programma kijken we de opgaven":PRINT
1250 PRINT"uit het vorige programma na.":PRINT
1260 W=2:GOSUB10000
1270 RETURN
1280 REM
1300 REM *****> OPGAVE 1 <******
1310 GOSUB100
1320 PRINT"Druk de wis-toets eens in.":PRINT
1330 GOSUB210:VB$=IN$
1340 PRINT"Hoeveel dm3 komt er bij jou uit":PRINT
1350 PRINT"opgave 1 ?":PRINT:PRINT
1360 GOSUB120:HA=HO:VA=VE
1370 GOSUB10400:PRINT:PRINT
1380 BW=VAL("9.5"):IFAW=BW THEN1430
1390 IFAW=9500000! THENPRINT"Je neemt : 1 cm = 10 dm."
1400 IFAW=.0095 THENPRINT"Je neemt : 1 cm = 0.01 dm."
1410 FA=1:GOSUB10700
1420 GOTO1360
1430 IFFA=0 THENGA=GA+1:PRINT:PRINT
1440 GOSUB10800:PRINT:PRINT
1450 W=2:GOSUB10000
1460 RETURN
1470 REM
1500 REM *****> OPGAVE 2 <******
1510 GOSUB100:FA=0
1520 PRINT
1540 PRINT"Hoeveel m3 komt er bij jou uit":PRINT
1550 PRINT"opgave 2 ?":PRINT:PRINT
1560 GOSUB120:HA=HO:VA=VE
1570 GOSUB10400:PRINT:PRINT
1580 BW=VAL(".495"):IFAW=BW THEN1630
1590 IFAW=495 THENPRINT"Je neemt : 1 cm = 0.1 m."
1600 IFAW=.000495 THENPRINT"Je neemt : 1 cm = 0.001 m."
1610 FA=1:GOSUB10700
1620 GOTO1560
1630 IFFA=0 THENGA=GA+1:PRINT:PRINT
1640 GOSUB10800:PRINT:PRINT
1650 W=2:GOSUB10000
1660 RETURN
1670 REM
1700 REM *****> OPGAVE 3A <******
1710 GOSUB100:FA=0
1720 PRINT
1740 PRINT"Hoeveel cm3 komt er bij jou uit":PRINT
1750 PRINT"opgave 3a ?":PRINT:PRINT
1760 GOSUB120:HA=HO:VA=VE
1770 GOSUB10400:PRINT:PRINT
1780 BW=VAL("4320"):IFAW=BW THEN1830
1790 IFAW=.00432 THENPRINT"Je neemt : 1 dm = 0.1 cm."
1800 IFAW=4320000! THENPRINT"Je neemt : 1 dm = 100 cm."
1810 FA=1:GOSUB10700
1820 GOTO1760
1830 IFFA=0 THENGA=GA+1:PRINT:PRINT
1840 GOSUB10800:PRINT:PRINT
1850 W=2:GOSUB10000
1860 RETURN
1870 REM
1900 REM *****> OPGAVE 3B <******
1910 GOSUB100:FA=0
1920 PRINT
1940 PRINT"Hoeveel m3 komt er bij jou uit":PRINT
1950 PRINT"opgave 3b ?":PRINT:PRINT
1960 GOSUB120:HA=HO:VA=VE
1970 GOSUB10400:PRINT:PRINT
1980 BW=VAL("0.00432"):IFAW=BW THEN2030
1990 IFAW=4320 THENPRINT"Je neemt : 1 dm = 10 m."
2010 FA=1:GOSUB10700
2020 GOTO1960
2030 IFFA=0 THENGA=GA+1:PRINT:PRINT
2040 GOSUB10800:PRINT
2045 IFGA=4 THENPRINT"Dat gaat goed zo"
2050 W=2:GOSUB10000
2060 RETURN
2070 REM
2100 REM *****> OPGAVE 4A <******
2110 GOSUB100:FA=0
2120 PRINT
2140 PRINT"Hoeveel m3 komt er bij jou uit":PRINT
2150 PRINT"opgave 4a ?":PRINT:PRINT
2160 GOSUB120:HA=HO:VA=VE
2170 GOSUB10400:PRINT:PRINT
2180 BW=VAL(".072"):IFAW=BW THEN2230
2210 FA=1:GOSUB10700
2220 GOTO2160
2230 IFFA=0 THENGA=GA+1:PRINT:PRINT
2240 GOSUB10800:PRINT
2250 IFGA=5 THENPRINT"Knap werk. Ga zo door."
2260 W=2:GOSUB10000
2270 RETURN
2280 REM
2300 REM *****> OPGAVE 4B <******
2310 GOSUB100:FA=0
2320 PRINT
2340 PRINT"Hoeveel dm3 komt er bij jou uit":PRINT
2350 PRINT"opgave 4b ?":PRINT:PRINT
2360 GOSUB120:HA=HO:VA=VE
2370 GOSUB10400:PRINT:PRINT
2380 BW=VAL("72"):IFAW=BW THEN2430
2410 FA=1:GOSUB10700
2420 GOTO2360
2430 IFFA=0 THENGA=GA+1:PRINT:PRINT
2440 GOSUB10800:PRINT
2450 IFGA=6 THENPRINT"Keurig .En nu de laatste nog."
2460 W=2:GOSUB10000
2470 RETURN
2480 REM
2500 REM *****> OPGAVE 4C <******
2510 GOSUB100:FA=0
2520 PRINT
2540 PRINT"Hoeveel cm3 komt er bij jou uit":PRINT
2550 PRINT"opgave 4c ?":PRINT:PRINT
2560 GOSUB120:HA=HO:VA=VE
2570 GOSUB10400:PRINT:PRINT
2580 BW=VAL("72000"):IFAW=BW THEN2630
2610 FA=1:GOSUB10700
2620 GOTO2560
2630 IFFA=0 THENGA=GA+1:PRINT:PRINT
2640 GOSUB10800:PRINT
2650 IFGA=7 THENPRINT"Fantastisch werk geleverd."
2660 IFGA=6 THENPRINT"Bijna alles goed, knap."
2670 IFGA=5 THENPRINT"Je hebt het wel door."
2680 IFGA=4 THENPRINT"Nog eens wat oefenen."
2690 IFGA<4 THENPRINT"Doe volume 1 nog eens een keer."
2700 W=3:GOSUB10000
2710 GOSUB100
2720 PRINT"Dat was het dan.":PRINT
2730 PRINT"Tot een volgende keer."
2740 RETURN
2750 REM
10000 REM *****> SPATIEBALK <*****
10010 FORW1=1 TOW*10:NEXTW1
10020 GOSUB120:VI=VE
10030 VE=23:HO=0:GOSUB110
10040 PRINT"   ****> Druk op de spatiebalk <**** ";
10050 GOSUB20000
10060 GOSUB200:IFIN$="" THEN10060
10065 :IFIN$<>" " THEN10060
10070 VE=23:HO=0:GOSUB110
10080 PRINT"                                      ";
10090 VE=VI:GOSUB110
10100 RETURN
10200 REM *****> RUIMTE MAKEN <*****
10210 REM
10220 FORI=0 TOY2-Y1
10230 HO=0:VE=Y1+I:GOSUB110
10240 PRINTA$;
10250 NEXTI
10260 VE=Y1:GOSUB110
10270 RETURN
10400 REM *****> INVOER ANTWOORD <****
10410 PRINT"Voer het getal in :";
10420 AW$="":I=0:GOSUB120:HI=HO
10430 GOSUB210
10440 IFIN$=CHR$(44) THENIN$="."
10450 IFIN$=VB$ THEN10490
10460 IFIN$=CHR$(13) THENAW=VAL(AW$):GOTO10530
10470 HO=HI+I:GOSUB110:PRINTIN$;:I=I+1
10480 AW$=AW$+IN$:GOTO10430
10490 IFI=0 THEN10430
10500 L=LEN(AW$):AW$=LEFT$(AW$,(L-1)):I=I-1
10510 HO=HI+I:GOSUB110:PRINT" ";
10520 GOTO10430
10530 RETURN
10540 REM
10600 REM ***> HET GOEDE ANTWOORD <***
10610 GOSUB100
10620 PRINT"Dat is het goede antwoord.":PRINT
10630 PRINT"Je hebt dus ook staan :":PRINT
10640 RETURN
10700 REM *****> FOUT ANTWOORD <*****
10705 PRINT
10710 PRINT"Je antwoord is niet juist.":PRINT
10720 PRINT"Reken het nog eens na.":PRINT
10730 W=2:GOSUB10000
10740 Y1=VA:Y2=20
10745 A$="                                       "
10750 GOSUB10200
10760 HO=HA:VE=VA:GOSUB110
10770 RETURN
10780 REM
10800 REM **> REAKTIE GOED ANTWOORD <**
10810 IFFA=1 THEN10855
10815 IFGA=1 THENGA$="Dit is het eerste goede antwoord."
10820 IFGA=2 THENGA$="Dit is het tweede goede antwoord."
10825 IFGA=3 THENGA$="Dit is het derde goede antwoord."
10830 IFGA=4 THENGA$="Dit is het vierde goede antwoord."
10835 IFGA=5 THENGA$="Dit is het vijfde goede antwoord."
10840 IFGA=6 THENGA$="Dit is het zesde goede antwoord."
10845 IFGA=7 THENGA$="Al je antwoorden zijn goed."
10850 PRINTGA$:GOTO10865
10855 GA$="Dit antwoord is goed."
10860 PRINTGA$
10865 RETURN
10870 REM
20000 REM TOETSENBORD BUFFER LEEG
20010 REM ******> C-64 <*******
20020 REM POKE 198,0
20030 REM *****> P-2000T <*****
20040 REM POKE &H600C,0
20050 REM *****> IBM PC <******
20060 REM WHILE INKEY$<>"":WEND
20070 RETURN
20080 REM
20100 REM INSTELLEN MONOCHROOM MONITOR
20110 REM ******> C-64 <*******
20120 REM POKE 53280,0:POKE 53281,0
20130 REM *****> IBM PC <******
20140 REM COLOR 7,0,0
20150 REM KLEINE LETTERMODE C-64
20160 REM POKE 53272,23
20170 RETURN
20180 REM
30000 REM******************************
30010 REM*                            *
30020 REM*      VOLUME 2              *
30030 REM*                            *
30040 REM* INGEZONDEN DOOR: J.WIELES  *
30050 REM* HOOFDWAL 68                *
30060 REM* 4207 EE GORINCHEM          *
30070 REM*                            *
30080 REM* GEMAAKT OP EEN C-64        *
30090 REM* UITGEPROBEERD OP EEN IBM PC*
30100 REM* EN EEN P2000T.             *
30110 REM*                            *
30120 REM* TOETSENBORD BUFFERS KUNNEN *
30130 REM* WORDEN LEEG GEMAAKT DOOR DE*
30140 REM* REM WEG TE HALEN BIJ REGEL *
30150 REM* 20020 VOOR C-64            *
30160 REM* 20040 VOOR P2000T          *
30170 REM* 20060 VOOR IBM PC          *
30180 REM*                            *
30190 REM* MONOCHROME MONITOR INSTEL- *
30200 REM* LEN DOOR REM WEG TE HALEN  *
30210 REM* BIJ REGEL                  *
30220 REM* 20120 VOOR C-64            *
30230 REM* 20150 VOOR IBM PC          *
30240 REM*                            *
30250 REM* KLEINE LETTERMODE VOOR     *
30260 REM* C-64 DOOR REM WEG TE HALEN *
30270 REM* BIJ REGEL 20160            *
30280 REM*                            *
30290 REM* LEESTIJD IN TE STELLEN DOOR*
30300 REM* FACTOR 10 IN REGEL 10100 TE*
30310 REM* VERANDEREN.                *
30320 REM*                            *
30330 REM* MET DANK AAN DE TWEEDE KLAS*
30340 REM* LEERLINGEN VAN DE OPENBARE *
30350 REM* SCHOOL VOOR MAVO TE        *
30360 REM* GORINCHEM.                 *
30370 REM*                            *
30380 REM******************************
30390 REM
30400 REM COPYRIGHT NOS HILVERSUM

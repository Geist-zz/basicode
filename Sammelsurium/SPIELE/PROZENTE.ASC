1000 A=500:GOTO 20:REM PROCENTEN
1010 CN=0:H=0:V=0:HT=HO:VT=VE:FR=0:GOSUB 280:VF=0:Z=0
1020 SP$="         ":SP$=SP$+SP$+SP$+SP$+"   "
1030 REM STARTSCHERM
1040 HO=12:VE=3:GOSUB 110:PRINT"%%PROCENTEN%%"
1050 HO=2:VE=15:GOSUB 110
1060 PRINT"EDUCATIEF PROGRAMMA VAN D.MAASKANT"
1070 HO=24:VE=20:GOSUB 110:PRINT"SEPTEMBER 1987":GOSUB 2580
1080 REM MENU
1090 GOSUB 100:HO=4:VE=0:GOSUB 110
1100 PRINT"MAAK EEN KEUZE UIT DIT MENU"
1110 HO=4:VE=8:GOSUB 110:PRINT"  1 = UITLEG"
1120 HO=4:VE=11:GOSUB 110:PRINT"  2 = OPGAVEN"
1130 HO=4:VE=14:GOSUB 110:PRINT"  3 = TOETS"
1140 HO=4:VE=17:GOSUB 110:PRINT"  4 = STOPPEN"
1150 GOSUB 2580:IF (IN<49)  OR (IN>52) THEN 1090
1160 ON IN-48 GOTO 1180,2270,2320,2490
1170 REM UITLEG
1180 GOSUB 600
1190 SR$="   UITLEG  PROCENT-BEREKENINGEN   "
1200 HO=0:VE=0:GOSUB 650
1210 H=0:V=.1:H1=.45:V1=.5:GOSUB 3260
1220 P=0:GOTO 1360
1230 HO=.5:VE=.1:SR$="Dit vierkant stelt":GOSUB 650
1240 VE=.2:SR$="het getal 100 voor":GOSUB 650
1250 VE=.3:SR$="Het hele vierkant is":GOSUB 650
1260 VE=.4:SR$="100 procent.(100%)":GOSUB 650
1270 VE=.5:SR$="Procent betekent:":GOSUB 650
1280 VE=.6:SR$="voor honderd.":GOSUB 650:P=P+1
1290 HO=0:VE=.7:SR$="Als je nu op een toets drukt delen we"
1300 GOSUB 650
1310 VE=.8:SR$="het vierkant in 100 stukjes van 1%":GOSUB 650
1320 IF P=2 THEN RETURN
1330 GOSUB 210
1340 HO=0:H2=.045:GOSUB 3300:VE=.1:V2=.05:GOSUB 3350:GOSUB 2660
1350 RETURN
1360 GOSUB 1230:CN=1:GOSUB 1230:CN=0
1370 P=0:GOTO 1540
1380 HO=0:VE=.1:GOSUB 620:HO=.045:GOSUB 630
1390 FOR N=1 TO 5
1400 HO=0:VE=VE+9E-03:GOSUB 620:HO=.045:GOSUB 630
1410 NEXT N
1420 HO=.5:VE=.1:SR$="Elk vierkantje is 1%":GOSUB 650
1430 VE=.2:SR$="of 1/100 deel van 't":GOSUB 650
1440 VE=.3:SR$="geheel. Daar gaan we ":GOSUB 650
1450 VE=.4:SR$="steeds mee beginnen. ":GOSUB 650
1460 VE=.5:SR$="Eerst 1% uitrekenen  ":GOSUB 650
1470 VE=.6:SR$="van het getal dat je ":GOSUB 650
1480 HO=0:VE=.7:SR$="gegeven krijgt. Dus delen door 100"
1490 GOSUB 650:P=P+1
1500 VE=.8:SR$="als het gegeven getal 100 % is. "
1510 GOSUB 650
1520 IF P=2 THEN RETURN
1530 GOSUB 2660:RETURN
1540 GOSUB 1380:CN=1:GOSUB 1420:CN=0
1550 CN=1:HO=0:VE=.55:GOSUB 620:HO=.45:GOSUB 630
1560 HO=0
1570 FOR N=1 TO 9
1580 HO=HO+H2:VE=.51:GOSUB 620:VE=.59:GOSUB 630
1590 NEXT N
1600 CN=0:HO=.5:VE=.1:SR$="Als nu gegeven is":GOSUB 650
1610 VE=.2:SR$="dat een getal":GOSUB 650
1620 VE=.3:SR$="80 % is dan is 1 %":GOSUB 650
1630 VE=.4:SR$="1/80 van dat getal.":GOSUB 650
1640 VE=.5:SR$="Dus het getal door ":GOSUB 650
1650 VE=.6:SR$="80 delen":GOSUB 650
1660 GOSUB 2660
1670 GOSUB 100:GOSUB 2820:PRINT:GOSUB 2770
1680 SR$="REKENEN MET PROCENTEN ":GOSUB 2780:GOSUB 2770
1690 PRINT:GOSUB 2770
1700 SR$="ALTIJD EERST NAAR 1%":GOSUB 2780
1710 GOSUB 2770:GOSUB 2580
1720 REM OEFENOPGAVEN
1730 GOSUB 100:HO=0:VE=4:GOSUB 110
1740 PRINT"DENK BIJ HET BEREKENEN OM HET VOLGENDE:":PRINT
1750 PRINT:PRINT"Geef je antwoord in vier decimalen."
1760 PRINT:PRINT"Als de 5e decimaal<5 dan verwaarlozen."
1770 PRINT:PRINT"Als de 5e decimaal>=5 dan 4e dec. +1 ."
1780 PRINT:PRINT
1810 PRINT:PRINT"Decimale komma is op computer een <.>."
1820 GOSUB 2580
1830 SR$="    oefenopgaven 1%               "
1840 V=1:S=1:R=0:GOSUB 3400
1850 CN=0
1860 GOSUB 600:SR$="  UITLEG PROCENT-BEREKENINGEN     "
1870 HO=0:VE=0:GOSUB 650
1880 H=0:V=.1:H1=.45:V1=.5:GOSUB 3260
1890 H0=0:H2=.045:GOSUB 3300:VE=.1:V2=.05:GOSUB 3350
1900 HO=.135:VE=.1:GOSUB 620:HO=.315:GOSUB 630
1910 FOR N=1 TO 5
1920 HO=.135:VE=VE+9E-03:GOSUB 620:HO=.315:GOSUB 630
1930 NEXT N
1940 HO=.5:VE=.1:SR$="Als het vierkant":GOSUB 650
1950 VE=.2:SR$="nu 320 voorstelt en":GOSUB 650:P=P+1
1960 VE=.3:SR$="100% is, is 1 vakje":GOSUB 650
1970 VE=.4:SR$="320 : 100 = 3.2":GOSUB 650
1980 VE=.5:SR$="4 vakjes zijn dus":GOSUB 650
1990 VE=.6:SR$="4 x 3.2 = 12.8":GOSUB 650
2000 HO=0:VE=.7:SR$="4 van 320 is dus 12.8. je kunt het"
2010 GOSUB 650
2020 VE=.8:SR$="ook zo berekenen 4/100 x 320 = 12.8"
2030 GOSUB 650
2040 GOSUB 2660
2050 GOSUB 100:HO=0:VE=4:GOSUB 110
2060 SR$="   oefenopgaven variabele %       "
2070 GOSUB 100:HO=0:VE=2:GOSUB 110
2080 PRINT"Nu gaan we de tweede stap doen."
2090 PRINT:PRINT"Als je 1% hebt uitgerekend kun je ook"
2100 PRINT:PRINT"andere percentages uitrekenen door "
2110 PRINT:PRINT"het getal dat 1% is te vermenigvuldigen"
2120 PRINT:PRINT"   MET HET GEVRAAGDE PERCENTAGE."
2130 PRINT:PRINT"   Getal is 1428. Hoeveel is 36%"
2140 PRINT:PRINT"1% = 14.28   36% = 36 x 14.28 = 514.08"
2150 PRINT:PRINT"Of 36/100 x 1428 = 514.08"
2160 PRINT:PRINT"Dit antwoord in 2 decimalen geven."
2170 GOSUB 2580
2180 GOSUB 2820:PRINT:GOSUB 2770
2190 SR$="REKENEN MET PROCENTEN ":GOSUB 2780
2200 GOSUB 2770:PRINT:GOSUB 2770
2210 SR$="HOEVEEL % IS HET GEGEVEN":GOSUB 2780
2220 GOSUB 2770:GOSUB 2770
2230 SR$="EERST 1% UITREKENEN ":GOSUB 2780
2240 GOSUB 2770:GOSUB 2770
2250 SR$="VERM. MET GEVR. PERCENTAGE":GOSUB 2780
2260 GOSUB 2770:GOSUB 2580
2270 SR$="   oefenopgaven variabele %       "
2280 V=2:R=0:S=2:GOSUB 3400
2290 SR$="    opgaven procentberekening     "
2300 S=3:GOSUB 3430
2310 REM TOETS
2320 GOSUB 100:HO=0:VE=5:GOSUB 110
2330 PRINT"Je krijgt nu een toets met 10 opgaven"
2340 PRINT:PRINT"Daarna kun je niet verder maar laat"
2350 PRINT:PRINT"je eerst de behaalde score aan je"
2360 PRINT:PRINT"leraar zien.":GOSUB 2580
2370 SR$="         toets procenten          "
2380 S=4:GOSUB 3400:Z1=0:Z2=0:Z3=0:Z4=0:Z5=0:Z6=0:Z7=0:Z8=0
2390 FOR N=1 TO 5
2400 GOSUB 260:Z=(INT(RV*8))+1
2410 IF (Z=Z1)  OR (Z=Z2)  OR (Z=Z3)  OR (Z=Z4)  OR (Z=Z5) THEN 2400
2420 IF (Z=6)  OR (Z=Z7)  OR (Z=Z8) THEN 2400
2430 GOSUB 2970:GOSUB 3940
2440 NEXT N
2450 HO=1:VE=VT:GOSUB 110
2460 PRINT"laat dit resultaat aan de leraar zien";
2470 GOSUB 210:IF IN<>67 THEN 2470
2480 REM EINDROUTINE
2490 GOSUB 100:HO=0:VE=4:GOSUB 110:GOSUB 2770:GOSUB 150
2500 SR$="  DIT IS HET EINDE VAN PROCENTEN  ":GOSUB 150
2510 GOSUB 2770
2520 VE=10:GOSUB 110:GOSUB 2770:GOSUB 150
2530 SR$="                  TOT ZIENS       ":GOSUB 150
2540 GOSUB 2770
2550 HO=23:VE=20:GOSUB 110:SR$="D.MAASKANT":GOSUB 150
2560 SD=40:GOSUB 450:GOTO 950
2570 REM SUBROUTINES%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
2580 HO=1:VE=VT-1:GOSUB 110
2590 PRINT"======================================"
2600 PRINT"   Druk op een toets (<M> is menu)    ";
2610 GOSUB 210:IF IN=77 THEN 1090
2620 RETURN
2630 HO=0:VE=VT:GOSUB 110
2640 PRINT"  Druk op een toets                  ";
2650 GOSUB 210:RETURN
2660 SR$="====================================="
2670 HO=0:VE=.93:GOSUB 650
2680 SR$="Druk op een toets (<M> is menu)"
2690 HO=0:VE=.96:GOSUB 650
2700 GOSUB 210:IF IN=77 THEN 1090
2710 RETURN
2720 HO=1:VE=VT-1:GOSUB 110
2730 PRINT"======================================"
2740 HO=1:VE=VT:GOSUB 110
2750 PRINT"geef eindantwoord en druk op <RETURN>";:RETURN
2760 REM LEGE REGEL
2770 SR$="  "
2780 LL=INT((HT-LEN(SR$))/2)-2:LS$=""
2790 IF LL>0 THEN LS$=LEFT$(SP$,LL)
2800 SR$=LS$+SR$+LS$
2810 GOSUB 150:RETURN
2820 GOSUB 100:PRINT
2830 SR$="----------------------------------":GOSUB 2780
2840 SR$="  LEER DEZE REGELS UIT JE HOOFD   ":GOSUB 2780
2850 SR$="----------------------------------":GOSUB 2780
2860 RETURN
2870 REM VOETNOTEN
2880 IF V=1 THEN SR$="delen door getal voor procentteken"
2890 IF V=2 THEN SR$="  1% x getal voor procentteken    "
2900 IF V=3 THEN SR$="    uitleg procentberekening      "
2910 IF V=4 THEN SR$=" geef eindantwoord in 2 decimalen "
2920 IF V=5 THEN SR$="        goed              fout    "
2930 HO=0:VE=VT-2:GOSUB 110:GOSUB 150:RETURN
2940 REM SCHEIDINGSSTREEP
2950 PRINT"--------------------------------------":RETURN
2960 REM SCHERM SCHOON
2970 HO=0:VE=0:GOSUB 110
2980 FOR I=1 TO 20
2990 VE=VE+1:GOSUB 110
3000 PRINT SP$
3010 NEXT I
3020 RETURN
3030 REM SCHERMINDELING
3040 IF P2<10 THEN T=16
3050 IF (9<P2)  AND (P2<100) THEN T=15
3060 IF P2>99 THEN T=14
3070 RETURN
3080 REM GETALLEN KIEZEN
3090 GOSUB 260:G1=INT(RV*1000):IF G1=0 THEN 3090
3100 SR=G1:GOSUB 300:G1$=SR$:SR$=""
3110 GOSUB 260:G2=INT(RV*20):IF G2<4 THEN 3110
3120 GOSUB 260:G3=INT(RV*10)+2:IF G3=G2 THEN 3120
3130 GOSUB 260:G4=INT(RV*4)+1
3140 GOSUB 260:P1=INT(RV*100):IF P1<2 THEN 3140
3150 SR=P1:GOSUB 300:P1$=SR$:SR$=""
3160 GOSUB 260:P2=INT(RV*100)+20
3170 SR=P2:GOSUB 300:P2$=SR$:SR$=""
3180 RETURN
3190 REM AFRONDEN
3200 SR=A:CT=7:CN=4:GOSUB 310:A$=SR$:RETURN
3210 SR=B:CT=7:CN=2:GOSUB 310:B$=SR$:RETURN
3220 CT=7:CN=4:GOSUB 310:PRINT SR$:RETURN
3230 CT=7:CN=2:GOSUB 310:PRINT SR$:RETURN
3240 CT=8:CN=4:GOSUB 310:PRINT SR$:RETURN
3250 REM VIERKANT
3260 HO=H:VE=V:GOSUB 620:HO=HO+H1:GOSUB 630:VE=VE+V1:GOSUB 630
3270 HO=HO-H1:GOSUB 630:VE=VE-V1:GOSUB 630
3280 RETURN
3290 REM VERDELEN VERT.
3300 FOR N=1 TO 9
3310 HO=HO+H2:VE=.1:GOSUB 620:VE=.6:GOSUB 630
3320 NEXT N
3330 RETURN
3340 REM VERDELEN HOR.
3350 FOR N=1 TO 9
3360 HO=0:VE=VE+V2:GOSUB 620:HO=.45:GOSUB 630
3370 NEXT N
3380 RETURN
3390 REM OPGAVEN
3400 GOSUB 100:HO=0:VE=0:GOSUB 110:GOSUB 150:GOSUB 2880
3410 GOSUB 2720
3420 VE=0
3430 ON S GOTO 3440,3560,3920,3790
3440 FOR I=1 TO 10
3450 HO=2:VE=VE+2:GOSUB 110
3460 GOSUB 3090
3470 IF R>4 THEN 3510
3480 R=R+1:PRINT"100% =";G1$;
3490 HO=16:GOSUB 110:PRINT"1%=";:INPUT A:GOSUB 3200
3500 X=G1/100:GOSUB 5790:GOTO 3740
3510 T=3:IF P1<10 THEN T=4
3520 HO=T:GOSUB 110:PRINT P1$;"%";
3530 HO=7:GOSUB 110:PRINT"=";G1$;:HO=16:GOSUB 110:PRINT"1%=";
3540 INPUT A:GOSUB 3200
3550 X=G1/P1:GOSUB 5790:GOTO 3740
3560 GOSUB 2720:VE=0
3570 FOR I=1 TO 5
3580 HO=2:VE=VE+2:GOSUB 110
3590 GOSUB 3090
3600 IF R>4 THEN 3670
3610 R=R+1:PRINT"100% =";G1$;
3620 HO=16:GOSUB 110:PRINT"1% =";:INPUT A:GOSUB 3200
3630 X=G1/100:GOSUB 5790:VE=VE+2:GOSUB 110
3640 GOSUB 3040
3650 HO=T:GOSUB 110:PRINT P2$;"% =";:INPUT B:GOSUB 3210
3660 Y=X*P2:GOSUB 5820:GOTO 3740
3670 R=R+1:T=1:IF P1<10 THEN T=2
3680 HO=T:GOSUB 110:PRINT P1$;"%";"=";G1$;
3690 HO=14:GOSUB 110:PRINT"1% =";:INPUT A:GOSUB 3200
3700 X=G1/P1:GOSUB 5790:VE=VE+2:GOSUB 110
3710 GOSUB 3040
3720 HO=T-2:GOSUB 110:PRINT P2$;"% =";:INPUT B:GOSUB 3210
3730 Y=X*P2:GOSUB 5820
3740 NEXT I
3750 GOSUB 2580
3760 IF (S=2)  AND (R<10) THEN GOSUB 2970:GOTO 3560
3770 RETURN
3780 REM TOETSOPGAVEN
3790 V=5:GOSUB 2880:GOSUB 2720:G=0:F=0:GOSUB 5900:VE=0
3800 FOR I=1 TO 5
3810 GOSUB 3090
3820 HO=2:VE=VE+2:GOSUB 110
3830 T=1:IF P1<10 THEN T=2
3840 HO=T:GOSUB 110:PRINT P1$;"%";:HO=7:GOSUB 110:PRINT"=";G1$;
3850 HO=14:GOSUB 110:PRINT P2$;"% =";
3860 INPUT B:GOSUB 3210
3870 Y=INT(G1/P1*P2*100+.5)/100
3880 GOSUB 5820:VF=VE:GOSUB 5900
3890 NEXT I
3900 GOSUB 2630:GOSUB 2970:RETURN
3910 REM TEKSTOPGAVEN
3920 GOSUB 100:HO=0:VE=0:GOSUB 110:GOSUB 150
3930 V=4:GOSUB 2880:GOTO 3950
3940 ON Z GOTO 3950,4160,4400,4640,4920,5120,5340,5570
3950 GOSUB 2720:GOSUB 3090:HO=0:VE=2:GOSUB 110:Z1=Z
3960 PRINT"Geg : 1 liter benzine kost 1.34 ex.BTW"
3970 PRINT"      BTW = 20 %"
3980 PRINT:PRINT"Gevr: Wat kost ";G2;" liter incl.BTW"
3990 PRINT:PRINT"Antw: ";G2;" liter kost  ";:INPUT B
4000 GOSUB 3210
4010 Y=INT(G2*1.2*1.34*100+.5)/100:VE=7:GOSUB 5820:GOSUB 2950
4020 IF V=5 THEN GOSUB 5900:GOSUB 2630:RETURN
4030 IF L$=B$ THEN 4140
4040 S=G2*1.34:T=S/100:U=T*20:W=INT((S+U)*100+.5)/100
4050 V=3:GOSUB 2880:HO=0:VE=12
4060 GOSUB 110
4070 PRINT"Ber :";G2;" l kost ";G2;" x 1.34 =";S
4080 PRINT:PRINT TAB(6);"1% van ";S;"=";
4090 SR=T:GOSUB 3220
4100 PRINT:PRINT TAB(6);"20% van ";S;"=";
4110 SR=U:GOSUB 3220
4120 PRINT:PRINT TAB(5);G2;" l incl BTW kost f ";
4130 SR=W:GOSUB 3230
4140 GOSUB 2580:GOSUB 2970
4150 V=4:GOSUB 2880
4160 GOSUB 2720:GOSUB 3090:HO=0:VE=2:GOSUB 110:Z8=Z
4170 PRINT"Geg :Een nieuwe brommer kost f 2495"
4180 PRINT"     excl.20% BTW"
4190 PRINT"     afschrijving per jaar 17%"
4200 PRINT:PRINT"Gevr:Hoeveel is de waarde na ";G4;" jaar"
4210 PRINT:PRINT"Antw:Waarde na ";G4;" jr";:INPUT B
4220 GOSUB 3210
4230 Y=INT((2994-(G4*508.98))*100+.5)/100
4240 VE=8:GOSUB 5820:GOSUB 2950
4250 IF V=5 THEN GOSUB 5900:GOSUB 2630:RETURN
4260 IF L$=B$ THEN 4380
4270 V=3:GOSUB 2880
4280 U=508.98*G4:W=2994-U
4290 HO=0:VE=12:GOSUB 110
4300 PRINT"Ber :Incl. BTW kost de brommer "
4310 PRINT TAB(5);"2495 + 20% = 2495 + 499 = 2994"
4320 PRINT:PRINT TAB(5);"Afschrijving per jaar 17% van 2994"
4330 PRINT TAB(7);" 17 x 29.49 = 508.98"
4340 PRINT:PRINT TAB(5);"afschrijving in ";G4;" jaar ";
4350 SR=U:GOSUB 3230:U$=SR$
4360 PRINT:PRINT TAB(5);"waarde 2994 -";U$;"=f ";
4370 SR=W:GOSUB 3230
4380 GOSUB 2580:GOSUB 2970
4390 V=4:GOSUB 2880
4400 GOSUB 2720:GOSUB 3090:HO=0:VE=2:GOSUB 110:Z2=Z
4410 PRINT"Geg : 1 nacht op een camping kost f";G2
4420 PRINT"      Daarbij komt nog 20% BTW"
4430 PRINT"      en 7% toeristenbelasting"
4440 PRINT:PRINT"Gevr: Hoeveel kosten ";G3;" nachten"
4450 PRINT:PRINT"Antw: ";G3;" nachten  ";:INPUT B:GOSUB 3210
4460 Y=INT(G3*(G2+27*G2/100)*100+.5)/100
4470 VE=8:GOSUB 5820:GOSUB 2950
4480 IF V=5 THEN GOSUB 5900:GOSUB 2630:RETURN
4490 IF L$=B$ THEN 4620
4500 V=3:GOSUB 2880
4510 S=G2/100:T=27*S:U=G2+T:W=G3*U
4520 HO=0:VE=12:GOSUB 110
4530 PRINT"Ber : 1 nacht kost f";G2;"+ 27%"
4540 PRINT:PRINT TAB(6);"1% =";
4550 SR=S:GOSUB 3220:S$=SR$
4560 PRINT:PRINT TAB(6);"27% = 27 x";S$;"=";
4570 SR=T:GOSUB 3220:T$=SR$
4580 PRINT:PRINT TAB(6);"1 nacht kost ";G2;"+";T$;"=";
4590 SR=U:GOSUB 3230:U$=SR$
4600 PRINT:PRINT TAB(5);G3;" nach. = ";G3;"x";U$;"=f";
4610 SR=W:GOSUB 3230
4620 GOSUB 2580:GOSUB 2970
4630 V=4:GOSUB 2880
4640 GOSUB 2720:GOSUB 3090:HO=0:VE=2:GOSUB 110:Z3=Z
4650 PRINT"Geg : 1 l grondverf kost 14.75 ex BTW"
4660 PRINT"      1 l is voldoende voor 12 m2"
4670 PRINT"      je kunt niet minder kopen dan 1l"
4680 PRINT"      BTW is 20%"
4690 PRINT
4700 PRINT"Gevr: Prijs voor ";G2+12;" m2 incl.BTW"
4710 PRINT:PRINT"Ber : De verf kost ";:INPUT B:GOSUB 3210
4720 Y=INT((INT((G2+12)/12)+1)*1.2*14.75*100+.5)/100:VE=9
4730 GOSUB 5820:GOSUB 2950
4740 IF V=5 THEN GOSUB 5900:GOSUB 2630:RETURN
4750 IF L$=B$ THEN 4900
4760 V=3:GOSUB 2880
4770 S1=G2+12:S2=INT(S1/12)+1:S=S2*14.75:T=S/100
4780 U=T*20:W=S+U
4790 HO=0:VE=11:GOSUB 110
4800 PRINT TAB(6);
4810 PRINT"Voor ";S1;" m2 ";S1;" : 12 =";S2;" liter"
4820 PRINT:PRINT TAB(5);S2;" l kost ";S2;" x 14.75 =";
4830 SR=S:GOSUB 3230:S$=SR$
4840 PRINT:PRINT TAB(6);"1% van";S$;"=";
4850 SR=T:GOSUB 3220:T$=SR$
4860 PRINT:PRINT TAB(6);"20% = 20 X";T$;"=";
4870 SR=U:GOSUB 3230:U$=SR$
4880 PRINT:PRINT TAB(5);S2;"l kost";S$;"+";U$;"=f";
4890 SR=W:GOSUB 3230
4900 GOSUB 2580:GOSUB 2970
4910 V=4:GOSUB 2880
4920 GOSUB 2720:GOSUB 3090:HO=0:VE=2:GOSUB 110:Z4=Z
4930 PRINT"Geg : Bij het stansen van ronde"
4940 PRINT"      plaatjes is het rendement 78%"
4950 PRINT"      Er is ";G2;" m2 plaatjes besteld"
4960 PRINT:PRINT"Gevr: Hoeveel m2 plaat is nodig"
4970 PRINT:PRINT"Antw: Benodigde m2";:INPUT B:GOSUB 3210
4980 Y=INT(G2/78*100*100+.5)/100:VE=8:GOSUB 5820:GOSUB 2950
4990 IF V=5 THEN GOSUB 5900:GOSUB 2630:RETURN
5000 IF L$=B$ THEN 5100
5010 S=G2/78:T=100*S
5020 V=3:GOSUB 2880
5030 HO=0:VE=12:GOSUB 110
5040 PRINT"Ber :";G2;"m2 is 78% van wat nodig is"
5050 PRINT:PRINT TAB(6);"1% =";G2;"/78 = ";
5060 SR=S:GOSUB 3220:S$=SR$
5070 PRINT:PRINT TAB(6);"Er is totaal nodig 100%"
5080 PRINT:PRINT TAB(6);"100 x";S$;"= ";
5090 SR=T:GOSUB 3230
5100 GOSUB 2580:GOSUB 2970
5110 V=4:GOSUB 2880
5120 GOSUB 2720:GOSUB 3090:HO=0:VE=2:GOSUB 110:Z5=Z
5130 PRINT"Geg : Isolatie-kosten van een woning "
5140 PRINT"      f 8500. Het rendement is";G2;"%"
5150 PRINT"      Gasverbruik 2800 m3 per jaar"
5160 PRINT"      1 m3 gas kost f0.45"
5170 PRINT:PRINT"Gevr: In hoeveel jaar is dit betaald"
5180 PRINT:PRINT"Antw: Aantal jaar";:INPUT B:GOSUB 3210
5190 Y=INT(8500/(G2*28*.45)*100+.5)/100:VE=9:GOSUB 5820
5200 GOSUB 2950
5210 IF V=5 THEN GOSUB 5900:GOSUB 2630:RETURN
5220 IF L$=B$ THEN 5320
5230 V=3:GOSUB 2880
5240 S=G2*28:T=S*.45:U=8500/T
5250 HO=0:VE=12:GOSUB 110
5260 PRINT:PRINT"Ber :";G2;"%=";G2;"x 28.00 =";
5270 SR=S:GOSUB 3240:S$=SR$
5280 PRINT:PRINT TAB(6);"besp.p/j ";S$;"x 0.45 =";
5290 SR=T:GOSUB 3240:T$=SR$
5300 PRINT:PRINT TAB(6);"8500 :";T$;"=";
5310 SR=U:GOSUB 3230
5320 GOSUB 2580:GOSUB 2970
5330 V=4:GOSUB 2880
5340 GOSUB 2720:GOSUB 3090:HO=0:VE=2:GOSUB 110:Z6=Z
5350 PRINT"Geg : Je weekloon is f 185.00 bruto"
5360 PRINT"      loonbelasting  is ";G2+5;"%"
5370 PRINT"      sociale lasten zijn ";G2;"%"
5380 PRINT:PRINT"Gevr: hoeveel krijg je in handen"
5390 PRINT:PRINT"Antw: Netto weekloon is ";:INPUT B
5400 GOSUB 3210
5410 Y=INT((185-((G2+G2+5)*1.85))*100+.5)/100
5420 VE=8:GOSUB 5820:GOSUB 2950
5430 IF V=5 THEN GOSUB 5900:GOSUB 2630:RETURN
5440 IF L$=B$ THEN 5550
5450 V=3:GOSUB 2880
5460 S=(G2+5)*1.85:T=G2*1.85:U=185-S-T
5470 HO=0:VE=12:GOSUB 110
5480 PRINT"Ber : 1% van 185.00 = 1.8500"
5490 PRINT:PRINT TAB(6);G2+5;"x 1.85 = ";
5500 SR=S:GOSUB 3220:S$=SR$
5510 PRINT:PRINT TAB(6);G2;"x 1.85 = ";
5520 SR=T:GOSUB 3220:T$=SR$
5530 PRINT:PRINT TAB(6);"185 -(";S$;"+";T$;")=f";
5540 SR=U:GOSUB 3230
5550 GOSUB 2580:GOSUB 2970
5560 V=4:GOSUB 2880
5570 GOSUB 2720:GOSUB 3090:HO=0:VE=2:GOSUB 110:Z7=Z
5580 PRINT"Geg :Je hebt nodig ";G2;" m3 beton"
5590 PRINT"     De uitlevering is 80 %"
5600 PRINT"(dat is wat je over hebt na het mengen)"
5610 PRINT:PRINT"Gevr:Hoeveel m3 grondstof is nodig"
5620 PRINT:PRINT"Antw:Nodig aan grondstof ";:INPUT B
5630 GOSUB 3210:Y=INT(((G2/80)*100)*100+.5)/100
5640 VE=9:GOSUB 5820:GOSUB 2950
5650 IF V=5 THEN GOSUB 5900:GOSUB 2630:RETURN
5660 IF L$=B$ THEN 5760
5670 V=3:GOSUB 2880
5680 S=G2/80:T=S*100
5690 HO=0:VE=12:GOSUB 110
5700 PRINT"Ber : 1% van ";G2;" m3 =";G2;":80 =";
5710 SR=S:GOSUB 3220:S$=SR$
5720 PRINT:PRINT TAB(6);"100 x";S$;"m3 =";
5730 SR=T:GOSUB 3230:T$=SR$
5740 PRINT:PRINT TAB(6);"Je hebt dus ";SR$;" m3 grondstof "
5750 PRINT:PRINT TAB(6);"nodig om ";G2;" m3 beton te maken"
5760 GOSUB 2580:GOSUB 2970
5770 RETURN
5780 REM ANTWOORDCONTROLE
5790 SR=X:CT=7:CN=4:GOSUB 310:K$=SR$
5800 IF VAL(A$)=VAL(K$) THEN 5840
5810 IF A$<>K$ THEN 5850
5820 SR=Y:CT=7:CN=2:GOSUB 310:L$=SR$
5830 IF VAL(B$)<>VAL(L$) THEN 5870
5840 HO=32:GOSUB 110:PRINT"GOED":G=G+1:HO=2:RETURN
5850 HO=30:GOSUB 110:CT=8:CN=4:SR=X:GOSUB 310:K$=SR$
5860 PRINT K$:A$=K$:GOTO 5880
5870 HO=28:GOSUB 110:CT=8:CN=2:SR=Y:GOSUB 310:L$=SR$:PRINT L$
5880 F=F+1:HO=2:RETURN
5890 REM G/F TELLER
5900 HO=6:VE=VT-2:GOSUB 110:PRINT G:HO=23:GOSUB 110:PRINT F
5910 VE=VF:RETURN
30000 REM ****************************
30010 REM NA TOETS DE <C> INDRUKKEN
30020 REM ****************************
30030 REM 
30040 REM PROCENTEN
30050 REM 
30060 REM GEMAAKT OP EEN SONY HB201P
30070 REM MSX-COMPUTER
30080 REM GECONTROLEERD OP BBC-MASTER
30090 REM 
32000 REM D.MAASKANT
32010 REM RUYSDAELLAAN 5
32020 REM 3931 TR WOUDENBERG
32030 REM 
32040 REM TROS-RADIO dd 871028
32050 REM Corr.dd 880218

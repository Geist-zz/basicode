1000 a=500:GOTO 20:REM zoek het woord
1010 s=1:p=0:kr=0:rr=0
1020 t$="ABCDEFGHIJKLMNOPQRSTUVWXYZ"
1030 ht$="abcdefghijklmnopqsrtuvwxyz"
1040 hk=ASC(LEFT$(t$,1))-ASC(LEFT$(ht$,1))
1050 DIM r$(8,8)
1060 GOSUB 100
1070 PRINT"Z O E K  H E T  W O O R D"
1080 PRINT 
1090 PRINT"Wilt U instrukties";
1100 INPUT a$:a$=LEFT$(a$,1)
1110 IF (a$="J") OR (a$="j") THEN GOSUB 2810
1120 GOSUB 1230:REM ?moeilijkh.
1130 GOSUB 1380:REM zoek woord
1140 GOSUB 1480:REM zoek patroon
1150 GOSUB 1940:REM druk matrix
1160 GOSUB 2080:REM vraag oploss.
1170 IF xo=0 THEN 1150
1180 GOSUB 2540:REM scores
1190 IF xo=0 THEN 2700
1200 GOSUB 2510:REM setup NEXT 
1210 GOTO 1120:REM new game
1220 REM vraag moeilijkheid en woordlengte
1230 PRINT 
1240 PRINT"Hoe moeilijk mag het worden"
1250 PRINT"(1 is eenvoudig en 2 moeilijker)";
1260 INPUT m
1270 IF m<1 THEN 1230
1280 IF m>2 THEN 1230
1290 PRINT 
1300 PRINT"Hoe lang mag het woord zijn"
1310 PRINT"(tussen 3 en 7 letters !)";
1320 INPUT l
1330 IF l<3 THEN 1290
1340 IF l>7 THEN 1290
1350 n=l+1
1360 RETURN
1370 REM zoek een woord v/d juiste lengte
1380 RESTORE
1390 READ a$
1400 IF LEN(a$)<>l THEN 1390
1410 GOSUB 260:q=15*rv+1
1420 FOR x=1 TO q:READ w$:NEXT x
1430 hw$="":FOR i=1 TO l
1440 hw$=hw$+CHR$(hk+ASC(MID$(w$,i,1)))
1450 NEXT i
1460 RETURN
1470 REM een 'woordpad' bij de moeilijkheidsgraad
1480 x=0
1490 GOSUB 260:r=int(rv*n+1)
1500 GOSUB 260:k=int(rv*n+1)
1510 IF (r<3) OR (r>n-2) THEN 1570
1520 IF (k<3) OR (k>n-2) THEN 1570
1530 x=x+1
1540 IF x=20 THEN stop
1550 REM na 20 keer houdt het progr. ermee op
1560 GOTO 1490
1570 FOR x=1 TO n
1580 FOR y=1 TO n
1590 GOSUB 260:q=int(rv*26+1)
1600 r$(x,y)=MID$(ht$,q,1)
1610 NEXT y
1620 NEXT x
1630 REM vul het woord in
1640 IF m=2 THEN 1820
1650 REM moeilijkheidsgraad 1, horiz. of vert.
1660 IF (r<3) OR (r>n-2) THEN 1750
1670 REM horizontaal woord dus
1680 IF k<3 THEN kr=1
1690 IF k>n-2 THEN kr=-1
1700 FOR x=1 TO LEN(w$)
1710 r$(k,r)=MID$(w$,x,1)
1720 k=k+kr
1730 NEXT x
1740 RETURN
1750 IF r<3 THEN rr=1
1760 IF r>n-2 THEN rr=-1
1770 FOR x=1 TO LEN(w$)
1780 r$(k,r)=MID$(w$,x,1)
1790 r=r+rr
1800 NEXT x
1810 RETURN
1820 REM moeilijkheidsgraad 2, diagonaal
1830 IF r<3 THEN rr=1
1840 IF r>n-2 THEN rr=-1
1850 IF k<3 THEN kr=1
1860 IF k>n-2 THEN kr=-1
1870 FOR x=1 TO LEN(w$)
1880 r$(k,r)=MID$(w$,x,1)
1890 k=k+kr
1900 r=r+rr
1910 NEXT x
1920 RETURN
1930 REM afdrukgedeelte
1940 GOSUB 100
1950 PRINT"====================";
1960 PRINT"  MOEILH= ";m
1970 FOR x=1 TO n
1980 PRINT 
1990 PRINT"     ";
2000 FOR y=1 TO n:PRINT r$(x,y);" ";:NEXT y
2010 PRINT 
2020 NEXT x
2030 PRINT 
2040 PRINT"====================";
2050 PRINT"  LENGTE= ";l
2060 RETURN
2070 REM vraag de oplossing
2080 PRINT 
2090 PRINT"Wat is het woord";
2100 INPUT a$
2110 p=p+1
2120 IF (a$=w$) OR (a$=hw$) THEN 2410
2130 PRINT 
2140 PRINT"Dat is NIET het goede woord"
2150 IF (p-2)=l THEN 2320
2160 IF p>2 THEN 2200
2170 PRINT 
2180 PRINT"Nog een keer proberen"
2190 GOTO 2270
2200 REM er is al >2 maal misgeraden
2210 h$=LEFT$(w$,p-2)
2220 PRINT 
2230 PRINT"Ik zal wat helpen.":PRINT 
2240 IF p>3 THEN 2290
2250 PRINT"De eerste letter is ";h$
2260 REM xo=0 betekent foute oplossing
2270 xo=0:FOR i=1 TO 3000:NEXT i
2280 RETURN
2290 REM meer dan 1 letter geven
2300 PRINT"De eerste ";p-2;" letters zijn ";h$
2310 GOTO 2270
2320 REM niet op tijd geraden
2330 PRINT:PRINT"JAMMER, maar het woord is NIET"
2340 PRINT:PRINT"geraden in ";p;" beurten"
2350 FOR i=1 TO 3000:NEXT i:GOSUB 1930
2360 PRINT:PRINT"Het was ";w$
2370 PRINT:PRINT"Verdergaan ";
2380 INPUT a$
2390 xo=-1
2400 RETURN
2410 REM juiste oplossing
2420 GOSUB 100
2430 PRINT"DAT IS HET GOEDE WOORD  !!!"
2440 PRINT 
2450 PRINT"en dat met ";p;" poging";
2460 IF p>1 THEN PRINT"en."
2470 PRINT 
2480 xo=1
2490 RETURN
2500 REM maak alles klaar voor volgend spel
2510 s=s+1:rr=0:kr=0:p=0
2520 RETURN
2530 REM rel.score, gem.score en volgend spel?
2540 PRINT 
2550 t=t+(l/p)*(l/p)*m
2560 PRINT"De score van dit spel ";(l/p)*(l/p)*m
2570 PRINT"(Maximaal mogelijk was ";l*l*m;")"
2580 PRINT 
2590 PRINT"De gemiddelde score tot nu toe over"
2600 PRINT"ALLE spelletjes ";t/s
2610 PRINT 
2620 PRINT 
2630 PRINT"Nog een spelletje ";
2640 INPUT a$:a$=LEFT$(a$,1)
2650 PRINT 
2660 IF (a$="J") OR (a$="j") THEN xo=1:RETURN
2670 IF (a$="N") OR (a$="n") THEN xo=0:RETURN
2680 PRINT"Type a.u.b. 'JA' of 'NEE'"
2690 GOTO 2620
2700 REM afscheid
2710 GOSUB 100
2720 PRINT"De EINDSCORE is       ";t
2730 PRINT"Het aantal spelletjes ";s
2740 PRINT"De GEMIDDELDE score   ";t/s
2750 PRINT 
2760 PRINT 
2770 PRINT"Ik hoop dat het leuk was"
2780 PRINT 
2790 PRINT"EINDE van 'ZOEK HET WOORD'"
2800 GOTO 950
2810 REM uitleg
2820 GOSUB 100
2830 PRINT"Probeer een woord te vinden in een"
2840 PRINT"vierkant blok met letters."
2850 PRINT"Dat woord kan op verschillende manieren"
2860 PRINT"in dat blok zijn verstopt :"
2870 PRINT 
2880 PRINT"HORIZONTAAL  of  VERTIKAAL"
2890 PRINT"   ..........    .........."
2900 PRINT"   ..........    ...w......"
2910 PRINT"   ..........    ...o......"
2920 PRINT"   ..woord...    ...o......"
2930 PRINT"   ..........    ...r......"
2940 PRINT"   ..........    ...d......"
2950 PRINT 
2960 PRINT"of DIAGONAAL (schuin)"
2970 PRINT"   ......."
2980 PRINT"   .w....."
2990 PRINT"   ..o...."
3000 PRINT"   ...o..."
3010 PRINT"   ....r.."
3020 PRINT"   .....d."
3030 PRINT"   ......."
3040 PRINT 
3050 PRINT"Verdergaan ";
3060 GOSUB 210
3070 GOSUB 100
3080 PRINT"Door bij de vraag hoe moeilijk"
3090 PRINT"het mag worden, een 1 in te typen,"
3100 PRINT"worden de woorden alleen horizontaal"
3110 PRINT"of vertikaal verstopt."
3120 PRINT"Door een 2 in te toetsen moet soms"
3130 PRINT"een diagonaal (schuin lopend) woord"
3140 PRINT"worden gezocht."
3150 PRINT 
3160 PRINT"Als na twee keer raden nog niet het"
3170 PRINT"juiste woord is geraden, gaat de"
3180 PRINT"computer eerst de eerste, daarna de"
3190 PRINT"tweede letter en tenslotte het hele"
3200 PRINT"woord geven."
3210 PRINT"Daarmee gaat natuurlijk wel het totaal"
3220 PRINT"aantal punten naar beneden."
3230 PRINT 
3240 PRINT"Verdergaan";
3250 GOSUB 210
3260 RETURN
25000 REM de woorden
25010 DATA "333","een","pop","gok","aal","kas"
25020 DATA "fit","vol","put"
25030 DATA "wit","nog","rit","het","dus","ook","bad"
25040 DATA "4444","vier","loop","kooi","maak","hooi","poes"
25050 DATA "fijn","klap","fris","goed","voor","vaat","hond"
25060 DATA "glas","lamp","deur"
25070 DATA "55555","fries","graat","klaar","tegel","maker"
25080 DATA "spoed","sloep","beest","paard","tafel","stoel"
25090 DATA "slaap","plant","kamer","radio","water"
25100 DATA "666666","yskast","gitaar","minuut","middag"
25110 DATA "kracht","kleren","keuken","kermis","honger"
25120 DATA "haring","gewoon","gevaar","geluid","domkop"
25130 DATA "dertig"
25140 DATA "7777777","noorden","groente","gezicht"
25150 DATA "gesprek","dREMpel","dochter","zeepsop"
25160 DATA "wanneer","vergiet","stengel","station"
25170 DATA "plezier","optocht","misdaad","meteoor"
30000 REM  z o e k  h e t  w o o r d
30010 REM
30020 REM  in de hobbyscoop-basicode
30030 REM  gemaakt door jos burghouts
30040 REM       op een
30050 REM    o.s.i. superboard.
30060 REM
30070 REM nu aangepast voor basicode-2

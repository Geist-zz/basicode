1000 A=500:GOTO 20:REM " Q  U I Z "
1010 GOSUB 100
1020 HO=8:VE=1:GOSUB 110:PRINT"EVEN GEDULD";
1030 DIM A(10),B(11),C$(10),D$(10),E$(10),F$(11)
1040 REM --- DATA tellen ---
1050 RESTORE :T=0
1060 READ A$:IF A$<>"*" THEN T=T+1:GOTO 1060
1070 RESTORE 
1080 REM === HOOFDPROGRAMMA ===
1090 GD=0:HO=8:VE=1:GOSUB 110:PRINT"EVEN GEDULD";
1100 REM --- B() vullen met 1-10---
1110 FOR N=1 TO 10:B(N)=N:NEXT N
1120 REM --- B() "geschud" naar A()--
1130 FOR N=1 TO 9:GOSUB 260:X=INT(1+RV*(11-N)):A(N)=B(X)
1140 FOR D=X TO 10:B(D)=B(D+1):NEXT D
1150 NEXT N:A(10)=B(1)
1160 REM --- titel lezen ---
1170 READ T$:P=P+1
1180 REM ---10 paren DATA inlezen ---
1190 FOR N=1 TO 10:READ A$:READ B$:P=P+2
1200 C$(A(N))=A$:E$(A(N))=B$:F$(A(N))=B$:NEXT N
1210 REM --- F$()"geschud"naar D$()---
1220 FOR N=1 TO 9:GOSUB 260:X=INT(1+RV*(11-N)):D$(N)=F$(X)
1230 FOR D=X TO 10:F$(D)=F$(D+1):NEXT D
1240 NEXT N:D$(10)=F$(1)
1250 GOSUB 110:PRINT T$;"            "
1260 REM --- C$ en D$ op het scherm---
1270 FOR N=1 TO 10
1280 HO=1:VE=N+3:GOSUB 110:PRINT CHR$(ASC("A")+N-1);" ";C$(N);
1290 SR=N:GOSUB 300:IF SR$="10" THEN SR$="0"
1300 PRINT TAB(20);SR$;" ";D$(N)
1310 NEXT N
1320 HO=1:VE=18:GOSUB 110:PRINT"Wat hoort bij elkaar?"
1330 FOR X=1 TO 10
1340 HO=1:VE=20:GOSUB 110
1350 PRINT CHR$(ASC("A")+X-1);" hoort bij nr: ";
1360 REM --- antwoorden innemen ---
1370 GOSUB 210:IF (IN<48) OR (IN>57) THEN 1370
1380 PRINT IN$:N=IN-48:IF N=0 THEN N=10
1390 REM --- antwoord toetsen aan E$()
1400 HO=1:VE=X+3:GOSUB 110
1410 IF E$(X)=D$(N) THEN GD=GD+1:PRINT"+":GOTO 1430
1420 PRINT"-"
1430 HO=18:VE=20:GOSUB 110:GOSUB 1640
1440 HO=1:VE=20:GOSUB 110:PRINT"                  "
1450 GOSUB 1640
1460 NEXT X
1470 REM --- de uitslag ---
1480 HO=1:VE=18:GOSUB 110
1490 PRINT"Je hebt er ";GD;" van de 10 goed."
1500 HO=1:VE=20:GOSUB 110:IF GD=10 THEN PRINT"GOED GEDAAN!"
1510 IF GD<10 THEN PRINT"Kijk maar hierboven."
1520 FOR X=1 TO 10:HO=20:VE=X+3:GOSUB 110
1530 PRINT"  ";LEFT$(E$(X)+"                   ",16)
1540 NEXT X
1550 HO=1:VE=22:GOSUB 110
1560 PRINT"WIL JE NOG EEN KEER?";
1570 GOSUB 210:IF (IN$="N") OR (IN$="n") THEN GOSUB 100:GOTO 950
1580 IF (IN$<>"J") AND (IN$<>"j") THEN 1570
1590 GOSUB 100
1600 IF P=T THEN P=0:RESTORE 
1610 GOTO 1090
1620 GOTO 950
1630 REM === ANTWOORD WACHTROUTINE ===
1640 SP=0:SV=0:SD=10:GOSUB 400
1650 RETURN
25000 REM ------------------
25010 DATA "RADIOPROGRAMMA'S"
25020 DATA "NIEUWS","NOS"
25030 DATA "BASICODE-3","TROS"
25040 DATA "HIER EN NU","NCRV"
25050 DATA "TIJDSEIN","EO"
25060 DATA "ECHO","KRO"
25070 DATA "VROEGE VOGELS","VARA"
25080 DATA "STRAUSS & CO","VOO"
25090 DATA "WILDE GANZEN","IKON"
25100 DATA "PRONTO","TELEAC"
25110 DATA "HET GEBOUW","VPRO"
25120 REM -----------------
25130 DATA "AARDRIJKSKUNDE"
25140 DATA "BELGIE","BRUSSEL"
25150 DATA "FINLAND","HELSINKI"
25160 DATA "ITALIE","ROME"
25170 DATA "GRIEKENLAND","ATHENE"
25180 DATA "INDONESIE","JAKARTA"
25190 DATA "SPANJE","MADRID"
25200 DATA "DENEMARKEN","KOPENHAGEN"
25210 DATA "IJSLAND","REYKJAVIK"
25220 DATA "JAPAN","TOKIO"
25230 DATA "HONGARIJE","BOEDAPEST"
25240 REM ------------------
25250 DATA "GRONDSTOFFEN"
25260 DATA "GERST","BIER"
25270 DATA "MELK","BOTER"
25280 DATA "KLEI","AARDEWERK"
25290 DATA "AARDOLIE","PLASTICS"
25300 DATA "DRUIVEN","WIJN"
25310 DATA "BIETEN","SUIKER"
25320 DATA "VLAS","LINNEN"
25330 DATA "BAUXIET","ALUMINIUM"
25340 DATA "LATEX","RUBBER"
25350 DATA "HOUT","PAPIER"
25360 REM -------------------
25370 DATA "TECHNIEK"
25380 DATA "SCHROEF","MOER"
25390 DATA "VIJL","AREND"
25400 DATA "COMPUTER","PROGRAMMA"
25410 DATA "COMPRESSOR","MANOMETER"
25420 DATA "METAALBOOR","CENTERPONS"
25430 DATA "VERFKWAST","TAKELING"
25440 DATA "SLEUTEL","BAARD"
25450 DATA "ACCU","ZUURWEGER"
25460 DATA "DEURSLOT","NACHTSCHOOT"
25470 DATA "SCHAKELAAR","KONTAKT"
25480 REM ------------------
25490 DATA "DIEREN"
25500 DATA "OLIFANT","SLURF"
25510 DATA "KANGOEROE","BUIDEL"
25520 DATA "KOE","HORENS"
25530 DATA "VIS","VINNEN"
25540 DATA "WESP","ANGEL"
25550 DATA "KREEFT","SCHAREN"
25560 DATA "WALVIS","BALEINEN"
25570 DATA "SLAK","HUISJE"
25580 DATA "SPIN","WEB"
25590 DATA "HERT","GEWEI"
25600 REM ------------------
25610 DATA "UITDRUKKINGEN"
25620 DATA "KOMMER","KWEL"
25630 DATA "VALLEN","OPSTAAN"
25640 DATA "HALEN","BRENGEN"
25650 DATA "MAKEN","BREKEN"
25660 DATA "REGEN","DRUP"
25670 DATA "GEVEN","NEMEN"
25680 DATA "JAN RAP","ZIJN MAAT"
25690 DATA "BOTER","VIS"
25700 DATA "EIEREN","GELD"
25710 DATA "PEPER","ZOUT"
25720 REM -----------------
25730 DATA "SPROOKJES"
25740 DATA "ROODKAPJE","WOLF"
25750 DATA "SNEEUWWITJE","DWERGEN"
25760 DATA "ALI BABA","ROVERS"
25770 DATA "HANS","GRIETJE"
25780 DATA "KLEIN DUIMPJE","REUS"
25790 DATA "KREKEL","MIER"
25800 DATA "DOORNROOSJE","SPINNEWIEL"
25810 DATA "HAAS","SCHILDPAD"
25820 DATA "VOS","RAAF"
25830 DATA "GEEST","FLES"
29999 DATA "*"
30000 REM Elk blok DATA bestaat uit een
30010 REM TITEL + 10 PAREN WOORDEN.
30020 REM Het aantal blokken is vrij
30030 REM 
30040 REM NA HET LAATSTE DATA-BLOK HET
30050 REM SLUITTEKEN "*" PLAATSEN.
30060 REM 
30070 REM Het programma leest telkens
30080 REM een TITEL + 10 opeenvolgende
30090 REM woordparen, de woordparen
30100 REM in "geschudde" volgorde.
30110 REM 
30120 REM De tweede kolom woorden wordt
30130 REM nogmaals "geschud" voor de
30140 REM afdruk op het scherm maar het
30150 REM origineel van deze kolom
30160 REM wordt bewaard voor de
30170 REM beoordeling van de gegeven
30180 REM antwoorden en voor de uitslag.
30190 REM 
30200 REM Door het "schudden" is de
30210 REM volgorde steeds weer anders.
30220 REM Toepassingen o.a. landen en
30230 REM hoofdsteden, jaartallen, en
30240 REM alles wat paarsgewijs bij
30250 REM elkaar hoort.
30260 REM =============================
32000 REM GEMAAKT OP EEN BBC COMPUTER
32010 REM DOOR
32020 REM Ch.W.Brederode
32030 REM Von Bonninghausenlaan 33
32040 REM 2161 ES  LISSE
32050 REM 
32060 REM TROS-RADIO dd 880921
32070 REM incl.corr. dd 881014

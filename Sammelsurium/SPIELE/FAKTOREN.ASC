1000 a=100:GOTO 20:REM  factors
1010 GOTO 2570:REM  initialization
1020 REM  start of main program
1030 GOSUB 100:PRINT"Good, we can start THEN ..."
1040 PRINT:PRINT 
1050 PRINT"Decide who will be player A and B."
1060 PRINT 
1070 PRINT"Tell me how far I should fill"
1080 PRINT"in the table.":PRINT 
1090 PRINT"So, choose a number":PRINT 
1100 PRINT"between 3 and 91";
1110 INPUT gi
1120 IF gi<92 AND gi>2 THEN 1190
1130 PRINT:PRINT"Your choice was  ";gi;" but that doesn't"
1140 PRINT"lie between 3 and 91 does it ?"
1150 PRINT:PRINT 
1160 PRINT"T r y   A g a i n ! "
1170 FOR i=1 TO 90*tf:NEXT i
1180 GOTO 1030
1190 be=0:GOSUB 2360
1200 GOSUB 2100:REM  fill in to gi
1210 FOR gw=2 TO gi:gt(gw)=1:NEXT gw:IF gi=91 THEN 1240
1220 FOR gw=gi+1 TO 91:gt(gw)=0
1230 NEXT gw
1240 sa=0:sb=0:REM  scores
1250 GOSUB 2450:GOSUB 2240:REM  print the results
1260 FOR i=1 TO 30*tf:NEXT i
1270 ve=21:ho=0:GOSUB 110:PRINT sp$:PRINT sp$:PRINT sp$;
1280 ve=21:ho=0:GOSUB 110
1290 PRINT"Player ";CHR$(65+be);" selects:  ";
1300 GOSUB 2160
1310 hg=18:gi=0:te=30:tp=0:cs=1:GOTO 1420
1320 GOSUB 2190:REM  get kr
1330 IF in$="" THEN 1390
1340 IF kr=13 THEN 1450
1350 kr=kr-48:IF kr<0 OR kr>9 THEN 1390
1360 gi=10*gi+kr
1370 ve=21:ho=18:GOSUB 110:sr=gi:GOSUB 300:PRINT sr$
1380 hg=hg+1
1390 te=te+1:IF te+te<tf THEN 1320
1400 te=0:ho=hg:ve=21:GOSUB 110
1410 PRINT MID$("ox",cs,1):cs=3-cs
1420 tp=tp+cs-1
1430 ve=10:ho=30:GOSUB 110:PRINT"Time: ";:sr=tp
1440 GOSUB 300:PRINT sr$;" ":GOTO 1320
1450 REM   number is chosen
1460 ho=hg:ve=21:GOSUB 110:PRINT" ";
1470 IF gi>91 THEN 1490
1480 IF gt(gi)=1 THEN 1540
1490 ve=22:ho=0:GOSUB 110
1500 PRINT"Sorry ";CHR$(65+be);", but that number has gone"
1510 PRINT"H a v e  A n o t h e r  G  o ! ! !    ";
1520 FOR i=1 TO 40*tf:NEXT i
1530 GOTO 1270
1540 PRINT"and gets ";
1550 sr=gi:GOSUB 300
1560 PRINT sr$;" pts."
1570 gw=gi:GOSUB 2300
1580 PRINT"  "
1590 FOR i=1 TO 15*tf:NEXT i
1600 ve=23:ho=0:GOSUB 110:PRINT"Player ";CHR$(66-be);
1610 sr=tp:GOSUB 300:PRINT" gets ";sr$;" pts";
1620 FOR i=1 TO 20*tf:NEXT i
1630 br=9
1640 FOR gw=2 TO gi-1
1650 IF gt(gw)=0 THEN 1790
1660 IF gi<>gw*INT(gi/gw) THEN 1790
1670 ct=2:cn=0:sr=gw:GOSUB 310
1680 GOSUB 2300:REM  position
1690 GOSUB 250:FOR i=1 TO 20
1700 GOSUB 110:IF 2*INT(i/2)=i THEN 1720
1710 PRINT sr$;:GOTO 1730
1720 PRINT"  ";
1730 FOR ii=1 TO 5:NEXT ii
1740 NEXT i
1750 tp=tp+gw
1760 ve=23:ho=14:GOSUB 110
1770 sr=tp:GOSUB 300
1780 PRINT sr$;" pts";
1790 NEXT gw
1800 gw=gi:GOSUB 2300:PRINT"  ";
1810 IF be=0 THEN sa=sa+gi:sb=sb+tp
1820 IF be=1 THEN sb=sb+gi:sa=sa+tp
1830 be=1-be
1840 FOR j=2 TO 91:IF gt(j)=1 THEN j=100:NEXT j:GOTO 1250
1850 NEXT j
1860 GOSUB 100:PRINT" * * *  F I N A L  S C O R E  * * * "
1870 PRINT" ********************************** "
1880 PRINT:PRINT 
1890 PRINT"Player A has ";sa;" points,"
1900 PRINT:PRINT 
1910 PRINT"Player B has ";sb;" points."
1920 w$="":IF sa>sb THEN w$="A"
1930 IF sa<sb THEN w$="B"
1940 IF w$="" THEN 1980
1950 PRINT:PRINT:PRINT 
1960 PRINT"So ";w$;" has won ! ! !"
1970 GOTO 2000
1980 PRINT:PRINT:PRINT 
1990 PRINT"  It's a tie ! ! !"
2000 FOR i=1 TO 120*tf:NEXT i
2010 ve=17:ho=0:GOSUB 110:PRINT"Want another game ?"
2020 GOSUB 2160
2030 PRINT:PRINT"Press Y or N"
2040 GOSUB 2190:IF in$="" THEN 2040
2050 in$=CHR$(kr)
2060 IF in$="y" OR in$="Y" THEN 1030
2070 PRINT"Shame, I was enjoying it."
2080 GOTO 950
2090 REM  subroutine fill in to g1
2100 ct=2:cn=0
2110 FOR sr=2 TO gi:gw=sr
2120 GOSUB 2300:GOSUB 310:PRINT sr$
2130 NEXT sr
2140 RETURN
2150 REM  sr empty inputbuffer
2160 GOSUB 200:IF in$<>"" THEN 2160
2170 RETURN
2180 REM  sr get ascii chr kr
2190 GOSUB 200:IF in$="" THEN RETURN
2200 kr=ASC(in$)
2210 IF kr>95 THEN kr=kr-32:GOTO 2210
2220 RETURN
2230 REM  sr PRINT  the score
2240 ve=6:ho=32:GOSUB 110:sr=sa:GOSUB 300:PRINT sr$
2250 ve=17:GOSUB 110:sr=sb:GOSUB 300:PRINT sr$
2260 GOSUB 250
2270 FOR i=1 TO 3*tf:NEXT i
2280 RETURN
2290 REM  sr positioner FOR  gw
2300 re=INT((gw-2)/9):REM  regel
2310 ko=gw-9*re:REM  kolom
2320 ve=re+re+1:ho=3*ko-5:GOSUB 110
2330 gt(gw)=0
2340 RETURN
2350 REM  sr set new table
2360 GOSUB 100
2370 bo$="+--------------------------+"
2380 tr$="!--+--+--+--+--+--+--+--+--!"
2390 cr$="!  !  !  !  !  !  !  !  !  !"
2400 PRINT bo$:PRINT cr$
2410 FOR i=1 TO 9:PRINT tr$:PRINT cr$:NEXT i
2420 PRINT bo$
2430 RETURN
2440 REM  subroutine score windows
2450 FOR i=0 TO 1
2460 ho=30:ve=1+11*i:GOSUB 110:PRINT"+--------+"
2470 ve=ve+1:GOSUB 110
2480 PRINT"!Player ";CHR$(65+i);"!"
2490 ve=ve+1:GOSUB 110:PRINT"!        !"
2500 ve=ve+1:GOSUB 110:PRINT"! Score: !"
2510 FOR ii=1 TO 3
2520 ve=ve+1:GOSUB 110:PRINT"!        !"
2530 NEXT ii
2540 ve=ve+1:GOSUB 110:PRINT"+--------+"
2550 NEXT i
2560 RETURN
2570 sp$="                                       "
2580 DIM gt(91)
2590 tf=30:REM  this is the time
2600 REM  factor. make it larger
2610 REM  FOR  faster computers,
2620 REM  smaller on slower.
2630 GOSUB 100
2640 PRINT"        F  A  C  T  O  R  S  "
2650 GOSUB 2800
2660 PRINT:PRINT:PRINT:PRINT:PRINT:PRINT 
2670 PRINT" An educational game for two players."
2680 GOSUB 2800
2690 PRINT:PRINT:PRINT:PRINT 
2700 PRINT" Need instructions ?"
2710 PRINT:PRINT"                (answer with Y or N )"
2720 GOSUB 2160:REM  flush buffer
2730 GOSUB 2190:IF in$="" THEN 2730
2740 in$=CHR$(kr)
2750 IF in$="n" OR in$="N" THEN 1030
2760 IF in$<>"y" AND in$<>"Y" THEN 2730
2770 GOSUB 100:GOSUB 2850:GOSUB 100:gi=25:GOTO 1190
2780 GOTO 1030:REM  program start
2790 REM  sr wait until in$
2800 FOR i=1 TO 100
2810 GOSUB 200:IF in$<>"" THEN i=100
2820 NEXT i
2830 RETURN
2840 REM  sruitleg
2850 RESTORE:co=1
2860 GOSUB 2160:REM  flush buffer
2870 READ p$
2880 IF p$="*" THEN FOR i=1 TO 1000:NEXT i:GOSUB 100:GOTO 2870
2890 GOSUB 200:IF in$="" THEN 2910
2900 RETURN
2910 IF p$<>"$" THEN 2970
2920 FOR i=1 TO 800:NEXT i
2930 GOSUB 2360:REM  PRINT  table
2940 gi=91:GOSUB 2090:REM  fill it
2950 FOR i=1 TO 2000:NEXT i
2960 GOTO 2870
2970 IF p$="#" THEN RETURN
2980 FOR i=1 TO LEN(p$)
2990 t$=MID$(p$,i,1)
3000 IF t$="&" THEN PRINT:GOTO 3040
3010 IF t$="*" THEN GOSUB 120:GOTO 3040
3020 IF t$="=" THEN GOSUB 110:GOTO 3040
3030 PRINT t$;
3040 FOR ii=1 TO tf:NEXT ii
3050 NEXT i
3060 FOR ii=1 TO 200:NEXT ii:PRINT:GOTO 2870
25000 DATA "          F A C T O R S  "
25010 DATA "&&A game for two people."
25020 DATA "&The player who starts may choose a"
25030 DATA "number from the table."
25040 DATA "&&Hang on, you haven't played this yet"
25050 DATA "so I had better let you see"
25060 DATA "the table..."
25070 DATA "&&A moment please."
25080 DATA "$"
25090 DATA "*"
25100 DATA "&&See what I mean?"
25110 DATA "&You saw numbers in the table between"
25120 DATA "2 and 91.  "
25130 DATA "&The player who starts may choose"
25140 DATA "a number from the table."
25150 DATA "&That number also becomes his/her"
25160 DATA "score !!"
25170 DATA "&&&&&So, if you choose 60, you get 60"
25180 DATA "points."
25190 DATA "&&But......"
25200 DATA "*"
25210 DATA "It is a bit more complicated."
25220 DATA "&Suppose you pick 60,"
25230 DATA "&then I quickly calculate that 60 "
25240 DATA "has a number of factors."
25250 DATA "&Namely:   2, 3, 4, 5, 6, 10, 12, 15,"
25260 DATA "20 and 30."
25270 DATA "&Added up that means 107."
25280 DATA "&And that means the OTHER player"
25290 DATA "gets those 107 points."
25300 DATA "*"
25310 DATA "Oh, and one more thing...."
25320 DATA "&&While you're figuring out which"
25330 DATA "number to choose,   "
25340 DATA "time ticks onward...   "
25350 DATA "second by second..... "
25360 DATA "&    *1","    =2","    =3","    =4"
25370 DATA "    =5","    =6","    =7","    =8"
25380 DATA "    = "
25390 DATA "&&See that time on the screen?"
25400 DATA "Each second of thinking time means "
25410 DATA "another point for the second player."
25420 DATA "                                            "
25430 DATA "*"
25440 DATA "Summary:"
25450 DATA "&Player one scores the value of"
25460 DATA "his/her chosen number.             "
25470 DATA "&&The second player scores the number"
25480 DATA "of seconds thinking time      "
25490 DATA "&plus the sum of all the factors in the"
25500 DATA "chosen number,                         "
25510 DATA "&providing they are still shown"
25520 DATA "in the table.                    "
25530 DATA "&As soon as a factor is chosen or is used"
25540 DATA "it will disappear, never to return...         "
25550 DATA "*"
25560 DATA "Oh, one last request."
25570 DATA "                                   "
25580 DATA "&I may ask you to give me a number."
25590 DATA "You do this by typing in the number,"
25600 DATA "                                        "
25610 DATA "but you must not forget"
25620 DATA "to press <RETURN> after the last"
25630 DATA "figure has been pressed."
25640 DATA "                           "
25650 DATA "If you have to type anything in"
25660 DATA "I will make this sign:"
25670 DATA "&   *x"
25680 DATA "=o","=x","=o","=x","=o","=x"
25690 DATA "=o","=x","=o","=x","=o","=x"
25700 DATA "   =x"
25710 DATA "= "
25720 DATA "Understand ?  "
25730 DATA "&If that sign flashes, I will wait"
25740 DATA "until you press a key."
25750 DATA "&&Please don't forget the <RETURN>"
25760 DATA "key, or I cannot continue."
25770 DATA "*"
25780 DATA "Perhaps the rules of the game may"
25790 DATA "still be confusing, but don't worry."
25800 DATA "&&&&If you get stuck, I will help you."
25810 DATA "&&&Let's have a go."
25820 DATA "                          "
25830 DATA "#"
25840 DATA "%"
30000 REM   2590-2620    check these
30010 REM   lines if the game goes too
30020 REM   fast or too slow. adjust-
30030 REM   ment is possible.
30040 REM  
30050 REM   adapted for basicode-2
30060 REM   on 27.12.82 by:
30070 REM  
30080 REM        
30090 REM        j. haubrich
30100 REM  
30110 REM   original by same author
30120 REM  
30130 REM   translated for "RADIO ACTIVITY"
30140 REM  
30150 REM   by jonathan marks  1.3.1984

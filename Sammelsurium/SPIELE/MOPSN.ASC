1000 A=1000: GOTO 20: REM ## MOPS ##
1010 SR$="MOPS": HO=INT((HO-LEN(SR$)-6)/2): VE=0
1020 GOSUB 110: GOSUB 150: PRINT
1030 PRINT"9 Eingaben muessen so erfolgen:"
1040 PRINT"der Esel, die Frau oder das Pferd"
1050 PRINT"Gross-Klein-Schreibung ist unwichtig"
1060 PRINT"Wichtig ist: der, die, das und 1 Leerzeichen"
1070 PRINT: F$="falsche Eingabe":
1080 DIM A(8), B(8), C$(8), D$(2), E$(2), F$(2)
1090 D$(0)= "DER": D$(1)= "DAS": D$(2)= "DIE"
1100 E$(0)= "DEM": E$(1)= "DEM": E$(2)= "DER"
1110 F$(0)= "DEN": F$(1)= "DAS": F$(2)= "DIE"
1120 FOR I=0 TO 8
1130   PRINT I+1;:INPUT". Wort mit Artikel";S$
1140   IF MID$(S$,4,1)<>" " THEN PRINT F$: GOTO 1130
1150   SR$=LEFT$(S$,3): GOSUB 330
1160   IF SR$="DER" THEN A(I)=0: GOTO 1200
1170   IF SR$="DAS" THEN A(I)=1: GOTO 1200
1180   IF SR$="DIE" THEN A(I)=2: GOTO 1200
1190   PRINT F$: GOTO 1130
1200   SR$=MID$(S$,4): GOSUB 330: C$(I)=SR$
1210 NEXT I: GOSUB 100
1220 PRINT
1230 FOR I=0 TO 8
1240   GOSUB 260:X=INT(8*RV+.5)
1250  C=0: IF I=0 THEN B(I)=X: GOTO 1300
1260   FOR J=0 TO I-1
1270    IF B(J)=X THEN C=1: J=I
1280   NEXT J: IF C=1 THEN 1240
1290  B(I)=X
1300 NEXT I
1310 PRINT"WENN ";D$(A(B(0)));C$(B(0));
1320 PRINT" MIT ";E$(A(B(1)));C$(B(1))
1330 PRINT"  UEBER ";F$(A(B(2)));C$(B(2));" SPRINGT"
1340 PRINT"UND ";D$(A(B(3)));C$(B(3));
1350 PRINT" IN ";E$(A(B(4)));C$(B(4))
1360 PRINT"  ";F$(A(B(5)));C$(B(5));" VERSCHLINGT"
1370 PRINT"DANN ";D$(A(B(6)));C$(B(6));
1380 PRINT" AUS ";E$(A(B(7)));C$(B(7))
1390 PRINT"  ALS";C$(B(8));" ERKLINGT": PRINT
1400 INPUT"E = Ende";A$: IF A$<>"E" THEN 1220
1410 GOTO 950
30000 REM -----------Anmerkungen------------
30010 REM Dieses Programm wurde fuer den Rundfunk
30020 REM zum Lehrgang "BASIC 1*1 des Programmierens"
30030 REM auf der Grundlage des Gedichts von Carl
30040 REM Reinhardt "Wenn der Mops mit der Wurst
30050 REM uebern Spucknapf springt" entwickelt.
30060 REM Da es viel Freunde gefunden hat, wurde
30070 REM hier auf BASICODE angepasst.
35000 REM ----------Autorschaft-------------
35010 REM H.Voelz; 19.7.89; fuer Buch BASICODE
35020 REM ueberarbeitet fuer Rundfunk 3.2.90 
35030 REM Auf IBM-compatiblem Rechner

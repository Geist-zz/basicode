1000 A=500:GOTO 20:REM BIORITME
1010 GOSUB 2030
1020 REM
1030 REM       HOOFDPROGRAMMA
1040 REM
1050 DIM C(12),A(2),B(2),F(2)
1060 GOSUB 100:PRINT"Met dit programma kunt u zelf de "
1070 PRINT"stand van uw bioritme bepalen."
1080 HO=0:VE=6:GOSUB 110:PRINT"Hoe wilt u de biorythmische";
1090 PRINT" standen ?"
1100 HO=8:VE=9:GOSUB 110:PRINT"1 - In woorden"
1110 VE=11:GOSUB 110:PRINT"2 - Grafiek op het scherm"
1120 VE=13:GOSUB 110:PRINT"3 - Of wilt u een uitleg"
1130 VE=15:GOSUB 110:PRINT"4 - Of wilt u stoppen"
1140 HO=5:VE=21:GOSUB 110:PRINT"DRUK NA DE UITVOERING RETURN"
1150 HO=8:VE=17:GOSUB 110:INPUT Z
1160 IF Z=4 THEN 950:REM stoppen
1170 IF (Z<1) OR (Z>3) THEN GOSUB 1230:GOTO 1150
1180 ON Z GOSUB 3030,4030,2030
1190 GOTO 1060
1200 REM
1210 REM        FOUTE INVOER
1220 REM
1230 HO=8:VE=17:GOSUB 110:PRINT"Niet bekend!"
1240 SD=20:GOSUB 450:REM wacht 2 s
1250 HO=8:VE=17:GOSUB 110:PRINT"            ":RETURN
2000 REM
2010 REM         INLEIDING
2020 REM
2030 GOSUB 100
2040 PRINT TAB(10);"***************"
2050 PRINT TAB(10);"* BIORYTHMIEK *"
2060 PRINT TAB(10);"***************"
2070 PRINT:PRINT"Bij de BIORYTHMIEK gaat men er vanuit "
2080 PRINT"dat bij de geboorte van een mens drie"
2090 PRINT"ritmes van start gaan. Namelijk:"
2100 PRINT"- het LICHAAMSRITME."
2110 PRINT"(met een lengte van 23 dagen)"
2120 PRINT"- HET GEVOELSRITME."
2130 PRINT"(met een lengte van 28 dagen)"
2140 PRINT"- HET INTELLIGENTIERITME."
2150 PRINT"(met een lengte van 33 dagen)"
2160 PRINT"Vanaf de geboorte gaan alle drie de"
2170 PRINT"ritmes naar een optimale toestand "
2180 PRINT"(positief) waarna ze naar een minimum"
2190 PRINT"dalen. Telkens wanneer een ritme van"
2200 PRINT"positief naar negatief (of anders om)"
2210 PRINT"overgaat is het ritme KRITISCH."
2220 PRINT"DWZ. dat de persoon wat dat ritme "
2230 PRINT"betreft instabiel is."
2240 PRINT:PRINT TAB(25);"druk <RETURN>"
2250 GOSUB 200:IF IN<>13 THEN 2250
2260 RETURN
3000 REM
3010 REM     STAND IN WOORDEN
3020 REM
3030 GOSUB 5030
3040 GOSUB 100:PRINT"de bioritmische standen:"
3050 PRINT:PRINT"het lichaamsritme is ";
3060 E=INT((D/23-INT(D/23))*23+.5)
3070 IF (E=0) OR (E=1) OR (E=12) THEN PRINT"KRITISCH":GOTO 3100
3080 IF (E>1) AND (E<12) THEN PRINT"POSITIEF":GOTO 3100
3090 PRINT"NEGATIEF"
3100 PRINT:PRINT"het gevoelsritme is ";
3110 E=INT((D/28-INT(D/28))*28+.5)
3120 IF (E<2) OR (E=14) OR (E=15) THEN PRINT"KRITISCH":GOTO 3150
3130 IF (E>0) AND (E<14) THEN PRINT"POSITIEF":GOTO 3150
3140 PRINT"NEGATIEF"
3150 PRINT:PRINT"en het intelligentieritme is ";
3160 E=INT((D/33-INT(D/33))*33+.5)
3170 IF (E=0) OR (E=1) OR (E=17) THEN PRINT"KRITISCH":GOTO 3200
3180 IF (E>0) AND (E<17) THEN PRINT"POSITIEF":GOTO 3200
3190 PRINT"NEGATIEF"
3200 PRINT:PRINT:PRINT"Ieder mens heeft een dag waarop alle"
3210 PRINT"ritmes door het kritische punt gaan."
3220 PRINT:Z=21252-D:IF Z<0 THEN 3260
3230 PRINT"die dag is voor u op:"
3240 F(0)=B(0):F(1)=B(1):F(2)=B(2):GOSUB 5700
3250 PRINT:PRINT F(0);"-";F(1);"-";F(2)
3260 IF Z<0 THEN PRINT"Die dag heeft u al gehad"
3270 PRINT:PRINT TAB(25);"druk <RETURN>"
3280 GOSUB 200:IF IN<>13 THEN 3280
3290 FOR T=0 TO 2:A(T)=0:B(T)=0:F(T)=0:NEXT T
3300 RETURN
4000 REM
4010 REM   GRAFIEK
4020 REM
4030 GOSUB 5030
4040 GOSUB 600
4050 D=D-B(0):Z=C(B(1))
4060 HO=0:VE=10/191:CN=0:GOSUB 620
4070 HO=0:VE=180/191:CN=0:GOSUB 630
4080 HO=0:VE=100/191:CN=0:GOSUB 620
4090 HO=6*Z/191:VE=100/191:CN=0:GOSUB 630
4100 A$="1-":SR=B(1):GOSUB 300:A$=A$+SR$
4110 A$=A$+"-":SR=B(2):GOSUB 300:A$=A$+SR$:SR$=A$
4120 HO=6/191:VE=172/191:CN=0:GOSUB 650
4130 FOR T=0 TO 6*Z-6 STEP 6
4140 IF T/42=INT(T/42) THEN 4180
4150 HO=T/191:VE=101/191:CN=0:GOSUB 620
4160 VE=102/191:GOSUB 620
4170 GOTO 4200
4180 HO=T/191:VE=101/191:CN=0:GOSUB 620
4190 VE=104/191:GOSUB 630
4200 NEXT T
4210 CN=0:HO=80/191:VE=1/191:SR$="KOERPER"
4220 GOSUB 650
4230 HO=0:VE=100/191:GOSUB 620
4240 X=0:FOR T=D TO D+Z STEP .5
4250 Y=-SIN(T*2*3.1415927/23)*67
4260 Y=INT(Y+.5)
4270 HO=X/191:VE=(Y+100)/191:GOSUB 630
4280 X=X+3
4290 NEXT T
4300 CN=0:HO=80/191:VE=11/191:SR$="GEFUEHL"
4310 GOSUB 650
4320 HO=0:VE=85/191:GOSUB 620
4330 X=0:FOR T=D TO D+Z STEP .5
4340 Y=-SIN(T*2*3.1415927/28)*67
4350 Y=INT(Y+.5)
4360 HO=X/191:VE=(Y+100)/191:GOSUB 630
4370 X=X+3
4380 NEXT T
4390 CN=0:HO=80/191:VE=21/191:SR$="INTELLIGENZ"
4400 GOSUB 650
4410 HO=0:VE=85/191:GOSUB 620
4420 X=0:FOR T=D TO D+Z STEP .5
4430 Y=-SIN(T*2*3.1415927/33)*67
4440 Y=INT(Y+.5)
4450 HO=X/191:VE=(Y+100)/191:GOSUB 630
4460 X=X+3
4470 NEXT T
4480 GOSUB 200:IF IN<>13 THEN 4480
4490 FOR T=0 TO 2:A(T)=0:B(T)=0:F(T)=0:NEXT T
4500 RETURN
5000 REM
5010 REM  GEGEVENS INVOEREN
5020 REM
5030 GOSUB 100
5040 PRINT:PRINT"Wat is uw geboorte datum ?"
5050 PRINT:PRINT"Dag ";:INPUT A(0)
5060 PRINT"Maand ";:INPUT A(1)
5070 PRINT"Jaar ";:INPUT A(2)
5080 PRINT:PRINT A(0);"-";A(1);"-";A(2)
5090 PRINT:PRINT"Is dat goed (j/n)";:INPUT A$:PRINT
5100 IF (A(0)=0) OR (A(0)>31) THEN GOSUB 5310
5110 IF (A(1)>12) OR (A(1)=0) THEN GOSUB 5310
5120 IF (A$="n") OR (A$="N") THEN 5040
5130 PRINT:PRINT"Wat is de peildatum ?"
5140 PRINT:PRINT"Dag ";:INPUT B(0)
5150 PRINT"Maand ";:INPUT B(1)
5160 PRINT"Jaar ";:INPUT B(2)
5170 PRINT:PRINT B(0);"-";B(1);"-";B(2)
5180 PRINT:PRINT"Is dat goed (j/n)";:INPUT A$:PRINT
5190 IF (B(0)=0) OR (B(0)>31) THEN GOSUB 5310
5200 IF (B(1)>12) OR (B(1)=0) THEN GOSUB 5310
5210 IF (A$="n") OR (A$="N") THEN 5130
5220 GOSUB 5380
5230 GOSUB 100
5240 PRINT"Op ";B(0);"-";B(1);"-";B(2);
5250 PRINT" bent u ";D;" dagen oud"
5260 SD=20:GOSUB 450
5270 RETURN
5280 REM
5290 REM  FOUTE INVOER (2)
5300 REM
5310 FOR T=0 TO 2:A(T)=0:B(T)=0:F(T)=0:NEXT T
5320 IF (A$="N") OR (A$="n") THEN RETURN
5330 PRINT"Dat kan nooit goed zijn!"
5340 A$="n":RETURN
5350 REM
5360 REM  DAGEN TELLEN
5370 REM
5380 C(1)=31:C(3)=31:C(4)=30:C(5)=31:C(6)=30:C(7)=31:C(8)=31
5390 C(9)=30:C(10)=31:C(11)=30:C(12)=31
5400 GOSUB 5990
5410 D=C(A(1))-A(0)+1
5420 IF A(2)=B(2) THEN 5540
5430 IF (A(2)+1)=B(2) THEN 5600
5440 IF A(1)=12 THEN 5460
5450 FOR T=(A(1)+1) TO 12:D=D+C(T):NEXT T
5460 FOR T=(A(2)+1) TO (B(2)-1)
5470 GOSUB 6020
5480 NEXT T
5490 GOSUB 6050
5500 IF B(1)=1 THEN 5520
5510 FOR T=1 TO (B(1)-1):D=D+C(T):NEXT T
5520 D=D+B(0):RETURN
5530 REM       A(2)=B(2)
5540 GOSUB 5990
5550 IF A(1)=B(1) THEN D=(B(0)-A(0))+1:RETURN
5560 IF (A(1)+1)=B(1) THEN D=D+B(0):RETURN
5570 FOR T=(A(1)+1) TO (B(1)-1):D=D+C(T):NEXT T
5580 D=D+B(0):RETURN
5590 REM      A(2)+1=B(2)
5600 GOSUB 5990
5610 IF A(1)=12 THEN 5630
5620 FOR T=(A(1)+1) TO 12:D=D+C(T):NEXT T
5630 GOSUB 6050
5640 IF B(1)=1 THEN 5660
5650 FOR T=1 TO (B(1)-1):D=D+C(T):NEXT T
5660 D=D+B(0):RETURN
5670 REM
5680 REM  DATUM ZOEKEN
5690 REM
5700 C(1)=31:C(3)=31:C(4)=30:C(5)=31:C(6)=30:C(7)=31:C(8)=31
5710 C(9)=30:C(10)=31:C(11)=30:C(12)=31
5720 GOSUB 6080
5730 Z=Z-1:IF Z<C(F(1))-F(0) THEN 5910
5740 Z=Z-C(F(1))+F(0)-1:F(1)=F(1)+1:F(0)=1
5750 IF Z=0 THEN RETURN
5760 FOR T=F(1) TO 12
5770 IF Z>C(T) THEN Z=Z-C(T):F(1)=F(1)+1:GOTO 5790
5780 GOTO 5910
5790 NEXT T:F(1)=1:F(2)=F(2)+1
5800 G=365
5810 GOSUB 6110
5820 IF Z>=G THEN Z=Z-G:F(2)=F(2)+1:GOTO 5840
5830 GOTO 5910
5840 IF Z=0 THEN RETURN
5850 GOTO 5810
5860 GOSUB 6080
5870 IF Z>=C(F(1)) THEN Z=Z-C(F(1)):F(1)=F(1)+1:GOTO 5890
5880 GOTO 5910
5890 IF Z=0 THEN RETURN
5900 GOTO 5870
5910 IF Z>0 THEN Z=Z-1:F(0)=F(0)+1
5920 IF F(0)>C(F(1)) THEN F(1)=F(1)+1:F(0)=1
5930 IF F(1)>12 THEN F(2)=F(2)+1:F(1)=1:F(0)=1:GOTO 5860
5940 IF Z=0 THEN RETURN
5950 GOTO 5910
5960 REM
5970 REM SCHRIKKELJAAR ?
5980 REM
5990 IF (A(2)/4<>INT(A(2)/4)) THEN C(2)=28:RETURN
6000 IF (A(2)/100<>INT(A(2)/100)) THEN C(2)=29:RETURN
6010 C(2)=28:RETURN
6020 IF (T/4<>INT(T/4)) THEN D=D+365:RETURN
6030 IF (T/100<>INT(T/100)) THEN D=D+366:RETURN
6040 D=D+365:RETURN
6050 IF (B(2)/4<>INT(B(2)/4)) THEN C(2)=28:RETURN
6060 IF (B(2)/100<>INT(B(2)/100)) THEN C(2)=29:RETURN
6070 C(2)=28:RETURN
6080 IF (F(2)/4<>INT(F(2)/4)) THEN C(2)=28:RETURN
6090 IF (F(2)/100<>INT(F(2)/100)) THEN C(2)=29:RETURN
6100 C(2)=28:RETURN
6110 IF (F(2)/4<>INT(F(2)/4)) THEN G=365:RETURN
6120 IF (F(2)/100<>INT(F(2)/100)) THEN G=366:RETURN
6130 G=365:RETURN
32000 REM**************************
32010 REM*   door: M. Nitters     *
32020 REM*     Oud-Beyerland      *
32030 REM*     NEDERLAND          *
32040 REM**************************
32050 REM TROS-RADIO dd 871111
32060 REM
32070 REM Erstausstrahlung: Radio DDR
32080 REM 900418

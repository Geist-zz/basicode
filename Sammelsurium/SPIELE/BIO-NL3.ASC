1000 A=100:GOTO 20:REM BIORITHME
1005 REM TROS-RADIO dd. 860712
1010 DIM ML(12)
1020 GOSUB 100
1030 HO=10:VE=12:GOSUB 110:PRINT "Biorithme"
1040 SD=30:GOSUB 450
1050 RESTORE 
1060 FOR I=1 TO 12:READ ML(I):NEXT I
1070 GOSUB 2180:REM INLEIDING
1080 GOSUB 100
1090 PRINT :PRINT 
1100 PRINT "Voor het tekenen van een grafiek"
1110 PRINT "heb ik tweemaal een datum nodig."
1120 PRINT 
1130 PRINT "Eerst uw geboortedatum:"
1140 PRINT 
1150 GOSUB 120:GOSUB 1620:REM DATUMINVOER
1160 D1=DD:M1=MM:J1=JJ
1170 HO=0:VE=13:GOSUB 110
1180 PRINT "en nu de datum van de grafiek:"
1190 GOSUB 120:GOSUB 1620:REM DATUMINVOER
1200 D2=DD:M2=MM:J2=JJ
1210 GOSUB 2010:REM BEREKEN VERSCHIL IN DAGEN
1220 IF TY<0 THEN 1080
1230 HO=0:VE=20:GOSUB 110
1240 PRINT "Aantal dagen sinds geboorte is ";TY
1250 GOSUB 2460:REM WACHT
1260 GOSUB 600:REM GRAFISCH BEDRIJF
1270 CN=0
1280 D=TY-10
1290 I=.039:Y=.45:XO=.078:YO=.5
1300 HO=XO:VE=YO-Y:GOSUB 620:VE=YO+Y:GOSUB 630
1310 VE=YO:GOSUB 620:HO=XO+20*I:GOSUB 630
1320 FOR K=1 TO 20
1330 HO=XO+K*I:VE=YO:GOSUB 620:VE=YO+.016:GOSUB 630
1340 NEXT K
1350 HO=XO-I:VE=YO-Y/2:SR$="+":GOSUB 650
1360 VE=YO+Y/2:SR$="-":GOSUB 650
1370 HO=XO+10*I:VE=YO+.06:GOSUB 620:VE=YO+.03:GOSUB 630
1380 HO=HO-.01:VE=VE+.01:GOSUB 630
1390 HO=HO+.02:GOSUB 630
1400 HO=HO-.01:VE=VE-.01:GOSUB 630
1410 WV=23:SR$="P":GOSUB 1520
1420 WV=28:SR$="E":GOSUB 1520
1430 WV=33:SR$="I":GOSUB 1520
1440 GOSUB 210
1450 GOSUB 100
1460 HO=0:VE=12:GOSUB 110
1470 PRINT "Nog een keer (J/N) ?"
1480 GOSUB 210:IF IN=74 THEN 1020
1490 GOTO 950:REM EINDE VAN HET PROGRAMMA
1510 REM SUBROUTINE TEKEN SINUS
1520 TP=6.28318531
1530 HO=XO:VE=YO-Y*SIN(TP/WV*D):GOSUB 620
1540 GOSUB 650
1550 FOR XX=0 TO 20 STEP .5:DX=D+XX
1560 HO=XO+XX*I:VE=YO-Y*SIN(TP/WV*(D+XX)):GOSUB 630
1570 NEXT XX
1580 GOSUB 650
1590 RETURN
1610 REM SUBROUTINE DATUMINVOER
1620 VP=VE:HP=HO
1630 VE=VP:GOSUB 110:SC=120:GOSUB 1880
1640 REM LUS DAGINVOER
1650 HO=HP:VE=VP:GOSUB 110:SC=39:GOSUB 1880
1660 HO=5:VE=VP:GOSUB 110:PRINT "Dag (1-31) : ";:INPUT D$
1670 DD=VAL(D$)
1680 IF (DD<1) OR (DD>31) THEN 1650
1690 REM LUS MAANDINVOER
1700 HO=HP:VE=VP+1:GOSUB 110:SC=39:GOSUB 1880
1710 HO=3:VE=VP+1:GOSUB 110:PRINT "Maand (1-12) : ";:INPUT M$
1720 MM=VAL(M$)
1730 IF (MM<1) OR (MM>12) THEN GOSUB 250:GOTO 1700
1740 IF (MM=2) AND (DD>29) THEN GOSUB 250:GOTO 1620
1750 IF (MM<>2) AND (DD>ML(MM)) THEN GOSUB 250:GOTO 1620
1760 REM LUS JAARINVOER
1770 HO=HP:VE=VP+2:GOSUB 110:SC=39:GOSUB 1880
1780 HO=11:VE=VP+2:GOSUB 110:PRINT "Jaar : ";:INPUT Y$
1790 JJ=VAL(Y$)
1800 IF JJ=0 THEN 1770
1810 IF JJ<100 THEN JJ=JJ+1900
1820 IF (JJ<1900) OR (JJ>2020) THEN GOSUB 250:GOTO 1770
1830 TJ=JJ:GOSUB 1940
1840 IF (MM=2) AND (DD>ML(MM)+SJ) THEN 1620
1850 RETURN
1870 REM PRINT SC SPATIES
1880 FOR C=1 TO SC
1890 PRINT " ";
1900 NEXT C
1910 RETURN
1930 REM SUBR. TEST OF TJ SCHRIKKELJAAR IS
1940 IF JJ<>4*INT(JJ/4) THEN 1980
1950 IF JJ<>100*INT(JJ/100) THEN 1970
1960 IF JJ<>400*INT(JJ/400) THEN 1980
1970 SJ=1:RETURN:REM IS SCHRIKKELJAAR
1980 SJ=0:RETURN:REM GEEN SCHRIKKELJAAR
2000 REM SUBR. BEREKEN VERSCHIL
2010 L=365*(J2-J1)
2020 IF M2>1 THEN FOR I=1 TO M2-1:L=L+ML(I):NEXT I
2030 IF M1>1 THEN FOR I=1 TO M1-1:L=L-ML(I):NEXT I
2040 L=L+D2-D1
2050 J4=4*INT(J1/4)
2060 J4=J4+4:IF J4>=J2 THEN 2090
2070 TJ=J4:GOSUB 1940:L=L+SJ
2080 GOTO 2060
2090 IF J1=J2 THEN 2130
2100 TJ=J1:GOSUB 1940:IF (SJ=1) AND (M1<3) THEN L=L+1
2110 TJ=J2:GOSUB 1940:IF (SJ=1) AND (M2>2) THEN L=L+1
2120 TY=L:RETURN
2130 TJ=J1:GOSUB 1940
2140 IF (SJ=1) AND (M1<3) AND (M2>2) THEN L=L+1
2150 TY=L:RETURN
2170 REM SUBROUTINE UITLEG
2180 GOSUB 100
2190 PRINT "Volgens de Zwitserse wetenschap van"
2200 PRINT "het biorithme beheersen drie cycli"
2210 PRINT "vanaf de geboorte in een regelmatig"
2220 PRINT "tempo het leven. Deze drie zijn:-"
2230 PRINT 
2240 PRINT "PHYSIEK    beheerst de lichamelijke"
2250 PRINT "23 dagen   kracht, agressiviteit en"
2260 PRINT "           uithoudingsvermogen."
2270 PRINT 
2280 PRINT "EMOTIE     beheerst gevoelen, intuitie"
2290 PRINT "28 dagen   creativiteit, omgang"
2300 PRINT "           liefde en genegenheid."
2310 PRINT 
2320 PRINT "INTELLECT  beheerst intellect, geheugen"
2330 PRINT "33 dagen   concentratie en snelheid"
2340 PRINT "           van het geheugen."
2350 GOSUB 2460:REM WACHT
2360 GOSUB 100
2370 PRINT "De meest kritieke dagen zijn die waar"
2380 PRINT "de cyclus de horizontale as snijdt,"
2390 PRINT "hetgeen een overgang aangeeft van de"
2400 PRINT "energie die wordt ontladen en weer"
2410 PRINT "wordt opgeladen."
2420 GOSUB 2460:REM WACHT
2430 RETURN
2450 REM WACHTROUTINE
2460 HO=0:VE=23:GOSUB 110
2470 PRINT "Druk op de SPATIE voor het vervolg";
2480 GOSUB 210:IF IN$<>" " THEN 2480
2490 RETURN
25000 DATA 31,28,31,30,31,30,31,31,30,31,30,31
32000 REM ******************************
32010 REM *                            *
32020 REM *    Copyright (C) 1986      *
32030 REM *    Stichting BASICODE      *
32040 REM *    Postbus 1410            *
32050 REM *    5602 BK  Eindhoven      *
32060 REM *                            *
32070 REM ******************************
32080 REM TROS-RADIO dd 860712

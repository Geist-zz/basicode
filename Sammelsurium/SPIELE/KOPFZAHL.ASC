1000 A=2000:GOTO 20:REM ZAHL AUF ZAHL
1010 GOSUB 100
1020 DIM A(8,8):S=0:C=0:SP=0:ZE=0:MA=0
1030 REM 
1040 REM ---------- KOPF
1050 REM 
1100 SR$="ZAHL auf ZAHL"
1110 HO=7:VE=0:GOSUB 110:GOSUB 150
1120 REM 
1130 REM ---------- ERLAEUTERUNG
1140 REM 
1200 HO=0:VE=3:GOSUB 110
1210 PRINT"Der Computer baut ein 8x8 grosses Feld":PRINT
1220 PRINT"mit Zufallszahlen (1...99) auf und":PRINT
1230 PRINT"gibt die Startposition vor. Es wird":PRINT
1240 PRINT"abwechselnd vom Spieler und Computer":PRINT
1250 PRINT"gezogen und aus den Spalten (Spieler)":PRINT
1260 PRINT"- Auswahl mit CURSOR UP/DOWN -":PRINT
1270 PRINT"und Zeilen (Computer) eine Zahl":PRINT
1280 PRINT"ausgewaehlt. Der Zug wird mit der":PRINT
1290 PRINT"RETURN-Taste abgeschlossen.":PRINT
1300 PRINT"Ende/neues Spiel => X"
1310 REM 
1320 HO=20:VE=23:GOSUB 110:PRINT"Taste druecken !";
1330 GOSUB 210:GOSUB 100
1340 REM 
1350 REM ---------- ZAHLENFELD
1360 REM 
1400 HO=0:VE=2:GOSUB 110:PRINT"Spieler:"
1410 HO=18:VE=2:GOSUB 110:PRINT"Computer:"
1420 HO=0:VE=3:GOSUB 110
1425 PRINT"--------------------------------"
1430 REM 
1440 HO=1:VE=4
1450 FOR I=1 TO 8
1460 FOR J=1 TO 8
1470 GOSUB 260:A(I,J)=INT(RV*99)+1
1480 SR=A(I,J):GOSUB 300
1490 IF (A(I,J)<10) THEN SR$=" "+SR$
1500 GOSUB 110:PRINT SR$
1510 HO=HO+4
1520 NEXT J
1530 HO=1:VE=VE+2
1540 NEXT I
1550 REM 
1560 REM ---------- SPIELER
1570 REM 
1580 HO=0:VE=21:GOSUB 110
1590 PRINT"Cursor UP/DOWN / RETURN / X"
1595 REM 
1600 GOSUB 260:J=INT(RV*8)+1:I=1
1610 SR=A(I,J):GOSUB 300
1620 IF (A(I,J)<10) THEN SR$=" "+SR$
1630 IF (A(I,J)=0) THEN SR$="--"
1640 SR$="*"+SR$+"*"
1650 HO=(J*4)-4:VE=4:GOSUB 110:PRINT SR$;
1660 REM 
1670 REM ---------- AUSWAHL
1680 REM 
1700 GOSUB 200
1710 IF (I<=1) AND (I>=8) THEN 1700
1720 IF (IN=30) THEN F1=0:GOTO 1800
1730 IF (IN=31) THEN F1=1:GOTO 1800
1740 IF (IN=13) THEN 1900
1745 IF (IN$="X") OR (IN$="x") THEN 2300
1750 GOTO 1700
1760 REM ---------- CS UP/DOWN
1800 SR=A(I,J):GOSUB 300
1810 IF (A(I,J)<10) THEN SR$=" "+SR$
1815 IF (A(I,J)=0) THEN SR$="--"
1820 SR$=" "+SR$+" "
1830 VE=(I*2)+2:GOSUB 110:PRINT SR$;
1840 IF (F1=0) THEN I=I+1:IF (I>=8) THEN I=8
1845 IF (F1=1) THEN I=I-1:IF (I<=1) THEN I=1
1850 SR=A(I,J):GOSUB 300
1860 IF (A(I,J)<10) THEN SR$=" "+SR$
1865 IF (A(I,J)=0) THEN SR$="--"
1870 SR$="*"+SR$+"*"
1880 VE=(I*2)+2:GOSUB 110:PRINT SR$;
1890 GOTO 1700
1895 REM ---------- FESTLEGEN
1900 S=S+A(I,J)
1910 A(I,J)=0:SR$=" -- "
1920 VE=(I*2)+2:GOSUB 110:PRINT SR$;
1940 HO=8:VE=2:GOSUB 110:PRINT S;
1950 REM 
1960 REM ---------- COMPUTER
1970 REM 
2000 HO=0:VE=21:GOSUB 110
2010 PRINT"Computer zieht !           "
2020 REM ---------- ZUG + MAX.WERT
2030 GOSUB 260:I=INT(RV*8)+1
2040 MA=A(I,0):MA=0
2050 FOR J=1 TO 8
2060 IF MA>A(I,J) THEN 2080
2070 MA=A(I,J):MI=I:MJ=J
2080 NEXT J
2085 REM ---------- ANZEIGE
2090 C=C+MA
2100 A(MI,MJ)=0:SR$=" -- "
2110 HO=(MJ*4)-4:VE=(MI*2)+2:GOSUB 110:PRINT SR$;
2120 HO=27:VE=2:GOSUB 110:PRINT C;
2130 REM 
2140 REM ---------- NAECHSTE FOLGE
2150 REM 
2200 GOTO 1580
2210 REM 
2220 REM ---------- ENDE/NEUES SPIEL
2230 REM 
2300 GOSUB 100
2310 HO=5:VE=10:GOSUB 110
2320 PRINT"Noch ein Spiel ? <J/N>";
2330 GOSUB 210
2340 IF (IN$="N") OR (IN$="n") THEN 2360
2350 IF (IN$="J") OR (IN$="j") THEN GOSUB 100:S=0:C=0:GOTO 1400
2360 HO=5:VE=10:GOSUB 110
2370 PRINT"    Das war es !      "
2380 HO=5:VE=12:GOSUB 110
2390 PRINT"Sie erreichten";S;"Punkte,"
2400 HO=5:VE=14:GOSUB 110
2410 PRINT"der Computer";C;"!!!";
2420 GOSUB 210:GOTO 950
2430 REM 
32000 REM ------------------------
32010 REM 
32020 REM     'ZAHL AUF ZAHL'
32030 REM 
32040 REM NACH EINER PROGRAMMIDEE:
32050 REM     'ZAHLENSCHACH'
32060 REM 64'ER, APRIL 1989, P.62
32070 REM 
32080 REM      C128/C64 MODE
32090 REM 
32100 REM F.DORMEIER
32110 REM BISMARCKSTR. 11A
32120 REM D-1000 BERLIN 39
32130 REM 
32140 REM MAI 1989
32150 REM 
32160 REM ------------------------

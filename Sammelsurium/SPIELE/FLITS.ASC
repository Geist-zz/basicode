1000 A=500:GOTO 20:REM "FLITS"
1010 DIM L$(8):AA=ASC("A"):SP=0:SV=0
1020 T$=" "
1030 GOSUB 100
1040 PRINT :PRINT 
1050 SR$=" F L I T S ":GOSUB 150:PRINT :PRINT 
1060 PRINT "Dit programma laat in een flits"
1070 PRINT "een getal, een woord of een"
1080 PRINT "auto-nummer zien."
1090 PRINT "Dat moet je precies na typen."
1100 PRINT "Maar als je dat goed doet, wordt"
1110 PRINT "de volgende flits iets korter!"
1120 PRINT "Als het niet goed gaat, dan wordt"
1130 PRINT "de flits juist weer wat langer."
1140 PRINT "Onder op het scherm staat de"
1150 PRINT "FLITS-TIJD."
1160 PRINT "Maak die zo klein mogelijk!"
1170 PRINT :PRINT :PRINT "Tik op een toets";:GOSUB 210:GOSUB 100
1180 PRINT :PRINT :GP=0.0001:SD=20:REM >> FLITSDUUR <<
1190 SR$=" K E U Z E ":GOSUB 150:PRINT :PRINT 
1200 PRINT "( S = stoppen )":PRINT :PRINT 
1210 PRINT "Wil je GETALLEN,":PRINT "       WOORDEN,"
1220 PRINT "       AUTO-NUMMERS ?":PRINT 
1230 PRINT "tik G, W of A";:GOSUB 210
1240 IF IN=83 THEN 950
1250 IF (IN<>65) AND (IN<>71) AND (IN<>87) THEN PRINT :GOTO 1230
1260 PRINT :PRINT :K1=1:IF IN=87 THEN K1=2
1270 IF IN=65 THEN K1=3:K2=8
1280 ON K1 GOTO 1290,1300,2000
1290 PRINT "GETALLEN van HOEVEEL CIJFERS?":GOTO 1310
1300 PRINT "WOORDEN van HOEVEEL LETTERS?"
1310 PRINT "Tik 1, 2, 3, 4 of 5 ";:GOSUB 210
1320 IF IN=83 THEN 950
1330 IF (IN<49) OR (IN>53) THEN PRINT :GOTO 1310
1340 K2=IN-48
2000 GOSUB 600:PH=1/HG:PV=1/VG
2010 REM ---trek lijnen---
2020 CN=0:HO=0.15:VE=0.3:GOSUB 620:HO=0.38:GOSUB 630
2030 HO=0.42+K2*9*PH:VE=0.3:GOSUB 620:HO=0.85:GOSUB 630
2040 HO=0:VE=0.2:GOSUB 620:HO=1-PH:GOSUB 630
2050 VE=0.4:GOSUB 630:HO=0:GOSUB 630:VE=0.2:GOSUB 630
2060 HO=0.3:VE=0.1:SR$="! = MENU":GOSUB 650
3000 REM ===HOOFDPROGRAMMA===
3010 ON K1 GOTO 3030,3060,3250
3020 REM ---getallengenerator---
3030 GOSUB 260:SR=10000+INT(RV*90000)
3040 GOSUB 300:X$=LEFT$(SR$,K2):SR=VAL(X$):GOTO 3510
3050 REM ---woordengenerator---
3060 ON K2 GOTO 3080,3080,3120,3150,3200
3070 REM ---1 letter---
3080 GOSUB 260:X=AA+INT(RV*26):X$=CHR$(X):IF K2=1 THEN 3510
3090 REM ---2 letters---
3100 GOSUB 260:X=AA+INT(RV*26):X$=X$+CHR$(X):GOTO 3510
3110 REM ---3 letters---
3120 RESTORE:GOSUB 260:FOR N=1 TO RV*50:READ X$:NEXT N
3130 GOTO 3510
3140 REM ---4 letters---
3150 RESTORE
3160 READ X$:IF X$<>"W4" THEN 3160
3170 GOSUB 260:FOR N=1 TO RV*50:READ X$:NEXT N
3180 GOTO 3510
3190 REM ---5 letters---
3200 RESTORE
3210 READ X$:IF X$<>"W5" THEN 3210
3220 GOSUB 260:FOR N=1 TO RV*50:READ X$:NEXT N
3230 GOTO 3510
3240 REM ---autonummergenerator---
3250 FOR N=1 TO 4:GOSUB 260:L$(N)=CHR$(AA+RV*26):NEXT N
3260 FOR N=5 TO 8
3270 GOSUB 260:SR=INT(RV*10):GOSUB 300:L$(N)=SR$:NEXT N
3280 X$=L$(1)+L$(2)+"-"+L$(5)+L$(6)+"-"+L$(3)+L$(4)
3290 REM 1-4=letters, 5-8=cijfers, hier dus LL-CC-LL
3500 REM --- wis vorige flitsduur ---
3510 CN=1:HO=0:VE=0.8:SR$=T$:GOSUB 650:CN=0
3520 REM ---print de flitsduur---
3530 SR=SD/10:CT=3:CN=1:GOSUB 310
3540 CN=0:SR$=SR$+" sek.":GOSUB 650:T$=SR$
3550 REM ---werk grafiek bij---
3560 CN=0:HO=GR:VE=1-PV:GOSUB 620:VE=1-SD*PV:GOSUB 630
3570 GP=GP+PH:IF GP>=1-PH THEN 950
3580 REM ---doe de flits---
3590 Z$="":FOR N=1 TO LEN(X$):Q$=MID$(X$,N,1)
3600 IF Q$="0" THEN Q$="O"
3610 Z$=Z$+Q$:NEXT N:REM ---0 wordt O---
3620 HO=0.4:VE=0.29:SR$=Z$:GOSUB 650
3630 GOSUB 400:REM >> FLITSDUUR <<
3640 CN=1:GOSUB 650:CN=0
3650 HO=0.37:VE=0.6:SR$="?":GOSUB 650
3660 REM ---input antwoord---
3670 Y$="":LY=0:HO=0.4:VE=0.6
3680 GOSUB 210:IF IN=13 THEN 3790
3690 IF IN$="!" THEN GOSUB 100:GOTO 1180
3700 IF (IN<33) OR (IN>127) THEN 3680
3710 IF (IN=127) AND (LY=0) THEN 3680
3720 IF IN<>127 THEN 3770
3730 CN=1:SR$=Y$:GOSUB 650:CN=0
3740 IF LEN(Y$)=1 THEN Y$="":GOTO 3760
3750 Y$=LEFT$(Y$,LEN(Y$)-1)
3760 LY=LY-1:SR$=Y$:GOSUB 650:GOTO 3680
3770 Y$=Y$+IN$:LY=LY+1:SR$=Y$:GOSUB 650:GOTO 3680
3780 REM ---beoordeling---
3790 G=0:SD=SD+1:IF X$=Y$ THEN G=1:SD=SD-2:IF SD=0 THEN SD=1
3800 IF SD>40 THEN SD=40
3810 HO=0.4:VE=0.6:SR$=Y$:GOSUB 650
3820 IF G=1 THEN HO=0.4:VE=0.7:SR$="GOED !":GOSUB 650:GOTO 3840
3830 HO=0:VE=0.7:SR$="JAMMER, het was "+X$:GOSUB 650
3840 HO=0.4:VE=0.8:SR$="tik SPATIEBALK":GOSUB 650
3850 IF CN=1 THEN 3890
3860 GOSUB 210:IF (IN<>32) AND (IN$<>"!") THEN 3860
3870 IF IN$="!" THEN GOSUB 100:GOTO 1180
3880 CN=1:GOTO 3810
3890 CN=0:ON K1 GOTO 3030,3060,3250
3900 REM 
25000 DATA "DAK","HOK","MAN","SAP","VIS"
25010 DATA "LAK","MIS","UIT","BOS","TAK"
25020 DATA "KAT","UIL","BOK","TOR","RAT"
25030 DATA "VAT","VET","PAN","WAS","ZOT"
25040 DATA "TIK","REM","POP","KAR","BUS"
25050 DATA "LEK","HIJ","WIJ","WEI","MEI"
25060 DATA "KOU","KOM","DUS","KAM","LAM"
25070 DATA "DAS","SOK","ELF","ZES","MES"
25080 DATA "KEI","EIK","IEP","GEK","DUN"
25090 DATA "BOL","MUG","SIP","KOE","OEF"
25100 DATA "W4"
25110 DATA "HALT","HOUT","JONG","MUIS","PLOF"
25120 DATA "HOOP","KIST","PLAK","BALK","KURK"
25130 DATA "HARK","BANG","SLAK","GOED","MOOI"
25140 DATA "HOOI","OLIE","OPEN","LAAT","VLUG"
25150 DATA "KOEK","WESP","VIER","VUUR","HAND"
25160 DATA "BOEK","TIJD","ZWAK","MIER","DOEK"
25170 DATA "BIJL","ZEIL","STER","REUS","NEUS"
25180 DATA "KNIE","FLES","TAAL","TAAK","KAAL"
25190 DATA "RAND","WENS","SNEL","TOUW","HIER"
25200 DATA "ZALM","BEER","KRUK","VOUW","DEUR"
25210 DATA "W5"
25220 DATA "NEGEN","LEVER","BEEST","KATER","STANG"
25230 DATA "VLIEG","KEVER","METER","SLANG","GROND"
25240 DATA "BOTER","ANKER","LEPEL","BETER","PLONS"
25250 DATA "RATEL","LATER","ZEKER","KOPER","REGEL"
25280 DATA "TANTE","GROEP","GELUK","JARIG","TAART"
25290 DATA "LENTE","BLOEM","TAFEL","STRIK","BOTER"
25300 DATA "BOVEN","KRIJT","BLAUW","HOPEN","KAART"
25310 DATA "LUCHT","KRAAI","LOKET","TYPEN","KLAAR"
25320 REM 
32000 REM Gemaakt op BBC-computer
32010 REM januari 1989
32020 REM Ch.W.Brederode
32030 REM Von Bonninghausenlaan 33
32040 REM 2161 ES  LISSE
32050 REM 

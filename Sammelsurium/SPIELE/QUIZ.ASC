1000 a=2000:GOTO 20:REM ## quiz ##
1010 DIM n$(11),pu(11),nf$(50),k$(11),f$(50)
1020 m=50:ho=10:ve=1:sr$="QUIZ":GOSUB 110:GOSUB 150
1030 PRINT:PRINT 
1040 INPUT"Name der Datei";nf$
1050 PRINT:PRINT"(0) von Kass.":INPUT"(2) von Disk.";nf
1060 IF nf<>0 THEN nf=4
1070 GOSUB 500
1080 FOR i=0 TO m:GOSUB 540:f$(i)=in$:NEXT i
1090 GOSUB 580:IF in<>-1 THEN PRINT"Das File ist okay."
1100 IF in=-1 THEN PRINT"LESEFEHLER!"
1110 IF in=-1 THEN PRINT"NEU EINLESEN?"
1120 IF in=-1 THEN  GOSUB 210:IF in=74 THEN 1040
1130 GOSUB 210:GOSUB 250:GOSUB 100
1140 FOR i=1 TO 10:n$(i)="R.T.Software":NEXT i
1150 n$="":s$="*********************************"
1160 r=0:f=0:ii=0:ho=1:ve=1
1170 GOSUB 110
1180 PRINT"  Teste deine Kenntnisse in"
1190 ho=12:ve=3:sr$=f$(0):GOSUB 110:GOSUB 150
1200 ho=5:ve=20:GOSUB 110:PRINT"Ich heisse Pfiffikus."
1210 PRINT:INPUT"     Wie heisst du?";n$:GOSUB 250
1220 GOSUB 110:PRINT"                     "
1230 PRINT:PRINT"                        "
1240 IF n$="" THEN n$="HERR NIEMAND"
1250 ho=4:ve=5:GOSUB 110:PRINT s$
1260 ho=4:ve=13:GOSUB 110:PRINT s$
1270 FOR i=6 TO 12:ve=i:ho=4:GOSUB 110
1280 PRINT"*":ho=ho+32:GOSUB 110
1290 PRINT"*"
1300 NEXT i
1310 FOR i=0 TO 36 step4:ii=ii+1
1320 ho=6:ve=7
1330 GOSUB 110:PRINT LEFT$(f$(i+1),29)
1340 ho=6:ve=9
1350 GOSUB 110:PRINT MID$(f$(i+1),30,29)
1360 ho=6:ve=11
1370 GOSUB 110:PRINT MID$(f$(i+1),59,29)
1380 ho=2:ve=15:GOSUB 110:PRINT"A ";:PRINT f$(i+2)
1390 ve=17:GOSUB 110:PRINT"B ";:PRINT f$(i+3)
1400 ve=19:GOSUB 110:PRINT"C ";:PRINT f$(i+4)
1410 h0=1:ve=20:GOSUB 110
1420 FOR j=1 TO 38:PRINT"*";:NEXT j
1430 ho=10:ve=22:GOSUB 110
1440 PRINT"Deine Antwort?"
1450 GOSUB 210:sr$=in$:GOSUB 330:in$=sr$
1460 IF (in<>65) AND (in<>66) AND (in<>67) THEN 1450
1470 IF in$=f$(40+ii) THEN r=r+1:b=1:GOTO 1490
1480 f=f+1:b=0
1490 FOR j=7 TO 11:ho=5:ve=j
1500 GOSUB 110
1510 PRINT"                              "
1520 NEXT j
1530 FOR j=15 TO 19 step2:ho=0:ve=j
1540 GOSUB 110
1550 PRINT"                                       "
1560 NEXT j
1570 ho=0:ve=4:GOSUB 110:PRINT"RICHTIG"
1580 ho=34:ve=4:GOSUB 110
1590 PRINT"FALSCH"
1600 ho=1:ve=6:GOSUB 110:PRINT r
1610 ho=38:ve=6:GOSUB 110:PRINT f
1620 ho=10:ve=22:GOSUB 110:PRINT"              "
1630 ho=10:ve=10:GOSUB 110
1640 IF b=1 THEN PRINT"Richtig":GOSUB 1690
1650 IF b=1 THEN  GOSUB 110:PRINT"       ":GOTO 1740
1660 IF b=0 THEN  GOSUB 110:PRINT"Falsch":GOSUB 1690
1670 IF b=0 THEN  GOSUB 110:PRINT"      ":GOTO 1740
1680 REM sound
1690 RESTORE:IF b=0 THEN 1710
1700 FOR k=1 TO 4:READ sp,sd:NEXT k
1710 sv=15
1720 FOR k=1 TO 4:READ sp,sd:GOSUB 400:NEXT k
1730 sv=0:GOSUB 400:RETURN
1740 NEXT i
1750 REM auswertung
1760 GOSUB 100
1770 PRINT:PRINT"Du hast von 10 Fragen ";
1780 PRINT r;" richtig beantwortet."
1790 RESTORE":FOR k=1 TO 8:READ sp,sd:NEXT k
1800 FOR i=1 TO 11:READ k$(i):NEXT i
1810 sd=10:GOSUB 450:PRINT"Das war ";k$(r+1);"."
1820 sd=100:GOSUB 450
1830 GOSUB 100
1840 REM bestenliste
1850 PRINT"   Liste der Besten 'Auskenner'"
1860 PRINT"   ============================"
1870 kk=kk+1:IF kk<10 THEN n$(kk)=n$:pu(kk)=r:GOTO 1900
1880 IF pu(10)<r THEN pu(10)=r:n$(10)=n$
1890 REM ordnen up-->down
1900 FOR i=10 TO 1 step-1
1910 FOR j=1 TO i
1920 IF pu(j)<pu(j+1) THEN h=pu(j+1):h$=n$(j+1)
1930 IF pu(j)<pu(j+1) THEN n$(j+1)=n$(j):n$(j)=h$
1940 IF pu(j)<pu(j+1) THEN pu(j+1)=pu(j):pu(j)=h
1950 NEXT j:NEXT i
1960 FOR i=1 TO 10:PRINT TAB(2);n$(i);
1970 PRINT TAB(27);pu(i);
1980 PRINT TAB(31);"PUNKTE"
1990 NEXT i:PRINT:PRINT:PRINT:PRINT:PRINT 
2000 INPUT"NEUER START-->ENTER!";f$:GOSUB 100:GOSUB 250
2010 IF f$="" THEN 1150
2020 GOTO 950
2030 FOR i=1 TO 10:f$(40+i)=f$(43+i):NEXT i
3000 INPUT"NAME";nf$:nf=1:GOSUB 500
3010 FOR i=0 TO 50:sr$=f$(i):GOSUB 560
3020 NEXT i
3030 GOSUB 580:GOTO 950
25000 DATA 72,3,71,3,69,3,67,3
25010 DATA 67,3,69,3,71,3,72,3
25050 DATA "nix","schlecht-nein-sehr-schlecht"
25060 DATA "ziemlich-schwach","schwach"
25070 DATA "maessig","einigermaszen","gut","ganz-gut"
25080 DATA "prima","toll","genial-Professor"
30000 REM ------- ende ----------------
30010 REM ein universelles quiz.dabei
30020 REM werden die jeweils 1o fragen
30030 REM als textfile eingelesen.
30040 REM das programm 'quizin'er-
30050 REM leichtert das eingeben der
30060 REM fragen.danach werden die 1o
30070 REM antwortkennbuchstaben ein-
30080 REM gegeben. der string f$(0)
30090 REM enthaelt den filetitel.
30100 REM weitere hinweise zum ein-
30110 REM gabemodus enthaelt das
30120 REM o.g.programm 'quizin'.
30130 REM ---------------------------
30140 REM literatur:h.voelz'basicode'
32000 REM autor u.idee:
32010 REM       rene'thoss
32020 REM    striegistalstr.22
32030 REM    9253 boehrigen
32040 REM    ==============
32050 REM    21.9.1989 /version1.1
32060 REM erstellt auf kc 87.11
32070 REM ----------------------------
32080 REM
32090 REM erstausstrahlung: ds-kultur
32100 REM -spezial 900725

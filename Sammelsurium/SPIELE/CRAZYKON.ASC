1000 A=500:GOTO 20:REM KRAZY KONG  830327
1010 GOTO 8000:REM EXPLAIN+SCENERY
1200 REM 
1210 REM ** PROC FILL(HP,VP,NC) **
1220 REM 
1230 HO=HP:VE=VP:GOSUB 110
1240 PRINT " ";:SD(HP,VP)=NC
1250 RETURN
1300 REM 
1310 REM ** PROC FILL(HP,VP,MC) **
1320 REM 
1330 IF (HP<0) OR (HP>38) THEN HP=HP-JD
1340 HO=HP:VE=VP:GOSUB 110
1350 PRINT MC$;:SD(HP,VP)=MC
1360 RETURN
1400 REM 
1410 REM ** PROC FILL(HP,VP,DC) **
1420 REM 
1430 HO=HP:VE=VP:GOSUB 110
1440 PRINT DC$;:SD(HP,VP)=DC
1450 GOSUB 250:REM BELL
1460 RETURN
1500 REM 
1510 REM ** PROC FILL(HP,VP,LC) **
1520 REM 
1530 HO=HP:VE=VP:GOSUB 110
1540 PRINT LC$;:SD(HP,VP)=LC
1550 RETURN
1600 REM 
1610 REM ** REMOVE PLUG AND COUNT **
1620 REM 
1630 HO=HN:VE=VN:GOSUB 110
1640 PRINT " ";:SD(HN,VN)=NC
1650 GOSUB 250:REM BELL
1660 C=C+1:SC=SC+100
1670 IF C>7 THEN 7030
1680 RETURN
1700 IF SD(HP,VP-1)=LC THEN GOSUB 1530
1710 RETURN
1750 IF SD(HP,VP+1)=LC THEN GOSUB 1530
1760 RETURN
2000 REM 
2010 REM ** MOVEMENT-MAIN-PROGRAM **
2020 REM 
2030 A1=SD(HP,VP+1):REM CHR UNDER MAN
2040 GOSUB 200
2050 IF IN$<>LE$ THEN 2550
2500 REM 
2510 REM ** MOVE LEFT **
2520 REM 
2530 MD=-1:REM MOVE DIRECTION
2540 GOTO 2600
2550 IF IN$<>RI$ THEN 2680
2560 REM 
2570 REM ** MOVE RIGHT **
2580 REM 
2590 MD=1:REM MOVE DIRECTION
2600 GOSUB 1230:REM ERASE MAN
2610 GOSUB 1700:REM CHECK LADDER UP
2620 GOSUB 1750:REM CHECK LADDER DOWN
2630 IF SD(HP,VP+1)=PC THEN VN=VP+1:HN=HP:GOSUB 1630
2640 HP=HP+MD:REM MAN TO NEW POSITION
2650 IF (HP<0) OR (HP>38) THEN HP=HP-MD
2660 GOTO 3550
2670 REM 
2680 IF IN$<>UP$ THEN 3070
3000 REM 
3010 REM ** MOVE UP **
3020 REM 
3030 A0=SD(HP,VP-1)
3040 IF A0<>LC THEN 5030
3050 GOSUB 1530:VP=VP-1
3060 GOTO 3550
3070 IF IN$<>DO$ THEN 3580
3500 REM 
3510 REM ** MOVE DOWN **
3520 REM 
3530 IF A1<>LC THEN 5030
3540 GOSUB 1530:VP=VP+1
3550 GOSUB 1340:REM PLACE MAN
3560 IF SD(HP,VP+1)=NC THEN 6530
3570 GOTO 5030
3580 IF IN$<>JR$ THEN 4210
4000 REM 
4010 REM ** >JUMP> **
4020 REM 
4030 JD=1:REM JUMP-DIRECTION
4040 GOSUB 1230:REM ERASE MAN
4050 GOSUB 1750:REM  CHECK LADDER DOWN
4060 GOSUB 1700:REM CHECK LADDER UP
4070 HP=HP+JD:VP=VP-1:GOSUB 1330:REM NEW MAN UR
4080 GOSUB 1230:REM ERASE MAN
4090 GOSUB 1700:REM CHECK LADDER UP
4100 HP=HP+JD:GOSUB 1330:REM NEW MAN  URR
4110 GOSUB 1230:REM ERASE MAN
4120 GOSUB 1700:REM CHECK LADDER UP
4130 HP=HP+JD:VP=VP+1:GOSUB 1330:REM NEW MAN RRR
4140 VN=VP+1
4150 IF SD(HP,VN)=NC THEN 6530
4160 HN=HP-3*JD
4170 IF SD(HN,VN)=PC THEN GOSUB 1630
4180 HN=HN+JD:IF HN<>HP THEN 4170
4190 GOTO 5030
4200 REM 
4210 IF IN$<>JL$ THEN 5030
4500 REM 
4510 REM ** <JUMP< **
4520 REM 
4530 JD=-1:GOTO 4040
5000 REM 
5010 REM ** FIREBALLS MOVE **
5020 REM 
5030 FOR FB=1 TO 4
5040 GOSUB 260:BM=2*INT(3*RV)-2
5050 HO=BH(FB):VE=BV(FB):GOSUB 110:PRINT " ";
5060 SD(HO,VE)=NC:HO=HO+BM
5070 IF HO>BR(FB) THEN HO=HO-BM
5080 IF HO<BL(FB) THEN HO=HO-BM
5090 BH(FB)=HO
5100 GOSUB 110:PRINT FC$;
5110 SD(HO,VE)=FC
5120 NEXT FB
5500 REM 
5510 REM ** BONUS & SCORE **
5520 REM 
5530 D=D-10
5540 HO=0:VE=0:GOSUB 110
5550 PRINT "bonus: ";D;TAB(16);"score: ";SC;
5560 PRINT TAB(29);"level: ";LE
5600 IF D<>0 THEN 6030
5610 GOSUB 1430:FOR I=1 TO 2000:NEXT I:GOTO 6620
6000 REM 
6010 REM ** F/BALL KILL CHECK **
6020 REM 
6030 FB=1
6040 IF ABS(BH(FB)-HP)>1 THEN 6060
6050 IF ABS(BV(FB)-VP)<2 THEN 6080
6060 FB=FB+1:IF FB<5 THEN 6040
6070 GOTO 2030
6080 GOSUB 1430:GOTO 6560
6500 REM 
6510 REM ** FALL AND DEATH THROUGH HOLE **
6520 REM 
6530 GOSUB 1230:REM ERASE
6540 VP=VP+1:GOSUB 1430:REM DEAD MAN
6550 IF SD(HP,VP+1)=NC THEN 6530
6560 FOR I=1 TO 300:NEXT I
6570 REM 
6580 REM ** MAN DEAD ACTION **
6590 REM 
6600 JM=JM-1
6610 IF JM>0 THEN 8460:REM NEW LIFE
6620 VE=10:HO=15:GOSUB 110:PRINT "game over"
6630 HO=10:GOSUB 110:PRINT "Another game? (Y/N) ";
6640 GOSUB 210
6650 IF (IN$="y") OR (IN$="Y") THEN 1000
6660 IF (IN$="n") OR (IN$="N") THEN 950
6670 GOTO 6640
7000 REM 
7010 REM ** RAFTERS FALL! **
7020 REM 
7030 HO=10:FOR VE=3 TO 20
7040 GOSUB 110:PRINT " ----------------- ";
7041 GOSUB 110:PRINT "                   ";
7060 NEXT VE
7070 FOR VE=21 TO 22
7080 FOR HO=11 TO 27:GOSUB 110:PRINT RF$;:NEXT HO
7090 NEXT VE:HO=11:GOSUB 250
7100 GOSUB 110:PRINT " xoox -KONG(RIP) ";
7110 REM ** CRASH SOUND **
7120 VE=6:HO=15:GOSUB 110:PRINT "You WIN!!"
7130 VE=8:GOSUB 110:PRINT "You have"
7140 VE=10:HO=13:GOSUB 110:PRINT "conquered kong."
7150 VE=12:HO=12:GOSUB 110:PRINT "isn't true love"
7160 VE=14:HO=14:GOSUB 110:PRINT "wonderful?"
7170 VE=17:HO=12:GOSUB 110:PRINT "Now try again.."
7180 SC=SC+D:LE=LE+1
7190 FOR I=1 TO 5000:NEXT I
7200 GOTO 8460
8000 GOSUB 100
8010 HO=12:VE=9:GOSUB 110
8020 PRINT "CONQUER  KONG"
8030 HO=9:VE=15:GOSUB 110
8040 PRINT "(one moment please)"
8050 REM 
8060 REM ** SETUP SYSTEM VARIABLES **
8070 REM 
8080 DIM SD(39,23):REM SCREEN DUPLICATE
8090 DIM BH(4),BV(4),BL(4),BR(4):REM FIREBALLS
8100 REM 
8110 REM ++++++++++++++++++++++++++++++
8120 REM 
8130 REM ADAPT THE FOLLOWING LINES
8140 REM TO YOUR OWN COMPUTER !
8150 REM 
8160 REM ++++++++++++++++++++++++++++++
8170 REM 
8190 PC$="o":REM PLUG CHARACTER
8200 FC$="*":REM FIREBALL CHARACTER
8210 RF$="-":REM RAFTER CHARACTER
8220 LC$="H":REM LADDER CHARACTER
8230 MC$="#":REM JUMPMAN CHARACTER
8240 DC$="%":REM DEADMAN CHARACTER
8250 UP$="I":REM MOVE UP
8260 DO$="M":REM MOVE DOWN
8270 RI$="K":REM MOVE RIGHT
8280 LE$="J":REM MOVE LEFT
8290 JR$="S":REM JUMP RIGHT
8300 JL$="A":REM JUMP LEFT
8310 REM 
8320 REM +++++++++++++++++++++++++++++
8330 REM 
8340 JM=3:REM NUMBER OF LIVES
8350 SC=0:REM SCORE+BONUSSES
8360 LE=1:REM LEVEL
8370 NC=0:REM EMPTY CODE
8380 PC=1:REM PLUG CODE
8390 FC=2:REM FIREBALL CODE
8400 RF=4:REM RAFTER CODE
8410 LC=8:REM LADDER CODE
8420 MC=16:REM JUMPMAN CODE
8430 DC=32:REM DEAD CODE
8440 D=5000:REM BONUSES
8450 REM 
8460 FOR HO=0 TO 38:FOR VE=0 TO 22:SD(HO,VE)=NC:NEXT VE
8470 GOSUB 110:PRINT DC$;:NEXT HO
8480 C=0:REM # OF PLUGS GONE
8500 REM 
8510 REM ** EXPLANATIONS **
8520 REM 
8530 GOSUB 100
8540 HO=12:VE=1:GOSUB 110
8550 PRINT "KRAZY KONG"
8560 PRINT :PRINT "     by Tim Boone, Southampton"
8570 PRINT :PRINT 
8580 PRINT " The basic idea: you are on the fourth"
8590 PRINT :PRINT "screen of the arcade game. That is,you"
8600 PRINT :PRINT "are in the attic. Knock out the plugs"
8610 PRINT :PRINT "by walking or jumping over them."
8620 PRINT :PRINT "When you have knocked out all the plugs"
8630 PRINT :PRINT "watch what happens!"
8640 PRINT :PRINT "P.S.: Make sure that the fireballs don't"
8650 PRINT "     come too close!"
8660 GOSUB 200:IF IN$<>"" THEN 8660
8670 PRINT :PRINT "           Press <space>               ";
8680 GOSUB 210:GOSUB 100
8690 PRINT :PRINT :PRINT "        Keyboard Commands:"
8700 PRINT :PRINT "          ";UP$;" = UP"
8710 PRINT :PRINT "          ";DO$;" = DOWN"
8720 PRINT :PRINT "          ";LE$;" = LEFT"
8730 PRINT :PRINT "          ";RI$;" = RIGHT"
8740 PRINT :PRINT "          ";JL$;" = JUMP LEFT"
8750 PRINT :PRINT "          ";JR$;" = JUMP RIGHT"
8760 PRINT :PRINT 
8770 PRINT "  You score 100 for each plug removed"
8780 PRINT :PRINT " and win the bonus if you conquer KONG."
8790 PRINT :PRINT 
8800 PRINT "           Press <space>               ";
9000 GOSUB 210
9010 REM 
9020 REM   ** SCENERY **
9030 REM 
9040 GOSUB 100:RESTORE 
9050 REM 
9060 REM ** DRAW FLOORS **
9070 REM 
9080 READ NF
9090 FOR I0=1 TO NF
9100 READ VE,HO,FT
9110 GOSUB 110
9120 FOR I1=HO TO FT
9130 PRINT RF$;:SD(I1,VE)=RF
9140 NEXT I1
9150 NEXT I0
9160 REM 
9170 REM ** DRAW LADDERS **
9180 REM 
9190 READ NF
9200 FOR I0=1 TO NF
9210 READ I1,HP
9220 FOR VP=I1 TO I1-5 STEP -1
9230 GOSUB 1530
9240 NEXT VP
9250 NEXT I0
9260 REM 
9270 REM ** DRAW PLUGS **
9280 REM 
9290 READ NF
9300 FOR I0=1 TO NF
9310 READ VE,HO
9320 GOSUB 110:PRINT PC$;:SD(HO,VE)=PC
9330 NEXT I0
9340 REM 
9350 REM ** PLACE FIREBALLS **
9360 REM 
9370 READ NF
9380 FOR I0=1 TO NF
9390 READ BV(I0),BL(I0),BH(I0),BR(I0)
9400 NEXT I0
9410 REM 
9420 HP=19:VP=22:GOSUB 1340:REM PLACE MAN
9430 HO=0:VE=2:GOSUB 110
9440 FOR Q=JM TO 1 STEP -1:PRINT MC$;:NEXT Q
9450 GOTO 2030
9460 REM 
10000 REM 
10010 REM ** DATA FOR BUILDING **
10020 REM 
10030 REM ** FLOORS **
10040 REM 
10050 DATA 5
10051 REM      # OF FLOORS
10060 DATA 3,6,32,8,4,34,13,2,36,18,0,38,23,0,38
10070 REM 
10080 REM ** LADDERS **
10090 REM 
10100 DATA 14
10110 DATA 22,0,22,19,22,38
10120 DATA 17,2,17,12,17,26,17,36
10130 DATA 12,4,12,19,12,34
10140 DATA 7,6,7,12,7,26,7,32
10150 REM 
10160 REM ** PLUGS **
10170 REM 
10180 DATA 8
10181 REM       # OF PLUGS
10190 DATA 3,10,3,28,8,10,8,28,13,10,13,28,18,10,18,28
10200 REM 
10210 REM ** FIREBALLS **
10220 REM 
10230 DATA 4
10231 REM      # OF FIREBALLS
10240 DATA 2,6,11,32, 7,4,31,34, 12,2,9,36, 17,0,22,38
10250 REM 
11000 REM 
11010 REM        CONQUER KONG
11020 REM 
11030 REM     AN ARCADE-LIKE GAME
11040 REM 
11050 REM ADAPTED FOR PERSONAL COMPUTER
11060 REM             BY:
11070 REM 
11080 REM 
11090 REM          TIM BOONE
11100 REM 
11110 REM         SOUTHAMPTON
11120 REM 
11125 REM 
11130 REM   REVISED FOR BASICODE-2
11140 REM   BY:        J. HAUBRICH
11150 REM         DD. 830217

1000 a=2000:GOTO 20:REM ## eingabe quiz ##
1010 sr$="EINGABEROUTINE FUER QUIZ"
1020 DIM f$(50)
1030 PRINT 
1040 le$="                                       "
1050 li$="         "
1060 st$="******************************rt*soft**"
1070 lf$="     "
1080 z$="=====I===========================F=A==="
1090 GOTO 3010
1100 GOSUB 100:ve=1:ho=5:GOSUB 110:GOSUB 150
1110 ve=3:ho=0:GOSUB 110:PRINT st$
1120 ho=8:GOSUB 110:PRINT"Eingabe per keyboard"
1130 PRINT:INPUT"WISSENSGEBIET ?";f$(0)
1140 ve=5:ho=0:GOSUB 110:PRINT le$
1150 REM *****************************
1160 i=i+1
1170 j=j+1
1180 ve=4:ho=1:GOSUB 110
1190 IF j>1 THEN ze=31:PRINT"ANTWORT ";CHR$(63+j):GOTO 1210
1200 PRINT"FRAGE: ";INT(i/4)+1:ze=87
1210 PRINT z$
1220 ve=22:ho=2:GOSUB 110:GOSUB 1230:GOTO 1240
1230 PRINT"<E>..control<@>..NEXT <DEL>..delete ":RETURN
1240 ve=7:ho=5:GOSUB 110
1250 GOSUB 210:GOSUB 250
1260 IF in=127 THEN GOSUB 1530:GOTO 1250
1270 IF in=13 THEN GOSUB 1390:GOTO 1450
1280 IF (in>27) AND (in<32) THEN GOSUB 1620:GOTO 1250
1290 IF in=64 THEN GOSUB 1390:ix=0:GOTO 1160
1300 PRINT in$;:ix=ix+1:ho=ho+1:f$(i)=f$(i)+in$
1310 IF j>1 THEN 1340
1320 IF (ix=29) OR (ix=58) THEN ve=ve+2:ho=5:GOSUB 110:GOTO 1250
1330 IF (ix=87) THEN GOSUB 250
1340 IF ix>=ze THEN v=ve:h=ho:ve=22:ho=2:GOSUB 110
1350 IF ix>=ze THEN PRINT"ZEILENENDE*NUR<ENT>ODER'@'ODER'<--'"
1360 IF ix>=ze THEN ho=h:ve=v:GOSUB 110:GOSUB 210:GOSUB 250
1370 GOTO 1250
1380 REM ## weiter bei enter **
1390 zz=0
1400 IF i=40 THEN 3010
1410 IF j=4 THEN j=0
1420 FOR iy=7 TO 11:ve=iy:ho=0:GOSUB 110
1430 PRINT le$
1440 NEXT iy:RETURN
1450 za=ze:IF za=87 THEN za=29
1460 FOR iy=7 TO 11 STEP 2:ve=iy:ho=5:GOSUB 110:zz=zz+1
1470 zx=(zz-1)*za+1
1480 PRINT MID$(f$(i),zx,za)
1490 NEXT iy
1500 ho=5:ve=7:GOSUB 110
1510 GOTO 1250
1520 REM ## loeschen des letzten zeichen
1530 ix=ix-1
1540 IF ix<0 THEN ix=0
1550 PRINT" ";:ho=ho-1:GOSUB 110
1560 IF (ho<5) AND (ve>7) THEN ho=33:ve=ve-2:GOSUB 110
1570 f$(i)=LEFT$(f$(i),ix)
1580 IF ix<ze THEN v=ve:h=ho:ve=22:ho=2:GOSUB 110:GOSUB 1230
1590 IF ix<ze THEN ve=v:ho=h:GOSUB 110
1600 RETURN
1610 REM ##pfeiltasten##
1620 IF in=28 THEN ho=ho-1:GOTO 1660
1630 IF in=29 THEN ho=ho+1:GOTO 1660
1640 IF in=30 THEN ve=ve+1:GOTO 1660
1650 IF in=31 THEN ve=ve-1
1660 GOSUB 110
1670 RETURN
1680 REM ## antwort buchstaben ##
1690 GOSUB 100
1700 ve=1:ho=5:GOSUB 110:GOSUB 150:PRINT 
1710 PRINT:PRINT st$:ve=3:ho=7:GOSUB 110
1720 PRINT"Richtige Antworten":PRINT 
1730 ve=10:ho=5:GOSUB 110
1740 FOR i=1 TO 10:PRINT"FRAGE ";i
1750 GOSUB 210:f$(40+i)=in$
1760 ve=10:ho=5:GOSUB 110:PRINT"         ":ve=10:ho=5
1770 GOSUB 110:NEXT i
1780 ve=3:ho=7:GOSUB 110
1790 PRINT"Kontrolle**(R/F)**"
1800 PRINT:PRINT 
1810 FOR i=1 TO 10
1820 ve=10:ho=5:GOSUB 110:PRINT"ANTWORT:";i;f$(40+i)
1830 GOSUB 210
1840 IF (in$<>"R") AND (in$<>"F") THEN 1830
1850 IF in$="F" THEN ve=10:ho=20:GOSUB 110:INPUT"Neu:";f$(40+i)
1860 ve=10:GOSUB 110:PRINT le$:NEXT i
1870 GOTO 3010
1880 GOSUB 100:ve=1:ho=5:GOSUB 110:GOSUB 150
1890 ve=3:ho=0:GOSUB 110:PRINT st$
1900 ho=4:GOSUB 110
1910 PRINT"Abspeichern des Textfile":PRINT 
1920 INPUT"NAME DER DATEI";nf$:nf=1:GOSUB 500
1930 FOR i=0 TO 50
1940 sr$=f$(i):GOSUB 560:NEXT i
1950 GOSUB 580
1960 ve=3:ho=0:GOSUB 110:PRINT st$:ho=4:GOSUB 110
1970 PRINT"PROGRAMMFORTSETZUNG (J/N)"
1980 GOSUB 210:IF in=78 THEN 950
1990 sr$="EINGABEROUTINE FUER QUIZ":GOTO 3010
2000 REM ----------ende---------------
2010 REM ###einlesen der datei###
2020 GOSUB 100
2030 ho=5:ve=1:GOSUB 110:GOSUB 150
2040 ve=3:ho=0:GOSUB 110:PRINT st$
2050 ve=3:ho=10:GOSUB 110:PRINT"Einlesen"
2060 PRINT 
2070 INPUT"DATEINAME?";f$
2080 IF LEN(f$)>7 THEN PRINT"Zu lang!":GOTO 2070
2090 nf$=f$:nf=0:GOSUB 500
2100 FOR i=0 TO 50
2110 GOSUB 540:f$(i)=in$
2120 NEXT i
2130 GOSUB 580:GOTO 3010
3000 REM ###hauptmenue###
3010 GOSUB 100
3020 ve=1:ho=5:GOSUB 110:GOSUB 150
3030 ve=3:ho=5:GOSUB 110
3040 PRINT"* 1 * EINGABE DER DATEI"
3050 ve=5:GOSUB 110
3060 PRINT"* 2 * EINLESEN DER DATEI"
3070 ve=7:GOSUB 110
3080 PRINT"* 3 * EINGABE DER ANTWORTBUCHSTABEN"
3090 ve=9:GOSUB 110
3100 PRINT"* 4 * ABSPEICHERN DER DATEI"
3110 ve=11:GOSUB 110
3120 PRINT"* 5 * DATEI LOESCHEN"
3130 ve=13:GOSUB 110
3140 PRINT"* 6 * DATEI ANSEHEN"
3142 ve=15:GOSUB 110
3144 PRINT"* 7 * PROGRAMMENDE"
3150 PRINT"     =============================="
3160 PRINT 
3170 ve=17:ho=15:GOSUB 110
3180 PRINT"Ihre Wahl?":GOSUB 210
3190 w=VAL(in$)
3200 ON w GOTO 1100,2020,1690,1880,1000,4010,5010
3210 GOTO 3010
4000 REM ### datei ansehen ###
4010 i=0
4020 GOSUB 100:ve=1:ho=0:GOSUB 110:GOSUB 150
4030 ve=3:GOSUB 110:PRINT st$
4040 ve=3:ho=5:GOSUB 110
4050 PRINT"Dateikontrolle"
4060 ve=5:ho=0:GOSUB 110
4070 PRINT f$(i):i=i+1
4080 GOSUB 210
4090 IF i<50 THEN 4020
4100 GOTO 3010
5000 REM ### programmende ###
5010 GOSUB 100:GOTO 950
30000 REM 'quizin' stellt die eingabe-
30010 REM routine fuer das programm
30020 REM 'quiz'dar.das fragen-antwor-
30030 REM ten-textfile umfasst 10 fragen
30040 REM und je 3 vorgegebene antworten
30050 REM zu beachten ist:
30060 REM (1) maximale laenge einer
30070 REM     frage ist 87 zeichen.
30080 REM (2) maximale laenge einer
30090 REM     antwort ist 31 zeichen.
30100 REM es koennen die pfeiltasten
30110 REM genutzt werden.das jeweils
30120 REM letzte zeichen kann mit<del>
30130 REM geloescht werden.mit<enter>
30140 REM wird der string angezeigt.
30150 REM mit <@> wird der string ge-
30160 REM speichert.
32000 REM ----------------------------
32010 REM autor:rene'thoss/01.02.1990
32020 REM   striegistalstr.22
32030 REM   9253 boehrigen
32040 REM   ==============
32050 REM erstellt auf kc 87.11
32060 REM version 1.3
32070 REM ---------------thossisoft-90-
32080 REM
32090 REM erstausstrahlung: ds-kultur
32100 REM -spezial 900725

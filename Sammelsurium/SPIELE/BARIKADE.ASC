1000 A=100:GOTO 20:REM #### BARRIKADE ####
1010 H=HO:V=VE:GOSUB 100
1020 SR$="BARRIKADE":VE=0:HO=INT(H/2)-7:GOSUB 110:GOSUB 150
1030 HO=INT(H/2)-8:VE=INT(V/2):GOSUB 110
1040 PRINT"Versuche, soviele"
1050 HO=INT(H/2)-09:VE=INT(V/2)+2:GOSUB 110
1060 PRINT"Kursorbewegungen wie"
1070 HO=(H/2)-11:VE=INT(V/2)+4:GOSUB 110
1080 PRINT"moeglich auszufuehren !"
1090 HO=INT(0.65*H):VE=V-2:GOSUB 110
1100 SR$="Taste":GOSUB 150:GOSUB 210
1110 GOSUB 100
1120 REM BILDSCHIRMRAND
1130 HO=0:VE=0:GOSUB 110
1140 FOR HO=0 TO H:PRINT"o";:NEXT HO
1150 HO=0:VE=V-1:GOSUB 110
1160 FOR HO=0 TO H:PRINT"o";:NEXT HO
1170 FOR VE=0 TO V-2
1180 HO=0:GOSUB 110:PRINT"o";
1190 HO=H:GOSUB 110:PRINT"o";
1200 NEXT VE
1210 DIM SP(4):DIM ZP(4):DIM C(5)
1220 SP(1)=0:SP(2)=0:SP(3)=1:SP(4)=-1
1230 ZP(1)=1:ZP(2)=-1:ZP(3)=0:ZP(4)=0
1240 GOSUB 260:I=INT(RV*(V-2))+1
1250 GOSUB 260:J=INT(RV*(H-1))+1
1260 VE=I:HO=J:GOSUB 110:PRINT"x";
1270 REM MANUELLER ZUG
1280 GOSUB 200:IF IN=0 THEN 1280
1290 IF IN<28 THEN 1280
1300 IF IN>31 THEN 1280
1310 Z=I:S=J
1320 IF IN=28 THEN S=S-1
1330 IF IN=29 THEN S=S+1
1340 IF IN=30 THEN Z=Z+1
1350 IF IN=31 THEN Z=Z-1
1360 HO=S:VE=Z:GOSUB 220
1370 IF IN=79 THEN 1280
1380 HO=J:VE=I:GOSUB 110:PRINT" ";
1390 N=N+1:I=Z:J=S
1400 HO=S:VE=Z:GOSUB 110:PRINT"x";
1410 REM RECHNERZUG
1420 F=0
1430 FOR K=1 TO 4
1440 Z=I+ZP(K):S=J+SP(K):HO=S:VE=Z:GOSUB 220
1450 IF IN=32 THEN F=F+1
1460 C(K)=F:NEXT K
1470 GOSUB 260:F=INT(F*RV)+1
1480 FOR K=1 TO 4
1490 IF C(5-K)=F THEN L=5-K
1500 NEXT K
1510 Z=I+ZP(L):S=J+SP(L)
1520 HO=S:VE=Z:GOSUB 110:PRINT"o";
1530 IF C(4)>1 THEN 1280
1540 HO=0:VE=0:GOSUB 110:PRINT"GEFANGEN NACH ";N;" SCHRITTEN"
1550 GOSUB 210
1560 GOTO 950
32000 REM ORIGINAL IN "Kleinstrechner TIPS"-HEFT 11
32010 REM Autor: Walter Goergens
32020 REM
32030 REM in BASICODE-3 von
32040 REM Thomas Schaefer (15 Jahre)
32050 REM H.-Heine-Str.42d
32060 REM Perleberg, 2910
32070 REM auf MRB Z 1 0 1 3
32080 REM
32090 REM Erstausstrahlung: Radio DDR
32100 REM -spezial 900501

1000 a=0:GOTO 20:REM labyrinth
1010 REM initialisatie en inlezen:
1020 GOSUB 100:READ lv,lh,a$,b$
1030 lv=lv-1:lh=lh-1:DIM b(lv,lh)
1040 FOR v=0 TO lv
1050 READ l$
1060 FOR h=0 TO lh
1070 m$=mid$(l$,h+1,1)
1080 b(v,h)=0
1090 IF m$=" " THEN 1110
1100 b(v,h)=1:ve=v:ho=h:GOSUB 110:PRINT a$
1110 NEXT h:NEXT v
1200 REM rand van labyrint eventueel herstellen:
1210 FOR v=0 TO lv
1220 h=0:GOSUB 1290
1230 h=lh:GOSUB 1290
1240 NEXT v
1250 FOR h=1 TO lh-1
1260 v=0:GOSUB 1290
1270 v=lv:GOSUB 1290
1280 NEXT h:GOTO 1450
1290 ve=v:ho=h:GOSUB 110:PRINT a$:b(v,h)=1:RETURN
1400 REM vraag naar welke methode:
1450 ve=lv+1:ho=0:GOSUB 110
1460 PRINT"welke methode? (1 of 2)"
1470 GOSUB 200:IF in$<>"1" AND in$<>"2" THEN 1450
1480 ve=lv+1:ho=0:GOSUB 110
1490 PRINT"                       "
1500 IF in$="1" THEN 1610
1510 GOTO 5010
1600 REM het zoeken van de weg(methode1)
1610 r=2:h=1:v=1:GOSUB 2110:a=1:sw=0
1620 la=a:IF sw=1 THEN 1650
1630 FOR v=1 TO lv-1:FOR h=1 TO lh-1
1640 GOSUB 2020:NEXT h:NEXT v:GOTO 1670
1650 FOR v=lv-1 TO 1 STEP -1:FOR h=lh-1 TO 1 STEP -1
1660 GOSUB 2020:NEXT h:NEXT v
1670 IF b(lv-1,lh-1)>1 THEN 1810
1680 IF (la=a) THEN 8060
1690 sw=1-sw:GOTO 1620
1800 REM er is een weg; veeg overbodige stukken weg uit
1810 v=lv-1:h=lh-1
1820 r=b(v,h):b(v,h)=6:IF v=1 AND h=1 THEN 1880
1830 IF r>1 THEN ON r-1 GOTO 1840,1850,1860,1870
1840 h=h-1:GOTO 1820
1850 v=v-1:GOTO 1820
1860 h=h+1:GOTO 1820
1870 v=v+1:GOTO 1820
1880 FOR v=1 TO lv-1:FOR h=1 TO lh-1
1890 IF (b(v,h)<2) OR (b(v,h)=6) THEN 1910
1900 ve=v:ho=h:GOSUB 110:PRINT" "
1910 NEXT h:NEXT v
1920 GOTO 8000
2000 REM subroutine die de buren van een plaats bekijkt
2010 REM en eventueel die plaats tot een pad verheft:
2020 IF b(v,h)>0 THEN RETURN
2030 IF b(v,h-1)<2 THEN 2050
2040 r=2:GOTO 2110
2050 IF b(v-1,h)<2 THEN 2070
2060 r=3:GOTO 2110
2070 IF b(v,h+1)<2 THEN 2090
2080 r=4:GOTO 2110
2090 IF b(v+1,h)<2 THEN RETURN
2100 r=5
2110 ho=h:ve=v:GOSUB 110:PRINT b$
2120 b(v,h)=r:a=a+1:RETURN
5000 REM initialisatie voor methode 2
5010 DIM f(lv,lh):v=1:h=1:f(v,h)=2:GOTO 6010
6000 REM aankomst in een punt
6010 b(v,h)=2
6020 ve=v:ho=h:GOSUB 110:PRINT b$
6030 IF v=lv-1 AND h=lh-1 THEN 8000
6100 REM bekijk een buur
6110 r=b(v,h)
6120 IF r=f(v,h) THEN b(v,h)=r+1:GOTO 6110
6130 IF r=6 THEN 6200
6140 IF r>1 THEN ON r-1 GOSUB 7000,7010,7020,7030
6150 IF b(v,h)=0 THEN 6180
6160 IF r>1 THEN ON r-1 GOSUB 7020,7030,7000,7010
6170 b(v,h)=r+1:GOTO 6110
6180 IF r>1 THEN ON r-1 GOSUB 7040,7050,7060,7070
6190 GOTO 6010
6200 IF (v=1) AND (h=1) THEN 8060
6210 ve=v:ho=h:GOSUB 110:PRINT" "
6220 ON f(v,h)-1 GOSUB 7000,7010,7020,7030
6230 b(v,h)=b(v,h)-1:GOTO 6110
7000 h=h-1:RETURN
7010 v=v-1:RETURN
7020 h=h+1:RETURN
7030 v=v+1:RETURN
7040 f(v,h)=4:RETURN
7050 f(v,h)=5:RETURN
7060 f(v,h)=2:RETURN
7070 f(v,h)=3:RETURN
8000 FOR x=1 TO 100
8010 ve=lv+1:ho=0:GOSUB 110:PRINT"p d g v n e "
8020 ve=lv+1:ho=0:GOSUB 110:PRINT" a   e o d n"
8030 NEXT x
8040 ve=lv+1:ho=0:GOSUB 110:PRINT"klaar met een  toets"
8050 GOSUB 210:GOTO 950
8060 FOR x=1 TO 100
8070 ve=lv+1:ho=0:GOSUB 110:PRINT"g e  p d"
8080 ve=lv+1:ho=0:GOSUB 110:PRINT" e n  a "
8090 NEXT x
8100 ve=lv+1:ho=0:GOSUB 110:PRINT"klaar met een  toets"
8120 GOSUB 210:GOTO 950
25000 REM vertikale lengte
25010 DATA 20
25020 REM horizontale lengte
25030 DATA 20
25040 REM karakter muur
25050 DATA "*"
25060 REM karakter pad
25070 DATA "."
25090 REM doolhof
25100 DATA "********************"
25110 DATA "*    **   **     * *"
25120 DATA "** * *  *    ** ** *"
25130 DATA "*  * ** *** **   * *"
25140 DATA "* **      * ** *** *"
25150 DATA "*  * *  * *        *"
25160 DATA "** * ** * * ********"
25170 DATA "*  * * **    *     *"
25180 DATA "* ** *  **** * *** *"
25190 DATA "*  *     * * * * * *"
25200 DATA "** ***** * * *     *"
25210 DATA "*      * ****** * **"
25220 DATA "**** * *    *   ****"
25230 DATA "*  * *  *** ***    *"
25240 DATA "**    *   *   * ** *"
25250 DATA "*  ** *** * *** *  *"
25260 DATA "* * *     *  *   * *"
25270 DATA "* * * * ** *** * * *"
25280 DATA "*     *        *   *"
25290 DATA "********************"
30000 REM
30010 REM de  doolhof  is  duidelijk
30020 REM zichtbaar   in   de  list;
30030 REM kleine  veranderingen (bv.
30040 REM extra doorgang of versperren
30050 REM van  een  doorgang)   zijn
30060 REM eenvoudig en zichtbaar aan
30070 REM te    brengen    door   de
30080 REM dataregels  die de doolhof
30090 REM bevatten  aan  te  passen,
30100 REM dit   is   dan  voldoende.
30110 REM
30120 REM wilt  u  een  heel   ander
30130 REM doolhof (ook ander formaat
30140 REM is toegestaan), teken deze
30150 REM op  ruitjespapier en breng
30160 REM deze tekening dan binnen in
30170 REM de dataregels (verwijder de
30180 REM oude   regels)   en    pas
30190 REM eventueel   verticale   en
30200 REM horizontale  lengte aan in
30210 REM regels   25010 en   25030,
30220 REM verder   hoeft   niets  te
30230 REM worden veranderd.
30240 REM
30250 REM in  de  doolhof-dataregels
30260 REM hoeven     alleen     maar
30270 REM sterretjes  en  spaties te
30280 REM staan,  als  u een  andere
30290 REM muur  wilt,  verander  dan
30300 REM alleen  het  character  in
30310 REM regel   25050;  als  u een
30320 REM ander  pad wilt,  verander
30330 REM dan alleen  het  character
30340 REM in regel 25070
30350 REM
30500 REM verklaring van gebruikte
30510 REM variabelen:
30520 REM r: "richting"  (2=links,
30530 REM        3=boven,4=rechts,
30540 REM        5=onder)
30550 REM b: bord (methode1:
30560 REM               0=open pad
30570 REM               1=muur
30580 REM         2,3,4,5=zie r)
30590 REM          methode2:
30600 REM                 idem met
30610 REM          6=geen oplossing
30620 REM            door dit punt
30630 REM v: verticale   lus-index
30640 REM h: horizontale lus-index
30650 REM lv: verticale   afmeting
30660 REM     van doolhof
30670 REM lh: horizontale afmeting
30680 REM     van ddolhof
30690 REM f: richting waar het pad
30700 REM   vandaan komt (methode2)
30710 REM sw:switch(om en om waarde
30720 REM    0 en 1)(methode1)
30730 REM a:aantal stukken pad  (1)
30740 REM la:vorige waarde van a
31000 REM *************************
31010 REM *                       *
31020 REM *    geschreven door    *
31030 REM *                       *
31040 REM *      jan de jong      *
31050 REM *                       *
31060 REM *    anrijperdiep 19    *
31070 REM *                       *
31080 REM *    8032 nr  zwolle    *
31090 REM *                       *
31100 REM *                   1983*
31110 REM *************************
31120 REM
31130 REM copyright  nos hilversum

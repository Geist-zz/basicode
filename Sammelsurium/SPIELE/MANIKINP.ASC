1000 KI=4.5:REM STRAALKRACHT
1010 LL=10:REM STRAALLENGTE
1020 TS=.07:REM STRAALSNELHEID
1030 C$="+":C=ASC(C$):REM STRAALCHR
1050 DIM TH(105+LL),TV(105+LL)
1060 SN=0:SO=0:S=0
1080 GOSUB 100
1090 PRINT"Hoeveel regels gaan er op uw beeldscherm";
1100 INPUT SH
1110 PRINT"Hoeveel tekens gaan er op een regel";
1120 INPUT SB
1130 GOSUB 100
1140 HO=INT((SB-19)/2):VE=0:GOSUB 110
1150 PRINT" ================="
1160 VE=1:GOSUB 110:PRINT"!                 !"
1170 VE=2:GOSUB 110:PRINT"!   MANNEKE PIS   !"
1180 VE=3:GOSUB 110:PRINT"!                 !"
1190 VE=4:GOSUB 110:PRINT" ================="
1200 VE=9:GOSUB 110
1210 PRINT"Wilt u instructies ?"
1220 GOSUB 210
1230 IF (IN$<>"j") AND (IN$<>"J") THEN 2520
1500 PRINT:PRINT"Probeer in de pot te plassen."
1510 PRINT"De eerste keer is een voorbeeld."
1520 PRINT"Vul dan voor de kracht b.v. in: 60"
1530 PRINT"en voor de hoek b.v. ook: 60,"
1540 PRINT"dan ziet u wat de bedoeling is."
1550 PRINT:PRINT"Daarna moet u zelf in de strijd."
1560 PRINT"Kies dan als kracht tussen 0 en 100"
1570 PRINT"en voor de hoek tussen 90 en -90"
1580 PRINT"Druk (een toets) als u wilt beginnen."
1590 GOSUB 210
2000 REM  VOORBEELD VOOR POT EN POP
2020 XP=0:YP=SH/2:REM X,Y VAN POP
2030 XQ=SB/2-2:YQ=5+YP:REM X,Y VAN POT
2040 GOSUB 3020:SN=0:SO=0
2500 REM  RANDOM  VOOR POT EN POP
2520 XP=0:GOSUB 260:YP=2+INT((SH-9)*RV)
2530 GOSUB 260:YQ=2+INT((SH-8)*RV)
2535 GOSUB 260:XQ=3+INT((SB-4)*RV)
2540 GOSUB 3020:GOTO 2520
3000 REM  *** SPELSUBROUTINE ***
3020 GOSUB 100
3030 GOSUB 4020:REM TEKEN POP EN POT
3040 VE=SH-4:HO=0:GOSUB 110
3050 PRINT"score is       ";
3060 VE=SH-4:HO=10:GOSUB 110
3070 SR=SN:GOSUB 300:PRINT SR$;" %"
3080 PRINT"score was      ";
3090 VE=SH-3:HO=10:GOSUB 110
3100 SR=SO:GOSUB 300:PRINT SR$;" %"
3110 GOSUB 200:IF IN$<>"" THEN 3110
3120 VE=SH-4:HO=SB-13:GOSUB 110:PRINT"kracht ";:INPUT U
3130 VE=SH-3:HO=SB-13:GOSUB 110:PRINT"hoek   ";:INPUT W
3140 V=U/KI:K=K+1:H=W*3.141592/180:T2=LL+1
3150 VE=SH-2:FOR HO=0 TO 25:GOSUB 110:PRINT" ";:NEXT 
3200 REM  BEWEGING VAN DE STRAAL
3220 FOR IT=0 TO 100:T=IT*TS
3240 REM WIS OUDSTE STRAALTEKEN
3260 VE=TV(T2-LL+1):HO=TH(T2-LL+1)
3270 GOSUB 110:PRINT" "
3300 REM BEREKEN NIEUWSTE PLAATS
3320 YS=INT((SIN(H)*V-5*T)*T)
3330 XS=INT(COS(H)*V*T)
3340 HO=XP+XS+1:VE=YP-YS
3350 IF HO>=SB THEN HO=SB-1:XS=SB-XP-2
3360 IF VE<0 THEN VE=0
3370 GOSUB 110:PRINT C$
3390 REM ONTHOUD DEZE POSITIE
3410 TV(T2)=VE:TH(T2)=HO:VE=YP-YS
3420 T2=T2+1:IF T2>=149 THEN 3800
3440 REM  CONTROLE OF STRAAL IN POT
3450 REM  OF ERNAAST OF TE LAAG
3470 IF (HO=XQ) AND (VE=YQ) THEN 3600
3480 IF (VE=YQ) AND (HO=XQ-1) THEN 3800
3490 IF (VE>YP) AND (VE>YQ) THEN 3800
3500 NEXT IT:GOTO 3800
3600 VE=SH-2:HO=0:GOSUB 110:PRINT"             raak !!!"
3610 REM  MAAK POT WEER HEEL EN
3620 REM  EN TEKEN SPETTERS   **
3640 GOSUB 250:REM BEL!
3650 HO=XQ:VE=YQ:GOSUB 110:PRINT"U"
3660 HO=XQ-3:VE=YQ-3:GOSUB 110:PRINT"\ \!/ /"
3670 HO=XQ-2:VE=YQ-2:GOSUB 110:PRINT"\ ! /"
3680 HO=XQ-1:VE=YQ-1:GOSUB 110:PRINT"\!/"
3690 TE=0
3700 FOR Z=0 TO 1000:NEXT Z
3710 SO=SN:S=S+1:SN=INT(100*S/K+.5):RETURN
3800 VE=SH-2:HO=0:GOSUB 110:PRINT"langs het potje gepiest"
3810 REM  TELLEN VAN PLASBEURTEN EN
3820 REM  SCHRIJVEN VAN CYFER
3840 TE=TE+1:IF TE>9 THEN TE=TE+7
3850 FOR T3=T2-LL TO T2
3860 VE=TV(T3):HO=TH(T3):GOSUB 110:PRINT" "
3870 NEXT T3
3880 HO=XP+XS+1:VE=YP-YS:GOSUB 110:PRINT CHR$(TE+48)
3890 SO=INT(SN):SN=INT(100*S/K+0.5):GOSUB 4020:GOTO 3040
4000 REM   TEKENEN VAN DE POP
4020 HO=XP:VE=YP-1:GOSUB 110:PRINT"O"
4030 VE=YP:GOSUB 110:PRINT">/"
4040 VE=YP+1:GOSUB 110:PRINT"I"
4060 REM   TEKENEN VAN DE POT
4080 HO=XQ:VE=YQ:GOSUB 110:PRINT"U"
4100 HO=0:VE=SH-5:GOSUB 110
4110 FOR I=1 TO SB:PRINT"=";:NEXT :RETURN
5000 REM 
5010 REM DE LENGTE VAN DE STRAAL KUNT
5020 REM  U VERANDEREN IN REGEL 1010
5030 REM       ( LL=10 )
5040 REM 
5050 REM DE INVLOED VAN DE KRACHT KUNT
5060 REM   U VERANDEREN IN REGEL 1000
5070 REM  (DE KRACHT WORD NU GEDEELD
5080 REM    DOOR  KI=4.5 )
5090 REM 
5100 REM DE STRAALSNELHEID KUNT
5110 REM   U VERANDEREN IN REGEL 1020
5120 REM       ( TS=.07 )
5130 REM 
5140 REM HET KARAKTER VAN DE STRAAL
5150 REM  KUNT U VERANDEREN IN REGEL
5160 REM    1040 : C$="+"
5170 REM 
8000 REM BEWERKT VOOR BASICODE-2 DOOR
8010 REM  LEDEN VAN DE BASICODEGROEP
9000 REM  HET ORIGINELE PROGRAMMA WAS
9010 REM      VOOR DE PET GEMAAKT
9020 REM      EN INGEZONDEN IN 1980
9030 REM  VOOR DE HOBBYSCOOPWEDSTRIJD
9040 REM         DOOR :
9050 REM 
9060 REM         JAN COLLY
9070 REM      GRONINGENLAAN 36
9080 REM      9642 EJ VEENDAM
9090 REM      TEL 05987-16140

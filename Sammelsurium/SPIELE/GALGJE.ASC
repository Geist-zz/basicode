1000 A=500:GOTO 20:REM galgje
1010 GOSUB 100:H=HO:V=VE:UW=1-1/HG:P4=.78539816:SV=15
1020 DR$="724722722674652644632624602602605"
1030 VR$="604602602624632644652674722722722"
1040 SR$="GALGJE":HO=H/2-6:VE=0:GOSUB 110:GOSUB 150
1050 PRINT :PRINT :PRINT 
1060 S$="":IF H<40 THEN 1080
1070 FOR I=1 TO (H-40)/2:S$=S$+" ":NEXT I
1080 PRINT S$;"Met dit programma kunt u het bekende"
1090 PRINT S$;"spelletje 'GALGJE' spelen."
1100 PRINT S$;"U krijgt dadelijk een galg te zien en"
1110 PRINT S$;"een rij sterretjes. Elk sterretje staat"
1120 PRINT S$;"voor een letter van een onbekend woord."
1130 PRINT S$;"Door nu een letter in te toetsen kunt u"
1140 PRINT S$;"kijken of deze letter zich in het woord"
1150 PRINT S$;"bevindt. Als dit zo is wordt de letter"
1160 PRINT S$;"(of letters) op de juiste plaats gezet."
1170 PRINT S$;"Mocht de letter niet in het woord voor-"
1180 PRINT S$;"komen dan komt u een stap dichter bij"
1190 PRINT S$;"de galg. Na zes beurten hangt u en"
1200 PRINT S$;"wordt het juiste woord getoond."
1210 PRINT :PRINT S$;"Veel plezier met dit programma!!!"
1220 HO=H/2-10:VE=V-1:SR$="Druk een toets"
1230 GOSUB 110:GOSUB 150:GOSUB 210:CN=0
1240 GOSUB 600
1250 HO=0:VE=0:GOSUB 620:HO=UW:GOSUB 630:VE=UW:GOSUB 630
1260 HO=0:GOSUB 630:VE=0:GOSUB 630
1270 HO=.05:VE=.05:GOSUB 620:HO=.95:GOSUB 630
1280 VE=.95:GOSUB 630:HO=.05:GOSUB 630:VE=.05:GOSUB 630
1290 V1=UW:V2=.95:VE=V2:GOSUB 1420
1300 V1=0:V2=.05:VE=V2:GOSUB 1420
1310 V1=0:V2=.05:HO=0:GOSUB 1450
1320 V1=UW:V2=.95:HO=V1:GOSUB 1450
1330 FOR VE=.7 TO .75 STEP 1/VG
1340 HO=.6:GOSUB 620:HO=.85:GOSUB 630:NEXT VE
1350 HO=.76:VE=.7:GOSUB 620:VE=.3:GOSUB 630
1360 HO=.8:GOSUB 630:VE=.7:GOSUB 630
1370 HO=.84:VE=.7:GOSUB 620:HO=.8:VE=.6:GOSUB 630
1380 HO=.72:VE=.7:GOSUB 620:HO=.76:VE=.6:GOSUB 630
1390 HO=.8:VE=.3:GOSUB 620:HO=.65:GOSUB 630
1400 VE=.35:GOSUB 630:VE=.3:HO=.73:GOSUB 620
1410 HO=.76:VE=.35:GOSUB 630:GOTO 1480
1420 HO=0:GOSUB 620:FOR I=0 TO .94999 STEP .099999
1430 VE=V1:HO=I+.05:GOSUB 630:VE=V2:HO=HO+.05:GOSUB 630
1440 NEXT I:RETURN
1450 VE=.05:GOSUB 620:FOR I=.05 TO .95 STEP .1
1460 HO=V2:VE=I+.05:GOSUB 630:HO=V1:VE=VE+.05:GOSUB 630
1470 NEXT I:RETURN
1480 SR$="@ = stoppen":HO=.4:VE=.8:GOSUB 650
1490 SR$="tik een letter in":HO=.15:VE=.2:GOSUB 650
1500 RESTORE :READ AW:GOSUB 260
1510 FOR I=1 TO INT(RV*AW+1):READ SR$:NEXT I:GOSUB 330
1520 W$=SR$:W3$=W$:AL=LEN(W$):W1$=""
1530 FOR I=1 TO AL:W1$=W1$+"*":NEXT I
1540 SR$=W1$:HO=.2:VE=.4:GOSUB 650:FL=2
1550 AF=0:AG=0:AG$="Goed: 0"
1560 AF$="Fout: 0"
1570 IF FL=0 THEN 1610
1580 SR$=AG$:CN=1:HO=.15:VE=.6:GOSUB 650
1590 SR=AG:GOSUB 300:AG$=LEFT$(AG$,6)+SR$
1600 SR$=AG$:CN=0:GOSUB 650
1610 IF FL=1 THEN 1650
1620 VE=.7:HO=.15:SR$=AF$:CN=1:GOSUB 650
1630 SR=AF:GOSUB 300:AF$=LEFT$(AF$,6)+SR$
1640 CN=0:SR$=AF$:GOSUB 650
1650 GOSUB 210:SR$=IN$:GOSUB 330:IN$=SR$
1660 IF IN$="@" THEN 2240
1670 IF (IN<65) OR (IN>90) THEN GOSUB 250:GOTO 1650
1680 FL=0:W2$=W1$:FOR I=1 TO AL
1690 IF MID$(W$,I,1)<>IN$ THEN 1800
1700 FL=1
1710 IF I>1 THEN 1740
1720 W1$=IN$+RIGHT$(W1$,AL-1):W$="-"+RIGHT$(W$,AL-I)
1730 GOTO 1790
1740 IF I<AL THEN 1770
1750 W1$=LEFT$(W1$,AL-1)+IN$:W$=LEFT$(W$,AL-1)+"-"
1760 GOTO 1790
1770 W1$=LEFT$(W1$,I-1)+IN$+RIGHT$(W1$,AL-I)
1780 W$=LEFT$(W$,I-1)+"-"+RIGHT$(W$,AL-I)
1790 AG=AG+1
1800 NEXT I
1810 IF FL<>0 THEN 2140
1820 SD=3:SP=10:SV=15:GOSUB 400:AF=AF+1
1830 ON AF GOSUB 1900,1980,2020,2050,2080,2110
1840 IF AF<6 THEN 1570
1850 SR$=AF$:CN=1:HO=.15:VE=.7:GOSUB 650
1860 CN=0:SR$=LEFT$(AF$,6)+"6":GOSUB 650
1870 SR$="Helaas...":HO=.3:VE=.14:GOSUB 650
1880 SR$="Het was: "+W3$:VE=.25:GOSUB 650
1890 MU$=DR$:GOSUB 2270:GOTO 2210
1900 HO=.65+.05*3/4:VE=.4:GOSUB 620
1910 FOR I=0 TO 6.28 STEP .1
1920 HO=COS(I)*.05*3/4+.65:VE=SIN(I)*.05+.4:GOSUB 630
1930 NEXT I:HO=.63:VE=.38:GOSUB 620:HO=.64:GOSUB 630
1940 HO=.66:GOSUB 620:HO=.67:GOSUB 630
1950 HO=.64:VE=.42:GOSUB 620:HO=.66:GOSUB 630
1960 HO=.67:VE=.41:GOSUB 630:HO=.63:GOSUB 620
1970 HO=.64:VE=.42:GOSUB 630:RETURN
1980 HO=.65+.07*3/4:VE=.53:GOSUB 620
1990 FOR I=0 TO 6.28 STEP .1
2000 HO=COS(I)*.07*3/4+.65:VE=SIN(I)*.08+.53
2010 GOSUB 630:NEXT I:RETURN
2020 HO=COS(7.5*P4)*.07*3/4+.65
2030 VE=SIN(7.5*P4)*.08+.53:GOSUB 620
2040 VE=.41:HO=.73:GOSUB 630:RETURN
2050 HO=COS(4.5*P4)*.07*3/4+.65
2060 VE=SIN(4.5*P4)*.08+.53:GOSUB 620
2070 VE=.41:HO=.57:GOSUB 630:RETURN
2080 HO=COS(P4)*.07*3/4+.65
2090 VE=SIN(P4)*.08+.53:GOSUB 620
2100 HO=.71:VE=.69:GOSUB 630:RETURN
2110 HO=COS(3*P4)*.07*3/4+.65
2120 VE=SIN(3*P4)*.08+.53:GOSUB 620
2130 HO=.59:VE=.69:GOSUB 630:RETURN
2140 SR$=W2$:CN=1:HO=.2:VE=.4:GOSUB 650
2150 CN=0:SR$=W1$:GOSUB 650:SD=3:SP=80:SV=15:GOSUB 400
2160 IF AL<>AG THEN 1570
2170 HO=.15:VE=.6:CN=1:SR$=AG$:GOSUB 650:SR=AL
2180 GOSUB 300:SR$=LEFT$(AG$,6)+SR$:CN=0:GOSUB 650
2190 SR$="Prima !!!":HO=.5:VE=.2:GOSUB 650
2200 MU$=VR$:GOSUB 2270
2210 SR$="Nogmaals (j/n)":HO=.4:VE=.85:GOSUB 650
2220 GOSUB 210:IF IN=74 THEN 1240
2230 IF IN<>78 THEN 2220
2240 GOSUB 100:PRINT :PRINT :PRINT "        Tot ziens !!!"
2250 GOSUB 210
2260 GOTO 950
2270 FOR I=1 TO 30 STEP 3
2280 H$=MID$(MU$,I,3):SP=VAL(LEFT$(H$,2))
2290 SD=VAL(RIGHT$(H$,1)):GOSUB 400:NEXT I:RETURN
24000 REM U kunt zelf woorden toevoegen.
24010 REM Hiervoor moet u de woorden aan
24020 REM het eind van de datalijst toe-
24030 REM voegen en het getal aan het be-
24040 REM gin van de lijst aanpassen.
24050 REM 
24060 REM Aantal woorden in de lijst
24070 REM 
25000 DATA 21
25010 DATA "boekentas","voetbal","toetsenbord"
25020 DATA "platenspeler","natuurkunde","basicode"
25030 DATA "boekenkast","missisippi","computer"
25040 DATA "programma","televisie","antenne"
25050 DATA "monitor","radio","microfoon"
25060 DATA "seinsleutel","diskette","boormachine"
25070 DATA "stoelpoot","telefoon","interface"
32000 REM ----------------------------------
32010 REM    Door Ronald van Vugt, PA3EAH
32020 REM 
32030 REM    postbus 160, 5580 AD, Waalre
32040 REM 
32050 REM     Met dank aan Peter Foliant
32060 REM 
32070 REM        V1.2, 28 januari 1988
32080 REM ----------------------------------
32090 REM TROS-RADIO dd 880330

1000 A=2000:GOTO 20:REM EXPERT-SYSTEM
1010 GOSUB 100
1020 FOR I=1 TO 6
1030 READ X,N
1040 FOR L=1 TO X
1050 READ HO,VE
1060 HO=HO-1:VE=VE-2
1070 FOR J=0 TO 50:NEXT J:REM VERTRAGING
1080 GOSUB 110:PRINT CHR$(N);
1090 NEXT L
1100 NEXT I
1110 HO=11:VE=14:GOSUB 110:PRINT"(Peter Maathuis)"
1120 HO=2:VE=20:GOSUB 110:PRINT"Wilt u uitleg (J/N)";
1130 INPUT A$:A$=LEFT$(A$,1)
1140 IF (A$<>"J") AND (A$<>"j") THEN 1330
1150 GOSUB 100
1160 PRINT"Dit programma kan vragen op een beperkt"
1170 PRINT"gebied beantwoorden."
1180 PRINT"Wanneer het antwoord niet bekend is,"
1190 PRINT"wordt u gevraagd aanvullende informatie"
1200 PRINT"te verstrekken."
1210 PRINT"De computer zal die nieuwe informatie"
1220 PRINT"in relatie brengen met de bestaande."
1230 PRINT
1240 PRINT"Zo'n programma wordt door deskundigen"
1250 PRINT"op het gebied van kunstmatige intel-"
1260 PRINT"ligentie een 'expert-systeem' genoemd."
1270 PRINT"Dit programma toont het principe van de"
1280 PRINT"opbouw van 'kennis' door een computer."
1290 PRINT
1300 PRINT"Het programma kan diersoorten van"
1310 PRINT"elkaar onderscheiden, maar is ook te"
1320 PRINT"gebruiken voor andere onderwerpen."
1330 PRINT
1340 PRINT"Bedenk een dier en beantwoord de"
1350 PRINT"gestelde vragen met JA of NEE."
1360 PRINT
1370 PRINT"Wilt u zien hoe 'kennis' ontstaat";
1380 INPUT A$:A$=LEFT$(A$,1)
1390 DJ=0
1400 IF (A$<>"J") AND (A$<>"j") THEN 1560
1410 DJ=1
1420 GOSUB 100
1430 PRINT
1440 PRINT"Na het vraag- en antwoordspel ziet u"
1450 PRINT"een lijst met dieren, de kennisboom en"
1460 PRINT"een lijst met vragen."
1470 PRINT"De linker rij getallen hoort bij de"
1480 PRINT"bevestigende antwoorden, de rechter"
1490 PRINT"bij ontkenningen."
1500 PRINT"Een negatief getal verwijst naar een"
1510 PRINT"dier als antwoord, een positief getal"
1520 PRINT"wijst naar een volgende vraag."
1530 REM 
1540 REM --- BEGIN VAN HET PROGRAMMA
1550 REM 
1560 L=50
1570 DIM Q$(L):REM VRAGEN
1580 DIM N$(L+1):REM NAMEN VAN DIEREN
1590 DIM Y(L):REM JA-TAK IN BOOM
1600 DIM N(L):REM NEE-TAK IN BOOM
1610 REM 
1620 REM --- BEGINKENNIS
1630 REM 
1640 N=4:REM HOEVEELHEID
1650 Q$(1)="Is het een zoogdier"
1660 Q$(2)="Is het een vogel"
1670 Q$(3)="Is het een vis"
1680 N$(1)="paard"
1690 N$(2)="duif"
1700 N$(3)="haring"
1710 N$(4)="mug"
1720 REM --- BOOM GEGEVENS
1730 Y(1)=-1:N(1)=2
1740 Y(2)=-2:N(2)=3
1750 Y(3)=-3:N(3)=-4
1760 REM 
1770 REM --- LAAT ZIEN WAT JE WEET
1780 REM 
1790 GOSUB 2010
1800 IF DJ=0 THEN 1950
1810 REM --- TOON DE BOOM
1820 CT=3:CN=0
1830 PRINT
1840 PRINT"--- OVERZICHT VAN DE HUIDIGE KENNIS ---"
1850 PRINT N;" dieren";TAB(11);"ja  nee  ";N-1;" vragen"
1860 PRINT"---------------------------------------"
1870 FOR I=1 TO N-1
1880 PRINT N$(I);TAB(10);
1890 SR=Y(I):GOSUB 310:PRINT SR$;" ";
1900 SR=N(I):GOSUB 310:PRINT SR$;"   ";
1910 PRINT Q$(I)
1920 NEXT I
1930 PRINT N$(N)
1940 PRINT"---------------------------------------"
1950 PRINT
1960 PRINT
1970 PRINT"Bedenk een dier..."
1980 GOTO 1790
1990 REM 
2000 REM --- BEGIN BOVEN IN DE BOOM
2010 X=1
2020 GOSUB 2120
2030 REM --- DIER GEVONDEN
2040 IF (A$="J") AND (Y(X)<0) THEN A=-Y(X):GOTO 2240
2050 IF (A$="N") AND (N(X)<0) THEN A=-N(X):GOTO 2240
2060 REM --- VERDERE VRAGEN BESCHIKBAAR
2070 IF A$="J" THEN X=Y(X)
2080 IF A$="N" THEN X=N(X)
2090 GOTO 2020
2100 REM 
2110 REM --- STEL VRAAG X EN LEES ANTWOORD
2120 PRINT
2130 PRINT Q$(X);
2140 INPUT A$:A$=LEFT$(A$,1)
2150 IF A$="j" THEN A$="J"
2160 IF A$="n" THEN A$="N"
2170 IF (A$="J") OR (A$="N") THEN RETURN
2180 PRINT"Graag als antwoord alleen JA en NEE"
2190 PRINT"of afkortingen daarvan."
2200 GOTO 2120
2210 REM 
2220 REM --- STEL DIER VOOR ALS ANTWOORD
2230 REM 
2240 PRINT
2250 PRINT"Is het een ";N$(A);
2260 INPUT B$:B$=LEFT$(B$,1)
2270 IF (B$="N") OR (B$="n") THEN 2350
2280 IF (B$="J") OR (B$="j") THEN 2310
2290 PRINT"JA of NEE graag..."
2300 GOTO 2240
2310 PRINT
2320 PRINT"Dat dacht ik al..."
2330 RETURN
2340 REM --- NIEUW DIER, BREID KENNISBOOM UIT
2350 PRINT
2360 PRINT"Dan weet ik het niet, ";
2370 IF N<=L THEN 2410
2380 PRINT"maar helaas is"
2390 PRINT"mijn opnamecapaciteit uitgeput!"
2400 RETURN
2410 PRINT"hoe heet het dier"
2420 PRINT"waaraan u denkt";
2430 INPUT B$
2440 IF B$="" THEN 2360
2450 N$(N+1)=B$:REM NIEUW DIER
2460 PRINT
2470 PRINT"Welke VRAAG onderscheidt de dieren"
2480 PRINT"'";N$(A);"' en '";B$;"'"
2490 INPUT C$
2500 IF C$="" THEN 2460
2510 Q$(N)=C$:REM NIEUWE VRAAG
2520 PRINT
2530 PRINT"Wat is voor een '";B$;"' het antwoord"
2540 PRINT"op '";C$;"'";
2550 INPUT D$:D$=LEFT$(D$,1)
2560 IF D$="j" THEN D$="J"
2570 IF D$="n" THEN D$="N"
2580 IF (D$="J") OR (D$="N") THEN 2620
2590 PRINT"JA of NEE graag..."
2600 GOTO 2520
2610 REM --- PLAATS GEGEVENS IN DE BOOM
2620 IF A$="N" THEN N(X)=N
2630 IF A$="J" THEN Y(X)=N
2640 IF D$="N" THEN N(N)=-N-1:Y(N)=-A
2650 IF D$="J" THEN Y(N)=-N-1:N(N)=-A
2660 N=N+1
2670 RETURN
25000 REM DATA VOOR TITEL
25010 DATA 14,101
25020 DATA 4,10,5,10,6,10,7,10,7,9,6,8,5,8,4,8
25030 DATA 3,9,3,10,3,11,4,12,5,12,6,12
25040 DATA 10,120
25050 DATA 9,8,10,9,11,10,10,11,9,12
25060 DATA 13,8,12,9,11,10,12,11,13,12
25070 DATA 14,112
25080 DATA 15,8,15,9,15,10,15,11,15,12,15,13,16,8
25090 DATA 17,8,18,8,19,9,19,10,18,11,17,11,16,11
25100 DATA 14,101
25110 DATA 22,10,23,10,24,10,25,10,25,9,24,8,23,8
25120 DATA 22,8,21,9,21,10,21,11,22,12,23,12,24,12
25130 DATA 9,114
25140 DATA 27,8,27,9,27,10,27,11,27,12
25150 DATA 28,9,29,8,30,8,31,8
25160 DATA 12,116
25170 DATA 34,7,34,8,34,9,34,10,34,11,35,12
25180 DATA 36,12,37,11,33,8,34,8,35,8,36,8
30000 REM **************************************
30010 REM uitgezonden op 14 augustus 1983
30020 REM * EEN BASICODE2-PROGRAMMA ONTWIKKELD *
30030 REM * MET EEN BBC-MICROCOMPUTER (32K)    *
30040 REM *                                    *
30050 REM * DOOR PETER MAATHUIS                *
30060 REM *      EDELSTEENWEG 15               *
30070 REM *      2651 SP  BERKEL EN RODENRIJS  *
30080 REM *                                    *
30090 REM **************************************

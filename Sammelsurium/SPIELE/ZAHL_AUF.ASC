1000 a=2000:GOTO 20:REM zahl auf zahl
1010 GOSUB 100
1020 DIM a(8,8):s=0:c=0:sp=0:ze=0:ma=0
1030 REM
1040 REM ---------- kopf
1050 REM
1060 sr$="ZAHL auf ZAHL"
1070 ho=7:ve=0:GOSUB 110:GOSUB 150
1080 REM
1090 REM ---------- erlaeuterung
1100 REM
1110 ho=0:ve=3:GOSUB 110
1120 PRINT"Der Computer baut ein 8x8 grosses Feld":PRINT 
1130 PRINT"mit Zufallszahlen (1...99) auf und":PRINT 
1140 PRINT"gibt die Startposition vor. Es wird":PRINT 
1150 PRINT"abwechselnd vom Spieler und Computer":PRINT 
1160 PRINT"gezogen und aus den Spalten (Spieler)":PRINT 
1170 PRINT"- Auswahl mit CURSOR UP/DOWN -":PRINT 
1180 PRINT"und Zeilen (Computer) eine Zahl":PRINT 
1190 PRINT"ausgewaehlt. Der Zug wird mit der":PRINT 
1200 PRINT"RETURN-Taste abgeschlossen.":PRINT 
1210 PRINT"Ende/neues Spiel => X"
1220 REM
1230 ho=20:ve=23:GOSUB 110:PRINT"Taste druecken !";
1240 GOSUB 210:GOSUB 100
1250 REM
1260 REM ---------- zahlenfeld
1270 REM
1280 ho=0:ve=2:GOSUB 110:PRINT"Spieler:"
1290 ho=18:ve=2:GOSUB 110:PRINT"Computer:"
1300 ho=0:ve=3:GOSUB 110
1310 PRINT"--------------------------------"
1320 REM
1330 ho=1:ve=4
1340 FOR i=1 TO 8
1350 FOR j=1 TO 8
1360 GOSUB 260:a(i,j)=INT(rv*99)+1
1370 sr=a(i,j):GOSUB 300
1380 IF (a(i,j)<10) THEN sr$=" "+sr$
1390 GOSUB 110:PRINT sr$
1400 ho=ho+4
1410 NEXT j
1420 ho=1:ve=ve+2
1430 NEXT i
1440 REM
1450 REM ---------- spieler
1460 REM
1470 ho=0:ve=21:GOSUB 110
1480 PRINT"Cursor UP/DOWN / RETURN / X"
1490 REM
1500 GOSUB 260:j=INT(rv*8)+1:i=1
1510 sr=a(i,j):GOSUB 300
1520 IF (a(i,j)<10) THEN sr$=" "+sr$
1530 IF (a(i,j)=0) THEN sr$="--"
1540 sr$="*"+sr$+"*"
1550 ho=(j*4)-4:ve=4:GOSUB 110:PRINT sr$;
1560 REM
1570 REM ---------- auswahl
1580 REM
1590 GOSUB 200
1600 IF (i<=1) AND (i>=8) THEN 1590
1610 IF (in=30) THEN f1=0:GOTO 1670
1620 IF (in=31) THEN f1=1:GOTO 1670
1630 IF (in=13) THEN 1810
1640 IF (in$="X") OR (in$="x") THEN 2090
1650 GOTO 1590
1660 REM ---------- cs up/down
1670 sr=a(i,j):GOSUB 300
1680 IF (a(i,j)<10) THEN sr$=" "+sr$
1690 IF (a(i,j)=0) THEN sr$="--"
1700 sr$=" "+sr$+" "
1710 ve=(i*2)+2:GOSUB 110:PRINT sr$;
1720 IF (f1=0) THEN i=i+1:IF (i>=8) THEN i=8
1730 IF (f1=1) THEN i=i-1:IF (i<=1) THEN i=1
1740 sr=a(i,j):GOSUB 300
1750 IF (a(i,j)<10) THEN sr$=" "+sr$
1760 IF (a(i,j)=0) THEN sr$="--"
1770 sr$="*"+sr$+"*"
1780 ve=(i*2)+2:GOSUB 110:PRINT sr$;
1790 GOTO 1590
1800 REM ---------- festlegen
1810 s=s+a(i,j)
1820 a(i,j)=0:sr$=" -- "
1830 ve=(i*2)+2:GOSUB 110:PRINT sr$;
1840 ho=8:ve=2:GOSUB 110:PRINT s;
1850 REM
1860 REM ---------- computer
1870 REM
1880 ho=0:ve=21:GOSUB 110
1890 PRINT"Computer zieht !           "
1900 REM ---------- zug + max.wert
1910 GOSUB 260:i=INT(rv*8)+1
1920 ma=a(i,0):ma=0
1930 FOR j=1 TO 8
1940 IF ma>a(i,j) THEN 1960
1950 ma=a(i,j):mi=i:mj=j
1960 NEXT j
1970 REM ---------- anzeige
1980 c=c+ma
1990 a(mi,mj)=0:sr$=" -- "
2000 ho=(mj*4)-4:ve=(mi*2)+2:GOSUB 110:PRINT sr$;
2010 ho=27:ve=2:GOSUB 110:PRINT c;
2020 REM
2030 REM ---------- naechste folge
2040 REM
2050 GOTO 1470
2060 REM
2070 REM ---------- ende/neues spiel
2080 REM
2090 GOSUB 100
2100 ho=5:ve=10:GOSUB 110
2110 PRINT"Noch ein Spiel ? <J/N>";
2120 GOSUB 210
2130 IF (in$="N") OR (in$="n") THEN 2150
2140 IF (in$="J") OR (in$="j") THEN  GOSUB 100:s=0:c=0:GOTO 1280
2150 ho=5:ve=10:GOSUB 110
2160 PRINT"    Das war es !      "
2170 ho=5:ve=12:GOSUB 110
2180 PRINT"Sie erreichten";s;"Punkte,"
2190 ho=5:ve=14:GOSUB 110
2200 PRINT"der Computer";c;"!!!";
2210 GOSUB 210:GOTO 950
32000 REM ------------------------
32010 REM     'zahl auf zahl'
32020 REM
32030 REM nach einer programmidee:
32040 REM     'zahlenschach'
32050 REM 64'er, april 1989, p.62
32060 REM
32070 REM      c128/c64 mode
32080 REM
32090 REM f.dormeier
32100 REM d-1000 berlin 39
32110 REM
32120 REM mai 1989
32130 REM
32140 REM erstausstrahlung: radio ddr
32150 REM -spezial 900216

1000 a=200:GOTO 20:REM  * word-game *
1010 nn=20:REM  maximum number letters per word
1020 mm=100:REM  number of words
1030 tt=200:REM  FOR  faster letters make tt smaller  
1040 GOSUB 1820:GOSUB 100:le=(le-22)/2:IF le<0 THEN le=0
1050 sc=0:an=0:DIM d$(nn),h$(nn),k(mm)
1060 GOSUB 1860:REM  instructions
1070 an=an+1
1080 IF an=mm+1 THEN an=mm:GOTO 1600
1090 GOSUB 260:y=INT(rv*mm)+1
1100 IF k(y)=1 THEN 1090
1110 k(y)=1:p=0
1120 FOR g=1 TO y:READ g$:NEXT g
1130 n=LEN(g$)
1140 FOR a=1 TO n
1150 d$(a)=MID$(g$,a,1):h$(a)=d$(a)
1160 NEXT a
1170 RESTORE
1180 GOSUB 100:ve=0:ho=le:GOSUB 110
1190 PRINT"**** Word Game ****"
1200 ve=2:GOSUB 110:PRINT"   Total score  ";sc
1210 ve=4:GOSUB 110:PRINT"  Number of words ";an
1220 REM  insertion sort
1230 FOR j=1 TO n-1
1240 b$=h$(j+1)
1250 FOR i=j TO 1 STEP -1
1260 IF b$>=h$(i) THEN 1290
1270 h$(i+1)=h$(i)
1280 NEXT i
1290 h$(i+1)=b$
1300 NEXT j
1310 li=le+(22-n)/2:ho=li:ve=7:GOSUB 110
1320 FOR i=1 TO n:PRINT h$(i);:NEXT i
1330 h$=" "
1340 l=0:FOR q=1 TO n:IF h$(q)<>d$(q) THEN l=l+1
1350 NEXT q:IF l=0 THEN 1510
1360 GOSUB 260:x=INT(rv*n)
1370 IF (h$(x)=d$(x)) OR (h$(x)=" ") THEN 1360
1380 i$=h$(x):h$(x)=h$:IF i$=" " THEN GOSUB 1700:GOTO 1330
1390 p=0:FOR q=1 TO n
1400 IF (i$=d$(q)) AND (d$(q)<>h$(q)) THEN p=q
1410 NEXT q:IF p=0 THEN GOTO 950
1420 c=1:IF x>p THEN c=-1
1430 ve=7:ho=x-1+li:GOSUB 110:PRINT h$:ve=6
1440 ho=x-1+li:GOSUB 110:PRINT i$:IF x=p THEN 1490
1450 FOR j=1 TO tt:NEXT j
1460 GOSUB 200:IF in$=" " THEN 1720
1470 GOSUB 110:PRINT" "
1480 x=x+c:GOTO 1440
1490 FOR j=1 TO tt/2:NEXT j:l=l-1
1500 h$=i$:GOTO 1380
1510 sr=INT(l/n*20)*5
1520 sc=sc+sr:GOSUB 120:ve=ve+1:ho=le
1530 GOSUB 110:PRINT"Number of points  ";sr
1540 ve=ve+2:GOSUB 110:PRINT"Want another word? "
1550 ve=ve+2:GOSUB 110:PRINT"       (y/n)";
1560 GOSUB 210
1570 IF (in$="Y") OR (in$="y") THEN 1070
1580 IF (in$="N") OR (in$="n") THEN 1620
1590 GOTO 1560
1600 GOSUB 120:ve=ve-1:ho=le:GOSUB 110
1610 PRINT"Sorry,no more words left!":FOR t=1 TO 2000:NEXT t
1620 GOSUB 100:ve=2:ho=le+1:GOSUB 110
1630 PRINT"You scored ";sc;" points"
1640 ve=4:ho=le+4:GOSUB 110:PRINT"after ";an;" word";
1650 IF an>1 THEN PRINT"s."
1660 ve=6:ho=le+1:GOSUB 110:PRINT"That's an average of ":ve=8
1670 ho=le+1:GOSUB 110:PRINT INT(sc/an+.5);" points per word."
1680 ve=10:ho=le+1:GOSUB 110
1690 PRINT"Goodbye from the Basicode team.":end
1700 ve=6:ho=x-1+li:GOSUB 110:PRINT" ":ve=7
1710 GOSUB 110:PRINT h$:RETURN
1720 ve=9:ho=le
1730 GOSUB 110:PRINT"The word is"
1740 ve=11:ho=li-2:GOSUB 110:INPUT wo$:ve=13:ho=le:GOSUB 110
1750 w$=wo$:wo$="":FOR i=1 TO LEN(w$):cc$=MID$(w$,i,1)
1760 IF cc$>="A" AND cc$<="Z" THEN 1780
1770 cc$=CHR$(ASC(cc$)-ASC("a")+ASC("A"))
1780 wo$=wo$+cc$:NEXT i
1790 IF wo$=g$ THEN PRINT"  You got it right!!!":GOTO 1510
1800 PRINT"Sorry,the word was"
1810 ve=ve+2:ho=li:GOSUB 110:PRINT g$:l=0:GOTO 1510
1820 REM  screen size determination
1830 GOSUB 100:ve=0:ho=10
1840 ho=ho+1:le=ho:GOSUB 110:GOSUB 120:IF le=ho THEN 1840
1850 le=le-1:RETURN
1860 GOSUB 100:ve=0:ho=le:GOSUB 110
1870 PRINT"**** Word  Game ****"
1880 ve=3:GOSUB 110:PRINT"The rules are very simple."
1890 ve=ve+1:GOSUB 110:PRINT"You will see a word appear, but"
1900 ve=ve+1:GOSUB 110:PRINT"the letters have been jumbled."
1910 ve=ve+1:GOSUB 110:PRINT"The computer will start to put"
1920 ve=ve+1:GOSUB 110:PRINT"the letters in the"
1930 ve=ve+1:GOSUB 110:PRINT"correct order."
1940 ve=ve+1:GOSUB 110:PRINT"Press the space-bar"
1950 ve=ve+1:GOSUB 110:PRINT"as soon as you know the word."
1960 ve=ve+1:GOSUB 110:PRINT"Good Luck !!!"
1970 ve=ve+2:GOSUB 110:PRINT"Press any key to start";
1980 GOSUB 210:RETURN
25000 DATA "RAINBOW","COMPUTER"
25010 DATA "GRAMOPHONE","DICTIONARY","DESERT"
25020 DATA "MUSHROOMS","DISHWASHER","PAINTING"
25030 DATA "SOFTWARE","UMBRELLA","WIRELESS"
25040 DATA "CROSSWORDPUZZLE","REFRIGERATOR"
25050 DATA "PLEBISCITE","PLANETARIUM"
25060 DATA "ARTISTS","CASSETTERECORDER"
25070 DATA "NEWSAGENT","MEDALLION","INTROVERT"
25080 DATA "HOROSCOPE","HARMONY","CHIPSHOP"
25090 DATA "PHILATELIST","CAMERA","HUMMINGBIRD"
25100 DATA "BAROMETER","MICROWAVE","FISHCAKE"
25110 DATA "FOUNDRY","BASIC","BIOCHEMISTRY"
25120 DATA "ANCIENT","MERIDIAN","SLOPE"
25130 DATA "GRAPEVINE","SUPERMARKET"
25140 DATA "PARROT","WATERMELON","CHURCH"
25150 DATA "CURTAINS","GOOSEPIMPLES","TELEPHONE"
25160 DATA "PIPECLEANER","CALCULATOR","DWARF"
25170 DATA "TRIBUTARY","WAISTLINE","VULTURE"
25180 DATA "SCREWDRIVER","WINTER","WINDOWSILL"
25190 DATA "WINDBREAK","WOODPECKER","GUESSWORK"
25200 DATA "GLISTEN","EMPHASIS","INTERIOR"
25210 DATA "MAMMAL","PASTEURIZE","PRONUNCIATION"
25220 DATA "STEWARDESS","STICK","TRANSISTOR"
25230 DATA "JUKEBOX","MEDIA","NETWORK"
25240 DATA "WINDSCREEN","ULTIMATUM","VICAR"
25250 DATA "TEACHER","EXAMS","WORLDWIDE"
25260 DATA "HOCKEYSTICK","WREATHE","RECOGNISE"
25270 DATA "ICECREAM","RECRUIT","SQUIGGLE"
25280 DATA "CHESSBOARD","VIDEORECORDER","INDUSTRY"
25290 DATA "MUSEUM","DUSTBIN","HEADPHONES"
25300 DATA "TORCH","TRANSFORMER","PYTHON"
25310 DATA "INSINUATE","FOODSTUFF","FOLIAGE"
25320 DATA "LIFEBOAT","GHOST","HUMBUG","MALNUTRITION"
25330 DATA "NITROGEN","NUISANCE","PERISHABLE"
25340 DATA "PRONOUNCE","ROOF","SHABBY","SPIDER"
25350 DATA "TACKLE","SYZYGY","UNFAIR"
25360 DATA "VERIFY","SLOWCOACH","SMOKESTACK"
25370 DATA "STRIPE","TRANSLATION","VOCABULARY"
25380 DATA "WATCHDOG","SIRLOIN","PAPERBACK"
25390 DATA "MIRROR","HOMESICK","PARASITE"
25400 DATA "West German TV, WDR-3."
25410 DATA "  ","Enough talking. Remember BASICODE"
25420 DATA "doesn't stop with this cassette."
25430 DATA "Try writing a few programs yourself."
25440 DATA "For now, best wishes from the BASICODE"
25450 DATA "developers in The Netherlands."
25460 DATA "."
30000 REM  ***********************
30010 REM  * made on a vic 20    *
30020 REM  *  on 12.12 1983      *
30030 REM  *   r.van der kraan   *
30040 REM  *  delflandstraat 20  *
30050 REM  *  2631 hd  nootdorp  *
30060 REM  *    holland          *
30070 REM  * translated for      *
30080 REM  * radio netherlands   *
30090 REM  * by jonathan  marks  *
30100 REM  ***********************

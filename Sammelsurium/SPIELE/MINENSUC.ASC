1000 a=500:GOTO 20:REM minensuche
1010 REM
1020 REM grundeinstellung
1030 REM
1040 mz=ve-4
1050 ms=ho-3
1060 DIM lf(250)
1070 DIM sf(mz,ms)
1080 GOTO 3700
1100 REM
1110 REM feld initialisieren
1120 REM
1130 FOR i=1 TO sz
1140 FOR j=1 TO ss
1150 sf(i,j)=0
1160 NEXT j
1170 NEXT i
1180 GOSUB 110:PRINT 0
1190 FOR i=1 TO bo
1200 GOSUB 260:ze=INT(sz*rv+1)
1210 GOSUB 260:sp=INT(ss*rv+1)
1220 IF sf(ze,sp)=-9 THEN 1200
1230 sf(ze,sp)=-9
1240 FOR j=ze-1 TO ze+1
1250 FOR k=sp-1 TO sp+1
1260 IF (j<1) OR (j>sz) OR (k<1) OR (k>ss) THEN 1280
1270 IF sf(j,k)>-9 THEN sf(j,k)=sf(j,k)-1
1280 NEXT k
1290 NEXT j
1300 GOSUB 110:PRINT i
1310 NEXT i
1320 RETURN
1400 REM
1410 REM einstellung anfaenger
1420 REM
1430 sz=INT((2*mz/3)+0.5)
1440 ss=INT((ms/2)+0.5)
1450 bo=INT((ss*sz/7)+0.5)
1460 xs=INT(((ms-ss)/2)+1.5)
1470 xz=INT(((mz-sz)/2)+1)
1480 RETURN
1500 REM
1510 REM einstellung fortgeschrittene
1520 REM
1530 sz=mz
1540 ss=INT((2*ms/3)+0.5)
1550 bo=INT((ss*sz/7)+0.5)
1560 xs=INT(((ms-ss)/2)+1.5)
1570 xz=1
1580 RETURN
1600 REM
1610 REM einstellung profis
1620 REM
1630 sz=mz
1640 ss=ms
1650 bo=INT((ss*sz/7)+0.5)
1660 xs=1
1670 xz=1
1680 RETURN
1700 REM
1710 REM bombe markieren
1720 REM
1730 IF (an=bo) AND (sf(ze,sp)>-10) THEN 1780
1740 IF sf(ze,sp)>-10 THEN an=an+1:GOTO 1770
1750 IF sf(ze,sp)>-20 THEN an=an-1:GOTO 1770
1760 sf(ze,sp)=sf(ze,sp)+30
1770 sf(ze,sp)=sf(ze,sp)-10
1780 RETURN
1800 REM
1810 REM aufblaettern
1820 REM
1830 z1=ze:s1=sp:af=af+1:nf=0
1840 ho=xs+sp:ve=xz+ze:GOSUB 110:PRINT" "
1850 sf(ze,sp)=1
1860 FOR i=ze-1 TO ze+1
1870 FOR j=sp-1 TO sp+1
1880 IF (i<1) OR (i>sz) OR (j<1) OR (j>ss) THEN 1980
1890 IF sf(i,j)>0 THEN 1980
1900 af=af+1
1910 IF sf(i,j)>-10 THEN 1950
1920 IF sf(i,j)>-20 THEN an=an-1:GOTO 1940
1930 sf(i,j)=sf(i,j)+10
1940 sf(i,j)=sf(i,j)+10
1950 b=ABS(sf(i,j)):s=j:z=i:GOSUB 2100
1960 IF sf(i,j)=0 THEN nf=nf+1:lf(nf)=100*i+j
1970 sf(i,j)=1
1980 NEXT j
1990 NEXT i
2000 IF nf=0 THEN 2050
2010 ze=INT(lf(nf)/100)
2020 sp=lf(nf)-100*ze
2030 nf=nf-1
2040 GOTO 1850
2050 sp=s1:ze=z1
2060 RETURN
2100 REM
2110 REM zeichen setzen
2120 REM
2130 IF b<=-20 THEN b$="?":GOTO 2190
2140 IF b<=-10 THEN b$="o":GOTO 2190
2150 IF b<0 THEN b$=".":GOTO 2190
2160 IF b=0 THEN b$=" ":GOTO 2190
2170 IF b=9 THEN b$="O":GOTO 2190
2180 sr=b:GOSUB 300:b$=sr$
2190 ho=xs+s:ve=xz+z:GOSUB 110
2200 PRINT b$
2210 RETURN
2300 REM
2310 REM anfangsbild zeichnen
2320 REM
2330 b$="........................................"
2340 b1$="----------------------------------------"
2350 b$=LEFT$(b$,ss):b$="I"+b$+"I":b1$=LEFT$(b1$,ss+2)
2360 ho=xs:ve=xz:GOSUB 110:PRINT b1$
2370 FOR i=1 TO sz:ho=xs:ve=xz+i:GOSUB 110:PRINT b$:NEXT i
2380 ho=xs:ve=xz+sz+1:GOSUB 110:PRINT b1$
2390 RETURN
2400 REM
2410 REM feld aufdecken
2420 REM
2430 IF sf(ze,sp)>0 THEN 2660
2440 IF (sf(ze,sp)<-9) AND (sf(ze,sp)>-20) THEN an=an-1
2450 IF sf(ze,sp)<-9 THEN sf(ze,sp)=sf(ze,sp)+10:GOTO 2450
2460 IF sf(ze,sp)<0 THEN 2490
2470 GOSUB 1800
2480 GOTO 2660
2490 IF sf(ze,sp)=-9 THEN 2540
2500 b=ABS(sf(ze,sp)):s=sp:z=ze:GOSUB 2100
2510 af=af+1
2520 sf(ze,sp)=1
2530 GOTO 2660
2540 fe=2
2550 GOSUB 250
2560 GOSUB 250
2570 FOR i=1 TO sz
2580 FOR j=1 TO ss
2590 b=sf(i,j)
2600 IF b<-9 THEN b=b+10:GOTO 2600
2610 IF b=-9 THEN b=9:z=i:s=j:GOSUB 2100
2620 NEXT j
2630 NEXT i
2640 GOSUB 250
2650 GOSUB 250
2660 RETURN
2700 REM
2710 REM informationen
2720 REM
2730 GOSUB 100
2740 PRINT
2750 PRINT
2760 PRINT"               Minensuche"
2770 PRINT"               =========="
2780 PRINT
2790 PRINT"Ziel dieses Spiels ist es,  alle Minen"
2800 PRINT"im Spielfeld zu finden.  Dazu kann man"
2810 PRINT"sich im Spielfeld bewegen  (Figur: *)."
2820 PRINT"Mit der SPACE-Taste kann das Feld, auf"
2830 PRINT"dem man steht, aufgedeckt werden (noch"
2840 PRINT"nicht aufgedecktes Feld: .). Eine Zahl"
2850 PRINT"gibt an,  wieviele  Minen  sich in den"
2860 PRINT"umgebenden 8 Feldern befinden. Felder,"
2870 PRINT"auf denen man Minen vermutet,  koennen"
2880 PRINT"mit der Taste  <ENTER> markiert werden"
2890 PRINT"(durch: o). Eine Markierung kann durch"
2900 PRINT"insgesamt  dreimaliges   Druecken  der"
2910 PRINT"ENTER-Taste wieder  aufgehoben werden."
2920 PRINT
2930 PRINT
2940 PRINT"                  Weiter mit Taste ..."
2950 GOSUB 210
2960 GOSUB 100
2970 PRINT
2980 PRINT
2990 PRINT"Ein  ? gibt an,  dass auf  diesem Feld"
3000 PRINT"eine  Mine  vermutet  wird.  Sie  wird"
3010 PRINT"nicht  als  markiert  gewertet.  Ein ?"
3020 PRINT"erhaelt man durch zweimaliges Druecken"
3030 PRINT"der  ENTER-Taste.   Sind  alle  Felder"
3040 PRINT"entweder  aufgedeckt   oder  als  Mine"
3050 PRINT"markiert,  so ist  das Spiel  beendet."
3060 PRINT"Wird eine Mine aufgedeckt,  so hat man"
3070 PRINT"verloren. Es gibt drei Schwierigkeits-"
3080 PRINT"stufen,  unter denen man waehlen kann."
3090 PRINT"Vorsicht!  Man kann hoechstens soviele"
3100 PRINT"Felder markieren,  wie Minen vorhanden"
3110 PRINT"sind.  Die noch vorhandene Anzahl wird"
3120 PRINT"oben angegeben."
3130 PRINT
3140 PRINT
3150 PRINT"                  Weiter mit Taste ..."
3160 GOSUB 210
3170 GOSUB 100
3180 RETURN
3200 REM
3210 REM tastaturabfrage
3220 REM
3230 GOSUB 200:IF in=0 THEN 3230
3250 IF in=32 THEN ta=6
3260 IF in=31 THEN ta=1
3270 IF in=30 THEN ta=2
3280 IF in=29 THEN ta=3
3290 IF in=28 THEN ta=4
3300 IF in=13 THEN ta=5
3310 IF ta=0 THEN 3230
3320 RETURN
3400 REM
3410 REM titelbild
3420 REM
3430 GOSUB 100
3440 PRINT
3450 PRINT"#######################################"
3460 PRINT"#                                     #"
3470 PRINT"#                                     #"
3480 PRINT"#             Minensuche              #"
3490 PRINT"#             ==========              #"
3500 PRINT"#                                     #"
3510 PRINT"#                                     #"
3520 PRINT"#     1 - Spielinformationen          #"
3530 PRINT"#                                     #"
3540 PRINT"#     2 - Anfaenger                   #"
3550 PRINT"#                                     #"
3560 PRINT"#     3 - Fortgeschrittene            #"
3570 PRINT"#                                     #"
3580 PRINT"#     4 - Profis                      #"
3590 PRINT"#                                     #"
3600 PRINT"#     5 - Spielende                   #"
3610 PRINT"#                                     #"
3620 PRINT"#                                     #"
3630 PRINT"#######################################"
3640 GOSUB 200:IF in=0 THEN 3640
3650 IF (in<49) OR (in>53) THEN 3640
3660 wa=in-48
3670 RETURN
3700 REM
3710 REM hauptprogramm
3720 REM
3730 GOSUB 3400
3740 IF wa=5 THEN 950
3750 IF wa>1 THEN 3780
3760 GOSUB 2700
3770 GOTO 3730
3780 ON wa-1 GOSUB 1400,1500,1600
3790 GOSUB 100
3800 ve=2:ho=8:GOSUB 110
3810 IF wa=2 THEN sr$="Anfaenger"
3820 IF wa=3 THEN sr$="Fortgeschrittene"
3830 IF wa=4 THEN sr$="Profis"
3840 GOSUB 150
3850 ve=5:ho=13:GOSUB 110
3860 PRINT"Moment bitte !"
3870 ve=10:ho=5:GOSUB 110
3880 PRINT"gelegte  Minen:"
3890 ho=21
3900 GOSUB 1100
3910 GOSUB 100
3920 GOSUB 2300
3930 ho=5:ve=0:GOSUB 110
3940 PRINT" Minen:"
3950 ze=1
3960 sp=1
3970 an=0:af=0
3980 fe=0
4000 REM
4010 REM spiel
4020 REM
4030 ho=12:ve=0:GOSUB 110
4040 sr=bo-an
4050 GOSUB 300
4060 IF LEN(sr$)<3 THEN sr$=" "+sr$:GOTO 4060
4070 PRINT sr$
4080 ho=xs+sp:ve=xz+ze:GOSUB 220
4090 ab$=chr$(in)
4100 IF ab$="O" THEN ab$="o"
4110 GOSUB 110
4120 PRINT"*"
4130 GOSUB 3200
4140 IF ta<5 THEN 4290
4150 IF sf(ze,sp)>0 THEN 4130
4160 IF ta=6 THEN 4250
4170 IF (an=bo) AND (sf(ze,sp)>-10) THEN 4220
4180 IF ab$="?" THEN ab$=".":GOTO 4210
4190 IF ab$="o" THEN ab$="?":GOTO 4210
4200 IF ab$="." THEN ab$="o"
4210 GOSUB 1700
4220 ho=xs+sp:ve=xz+ze:GOSUB 110:PRINT ab$
4230 IF bo+af=ss*sz THEN 4440
4240 GOTO 4000
4250 GOSUB 2400
4260 IF fe=2 THEN 4350
4270 IF bo+af=ss*sz THEN 4440
4280 GOTO 4030
4290 ho=xs+sp:ve=xz+ze:GOSUB 110:PRINT ab$
4300 IF (ta=1) AND (ze>1) THEN ze=ze-1
4310 IF (ta=2) AND (ze<sz) THEN ze=ze+1
4320 IF (ta=3) AND (sp<ss) THEN sp=sp+1
4330 IF (ta=4) AND (sp>1) THEN sp=sp-1
4340 GOTO 4000
4350 ve=0:ho=5:GOSUB 110
4360 PRINT"                "
4370 ho=7
4380 GOSUB 110
4390 sr$="VERLOREN!"
4400 GOSUB 150
4410 ho=30:GOSUB 110:PRINT"Taste..."
4420 GOSUB 200:IF in=0 THEN 4420
4430 GOTO 3700
4440 ve=0:ho=5:GOSUB 110
4450 PRINT"                "
4460 ho=7
4470 GOSUB 110
4480 sr$="GEWONNEN!"
4490 GOTO 4400
30000 REM
30010 REM ****************************
30020 REM *                          *
30030 REM *                          *
30040 REM *        minensuche        *
30050 REM *        ==========        *
30060 REM *                          *
30070 REM *                          *
30080 REM * ein logisches spiel fuer *
30090 REM *                          *
30100 REM * jeden.   eine  anleitung *
30110 REM *                          *
30120 REM * ist  im programm  selbst *
30130 REM *                          *
30140 REM * enthalten.  dieses  prg. *
30150 REM *                          *
30160 REM * wurde von  den pc's kon- *
30170 REM *                          *
30180 REM * vertiert.  eine  bessere *
30190 REM *                          *
30200 REM * grafische gestaltung war *
30210 REM *                          *
30220 REM * in  basicode - 3  leider *
30230 REM *                          *
30240 REM * noch nicht moeglich.     *
30250 REM *                          *
30260 REM * ich wuensche  viel spass *
30270 REM *                          *
30280 REM * mit diesem denkspiel.    *
30290 REM *                          *
30300 REM *                          *
30310 REM *              olaf krause *
30320 REM *                          *
30330 REM *                          *
30340 REM ****************************
30350 REM
32000 REM
32010 REM ****************************
32020 REM *                          *
32030 REM *                          *
32040 REM * autor:                   *
32050 REM * ======                   *
32060 REM *                          *
32070 REM *                          *
32080 REM *   olaf krause            *
32090 REM *   h.-heine-str. 13/32    *
32100 REM *   freiberg               *
32110 REM *   o-9200                 *
32120 REM *                          *
32130 REM *                          *
32140 REM * erstellt vom 20. bis 21. *
32150 REM *                          *
32160 REM * januar 1992  in freiberg *
32170 REM *                          *
32180 REM *                          *
32190 REM * programmiert auf kc85/4  *
32200 REM *                          *
32210 REM * getestet auf: kc85/4     *
32220 REM *               cpc 464    *
32230 REM *               c plus/4   *
32240 REM *                          *
32250 REM *                          *
32260 REM ****************************
32270 REM

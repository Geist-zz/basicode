1000 a=500:GOTO 20:REM "Reaktion"
1010 DIM l$(8):aa=ASC("A"):sp=0:sv=0
1020 t$=" "
1030 GOSUB 100
1040 PRINT:PRINT
1050 sr$=" R E A K T I O N":GOSUB 150:PRINT:PRINT
1060 PRINT"Das Programm laesst kurz eine Zahl"
1070 PRINT"ein Wort oder eine Autonummer"
1080 PRINT"auf dem Bildschirm erscheinen. Das Er-"
1090 PRINT"schienene muss danach genau eingeben"
1100 PRINT"werden. Nur wenn das richtig gemacht"
1110 PRINT"wurde, wird die folgende Anzeigezeit "
1120 PRINT"kuerzer. Wenn es nicht richtig war, dann"
1130 PRINT"dauert es beim naechsten mal etwas "
1140 PRINT"laenger. Unter dem Ausgabefeld steht die"
1150 PRINT"Ausgabezeit."
1160 PRINT"Mach sie so klein wie moeglich!"
1170 PRINT:PRINT:PRINT"Eine Taste --->";:GOSUB 210:GOSUB 100
1180 PRINT:PRINT:gp=0.0001:sd=20:REM >> flitsduur <<
1190 sr$=" W A E H L E ":GOSUB 150:PRINT:PRINT
1200 PRINT"( S = Stoppen )":PRINT:PRINT
1210 PRINT"Willst du Zahlen ":PRINT"          Woerter,"
1220 PRINT"          Autonummern ?":PRINT
1230 PRINT"Druecke Z, W oder A (S=Stop)";:GOSUB 210
1240 IF in=83 THEN 950
1250 IF (in<>65) AND (in<>90) AND (in<>87) THEN PRINT:GOTO 1230
1260 PRINT:PRINT:k1=1:IF in=87 THEN k1=2
1270 IF in=65 THEN k1=3:k2=8
1280 ON k1 GOTO 1290,1300,2000
1290 PRINT"Zahlen mit wieviel Ziffern?":GOTO 1310
1300 PRINT"Woerter mit wieviel Buchstaben?"
1310 PRINT"Druecke 1, 2, 3, 4 oder 5 ";:GOSUB 210
1320 IF in=83 THEN 950
1330 IF (in<49) OR (in>53) THEN PRINT:GOTO 1310
1340 k2=in-48
2000 GOSUB 600:ph=1/hg:pv=1/vg
2010 REM ---trek lijnen---
2020 cn=0:ho=.15:ve=.3:GOSUB 620:ho=.38:GOSUB 630
2030 ho=.42+k2*9*ph:ve=.3:GOSUB 620:ho=.85:GOSUB 630
2040 ho=0:ve=.2:GOSUB 620:ho=1-ph:GOSUB 630
2050 ve=.4:GOSUB 630:ho=0:GOSUB 630:ve=.2:GOSUB 630
2060 ho=.3:ve=.1:sr$="! = MENU":GOSUB 650
3000 REM ===hoofdprogramma===
3010 ON k1 GOTO 3030,3060,3250
3020 REM ---getallengenerator---
3030 GOSUB 260:sr=10000+INT(rv*90000)
3040 GOSUB 300:x$=LEFT$(sr$,k2):sr=VAL(x$):GOTO 3510
3050 REM ---woordengenerator---
3060 ON k2 GOTO 3080,3080,3120,3150,3200
3070 REM ---1 letter---
3080 GOSUB 260:x=aa+INT(rv*26):x$=CHR$(x):IF k2=1 THEN 3510
3090 REM ---2 letters---
3100 GOSUB 260:x=aa+INT(rv*26):x$=x$+CHR$(x):GOTO 3510
3110 REM ---3 letters---
3120 RESTORE:GOSUB 260:FOR n=1 TO rv*50:READ x$:NEXT n
3130 GOTO 3510
3140 REM ---4 letters---
3150 RESTORE
3160 READ x$:IF x$<>"W4" THEN 3160
3170 GOSUB 260:FOR n=1 TO rv*50:READ x$:NEXT n
3180 GOTO 3510
3190 REM ---5 letters---
3200 RESTORE
3210 READ x$:IF x$<>"W5" THEN 3210
3220 GOSUB 260:FOR n=1 TO rv*50:READ x$:NEXT n
3230 GOTO 3510
3240 REM ---autonummergenerator---
3250 FOR n=1 TO 4:GOSUB 260:l$(n)=CHR$(aa+rv*26):NEXT n
3260 FOR n=5 TO 8
3270 GOSUB 260:sr=INT(rv*10):GOSUB 300:l$(n)=sr$:NEXT n
3280 x$=l$(1)+l$(2)+"-"+l$(5)+l$(6)+"-"+l$(3)+l$(4)
3290 REM 1-4=letters, 5-8=cijfers, hier dus ll-cc-ll
3500 REM --- wis vorige flitsduur ---
3510 cn=1:ho=0:ve=.8:sr$=t$:GOSUB 650:cn=0
3520 REM ---PRINT  de flitsduur---
3530 sr=sd/10:ct=3:cn=1:GOSUB 310
3540 cn=0:sr$=sr$+" sek.":GOSUB 650:t$=sr$
3550 REM ---werk grafiek bij---
3560 cn=0:ho=gr:ve=1-pv:GOSUB 620:ve=1-sd*pv:GOSUB 630
3570 gp=gp+ph:IF gp>=1-ph THEN 950
3580 REM ---doe de flits---
3590 z$="":FOR n=1 TO LEN(x$):q$=MID$(x$,n,1)
3600 IF q$="0" THEN q$="O"
3610 z$=z$+q$:NEXT n:REM ---0 wordt o---
3620 ho=.4:ve=.29:sr$=z$:GOSUB 650
3630 GOSUB 400:REM >> flitsduur <<
3640 cn=1:GOSUB 650:cn=0
3650 ho=.37:ve=.6:sr$="?":GOSUB 650
3660 REM ---input antwoord---
3670 y$="":ly=0:ho=.4:ve=.6
3680 GOSUB 210:IF in=13 THEN 3790
3690 IF in$="!" THEN GOSUB 100:GOTO 1180
3700 IF (in<33) OR (in>127) THEN 3680
3710 IF (in=127) AND (ly=0) THEN 3680
3720 IF in<>127 THEN 3770
3730 cn=1:sr$=y$:GOSUB 650:cn=0
3740 IF LEN(y$)=1 THEN y$="":GOTO 3760
3750 y$=LEFT$(y$,LEN(y$)-1)
3760 ly=ly-1:sr$=y$:GOSUB 650:GOTO 3680
3770 y$=y$+in$:ly=ly+1:sr$=y$:GOSUB 650:GOTO 3680
3780 REM ---beoordeling---
3790 g=0:sd=sd+1:IF x$=y$ THEN g=1:sd=sd-2:IF sd=0 THEN sd=1
3800 IF sd>40 THEN sd=40
3810 ho=.4:ve=.6:sr$=y$:GOSUB 650
3820 IF g=1 THEN ho=.4:ve=.7:sr$="RICHTIG !":GOSUB 650:GOTO 3840
3830 ho=0:ve=.7:sr$="SCHADE, es war "+x$:GOSUB 650
3840 ho=.4:ve=.8:sr$="  << SPACE >> ":GOSUB 650
3850 IF cn=1 THEN 3890
3860 GOSUB 210:IF (in<>32) AND (in$<>"!") THEN 3860
3870 IF in$="!" THEN GOSUB 100:GOTO 1180
3880 cn=1:GOTO 3810
3890 cn=0:ON k1 GOTO 3030,3060,3250
3900 REM
25000 DATA"DAK","HOK","MAN","SAP","VIS"
25010 DATA"LAK","MIS","UIT","BOS","TAK"
25020 DATA"KAT","UIL","BOK","TOR","RAT"
25030 DATA"VAT","VET","PAN","WAS","ZOT"
25040 DATA"TIK","REM","POP","KAR","BUS"
25050 DATA"LEK","HIJ","WIJ","WEI","MEI"
25060 DATA"KOU","KOM","DUS","KAM","LAM"
25070 DATA"DAS","SOK","ELF","ZES","MES"
25080 DATA"KEI","EIK","IEP","GEK","DUN"
25090 DATA"BOL","MUG","SIP","KOE","OEF"
25100 DATA"W4"
25110 DATA"HALT","HOUT","JONG","MUIS","PLOF"
25120 DATA"HOOP","KIST","PLAK","BALK","KURK"
25130 DATA"HARK","BANG","SLAK","GOED","MOOI"
25140 DATA"HOOI","OLIE","OPEN","LAAT","VLUG"
25150 DATA"KOEK","WESP","VIER","VUUR","HAND"
25160 DATA"BOEK","TIJD","ZWAK","MIER","DOEK"
25170 DATA"BIJL","ZEIL","STER","REUS","NEUS"
25180 DATA"KNIE","FLES","TAAL","TAAK","KAAL"
25190 DATA"RAND","WENS","SNEL","TOUW","HIER"
25200 DATA"ZALM","BEER","KRUK","VOUW","DEUR"
25210 DATA"W5"
25220 DATA"NEGEN","LEVER","BEEST","KATER","STANG"
25230 DATA"VLIEG","KEVER","METER","SLANG","GROND"
25240 DATA"BOTER","ANKER","LEPEL","BETER","PLONS"
25250 DATA"RATEL","LATER","ZEKER","KOPER","REGEL"
25260 DATA"MOTOR","KAPOT","STUUR","WAGON","KRUIK"
25270 DATA"KRAAN","WATER","GROEN","VADER","BROER"
25280 DATA"TANTE","GROEP","GELUK","JARIG","TAART"
25290 DATA"LENTE","BLOEM","TAFEL","STRIK","BOTER"
25300 DATA"BOVEN","KRIJT","BLAUW","HOPEN","KAART"
25310 DATA"LUCHT","KRAAI","LOKET","TYPEN","KLAAR"
25320 REM
32000 REM gemaakt op bbc-computer
32010 REM januari 1989
32020 REM ch.w.brederode
32030 REM von bonninghausenlaan 33
32040 REM 2161 es  lisse
32050 REM
32060 REM
32070 REM uebersetzt von
32080 REM ronald knak
32090 REM dr.-h.-wolf-str.1
32100 REM schwerin
32110 REM 2 7 5 8
32120 REM 
32130 REM mit dank an bernd schneider
32140 REM (brand-erbisdorf)

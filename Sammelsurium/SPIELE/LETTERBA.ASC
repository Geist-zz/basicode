1000 A=500:GOTO 20:REM  LETTERBAK
1010 DIM A$(27),B$(27),C(4)
1020 GOSUB 100:PRINT
1030 GOSUB 20000
1040 REM === instructies ===
1050 PRINT"=================================":PRINT
1060 PRINT" HELP!   DEZE COMPUTER SCHUIFT"
1070 PRINT:PRINT" HET HELE ALFABET DOOR ELKAAR!":PRINT
1080 PRINT"=================================":PRINT
1090 PRINT" Maak  jij  het weer in  orde?":PRINT
1100 PRINT" Je hoeft alleen maar de letters":PRINT
1110 PRINT" aan te toetsen die je in de lege":PRINT
1120 PRINT" plek wilt schuiven.":PRINT:PRINT
1130 PRINT"=================================":PRINT
1140 REM === inname aantal ===
1150 PRINT" HOEVEEL KEER MAG DE COMPUTER"
1160 PRINT:PRINT" SCHUIVEN ";:INPUT I
1170 GOSUB 100
1180 REM === letterbak tekenen ===
1190 GOSUB 600:HB=0.14:VB=0.32:CN=0
1200 FOR N=0 TO 3
1210 HO=HB:VE=VB+N*0.08:GOSUB 620
1220 HO=HB+0.72:VE=VB+N*0.08:GOSUB 630
1230 NEXT N:FOR N=0 TO 9
1240 HO=HB+N*0.08:VE=VB:GOSUB 620
1250 HO=HB+N*0.08:VE=VB+0.24:GOSUB 630
1260 NEXT N:HL=HB+0.03:VL=VB+0.03
1270 REM === A$() en B$() vullen ===
1280 FOR N=1 TO 26
1290 A$(N)=CHR$(64+N):B$(N)=A$(N)
1300 NEXT N
1310 A$(27)=" ":B$(27)=" "
1320 REM === letters in bak zetten ==
1330 FOR NR=1 TO 27:GOSUB 3000:NEXT NR
1340 REM === teksten ===
1350 HO=0.28:VE=0.04
1360 SR$="L E T T E R B A K":GOSUB 650
1370 HO=0.45:VE=0.12
1380 CP$="0 x":SR$=CP$:GOSUB 650
1390 HO=0.28:VE=0.2
1400 SR$="G E S C H O V E N":GOSUB 650
1410 HO=0.3:VE=0.64
1420 SR$="J I J   H E B T":GOSUB 650
1430 HO=0.45:VE=0.72
1440 SP$="0 x":SR$=SP$:GOSUB 650
1450 HO=0.28:VE=0.8
1460 SR$="G E S C H O V E N":GOSUB 650
1470 REM === A$() in de war maken ===
1480 D=27:E=0
1490 FOR N=1 TO I
1500 GOSUB 3500:REM zoek buren
1510 GOSUB 260:X=1+INT(RV*(B-1))
1520 IF C(X)=E THEN 1510
1530 CN=1:NR=C(X):GOSUB 3000:REM wissen
1540 A$(D)=A$(C(X)):A$(C(X))=" "
1550 CN=0:NR=D:GOSUB 3000:REM printen
1560 HO=0.45:VE=0.12
1570 SR$=CP$:CN=1:GOSUB 650:REM wissen
1580 E=D:D=C(X)
1590 SR=N:GOSUB 300:SR$=SR$+" x"
1600 CP$=SR$:CN=0:GOSUB 650:REM printen
1610 NEXT N:T=0
2000 REM === DE SPEEL ROUTINE ===
2010 F=0:REM foutvlag als A$()<>B$()
2020 REM === alles al op z'n plaats?==
2030 FOR N=1 TO 27
2040 IF A$(N)<>B$(N) THEN F=1
2050 NEXT N
2060 IF F=0 THEN GOSUB 4000:GOTO 1020
2070 REM === zoek leeg hokje ===
2080 D=1
2090 IF A$(D)<>" " THEN D=D+1:GOTO 2090
2100 REM === wie zijn de buren? ===
2110 GOSUB 3500
2120 REM === letter intoetsen ===
2130 GOSUB 210
2140 REM === is dit 'n buurletter? ===
2150 S=0
2160 IF IN$=A$(BA) THEN S=BA:GOTO 2210
2170 IF IN$=A$(BB) THEN S=BB:GOTO 2210
2180 IF IN$=A$(BC) THEN S=BC:GOTO 2210
2190 IF IN$=A$(BD) THEN S=BD:GOTO 2210
2200 IF S=0 THEN 2290
2210 A$(D)=A$(S)
2220 CN=1:NR=S:GOSUB 3000:REM wissen
2230 CN=0:NR=D:GOSUB 3000:REM printen
2240 A$(S)=" "
2250 T=T+1:HO=0.45:VE=0.72
2260 SR$=SP$:CN=1:GOSUB 650:REM wissen
2270 SR=T:GOSUB 300:SP$=SR$+" x"
2280 SR$=SP$:CN=0:GOSUB 650:REM printen
2290 GOTO 2000
3000 REM === SUBR.A$(NR)PRINTEN ===
3010 PS=NR-1
3020 IF PS>8 THEN PS=PS-9:GOTO 3020
3030 HO=HL+(PS*0.08)
3040 VE=VL+INT((NR-1)/9)*0.08
3050 SR$=A$(NR):GOSUB 650
3060 GOSUB 4530:REM --- vertraging --
3070 RETURN
3500 REM === SUBR.BUURHOKJES ZOEKEN ==
3510 B=1
3520 BA=D+9:IF BA>27 THEN BA=0:GOTO 3540
3530 C(B)=BA:B=B+1
3540 BB=D-9:IF BB<1 THEN BB=0:GOTO 3560
3550 C(B)=BB:B=B+1
3560 BC=D+1:IF (BC=10) OR (BC=19) OR (BC=28) THEN BC=0:GOTO 3580
3570 C(B)=BC:B=B+1
3580 BD=D-1:IF (BD=0) OR (BD=9) OR (BD=18) THEN BD=0:GOTO 3600
3590 C(B)=BD:B=B+1
3600 RETURN
4000 REM === SUBR. 'KLAAR!' ===
4010 CN=1:HO=0.3:VE=0.64
4020 SR$="J I J   H E B T":GOSUB 650
4030 CN=0:HO=0.25
4040 SR$="--> K L A A R ! <--":GOSUB 650
4050 HO=0.36:VE=0.95
4060 SR$="NOG 'N KEER?":GOSUB 650
4070 GOSUB 210
4080 IF IN=78 THEN 950
4090 IF IN<>74 THEN 4070
4100 RETURN
4500 REM === SUBR.VERTRAGINGSLUS ===
4510 REM eventueel aanpassen aan
4520 REM eigen tempo
4530 SD=1:GOSUB 450
4540 RETURN
20000 REM === CURSOR ===
20010 REM hier commando 'cursor off'
20020 RETURN
20080 REM ============================
30000 REM GEMAAKT 8 FEBRUARI 1987
30010 REM OP EEN  BBC B COMPUTER
30020 REM 
32000 REM door Ch.W.Brederode
32010 REM Von Bonninghausenlaan 33
32030 REM 2161 ES  LISSE
32100 REM TROS-RADIO dd. 870325

1000 A=100:GOTO 20:REM sneeuwvlokkromme
1010 DIM SW(500)
1015 HM=HO:VM=VE
1020 GOSUB 1810
1030 PRINT"Wilt u uitleg (J/N) ";
1040 GOSUB 210
1050 PRINT IN$
1060 IF (IN$="J") OR (IN$="j") THEN GOSUB 1570
1070 WS=0
1080 XM=.5:YM=.5:R0=.49
1090 W3=SQR(3)
1100 PRINT"Diepte (0-...) ";:INPUT N
1110 N=1+INT(N)
1120 GOSUB 600
1130 CN=0
1140 FOR NI=1 TO N
1150 R=R0*(NI/N)^2
1160 XA=XM-.375*W3*R:YA=YM-.5*R
1170 XB=XM:YB=YM+R
1180 XC=XM+.375*W3*R:YC=YM-.5*R
1190 P1=XA:P2=YA:Q1=XB:Q2=YB:P5=NI-1
1200 GOSUB 1350
1210 P1=XB:P2=YB:Q1=XC:Q2=YC:P5=NI-1
1220 GOSUB 1350
1230 P1=XC:P2=YC:Q1=XA:Q2=YA:P5=NI-1
1240 GOSUB 1350
1250 NEXT NI
1255 HO=.937:VE=.956:SR$="=>":GOSUB 650
1260 GOSUB 210
1270 GOSUB 100
1280 PRINT"Nog een (J/N) ";
1290 GOSUB 210
1300 PRINT IN$
1310 IF (IN$<>"N") AND (IN$<>"n") THEN 1070
1320 GOTO 950
1340 REM routine teken recursief driehoek
1350 IF P5>0 THEN 1370
1360 HO=P1:VE=P2:GOSUB 620:HO=Q1:VE=Q2:GOSUB 630:RETURN
1370 P5=P5-1
1380 L1=(P1+P1+Q1)/3:L2=(P2+P2+Q2)/3
1390 N1=(3*(P1+Q1)-.75*W3*(Q2-P2))/6
1400 N2=(3*(P2+Q2)+4/3*W3*(Q1-P1))/6
1410 M1=(P1+Q1+Q1)/3:M2=(P2+Q2+Q2)/3
1420 SW(WS)=M1:SW(WS+1)=M2:SW(WS+2)=Q1:SW(WS+3)=Q2
1430 SW(WS+4)=P5:WS=WS+5
1440 SW(WS)=N1:SW(WS+1)=N2:SW(WS+2)=M1:SW(WS+3)=M2
1450 SW(WS+4)=P5:WS=WS+5
1460 SW(WS)=L1:SW(WS+1)=L2:SW(WS+2)=N1:SW(WS+3)=N2
1470 SW(WS+4)=P5:WS=WS+5
1480 Q1=L1:Q2=L2
1490 GOSUB 1350
1500 P1=SW(WS-5):P2=SW(WS-4):Q1=SW(WS-3):Q2=SW(WS-2)
1510 P5=SW(WS-1):WS=WS-5:GOSUB 1350
1520 P1=SW(WS-5):P2=SW(WS-4):Q1=SW(WS-3):Q2=SW(WS-2)
1530 P5=SW(WS-1):WS=WS-5:GOSUB 1350
1540 P1=SW(WS-5):P2=SW(WS-4):Q1=SW(WS-3):Q2=SW(WS-2)
1550 P5=SW(WS-1):WS=WS-5:GOSUB 1350
1560 RETURN
1570 REM uitlegroutine
1590 GOSUB 1810
1600 PRINT"Dit programma ontwikkelt een figuur"
1610 PRINT"die aan een sneeuwvlok doet denken."
1620 PRINT:PRINT"Er wordt op niveau 0 alleen een"
1630 PRINT"driehoek getekend."
1640 PRINT:PRINT"Kiest u niveau 1 dan wordt dezelfde"
1650 PRINT"driehoek getekend, maar elke zijde"
1660 PRINT"krijgt in het midden een driehoekige"
1670 PRINT"uitstulping, zodat het geheel een"
1680 PRINT"zespuntige ster wordt."
1690 GOSUB 1780
1700 PRINT"Voor elk volgend niveau wordt op"
1710 PRINT"vergelijkbare manier elk lijnstuk"
1720 PRINT"opnieuw van een driehoekige uitstulping"
1730 PRINT"voorzien."
1740 PRINT:PRINT"Dit gaat net zo ver door als de"
1750 PRINT"resolutie van uw beeld toelaat."
1760 GOSUB 1780
1770 RETURN
1780 HO=0:VE=VM:GOSUB 110
1790 PRINT"Druk op de spatie voor vervolg";
1800 GOSUB 210:IF IN$<>" " THEN 1800
1810 GOSUB 100
1820 HO=(HM-16)/2:VE=0:GOSUB 110
1830 SR$="Sneeuwvlok":GOSUB 150
1840 PRINT:PRINT:RETURN
30000 REM De sneeuwvlokkromme is
30010 REM in 1904 bedacht door de
30020 REM Duitse wiskundige
30030 REM Helge von Koch
32000 REM ******************************
32010 REM *                            *
32020 REM * Een grafische demonstratie *
32030 REM *             in             *
32040 REM *     B A S I C O D E - 3    *
32050 REM *                            *
32060 REM *    Copyright (c) 1986      *
32070 REM *    Stichting BASICODE      *
32080 REM *                            *
32090 REM ******************************
32100 REM TROS RADIO dd 860809

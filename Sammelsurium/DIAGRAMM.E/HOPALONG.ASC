1000 A=100:GOTO 20:REM HOPALONG
1010 PRINT "Kies het onderdeel"
1020 PRINT :PRINT 
1030 PRINT "1  Hopalong met sinusfunctie"
1040 PRINT "2  Hopalong met wortelfunctie"
1050 PRINT "3  Hopalong met LOG-functie"
1060 PRINT "4  UITLEG"
1070 PRINT "0  Stoppen"
1080 PRINT :PRINT "U kiest ";
1090 GOSUB 210
1100 IF IN=48 THEN 950
1110 IF (IN<49) AND (IN>53) THEN GOSUB 250:GOTO 1090
1120 KZ=IN-48:REM keuze
2000 ON KZ GOTO 2010,2110,2300,5010
2010 GOSUB 100
2020 PRINT "HOPALONG met SINUSFUNCTIES":PRINT 
2030 PRINT "Tik een getal in dat de grafiek"
2040 PRINT "moet beinvloeden.":PRINT 
2050 PRINT "Als u een getal geeft tussen"
2060 PRINT "3.09 en  3.18 dan onstaan de"
2070 PRINT "de aardigste effecten.":PRINT 
2080 PRINT "Uw getal : ";
2090 INPUT A
2100 SX=50:SY=38:GOTO 3010
2110 GOSUB 100:PRINT "HOPALONG met WORTELFUNCTIE"
2120 PRINT :PRINT "Geef drie getallen op."
2130 PRINT "Enkele suggesties:"
2140 PRINT "  2,  -4  en 6"
2150 PRINT "  2,  -4  en 7"
2160 PRINT "  2,  -4  en 8"
2170 PRINT "  1,  -2  en 50"
2180 PRINT "  1,  -2  en 100"
2190 PRINT " 0.4,  1  en 9"
2200 PRINT "-0.4,  1  en 9"
2210 PRINT "-1.1,-50  en 7"
2220 PRINT " -2,   5  en 40"
2230 PRINT " -5,   1  en -100"
2240 SX=200:SY=150
2250 HO=0:VE=19:GOSUB 110
2260 PRINT "Als eerste getal wilt u : ";:INPUT A
2270 PRINT "Als tweede getal        : ";:INPUT B
2280 PRINT "en als derde getal      : ";:INPUT C
2290 GOTO 3010
2300 GOSUB 100:PRINT "HOPALONG met LOG-FUNCTIE"
2310 PRINT :PRINT "Geef drie getallen op."
2320 PRINT "Enkele suggesties:"
2330 PRINT "  1,  -2  en 50"
2340 PRINT "  1, -10  en 50"
2350 PRINT " -5,  50  en -1"
2360 SX=80:SY=60:GOTO 2250
3000 REM start de tekencyclus
3010 X=0:Y=0:CN=0:GOSUB 600
3020 REM herhalingslus
3030 HO=.5+(X+Y)/SX:VE=.5+(X-Y)/SY
3040 IF (HO<0) OR (VE<0) THEN 3070
3050 IF (HO>=1) OR (VE>=1) THEN 3070
3060 GOSUB 620
3070 ON KZ GOTO 3080,3090,3100
3080 XX=Y-SIN(X):GOTO 3110
3090 XX=Y-SGN(X)*SQR(ABS(B*X-C)):GOTO 3110
3100 XX=Y-SGN(X)*LOG(ABS(B*X-C)+.0001):GOTO 3110
3110 Y=A-X
3120 X=XX
3130 GOSUB 200:IF IN=0 THEN 3030
3140 IF IN=83 THEN GOSUB 20000:REM screendump
3150 GOTO 1000:REM start opnieuw
5000 REM uitleg
5010 GOSUB 100:PRINT "HOPALONG-UITLEG"
5020 PRINT :PRINT "Dit programma tekent de vreemdste"
5030 PRINT "tekeningen op het scherm. Een tekening"
5040 PRINT "wordt uit punten opgebouwd en die"
5050 PRINT "punten worden in een erg vreemde volg-"
5060 PRINT "orde getekend. In het begin lijkt het"
5070 PRINT "chaos, maar er komt steeds meer teke-"
5080 PRINT "ning in."
5090 PRINT "Het kan nuttig zijn, vanaf regel 20000"
5100 PRINT "een screendump in te bouwen."
5110 PRINT "Het kan nodig zijn, de delingen in"
5120 PRINT "regel 2310 wat te veranderen omdat soms"
5130 PRINT "de tekening te veel buiten het scherm"
5140 PRINT "terecht komt. VEEL BASICODE-PRET!"
5150 PRINT 
5160 PRINT "---spatie---":GOSUB 210:GOSUB 100:GOTO 1010
20000 Q=0
20010 REM plaats hier een eigen screendump
20020 RETURN
30000 REM Voor de fijnproevers:
30010 REM 
30020 REM In feite tekent dit programma een
30030 REM fractal. De getekende fractal is
30040 REM door zijn transcendente karakter
30050 REM nogal onvoorspelbaar.
30060 REM Een ander effect, vooral merkbaar
30070 REM bij de SINUS-versie met voor A een
30080 REM waarde nabij PI, is cijferverlies
30090 REM bij het uitvoeren van de berekening,
30100 REM waardoor de getekende punten min
30110 REM of meer RANDOM verspringen van de
30120 REM ene naar de andere sinuskromme.
30130 REM 
30140 REM Toch veel plezier !
32000 REM HOPALONG is geschreven in mei 1988 door
32010 REM 
32020 REM  S.G. van Dijk
32030 REM  Amsterdam
32040 REM 
32050 REM TROS-RADIO dd 880831

1000 a=100:GOTO 20:REM  boom van pythagoras
1010 DIM x(15),y(15),h(15),v(15),n(15)
1020 s=0:ht=(ho-22)/2:vt=ve/2
1030 GOSUB 2010
1040 GOSUB 100:ho=ht-3:ve=vt:GOSUB 110
1050 sr$=b$:GOSUB 150:PRINT :PRINT 
1060 PRINT "Welche Tiefe waehlen Sie? (0=Ende) ";
1070 INPUT sr:IF sr=0 THEN 950
1080 GOSUB 600:cn=0:ho=0:ve=0
1085 GOSUB 300:sr$=b$+", Tiefe "+sr$:GOSUB 650
1090 x=.5:hb=.0828125:y=.73-hb:f=4/3:h=-hb:v=hb
1100 GOSUB 1170
1110 REM  screendump
1120 ho=1-3/32:ve=1-1/23:sr$="=>":GOSUB 650
1130 GOSUB 210
1140 GOTO 1040
1150 REM  teken vierkanten
1170 ho=x+h:ve=f*(y+v):GOSUB 620
1180 ho=x-v:ve=f*(y+h):GOSUB 630
1190 ho=x-h:ve=f*(y-v):GOSUB 630
1200 ho=x+v:ve=f*(y-h):GOSUB 630
1210 ho=x+h:ve=f*(y+v):GOSUB 630
1220 p=(h+v)/2:q=(v-h)/2:IF sr<2 THEN return
1230 x(s)=x+v+p:y(s)=y+h-q:h(s)=p:v(s)=q:n(s)=sr-1
1240 s=s+1:x=x+h+p:y=y-v-q:h=-q:v=p:sr=sr-1
1250 GOSUB 1170
1260 s=s-1:x=x(s):y=y(s):h=h(s):v=v(s):sr=n(s)
1270 GOTO 1170
2000 REM  uitleg
2010 GOSUB 4000
2020 PRINT "Der  Satz  von  PYTHAGORAS   ist"
2030 PRINT "bereits aus der Schule bekannt ."
2035 PRINT 
2040 PRINT "Er  beschreibt  das  Verhaeltnis"
2050 PRINT "der Seitenlaengen in einem recht-"
2060 PRINT "winkligen Dreieck:"
2070 PRINT 
2080 PRINT "      2       2         2"
2090 PRINT "     A   +   B    =    C"
2100 PRINT 
2110 PRINT "Das Programm zeichnet eine Baum-"
2120 PRINT "struktur, die  auf  diesen  Satz"
2125 PRINT "beruht."
2130 PRINT 
2140 PRINT "Die Tiefe der Veraestelung koen-"
2150 PRINT "nen Sie selbst festlegen."
2160 PRINT "(Sinnvollerweise in den Grenzen"
2165 PRINT " von   3 ... 10 )"
2200 GOSUB 3000
2210 PRINT "Bei einer Tiefe von  8 und mehr"
2220 PRINT "werden die letzten  Quadrate im"
2230 PRINT "Baum auf dem Bildschirm nur noch"
2235 PRINT "mit geringer  Aufloesung  darge-"
2240 PRINT "stellt."
2245 PRINT 
2250 PRINT "Ihre Geduld  bei  grosser  Tiefe"
2260 PRINT "wird belohnt."
2270 PRINT 
2280 PRINT :PRINT "Viel Vergnuegen  !"
3000 ho=0:ve=vt*2:GOSUB 110
3010 sr$="<<return>>":GOSUB 150
3020 GOSUB 210:IF in<>13 THEN 3020
4000 GOSUB 100
4010 b$=" Der BAUM von PYTHAGORAS "
4020 sr$=b$:GOSUB 150:PRINT :PRINT 
4030 return
32000 REM ******************************
32010 REM *                            *
32020 REM * een grafische demonstratie *
32030 REM *             in             *
32040 REM *     b a s i c o d e - 3    *
32050 REM *                            *
32060 REM *    copyright (c) 1986      *
32070 REM *    stichting basicode      *
32080 REM *                            *
32090 REM ******************************
32100 REM  tros dd 860726
32110 REM -computernacht 15/16.9.89
32120 REM    acorn-master

1000 a=100:GOTO 20:REM perspectief
1010 GOSUB 100:REM inleiding
1020 PRINT"**********************"
1030 PRINT"*                    *"
1040 PRINT"* PERSPECTIEF HULPJE *"
1050 PRINT"*                    *"
1060 PRINT"**********************"
1070 PRINT :PRINT"MET BEHULP VAN DIT PROGRAMMA KAN MEN"
1080 PRINT"INDIEN COORDINATEN VAN BEPAALDE PUNTEN"
1090 PRINT"VAN EEN VOORWERP IN DE DRIE DIMENSIES"
1100 PRINT"GEKEND ZIJN EN MEN TEVENS DE PARAMETERS"
1110 PRINT"KENT VAN HET PUNT VANWAAR MEN KIJKT,"
1120 PRINT"DE OVEREENKOMENDE X,Z PUNTEN BEREKENEN"
1130 PRINT"OM TEKENINGEN TE MAKEN IN PERSPECTIEF."
1140 PRINT :PRINT"DRUK EEN TOETS IN VOOR VERVOLG...";
1150 GOSUB 210:GOSUB 100
1160 PRINT"TEVENS IS DE MOGELIJKHEID VOORZIEN OM 2"
1170 PRINT"HORIZONTALE COORDINATEN TE BEREKENEN NL"
1180 PRINT"LINKS EN RECHTS, BEIDE MET VERTIKALE"
1185 PRINT"COORDINAAT Z. ZO VERKRIJGT MEN 3 DIMEN-"
1190 PRINT"SIONALE EFFEKTEN. OP EEN BEPAALD MOMENT"
1200 PRINT"ZAL HET PROGRAMMA VRAGEN OM"
1210 PRINT"'DIEPTE VAN ZICHT (3=3DIM,0=2DIM)'."
1220 PRINT"INGAVE VAN TUSSENLIGGENDE WAARDEN GEEFT"
1230 PRINT"MINDER DIMENSIES (GEEN 2 DIM!)."
1240 PRINT :PRINT"IN HET PROGRAMMA IS RUIMTE VOORZIEN"
1250 PRINT"OM MAX 20 PUNTEN IN TE GEVEN, WENST MEN"
1260 PRINT"DIT AANTAL TE VERGROTEN, WIJZIG DAN DE"
1265 PRINT"2 WAARDES IN HET DIM STATEMENT OP REGEL"
1270 PRINT"1510."
1280 PRINT :PRINT"DRUK EEN TOETS IN VOOR VERVOLG...";
1290 GOSUB 210:GOSUB 100
1300 PRINT"BETEKENIS DER PARAMETERS :"
1310 PRINT 
1320 PRINT"         Z      D=AFSTAND T.O.V. O"
1330 PRINT"         Û   Y  E=HOOGTE T.O.V. Y-AS"
1340 PRINT"    .    I  /"
1350 PRINT"(X,Y,Z)  I /"
1360 PRINT"         I/"
1370 PRINT"---------O---------> X"
1380 PRINT"        /I"
1390 PRINT"       / I  A=HOEK IN XY-VLAK IN TEGEN-"
1400 PRINT"      /  I    WIJZERZIN ROND DE Z-AS"
1410 PRINT"     /   I    Y-AS=REFERENTIE AS."
1420 PRINT"            B=HOEK IN YZ-VLAK IN TEGEN-"
1430 PRINT"              WIJZERZIN ROND DE X-AS"
1440 PRINT"              Y-AS=REFERENTIE AS."
1450 PRINT"DRUK EEN TOETS IN VOOR VERVOLG...";
1460 GOSUB 210
1500 REM initialisatie
1510 DIM mi(20,3),mo(20,3)
1520 p1=3.141592654:f=0
1600 REM invoer v.d. parameters ta,a,b,d,e
1610 GOSUB 100:PRINT"PERSPECTIEF"
1620 PRINT"-----------"
1630 ve=3:ho=0:GOSUB 110
1640 PRINT"DIEPT VAN ZICHT (3=3DIM,0=2DIM) ";
1650 INPUT ta
1670 IF (ta<0) OR (ta>3) THEN 1630
1680 ve=4
1690 GOSUB 110
1700 PRINT"HOEK A ";
1710 INPUT a
1720 IF (a<-360) OR (a>360) THEN 1680
1730 ve=5
1740 GOSUB 110
1750 PRINT"HOEK B ";
1760 INPUT b
1770 IF (b<-360) OR (b>360) THEN 1730
1780 ve=6
1790 GOSUB 110
1800 PRINT"AFSTAND D ";
1810 INPUT d
1820 ve=7
1830 GOSUB 110
1840 PRINT"HOOGTE  E ";
1850 INPUT e
1860 ta=TAN(ta*2*p1/360)
1870 a=a*2*p1/360:b=b*2*p1/360
1880 IF f=1 THEN 2010
1890 ve=9:ho=0:GOSUB 110
1900 PRINT"INGAVE COORD. (999,999,999=END)."
1910 i=1
1920 ve=11:ho=1:GOSUB 110
1925 PRINT"PUNT";i
1930 PRINT" X ";:INPUT x
1940 PRINT" Y ";:INPUT y
1950 PRINT" Z ";:INPUT z
1960 IF (x=999) AND (y=999) AND (z=999) THEN 1990
1970 mi(i,1)=x:mi(i,2)=y:mi(i,3)=z
1980 i=i+1:GOTO 1920
1990 n=i-1
2000 REM start v.d. berekening
2010 GOSUB 100
2020 PRINT"DE BEREKENING IS GESTART !"
2030 FOR i=1 TO n
2040 x1=0:y1=0:z1=0:h=0
2050 z=mi(i,3)-e
2060 x=mi(i,1):y=mi(i,2)
2070 GOSUB 4010
2080 h=h-a
2090 GOSUB 5010
2100 x1=x:y1=y
2110 IF b=0 THEN 2170
2120 x=z
2130 GOSUB 4010
2140 h=h-b
2150 GOSUB 5010
2160 z=x:y1=y
2170 k=y1/d+1
2180 IF ta=0 THEN 2230
2190 c=ta*y1/k
2200 mo(i,1)=(x1-c)/k
2210 mo(i,2)=(x1+c)/k
2220 GOTO 2240
2230 mo(i,1)=x1/k
2240 mo(i,3)=z/k
2250 NEXT i
3000 REM afdruk v.d. resultaten
3010 ve=2:ho=0:GOSUB 110
3020 PRINT"GEGEVENS NAAR PRINTER OF VDU <P/V> ";
3030 GOSUB 210
3040 IF in$="V" THEN 3110
3050 IF in$="P" THEN 3400
3060 GOTO 3010
3100 REM afdruk op vdu v.d. resultaten
3110 i=1:f=1
3120 j=1
3130 GOSUB 100
3140 PRINT"RESULTATEN :":PRINT 
3150 ct=3:cn=0:sr=i
3160 GOSUB 310:i$=sr$
3170 IF ta=0 THEN 3215
3180 ct=7:cn=2
3190 sr=mo(i,1):GOSUB 310:m1$=sr$
3200 sr=mo(i,2):GOSUB 310:m2$=sr$
3210 GOTO 3230
3215 ct=7:cn=2
3220 sr=mo(i,1):GOSUB 310:m1$=sr$
3230 sr=mo(i,3):GOSUB 310:m3$=sr$
3240 IF ta=0 THEN 3270
3250 PRINT"P. ";i$;" LX ";m1$;" RX ";m2$;" Z ";m3$
3260 GOTO 3280
3270 PRINT"P. ";i$;" X ";m1$;" Z ";m3$
3280 i=i+1:j=j+1
3290 IF i>n THEN 3340
3300 IF j>10 THEN 3320
3310 GOTO 3150
3320 PRINT :PRINT"DRUK EEN TOETS IN VOOR VOLGENDE GEG. ..";
3330 GOSUB 210:GOTO 3120
3340 ve=14:ho=0:GOSUB 110
3350 PRINT :PRINT"EINDE OF ANDERE PARAMETERS <E/P> ";
3360 GOSUB 210
3370 IF in$="E" THEN GOTO 950
3380 IF in$="P" THEN f=1:GOTO 1610
3400 REM afdruk v.d. resultaten op PRINT er
3410 FOR i=1 TO n
3420 ct=3:cn=0:sr=i:GOSUB 360
3430 GOSUB 310:sr$="PUNT "+sr$:GOSUB 350
3440 IF ta=0 THEN 3485
3450 ct=7:cn=2:sr=mo(i,1)
3460 GOSUB 310:sr$=" LINKER X "+sr$:GOSUB 350
3470 sr=mo(i,2):GOSUB 310:sr$=" RECHTER X "+sr$
3480 GOSUB 350:GOTO 3510
3485 ct=7:cn=2
3490 sr=mo(i,1):GOSUB 310:sr$=" X "+sr$
3500 GOSUB 350
3510 sr=mo(i,3):GOSUB 310:sr$=" Z "+sr$
3520 GOSUB 350
3530 NEXT i
3540 GOTO 3340
4000 REM subroutine rechthoekig naar pool coord.
4010 r=SQR(x*x+y*y)
4020 IF (x=0) AND (y>0) THEN h=p1/2:GOTO 4080
4030 IF (x=0) AND (y<0) THEN h=3*p1/2:GOTO 4080
4040 IF (x=0) AND (y=0) THEN h=0:GOTO 4080
4050 IF (x>0) AND (y>=0) THEN h=atn(y/x):GOTO 4080
4060 IF (x>0) AND (y<0) THEN h=atn(y/x)+2*p1:GOTO 4080
4070 h=atn(y/x)+p1
4080 RETURN
5000 REM subroutine pool naar rechthoekige coord.
5010 x=ABS(r*COS(h)):y=ABS(r*SIN(h))
5020 IF (h>=0) AND (h<p1/2) THEN 5060
5030 IF (h>=p1/2) AND (h<=p1) THEN x=-x:GOTO 5060
5040 IF (h>p1) AND (h<=3*p1/2) THEN x=-x:y=-y:GOTO 5060
5050 y=-y
5060 RETURN
30000 REM beschikt men over een grafisch display
30010 REM of een plotter, dan kan men het progr.
30020 REM uitbreiden met de nodige routines,
30030 REM zodat het mogelijk wordt een afbeelding
30040 REM van het voorwerp te verkrijgen.
30050 REM volgende uitbreidingen zijn dan nodig :
30060 REM 1. in het input gedeelte dient men een
30070 REM    routine bij te voegen welke bij in-
30080 REM    gave v.e. punt vraagt met welke punten
30090 REM    dit verbonden is, waardes moeten groter
30100 REM    zijn dan de waarde v.h. punt zelf.
30110 REM    b.v. punt 1 is met punt 5 verbonden,
30120 REM    maar punt 5 ook met punt 1 !
30130 REM 2. in het output gedeelte de nodige
30140 REM    grafische routine welke de pen (spot)
30150 REM    verplaatst naar het betreffende punt
30160 REM    en van daaruit de lijnen trekt naar
30170 REM    de verschillende connectiepunten,
30180 REM    verplaatsen naar volgende punt enz...
30190 REM 
30200 REM gebruikt men dit programma voor de eerste
30210 REM maal, voer dan de coord. in van een een-
30220 REM voudig voorwerp b.v. kubus om alzo de in-
30230 REM vloed van de verschillende parameters na
30240 REM te gaan.
30250 REM teken eventueel telkens het voorwerp uit
30260 REM op een blad papier...
30270 REM 
30280 REM ****  v e e l  s u c s e s ****
30290 REM
30300 REM ****************************************
30310 REM * perspectief, op een junior pc met    *
30320 REM * 32k ram, 8k sym basic, 8k rae en     *
30330 REM * een cgp-115 (plotter van tandy)      *
30340 REM * door :                               *
30350 REM * ing. s.onderbeke                     *
30360 REM * brielstraat 2                        *
30370 REM * 9840 landegem      tel.:091/71.60.17 *
30380 REM * belgie             november 1983     *
30390 REM ****************************************
30400 REM
30410 REM copyright  nos hilversum

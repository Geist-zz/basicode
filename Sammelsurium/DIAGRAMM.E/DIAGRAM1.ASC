1000 A=500:GOTO 20:REM DIAGRAMMEN
1010 HB=HO+1:VH=VE-4:BR=INT((HB-8)/3):S$="           "
1020 SP$=S$+" ":CN=0:P1=3.14159:UW=1-1/HG
1030 TG=.82:REM ***TG=.895-3/AANTAL KAR. OP HI-RES***
1040 DIM BR(3),HO(3),IN$(3,VH+1),KE$(20),PR(VH)
1050 HO(0)=0:HO(1)=7:FOR I=2 TO 3:HO(I)=(I-1)*BR+HO(1):NEXT I
1060 BR(0)=3:FOR I=1 TO 3:BR(I)=BR-4:NEXT I
1070 FOR I=0 TO 20:READ KE$(I):NEXT I
1080 READ TE$:READ A$:TE$=TE$+A$
1090 FOR Y=0 TO 3:FOR X=0 TO 3:READ IN$(X,Y):NEXT X:NEXT Y
1100 FL=0:SR$="<<<DIAGRAMMEN>>>":GOSUB 3220
1110 YB=0:YE=5:GOSUB 2950
1120 ON VE-5 GOSUB 1260,1140,2720,2790,2860,950
1130 GOTO 1100
1140 SR$="SOORT DIAGRAM":GOSUB 3220:YB=6:YE=8:GOSUB 2950
1150 IF VE=YE-YB+7 THEN RETURN
1160 ON VE-5 GOTO 1170,1200,2520
1170 SR$="SOORT STAAFDIAGRAM":GOSUB 3220:YB=9:YE=11
1180 GOSUB 2950:IF VE=YE-YB+7 THEN RETURN
1190 ON VE-5 GOTO 1830,1930,2060
1200 SR$="SOORT LIJNDIAGRAM":GOSUB 3220:YB=12:YE=14
1210 GOSUB 2950:IF VE=YE-YB+7 THEN RETURN
1220 ON VE-5 GOTO 2190,2300,2420
1230 SR$="ABSOLUUT OF PERCENTUEEL":GOSUB 3220:YB=15:YE=16
1240 GOSUB 2950:AP=1:IF VE=6 THEN AP=0
1250 RETURN
1260 GOSUB 100:PRINT "    X  ";:H=(BR(1)+4)/2:FOR I=1 TO 3
1270 HO=HO(I)+H:VE=0:GOSUB 110:PRINT "Y";I;:NEXT I
1280 PRINT :GOSUB 3310
1290 FOR Y=0 TO VH:FOR X=0 TO 3
1300 VE=Y+2:H$=IN$(X,Y):IF LEN(H$)<=BR(X) THEN 1320
1310 H$=LEFT$(H$,BR(X))
1320 HO=HO(X)+3+BR(X)/2-LEN(H$)/2:GOSUB 110:PRINT H$
1330 NEXT X:NEXT Y:VE=2:HO=0:X=0:Y=0:XX=0:YY=0
1340 T=HO(X)+3:TT=0:TB=1:GOTO 1620
1350 GOSUB 200:IF IN=0 THEN 1350
1360 H$=IN$(X,Y):IF IN=36 THEN 1700
1370 IF (IN>27) AND (IN<32) THEN 1480
1380 IF IN=35 THEN IN$(X,Y)="":T=HO(X)+3:TT=0:TB=1:GOTO 1620
1390 IF IN=94 THEN RETURN
1400 IF (IN=60) AND (TT>0) THEN T=T-1:TT=TT-1:GOTO 1620
1410 IF (IN=62) AND (TT<LEN(H$)) THEN T=T+1:TT=TT+1:GOTO 1620
1420 IF IN=127 THEN 1770
1430 IF IN=37 THEN 1820
1440 IF (IN<32) OR (IN>127) OR (IN=60) OR (IN=62) THEN 1350
1450 T=T+1:TT=TT+1:H=LEN(H$)
1460 IF TT=LEN(H$)+1 THEN IN$(X,Y)=H$+IN$:GOTO 1620
1470 IN$(X,Y)=LEFT$(H$,TT-1)+IN$+RIGHT$(H$,H-TT):GOTO 1620
1480 IF IN=28 THEN X=X-1
1490 IF IN=29 THEN X=X+1
1500 IF IN=30 THEN Y=Y+1
1510 IF IN=31 THEN Y=Y-1
1520 IF X=-1 THEN X=3
1530 IF X=4 THEN X=0
1540 IF Y=-1 THEN Y=VH
1550 IF Y=VH+1 THEN Y=0
1560 HX=X:HY=Y:X=XX:Y=YY:SR$=IN$(X,Y)
1570 IF LEN(SR$)>=BR(X) THEN H$="":GOTO 1590
1580 H$=LEFT$(SP$,BR(X)/2-LEN(SR$)/2):GOSUB 1660
1590 SR$="   "+H$+LEFT$(SR$,BR(X)-LEN(H$))+"   "
1600 HO=HO(X):VE=Y+2:GOSUB 110:X=HX:Y=HY
1610 PRINT SR$;:XX=X:YY=Y:T=HO(X)+3:TB=1:TT=0
1620 GOSUB 1640:HO=HO(X):VE=Y+2:GOSUB 110
1630 GOSUB 150:HO=T:GOSUB 110:GOTO 1350
1640 SR$=IN$(X,Y):IF LEN(SR$)=BR(X) THEN RETURN
1650 IF LEN(SR$)>BR(X) THEN 1670
1660 FOR I=LEN(SR$)+1 TO BR(X):SR$=SR$+" ":NEXT I:RETURN
1670 IF T<HO(X)+3 THEN TB=TB-1:T=HO(X)+3
1680 IF T>HO(X)+BR(X)+3 THEN TB=TB+1:T=HO(X)+BR(X)+3
1690 SR$=MID$(IN$(X,Y),TB,BR(X)):RETURN
1700 GOSUB 1760:PRINT "Begin: ";:INPUT H$:H=VAL(H$)
1710 GOSUB 1760:PRINT "Eind:  ";:INPUT HH$:HH=VAL(HH$)
1720 GOSUB 1760:PRINT "Stap:  ";:INPUT J$:J=VAL(J$)
1730 Y=0:FOR I=H TO HH STEP J:SR=I:GOSUB 300:IN$(X,Y)=SR$
1740 Y=Y+1:IF Y>VH THEN I=HH+1
1750 NEXT I:GOTO 1260
1760 HO=0:VE=0:GOSUB 110:PRINT SP$:GOSUB 110:RETURN
1770 IF TT=0 THEN 1350
1780 H=LEN(H$):IF H=1 THEN H1$=" ":GOTO 1810
1790 IF RIGHT$(H$,1)=" " THEN 1350
1800 H1$=LEFT$(H$,TT-1)+" "
1810 IN$(X,Y)=H1$:T=T-1:TT=TT-1:GOTO 1620
1820 FOR Y=0 TO VH:FOR X=0 TO 3:IN$(X,Y)=""
1825 NEXT X:NEXT Y:GOTO 1260
1830 FL=2:GOSUB 1930:IF (VE=YE-YB+7) OR (G=0) THEN RETURN
1840 HO=.125:VE=.92:GOSUB 620
1850 FOR I=0 TO Y:HO=.15+(I/(Y+1)*.84)+.5*BR
1860 IF AP=0 THEN VE=.92-VAL(IN$(1,I))/H*.92:GOTO 1880
1870 VE=.92-VAL(IN$(1,I))/G*.92
1880 GOSUB 630:NEXT I:HO=.99:VE=.92:GOSUB 630
1890 GOSUB 200:IF IN=0 THEN 1890
1900 IF IN=83 THEN GOSUB 20050:GOTO 1890
1910 IF IN=84 THEN GOSUB 3320:GOTO 1890
1920 RETURN
1930 GOSUB 1230:IF VE=YE-YB+7 THEN RETURN
1940 GOSUB 3240:IF G=0 THEN RETURN
1950 GOSUB 3560:BR=(1/(Y+1)*.84)-.02
1960 IF FL=2 THEN BR=(1/(Y+1)*.84)
1970 FOR I=0 TO Y:HO=.15+(I/(Y+1)*.84):VE=.92:V=VE:GOSUB 620
1980 IF AP=0 THEN VE=.92-VAL(IN$(1,I))/H*.92:GOTO 2000
1990 VE=.92-VAL(IN$(1,I))/G*.92
2000 GOSUB 630:HO=HO+BR:GOSUB 630:VE=.92:GOSUB 630
2010 NEXT I:IF FL=2 THEN RETURN
2020 GOSUB 200:IF IN=0 THEN 2020
2030 IF IN=83 THEN GOSUB 20050:GOTO 2020
2040 IF IN=84 THEN GOSUB 3320:GOTO 2020
2050 RETURN
2060 GOSUB 1230:IF VE=YE-YB+7 THEN RETURN
2070 GOSUB 3640:IF G=0 THEN RETURN
2080 GOSUB 3560:BR=.13+(1/(Y+1)*.84)-.15
2090 FOR I=0 TO Y:HH=.15+(I/(Y+1)*.84):VE=.92:V=VE
2100 FOR X=1 TO 3:HO=HH:VE=V:GOSUB 620
2110 IF AP=0 THEN VE=ABS(V-VAL(IN$(X,I))/PR(I)*.92):GOTO 2130
2120 VE=ABS(V-VAL(IN$(X,I))/G*.92)
2130 GOSUB 630:HO=HO+BR:GOSUB 630:J=VE:VE=V
2140 GOSUB 630:V=J:NEXT X:NEXT I
2150 GOSUB 200:IF IN=0 THEN 2150
2160 IF IN=83 THEN GOSUB 20050:GOTO 2150
2170 IF IN=84 THEN GOSUB 3320:GOTO 2150
2180 RETURN
2190 GOSUB 1230:IF VE=YE-YB+7 THEN RETURN
2200 GOSUB 3240:IF G=0 THEN RETURN
2210 FL=1:GOSUB 3560
2220 HO=.1:VE=.92:GOSUB 620:FOR I=0 TO Y:HO=I/Y*.89+.1
2230 IF AP=0 THEN VE=.92-VAL(IN$(1,I))/H*.92:GOTO 2250
2240 VE=.92-VAL(IN$(1,I))/G*.92
2250 GOSUB 630:NEXT I
2260 GOSUB 200:IF IN=0 THEN 2260
2270 IF IN=83 THEN GOSUB 20050:GOTO 2260
2280 IF IN=84 THEN GOSUB 3320:GOTO 2260
2290 RETURN
2300 GOSUB 1230:IF VE=YE-YB+7 THEN RETURN
2310 GOSUB 3640:IF G=0 THEN RETURN
2320 FL=1:GOSUB 3560:FOR X=1 TO 3:HO=.1:VE=.92:GOSUB 620
2330 FOR I=0 TO Y:HO=I/Y*.89+.1:HH=0:FOR J=1 TO X
2340 HH=HH+VAL(IN$(J,I)):NEXT J
2350 IF AP=0 THEN VE=ABS(.92-HH/PR(I)*.92):GOTO 2370
2360 VE=ABS(.92-HH/G*.92)
2370 GOSUB 630:NEXT I:NEXT X
2380 GOSUB 200:IF IN=0 THEN 2380
2390 IF IN=83 THEN GOSUB 20050:GOTO 2380
2400 IF IN=84 THEN GOSUB 3320:GOTO 2380
2410 RETURN
2420 GOSUB 1230:IF VE=YE-YB+7 THEN RETURN
2430 GOSUB 3240:IF G=0 THEN RETURN
2440 FL=1:GOSUB 3560:HO=.1:VE=.92:GOSUB 620
2450 VE=.92:FOR I=0 TO Y:HO=I/Y*.89+.1
2460 VE=ABS(VE-VAL(IN$(1,I))/H*.92)
2470 GOSUB 630:NEXT I
2480 GOSUB 200:IF IN=0 THEN 2480
2490 IF IN=83 THEN GOSUB 20050:GOTO 2480
2500 IF IN=84 THEN GOSUB 3320:GOTO 2480
2510 RETURN
2520 GOSUB 1230:IF VE=YE-YB+7 THEN RETURN
2530 GOSUB 3240:IF G=0 THEN RETURN
2540 GOSUB 600:HO=.875:VE=.5:GOSUB 620
2560 FOR I=0 TO 6.31 STEP .1
2570 HO=.5+.375*COS(I):VE=.5+.5*SIN(I):GOSUB 630
2580 NEXT I:HO=.5:VE=.5:GOSUB 620:HO=.875:GOSUB 630
2590 T=0:FOR I=0 TO Y:HK=VAL(IN$(1,I))/H*2*P1:T=T+HK
2600 HO=.5+.375*COS(T):VE=.5+.5*SIN(T):GOSUB 620
2610 HO=.5:VE=.5:GOSUB 630:HO=.5+.25*COS(T-.5*HK)
2620 VE=.5+.3125*SIN(T-.5*HK):SR$=IN$(0,I):GOSUB 650
2630 IF AP=1 THEN 2670
2640 SR=VAL(IN$(1,I))/H*100:CT=2:GOSUB 310
2650 HO=.5+.15*COS(T-.5*HK):VE=.5+.1875*SIN(T-.5*HK)
2660 SR$=SR$+"%":GOSUB 650
2670 NEXT I
2680 GOSUB 200:IF IN=0 THEN 2680
2690 IF IN=83 THEN GOSUB 20050:GOTO 2680
2700 IF IN=84 THEN GOSUB 3320:GOTO 2680
2710 RETURN
2720 SR$="   X   "+S$+"Y1"+S$+S$+"Y2"+S$+S$+"Y3"
2730 GOSUB 350:GOSUB 360:GOSUB 360
2740 FOR Y=0 TO 2:FOR X=0 TO 3:H=24:IF X=0 THEN H=7
2750 H$=IN$(X,Y):IF LEN(H$)>H-2 THEN H$=LEFT$(H$,H-2)
2760 SR$=LEFT$(SP$,H/2-LEN(H$)/2)+H$
2770 SR$=SR$+LEFT$(SP$,H-LEN(SR$)):GOSUB 350:NEXT X
2780 GOSUB 360:NEXT Y:GOSUB 360:RETURN
2790 SR$="VAN WELK MEDIUM":GOSUB 3220:YB=18:YE=20
2800 GOSUB 2950:IF VE=YE-YB+7 THEN RETURN
2810 GOSUB 100:PRINT :PRINT "Wat is de naam van het bestand ";
2820 INPUT NF$:NF=(VE-6)*2:GOSUB 500:PRINT :PRINT "Laden...";
2830 FOR Y=0 TO VH:FOR X=0 TO 3:GOSUB 540:IF IN=-1 THEN 2850
2840 IN$(X,Y)=IN$:NEXT X:NEXT Y:GOSUB 580:RETURN
2850 PRINT :PRINT "Leesfout geconstateerd":GOTO 950
2860 SR$="OP WELK MEDIUM":GOSUB 3220:YB=18:YE=20
2870 GOSUB 2950:IF VE=YE-YB+7 THEN RETURN
2880 GOSUB 100:PRINT 
2890 PRINT "Onder welke naam moet ik het saven ";:INPUT NF$
2900 IF LEN(NF$)<8 THEN 2920
2910 NF$=LEFT$(NF$,7)
2920 NF=(VE-6)*2+1:GOSUB 500:PRINT :PRINT "Saven..."
2930 FOR Y=0 TO VH:FOR X=0 TO 3:SR$=IN$(X,Y)
2935 GOSUB 560:NEXT X:NEXT Y
2940 GOSUB 580:RETURN
2950 VE=6:FOR I=YB TO YE:HO=HB/2-LEN(KE$(I))/2
2960 GOSUB 110:VE=VE+1:PRINT KE$(I):NEXT I
2970 IF YB<>0 THEN HO=HB/2-5:GOSUB 110:PRINT KE$(17)
2980 J=3:IF HB>75 THEN J=2
2990 VE=VE+3:HO=0:GOSUB 110:FOR I=0 TO J:GOSUB 3310:NEXT I
3000 IF J=2 THEN SR$=TE$:GOTO 3050
3010 SR$=LEFT$(TE$,HB):I=HB
3020 IF MID$(SR$,I,1)<>" " THEN I=I-1:GOTO 3020
3030 SR$=LEFT$(TE$,I-1):HO=HB/2-LEN(SR$)/2:VE=VE+1:GOSUB 110
3040 PRINT SR$:SR$=RIGHT$(TE$,I-1)
3050 HO=HB/2-LEN(SR$)/2:VE=VE+1:GOSUB 110:PRINT SR$
3060 YT=YB:VE=6:GOSUB 3200
3070 GOSUB 200:IF IN=0 THEN 3070
3080 IF IN=13 THEN RETURN
3090 IF (IN<>30) AND (IN<>31) THEN 3070
3100 IF VE=YE-YB+7 THEN H$=KE$(17):GOTO 3120
3110 H$=KE$(YT)
3120 GOSUB 110:PRINT "   ";H$;"   ";
3130 IF IN=30 THEN YT=YT+1:VE=VE+1
3140 IF IN=31 THEN YT=YT-1:VE=VE-1
3150 IF (YT>YE) AND (YB=0) THEN YT=YB:VE=6:GOTO 3180
3160 IF VE>YE-YB+7 THEN YT=YB:VE=6
3170 IF YT<YB THEN YT=YE+SGN(YB):VE=6+SGN(YB)+YE-YB
3180 H=YT:IF VE=YE-YB+7 THEN YT=17
3190 GOSUB 3200:YT=H:GOTO 3070
3200 SR$=KE$(YT):HO=HB/2-LEN(SR$)/2-3:GOSUB 110:GOSUB 150
3210 RETURN
3220 GOSUB 100:FOR I=0 TO 2:GOSUB 3310
3225 NEXT I:HO=HB/2-LEN(SR$)/2
3230 VE=1:GOSUB 110:PRINT SR$:RETURN
3240 H=0:Y=0:G=0
3250 HH=VAL(IN$(1,Y)):IF IN$(0,Y)="" THEN 3280
3260 H=H+HH:Y=Y+1:IF HH>G THEN G=HH
3270 GOTO 3250
3280 Y=Y-1:IF G<>0 THEN RETURN
3290 GOSUB 100:PRINT "U heeft geen gegevens ingevoerd!!!"
3300 PRINT :PRINT "<<<Druk een toets>>>";:GOSUB 210:RETURN
3310 FOR X=1 TO HB:PRINT "-";:NEXT X:RETURN
3320 HO=0:VE=1/22:GOSUB 620:SR$="":FL=0
3330 SD=1:GOSUB 450:IF IN<>0 THEN 3360
3340 CN=1-CN:IF FL=0 THEN GOSUB 620
3350 GOTO 3330
3360 IF FL=0 THEN CN=1:GOSUB 620
3370 CN=0:IF IN=13 THEN RETURN
3380 IF IN=35 THEN FL=1-FL:GOTO 3330
3390 IF (IN<28) OR (IN>31) THEN 3500
3400 SR$="":IF IN=28 THEN HO=HO-2/HG
3410 IF IN=29 THEN HO=HO+2/HG
3420 IF IN=30 THEN VE=VE+2/VG
3430 IF IN=31 THEN VE=VE-2/VG
3440 IF HO>=1 THEN HO=0
3450 IF HO<0 THEN HO=1-2/HG
3460 IF VE>=1 THEN VE=0
3470 IF VE<0 THEN VE=1-2/VG
3480 IF FL=0 THEN GOSUB 620
3490 GOTO 3330
3500 IF (IN=127) AND (LEN(SR$)=0) THEN 3330
3510 IF IN=127 THEN CN=1:GOTO 3530
3520 SR$=SR$+IN$
3530 VE=VE-1/24:GOSUB 650:VE=VE+1/24:IF IN<>127 THEN 3330
3540 SR$=LEFT$(SR$,LEN(SR$)-1):VE=VE-1/24:CN=0:GOSUB 650
3550 VE=VE+1/24:GOTO 3330
3560 GOSUB 600:HO=.1:VE=.92:GOSUB 620:HO=UW:GOSUB 630
3570 VE=.92:HO=.1:GOSUB 620:VE=0:GOSUB 630
3580 IF FL=1 THEN GOSUB 3720:GOTO 3610
3590 FOR I=0 TO Y:HO=.17+(I/(Y+1)*.84):VE=.93
3600 SR$=IN$(0,I):GOSUB 650:NEXT I:SR=G:CT=3:GOSUB 310
3610 SR=G:CT=3:GOSUB 310:IF (SR$="***") AND (AP=1) THEN RETURN
3620 FOR I=4 TO 0 STEP -1:SR=G*I/4:IF AP=0 THEN SR=100-(4-I)*25
3630 GOSUB 310:HO=0:VE=(4-I)/4.5:GOSUB 650:NEXT I:RETURN
3640 FOR I=0 TO VH:PR(I)=0:NEXT I
3650 Y=0:G=0
3660 IF IN$(0,Y)="" THEN 3700
3670 FOR X=1 TO 3:PR(Y)=PR(Y)+VAL(IN$(X,Y)):NEXT X
3680 IF PR(Y)>G THEN G=PR(Y)
3690 Y=Y+1:GOTO 3660
3700 Y=Y-1:IF G<>0 THEN RETURN
3710 GOTO 3290
3720 FOR I=0 TO Y:HO=I/Y*TG+.1:SR$=IN$(0,I)
3730 VE=.93:GOSUB 650:NEXT I:RETURN
20000 REM VANAF REGEL 20050 KUNT U EEN
20010 REM SCREENDUMPPROGRAMMA PLAATSEN.
20020 REM HET PROGRAMMA SPRINGT NAAR DEZE
20030 REM REGEL, NADAT U 'S' HEBT INGEDRUKT
20040 REM ALS EEN GRAFIEK IS GETEKEND.
20050 HO=HO
20060 REM 
20070 RETURN
25000 DATA "Invoer","Uitvoer","Printen","Laden","Saven"
25010 DATA "Stoppen","Staafdiagram","Lijndiagram"
25020 DATA "Cirkeldiagram","Polygoon","Enkel staafdiagram"
25030 DATA "Samengesteld staafdiagram","Enkel lijndiagram"
25040 DATA "Samengesteld lijndiagram"
25050 DATA "Cumulatieve frequentiepolygoon","Percentueel"
25060 DATA "Absoluut","Hoofdmenu","Basicode","Cassette"
25070 DATA "Disk-drive"
25080 DATA "Maak uw keuze met de cursortoetsen en bevestig"
25090 DATA " uw keuze met de invoertoets."
25100 DATA "'60","453","342","143","'65","503","369","203"
25110 DATA "'70","564","403","254","'75","603","436","251"
32000 REM -----------------------------
32010 REM DOOR RONALD VAN VUGT, PA3EAH
32020 REM 
32030 REM POSTBUS 160, 5580 AD, WAALRE
32040 REM 
32050 REM      SEPTEMBER 1987 V1.3
32060 REM -----------------------------
32070 REM TROS-RADIO dd 870930

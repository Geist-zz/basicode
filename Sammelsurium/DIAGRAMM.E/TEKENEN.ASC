1000 A=2000:GOTO 20:REM TEKENEN
1010 MA=300:DIM M$(MA)
1020 REM MA = max.aant.tekenopdrachten
1030 G=1:S=1:X=50:Y=50:V=50:W=50
1040 VX=50:VY=50:TX=0:TY=0
1050 MX=50:MY=50:R=0:CN=0:P=0
1060 FOR N=1 TO 37:READ D$:M$(N-1)=D$:NEXT N:P=N-1
1070 GOSUB 600:GOSUB 3510
1080 SD=50:GOSUB 450
1090 GOSUB 5010:PRINT IN$:PRINT
1100 IF (K>3) AND (K<8) THEN PRINT"  momentje";
1110 ON K GOTO 1120,1130,1140,1150,1160,1170,1180,1190,1500
1120 GOSUB 6510:GOTO 1090:REM --uitleg--
1130 GOSUB 6010:GOTO 1090:REM --comm's--
1140 P=0:X=50:Y=50:GOTO 1520:REM --nwe tekening--
1150 NF=0:GOSUB 7510:GOTO 1190:REM --loadcass--
1160 NF=4:GOSUB 7510:GOTO 1190:REM --loaddisk--
1170 NF=1:GOSUB 7010:GOTO 1090:REM --savecass--
1180 NF=5:GOSUB 7010:GOTO 1090:REM --savedisk--
1190 IF (K=4) OR (K=5) THEN K=8:REM --tek uit geh.--
1200 GOTO 1510
1500 GOTO 950:REM --stop--
1510 IF P=0 THEN GOSUB 250:GOTO 1090
1520 GOSUB 600:IF K=8 THEN GOSUB 3510:GOTO 1540
1530 CN=0:HO=X/100:VE=Y/100:GOSUB 620
1540 GOSUB 210:REM --toetsinvoer--
1550 IF IN=71 THEN S=5:GOTO 1540:REM --G--
1560 IF (IN>27) AND (IN<32) THEN GOSUB 2010:GOTO 1540:REM -cursor-
1570 IF (IN=72) AND (P>0) THEN P=P-1:M$(P)="":GOTO 1540:REM --H--
1580 IF IN=75 THEN S=1:GOTO 1540:REM --K--
1590 IF (IN=13) AND (P>0) THEN GOSUB 600:GOSUB 3510:GOTO 1540
1600 IF IN=13 THEN 1540
1610 IF IN$="?" THEN 1090
1620 GOSUB 2510:GOTO 1540
1630 REM --- einde hoofdprogramma ---
2000 REM === CURSOR VERPLAATSEN ===
2010 T=IN-27
2020 ON T GOTO 2030,2050,2070,2090
2030 X=X-S:IF X<0 THEN X=0
2040 GOTO 2100
2050 X=X+S:IF X>99 THEN X=99
2060 GOTO 2100
2070 Y=Y+S:IF Y>99 THEN Y=99
2080 GOTO 2100
2090 Y=Y-S:IF Y<0 THEN Y=0
2100 CN=G:HO=V/100:VE=W/100:GOSUB 620
2110 V=X:W=Y:G=1
2120 CN=0:HO=X/100:VE=Y/100:GOSUB 620
2130 RETURN
2500 REM === TEKENCOMMANDO'S ===
2510 IF IN<>86 THEN 2530:REM -- V --
2520 G=0:VX=X:VY=Y:Z=1:GOSUB 3010:RETURN
2530 IF IN<>79 THEN 2570:REM -- O --
2540 HO=VX/100:VE=VY/100:GOSUB 620
2550 HO=X/100:VE=Y/100:GOSUB 630
2560 VX=X:VY=Y:Z=2:GOSUB 3010:RETURN
2570 IF IN<>84 THEN 2610:REM -- T --
2580 HO=VX/100:VE=VY/100:GOSUB 620
2590 HO=X/100:VE=Y/100:GOSUB 630
2600 Z=3:GOSUB 3010:RETURN
2610 IF IN<>77 THEN 2630:REM -- M --
2620 G=0:MX=X:MY=Y:Z=4:GOSUB 3010:RETURN
2630 IF IN<>67 THEN 2660:REM -- C --
2640 Z=5:GOSUB 3010
2650 GOSUB 4010:RETURN
2660 IF IN<>66 THEN 2690:REM -- B --
2670 Z=6:GOSUB 3010
2680 BX=X:BY=Y:GOSUB 4510
2690 IF IN$<>"$" THEN 2710
2700 Z=7:GOSUB 3010:GOSUB 8010
2710 RETURN
3000 REM === NAAR TEKENGEHEUGEN ===
3010 IF MA=P THEN GOSUB 250:RETURN
3020 SR=INT(X):GOSUB 300:X$=RIGHT$("0"+SR$,2)
3030 SR=INT(Y):GOSUB 300:Y$=RIGHT$("0"+SR$,2)
3040 SR=Z:GOSUB 300
3050 M$(P)=X$+Y$+SR$:P=P+1
3060 RETURN
3500 REM === TEKENEN UIT GEHEUGEN ===
3510 CN=0
3520 FOR N=0 TO P-1
3530 X=VAL(LEFT$(M$(N),2))
3540 Y=VAL(MID$(M$(N),3,2))
3550 Z=VAL(RIGHT$(M$(N),1))
3560 ON Z GOTO 3570,3580,3610,3630,3640,3650,3660
3570 VX=X:VY=Y:GOTO 3670
3580 HO=VX/100:VE=VY/100:GOSUB 620
3590 HO=X/100:VE=Y/100:GOSUB 630
3600 VX=X:VY=Y:GOTO 3670
3610 HO=VX/100:VE=VY/100:GOSUB 620
3620 HO=X/100:VE=Y/100:GOSUB 630:GOTO 3670
3630 MX=X:MY=Y:GOTO 3670
3640 GOSUB 4010:GOTO 3670
3650 BX=X:BY=Y:GOSUB 4510:GOTO 3670
3660 GOSUB 8070:GOTO 3670
3670 NEXT N
3680 X=100*HO:Y=100*VE
3690 RETURN
4000 REM === TEKEN EEN CIRKEL ===
4010 J=X-MX:K=(Y-MY)*.75
4020 R=.01*SQR(J*J+K*K)
4030 CX=MX/100:CY=MY/100
4040 HO=CX+R:VE=CY:GOSUB 620
4050 FOR L=0 TO 6.31 STEP .1
4060 HO=CX+R*COS(L)
4070 VE=CY+1.33*R*SIN(L):GOSUB 630
4080 NEXT L:RETURN
4500 REM === TEKEN EEN BLOK ===
4510 HO=BX/100:VE=BY/100:GOSUB 620
4520 HO=VX/100:GOSUB 630
4530 VE=VY/100:GOSUB 630
4540 HO=BX/100:GOSUB 630
4550 VE=BY/100:GOSUB 630
4560 RETURN
5000 REM === MENU ===
5010 GOSUB 100
5020 PRINT"     ";:SR$="TEKENEN IN BASICODE-3":GOSUB 150
5030 PRINT:PRINT:PRINT"  MENU":PRINT
5040 PRINT"  1 = UITLEG":PRINT
5050 PRINT"  2 = DE COMMANDO'S ZIEN":PRINT
5060 PRINT"  3 = EEN NIEUWE TEKENING MAKEN":PRINT
5070 PRINT"  4 = EEN TEKENING VAN CASSETTE LADEN":PRINT
5080 PRINT"  5 = EEN TEKENING VAN DISKETTE LADEN":PRINT
5090 PRINT"  6 = EEN TEKENING NAAR CASSETTE":PRINT
5100 PRINT"  7 = EEN TEKENING NAAR DISKETTE":PRINT
5110 PRINT"  8 = UIT HET GEHEUGEN TEKENEN":PRINT
5120 PRINT"  9 = STOPPEN":PRINT
5130 PRINT"  MAAK JE KEUZE (1..9) ";
5140 GOSUB 210:IF (IN<49) OR (IN>57) THEN 5140
5150 K=IN-48:RETURN
6000 REM === COMMANDO'S ZIEN ===
6010 GOSUB 100
6020 PRINT" TEKENEN IN BASICODE-3"
6030 PRINT" =====================":PRINT
6040 PRINT" CURSOR bewegen met de pijltoetsen":PRINT
6050 PRINT" (stapgrootte G(root) of K(lein) )":PRINT
6060 PRINT" intussen  TEKENEN  met de letters":PRINT
6070 PRINT" V en T (van...tot), of V O O..O T":PRINT
6080 PRINT" (van..over..tot) in elke richting.":PRINT
6090 PRINT" TEKST na $, afsluiten met RETURN.":PRINT
6100 PRINT" BLOKKEN met V en B (van....blok),":PRINT
6110 PRINT" CIRKELS met M en C (middelpunt en":PRINT
6120 PRINT" de cirkelomtrek).":PRINT
6130 PRINT" FOUT HERSTELLEN MET H en RETURN.":PRINT
6140 PRINT" ? = MENU";
6150 PRINT"  ---> TIK OP EEN TOETS";:GOSUB 210:RETURN
6500 REM === UITLEG ===
6510 GOSUB 100
6520 PRINT" Op het tekenscherm staat een stip":PRINT
6530 PRINT" die je met de pijltoetsen in alle":PRINT
6540 PRINT" richtingen kunt verplaatsen.  Als":PRINT
6550 PRINT" je op 'G' tikt gaat dat met grote":PRINT
6560 PRINT" stappen, na'K' met kleine stappen":PRINT
6570 PRINT" Om lijnen te tekenen zet je eerst":PRINT
6580 PRINT" met 'V' een stip op het scherm en":PRINT
6590 PRINT" met de pijltoetsen ga je naar een":PRINT
6600 PRINT" andere plaats. Als je op 'T' tikt":PRINT
6610 PRINT" teken je een lijn VAN  het eerste":PRINT
6620 PRINT" punt TOT het tweede punt.":PRINT
6630 GOSUB 6150:GOSUB 100
6640 PRINT" Als je alleen  van richting wilt":PRINT
6650 PRINT" veranderen tik je op 'O' en gaat":PRINT
6660 PRINT" verder in de nieuwe richting. Op":PRINT
6670 PRINT" het eind van de lijn komt de 'T'":PRINT
6680 PRINT" Met de 'V' en de 'B' maak je een":PRINT
6690 PRINT" BLOK en met 'M' en 'C' teken  je":PRINT
6700 PRINT" een CIRKEL.":PRINT
6710 PRINT" Je kunt ook tekst in de tekening":PRINT
6720 PRINT" zetten. Je tikt dan eerst op '$'":PRINT
6730 PRINT" en daarna tik je de tekst met op":PRINT
6740 PRINT" het eind <RETURN> of <ENTER>.":PRINT
6750 GOSUB 6150:GOSUB 100
6760 PRINT" Fouten kun je altijd herstellen!":PRINT
6770 PRINT" Als je een fout hebt gemaakt tik":PRINT
6780 PRINT" je op 'H' en op <RETURN>. Dan is":PRINT
6790 PRINT" de laatste teken-opdracht gewist":PRINT
6800 PRINT" en de tekening opnieuw getekend.":PRINT
6810 PRINT" Je kunt een tekening op cassette":PRINT
6820 PRINT" bewaren. Als je op '?' tikt, dan":PRINT
6830 PRINT" komt het MENU waarin je dat kunt":PRINT
6840 PRINT" doen. Je kunt je tekening  later":PRINT
6850 PRINT" weer laden, en  door de computer":PRINT
6860 PRINT" laten tekenen.":PRINT
6870 GOSUB 6150:GOSUB 100
6880 PRINT" Puntjes, en andere  schoonheids-":PRINT
6890 PRINT" foutjes, die tijdens het tekenen":PRINT
6900 PRINT" ontstaan,  verdwijnen  als je de":PRINT
6910 PRINT" tekening  met  <RETURN>  opnieuw":PRINT
6920 PRINT" laat tekenen. Ze vormen dus geen":PRINT
6930 PRINT" enkel probleem!":PRINT
6940 GOSUB 6150:RETURN
7000 REM === TEKENING SAVEN ===
7010 IF P=0 THEN GOSUB 250:RETURN
7020 NF$="TEKDATA":GOSUB 500
7030 FOR N=0 TO P-1:SR$=M$(N):GOSUB 560
7040 NEXT N:GOSUB 580:RETURN
7500 REM === TEKENING LADEN ===
7510 NF$="TEKDATA":GOSUB 500:P=0
7520 GOSUB 540:M$(P)=IN$:P=P+1
7530 IF IN=0 THEN 7520
7540 GOSUB 580
7550 RETURN
8000 REM === TEKST PLAATSEN ===
8010 HO=X/100:VE=Y/100:CN=0:SR$=""
8020 GOSUB 210:IF IN=13 THEN 8050
8030 IF IN=127 THEN SR$=LEFT$(SR$,LEN(SR$)-1):GOTO 8020
8040 SR$=SR$+IN$:GOSUB 650:GOTO 8020
8050 M$(P)=SR$:P=P+1:RETURN
8060 REM --UIT GEHEUGEN--
8070 N=N+1:SR$=M$(N):HO=X/100:VE=Y/100:GOSUB 650:RETURN
25000 DATA "05051","65052","05652","05053","15151","40152"
25010 DATA "15402","15153","02101","77102","02552","02103"
25020 DATA "12201","47202","12392","12203","76911","81816"
25030 DATA "81891","89846","76811","28846","76911","25886"
25040 DATA "28831","19832","28813","23871","26926","23901"
25050 DATA "19896","72394","88395","24567","T E K E N E N"
25060 DATA "12667","I N   B A S I C O D E - 3"
25070 REM ==============================
32000 REM GEMAAKT OP EEN BBC-COMPUTER
32010 REM 30 MAART 1987 DOOR
32020 REM Ch.W.Brederode
32030 REM Von Bonninghausenlaan 33
32040 REM 2161 ES  LISSE
32100 REM TROS-RADIO dd 870624

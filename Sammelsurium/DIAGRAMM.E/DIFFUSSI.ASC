1000 A=400:GOTO 20:REM  DIFFUSIE
1010 GOSUB 100:REM  SCHERM SCHOON
1020 PRINT "Dit programma laat zien hoe"
1030 PRINT "luchtverontreiniging zich"
1040 PRINT "verspreidt over grote afstand."
1050 PRINT 
1060 PRINT "In de linkeronderhoek van het"
1070 PRINT "scherm is een ongeluk gebeurd"
1080 PRINT "met een kerncentrale. De wind"
1090 PRINT "komt van links."
1100 PRINT 
1110 PRINT "Ieder stipje kan een plaats op"
1120 PRINT "de kaart zijn, waar een"
1130 PRINT "radio-actieve regenbui neerkomt."
1140 PRINT 
1150 PRINT "toets voor ramp>";
1160 GOSUB 210:REM  WACHT OP TOETS
1170 REM 
1180 REM  AANTAL STEEKPROEFDEELTJES
1190 REM  (AANTAL BUIEN)
1200 REM  AFHANKELIJK VAN
1210 REM  SCHERMHOOGTE
1220 REM 
1230 NY=VG/20
1240 REM 
1250 REM  POSITIES DEELTJES
1260 REM  BEGIN OP 0
1270 REM 
1280 DIM X(NY)
1290 FOR I=1 TO NY
1300 X(I)=0
1310 NEXT I
1320 REM 
1330 REM  VERWACHTE GROOTSTE
1340 REM  ZIJWAARTSE AFWIJKING
1350 REM 
1360 F1=2
1370 MX=F1*SQR(1/3)*SQR(HG)
1380 REM 
1390 REM  GRAFISCH SCHERM
1400 REM  VOORGRONDKLEUR
1410 REM 
1420 GOSUB 600:CN=0
1430 REM 
1440 REM  WOLK VERSCHUIFT VAN
1450 REM  LINKS NAAR RECHTS
1460 REM 
1470 FOR HO=0 TO 1-1/HG STEP 1/HG
1480 REM 
1490 REM  LAAT IEDER DEELTJE
1500 REM  DEELNEMEN AAN EEN
1510 REM  WERVEL
1520 REM 
1530 FOR I=1 TO NY
1540 REM 
1550 REM  HAAL EEN TOEVALSGETAL
1560 REM  IN RV (0-1)
1570 REM 
1580 GOSUB 260
1590 REM 
1600 REM  NIEUWE POSITIE VAN
1610 REM  DEELTJE
1620 REM 
1630 X(I)=X(I)+(RV-.5)*2
1640 REM 
1650 REM  PLOTPOSITIE
1660 REM 
1670 VE=1-1/VG-ABS(X(I)/MX)
1680 REM 
1690 REM  BUITEN SCHERM?
1700 REM 
1710 IF VE<0 THEN 1790
1720 REM 
1730 REM  TEKEN PUNT
1740 REM 
1750 GOSUB 620
1760 REM 
1770 REM  VOLGENDE DEELTJE
1780 REM 
1790 NEXT I
1800 REM 
1810 REM  MET DE WIND MEE
1820 REM 
1830 NEXT HO
1840 REM 
1850 REM  TEKEN THEORETISCHE KROMME
1860 REM  VAN GEMIDDELDE VERBREDING
1870 REM 
1880 FOR HO=0 TO 1-1/HG STEP 1/HG
1890 VE=1-(1/VG)-(1/F1)*SQR(HO+1/HG)
1900 IF HO=0 THEN GOSUB 620
1910 IF HO<>0 THEN GOSUB 630
1920 NEXT HO
1930 REM 
1940 REM  WACHT OP TOETS
1950 REM 
1960 GOSUB 210
1970 REM 
1980 REM  STOP
1990 REM 
2000 GOTO 950
32000 REM 
32010 REM Dit is een
32020 REM BASICODE-3 programma
32030 REM van
32040 REM 
32050 REM Pim van Tend
32060 REM Veldheimweg 8
32070 REM 6871 CD  RENKUM
32080 REM 
32090 REM copyright (c) 1986
32100 REM 
32110 REM Nadere uitleg vindt u in:
32120 REM 
32130 REM AARDE & KOSMOS Informatica
32140 REM 1987-2
32150 REM 
32160 REM uitg. MENS & WETENSCHAP
32170 REM Postbus 108
32180 REM 1270 AC Huizen
32190 REM 
32200 REM TROS-RADIO dd 10-12-1986

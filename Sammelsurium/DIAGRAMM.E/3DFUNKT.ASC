1000 A=256:GOTO 20:REM 3D-FUNKTION
1010 GOSUB 100:SA=HO:ZA=VE:GOSUB 600:ZG=VG-1:SG=HG-1:NW=0
1012 GOSUB 100:GOTO 4000
1015 REM ====FUNKTIONSBERECHNUNG====
1020 HO=HO/SG:VE=VE/ZG:GOTO 620
1030 HO=HO/SG:VE=VE/ZG:GOTO 630
1100 R=SQR(X*X+Y*Y):Z=SIN(R)/R:RETURN
1200 R=X*X+Y*Y:RS=SQR(R):RS=SIN(RS)
1210 Z=RS*RS/R:RETURN
1300 Z=SIN(X)*SIN(Y):RETURN
1400 Z=SIN(X)+SIN(Y):RETURN
1500 Z=0:R=X*X+Y*Y:IF R<1 THEN Z=SQR(1-R)
1510 RETURN
1600 Z=0:R=X*X+Y*Y:IF R<1 THEN Z=-SQR(1-R)
1610 RETURN
1700 IF I=0 THEN ZX=0:ZV=1
1710 GOSUB 260:Z=RV:IF ZX>5 THEN ZV=-1
1720 IF ZX<-5 THEN ZV=1
1730 ZX=ZX+ZV*Z:Z=ZX:RETURN
1800 GOSUB 260:Z=RV:ZX=(ZX+Z)/2:Z=ZX:RETURN
2000 Z=0:REM ANWENDERFUNKTION
2010 RETURN
2990 REM ==HILFSROUTINEN F. HIDDEN LINES
3000 S2=0:IF S0>0 THEN S2=1
3010 M0=M(IG,S2):M1=M(IG-1,S2)
3020 HO=XB+((YB-M1)*(XG-XB)/(M0-M1-YG+YB))
3030 VE=YB+(YG-YB)*(HO-XB)/(XG-XB)
3040 GOSUB 1020:HO=XB:VE=YB:GOSUB 1030:RETURN
3100 S2=0:IF S1>0 THEN S2=1
3110 M0=M(IG,S2):M1=M(IG-1,S2)
3120 HO=XB+((YB-M1)*(XG-XB)/(M0-M1-YG+YB))
3130 VE=YB+(YG-YB)*(HO-XB)/(XG-XB)
3140 GOSUB 1020:HO=XG:VE=YG:GOSUB 1030:RETURN
3300 IF YG>M(IG,1) THEN M(IG,1)=YG
3310 IF YG<M(IG,0) THEN M(IG,0)=YG
3320 IF S3*S1>0 THEN HO=X3:VE=Y3:GOSUB 1030
3330 X3=XG:Y3=YG:S3=S1
3340 RETURN
3990 REM ==HAUPTPROGRAMM================
4000 PRINT:RESTORE 25000:READ A$:PRINT A$
4010 FOR I=1 TO 12:READ B$:PRINT B$:NEXT I
4020 PRINT A$:PRINT:SR$="(C) BIDAT'89":GOSUB 150
4030 PRINT:PRINT:FOR I=1 TO 5:READ A$:PRINT A$:NEXT I:GOSUB 210
4040 IF NW>0 THEN 4130
4050 IF (SG>=80) AND (ZG>=80) THEN 4120
4060 GOSUB 100:PRINT"UNERWARTETES BILDSCHIRMFORMAT:"
4070 PRINT ZG+1;" ZEILEN UND";SG+1;" SPALTEN"
4080 INPUT"IST DAS KORREKT (J/N) ?";SR$
4090 GOSUB 330:IF SR$<>"N" THEN 4120
4100 INPUT"ZEILENANZAHL :";ZG
4110 INPUT"SPALTENANZAHL:";SG:SG=SG-1:ZG=ZG-1
4120 DIM M(SG+1,1):NW=1
4130 GOSUB 100:SR$="3D-FUNKTIONSDARSTELLUNG":GOSUB 150
4140 PRINT:PRINT:SR$="AUSWAHL DER FUNKTION":GOSUB 150
4150 PRINT:PRINT:PRINT" <1> Z=SIN(R)/R"
4160 PRINT" <2> Z=(SIN(R)/R)^2"
4170 PRINT" <3> Z=SIN(X)*SIN(Y)"
4180 PRINT" <4> Z=SIN(X)+SIN(Y)"
4190 PRINT" <5> OBERE HALBKUGEL"
4200 PRINT" <6> UNTERE HALBKUGEL"
4210 PRINT" <7> ZUFALLSFUNKTION 1"
4220 PRINT" <8> ZUFALLSFUNKTION 2"
4230 PRINT" <9> ANWENDERFUNKTION"
4240 PRINT" <0> ENDE"
4250 PRINT:SR$="IHRE AUSWAHL":GOSUB 150:INPUT"";MO
4260 IF MO=0 THEN 950
4270 IF (MO<1) OR (MO>9) THEN 4250
4280 IF MO<9 THEN 4340
4290 PRINT:PRINT"HABEN SIE IHRE ANWENDERFUNKTION"
4300 INPUT"IN ZEILE 2000 DEFINIERT (J/N) ?";A$
4310 IF A$="J" THEN 4340
4320 PRINT"BITTE FORMEL AB ZEILE 2000 EINGEBEN"
4330 PRINT"UND PROGRAMM NEU STARTEN! <ENTER>":GOSUB 210
4333 GOTO 950
4340 GOSUB 100:SR$="3D-FUNKTIONSDARSTELLUNG":GOSUB 150
4350 PRINT:PRINT:SR$="PARAMETEREINGABE":GOSUB 150
4360 PRINT:PRINT:INPUT"UNTERGRENZE X :";XU
4370 INPUT"OBERGRENZE  X :";XO
4380 INPUT"UNTERGRENZE Y :";YU
4390 INPUT"OBERGRENZE  Y :";YO
4400 INPUT"UNTERGRENZE Z :";ZU
4410 INPUT"OBERGRENZE  Z :";ZO
4420 INPUT"LINIENANZAHL  :";L
4430 IF (L<2) OR (L>ZG/2+1) THEN 4420
4440 L=INT((ZG+1)/2/(L-1)):N=INT((ZG+1)/2/L)
4450 PRINT"LINIENANZAHL OPTIMIERT AUF";N
4460 M=INT((SG+1-N*L)/L)
4470 PRINT"BERECHN. PUNKTE PRO LINIE:";M
4480 PRINT:INPUT"IST DAS KORREKT (J/N) ?";SR$
4490 GOSUB 330:IF SR$="N" THEN 4360
4495 REM ===VORBEREITUNGEN,UMRANDUNG===
4500 FOR J=0 TO M+N:M(J,0)=999:M(J,1)=0:NEXT J
4510 QB=L*(N-1):QX=L*(M-1):QY=INT(.4*ZG):ZV=1:ZX=0
4520 GOSUB 600:HO=0:VE=0:GOSUB 1020:HO=0:VE=QY:GOSUB 1030
4530 HO=QB:VE=QY+QB:GOSUB 1030:HO=QB+QX:VE=QB+QY:GOSUB 1030
4540 HO=QB+QX:VE=QB:GOSUB 1030:HO=QX:VE=0:GOSUB 1030
4550 HO=0:VE=0:GOSUB 1030:DX=(XO-XU)/(M-1)
4560 DY=(YO-YU)/(N-1):IF UZ*QZ>0 THEN 4600
4570 NL=ZO/(ZO-ZU)*QY:FOR I=0 TO QB STEP 2
4580 HO=I:VE=NL+I:GOSUB 1020:NEXT I
4590 FOR I=QB TO QB+QX STEP 3:HO=I:VE=NL+QB:GOSUB 1020:NEXT I
4595 REM ===FUNKTIONSBERECHNUNG+GRAFIK===
4600 S3=0:Y=YU-DY:FOR J=0 TO N-1:Y=Y+DY:X=XU-DX
4610 FOR I=0 TO M-1:X=X+DX
4620 ON MO GOSUB 1100,1200,1300,1400,1500,1600,1700,1800,2000
4630 IF Z<ZU THEN Z=ZU
4632 IF Z>ZO THEN Z=ZO
4638 Z=(Z-ZU)/(ZO-ZU)*QY:IG=I+N-J
4640 XG=QB+L*(I-J):YG=QB+QY-L*J-Z:S1=0
4650 IF I>0 THEN 4680
4660 IF J>0 THEN HO=XA:VE=YA:GOSUB 1020:HO=XG:VE=YG:GOSUB 1030
4665 IF J=0 THEN HO=XG:VE=YG:GOSUB 1020
4670 S0=0:XA=XG:YA=YG
4680 IF YG>=M(IG,1) THEN S1=1
4690 IF YG<=M(IG,0) THEN S1=-1
4695 IF I=0 THEN 4758
4700 IF (S0>0) AND (S1<=0) THEN GOSUB 3000
4710 IF (S0<0) AND (S1>=0) THEN GOSUB 3000
4720 IF S0*S1>0 THEN HO=XG:VE=YG:GOSUB 1030:GOTO 4750
4730 IF (S1>0) AND (S0<=0) THEN GOSUB 3100
4740 IF (S1<0) AND (S0>=0) THEN GOSUB 3100
4750 IF YB>M(IG-1,1) THEN M(IG-1,1)=YB
4752 IF YB<M(IG-1,0) THEN M(IG-1,0)=YB
4754 IF I=M-1 THEN GOSUB 3300
4758 XB=XG:YB=YG:S0=S1:NEXT I:NEXT J
4760 SR=XU:GOSUB 300:X$="X:"+SR$
4770 SR=XO:GOSUB 300:X$=X$+"..."+SR$
4780 SR=YU:GOSUB 300:X$=X$+" Y:"+SR$
4790 SR=YO:GOSUB 300:X$=X$+"..."+SR$
4800 SR$=X$:HO=0:VE=.95:GOSUB 650:GOSUB 210
4810 GOTO 4130
25000 DATA "======================================="
25010 DATA "        ****     *****"
25020 DATA "       *    *     *   *"
25030 DATA "            *     *    *"
25040 DATA "         ***      *    *"
25050 DATA "            *     *    *"
25060 DATA "       *    *     *   *"
25070 DATA "        ****     *****"
25080 DATA ""
25090 DATA "  **** *  * *  * *  * *** *  **  *  *"
25100 DATA "  *    *  * ** * * *   *  * *  * ** *"
25110 DATA "  ***  *  * * ** **    *  * *  * * **"
25120 DATA "  *     **  *  * * *   *  *  **  *  *"
25130 DATA "DAS PROGRAMM STELLT FUNKTIONEN Z=F(X,Y)"
25140 DATA "GRAFISCH  DAR. NEBEN  DEN  VORGEGEBENEN"
25150 DATA "FUNKTIONEN KOENNEN SIE  VOR  DEM  START"
25160 DATA "AUCH EINE EIGENE FUNKTION IN ZEILE 2000"
25170 DATA "EINGEBEN UND DARSTELLEN LASSEN. <ENTER>"
32000 REM        (C) BIDAT 29.6.89
32010 REM AUTOR: BERTHOLD BIENER
32020 REM        R.-EYERMANN-RING 4/0106
32030 REM        5063 ERFURT
32040 REM ENTWICKELT AUF COMMODORE+4
32050 REM FUER RADIO DDR COMPUTERMAGAZIN

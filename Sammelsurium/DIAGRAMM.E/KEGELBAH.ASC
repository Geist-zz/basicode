1000 A=500:GOTO 20:REM kogelbaan
1010 GOSUB 100:REM scherm schoon
1020 PRINT"Dit programma tekent kogelbanen."
1030 PRINT
1040 G=10:REM zwaartekrachtsversnelling
1050 M=1:REM de kogelmassa
1060 PRINT"beginsnelheid kogel"
1070 PRINT"       (0=stoppen) ";
1080 INPUT V
1090 IF V=0 THEN 950
1100 PRINT"elevatie (0-90)";
1110 INPUT EL
1120 EL=EL*3.14159/180:REM graden naar radialen
1130 VH=V*COS(EL):REM beginsnelheid horizontaal
1140 VV=V*SIN(EL):REM beginsnelheid verticaal
1150 PRINT"windsnelheid";
1160 INPUT VW
1170 PRINT"wrijvingsconstante (0 of 1)";
1180 INPUT CW:REM wrijvingsconstante
1190 CW=CW*1E-04:REM schaling
1300 DT=.05:REM tijdstap
1310 TT=15:REM totale duur
1320 S1=0:REM verticale beginpositie
1330 S2=0:REM horizontale beginpositie
1380 VM=100:REM hoogte top scherm
1390 BO=.85:REM tekencoordinaat bodem
1500 GOSUB 600:REM grafisch scherm
1510 CN=0:REM voorgrondkleur
1520 NG=1:REM geen vorig punt
2010 FOR T=0 TO TT STEP DT
2020 L2=VH-VW:REM tov wind, horizontaal
2030 VL=SQR(L2*L2+VV*VV):REM tov lucht, totaal
2040 FH=-CW*L2*VL*VL:REM horizontale wrijving
2050 V2=DT*FH/M:REM horizontale snelheidsverandering
2060 VH=VH+V2:REM nieuwe snelheid horizontaal
2070 S2=S2+DT*(VH-V2/2):REM horizontale positie
2100 FA=-M*G:REM kracht is gewicht
2110 FW=-CW*VV*VL*VL:REM verticale wrijving
2120 FV=FW+FA:REM totale verticale kracht
2130 V1=DT*FV/M:REM verticale snelheidsverandering
2140 VV=VV+V1:REM nieuwe snelheid verticaal
2150 S1=S1+DT*(VV-V1/2):REM verticale positie
2400 GOSUB 200:REM kijk of toets
2410 IF IN$<>"" THEN T=TT+1:REM grens over
2510 VE=BO*(1-S1/VM):REM tekenwaarde verticaal
2520 HO=(1-BO)+.75*S2/VM:REM tekenwaarde horizontaal
2530 NT=0:REM wel tekenen
2540 IF VE<0 THEN NT=1:REM te hoog
2550 IF HO<0 THEN NT=1:REM te links
2560 IF VE>1-1/VG THEN NT=1:REM te laag
2570 IF HO>1-1/HG THEN NT=1:REM te rechts
2580 IF NT=1 THEN NG=1:GOTO 3000:REM dit keer niets
2600 IF NG=1 THEN GOSUB 620:REM punt
2610 IF NG=0 THEN GOSUB 630:REM lijntje
2620 NG=0:REM wel getekend
3000 NEXT T:REM volgende moment
4990 GOSUB 100:REM terug naar tekstscherm
5000 GOTO 1060:REM volgende invoer
30000 REM 
30010 REM Zie voor nadere toelichting:
30020 REM AARDE & KOSMOS jg.1987 nummer 5
30030 REM 
30040 REM Basicode-3
30050 REM 
30060 REM Ogee Kruijt
30070 REM Pr. W. de Zwijgerln 2
30080 REM 3991 BR  HOUTEN
30090 REM 
30100 REM copyright (c) 1987
30110 REM Stichting Mens en Wetenschap,
30120 REM Huizen NH
30130 REM 
30140 REM TROS-RADIO dd 870715

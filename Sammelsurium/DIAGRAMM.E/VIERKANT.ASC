1000 A=100:GOTO 20:REM vierkanten
1010 DIM X(20),Y(20),S(20)
1020 HT=HO:VT=VE
1030 GOSUB 3000:REM UITLEG
1040 SP=0:CN=0
1050 PRINT"Tot welke diepte (0-5) ";
1060 INPUT DY
1070 GOSUB 600:REM grafisch bedrijf
1080 V=.375*2^(-DY)
1090 X=.5:Y=.5:S=.375:GOSUB 2000
1100 HO=.937:VE=.956:SR$="=>":GOSUB 650
1110 GOSUB 210
1120 GOSUB 100
1130 PRINT"Nog eentje (J/N) ";
1140 GOSUB 210:PRINT IN$
1150 IF (IN$<>"n") AND (IN$<>"N") THEN 1040
1160 GOTO 950
2000 IF S<V THEN RETURN
2010 S=S/2:YS=S*4/3
2020 HO=X+S:VE=Y:GOSUB 620
2030 HO=X:VE=Y+YS:GOSUB 630
2040 HO=X-S:VE=Y:GOSUB 630
2050 HO=X:VE=Y-YS:GOSUB 630
2060 HO=X+S:VE=Y:GOSUB 630
2070 X(SP)=X:Y(SP)=Y-YS:S(SP)=S:SP=SP+1
2080 X(SP)=X:Y(SP)=Y+YS:S(SP)=S:SP=SP+1
2090 X(SP)=X-S:Y(SP)=Y:S(SP)=S:SP=SP+1
2100 X=X+S:GOSUB 2000
2110 SP=SP-1:X=X(SP):Y=Y(SP):S=S(SP):GOSUB 2000
2120 SP=SP-1:X=X(SP):Y=Y(SP):S=S(SP):GOSUB 2000
2130 SP=SP-1:X=X(SP):Y=Y(SP):S=S(SP):GOSUB 2000
2140 RETURN
3000 GOSUB 100
3010 VE=0:HO=INT(HT/2)-8:GOSUB 110
3020 SR$="VIERKANTEN":GOSUB 150
3030 PRINT:PRINT
3040 PRINT"Dit programma tekent een vierkant op uw"
3050 PRINT"scherm. Met elk hoekpunt als middelpunt"
3060 PRINT"tekent het een kleiner vierkant."
3070 PRINT"Rond de hoekpunten van elk kleiner"
3080 PRINT"vierkant gebeurt weer hetzelfde, enz."
3090 PRINT:PRINT"U kunt zelf opgegeven hoe vaak dit moet"
3100 PRINT"worden herhaald."
3110 PRINT:PRINT"Veel kijkgenot !"
3120 HO=0:VE=VT:GOSUB 110
3130 PRINT"Druk op RETURN voor 't vervolg";
3140 GOSUB 210:IF IN<>13 THEN 3140
3150 GOSUB 100
3160 RETURN
30000 REM ******************************
30010 REM *                            *
30020 REM *    V I E R K A N T E N     *
30030 REM *  is een demonstratie van   *
30040 REM * de grafische mogelijkheden *
30050 REM *            in              *
30060 REM *    B A S I C O D E - 3     *
30070 REM *                            *
32000 REM *    Copyright (C) 1986      *
32010 REM *    Stichting BASICODE      *
32020 REM *                            *
32030 REM ******************************
32200 REM TROS-radio dd 861231

1000 A=200:GOTO 20:REM  vloeiende kromme
1010 P1=3.141592653589
1020 PRINT
1030 PRINT"Dit programma tekent een vloeiende"
1040 PRINT"kromme door een reeks punten"
1050 PRINT"waarvan we de coordinaten"
1060 PRINT"invoeren."
1070 PRINT
1080 PRINT"Bedenk dat een kleine y-waarde"
1090 PRINT"betekent bovenaan het scherm en"
1100 PRINT"een grote y-waarde onderaan het"
1110 PRINT"scherm."
1120 PRINT
1130 PRINT"Hoeveel punten invoeren";
1140 INPUT NN
1150 DIM XK(NN),YK(NN),TK(NN)
1160 FOR I=1 TO NN
1170 PRINT
1180 PRINT"Punt nummer ";I
1190 PRINT"x-waarde (  0 <= x < 1  )";
1200 INPUT XK(I)
1205 REM  gosub 250 = piepje
1210 IF (XK(I)<0) OR (XK(I)>=1) THEN GOSUB 250:GOTO 1190
1220 PRINT"y-waarde (  0 <= y < 1  )";
1230 INPUT YK(I)
1240 IF (YK(I)<0) OR (YK(I)>=1) THEN GOSUB 250:GOTO 1220
1250 REM 
1260 REM  nulpunt van Chebychev
1270 REM  veelterm geeft hoge
1280 REM  nauwkeurigheid
1290 REM 
1300 TK(I)=COS((2*I-1)*P1/(2*NN))
1310 NEXT I
1320 PRINT
1330 PRINT"Hoeveel punten tekenen ( >> 1 )";
1340 INPUT N
1350 REM 
1360 REM  stapgrootte
1370 REM 
1380 S1=(TK(NN)-TK(1))/(N-1)
1390 REM 
1400 REM  beginpunt
1410 REM 
1420 T=TK(1)
1430 REM 
1440 REM  grafisch scherm
1445 REM  voorgrondkleur
1450 REM 
1460 GOSUB 600:CN=0
1470 REM 
1480 REM  teken eerst de ingevoerde
1490 REM  punten
1500 REM 
1510 FOR I=1 TO NN
1520 HO=XK(I):VE=YK(I):GOSUB 620
1530 NEXT I
1540 REM 
1550 REM  bereken tussenwaarden
1560 REM  op alle tussenpunten
1570 REM 
1580 FOR TP=1 TO N
1590 REM 
1600 REM  beginwaarde nul voor optellen
1610 REM 
1620 X=0:Y=0
1630 REM 
1640 REM  loop de ingevoerde punten af
1650 REM 
1660 FOR K=1 TO NN
1670 REM 
1680 REM  beginwaarde voor
1690 REM  vermenigvuldigen
1700 REM 
1710 PD=1
1720 REM 
1730 REM  bepaal de weging van
1740 REM  dit invoerpunt ten opzichte
1750 REM  de andere
1760 REM 
1770 FOR J=1 TO NN
1780 IF J=K THEN 1800
1790 PD=PD*(T-TK(J))/(TK(K)-TK(J))
1800 NEXT J
1810 REM 
1820 REM  tel bijdrage van dit
1830 REM  invoerpunt bij
1840 REM 
1850 X=X+XK(K)*PD
1860 Y=Y+YK(K)*PD
1870 REM 
1880 REM  ga bijdrage van volgende
1890 REM  invoerpunt bepalen
1900 REM 
1910 NEXT K
1920 REM 
1930 REM  alle bijdragen opgeteld.
1940 REM  ga tussenpunt tekenen
1950 REM 
1960 HO=X:VE=Y
1970 IF TP=1 THEN GOSUB 620
1980 GOSUB 630
1990 REM 
2000 REM  volgende tussenpunt
2010 REM  een stapje verder
2020 REM 
2030 T=T+S1
2040 NEXT TP
2050 REM 
2060 REM  alle tussenpunten getekend.
2070 REM  laat tekening staan tot
2080 REM  toetsaanslag.
2090 REM 
2100 HO=.9:VE=.9:SR$="=>"
2110 GOSUB 650
2120 GOSUB 210
2130 REM 
2140 REM  klaar
2150 REM 
2160 GOTO 950
30000 REM 
30010 REM  dit programma tekent een
30020 REM  vloeiende kromme met
30030 REM  behulp van Lagrange-
30040 REM  interpolatie
30050 REM 
30060 REM  basicode-3
30070 REM 
32000 REM 
32010 REM  gemaakt door:
32020 REM 
32030 REM  Pim van Tend
32040 REM  Veldheimwg 8
32050 REM  6871 CD  RENKUM
32060 REM 
32080 REM  copyright (c) 1986
32200 REM TROS-RADIO dd 870218

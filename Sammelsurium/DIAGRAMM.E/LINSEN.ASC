1000 A=250:GOTO 20:REM   ** BEELDCONSTRUCTIE **
1010 GOSUB 100:BS=HO:HS=VE:CH=0:UW=.99999
1020 V=0:B=0:F=0:H2=HG/2:V2=VG/2:CN=0:H4=H2-4:V4=V2-4
1030 HO=(HO+1)/2-11:VE=0:GOSUB 110
1040 SR$="BEELDCONSTRUCTIE":GOSUB 150
1050 HO=0:VE=5:GOSUB 110
1060 PRINT"Wilt U de theorie lezen ? ";
1070 GOSUB 15350
1080 IF IN=74 THEN GOSUB 10010
1090 REM   ** INVOER **
1100 GOSUB 100
1110 PRINT"Geef de lengte van het voorwerp"
1120 PRINT"in centimeters : ";
1130 INPUT L:L=L*10:IF L<=0 THEN 1270
1140 PRINT:PRINT"Geef de voorwerpsafstand in"
1150 PRINT"centimeters : ";
1160 INPUT V:V=V*10:IF V<0 THEN 1270
1170 PRINT:PRINT"Geef de brandpuntsafstand van"
1180 PRINT"de lens in centimeters : ";
1190 INPUT F:F=F*10
1200 IF (V<>0) AND (F<>0) THEN 1510
1210 IF (V=0) AND (F=0) THEN 1270
1220 PRINT:PRINT"Geef de beeldpuntsafstand in"
1230 PRINT"centimeters : ";
1240 INPUT B:B=B*10
1250 IF ((F<0) AND (B>0)) OR (B<=F) THEN 1270
1260 IF B<>0 THEN 1510
1270 PRINT:PRINT"Sorry, ik heb te weinig of verkeerde"
1280 PRINT"gegevens gekregen."
1290 PRINT:SR$="Voer nogmaals in.":GOSUB 150
1300 GOSUB 15030:GOTO 1100
1500 REM   ** LENZENFORMULE **
1510 IF V=0 THEN V=1/((1/F)-(1/B))
1520 IF F=0 THEN F=1/((1/B)+(1/V))
1530 V1=V:F1=F:L1=L
1540 GOSUB 16070
1550 REM   ** AANPASSEN LENGTES **
1560 HV=L/V4:IF L*N/V4>HV THEN HV=L*N/V4
1570 IF V/H4>HV THEN HV=V/H4
1580 IF ABS(B/H4)>HV THEN HV=ABS(B/H4)
1590 IF ABS(F/H4)>HV THEN HV=ABS(F/H4)
1600 HV=HV+.001:CF=HV:HV=1/HV
1610 V=V*HV:L=L*HV:F=F*HV
1620 GOSUB 16070
1630 IF L*N>V4 THEN HV=L*N/V4:GOTO 1600
1640 IF ABS(B)>H4 THEN HV=ABS(B/H4):GOTO 1600
1800 REM   ** HOOFDAS EN LENS **
1810 GOSUB 1820:GOTO 2660
1820 GOSUB 600
1830 HO=0:VE=.5:GOSUB 620:HO=UW:GOSUB 630
1840 HO=.5:VE=30/VG:GOSUB 620:VE=(VG-30)/VG:GOSUB 630
1850 HO=H4/HG:VE=20/VG
1860 SR$="+":CN=1:GOSUB 650
1870 SR$="-":IF F>0 THEN SR$="+"
1880 CN=0:GOSUB 650
1890 REM   ** VOORWERP **
1900 XV=(H2-V)/HG:VE=.5:HO=XV:GOSUB 620
1910 YV=(V2-L)/VG:VE=YV:GOSUB 630
1920 HV=0:GOSUB 15080
1930 REM   ** BEELD **
1940 IF B=0 THEN 2010
1950 XB=(H2+B)/HG:HO=XB:VE=.5:GOSUB 620
1960 YB=(V2+SGN(B)*L*N)/VG:VE=YB:GOSUB 630
1970 HV=1:GOSUB 15080
2000 REM   ** STRAAL DOOR M **
2010 HO=XV:VE=YV:GOSUB 620
2020 HO=.5+(V*V2/L)/HG:VE=UW
2030 IF HO>=1 THEN VE=.5+(L*H2/V)/(VG+1):HO=UW
2040 GOSUB 630
2050 REM   ** F1 (F2 VOOR NEG LENS) **
2060 IF B=0 THEN 2100
2070 HO=XV:VE=YV:GOSUB 620
2080 HO=.5:VE=YB:GOSUB 630:HO=UW:GOSUB 630
2090 REM   ** F2 VOOR POS LENS **
2100 HO=XV:VE=YV:GOSUB 620
2110 HO=.5:GOSUB 630
2120 IF F<0 THEN 2170
2130 HO=.5+(F+F*V2/L)/HG:VE=UW
2140 IF HO>=1 THEN VE=.5+(L*(H2-F)/F)/(VG+1):HO=UW
2150 GOSUB 630:GOTO 2410
2160 REM   ** F1 VOOR NEG LENS **
2170 HO=.5+(-F*(V2-L)/L)/HG:VE=0
2180 IF HO>=1 THEN VE=.5-(L+L*H2/-F)/(VG+1):HO=UW
2190 GOSUB 630
2300 REM   ** VERLENGEN NEG LENS**
2310 BX=H2+F:BY=V2:EX=H2:EY=V2-L:GOSUB 15190
2320 BX=XB*HG:BY=YB*VG:EX=H2:EY=BY:GOSUB 15190
2330 BX=H2:BY=YB*VG:EX=H2-F:EY=V2:GOSUB 15190
2340 GOTO 2510
2400 REM   ** VERLENGEN POS LENS **
2410 IF B>=0 THEN 2510
2420 BX=XV*HG:BY=YV*VG:EX=XB*HG:EY=YB*VG:GOSUB 15190
2430 BX=XB*HG:BY=YB*VG:EX=H2:EY=BY:GOSUB 15190
2440 BX=H2:BY=V2-L:EX=XB*HG:EY=YB*VG:GOSUB 15190
2450 BX=XV*HG:BY=YV*VG:EX=H2-F:EY=V2:GOSUB 15190
2500 REM   ** PRINTEN OP HI-RES **
2510 HV=2:SR$="F1":HO=(H2-SGN(F)*F-7)/HG
2520 VE=(V2+4)/VG:GOSUB 15290
2530 HO=(H2-ABS(F))/HG:VE=(V2+2)/VG:GOSUB 620
2540 VE=V4/VG:GOSUB 630
2550 VE=(V2+4)/VG:SR$="F2":HO=(H2+SGN(F)*F-7)/HG
2560 GOSUB 15290:HV=1
2570 HO=(H2+ABS(F))/HG:VE=(V2+2)/VG:GOSUB 620
2580 VE=V4/VG:GOSUB 630
2590 SR$="L":HO=(H2-V-4)/HG:VE=(V2+11)/VG
2600 GOSUB 15290:IF B=0 THEN 2640
2610 HO=(H2+B-4)/HG:VE=(V2+11)/VG:SR$="B"
2620 IF B>0 THEN VE=(V2-10)/VG
2630 GOSUB 15290
2640 RETURN
2650 REM   ** BEWEGEN **
2660 SE=INT(H2/6):FV=1:MO=0
2670 HO=0:VE=.93:SR$="*":GOSUB 650
2680 GOSUB 200:IF IN$="" THEN 2680
2690 CN=1:GOSUB 650:CN=0
2700 IF (IN=66) OR (IN=70) OR (IN=78) OR (IN=28) THEN 2730
2710 IF (IN=83) OR (IN=86) OR (IN=87) OR (IN=29) THEN 2730
2720 GOTO 2670
2730 GOSUB 250:IF IN=66 THEN 3010
2740 IF IN=86 THEN FV=1:GOTO 2670
2750 IF IN=70 THEN FV=0:GOTO 2670
2760 IF IN=87 THEN MO=0:GOTO 2670
2770 IF IN=78 THEN MO=1:GOTO 2670
2780 IF IN<>83 THEN 2870
2790 RS$="Geef stapgrootte (01 t/m 99)":SR$=RS$:GOSUB 650
2800 GOSUB 200:IF (IN<48) OR (IN>57) THEN 2800
2810 GOSUB 250:G$=IN$:HO=.8:SR$=G$:GOSUB 650
2820 GOSUB 200:IF (IN<48) OR (IN>57) THEN 2820
2830 GOSUB 250:G$=G$+IN$:SE=VAL(G$)
2840 SR$=G$:CN=1:GOSUB 650:HO=0
2850 IF SE=0 THEN CN=0:GOTO 2790
2860 SR$=RS$:GOSUB 650:CN=0:GOTO 2670
2870 BB=B:VV=V:FF=F:V3=V1:F3=F1
2880 GOSUB 16010
2890 IF (V<=0) OR (V>H4) THEN 2950
2900 IF ABS(F)>H4 THEN 2950
2910 IF F=0 THEN 2930
2920 GOSUB 16070
2930 GOSUB 15380
2940 IF CH=0 THEN 2980
2950 B=BB:V=VV:F=FF:V1=V3:F1=F3
2960 SR$="ONMOGELIJKE WAARDE"
2970 GOSUB 650:CH=0:GOTO 2680
2980 IF MO=0 THEN GOSUB 600
2990 GOSUB 1830:GOTO 2670
3000 REM   ** BEREKENINGEN **
3010 IF (F=V) OR (F1=V1) THEN B1=0:GOTO 3050
3020 B1=1/((1/F1)-(1/V1))
3030 IF B1<0 THEN MD$="links":DM$="virtueel":GOTO 3060
3040 IF B1>0 THEN MD$="rechts":DM$="reeel":GOTO 3060
3050 MD$="oneindig ver":DM$="niet aanwezig"
3060 IF F1<0 THEN L$="negatief":GOTO 3080
3070 L$="positief"
3080 GOSUB 100:PRINT"BEREKENINGEN :"
3090 PRINT:PRINT:PRINT"De lens is ";L$
3100 SR=ABS(F1/10):CT=7:CN=2:GOSUB 310:PRINT
3110 PRINT"Brandpuntsafstand (f)  =    ";SR$;" cm"
3120 SR=V1/10:GOSUB 310:PRINT:N=ABS(B1/V1)
3130 PRINT"Voorwerpsafstand  (v)  =    ";SR$;" cm"
3140 SR=ABS(B1/10):GOSUB 310:PRINT
3150 PRINT"Beeldpuntsafstand (b)  =    ";SR$;" cm"
3160 PRINT MD$;" van de lens"
3170 SR=N:GOSUB 310:PRINT
3180 PRINT"Lineare vergroting (N) =    ";SR$;" X"
3190 SR=L1/10:GOSUB 310:PRINT
3200 PRINT"Grootte v.h. voorwerp  =    ";SR$;" cm"
3210 SR=L1*N/10:GOSUB 310:PRINT
3220 PRINT"Grootte van het beeld  =    ";SR$;" cm"
3230 PRINT:PRINT"Het beeld is ";DM$
3500 GOSUB 15010
3510 PRINT"Wilt U nog een keer ? ";
3520 GOSUB 15350
3530 IF IN=74 THEN 1000
3540 GOTO 950
10000 REM   ** THEORIE **
10010 GOSUB 100:PRINT"BEELDVORMING en BEELDCONSTRUCTIE"
10020 PRINT"--------------------------------":PRINT
10030 PRINT:PRINT"Dit programma laat U kennis maken"
10040 PRINT"met beeldvorming van een voorwerp mbv."
10050 PRINT"een lens.":PRINT
10060 PRINT"Wat is een lens eigenlijk ?":PRINT
10070 PRINT"Een lens is een doorschijnend voor-"
10080 PRINT"werp, dat begrensd wordt door een"
10090 PRINT"of twee gebogen oppervlakken.":PRINT
10100 PRINT"Er wordt onderscheid gemaakt tussen"
10110 PRINT"bolle (positieve) en holle (nega-"
10120 PRINT"tieve) lenzen.":GOSUB 15010
10130 PRINT"Bolle lenzen: deze lenzen zijn in"
10140 PRINT"het midden dikker dan aan de randen.":PRINT
10150 PRINT"Holle lenzen: deze lenzen zijn in"
10160 PRINT"het midden dunner dan aan de randen.":PRINT
10170 PRINT"Er bevindt zich een hoofdas, precies"
10180 PRINT"in het midden (= het optisch mid-"
10190 PRINT"delpunt, M), loodrecht op de lens.":PRINT
10200 PRINT"Een lichtstraal wordt door een bol-"
10210 PRINT"le lens tweemaal naar de hoofdas toe-"
10220 PRINT"gebroken, behalve als de lichtstraal"
10230 PRINT"door M gaat.":PRINT
10240 PRINT"Dit geldt ook voor een holle lens,"
10250 PRINT"alleen wordt nu de lichtstraal van"
10260 PRINT"de hoofdas afgebroken.":GOSUB 15010
10270 PRINT"Alle lichtstralen komen, al dan niet"
10280 PRINT"na verlenging, in een punt samen."
10290 PRINT"Hier ontstaat het beeld.":PRINT
10300 PRINT"Hier volgen nog enige begrippen:":PRINT
10310 PRINT" v = voorwerpsafstand, afstand van"
10320 PRINT"     het voorwerp tot de lens"
10330 PRINT" b = beeldpuntsafstand, afstand van"
10340 PRINT"     het beeld tot de lens"
10350 PRINT" f = brandpuntsafstand van de lens":PRINT
10360 PRINT"Het brandpunt is een van de kenmer-"
10370 PRINT"ken van de lens. Elke lens heeft 2"
10380 PRINT"brandpunten F1 (links van de lens),"
10390 PRINT"en F2 (rechts van de lens).":PRINT
10400 PRINT"Volgens afspraak staat het voorwerp"
10410 PRINT"altijd links van de lens.":GOSUB 15010
10420 PRINT"Bij een bolle lens kan het beeld re-"
10430 PRINT"eel (projekteerbaar), in dit geval"
10440 PRINT"is het beeld omgedraaid, of virtu-"
10450 PRINT"eel (niet projekteerbaar) zijn, dan"
10460 PRINT"is het beeld niet omgedraaid.":PRINT
10470 PRINT"Bij een holle lens is het beeld al-"
10480 PRINT"tijd virtueel.":PRINT
10490 PRINT"Hoe construeren we het beeld ?":PRINT
10500 PRINT"Hiervoor moeten 2 van de onderstaande"
10510 PRINT"gegevens bekend zijn:":PRINT
10520 PRINT" brandpuntsafstand (f)"
10530 PRINT" voorwerpsafstand  (v)"
10540 PRINT" beeldpuntsafstand (b)":PRINT
10550 PRINT"Verder moet de lengte van het voor-"
10560 PRINT"werp bekend zijn.":GOSUB 15010
10570 PRINT"Als we het hebben over een dunne sfe-"
10580 PRINT"rische lens (lens waarvan de gebogen"
10590 PRINT"oppervlakken bolvormig zijn) zijn er"
10600 PRINT"3 constructiestralen bekend nm.:":PRINT
10610 PRINT"Bij een bolle en holle lens:":PRINT
10620 PRINT"- een lichtstraal gericht op M, gaat"
10630 PRINT"  ongebroken door.":PRINT
10640 PRINT"Bij een bolle lens:":PRINT
10650 PRINT"- een lichtstraal evenwijdig aan de"
10660 PRINT"  hoofdas gaat na breking door F2"
10670 PRINT"- een lichtstraal gericht op F1,"
10680 PRINT"  treedt na breking evenwijdig aan de"
10690 PRINT"  hoofdas uit":GOSUB 15010
10700 PRINT"Bij een holle lens:":PRINT
10710 PRINT"- een lichtstraal evenwijdig aan de"
10720 PRINT"  hoofdas, komt na breking schijnbaar"
10730 PRINT"  uit F1"
10740 PRINT"- een lichtstraal gericht op F2,"
10750 PRINT"  treedt na breking evenwijdig"
10760 PRINT"  aan de hoofdas uit":PRINT
10770 PRINT"Hierop berust dit programma.":PRINT
10780 PRINT"Het tekent de 3 constructiestralen"
10790 PRINT"na de invoer van de gegevens.":GOSUB 15010
10800 PRINT"Hier volgen nog enige opmerkingen"
10810 PRINT"over dit programma:":PRINT
10820 PRINT"Als de verschillen tussen de inge-"
10830 PRINT"voerde waarden groot zijn, zou er"
10840 PRINT"wel eens een niet zo'n mooi plaatje"
10850 PRINT"kunnen verschijnen.":PRINT
10860 PRINT"Als U voor f een negatief getal invult"
10870 PRINT"wordt er met een holle lens gewerkt.":PRINT
10880 PRINT"Als U een waarde niet weet, vult U"
10890 PRINT"0 in. De computer geeft aan wanneer"
10900 PRINT"U te weinig, of verkeerde gegevens"
10910 PRINT"hebt ingevoerd."
10920 GOSUB 15010
10930 PRINT"Als de tekening gemaakt is, kunt U"
10940 PRINT"met de cursortoetsen het beeld ver-"
10950 PRINT"schuiven.":PRINT
10960 PRINT"In de beginsituatie kunt U de voor-"
10970 PRINT"werpsafstand verschuiven en wordt het"
10980 PRINT"beeld gewist.":PRINT
10990 PRINT"U kunt dit te allen tijde veranderen"
11000 PRINT"met de volgende toetsen :":PRINT
11010 PRINT"V : voorwerpsafstand verschuiven"
11020 PRINT"F : brandpuntsafstand verschuiven"
11030 PRINT"W : beeld wissen"
11040 PRINT"N : beeld niet wissen"
11050 PRINT"B : berekeningen tonen"
11060 PRINT"S : stapgrootte veranderen (hoeveel"
11070 PRINT"    beeldpunten verschuiven)":PRINT
11080 PRINT"Let op: bij stapgrootte kleiner dan"
11090 PRINT"10, toch twee cijfers invullen (bv 04) !"
11100 GOSUB 15030
11110 PRINT"Als U een juiste opdracht geeft hoort"
11120 PRINT"U een pieptoontje.":PRINT
11130 PRINT"Als er een sterretje linksonder in"
11140 PRINT"het scherm verschijnt wil dat zeg-"
11150 PRINT"gen dat U een opdracht kunt geven.":PRINT
11160 PRINT"Als de tekening niet op het scherm"
11170 PRINT"past, wordt er een stap overgeslagen."
11180 PRINT"(Er wordt ahw. nogmaals op de cursor-"
11190 PRINT"toetsen gedrukt).":PRINT
11200 PRINT"Als de tekening onmogelijk op het"
11210 PRINT"scherm past, komt linksonder ONMOGE-"
11220 PRINT"LIJKE WAARDE te staan."
11230 PRINT"U kunt nu alleen de andere kant op.":PRINT
11240 PRINT"U verschuift F1 als de lens bol en"
11250 PRINT"F2 als de lens hol is.":PRINT
11260 PRINT"VEEL PLEZIER MET DIT PROGRAMMA !!!"
11270 GOSUB 15030
12000 REM   ** NOGMAALS? **
12010 PRINT"Wilt U dit nogmaals zien ? ";
12020 GOSUB 15350
12030 IF IN=74 THEN 10010
12040 RETURN
15000 REM   ** SUBROUTINES **
15010 REM 
15020 REM   ** TOETS ? **
15030 HO=BS-17:VE=HS:GOSUB 110
15040 PRINT"Druk een toets =>";
15050 GOSUB 200:IF IN$="" THEN 15050
15060 GOSUB 100:RETURN
15070 REM   ** PIJLTJE **
15080 IF (L<8) AND (HV=0) THEN RETURN
15090 IF (L*N<8) AND (HV=1) THEN RETURN
15100 HO=HO-3/HG
15110 VE=VE-3/VG:IF (HV=0) OR (B<0) THEN VE=VE+6/VG
15120 GOSUB 620:HO=HO+6/HG
15130 GOSUB 630:HO=XV:IF HV=1 THEN HO=XB
15140 VE=VE+3/VG:IF (HV=0) OR (B<0) THEN VE=VE-6/VG
15150 GOSUB 630:HO=HO-3/HG
15160 VE=VE-3/VG:IF (HV=0) OR (B<0) THEN VE=VE+6/VG
15170 GOSUB 630:RETURN
15180 REM   ** STIPPELLIJN **
15190 IF BX>EX THEN SG=-2:GOTO 15210
15200 SG=2
15210 IF ABS((BY-EY)/(BX-EX))>2 THEN SG=SGN(SG)
15220 FOR I=BX+SGN(SG) TO EX-SGN(SG) STEP SG
15230 VE=(BY+(BY-EY)/(BX-EX)*(I-BX))/VG
15240 HO=I/HG
15250 GOSUB 620
15260 NEXT I
15270 RETURN
15280 REM   ** CONTROLEREN HO&VE **
15290 IF HO<0 THEN HO=0
15300 IF HO>=(HG-8*HV)/HG THEN HO=(HG-8*HV)/HG
15310 IF VE<0 THEN VE=0
15320 IF VE>=(VG-8*HV)/VG THEN VE=(VE-8*HV)/VG
15330 GOSUB 650:RETURN
15340 REM   ** J/N **
15350 GOSUB 210:IF (IN<>74) AND (IN<>78) THEN 15350
15360 RETURN
15370 REM   ** AANPASSEN **
15380 IF (ABS(B)>H4) OR (N*L>V4) OR (F=0) THEN 15400
15390 RETURN
15400 GOSUB 16010
15410 IF (V<=0) OR (V>H4) THEN CH=1:RETURN
15420 IF ABS(F)>H4 THEN CH=1:RETURN
15430 IF F=0 THEN 15400
15440 GOSUB 16070:GOTO 15380
16000 REM   ** BEWEGEN **
16010 IF (IN=29) AND (FV=1) THEN V=V-SE:V1=V1-SE*CF
16020 IF (IN=29) AND (FV=0) THEN F=F-SE:F1=F1-SE*CF
16030 IF (IN=28) AND (FV=1) THEN V=V+SE:V1=V1+SE*CF
16040 IF (IN=28) AND (FV=0) THEN F=F+SE:F1=F1+SE*CF
16050 RETURN
16060 REM   ** "AFRONDEN" **
16070 IF ABS(F-V)<.000001 THEN B=0:N=0:RETURN
16080 B=1/((1/F)-(1/V))
16090 B=INT(ABS(B))*SGN(B)
16100 IF B=0 THEN B=SGN(F)
16110 N=ABS(B/V):RETURN
32000 REM   ************************
32010 REM   DOOR RONALD VAN VUGT,
32020 REM   PA3EAH, POSTBUS 160
32030 REM   5580 AD, WAALRE (NB)
32040 REM 
32050 REM   DIT PROGRAMMA IS GEMAAKT
32060 REM   OP EEN ZX SPECTRUM
32070 REM 
32080 REM   JULI 1986, VERSIE 1.5
32090 REM   ************************
32100 REM TROS RADIO dd 860809

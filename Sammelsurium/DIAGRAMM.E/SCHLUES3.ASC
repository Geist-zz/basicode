1000 A=1000:GOTO 20:REM Keynesiaanse model
1010 DIM IN$(30,1),FO$(14),AF(1),Y(1),RM$(1),MR$(1)
1020 DIM XC(10,1),YC(10,1),VA$(10)
1030 HB=HO+1:VH=VE-2:PR$="off":PR=0
1040 RM$(0)="reele":RM$(1)="monetaire":Y1=0:R1=0:RM=2
1050 Y=0:R=0:W1=1:YE=0:YM=0:UW=(HG-1/HG)/HG:MV=1:S1=0
1060 MR$(0)="REELE SECTOR":MR$(1)="MONETAIRE SECTOR"
1070 READ AF(0),AF(1):FOR I=0 TO SGN(AF(1))
1080 FOR J=1 TO AF(I):READ IN$(J,I):NEXT J:NEXT I
1090 S$="":FOR I=1 TO HB/2:S$=S$+" ":NEXT I
1100 FOR I=1 TO 14:READ FO$(I):NEXT I
1110 GOSUB 100:FO=0
1120 SR$="KEYNESIAANSE MODEL"
1130 GOSUB 17710
1140 VE=3:HO=0:GOSUB 110
1150 PRINT"Geef uw keuze,":PRINT
1160 PRINT" 1. invoer"
1170 PRINT" 2. uitvoer"
1180 PRINT" 3. wijzigen"
1190 PRINT" 4. wissen"
1200 PRINT" 5. printer on/off"
1210 PRINT" 6. grafische voorstelling"
1220 PRINT" 7. stoppen"
1230 PRINT:PRINT"Uw keuze: ";:GOSUB 120:PRINT:PRINT
1240 PRINT"reele sector:........";
1250 IF AF(0)=0 THEN PRINT"afwezig":GOTO 1270
1260 PRINT"aanwezig"
1270 PRINT"monetaire sector:....";
1280 IF AF(1)=0 THEN PRINT"afwezig":GOTO 1300
1290 PRINT"aanwezig"
1300 PRINT"printer:.............";PR$
1310 PRINT"vrij geheugen in Kb: ";
1320 GOSUB 270:SR=FR/1024:CN=1:CT=4:GOSUB 310:PRINT SR$
1330 GOSUB 110
1340 GOSUB 210:IN=IN-48:IF (IN<1) OR (IN>7) THEN 1340
1350 GOSUB 100:IF IN=7 THEN 950
1360 ON IN GOSUB 2010,5010,7010,7410,7510,7610
1370 GOTO 1110
2000 REM *** invoer ***
2010 PRINT:PRINT"In welke sector wilt u invoeren:"
2020 GOSUB 17540
2030 IF IN=3 THEN RETURN
2040 GOSUB 100
2050 RM=IN-1:GOSUB 7450:SR$="INVOER "+MR$(RM)
2060 GOSUB 17710:W1=1
2070 AF(RM)=1:PRINT:PRINT:SP$=" "
2080 PRINT"verg.";SP$;AF(RM);": ";:INPUT IN$(AF(RM),RM)
2090 IF IN$(AF(RM),RM)="" THEN RETURN
2100 AF(RM)=AF(RM)+1:IF AF(RM)>9 THEN SP$=""
2110 GOTO 2080
3000 REM *** vergelijking verwerken ***
3010 K=1:SA=0:LG=LEN(IN$):F1=0:F2=0:FO=0:H6=0:H5=0
3020 H$=MID$(IN$,K,1):H1$=H$
3030 GOSUB 17180:IF (F3=0) OR (F3=2) THEN 3060
3040 K=K+1:IF K>LG THEN 3250
3050 GOTO 3020
3060 SA=K-1:K=K+1:F1=1:Z=0
3070 IF K>LG THEN 3120
3080 H$=MID$(IN$,K,1):H2$=H$
3090 GOSUB 17180:IF F3=1 THEN 3120
3100 IF F3=3 THEN F2=1:GOTO 3120
3110 K=K+1:H1$=H1$+H2$:GOTO 3070
3120 Z=Z+1:IF Z=Y(RM) THEN 3120
3130 IF Z>AF(RM) THEN FO=2:RETURN
3140 GOSUB 15010
3150 IF H1$=VA$ THEN 3170
3160 GOTO 3120
3170 H1=LEN(IN$(Z,RM))-VL+1:H2$=""
3180 H2=LEN(IN$)+1
3190 IF F2=1 THEN H2$="*":F2=0
3200 H1$="("+RIGHT$(IN$(Z,RM),H1)+")"
3210 IF SA<1 THEN IN$=H1$+H2$+RIGHT$(IN$,H2-K):GOTO 3230
3220 IN$=LEFT$(IN$,SA)+H1$+H2$+RIGHT$(IN$,H2-K)
3230 K=K+H1+1:LG=LEN(IN$)
3240 IF K<LG THEN 3020
3250 IF F1<>0 THEN 3010
3260 IF FO<>0 THEN RETURN
3270 IF LEFT$(IN$,1)="(" THEN 3290
3280 IN$="("+IN$+")"
3290 F2=0:LG=LEN(IN$):FOR I=1 TO LG
3300 H$=MID$(IN$,I,1)
3310 IF H$="(" THEN H6=H6+1:GOTO 3340
3320 IF H$=")" THEN H5=H5+1:GOTO 3340
3330 GOSUB 17180:IF (F3=0) OR (F3=2) THEN F2=1
3340 NEXT I
3350 IF H6<>H5 THEN FO=4
3360 RETURN
3400 REM *** haakjes uitwerken ***
3410 LG=LEN(IN$):DU$="":I=1
3420 IF I=LG THEN RETURN
3430 I=1:GB$="!":LG=LEN(IN$)
3440 H$=MID$(IN$,I,1)
3450 IF H$="(" THEN F1=I-1:GU$=GB$:GB$=""
3460 GB$=GB$+H$
3470 IF H$=")" THEN F2=I+1:GOSUB 3500:GOTO 3420
3480 I=I+1:IF I>LG THEN FO=4:RETURN
3490 GOTO 3440
3500 IF (I=LG) OR (GU$="!") THEN GOSUB 3830:RETURN
3510 J=LEN(GU$)-1
3520 H$=RIGHT$(GU$,1):H1$=H$
3530 IF (H$="*") OR (H$="/") THEN 3570
3540 IF (H$="+") OR (H$="-") THEN 3640
3550 IF H$="(" THEN H$="+":GOTO 3640
3560 H1$=H$+"*":F1=F1+1
3570 H$=MID$(GU$,J,1):IF H$="(" THEN 3620
3580 H1$=H$+H1$
3590 IF (H$="+") OR (H$="-") THEN 3640
3600 J=J-1
3610 GOTO 3570
3620 IF (LEFT$(H1$,1)="+") OR (LEFT$(H1$,1)="-") THEN 3640
3630 H1$="+"+H1$:H$="+":F1=F1+1
3640 TG$=H$:H2$="":H3$="":J=2:F1=F1-LEN(H1$)
3650 TE$=MID$(GB$,2,1):IF TE$<>"-" THEN TE$="+"
3660 H$=MID$(GB$,J,1)
3670 IF ((H$<>"+") AND (H$<>"-")) OR (J=2) THEN 3690
3680 GOSUB 3740:H2$="":TE$=H$:GOTO 3730
3690 IF H$=")" THEN GOSUB 3740:GOTO 3800
3700 IF (H$<>"+") AND (H$<>"-") THEN 3720
3710 IF J=2 THEN 3730
3720 H2$=H2$+H$
3730 J=J+1:GOTO 3660
3740 IF LEN(H1$)<2 THEN H4$="":GOTO 3760
3750 H4$=RIGHT$(H1$,LEN(H1$)-1)
3760 IF (TG$="-") AND (TE$="-") THEN H1$="+"+H4$
3770 IF (TG$="+") AND (TE$="-") THEN H1$="-"+H4$
3780 H3$=H3$+H1$+H2$:H1$=TG$+H4$
3790 RETURN
3800 IF F1=0 THEN F1=1
3810 IN$=LEFT$(IN$,F1)+H3$+RIGHT$(IN$,LG-F2+1)
3820 RETURN
3830 H$=MID$(IN$,2,1):GB$=""
3840 IF (H$="+") OR (H$="-") THEN 3870
3850 GB$="+":IF H$<>"(" THEN 3870
3860 IN$=RIGHT$(IN$,LEN(IN$)-1)
3870 DU$=DU$+GB$+MID$(IN$,2,F2-3)
3880 IF I=LG THEN 3900
3890 IN$="("+RIGHT$(IN$,LG-F2+1)+")"
3900 RETURN
4000 REM *** evenwichtsinkomen berekenen ***
4010 F1=0:F2=0:YG=0:GG=0:RY=0:K=1:LG=LEN(IN$):H1$=""
4020 H$=MID$(IN$,K,1)
4030 IF (H$="*") OR (H$="/") THEN GOSUB 4210
4040 IF FO<>0 THEN RETURN
4050 IF (H$="+") OR (H$="-") THEN GOSUB 4120
4060 IF H$="Y" THEN H$="":F1=1
4070 IF H$="R" THEN H$="":F1=2
4080 H1$=H1$+H$
4090 K=K+1
4100 IF K>LG THEN GOSUB 4120:RETURN
4110 GOTO 4020
4120 G1=VAL(H1$)
4130 IF (G1=0) AND (F1=0) THEN RETURN
4140 IF G1=0 THEN G1=VAL(H1$+"1")
4150 IF F1=0 THEN GG=GG+G1
4160 IF F1=1 THEN YG=YG+G1
4170 IF F1=2 THEN RY=RY+G1
4180 F1=0:H1$="":RETURN
4200 REM *** delen of vermenigvuldigen ***
4210 G1=VAL(H1$):F2=0
4220 IF G1=0 THEN G1=VAL(H1$+"1")
4230 B1$=H$:H1$=""
4240 K=K+1:IF K>LG THEN H$="+":GOTO 4310
4250 H$=MID$(IN$,K,1)
4260 IF (H$="+") OR (H$="-") THEN 4310
4270 IF (H$="*") OR (H$="/") THEN 4310
4280 IF (H$="Y") OR (H$="y") THEN H$="":F2=1
4290 IF (H$="R") OR (H$="r") THEN H$="":F2=2
4300 H1$=H1$+H$:GOTO 4240
4310 G2=VAL(H1$):B2$=H$
4320 IF (G2=0) AND (B1$="/") THEN FO=10:RETURN
4330 IF (G2=0) AND (F2=0) THEN 4300
4340 IF G2=0 THEN G2=VAL(H1$+"1")
4350 IF B1$="*" THEN G1=G1*G2:GOTO 4390
4360 IF B1$="/" THEN G1=G1/G2
4370 IF (F1>0) AND (F2>0) THEN F1=0:F2=0
4380 IF F2>0 THEN F1=F2:F2=0
4390 IF (F1>0) AND (F2>0) THEN FO=5
4400 IF F2>0 THEN F1=F2:F2=0
4410 IF (B2$<>"-") AND (B2$<>"+") THEN 4430
4420 SR=G1:GOSUB 300:H1$=SR$:H$=B2$:RETURN
4430 H1$="":B1$=B2$:GOTO 4240
5000 REM *** berekeningen ***
5010 PRINT:PRINT"Wat wilt u weten:":PRINT
5020 PRINT:PRINT" 1. evenwichtsinkomen en rentevoet"
5030 PRINT:PRINT" 2. totaal overzicht"
5040 GOSUB 17560:GOSUB 100
5050 IF IN=3 THEN RETURN
5060 ON IN GOTO 5810,5210
5200 REM *** totaaloverzicht ***
5210 GOSUB 5810:IF FO<>0 THEN RETURN
5220 GOSUB 100:SL=0:SH=1:IF AF(1)=0 THEN SH=0
5230 IF PR=1 THEN GOSUB 17810
5240 CN=2:CT=6:SR$="TOTAALOVERZICHT":GOSUB 17710
5250 IF PR=1 THEN F1=1:GOSUB 17510
5260 PRINT:PRINT:IF FO<>0 THEN 16040
5270 SR=Y:GOSUB 310:SR$="Y="+SR$
5280 IF PR=1 THEN F1=0:GOSUB 17510
5290 PRINT SR$
5300 IF SH=0 THEN 5350
5310 CN=3:CT=6
5320 SR=R:GOSUB 310:SR$="R="+SR$
5330 IF PR=1 THEN F1=1:GOSUB 17510
5340 PRINT SR$
5350 H1=AF(0):IF AF(1)>H1 THEN H1=AF(1)
5360 IF PR=1 THEN GOSUB 360
5370 CN=1:CT=5
5380 SR$=MR$(0)+RIGHT$(S$,HB/2-14)
5390 IF SH=0 THEN 5410
5400 SR$=SR$+MR$(1)
5410 IF PR=1 THEN F1=1:GOSUB 17510
5420 PRINT:PRINT SR$:PRINT
5430 FOR TE=1 TO H1:B$=" "
5440 FOR RM=0 TO SH
5450 IF TE=Y(RM) THEN 5510
5460 GOSUB 17610:IF IN$="" THEN 5510
5470 IF FO<>0 THEN 16040
5480 SR=YG*Y+RY*R+GG:GOSUB 310
5490 H1=LEN(LF$)
5500 B$=B$+LF$+"="+RIGHT$(S$,3-H1)+SR$
5510 IF RM=1 THEN 5530
5520 B$=B$+RIGHT$(S$,HB/2-LEN(B$)+1)
5530 NEXT RM
5540 SR$=B$:IF PR=1 THEN F1=0:GOSUB 17510
5550 PRINT SR$
5560 NEXT TE
5570 SR=ME:GOSUB 310:B$="mpr: "+SR$
5580 IF AF(1)=0 THEN SR$=B$:GOTO 5610
5590 SR=MM:GOSUB 310
5600 SR$=B$+RIGHT$(S$,HB/2-LEN(B$)+5)+SR$
5610 IF PR=1 THEN GOSUB 17510
5620 PRINT SR$:GOTO 16010
5800 REM *** evenwichtsinkomen berekenen ***
5810 SL=0:SH=1
5820 IF AF(0)=0 THEN SL=1
5830 IF AF(1)=0 THEN SH=0
5840 IF (SL<>1) OR (SH<>0) THEN 5860
5850 FO=3:GOTO 16040
5860 IF ((Y<>0) OR (R<>0)) AND (W1=0) THEN 6270
5870 PRINT"Momentje, dit kost zelfs voor mij tijd!";
5880 RM=SL
5890 Y(RM)=0
5900 Y(RM)=Y(RM)+1:FO=0
5910 IF Y(RM)>AF(RM) THEN FO=6:GOTO 16040
5920 Z=Y(RM):GOSUB 17050:IF FO<>0 THEN 16040
5930 B=I:IF F2=1 THEN 5990
5940 IN$=LF$:GOSUB 3010
5950 IF (F2=1) OR (FO<>0) THEN 5900
5960 B$=IN$:H$=IN$(Y(RM),RM):IN$=RIGHT$(H$,LEN(H$)-B+1)
5970 GOSUB 3010:IF (FO<>0) OR (F2=1) THEN 5900
5980 GOTO 6030
5990 IN$=LF$:GOSUB 3010:B$=IN$
6000 IF FO<>0 THEN 16040
6010 H$=IN$(Y(RM),RM):IN$=RIGHT$(H$,LEN(H$)-B+1)
6020 GOSUB 3010:IF FO<>0 THEN 16040
6030 H$=B$:B$=IN$:IN$=H$:GOSUB 3410:IF FO<>0 THEN 16040
6040 IN$=DU$:GOSUB 4010:IF FO<>0 THEN 16040
6050 GY=YG:GH=GG:YR=RY
6060 IN$=B$:GOSUB 3410:IF FO<>0 THEN 16040
6070 IN$=DU$:GOSUB 4010:IF FO<>0 THEN 16040
6080 YG=GY-YG:GG=GG-GH:YR=RY-YR
6090 IF RM=0 THEN YE=YG:RG=GG:RR=YR
6100 IF RM=1 THEN YM=YG:GM=GG:MR=YR
6110 RM=RM+1:IF RM=SH THEN 5890
6120 RM=2:IF YE=0 THEN FO=9:RM=0:GOTO 16040
6130 ME=1/YE*SGN(RG)
6140 IF AF(1)=0 THEN Y=RG/YE:RM=0:GOTO 6260
6150 IF YM=0 THEN FO=9:RM=1:GOTO 16040
6160 GOSUB 17910:IF FO<>0 THEN 16040
6170 Y=H2:R=H1
6180 REM *** multiplier berekenen ***
6190 FOR RM=0 TO 1
6200 IF RM=0 THEN RG=RG+SGN(RG)
6210 IF RM=1 THEN GM=GM+SGN(GM)
6220 GOSUB 17910:FO=0
6230 IF RM=0 THEN ME=H2-Y:RG=RG-SGN(RG)
6240 IF RM=1 THEN MM=H2-Y:GM=GM-SGN(GM)
6250 NEXT RM
6260 IF Y<0 THEN FO=7:GOTO 16040
6270 W1=0:IF IN=2 THEN RETURN
6280 GOSUB 100:SR$="OPLOSSING":
6290 GOSUB 17710:PRINT:PRINT:PRINT
6300 CN=2:CT=6
6310 IF PR=1 THEN GOSUB 17810
6320 SR$="Van het opgegeven model"
6330 IF PR=1 THEN GOSUB 350
6340 PRINT SR$;:IF AF(1)=0 THEN 6560
6350 SR$=" zijn de even-":IF PR=1 THEN F1=0:GOSUB 17510
6360 PRINT SR$:SR$="wichtsvergelijkingen:"
6370 IF PR=1 THEN F1=1:GOSUB 17510
6380 PRINT SR$:SR$=RM$(0)+" sector: "+IN$(Y(0),0)
6390 PRINT:IF PR=1 THEN F1=0:GOSUB 17510
6400 PRINT SR$:SR$=RM$(1)+" sector: "+IN$(Y(1),1)
6410 IF PR=1 THEN F1=1:GOSUB 17510
6420 PRINT SR$:PRINT:SR=Y:GOSUB 310
6430 SR$="Het evenwichtsinkomen (Y) is: "+SR$
6440 IF PR=1 THEN F1=0:GOSUB 17510
6450 PRINT SR$:SR=R:CN=3:GOSUB 310
6460 SR$="De rentevoet (R) is: "+SR$
6470 IF PR=1 THEN F1=1:GOSUB 17510
6480 PRINT SR$:PRINT:CN=3
6490 FOR RM=0 TO 1:SR$=""
6500 IF RM=0 THEN H1=ME:GOTO 6520
6510 H1=MM
6520 SR=H1:GOSUB 310
6530 SR$="Multiplier "+RM$(RM)+" sector: "+SR$
6540 IF PR=1 THEN F1=0:GOSUB 17510
6550 PRINT SR$:NEXT RM:GOTO 16010
6560 SR$=" is de even-":IF PR=1 THEN F1=0:GOSUB 17510
6570 PRINT SR$:SR$="wichtsvergelijking: "+IN$(Y(0),0)
6580 IF PR=1 THEN F1=1:GOSUB 17510
6590 PRINT SR$:PRINT:SR=Y:GOSUB 310
6600 SR$="Het evenwichtsinkomen (Y) is: "+SR$
6610 IF PR=1 THEN GOSUB 17510
6620 PRINT SR$:PRINT:CN=3:SR=ME:GOSUB 310
6630 SR$="De multiplier is: "+SR$
6640 IF PR=1 THEN GOSUB 17510
6650 PRINT SR$:GOTO 16010
7000 REM *** wijzigen ***
7010 PRINT:PRINT"Welke sector wilt u wijzigen:"
7020 GOSUB 17540
7030 IF IN=3 THEN RETURN
7040 GOSUB 100:W1=1
7050 H1=1:H2=VH-4:RM=IN-1
7060 GOSUB 100:SP$=" "
7070 SR$=MR$(RM)+" WIJZIGEN"
7080 GOSUB 17710
7090 PRINT:PRINT:FOR I=H1 TO H2
7100 IF I>9 THEN SP$=""
7110 IF I>40 THEN 7130
7120 PRINT SP$;I;":";IN$(I,RM)
7130 NEXT I
7140 HO=0:VE=VH-1:GOSUB 110:IN$=""
7150 PRINT"Geef commando: ";:INPUT IN$
7160 IF IN$="" THEN RETURN
7170 IF LEN(IN$)>1 THEN IN=VAL(RIGHT$(IN$,LEN(IN$)-1))
7180 IN$=LEFT$(IN$,1)
7190 SR$=IN$:GOSUB 330
7200 IF (SR$="W") AND (IN<>0) THEN GOSUB 7250
7210 IF SR$="T" THEN GOSUB 7270
7220 IF (SR$="V") AND (IN<>0) THEN GOSUB 7280
7230 IF SR$="P" THEN 7310
7240 GOTO 7060
7250 FOR I=IN TO AF(RM):IN$(I,RM)=IN$(I+1,RM)
7260 NEXT I:AF(RM)=AF(RM)-1:RETURN
7270 AF(RM)=AF(RM)+1:IN=AF(RM)
7280 HO=0:VE=VH-1:GOSUB 110:PRINT"Geef vergelijking: ";IN
7290 INPUT IN$(IN,RM)
7300 RETURN
7310 H1=H2+1:H2=H2+VH-4
7320 IF H1>AF THEN H1=1:H2=VH-4
7330 GOTO 7060
7400 REM *** wissen ***
7410 PRINT:PRINT"Welke sector wilt u wissen:"
7420 GOSUB 17540
7430 IF IN=3 THEN RETURN
7440 GOSUB 100:RM=IN-1:W1=1
7450 FOR I=1 TO AF(RM):IN$(I,RM)="":NEXT I
7460 AF(RM)=0:RETURN
7500 REM *** printer on/off ***
7510 PR=1-PR
7520 IF PR$="on" THEN PR$="off":RETURN
7530 PR$="on":RETURN
7600 REM *** grafische voorstelling ***
7610 GOSUB 100
7620 SR$="GRAFISCHE VOORSTELLING":GOSUB 17710
7630 PRINT:PRINT:PRINT:PRINT"Geef uw keuze:"
7640 PRINT:PRINT" 1. IS-LM grafiek"
7650 PRINT:PRINT" 2. IS-LM grafiek, nieuw en oud"
7660 PRINT:PRINT" 3. een grafiek"
7670 PRINT:PRINT" 4. meerdere grafieken"
7680 PRINT:PRINT" 5. opgeslagen vergelijken tonen"
7690 PRINT:PRINT" 6. terug naar hoofdmenu"
7700 PRINT:PRINT"Uw keuze: ";
7710 GOSUB 210:IN=IN-48:IF (IN<1) OR (IN>6) THEN 7710
7720 IF IN=6 THEN RETURN
7730 GOSUB 100
7740 ON IN GOSUB 8100,8010,8410,8510,9110
7750 IF FO<>0 THEN RETURN
7760 IF IN=5 THEN GOSUB 16010:GOTO 7610
7770 IF IN<>5 THEN GOSUB 210:IF IN=67 THEN GOSUB 20020
7780 GOTO 7610
8000 REM *** IS-LM grafiek ***
8010 IF (Y1=0) OR (R1=0) THEN 8100
8020 FOR I=9 TO 10:FOR J=0 TO 1:XC(I-2,J)=XC(I,J)
8030 YC(I-2,J)=YC(I,J):NEXT J:NEXT I:S1=7
8040 VA$(7)="R.I=S":VA$(8)="M.L=M":GOTO 8110
8100 S1=9
8110 IF (AF(0)=0) OR (AF(1)=0) THEN FO=12:GOTO 16040
8120 IF W1=1 THEN IN=2:FO=0:GOSUB 5810:W1=0
8130 IF (R=0) AND (FO=0) THEN FO=11:GOTO 16040
8140 IF FO<>0 THEN RETURN
8150 IF S1=9 THEN Y1=2*Y:R1=2*R
8160 H6=Y1:H4=R1
8170 F1=H6/.8999:F2=.9/H4:S2=10
8180 FOR I=9 TO 10
8190 IF I=9 THEN H1=YE:H2=RR:H3=RG
8200 IF I=10 THEN H1=YM:H2=MR:H3=GM
8210 GOSUB 9280:IF FO<>0 THEN I=11
8220 NEXT I
8230 IF FO<>0 THEN 16040
8240 GOSUB 9500
8250 VA$(9)="R.I=S":VA$(10)="M.L=M"
8260 GOSUB 9170
8270 FOR HO=Y/F1+.1 TO .1 STEP -3/HG
8280 VE=.9-F2*R:GOSUB 620:NEXT HO
8290 FOR VE=.9-F2*R TO .9 STEP 3/VG
8300 HO=Y/F1+.1:GOSUB 620:NEXT VE
8310 RETURN
8400 REM *** 1 verg. invoeren ***
8410 SR$="EEN VERGELIJKING INVOEREN":GOSUB 17710
8420 AF=1:PRINT:PRINT:GOTO 8550
8500 REM *** verg. invoeren ***
8510 SR$="VERGELIJKINGEN INVOEREN":GOSUB 17710
8520 PRINT:PRINT:PRINT
8530 PRINT"Hoeveel vergelijkingen: ";
8540 INPUT AF:IF (AF<1) OR (AF>8) THEN 8530
8550 S1=MV:PRINT:FOR I=1 TO AF
8560 PRINT:PRINT"  Geef vergelijking: ";
8570 INPUT VA$(MV):SR$=LEFT$(VA$(MV),2):GOSUB 330
8580 IF (SR$="M.") OR (SR$="R.") THEN 8640
8590 IF SR$<>"O." THEN 8630
8600 SR$=MID$(VA$(MV),3,2):IF VAL(SR$)<>0 THEN 8640
8610 IF SR$="/." THEN 8640
8620 GOTO 8580
8630 PRINT"Niet correct":PRINT:GOTO 8560
8640 MV=MV+1:IF MV>8 THEN MV=1
8650 NEXT I:L=S1-1:PRINT:PRINT:PRINT"Momentje...";
8660 L=L+1:F1=0:IF L>8 THEN L=1
8670 LG=LEN(VA$(L)):IF LG=0 THEN 8860
8680 SR$=LEFT$(VA$(L),1):GOSUB 330
8690 IF SR$="M" THEN RM=1
8700 IF SR$="R" THEN RM=0
8710 IF SR$="O" THEN F1=1
8720 H1$=MID$(VA$(L),3,LG-2):H1=VAL(H1$)
8730 IF F1=1 THEN VA$(L)=H1$:GOTO 8940
8740 IF H1=0 THEN 8780
8750 IF H1>AF(RM) THEN FO=14:VA$(L)="":GOTO 16040
8760 IN$=IN$(H1,RM):Z=H1:GOSUB 15010
8770 VA$(L)=LEFT$(VA$(L),2)+VA$:GOTO 8830
8780 Z=0
8790 Z=Z+1:GOSUB 15010:IF H1$=VA$ THEN 8820
8800 IF Z>=AF(RM) THEN FO=2:VA$="":GOTO 16040
8810 GOTO 8790
8820 IN$=IN$(Z,RM)
8830 TE=Z:GOSUB 17610:IF YG=0 THEN H3=GG+R*RY:H1=0:GOTO 8850
8840 H1=1:H2=1/YG:H3=-GG/YG-R*RY
8850 H6=400:H4=H6:I=L:GOSUB 9280:IF FO<>0 THEN 16040
8860 IF L=MV-1 THEN 8880
8870 GOTO 8660
8880 F1=H6/.8999:F2=.9/H4:GOSUB 9500
8890 S2=MV-1:IF MV<S1 THEN S2=8
8900 GOSUB 9170
8910 IF MV<S1 THEN S1=1:S2=MV:GOSUB 9170
8920 RETURN
8930 REM *** zoek opgeslagen verg. ***
8940 H6=400:H4=H6:IF H1=0 THEN 8990
8950 IF H1>10 THEN FO=14:VA$(L)="":GOTO 16040
8960 IF LEFT$(VA$(H1),1)="O" THEN VA$(L)="":GOTO 8860
8970 FOR J=0 TO 1:XC(L,J)=XC(H1,J):YC(L,J)=YC(H1,J)
8980 NEXT J:VA$(L)=VA$(H1):VA$(H1)="":GOTO 8860
8990 IF H1$<>"/." THEN 9020
9000 VA$(L)="":XC(L,0)=0:XC(L,1)=H6:YC(L,0)=0:YC(L,1)=H4
9010 GOTO 8860
9020 H1=-1
9030 H1=H1+1:IF H1=L THEN 9050
9040 IF H1$=VA$(H1) THEN 8970
9050 IF H1>9 THEN FO=2:VA$(L)="":GOTO 16040
9060 GOTO 9030
9100 REM *** tonen ***
9110 SR$="OPGESLAGEN VARIABELEN TONEN":GOSUB 17710
9120 HO=0:VE=6:GOSUB 110:FOR I=1 TO 10
9130 IF VA$(I)<>"" THEN PRINT"   ";I;". => ";VA$(I):PRINT
9140 NEXT I:RETURN
9160 REM *** verg. tekenen ***
9170 FOR I=S1 TO S2
9180 SR$=MID$(VA$(I),3,LEN(VA$(I))-2)
9190 FOR J=0 TO 1
9200 HO=XC(I,J)/F1+.1:VE=.9-F2*YC(I,J)
9210 IF J=0 THEN GOSUB 620:GOTO 9250
9220 GOSUB 630:IF VE>.88 THEN VE=.85
9230 IF HO>.98 THEN HO=.96
9240 HO=HO-.017*LEN(SR$):VE=VE+.01:GOSUB 650
9250 NEXT J:NEXT I
9260 RETURN
9270 REM *** coordinaten berekenen ***
9280 IF H1<>0 THEN 9320
9300 XC(I,0)=0:XC(I,1)=H6:YC(I,0)=H3:YC(I,1)=H3:RETURN
9320 F3=H2/H1:IF F3>=0 THEN H5=H3/H1:GOTO 9340
9330 H5=(H4*H2+H3)/H1
9340 IF (H5<0) OR (H5>H6) THEN 9380
9350 XC(I,0)=H5:YC(I,0)=0
9360 IF F3<0 THEN YC(I,0)=H4
9370 GOTO 9400
9380 H5=-H3/H2:IF (H5<0) OR (H5>H4) THEN FO=13:RETURN
9390 YC(I,0)=H5:XC(I,0)=0
9400 IF F3>=0 THEN H5=(H4*H2+H3)/H1:GOTO 9420
9410 H5=H3/H1
9420 IF (H5<0) OR (H5>H6) THEN 9460
9430 XC(I,1)=H5:YC(I,1)=H4
9440 IF F3<0 THEN YC(I,1)=0
9450 GOTO 9480
9460 H5=(H1*H6-H3)/H2:IF (H5<0) OR (H5>H4) THEN FO=13:RETURN
9470 YC(I,1)=H5:XC(I,1)=H6
9480 RETURN
9490 REM *** assenstelsel ***
9500 GOSUB 600:CN=0:CT=3:HO=.1:VE=.9:GOSUB 620
9510 HO=UW:GOSUB 630:HO=.1:VE=0:GOSUB 620
9520 VE=.9:GOSUB 630
9530 FOR SR=H6/4 TO H6 STEP H6/4.001
9540 HO=SR/F1+.1:VE=.89:GOSUB 620
9550 VE=.91:GOSUB 630:GOSUB 310
9560 HO=HO-.075:VE=.93:GOSUB 650
9570 NEXT SR
9580 FOR I=0 TO H4 STEP H4/4.001
9590 VE=.9-I/H4*.9:HO=.09:GOSUB 620
9600 HO=.11:GOSUB 630:SR=I:GOSUB 310
9610 IF (VAL(SR$)>=1) OR (I=0) THEN 9630
9620 CT=4:CN=2:GOSUB 310:SR$=RIGHT$(SR$,3):CT=3:CN=0
9630 HO=0:GOSUB 650:NEXT I
9640 RETURN
15000 REM *** variabele uit string halen ***
15010 VA$="":VL=1:LG=LEN(IN$(Z,RM))
15020 H$=MID$(IN$(Z,RM),VL,1)
15030 IF H$="=" THEN VL=VL+1:RETURN
15040 IF H$=" " THEN 15060
15050 VA$=VA$+H$
15060 VL=VL+1:IF VL<LG THEN 15020
15070 FO=1:RETURN
16000 REM *** toets ***
16010 HO=HB-8:VE=VH:GOSUB 110:PRINT"<TOETS>";
16020 GOSUB 210:RETURN
16030 REM *** foutmelding ***
16040 GOSUB 100:SR$="FOUTMELDING":GOSUB 17710
16050 PRINT:PRINT:PRINT
16060 PRINT"Er is een fout geconstateerd, en wel:"
16070 IF FO<>2 THEN PRINT FO$(FO):GOTO 17010
17000 PRINT H1$;FO$(FO)
17010 IF RM>1 THEN 16010
17020 PRINT:PRINT"Deze fout komt voor in de ";RM$(RM)
17030 PRINT"sector.":GOTO 16010
17040 REM *** evenwichtsvergelijkingen zoeken ***
17050 I=0:H1$="":F2=0
17060 LG=LEN(IN$(Z,RM))
17070 I=I+1:IF I>=LG THEN FO=1:RETURN
17080 H$=MID$(IN$(Z,RM),I,1)
17090 IF H$=" " THEN 17070
17100 IF (H$="+") OR (H$="-") THEN F2=1
17110 IF (H$>="0") AND (H$<="9") THEN F2=1
17120 IF H$="=" THEN 17140
17130 H1$=H1$+H$:GOTO 17070
17140 IF H1$="R" THEN F2=1
17150 IF H1$="Y" THEN F2=1
17160 I=I+1:LF$=H1$:RETURN
17170 REM *** variabele of cijfer ? ***
17180 F3=0:IF (H$="Y") OR (H$="R") THEN GOSUB 17250
17200 IF (H$>="0") AND (H$<="9") THEN F3=F3+1
17210 IF (H$="*") OR (H$="-") OR (H$="+") THEN F3=F3+1
17220 IF (H$="/") OR (H$="(") OR (H$=")") THEN F3=F3+1
17230 IF (H$=".") OR (H$="") THEN F3=F3+1
17240 RETURN
17250 F3=2:H$=MID$(IN$,K+1,1):RETURN
17500 REM *** printen ***
17510 GOSUB 350:FOR J=0 TO F1:GOSUB 360:NEXT J
17520 RETURN
17530 REM *** reel of monetair ? ***
17540 PRINT:PRINT:PRINT" 1. reele sector"
17550 PRINT:PRINT" 2. monetaire sector"
17560 PRINT:PRINT" 3. terug naar het hoofdmenu"
17570 PRINT:PRINT:PRINT"Geef uw keuze: ";
17580 GOSUB 210:IN=IN-48:IF (IN<1) OR (IN>3) THEN 17580
17590 RETURN
17600 REM *** waarden verg. zoeken ***
17610 Z=TE:GOSUB 17050
17620 IN$=RIGHT$(IN$(Z,RM),LG-I+1)
17630 IF IN$="" THEN RETURN
17640 GOSUB 3010:IF FO<>0 THEN RETURN
17650 GOSUB 3410:IF FO<>0 THEN RETURN
17660 IN$=DU$:GOSUB 4010
17670 RETURN
17700 REM *** reverse printen ***
17710 HO=INT(HB/2-LEN(SR$)/2-3):VE=0
17720 GOSUB 110:GOSUB 150:RETURN
17800 REM *** model printen ***
17810 SR$="HET OPGEGEVEN MODEL IS:"
17820 F1=1:GOSUB 17510
17830 FOR RM=SL TO SH:FOR I=1 TO AF(RM)
17840 IF I>1 THEN 17870
17850 SR$=MR$(RM):F1=1:GOSUB 17510
17870 F1=0:SR$=IN$(I,RM):GOSUB 17510
17880 NEXT I:GOSUB 360:NEXT RM:GOSUB 360
17890 RETURN
17900 REM *** Y en R berekenen ***
17910 H3=1/YE*RG:H4=1/YE*RR
17920 H5=1/YM*GM:H6=1/YM*MR
17930 IF (RR=0) AND (MR=0) THEN FO=11:RETURN
17940 H1=(H5-H3)/(H4-H6):H2=H4*H1+H3
17950 IF H1<0 THEN FO=8:RM=1
17960 IF H2<0 THEN FO=7
17970 RETURN
20000 REM *** hier kunt u een screen- ***
20010 REM *** dumpprogramma plaatsen  ***
20020 IF PR=0 THEN RETURN:REM *** LATEN STAAN! ***
20025 *GDUMP:
20030 RETURN
25000 DATA 13,4
25010 DATA "C=.75(Y-B-Sp+Ou)+40"
25020 DATA "B=.35Y+40"
25030 DATA "Sp=.1Y","I=-800R+200"
25040 DATA "O=Ob+Ou","Ob=.225Y+80"
25050 DATA "Ou=5W+54","E=60","M=100"
25060 DATA "W=Na-Nv","Na=17"
25070 DATA "Nv=Y/25","Y=C+I+Ob+E-M"
25080 DATA "La=.5Y"
25090 DATA "Lb=-660R+150","M=210"
25100 DATA "La+Lb=M"
25150 DATA "Geen juiste vergelijking"
25160 DATA " is een onbekende variabele"
25170 DATA "Er is geen invoer aanwezig"
25180 DATA "De haakjes zijn fout geplaatst"
25190 DATA "Er is een kwadratische functie ontstaan"
25200 DATA "Er is geen evenwichtsvergelijking"
25210 DATA "Er is een negatieve Y ontstaan"
25220 DATA "Er is een negatieve R ontstaan"
25230 DATA "Er is geen Y aanwezig"
25240 DATA "Een noemer is nul geworden"
25250 DATA "Er is geen R aanwezig"
25260 DATA "Niet beide sectoren zijn aanwezig"
25270 DATA "Grafiek zit niet in het eerste kwadrant"
25280 DATA "Vergelijking bestaat (nog) niet"
32000 REM ****************************
32010 REM   KEYNESIAANSE MODEL (lang)
32020 REM 
32030 REM (C) Ronald van Vugt, PA3EAH
32040 REM 
32050 REM Postbus 160, 5580 AD, Waalre
32060 REM 
32070 REM       maart 1987, v1.4
32080 REM ****************************

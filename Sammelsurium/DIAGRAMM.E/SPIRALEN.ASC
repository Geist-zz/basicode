1000 A=200:GOTO 20:REM spiraalstelsel
1010 P1=3.141592653589
1020 REM 
1030 REM  ruimte voor tabellen
1040 REM 
1050 DIM SN(180),CS(180)
1060 PRINT
1070 PRINT"Dit programma tekent een"
1080 PRINT"spiraal-melkwegstelsel. In een"
1090 PRINT"dergelijk stelsel volgt het gas"
1100 PRINT"ellipsachtige banen, die op"
1110 PRINT"verschillende afstanden van het"
1120 PRINT"centrum verschillende standen hebben."
1130 PRINT"Waar de banen het dichtst bij elkaar"
1140 PRINT"komen, ontstaan sterren. De meest"
1150 PRINT"opvallende sterren markeren daar de"
1160 PRINT"twee armen van de spiraal."
1170 PRINT
1180 PRINT"Via twee invoergetallen geven we de"
1190 PRINT"vorm van het stelsel aan."
1200 PRINT"Drie andere invoergetallen bepalen"
1210 PRINT"de plaats vanwaar we het stelsel"
1220 PRINT"gaan bekijken."
1230 PRINT
1240 PRINT"cirkelafwijking (0.05 - 0.20) ";
1250 INPUT A
1260 REM 
1270 REM  binnenstraal buitenste kring
1280 REM 
1290 B=.8
1300 PRINT"verdraaiing per keer (5 - 30) ";
1310 INPUT T1
1320 T1=T1*P1/180
1330 C=(B+A*COS(P1/4+.5*T1)^2)/(B+A*COS(P1/4-.5*T1)^2)
1340 PRINT"kijkhoek vanaf pool (0-180) ";
1350 INPUT F
1360 PRINT"kijkhoek langs evenaar (0-360) ";
1370 INPUT T
1380 PRINT"afstand ( >1, bijv. 100) ";
1390 INPUT AF
1400 PRINT"even geduld aub"
1410 REM 
1420 REM  asmaat, aantal namaakpixels
1430 REM  basicode, omrekening naar
1440 REM  radialen, sinus en cosinus
1450 REM 
1460 AM=2:PX=.74:F=F*P1/180:T=T*P1/180
1470 FS=SIN(F):FC=COS(F)
1480 TS=SIN(T):TC=COS(T)
1490 REM 
1500 REM  vul de tabellen
1510 REM 
1520 FOR RO=0 TO 180
1530 CS(RO)=COS(2*RO*P1/180)*(B+A*COS(2*RO*P1/180)^2)
1540 SN(RO)=SIN(2*RO*P1/180)*(B+A*COS(2*RO*P1/180)^2)
1550 NEXT RO
1560 B=1:REM  verkleining
1570 GOSUB 600:REM  grafisch scherm
1580 CN=0:REM  voorgrondkleur
1590 REM 
1600 REM  maximaal aantal kringen
1610 REM 
1620 K9=100
1630 FOR KR=1 TO K9
1640 REM 
1650 REM  doorloop de kring
1660 REM 
1670 FOR RO=0 TO 180
1680 GOSUB 2050:REM  tekenen
1690 NEXT RO
1700 REM 
1710 REM  binnenstraal iets kleiner
1720 REM 
1730 B=B*C
1740 B=B-1/VG
1750 REM 
1760 REM  en iets verdraaid
1770 REM 
1780 T=T+T1
1790 TS=SIN(T):TC=COS(T)
1800 REM 
1810 REM  kern bereikt?
1820 REM 
1830 IF B<.2 THEN KR=K9+1
1840 REM 
1850 REM  volgende kring
1860 REM 
1870 NEXT KR
1880 REM 
1890 REM  wacht op toets
1900 REM 
1910 GOSUB 2220
1920 REM 
1930 REM  een nieuw stelsel ?
1940 REM 
1950 PRINT:PRINT
1960 PRINT"Wilt u nog een stelsel (J/N) ?";
1970 GOSUB 210
1980 IF IN=74 THEN 1000:REM Ja, opnieuw
1990 IF IN<>78 THEN 1970
2000 GOTO 950
2010 REM 
2020 REM  subroutine
2030 REM  teken een kring
2040 REM 
2050 X=CS(RO)*B
2060 Y=SN(RO)*B
2070 Z=0
2080 GOSUB 2150:REM  projekteer
2090 IF RO=0 THEN GOSUB 620:REM  punt
2100 GOSUB 630:REM  lijntje
2110 RETURN
2120 REM 
2130 REM  subroutine projektie
2140 REM 
2150 Q=AF-X*TC*FS-Y*TS*FS-Z*FC
2160 Q=AF/Q
2170 R=Q*PX/AM
2180 HO=(-X*TS+Y*TC)*R+.5
2190 VE=-(-X*TC*FC)*R*4/3+.5
2200 VE=VE-(-Y*TS*FC+Z*FS)*R*4/3
2210 RETURN
2220 REM 
2230 REM  wacht op afsluitende toets
2240 REM 
2250 HO=.8:VE=.9
2260 SR$="toets"
2270 GOSUB 650:REM  tekst op grafisch
2280 REM            scherm
2290 GOSUB 210:REM  wacht op toets
2300 GOSUB 100:REM  schoon tekstscherm
2310 RETURN
30000 REM 
30010 REM  literatuur:
30020 REM 
30030 REM  - over het Melkwegstelsel:
30040 REM 
30050 REM  Aarde en Kosmos,
30060 REM  4/1984,
30070 REM  blz. 334-339
30080 REM 
30090 REM  - over de tekenwijze:
30100 REM 
30110 REM  Aarde en Kosmos,
30120 REM  7/1985,
30130 REM  blz. 494.
30140 REM 
30150 REM 
32000 REM dit is een
32010 REM Basicode-3 programma
32020 REM van
32030 REM 
32040 REM Pim van Tend
32050 REM Veldheimweg 8
32060 REM 6871 CD  RENKUM
32070 REM 
32080 REM copyright (c) 1986
32090 REM 
32100 REM Nadere uitleg vindt u in:
32110 REM 
32120 REM AARDE & KOSMOS Informatica
32130 REM 1987-1
32140 REM 
32150 REM uitg: MENS & WETENSCHAP
32160 REM postbus 108
32170 REM 1270 AC Huizen
32180 REM 
32200 REM TROS-RADIO dd.870114
32210 REM gewijzigd  dd.870117

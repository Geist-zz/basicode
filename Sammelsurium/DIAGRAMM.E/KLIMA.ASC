1000 A=1000:GOTO 20:REM KLIMAATGRAFIEK
1010 REM INITIALISATIE ****************
1020 DIM MA$(12),NS(12),TE(12)
1030 FOR M=1 TO 12
1040 READ MA$(M)
1050 NEXT M
1060 REM
1070 REM UITLEG ***********************
1080 GOSUB 2260
1090 REM
1100 REM PLAATS INVOEREN **************
1110 GOSUB 100:HO=9:VE=0:GOSUB 110
1120 SR$="KLIMAGRAFIK":GOSUB 150
1130 HO=0:VE=2:GOSUB 110
1140 INPUT"Ort ";PL$
1150 HO=0:VE=4:GOSUB 110
1160 SR$="Correct (J/N)":GOSUB 150
1170 GOSUB 250:GOSUB 210
1180 IF IN=78 THEN 1130
1190 IF IN<>74 THEN 1170
1200 REM
1210 REM NEERSLAG/TEMP INVOEREN *******
1220 FOR X=1 TO 12
1230 GOSUB 100:HO=9:VE=0:GOSUB 110
1240 SR$="KLIMAGRAFIK":GOSUB 150
1250 HO=0:VE=2:GOSUB 110
1260 PRINT MA$(X):FOR S=1 TO LEN(MA$(X)):PRINT"-";:NEXT S
1270 HO=0:VE=4:GOSUB 110
1280 INPUT"Niederschlag (cm)   ";NS(X)
1290 IF NS(X)>40 THEN NS(X)=40
1300 IF NS(X)<0 THEN 1270
1310 INPUT"Temperatur (C) ";TE(X)
1320 IF TE(X)>40 THEN TE(X)=40
1330 PRINT
1340 REM CONTROLE *********************
1350 SR$="Correct (J/N)":GOSUB 150
1360 GOSUB 210
1370 IF IN=74 THEN 1400
1380 IF IN=78 THEN 1270
1390 GOSUB 250:GOTO 1360
1400 NEXT X
1410 REM
1420 REM OVERZICHT ********************
1430 GOSUB 100:HO=9:VE=0:GOSUB 110
1440 SR$="KLIMAGRAFIK":GOSUB 150
1450 HO=0:VE=2:GOSUB 110
1460 PRINT"Monat  Niederschlag Temperatur"
1470 PRINT"-----  ------------ ----------"
1480 FOR X=1 TO 12
1490 PRINT MA$(X);
1500 PRINT TAB(12);NS(X);
1510 PRINT TAB(23);TE(X)
1520 NEXT X
1530 REM
1540 REM CONTROLE *********************
1550 HO=0:VE=17:GOSUB 110
1560 SR$="Correct (J/N)":GOSUB 150
1570 GOSUB 250:GOSUB 210
1580 IF IN=74 THEN 1800
1590 IF IN<>78 THEN 1570
1600 REM
1610 HO=0:VE=17:GOSUB 110
1620 SR$="Monat (1-12)":GOSUB 150
1630 INPUT MA
1640 IF (MA<1) OR (MA>12) THEN 1610
1650 HO=0:VE=17:GOSUB 110
1660 PRINT MA$(MA);
1670 PRINT"                  "
1680 FOR X=1 TO LEN(MA$(MA))
1690 PRINT"-";
1700 NEXT X
1710 HO=0:VE=19:GOSUB 110
1720 INPUT"Niederschlag (cm)  ";NS(MA)
1730 IF NS(MA)>40 THEN NS(MA)=40
1740 IF NS(MA)<0 THEN 1710
1750 INPUT"Temperatur (C)";TE(MA)
1760 IF TE(MA)>40 THEN TE(MA)=40
1770 GOTO 1430
1780 REM
1790 REM GRAFIEK TEKENEN **************
1800 CN=0:GOSUB 600
1810 HO=0:VE=0:SR$=PL$
1820 GOSUB 650
1830 SR$=""
1840 FOR X=1 TO LEN(PL$)
1850 SR$=SR$+"-"
1860 NEXT X
1870 HO=0:VE=.03:GOSUB 650
1880 REM
1890 HO=.2:VE=.2:GOSUB 620
1900 VE=.8:GOSUB 630
1910 HO=.8:GOSUB 630
1920 VE=.2:GOSUB 630
1930 REM
1940 FOR X=0 TO 30 STEP 10
1950 HO=.13:VE=.8-X*.02
1960 SR=X:GOSUB 300:GOSUB 650
1970 HO=.82:VE=.8-X*.02
1980 GOSUB 650:NEXT X
1990 REM
2000 FOR X=1 TO 12
2010 HO=.17+.05*X:VE=.85
2020 SR$=LEFT$(MA$(X),1)
2030 GOSUB 650
2040 NEXT X
2050 REM
2060 FOR X=1 TO 12
2070 HO=.15+.05*X:VE=.8:GOSUB 620
2080 VE=.8-NS(X)/50:GOSUB 630
2090 HO=HO+.05:VE=.8-NS(X)/50
2100 GOSUB 630
2110 VE=.8:GOSUB 630
2120 NEXT X
2130 REM
2140 HO=.2:VE=.8-TE(12)/50:GOSUB 620
2150 FOR X=1 TO 12
2160 HO=.175+X*.05:VE=.8-TE(X)/50
2170 GOSUB 630
2180 NEXT X
2190 HO=.8:VE=.8-TE(1)/50:GOSUB 630
2200 GOSUB 210
2210 IF IN=80 THEN GOSUB 20030
2220 IF IN=83 THEN 950
2230 GOTO 1430
2240 REM
2250 REM UITLEG ***********************
2260 GOSUB 100:HO=9:VE=0:GOSUB 110
2270 SR$="KLIMAGRAFIK":GOSUB 150
2280 HO=0:VE=2:GOSUB 110
2290 PRINT"Mit dem Programm koennen Sie fuer"
2300 PRINT"einen Ort eine Klimauebersicht"
2310 PRINT"erstellen. Dazu sind fuer die zwoelf"
2320 PRINT"Monate die mittlere Niederschlagsmenge"
2330 PRINT"und Temperatur einzugeben."
2340 PRINT"Danach erfolgt eien Tabellenuebersicht"
2350 PRINT"und eine Grafik kann erzeugt werden."
2360 PRINT"An dieser Stelle kann das Programm mit"
2370 PRINT" (S) - abgebrochen oder die Grafik mit"
2380 PRINT" (P) - ausgedruckt werden."
2390 PRINT
2400 PRINT"Die Druckroutine muss aber von Ihnen"
2410 PRINT"selbst geschrieben werden (ab Z. 20000)"
2420 PRINT
2430 SR$="Taste":GOSUB 150:GOSUB 210
2440 RETURN
20000 REM PRINTEN
20010 REM PROGRAMMEER HIER ZELF
20020 REM EEN SCREENDUMP ROUTINE
20030 RETURN
20040 REM
25000 DATA "Januar","Februar","Maerz"
25010 DATA "April","Mai","Juni"
25020 DATA "Juli","August","September"
25030 DATA "Oktober","November","Dezember"
30000 REM ****************************
30010 REM *     KLIMAATGRAFIEK       *
30020 REM *  (c) JBSoft, Alkmaar     *
30030 REM ****************************
30040 REM
32000 REM Gemaakt door:
32010 REM Jan Benjamin
32020 REM Stationstraat 26
32030 REM 1815 TC ALKMAAR
32040 REM (072-123809)
32050 REM Op een:
32060 REM Philips VG8020 MSX-1 Computer
32070 REM ****************************
32080 REM
32090 REM TROS-RADIO dd 891220
32100 REM
32110 REM Erstausstrahlung: Deutschlandsender Kultur
32120 REM -spezial 910206

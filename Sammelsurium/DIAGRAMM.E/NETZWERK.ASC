1000 A=100:GOTO 20:REM netwerken
1010 DIM X(27),Y(27),VE(7),HO(7),NM$(7)
1020 I=0:VH=0.05:P1=3.14159
1030 S=0:H=HO+1:V=VE+1
1040 FOR J=0 TO 7:READ NM$(J):NEXT J
1050 READ X,Y:IF X=-1 THEN 1070
1060 X(I)=X:Y(I)=Y:I=I+1:GOTO 1050
1070 CT=7:CN=0:SA=0:TE=0
1080 SR$="PASSIEVE NETWERKEN":GOSUB 5060
1090 PRINT"1. T-verzwakker"
1100 PRINT:PRINT"   2. T-filter"
1110 PRINT:PRINT"   3. PI-verzwakker"
1120 PRINT:PRINT"   4. PI-filter"
1130 PRINT:PRINT"   5. Uitleg"
1140 PRINT:PRINT"   6. Stoppen"
1150 Y=V-5:GOSUB 5010
1160 IF KE=6 THEN 950
1170 IF (KE<1) OR (KE>5) THEN 1070
1180 M1=KE:ON KE GOSUB 1310,1410,1510,1610,4010
1190 GOTO 1070
1300 REM T-verzwakker
1310 SR$="T-verzwakker":GOSUB 5060
1320 GOSUB 5100:Y=10:GOSUB 5010:GOSUB 1700
1360 RETURN
1400 REM T-filter
1410 SR$="T-filter":GOSUB 5060
1420 GOSUB 5100:Y=10:GOSUB 5010:GOSUB 1900
1460 RETURN
1500 REM PI-verzwakker
1510 SR$="PI-verzwakker":GOSUB 5060
1520 GOSUB 5100:Y=10:GOSUB 5010:GOSUB 2200
1560 RETURN
1600 REM PI-filter
1610 SR$="PI-filter":GOSUB 5060
1620 GOSUB 5100:Y=10:GOSUB 5010:GOSUB 2400
1660 RETURN
1700 REM T-verzwakker
1710 GOSUB 5140
1720 X=.5-1/3:Y=.15:P=0:A=5:GOSUB 3010:GOSUB 3070
1730 X=.5:GOSUB 3010:GOSUB 3070
1740 P=6:X=.5-1/60:Y=.15+1/50:GOSUB 3010:GOSUB 3070
1750 HO=.5-1/3:VE=.45+1/50:GOSUB 620:HO=.5+1/3
1760 GOSUB 630:SR$="in":HO=.15:VE=.3
1770 GOSUB 650:SR$="out":HO=.81:GOSUB 650
1780 SR$="R2":HO=.535:GOSUB 650
1790 SR$="R1":HO=.312:VE=.1:GOSUB 650
1800 IF KE=2 THEN SR$="R3"
1810 HO=.65:GOSUB 650
1820 GOTO 5170
1900 REM T-filter
1910 GOSUB 5140
1920 IF KE=2 THEN 2110
1930 X=.5-17/60:Y=.12:P=12:A=7:GOSUB 3010:GOSUB 3120
1940 X=.5:GOSUB 3010:GOSUB 3120
1950 Y=.15:GOSUB 3230
1960 HO=.217:GOSUB 620:HO=.784:GOSUB 630
1970 SR$="C1":HO=.34:VE=.07:GOSUB 650
1980 HO=.62:GOSUB 650:SR$="L1":HO=.533
1990 VE=.32:GOSUB 650:HO=.2:SR$="in":GOSUB 650
2000 SR$="out":HO=.76:GOSUB 650:GOTO 5170
2100 REM laag. T-filter
2110 X=.13:Y=.2:GOSUB 3160:X=.5:GOSUB 3160
2120 P=20:A=7:X=.47:Y=.2:GOSUB 3010:GOSUB 3120
2130 HO=.13:VE=.429:GOSUB 620:HO=.87:GOSUB 630
2140 SR$="L1":HO=.295:VE=.11:GOSUB 650:HO=.665
2150 GOSUB 650:VE=.3:HO=.55:SR$="C1":GOSUB 650
2160 HO=.11:SR$="in":GOSUB 650:HO=.85
2170 SR$="out":GOSUB 650:GOTO 5170
2200 REM PI-verzwakker
2210 GOSUB 5140:X=1/3:Y=.15:P=0:A=5:GOSUB 3010
2220 GOSUB 3070:Y=Y+1/50:X=X-1/60:P=6:GOSUB 3010
2230 GOSUB 3070:X=.65:GOSUB 3010:GOSUB 3070
2240 VE=.17:HO=1/3:GOSUB 620:HO=.2:GOSUB 630
2250 HO=2/3:GOSUB 620:HO=.79:GOSUB 630
2260 VE=VE+.3:HO=.2:GOSUB 620:HO=.79:GOSUB 630
2270 SR$="in":VE=.3:HO=.19:GOSUB 650:SR$="out"
2280 HO=.78:GOSUB 650:SR$="R1":HO=.37:GOSUB 650
2290 HO=.585:IF KE=2 THEN SR$="R3"
2300 GOSUB 650:VE=.1:HO=.48:SR$="R2":GOSUB 650
2310 GOTO 5170
2400 REM PI-filter
2410 GOSUB 5140:IF KE=2 THEN 2600
2420 Y=.12:X=.36:P=12:A=7:GOSUB 3010:GOSUB 3110
2430 Y=.15:GOSUB 3230:X=.642:Y=.15:GOSUB 3230
2440 HO=.23:GOSUB 620:HO=.77:GOSUB 630
2450 HO=.23:VE=.15:GOSUB 620:HO=.36:GOSUB 630
2460 HO=.64:GOSUB 620:HO=.77:GOSUB 630
2470 SR$="C1":HO=.48:VE=.07:GOSUB 650:HO=.4
2480 SR$="L1":VE=.32:GOSUB 650:HO=.58
2490 GOSUB 650:HO=.21:SR$="in":GOSUB 650
2500 HO=.75:SR$="out":GOSUB 650:GOTO 5170
2600 REM laag. PI-filter
2610 X=.315:Y=.2:GOSUB 3160:P=20:A=7:X=.287:Y=.2
2620 GOSUB 3010:GOSUB 3120:X=.657:GOSUB 3010:GOSUB 3120
2630 HO=.19:VE=.2:GOSUB 620:HO=.315:GOSUB 630
2640 HO=.685:GOSUB 620:HO=.81:GOSUB 630
2650 HO=.19:VE=.429:GOSUB 620:HO=.81:GOSUB 630
2660 SR$="L1":HO=.48:VE=.12:GOSUB 650
2670 SR$="C1":HO=.36:VE=.3:GOSUB 650
2680 HO=.59:GOSUB 650:SR$="in":HO=.17:GOSUB 650
2690 HO=.79:SR$="out":GOSUB 650:GOTO 5170
3000 REM naar goede positie
3010 FOR I=0 TO A
3020 HO(I)=X(I+P)+X:VE(I)=Y(I+P)+Y
3030 NEXT I
3040 HO=HO(0):VE=VE(0):GOSUB 620
3050 HO=HO(1):VE=VE(1):GOSUB 630:RETURN
3060 REM weerstand
3070 HO=HO(2):VE=VE(2):GOSUB 620:VE=VE(3):GOSUB 630
3080 HO=HO(3):GOSUB 630:VE=VE(2):GOSUB 630
3090 HO=HO(2):GOSUB 630:HO=HO(4):VE=VE(4):GOSUB 620
3100 HO=HO(5):VE=VE(5):GOSUB 630:RETURN
3110 REM Condensator
3120 FOR I=2 TO 6 STEP 2
3130 HO=HO(I):VE=VE(I):GOSUB 620
3140 HO=HO(I+1):VE=VE(I+1):GOSUB 630:NEXT I:RETURN
3150 REM liggende spoel
3160 HO=X:VE=Y:GOSUB 620:HO=HO+1/8:X=HO+1/50:GOSUB 630
3170 RX=1/50:RY=1/30:FOR I=1 TO 3
3180 FOR J=-P1 TO 0 STEP 1/8
3190 HO=RX*COS(J)+X:VE=RY*SIN(J)+Y:GOSUB 620
3200 NEXT J:X=X+2*RX:NEXT I
3210 HO=X-RX:VE=Y:GOSUB 620:HO=HO+1/8:GOSUB 630:RETURN
3220 REM staande spoel
3230 HO=X:VE=Y:GOSUB 620:VE=VE+1/10:GOSUB 630:Y=VE+1/38
3240 RX=1/50:RY=1/38:FOR I=1 TO 3
3250 FOR J=-P1/2 TO P1/2 STEP 1/7
3260 HO=RX*COS(J)+X:VE=RY*SIN(J)+Y:GOSUB 620:NEXT J
3270 Y=Y+2*RY:NEXT I
3280 HO=X:VE=Y-RY:GOSUB 620:VE=VE+1/10:GOSUB 630:RETURN
4000 REM uitleg
4010 SR$="PASSIEVE FILTERS":SA=1:GOSUB 5060:PRINT
4020 PRINT:PRINT"Met dit programma kunt U zelf passieve"
4030 PRINT"filters ontwerpen, of de eigenschappen"
4040 PRINT"ervan bepalen."
4050 PRINT"U kiest eerst het gewenste filter,"
4060 PRINT"hierna wordt het filter getekend en"
4070 PRINT"wordt U gevraagd 'C' of 'O' in te"
4080 PRINT"drukken. Als U 'C' indrukt moet U de"
4090 PRINT"eigenschappen van het filter intikken"
4100 PRINT"en worden de waarden van de componen-"
4110 PRINT"ten berekend."
4120 PRINT"Als U 'O' intikt, moet U de waarden"
4130 PRINT"van de componenten intikken en worden"
4140 PRINT"de eigenschappen berekend."
4150 PRINT"Weerstanden worden altijd in ohm, con-"
4160 PRINT"densators in nF en indukties in mH"
4170 PRINT"in- en uitgevoerd."
4180 PRINT"Als de demping bij filters wordt be-"
4190 PRINT"rekend, wordt er weer terug gesprongen"
4200 PRINT"naar het tekstscherm, zodat het schema"
4210 PRINT"verdwijnt.":PRINT
4220 PRINT"VEEL PLEZIER MET DIT PROGRAMMA !!! =>";
4230 GOSUB 210:RETURN
4240 GOTO 4240
5000 REM keuze
5010 VE=Y:HO=3:GOSUB 110:PRINT"Geef uw keuze ";
5020 INPUT KE:IF Y<>10 THEN RETURN
5030 IF (KE<1) OR (KE>2) THEN 5010
5040 RETURN
5050 REM cls, reverse, centreren, cursor plaatsen
5060 GOSUB 100:VE=0:HO=H/2-LEN(SR$)/2-3:GOSUB 110:GOSUB 150
5070 IF SA<>0 THEN RETURN
5080 VE=5:HO=3:GOSUB 110:RETURN
5090 REM keuze filter printen
5100 I=KE*2-2
5110 PRINT"1. ";NM$(I):PRINT:PRINT"   2. ";NM$(I+1)
5120 RETURN
5130 REM soort filter op hi-res
5140 SR$=NM$(2*M1-3+KE):HO=.2:VE=0
5150 GOSUB 600:GOSUB 650:RETURN
5160 REM invoer verzwakker
5170 SR$="(C)omponenten of (O)verdracht":HO=0:VE=.55
5180 GOSUB 650:FL=0
5190 GOSUB 200:IF (IN<>67) AND (IN<>79) THEN 5190
5200 CN=1:GOSUB 650:CN=0:IF INT(M1/2)=M1/2 THEN 5660
5210 IF IN=67 THEN 5460
5220 SR$="R1 in ohm:":GOSUB 7010
5230 R1=H1:SR$="R2 in ohm:":VE=VE+VH:GOSUB 7010:R2=H1
5240 IF KE=1 THEN 5260
5250 SR$="R3 in ohm:":VE=VE+VH:GOSUB 7010:R3=H1
5260 ON (2*M1-2+KE) GOSUB 5290,5320,1,1,5360,5400
5270 GOTO 7130
5280 REM sym T-verzwakker
5290 H1=(R1*R1+2*R1*R2)/(R1+R2):H2=R1+R2
5300 RI=SQR(H1*H2):H3=SQR(H1/H2)
5310 H4=LOG((1+H3)/(1-H3))/2:D=20*H4/LOG(10):RETURN
5320 REM a-sym T-verzwakker
5330 H1=R1+R2:H2=R2+R3:H3=R1+R2*R3/H2:H4=R3+R1*R2/H1
5340 RI=SQR(H1*H3):RO=SQR(H2*H4):H5=SQR(H3/H1)
5350 D=(20*LOG((1+H5)/(1-H5))/2)/LOG(10):RETURN
5360 REM sym PI-verzwakker
5370 H1=R1*R2/(R1+R2):H2=(R1*R1+R1*R2)/(2*R1+R2)
5380 RI=SQR(H1*H2):H3=SQR(H1/H2)
5390 D=20*(LOG((1+H3)/(1-H3))/2)/LOG(10):RETURN
5400 REM a-sym PI-verzwakker
5410 H1=(R1*(R2+R3))/(R1+R2+R3):H2=(R3*(R1+R2))/(R1+R2+R3)
5420 H3=(R1*R2)/(R1+R2):H4=(R2*R3)/(R2+R3)
5430 RI=SQR(H1*H3):RO=SQR(H2*H4):H5=SQR(H4/H2)
5440 D=20*(LOG((1+H5)/(1-H5))/2)/LOG(10):RETURN
5450 REM componenten berekenen
5460 SR$="Ingangs-R:":GOSUB 7010:RI=H1
5470 IF KE=1 THEN 5490
5480 SR$="Uitgangs-R:":VE=VE+VH:GOSUB 7010:RO=H1
5490 SR$="Demping:":VE=VE+VH:GOSUB 7010:D=H1
5500 ON (2*M1-2+KE) GOSUB 5520,5550,1,1,5580,5610
5510 GOTO 7220
5520 REM sym T-verzwakker
5530 H1=D*LOG(10)/20:GOSUB 6040:R2=RI/H2:H1=H1/2
5540 GOSUB 6010:R1=RI*H2:RETURN
5550 REM a-sym T-verzwakker
5560 H1=D*LOG(10)/20:GOSUB 6040:R2=SQR(RI*RO)/H2
5570 GOSUB 6010:R1=RI/H2-R2:R3=RO/H2-R2:RETURN
5580 REM sym PI-verzwakker
5590 H1=D*LOG(10)/20:GOSUB 6040:R2=RI*H2:H1=H1/2
5600 GOSUB 6010:R1=RI/H2:RETURN
5610 REM asym PI-verzwakker
5620 H1=D*LOG(10)/20:GOSUB 6040:R2=SQR(RI*RO)*H2
5630 GOSUB 6010:R1=1/(1/(RI*H2)-1/R2)
5640 R3=1/(1/(RO*H2)-1/R2):RETURN
5650 REM invoer filters
5660 IF IN=67 THEN 5760
5670 SR$="L1 in mH:":GOSUB 7010:L=H1
5680 VE=VE+VH:SR$="C1 in nF:":GOSUB 7010:C=H1
5690 C=C*10^-9:L=L*.001
5700 ON (2*M1-4+KE) GOSUB 5720,5730,1,1,5740,5750
5710 F=H1/(2*P1):GOTO 7290
5720 H1=1/(SQR(2*L*C)):R=.6*SQR(2*L/C):RETURN
5730 H1=1/(SQR(L*C/2)):R=.6*SQR(2*L/C):RETURN
5740 H1=1/(SQR(2*L*C)):R=(SQR(.5*L/C))/.6:RETURN
5750 H1=1/(SQR(L*C/2)):R=(SQR(.5*L/C))/.6:RETURN
5760 REM componenten berekenen
5770 SR$="Afsluit-R:":GOSUB 7010:R=H1
5780 VE=VE+VH:SR$="Grensfreq:":GOSUB 7010:F=H1
5790 ON (2*M1-4+KE) GOSUB 5810,5820,1,1,5830,5840
5800 L=L*1000:C=C*10^9:GOTO 7340
5810 H1=R/.6:L=H1/(4*P1*F):C=1/(H1*2*P1*F):RETURN
5820 H1=R/.6:L=H1/(2*P1*F):C=2/(H1*2*P1*F):RETURN
5830 H1=R*.6:L=H1/(2*P1*F):C=1/(H1*4*P1*F):RETURN
5840 H1=R*.6:L=2*H1/(2*P1*F):C=1/(H1*2*P1*F):RETURN
6000 REM berekenen TANH
6010 H2=(EXP(H1)-EXP(-H1))/(EXP(H1)+EXP(-H1))
6020 RETURN
6030 REM berekenen SINH
6040 H2=(EXP(H1)-EXP(-H1))/2:RETURN
7000 REM invoer hi-res
7010 HO=0:GOSUB 650:SR$=SR$+" ":H$=""
7020 GOSUB 200:IF IN$="" THEN 7020
7030 IF IN=13 THEN 7070
7040 IF IN=127 THEN 7080
7050 IF ((IN<48) OR (IN>57)) AND (IN<>46) THEN 7020
7060 H$=H$+IN$:SR$=SR$+IN$:GOSUB 650:GOTO 7020
7070 H1=VAL(H$):HO=0:RETURN
7080 IF H$="" THEN 7020
7090 CN=1:GOSUB 650:CN=0:H$=LEFT$(H$,LEN(H$)-1)
7100 SR$=LEFT$(SR$,LEN(SR$)-1):GOSUB 650
7110 GOTO 7020
7120 REM uitvoer overdrachtseigenschappen
7130 VE=VE+2*VH:HO=0:H$="Ingangs-R: "
7140 SR=RI:CN=1:GOSUB 310:CN=0:SR$=H$+SR$+" ohm"
7150 GOSUB 650:VE=VE+VH
7160 IF KE=1 THEN 7190
7170 H$="Uitgangs-R: ":CN=1:SR=RO:GOSUB 310
7180 CN=0:SR$=H$+SR$+" ohm":GOSUB 650:VE=VE+VH
7190 H$="Demping:   ":SR=D:CN=1:GOSUB 310:CN=0
7200 SR$=H$+SR$+" dB":GOSUB 650:GOTO 8070
7210 REM Uitvoer componenten
7220 VE=VE+2*VH:H$="R1: ":CN=1:SR=R1:GOSUB 310:CN=0
7230 SR$=H$+SR$+" ohm":GOSUB 650:VE=VE+VH
7240 H$="R2: ":CN=1:SR=R2:GOSUB 310:CN=0
7250 SR$=H$+SR$+" ohm":GOSUB 650:IF KE=1 THEN 8070
7260 H$="R3: ":VE=VE+VH:CN=1:SR=R3:GOSUB 310:CN=0
7270 SR$=H$+SR$+" ohm":GOSUB 650:GOTO 8070
7280 REM filters uitvoer
7290 VE=VE+2*VH:HO=0:H$="Afsluit-R: ":SR=R
7300 CN=1:GOSUB 310:GOSUB 310:CN=0:SR$=H$+SR$+" ohm"
7310 GOSUB 650:VE=VE+VH:H$="Grensfreq: ":CN=1:SR=F
7320 GOSUB 310:CN=0:SR$=H$+SR$+" Hz":GOSUB 650:GOTO 7390
7330 REM uitvoer filter componenten
7340 HO=0:VE=VE+2*VH:H$="L1: ":CN=1:SR=L:GOSUB 310
7350 CN=0:SR$=H$+SR$+" mH":GOSUB 650:VE=VE+VH
7360 H$="C1: ":CN=1:SR=C:GOSUB 310:CN=0
7370 SR$=H$+SR$+" nF":GOSUB 650
7380 REM demping
7390 VE=VE+2*VH:HO=0
7400 SR$="Demping berekenen? (j/n)":GOSUB 650
7410 GOSUB 200:IF IN$="" THEN 7410
7420 IF IN=78 THEN RETURN
7430 IF IN<>74 THEN 7410
7440 GOSUB 100:SR$=NM$(2*M1-3+KE):PRINT SR$:PRINT:PRINT
7450 FL=0:TE=0:PRINT"Laagste frequentie: ";:INPUT LF
7460 PRINT"Hoogste frequentie: ";:INPUT HF
7470 PRINT"Stapgrootte: ";:INPUT SA
7480 PRINT"Uitvoer op de printer (j/n) ";
7490 GOSUB 210:IF (IN<>74) AND (IN<>78) THEN 7490
7500 IF IN=74 THEN FL=1
7510 GOSUB 8010:CN=1:CT=9:T=4
7520 FOR I=LF TO HF STEP SA
7530 FR=F/I:IF KE=2 THEN FR=1/FR
7540 GOSUB 7620:SR=I:GOSUB 310:H1$=SR$
7550 SR=D:GOSUB 310:SR$=H1$+"       "+SR$
7560 IF FL=1 THEN GOSUB 350:GOSUB 360
7570 PRINT SR$:T=T+1:IF T=V-3 THEN GOSUB 7590
7580 NEXT I:GOSUB 7610:RETURN
7590 REM scherm vol
7600 T=4:GOSUB 7610:GOSUB 8000:RETURN
7610 VE=V-2:HO=H-4:GOSUB 110:PRINT"=>";:GOSUB 210:RETURN
7620 REM demping berekenen
7630 H1=1-2*FR*FR:H1=H1*H1:H2=FR*(1+2.778*(1-FR*FR))
7640 H2=H2*H2/2.778:D=10*LOG(H1+H2)/LOG(10):RETURN
8000 REM kop printen
8010 GOSUB 100:SR$=NM$(2*M1-3+KE):GOSUB 5060:PRINT:PRINT
8020 IF (FL=1) AND (TE=0) THEN GOSUB 350:GOSUB 360:GOSUB 360
8030 SR$=" Frequentie         Demping"
8040 IF (FL=1) AND (TE=0) THEN GOSUB 350:GOSUB 360:GOSUB 360
8050 PRINT SR$:PRINT:TE=1:RETURN
8060 REM toets
8070 SR$="=>":HO=.95:VE=.93:GOSUB 650
8080 GOSUB 200:IF IN$="" THEN 8080
8090 RETURN
8100 REM ohm printen
25000 DATA "Symmetrische T-verzwakker"
25010 DATA "A-symmetrische T-verzwakker"
25020 DATA "Hoogdoorlaat T-filter","Laagdoorlaat T-filter"
25030 DATA "Symmetrische PI-verzwakker"
25040 DATA "A-symmetrische PI-verzwakker"
25050 DATA "Hoogdoorlaat PI-filter"
25060 DATA "Laagdoorlaat PI-filter"
25070 DATA 0,.02,.125,.02,.125,0,.2083333,.04
25080 DATA .2083333,.02,.3333333,.02
25090 REM liggende weerstand
25100 DATA .0166666,0,.0166666,.1,0,.1,.03333333,.2
25110 DATA .0166666,.2,.0166666,.3
25120 REM staande weerstand
25130 DATA 0,.033333,.125,.033333,.125,0,.125,.066666
25140 DATA .158333,0,.158333,.066666,.158333
25150 DATA .033333,.2833333,.0333333
25160 REM liggende condensator
25170 DATA .0277777,0,.0277777,.1,0,.1,.055555,.1
25180 DATA 0,.128571,.055555,.128571,.0277777
25190 DATA .128571,.0277777,.228571
25200 REM staande condensator
25210 DATA -1,1986
32000 REM ++++++++++++++++++++++++++++++
32010 REM (C) Ronald van Vugt, PA3EAH
32020 REM      postbus 160, 5580 AD,
32030 REM 
32040 REM Gemaakt op een ACORN ELECTRON
32050 REM 
32060 REM        jan 1987, V1.2
32070 REM ++++++++++++++++++++++++++++++
32100 REM TROS-radio dd.870121

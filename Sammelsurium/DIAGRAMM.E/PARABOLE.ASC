1000 A=200:GOTO 20:REM  PARABOLEN
1010 DIM F$(9),K$(9),A(9),B(9),C(9)
1020 GOSUB 1500:REM --inleiding --
1030 TW=0:FT=0
1040 FP$="":KP$="":FQ$="":KQ$=""
1050 GOSUB 2000:REM --input parameters
1060 GOSUB 2500:REM --maak functie$ --
1070 GOSUB 3000:REM --NOG 'n functie?-
1080 IF TW=1 THEN TW=0:GOTO 1050
1090 GOSUB 3200:REM --teken functie --
1100 GOSUB 3490:REM --nog een keer? --
1110 IF IN=74 THEN 1030
1120 GOTO 950
1500 REM === INLEIDING ===
1510 GOSUB 100
1520 PRINT:PRINT" P A R A B O L E N"
1530 PRINT" =================":PRINT
1540 PRINT" Dit programma tekent de grafiek":PRINT
1550 PRINT" van de functie":PRINT
1560 PRINT"       2"
1570 PRINT" Y = aX  + bX + c":PRINT
1580 PRINT" Als je wilt zien wat a, b en c":PRINT
1590 PRINT" voor invloed hebben op de vorm":PRINT
1600 PRINT" van de grafiek, dan is dit een":PRINT
1610 PRINT" programma om uitgebreid mee te":PRINT
1620 PRINT" experimenteren.":PRINT
1630 PRINT"---> TIK OP 'N TOETS";
1640 GOSUB 210:RETURN
2000 REM === INPUT PARAMETERS ===
2010 GOSUB 100
2020 PRINT"Het programma tekent de functie":PRINT
2030 PRINT"         2"
2040 PRINT"   Y = ax  + bx + c":PRINT
2050 PRINT"   (a, b of c mogen ook =0 zijn)":PRINT
2060 PRINT"   (decimale punt of breukstreep MAG)":PRINT
2070 PRINT:PRINT"Geef de waarden van a, b en c)"
2080 PRINT:PRINT"   a = ";:INPUT A$
2090 P$=A$:GOSUB 3100:A=P:A(FT)=P
2100 PRINT:PRINT"   b = ";:INPUT B$
2110 P$=B$:GOSUB 3100:B=P:B(FT)=P
2120 PRINT:PRINT"   c = ";:INPUT C$
2130 P$=C$:GOSUB 3100:C=P:C(FT)=P
2140 RETURN
2500 REM === MAAK FUNCTIESTRING ===
2510 XX=1:F$="Y = "
2520 IF A=0 THEN XX=0:GOTO 2560
2530 IF A=1 THEN F$=F$+" X":GOTO 2560
2540 IF A=-1 THEN F$=F$+"- X":GOTO 2560
2550 F$=F$+A$+" X"
2560 G$="":IF B=0 THEN 2610
2570 IF B=1 THEN G$=" + X":GOTO 2610
2580 IF B=-1 THEN G$=" - X":GOTO 2610
2590 IF B<0 THEN G$=" "+B$+" X":GOTO 2610
2600 IF B>0 THEN G$=" + "+B$+" X"
2610 IF (A=0) AND (B>0) THEN L=LEN(G$):G$=RIGHT$(G$,L-2)
2620 IF C=0 THEN 2650
2630 IF C>0 THEN G$=G$+" + "+C$:GOTO 2650
2640 IF C<0 THEN G$=G$+" "+C$
2650 IF (A=0) AND (B=0) AND (C>0) THEN G$=RIGHT$(G$,LEN(G$)-2)
2660 L=LEN(F$):KW$=""
2670 IF XX=1 THEN KW$=RIGHT$("          2",L+1)
2680 IF (A=0) AND (B=0) AND (C=0) THEN G$=" 0"
2690 F$=F$+G$
2700 F$(FT)=F$:K$(FT)=KW$:FT=FT+1
2710 PRINT
2720 PRINT"   ";KW$
2730 PRINT"   ";F$:RETURN
3000 REM === 2e FUNCTIE? ===
3010 IF FT=10 THEN 3050
3020 TW=0
3030 PRINT:PRINT" NOG een functie invoeren?";:GOSUB 210
3040 IF IN=74 THEN TW=1
3050 RETURN
3100 REM === VAL(PARAMETER$) ===
3110 IF P$="" THEN P=0:RETURN
3120 L=LEN(P$)
3130 BR=0:FOR N=1 TO L
3140 IF MID$(P$,N,1)="/" THEN BR=N
3150 NEXT N
3160 IF BR=0 THEN P=VAL(P$):RETURN
3170 TL$=LEFT$(P$,BR-1):TL=VAL(TL$)
3180 NM$=RIGHT$(P$,L-BR):NM=VAL(NM$)
3190 P=(TL/NM):RETURN
3200 REM === FUNCTIE(S) TEKENEN ===
3210 GOSUB 600:CN=0:ZK=0
3220 REM -- teken de assen --
3230 HO=0:VE=0.5:GOSUB 620
3240 HO=.999:VE=0.5:GOSUB 630
3250 HO=0.5:VE=0:GOSUB 620
3260 HO=0.5:VE=.999:GOSUB 630
3270 REM -- schaalverdeling --
3280 CN=1:VE=0.5
3290 FOR HO=0 TO .999 STEP 0.05
3300 GOSUB 620:NEXT HO
3310 HO=0.5
3320 FOR VE=0 TO .999 STEP 0.05
3330 GOSUB 620:NEXT VE
3340 FOR N=0 TO FT-1
3350 REM -- gegevens uit array --
3360 F$=F$(N):KW$=K$(N)
3370 A=A(N):B=B(N):C=C(N)
3380 REM -- print de functiestring --
3390 ZK=N*0.1
3400 CN=0:HO=0:VE=ZK:SR$=KW$:GOSUB 650
3410 HO=0:VE=ZK+0.04:SR$=F$:GOSUB 650
3420 REM -- teken de functie --
3430 FOR X=-10 TO 10 STEP 0.02
3440 Y=A*X*X+B*X+C:IF (Y>10) OR (Y<-10) THEN 3460
3450 HO=(X+10)/20:VE=1-((Y+10)/20):GOSUB 620
3460 NEXT X
3470 NEXT N
3480 RETURN
3490 REM === NOG EEN KEER? ===
3500 HO=0.52:VE=0.95:SR$="Nog een keer?":GOSUB 650
3510 GOSUB 210:RETURN
30000 REM ==============================
30010 REM GEMAAKT OP EEN BBC B COMPUTER
30020 REM 11 FEBRUARI 1987
30030 REM DOOR
32000 REM Ch.W.Brederode
32010 REM Von Bonninghausenlaan 33
32020 REM 2161 ES  LISSE
32100 REM 
32110 REM TROS-RADIO dd 870325
32120 REM incl corr. dd 870822

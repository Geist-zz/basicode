1000 A=100:GOTO 20:REM Roof/Prooidieren-cyclus
1010 HB=HO:VB=VE:UW=1-1/HG:P1=3.14159:MT=0:TM=0:IV=255
1020 IT=0:RA=0:RB=0:RC=0:RD=0:PA=0:PB=0:PC=0:PD=0
1030 DIM CO$(8)
1040 FOR I=0 TO 8:READ CO$(I):NEXT I
1050 GOSUB 100:GOSUB 9040:SR$="ROOF/PROOIDIEREN-CYCLUS"
1060 VE=2:HO=INT(HB/2-11)-3:GOSUB 110:GOSUB 150
1070 PRINT:PRINT:GOSUB 9040
1080 RG=RA+RB:RK=RA-RB:PG=PA+PB:PK=PA-PB:PR=1
1090 FOR VE=6 TO 14
1100 HO=HB/2-5:GOSUB 110:PRINT CO$(VE-6)
1110 NEXT VE
1120 PRINT:GOSUB 9040
1130 VE=18:HO=0:GOSUB 110
1140 PRINT"Maak uw keuze mbv. de cursortoetsen en"
1150 PRINT"bevestig uw keuze met de invoertoets."
1160 PRINT:GOSUB 9040
1170 VE=6:HO=HB/2-8:SR$=CO$(VE-6):GOSUB 110:GOSUB 150
1180 GOSUB 200:IF ((IN<28) OR (IN>31)) AND (IN<>13) THEN 1180
1190 IF IN=13 THEN 1280
1200 SR$="   "+CO$(VE-6)+"   ":GOSUB 110:PRINT SR$
1210 IF (IN=30) AND (VE=14) THEN VE=5
1220 IF IN=30 THEN VE=VE+1
1230 IF (IN=31) AND (VE=6) THEN VE=15
1240 IF IN=31 THEN VE=VE-1
1250 IF IN=28 THEN VE=6
1260 IF IN=29 THEN VE=14
1270 SR$=CO$(VE-6):GOSUB 110:GOSUB 150:GOTO 1180
1280 IF VE=14 THEN GOSUB 100:GOTO 950
1290 IF (VE<8) OR (IV=0) OR (VE=13) THEN 1330
1300 HO=INT(HB/2-17):VE=VB-2:GOSUB 110
1310 SR$="U heeft nog niets ingevoerd!":GOSUB 150
1320 GOSUB 9110:GOTO 1050
1330 ON VE-5 GOSUB 2010,3010,4010,5010,6010,7010,8010,8510
1340 IF (IN=83) AND (VE<>VB) THEN GOSUB 20000
1350 GOTO 1050
2000 REM uitleg
2010 SR$="PROOI/ROOFDIER-CYCLUS":GOSUB 9010
2020 PRINT:PRINT:PRINT"De wiskundige Volterra formuleerde,"
2030 PRINT"geinspireerd door de bioloog d'Ancona,"
2040 PRINT"al in 1931 een wiskundig model dat de"
2050 PRINT"wisselwerking beschrijft tussen prooi-"
2060 PRINT"en roofdieren."
2070 PRINT"De gedachtegang is dat een toename van"
2080 PRINT"het aantal roofdieren ervoor zorgt dat"
2090 PRINT"er meer prooidieren worden gevangen."
2100 PRINT"Hierdoor nemen echter de prooidieren"
2110 PRINT"in aantal af, zodat er te weinig voed-"
2120 PRINT"sel is voor de roofdieren en zij in"
2130 PRINT"aantal afnemen. Hierdoor nemen de"
2140 PRINT"prooidieren weer in aantal toe, er is"
2150 PRINT"weer voldoende voedsel zodat het aan-"
2160 PRINT"tal roofdieren weer toeneemt, enz."
2170 PRINT:PRINT"Uiteraard zijn er veel bezwaren tegen"
2180 PRINT"dit model ingebracht. Zo zal het vaak"
2190 PRINT"zijn dat roofdieren van meer dan een"
2200 PRINT"soort prooidieren leven. Verder zou"
2210 PRINT"het klimaat invloed kunnen hebben."
2220 GOSUB 9110:GOSUB 100
2230 PRINT"Als er echter geen rekening gehouden"
2240 PRINT"wordt met deze factoren, is deze cy-"
2250 PRINT"clus gemakkelijk wiskundig te be-"
2260 PRINT"schrijven door een functie op te"
2270 PRINT"stellen voor het aantal prooi- en"
2280 PRINT"roofdieren. Deze functie luidt als"
2290 PRINT"volgt: P(t)=A+B*sin C(t+D). Hierbij"
2300 PRINT"stelt 't' de tijd voor."
2310 PRINT:PRINT"Met dit programma kunt u zelf de"
2320 PRINT"functie samenstellen en dan gra-"
2330 PRINT"fisch bekijken."
2340 PRINT:PRINT"U kunt een screendumpprogramma"
2350 PRINT"plaatsen vanaf regel 20000. Als"
2360 PRINT"u op 'S' drukt als een grafiek"
2370 PRINT"is getekend, krijgt u een afdruk"
2380 PRINT"op papier."
2390 PRINT:PRINT"Er is een demo aanwezig, zodat"
2400 PRINT"u kunt zien wat de bedoeling is.":PRINT
2410 PRINT"VEEL PLEZIER MET DIT PROGRAMMA!"
2420 GOSUB 9110:RETURN
3000 REM invoer
3010 SR$="INVOER":GOSUB 9010:IV=0
3020 PRINT:PRINT"De roof/prooidierfunctie heeft als"
3030 PRINT"vorm: A+B*sin C(t+D), (t is de tijd)."
3040 PRINT:PRINT"Geef de parameters voor de roof-"
3050 PRINT"dierenfunctie."
3060 GOSUB 3130:RA=PA:RB=PB:RC=PC:RD=PD
3070 PRINT:PRINT"Geef de parameters voor de prooi-"
3080 PRINT"dierenfunctie.":GOSUB 3130
3090 PRINT:PRINT"Geef de minimale tijd: ";:INPUT MT
3100 PRINT"Geef de maximale tijd: ";:INPUT TM
3110 PRINT"Geef het tijdsinterval: ";:INPUT IT
3120 RETURN
3130 PRINT:PRINT"A=";:INPUT PA
3140 PRINT"B=";:INPUT PB
3150 PRINT"C=";:INPUT PC$:PC=VAL(PC$)
3160 FOR I=1 TO LEN(PC$):H$=MID$(PC$,I,2)
3170 IF (H$="PI") OR (H$="pi") THEN PC=PC*P1
3180 NEXT I
3190 PRINT"D=";:INPUT PD:RETURN
4000 REM wijzigen
4010 SR$="WIJZIGEN":GOSUB 9010
4020 PRINT:PRINT"Wat wilt u wijzigen:"
4030 PRINT:PRINT"Van de roofdierenfunctie:":PRINT
4040 PRINT" 1. A (nu ";RA;")":PRINT" 2. B (nu ";RB;")"
4050 PRINT" 3. C (nu ";RC;")":PRINT" 4. D (nu ";RD;")"
4060 PRINT:PRINT"Van de prooidierenfunctie:":PRINT
4070 PRINT" 5. A (nu ";PA;")":PRINT" 6. B (nu ";PB;")"
4080 PRINT" 7. C (nu ";PC;")":PRINT" 8. D (nu ";PD;")"
4090 PRINT:PRINT" 9. De minimale tijd (nu ";MT;")"
4100 PRINT"10. De maximale tijd (nu ";TM;")"
4110 PRINT"11. Het tijdsinterval (nu ";IT;")"
4120 PRINT:PRINT"Geef uw keuze: ";:INPUT KE
4130 IF KE=0 THEN RETURN
4140 IF (KE<1) OR (KE>11) THEN 4120
4150 IF KE>8 THEN 4170
4160 ON KE GOTO 4180,4210,4240,4300,4180,4210,4240,4300
4170 ON KE-8 GOTO 4330,4340,4350
4180 PRINT"Geef de nieuwe A:";:INPUT H
4190 IF KE=1 THEN RA=H:GOTO 4360
4200 PA=H:GOTO 4360
4210 PRINT"Geef de nieuwe B:";:INPUT H
4220 IF KE=2 THEN RB=H:GOTO 4360
4230 PB=H:GOTO 4360
4240 PRINT"Geef de nieuwe C:";:INPUT H$
4250 H=VAL(H$):FOR I=1 TO LEN(H$):PC$=MID$(H$,I,2)
4260 IF (PC$="PI") OR (PC$="pi") THEN H=H*P1
4270 NEXT I
4280 IF KE=3 THEN RC=H:GOTO 4360
4290 PC=H:GOTO 4360
4300 PRINT"Geef de nieuwe D:";:INPUT H
4310 IF KE=4 THEN RD=H:GOTO 4360
4320 PD=H:GOTO 4360
4330 PRINT"Geef de nieuwe minimale t:";:INPUT MT:GOTO 4360
4340 PRINT"Geef de nieuwe maximale t:";:INPUT TM:GOTO 4360
4350 PRINT"Geef de nieuwe tijdsinterval";:INPUT IT
4360 HO=0:VE=VB:GOSUB 110
4370 PRINT"Wilt u nog meer wijzigen (J/N)";:GOSUB 210
4380 IF IN=74 THEN 4010
4390 RETURN
5000 REM tabel
5010 SR$="TABEL":GOSUB 9010:IF PR=2 THEN 5130
5020 PRINT:PRINT"Wilt u de uitvoer op de printer (J/N)";
5030 GOSUB 210:IF IN=78 THEN PR=0:GOTO 5130
5040 SR$="De roofdierenfunctie is: ":GOSUB 350
5050 H1=RA:H2=RB:H3=RC:H4=RD:GOSUB 5080
5060 SR$="De prooidierenfunctie is: ":GOSUB 350
5070 H1=PA:H2=PB:H3=PC:H4=PD:GOSUB 5080:GOTO 5130
5080 SR=H1:GOSUB 300:H$=SR$+"+"
5090 SR=H2:GOSUB 300:H$=H$+SR$+"*sin ":SR=H3:GOSUB 300
5100 H$=H$+SR$+"(t":IF H4>=0 THEN H$=H$+"+"
5110 SR=H4:GOSUB 300:H$=H$+SR$+")":SR$=H$
5120 GOSUB 350:GOSUB 360:RETURN
5130 HO=0:VE=2:GOSUB 110
5140 PR$="       Tijd    Prooidier   Roofdier    ":SR$=PR$
5150 IF PR=1 THEN GOSUB 360:GOSUB 350:GOSUB 360
5160 PRINT SR$:SP$="       ----------------------------"
5170 SR$=SP$:IF PR=1 THEN GOSUB 350:GOSUB 360
5180 PRINT SR$:RT=2
5190 FOR T=MT TO TM STEP IT
5200 SR=T:CT=4:CN=1:GOSUB 310:H$="       "+SR$+"     "
5210 GOSUB 9140:CN=0:CT=6
5220 RT=RT+1:IF (RT<VB-1) OR (PR=1) THEN 5240
5230 GOSUB 9110:GOSUB 100:PRINT PR$:PRINT SP$:RT=1
5240 SR=X:GOSUB 310:H$=H$+SR$:SR=Y:GOSUB 310
5250 SR$=H$+"     "+SR$:PRINT SR$
5260 IF PR=1 THEN GOSUB 350:GOSUB 360
5270 NEXT T
5280 GOSUB 9110:RETURN
6000 REM parameter
6010 GOSUB 600:CN=0
6020 HO=.01:VE=0:GOSUB 620:VE=.91:GOSUB 630
6030 HO=UW:GOSUB 630
6040 FOR VE=0 TO .91 STEP .182
6050 HO=0:GOSUB 620:HO=.02:GOSUB 630
6060 NEXT VE
6070 FOR HO=UW TO .01 STEP -.1
6080 VE=.903:GOSUB 620:VE=.917:GOSUB 630
6090 NEXT HO
6100 SR$="0":VE=.94:HO=0:GOSUB 650
6110 SR=RG+INT(.1*RG):GOSUB 300:HO=.02:VE=0:GOSUB 650
6120 SR=PG+INT(.1*PG):GOSUB 300:HO=1-LEN(SR$)/32
6130 VE=.86:GOSUB 650:FOR T=MT TO TM STEP IT
6140 GOSUB 9140:H=X/(PG+.1*PG):V=1-Y/(RG+.1*RG)
6150 IF T=MT THEN HO=H:VE=V:GOSUB 620:X1=H:Y1=V:GOTO 6170
6160 HO=X1:VE=Y1:GOSUB 620:HO=H:VE=V:GOSUB 630:X1=H:Y1=V
6170 NEXT T
6180 SR$="Prooidier":HO=.75:VE=.94:GOSUB 650
6190 FOR T=1 TO 8
6200 SR$=MID$("Roofdier",T,1):VE=T/25+.05:HO=.03
6210 GOSUB 650:NEXT T
6220 GOSUB 9180:RETURN
7000 REM grafiek
7010 GOSUB 600:CN=0
7020 VE=0:HO=.01:GOSUB 620:VE=.91:GOSUB 630
7030 HO=.989:GOSUB 630:VE=0:GOSUB 630
7040 FOR VE=0 TO .91 STEP .185
7050 HO=0:GOSUB 620:HO=.02:GOSUB 630
7060 HO=.98:GOSUB 620:HO=UW:GOSUB 630
7070 NEXT VE
7080 FOR HO=.01 TO 1 STEP 1/((TM-MT)/IT)
7090 VE=.903:GOSUB 620:VE=.917:GOSUB 630
7100 NEXT HO
7110 SR=MT:GOSUB 300:HO=0:VE=.94:GOSUB 650
7120 SR=TM:GOSUB 300:HO=1-LEN(SR$)/32:GOSUB 650
7130 SR=PG:GOSUB 300:HO=.02:VE=0.02:GOSUB 650
7140 VE=.87:SR=PK:GOSUB 300:GOSUB 650
7150 SR=RG:GOSUB 300:VE=.02:HO=1-LEN(SR$)/32:GOSUB 650
7160 SR=RK:GOSUB 300:VE=.87:HO=1-LEN(SR$)/32:GOSUB 650
7170 FOR T=MT TO TM STEP IT
7180 H=(T-MT)*TM/(TM-MT)/(TM/.98)+.01
7190 GOSUB 9140
7200 V=.91-(X-PK)*PG/(PG-PK)/(PG/.91)
7210 IF T=MT THEN VE=V:HO=H:GOSUB 620:Y1=V:X1=H:GOTO 7230
7220 HO=X1:VE=Y1:GOSUB 620:HO=H:VE=V:GOSUB 630:Y1=V
7230 IF T=MT+10*IT THEN SR$="P":GOSUB 650
7240 V=.91-(Y-RK)*RG/(RG-RK)/(RG/.91)
7250 IF T=MT THEN VE=V:GOSUB 620:Y2=V:GOTO 7270
7260 HO=X1:VE=Y2:GOSUB 620:HO=H:VE=V:GOSUB 630:Y2=V:X1=H
7270 IF T=MT+7*IT THEN SR$="R":GOSUB 650
7280 NEXT T
7290 GOSUB 9180:RETURN
8000 REM combinatie
8010 GOSUB 600:CN=0
8020 HO=.5:VE=0:GOSUB 620:VE=UW:GOSUB 630
8030 HO=UW:VE=.5:GOSUB 620:HO=0:GOSUB 630
8040 FOR VE=0 TO 1 STEP .0999
8050 HO=.495:GOSUB 620:HO=.507:GOSUB 630:NEXT VE
8060 FOR HO=0 TO 1 STEP .0999
8070 VE=.495:GOSUB 620:VE=.505:GOSUB 630:NEXT HO
8080 SR=RG:GOSUB 300:HO=.5-LEN(SR$)/32:VE=0:GOSUB 650
8090 SR=RK:GOSUB 300:HO=.5-LEN(SR$)/32:VE=.45:GOSUB 650
8100 SR=PK:GOSUB 300:HO=.51:GOSUB 650
8110 SR=PG:GOSUB 300:HO=1-LEN(SR$)/32:GOSUB 650
8120 SR=MT:GOSUB 300:HO=0:VE=.52:GOSUB 650
8130 HO=.5+LEN(SR$)/32:GOSUB 650
8140 SR=TM:GOSUB 300:HO=.5-LEN(SR$)/32:GOSUB 650
8150 VE=.96:HO=.5+(LEN(SR$)-1)/32:GOSUB 650
8160 SR$="tijd":HO=.2:VE=.52:GOSUB 650
8170 FOR I=1 TO 4:SR$=MID$("tijd",I,1)
8180 VE=I*.04+.6:HO=.46:GOSUB 650:NEXT I
8190 SR$="prooi":HO=.7:VE=.52:GOSUB 650
8200 FOR I=1 TO 4:SR$=MID$("roof",I,1)
8210 VE=I*.04+.1:HO=.46:GOSUB 650:NEXT I
8220 N=1:IP=INT(((TM-MT)/IT/4)):AP=0
8230 FOR T=MT TO TM STEP IT
8240 GOSUB 9140
8250 V=(T-MT)*TM/(TM-MT)/(TM/.485)+.51
8260 H=(X-PK)*PG/(PG-PK)/(PG/.485)+.51
8270 IF T=MT THEN VE=V:HO=H:GOSUB 620:X1=H:Y1=V:GOTO 8290
8280 HO=X1:VE=Y1:GOSUB 620:HO=H:VE=V:GOSUB 630:X1=H:Y1=V
8290 H=(T-MT)*TM/(TM-MT)/(TM/.49)+.01
8300 V=.49-(Y-RK)*RG/(RG-RK)/(RG/.49)
8310 IF T=MT THEN VE=V:HO=H:GOSUB 620:X2=H:Y2=V:GOTO 8330
8320 HO=X2:VE=Y2:GOSUB 620:HO=H:VE=V:GOSUB 630:X2=H:Y2=V
8330 H=(X-PK)*PG/(PG-PK)/(PG/.485)+.51
8340 V=.49-(Y-RK)*RG/(RG-RK)/(RG/.49)
8350 IF T=MT THEN HO=H:VE=V:GOSUB 620:X3=H:Y3=V:GOTO 8390
8360 HO=X3:VE=Y3:GOSUB 620:HO=H:VE=V:GOSUB 630:X3=H:Y3=V
8370 AP=AP+1:IF (AP<IP*N) OR (N>3) THEN 8390
8380 ON N GOSUB 9210,9410,9610:N=N+1
8390 NEXT T
8400 GOSUB 9180:RETURN
8500 REM Demo
8510 RESTORE :FOR I=1 TO 9:READ IN$:NEXT I
8520 READ PA:READ PB:READ PC:READ PD
8530 READ RA:READ RB:READ RC:READ RD
8540 READ MT:READ TM:READ IT
8550 RG=RA+RB:RK=RA-RB:PG=PA+PB:PK=PA-PB:PR=2
8560 FOR I=1 TO 4:ON I GOSUB 5010,6010,7010,8010
8570 NEXT I:IV=0:RETURN
9000 REM centreren, reverse printen
9010 GOSUB 100:VE=0:HO=INT(HB/2-LEN(SR$)/2)-3
9020 GOSUB 110:GOSUB 150:PRINT:RETURN
9030 REM trek lijn
9040 FOR I=1 TO HB:PRINT"-";:NEXT I:RETURN
9100 REM toets
9110 VE=VB:HO=HB-7:GOSUB 110:PRINT"<TOETS>";
9120 GOSUB 210:RETURN
9130 REM prooidierenfunctie
9140 X=PA+PB*SIN(PC*(T+PD))
9150 REM roofdierenfunctie
9160 Y=RA+RB*SIN(RC*(T+RD)):RETURN
9170 REM toets hi-res
9180 SV=15:SD=1:SP=70:GOSUB 400:SP=90:GOSUB 400
9190 GOSUB 210:RETURN
9200 REM driehoek (punt omhoog)  
9210 F1=X1:F2=Y1:GOSUB 9300
9220 F1=X2:F2=Y2:GOSUB 9300
9230 F1=X3:F2=Y3:GOSUB 9300
9240 GOSUB 9810:RETURN
9300 HO=F1-.015:VE=F2+.01:GOSUB 620:HO=HO+.03:GOSUB 630
9310 HO=HO-.015:VE=VE-.03:GOSUB 630:HO=HO-.015
9320 VE=VE+.03:GOSUB 630:HO=HO+.03:GOSUB 630:RETURN
9400 REM vierkantje
9410 F1=X1:F2=Y1:GOSUB 9500
9420 F1=X2:F2=Y2:GOSUB 9500
9430 F1=X3:F2=Y3:GOSUB 9500
9440 GOSUB 9810:RETURN
9500 HO=F1-.01:VE=F2+.015:GOSUB 620:HO=HO+.02
9510 GOSUB 630:VE=VE-.03:GOSUB 630:HO=HO-.02
9520 GOSUB 630:VE=VE+.03:GOSUB 630:RETURN
9600 REM driehoekje (punt naar onderen)
9610 F1=X1:F2=Y1:GOSUB 9700
9620 F1=X2:F2=Y2:GOSUB 9700
9630 F1=X3:F2=Y3:GOSUB 9700
9640 GOSUB 9810:RETURN
9700 HO=F1-.015:VE=F2-.015:GOSUB 620:HO=HO+.03
9710 GOSUB 630:HO=HO-.015:VE=VE+.03:GOSUB 630
9720 HO=HO-.015:VE=VE-.03:GOSUB 630:RETURN
9800 REM stippellijn
9810 HO=X1:FOR VE=Y1 TO Y2 STEP -2/VG
9820 GOSUB 620:NEXT VE
9830 VE=Y2:FOR HO=X2 TO X3 STEP 2/HG
9840 GOSUB 620:NEXT HO
9850 RETURN
20000 RETURN
20010 REM ***********************************
20020 REM Hier kunt u een screendumpprogramma
20030 REM plaatsen. Als een grafiek is ge-
20040 REM tekend en u drukt op 'S' springt
20050 REM het programma hier naartoe.
20060 REM ***********************************
20070 RETURN
25000 DATA "Uitleg","Invoer","Wijzigen","Tabel"
25010 DATA "Parameter","Grafiek","Combinatie"
25020 DATA "Demo","Einde"
25030 REM Data voor de DEMO. U kunt deze data 
25040 REM naar eigen behoefte wijzigen. 
25050 REM Achtereenvolgens staat hier: 
25060 REM A, B, C en D van de prooidierenfunctie. 
25070 REM A, B, C en D van de roofdierenfunctie. 
25080 REM minimale tijd, maximale tijd, tijdsinterval.
25090 DATA 3400,2200,0.2618,-19
25100 DATA 240,160,0.2618,-4
25110 DATA 0,24,1
32000 REM *****************************
32010 REM Door Ronald van Vugt (PA3EAH)
32020 REM 
32030 REM Postbus 160, 5580 AD, Waalre
32040 REM 
32050 REM        mei, 1987, v1.2
32060 REM *****************************
32070 REM TROS-RADIO dd 870722

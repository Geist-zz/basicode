1000 A=250:HG=0:GOTO 20:REM FUNCTIEPLOT
1010 GOSUB 100:REM SCHERM SCHOON
1110 IF HG/2=INT(HG/2) THEN HG=HG-1
1120 IF VG/2=INT(VG/2) THEN VG=VG-1
1130 REM VOORBEREIDINGEN
1140 REM ******************************
2000 GOSUB 100:REM SCHERM SCHOON
2010 PRINT "         x"
2020 PRINT "1   Y = A"
2030 PRINT "          2"
2040 PRINT "2   Y = AX +BX+C"
2050 PRINT "          3   2"
2060 PRINT "3   Y = AX +BX +CX+D"
2070 PRINT "        a"
2080 PRINT "4   Y =  LOG X"
2090 PRINT "5   Y = A*SIN BX"
2100 PRINT "             2"
2110 PRINT "6   Y = A*SIN BX"
2120 PRINT "         a"
2130 PRINT "7   Y = X"
2140 PRINT "8   Twee grafieken"
2150 PRINT "9   Stop programma";
2160 G=20:DH=0:DV=0:REM BEGINVERGROTING
2170 GOSUB 210:F=IN-48:REM KIES
2180 IF F=9 THEN 950:REM EINDE
2190 IF F=8 THEN 3000
2200 IF (F<1)OR(F>7) THEN 2170
2210 F2=0:REM GEEN TWEE FUNCTIES
2220 GOSUB 100:REM SCHERM SCHOON
2230 ON F GOSUB 9000,9100,9200,9300,9400,9500,9600
2260 GOSUB 100:REM SCHERM SCHOON
2270 PRINT "Stapgrootte (1-4)?"
2280 GOSUB 210:S=IN-48:REM VRAAG STAPGROOTTE
2290 IF (S<1)OR(S>4) THEN 2260
2300 GOTO 10000:REM TEKEN IN HI-RES.
2310 REM KIES FUNCTIE DIE MOET GETEKEND
2320 REM ******************************
3000 GOSUB 100:REM SCHERM SCHOON
3010 PRINT "Welke van de genoemde"
3020 PRINT "functies zijn het?"
3030 PRINT :PRINT "Functie 1: ";
3040 GOSUB 210:F=VAL(IN$):REM KIES
3050 IF (F<1)OR(F>7) THEN 3040
3060 A=0:B=0:C=0:D=0:PRINT IN$
3070 ON F GOSUB 9000,9100,9200,9300,9400,9500,9600
3080 A1=A:B1=B:C1=C:D1=D:REM FUNCTIE 1
3090 PRINT :PRINT "Functie 2: ";
3100 GOSUB 210:F2=VAL(IN$):REM KIES
3110 IF (F2<1)OR(F2>7) THEN 3100
3120 PRINT IN$
3130 ON F2 GOSUB 9000,9100,9200,9300,9400,9500,9600
3140 A2=A:B2=B:C2=C:D2=D:A=A1:B=B1:C=C1:D=D1:F1=F
3150 REM VARIABELEN VOOR FUNCTIE 1 EN 2
3160 GOTO 2260:REM KIES TWEE FUNCTIES
3170 REM ******************************
7000 GOSUB 210:REM WACHT OP TOETS
7100 IF IN=77 THEN 2000:REM M
7110 IF IN=71 THEN 7160:REM G
7120 IF IN=75 THEN 7140:REM K
7130 GOTO 7000:REM VRAAG OPDRACHT
7140 G=G*.5:DH=0:DV=0:REM VERKLEIN
7150 GOTO 2260:REM TEKEN VERKLEINING
7160 G=G*2:REM VERGROOT
7170 GOSUB 100:REM SCHERM SCHOON
7180 PRINT "Welk gebied?"
7190 PRINT :PRINT "0  rond Oorsprong"
7200 PRINT "1  kwadrant I"
7210 PRINT "2  kwadrant II"
7220 PRINT "3  kwadrant III"
7230 PRINT "4  kwadrant IV"
7240 GOSUB 210:REM WACHT OP TOETS
7250 IF IN=48 THEN DX=0:DY=0:GOTO 7310
7260 IF IN=49 THEN DX=1:DY=1:GOTO 7310
7270 IF IN=50 THEN DX=-1:DY=1:GOTO 7310
7280 IF IN=51 THEN DX=-1:DY=-1:GOTO 7310
7290 IF IN=52 THEN DX=1:DY=-1:GOTO 7310
7300 GOTO 7240
7310 DH=DH+EX*DX/G:DV=DV+EY*DY/G
7320 REM KIES GEBIED EN VERSCHUIF
7330 GOTO 2260:REM TEKEN VERGROTING
7340 REM BEPAAL NIEUWE TEKENOPDRACHT
7350 REM ******************************
8000 PRINT "Geef A een waarde:"
8010 INPUT W$:A=VAL(W$)
8020 IF (W$="e")OR(W$="E") THEN A=2.71828183
8030 IF (W$="pi")OR(W$="PI") THEN A=3.14159265
8040 RETURN:REM HAAL WAARDE A
8100 PRINT "Geef B een waarde:"
8110 INPUT W$:B=VAL(W$)
8120 IF (W$="e")OR(W$="E") THEN B=2.71828183
8130 IF (W$="pi")OR(W$="PI") THEN B=3.14159265
8140 RETURN:REM HAAL WAARDE B
8200 PRINT "Geef C een waarde:"
8210 INPUT W$:C=VAL(W$)
8220 IF (W$="e")OR(W$="E") THEN C=2.71828183
8230 IF (W$="pi")OR(W$="PI") THEN C=3.14159265
8240 RETURN:REM HAAL WAARDE C
8300 PRINT "Geef D een waarde:"
8310 INPUT W$:D=VAL(W$)
8320 IF (W$="e")OR(W$="E") THEN D=2.71828183
8330 IF (W$="pi")OR(W$="PI") THEN D=3.14159265
8340 RETURN:REM HAAL WAARDE D
8350 REM HAAL WAARDEN VAN CONSTANTEN
8360 REM ******************************
9000 PRINT "     x"
9010 PRINT "Y = A"
9020 PRINT :GOSUB 8000:REM HAAL A
9030 IF A<0 THEN 9020
9040 RETURN
9100 PRINT "      2"
9110 PRINT "Y = AX +BX+C"
9120 PRINT :GOSUB 8000
9130 GOSUB 8100:GOSUB 8200
9140 RETURN:REM HAAL A EN B EN C
9200 PRINT "      3   2"
9210 PRINT "Y = AX +BX +CX+D"
9220 PRINT :GOSUB 8000:GOSUB 8100
9230 GOSUB 8200:GOSUB 8300
9240 RETURN:REM HAAL A EN B EN C EN D
9300 PRINT "    a"
9310 PRINT "Y =  LOG X"
9320 PRINT :GOSUB 8000:REM HAAL A
9330 IF (A<=0)OR(A=1) THEN 9320
9340 RETURN
9400 PRINT "Y = A*SIN BX"
9410 PRINT :GOSUB 8000:GOSUB 8100
9420 RETURN:REM HAAL A EN B
9500 PRINT "         2"
9510 PRINT "Y = A*SIN BX"
9520 PRINT :GOSUB 8000:GOSUB 8100
9530 RETURN:REM HAAL A EN B
9600 PRINT "     a"
9610 PRINT "Y = X"
9620 PRINT :GOSUB 8000:REM HAAL A
9630 IF A<>INT(A) THEN 9620
9640 RETURN
9650 REM BEPAAL CONSTANTEN IN FUNCTIES
9660 REM ******************************
9700 ON F GOSUB 9800,9810,9820,9830,9850,9860,9870
9710 RETURN:REM ZOEK GOEDE FUNCTIE
9800 Y=A^T:RETURN
9810 Y=(A*T+B)*T+C:RETURN
9820 Y=((A*T+B)*T+C)*T+D:RETURN
9830 IF T>0 THEN Y=LOG(T)/LOG(A):RETURN
9840 Y=(-EY-1)*SGN(A-1)/G+DV:RETURN
9850 Y=A*SIN(B*T):RETURN
9860 Y=A*(SIN(B*T)^2):RETURN
9870 Y=T^A:RETURN
9880 REM BEPAAL Y-BEELD VAN X
9890 REM ******************************
10000 GOSUB 600:REM GRAFISCH BEDRIJF
10010 FX=1/HG:FY=1/VG:CN=0:REM KLEUR
10020 EY=(VG+1)/2:VE=EY*FY:REM HELFT Y-AS
10030 EX=(HG+1)/2:REM HELFT X-AS
10040 FOR HO=8*FX TO HG*FX-.001 STEP 2*FX
10050 IF (HO>(EX-5)*FX)AND(HO<(EX+3)*FX) THEN 10080
10060 IF (HO>(EX+21)*FX)AND(HO<(EX+38)*FX) THEN 10080
10070 GOSUB 620:REM PLOT PUNT
10080 NEXT HO:REM TEKEN X-AS
10090 HO=EX*FX
10100 FOR VE=8*FY TO VG*FY STEP 2*FY
10110 IF (VE>(EY-4)*FY)AND(VE<(EY+4)*FY) THEN 10130
10120 GOSUB 620:REM PLOT PUNT
10130 NEXT VE:REM TEKEN Y-AS
10140 VE=(EY-3)*FY:HO=(EX-4)*FX:SR$="O"
10150 IF (INT(DH)<>0)OR(INT(DV)<>0) THEN SR$="+"
10160 GOSUB 650:REM PRINT SR$ (O OF +)
10170 SR=20/G:GOSUB 300:REM SR$=SR
10180 HO=(EX+22)*FX:GOSUB 650:REM PRINT SR$
10190 HO=(EX+20)*FX:VE=VE+4*FY:GOSUB 620
10200 VE=VE+FY:GOSUB 620:REM PLOT PUNTEN
10210 REM TEKEN LIJNTJE VOOR EENHEID
10220 VE=(EY-3)*FY:HO=0:SR$="X"
10230 GOSUB 650:REM PRINT SR$
10240 VE=0:HO=(EX-4)*FX:SR$="Y"
10250 GOSUB 650:REM PRINT SR$
10260 Q=0:REM TEKEN ASSENSTELSEL
10270 REM *****************************
11000 T=(-EX-1)/G+DH:GOSUB 9700
11010 LY=INT(G*(Y-DV)+0.5):REM EERSTE Y
11020 FOR X=-EX TO EX-2 STEP S
11030 IF LY>EY-3 THEN LY=EY-2
11040 IF LY<-EY+3 THEN LY=-EY+2
11050 HO=(X+EX)*FX:T=X/G+DH:GOSUB 9700
11060 Y=INT(G*(Y-DV)+0.5):REM Y NU
11070 IF ABS(Y)<EY-2 THEN 11120:REM Y BINNEN BEELD
11080 IF (ABS(LY)>EY-3)AND(SGN(LY)=SGN(Y)) THEN 11180
11090 IF (ABS(LY)>EY-3) THEN 12000
11100 IF Y>0 THEN 12100
11110 GOTO 12200:REM Y BUITEN BEELD
11120 VE=(-Y+EY)*FY:GOSUB 620:REM PLOT PUNT
11130 IF LY>EY-3 THEN 12300
11140 IF LY<-EY+3 THEN 12400
11150 IF ABS(LY-Y)<=1 THEN 11180
11160 IF LY>Y THEN 12500
11170 GOTO 12600:REM Y BINNEN BEELD
11180 LY=Y:NEXT X:REM VORIGE Y=Y NU
11190 GOTO 13000
11200 REM LUS VOOR TEKENEN GRAFIEK
11210 REM *****************************
12000 VE=2*FY:GOSUB 620:REM PLOT PUNT
12010 VE=(VG-2)*FY:GOSUB 630:REM TREK LIJN TOT DIT PUNT
12020 GOTO 11180:REM BOVEN TOT ONDER
12100 VE=2*FY:GOSUB 620:REM PLOT PUNT
12110 VE=(-LY+EY-1)*FY:GOSUB 630:REM TREK LIJN
12120 GOTO 11180:REM BOVEN TOT PUNT
12200 VE=(VG-2)*FY:GOSUB 620:REM PLOT PUNT
12210 VE=(-LY+EY+1)*FY:GOSUB 630:REM TREK LIJN
12220 GOTO 11180:REM ONDER TOT PUNT
12300 VE=2*FY:GOSUB 630:REM TREK LIJN
12310 GOTO 11180:REM PUNT TOT BOVEN
12400 VE=(VG-2)*FY:GOSUB 630:REM TREK LIJN
12410 GOTO 11180:REM PUNT TOT ONDER
12500 VE=(-LY+EY+1)*FY:GOSUB 630:REM TREK LIJN
12510 GOTO 11180:REM PUNT TOT HOOG PUNT
12600 VE=(-LY+EY-1)*FY:GOSUB 630:REM TREK LIJN
12610 GOTO 11180:REM PUNT TOT LAAG PUNT
12700 REM DELEN VAN GRAFIEK TEKENEN
12710 REM *****************************
13000 Q=Q+1:REM TELLER VOOR 2 FUNCTIES
13010 IF (Q>1)OR(F2=0) THEN 13040
13020 F=F2:A=A2:B=B2:C=C2:D=D2
13030 GOTO 11000:REM TEKEN GRAFIEK 2
13040 IF Q=1 THEN 13070
13050 F=F1:A=A1:B=B1:C=C1:D=D1
13060 REM HERSTEL SITUATIE GRAFIEK 1
13070 CN=1:FOR VE=0 TO 8*FY STEP FY
13080 HO=0:GOSUB 620:REM WIS PUNT
13090 HO=(HG-2)*FX:GOSUB 630:REM WIS LIJN TOT DIT PUNT
13100 NEXT VE:REM WIS BOVENSTE REGELS
13110 CN=0:SR$="Menu Groter Kleiner ?"
13120 HO=0:VE=0:GOSUB 650:REM PRINT SR$
13130 GOTO 7000:REM VRAAG OPDRACHT
13140 REM MAAK GRAFIEK AF
13150 REM *****************************
30000 REM 0-999 STANDAARDROUTINES
30010 REM 1000  VERPLICHTE REGEL
30020 REM 1010-1140 VOORBEREIDINGEN
30030 REM 2000-2320 FUNCTIE-KEUZE
30040 REM 3000-3170 VOOR TWEE FUNCTIES
30050 REM 8000-8360 VRAAGT CONSTANTEN
30060 REM 9000-9660 CONST. IN FUNCTIES
30070 REM 9700-9890 ZOEKT Y-BEELD VAN X
30080 REM 9970-9990 EINDROUTINE
30090 REM 10000-10270 ASSENSTELSEL
30100 REM 11000-11210 TEKEN GRAFIEK
30110 REM 12000-12710 TEKENROUTINES
30120 REM 13000-13150 MAAK GRAFIEK AF
30130 REM 30000-30260 BESCHRIJVING
30140 REM 32000-32110 AUTEUR
30150 REM 
30160 REM VERDERE GEGEVENS STAAN IN
30170 REM AARDE EN KOSMOS INFORMATICA
30180 REM NUMMER 7, 1987
30190 REM 
30200 REM *****************************
30210 REM FUNCTIEPLOT
30220 REM 
30230 REM EEN PROGRAMMA IN
30240 REM BASICODE-3
30250 REM 
32000 REM GESCHREVEN DOOR
32010 REM HANS VAN DONGEN
32020 REM HENDRIK VAN VELDEKESTRAAT 34
32030 REM 6367 SB  VOERENDAAL
32040 REM 
32050 REM 28-08-1987
32060 REM 
32100 REM TROS-RADIO dd 871007

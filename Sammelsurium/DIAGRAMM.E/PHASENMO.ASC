1000 A=200:GOTO 20:REM ***VAZENMODEL*** 
1010 DIM P(300):H1=HO+1:V=VE+1
1020 GOSUB 100:SR$="VAZENMODEL":VE=0:HO=H1/2-8
1030 GOSUB 110:GOSUB 150:HO=0:VE=4:GOSUB 110
1040 PRINT"Met dit programma wordt het trekken van"
1050 PRINT"K knikkers uit een vaas met n knikkers"
1060 PRINT"gesimuleerd."
1070 PRINT
1080 PRINT" In de vaas zitten zwarte en witte"
1090 PRINT"knikkers, de aantallen moet u zelf"
1100 PRINT"invoeren."
1110 PRINT
1120 PRINT" U moet ook invoeren of het experiment"
1130 PRINT"met of zonder terugleggen moet worden"
1140 PRINT"uitgevoerd."
1150 PRINT
1160 PRINT" Tenslotte moet u dan nog kiezen of u"
1170 PRINT"een totale kansverdeling wilt hebben"
1180 PRINT"of slechts de kans op een gebeurtenis."
1190 GOSUB 9080
1200 GOSUB 100:PRINT:PRINT
1210 PRINT"Als u kiest voor een kansverdeling"
1220 PRINT"bestaat de mogelijkheid om de"
1230 PRINT"resultaten verwerkt te zien in een"
1240 PRINT"grafiek.":PRINT
1250 PRINT" In een grafiek worden de resultaten"
1260 PRINT"dan op twee manieren verwerkt."
1270 PRINT"Namelijk in de vorm van een histogram"
1280 PRINT"en in de vorm van een cumulatieve"
1290 PRINT"grafiek, waarbij op de y-as telkens"
1300 PRINT"de kans staat uitgezet en op de x-as"
1310 PRINT"het aantal witte knikkers van de"
1320 PRINT"greep.":PRINT:PRINT:PRINT:PRINT
2000 GOSUB 9080
2010 GOSUB 100:PRINT:PRINT
2020 PRINT"Hoeveel knikkers zitten er"
2030 PRINT"in de vaas ";:INPUT N
2040 PRINT:PRINT"Hoeveel daarvan zijn er wit ";:INPUT M
2050 IF (M>N) OR (M<0) THEN 2010
2060 IF M<>INT(M) THEN 2010
2070 L=N-M
2080 GOSUB 100:PRINT:PRINT
2090 PRINT"Maak nu uw keuze uit een experiment"
2100 PRINT:PRINT:PRINT"      1) met terugleggen"
2110 PRINT:PRINT"      2) zonder terugleggen"
2120 GOSUB 210:T=IN-48:IF (T<>1) AND (T<>2) THEN 2120
2130 PRINT:PRINT:PRINT"Wilt u nu"
2140 PRINT:PRINT"      1) de kans op een gebeurtenis"
2150 PRINT:PRINT"      2) een totale kansverdeling"
2160 GOSUB 210:E=IN-48:IF (E<>1) AND (E<>2) THEN 2160
2170 GOSUB 100:PRINT:PRINT
2180 PRINT"Op hoeveel decimalen nauwkeurig wilt u"
2190 PRINT"de kansen berekend hebben ";:INPUT CN:CT=CN+2
2200 IF (CN<0) OR (CN>9) THEN 2170
2210 ON (2*T-2+E) GOSUB 3000,4000,5000,6000
2220 IF (E=2) AND (IN=78) THEN 2240
2230 GOSUB 210
2240 GOSUB 8000
2250 IF IN=49 THEN 2010
2260 PRINT:PRINT:PRINT
2270 SR$="TOT ZIENS !":GOSUB 150:GOSUB 210
2280 GOTO 950
3000 REM *** EXPERIMENT 1,1 ***
3010 GOSUB 100:PRINT:PRINT
3020 PRINT"Hoeveel knikkers wilt u trekken ";:INPUT K
3030 IF K<0 THEN 3000
3040 PRINT:PRINT
3050 PRINT"Hoeveel witte moeten daarbij zitten ";:INPUT A
3060 PRINT:PRINT:PRINT"X=aantal witte knikkers."
3070 IF (A<0) OR (A>N) THEN PRINT:PRINT"P(X=";A;")=0":RETURN
3080 IF (A=INT(A)) OR (A>=1E4) THEN 3100
3090 PRINT:PRINT"P(X=";A;")=0":RETURN
3100 IF A>1E4 THEN 3050
3110 GOSUB 9000
3120 SR=(M/N)^A*(L/N)^(K-A)*KB:GOSUB 310
3130 PRINT:PRINT:PRINT"P(X=";A;")=";SR$
3140 RETURN
4000 REM *** EXPERIMENT 1,2 ***
4010 GOSUB 100:PRINT:PRINT
4020 PRINT"Hoeveel knikkers wilt u trekken ";:INPUT K
4030 PRINT:PRINT:PRINT"X=aantal witte knikkers."
4040 S=0:PRINT:PRINT
4050 FOR A=0 TO K
4060 GOSUB 9000
4070 P(A)=(M/N)^A*(L/N)^(K-A)*KB
4080 SR=P(A):GOSUB 310
4090 PRINT"P(X=";A;")=";SR$
4100 S=S+SR
4110 NEXT A
4120 SR=S:GOSUB 310
4130 PRINT:PRINT"De som van alle kansen is :";SR$
4140 PRINT:PRINT
4150 PRINT"Wilt u hiervan de grafiek zien (j/n)?";
4160 GOSUB 210:IF (IN<>74) AND (IN<>78) THEN 4160
4170 IF IN=74 THEN GOSUB 7000
4180 RETURN
5000 REM *** EXPERIMENT 2,1 ***
5010 GOSUB 100:PRINT:PRINT
5020 PRINT"Hoeveel knikkers wilt u trekken ";:INPUT K
5030 IF (K<0) OR (K>N) THEN 5000
5040 PRINT:PRINT"Hoeveel witte moeten daarbij zitten ";
5050 INPUT A
5060 IF (A<0) OR (A>M) THEN PRINT:PRINT"P(X=";A;")=0":RETURN
5070 IF (A=INT(A)) OR (A>=1E4) THEN 5090
5080 PRINT:PRINT"P(X=";A;")=0":RETURN
5090 IF A>1E4 THEN 5040
5100 PRINT:PRINT:PRINT"X=aantal witte knikkers."
5110 P=1:H=M:B=A
5120 FOR I=1 TO K
5130 IF B=0 THEN H=L
5140 P=P*H/N
5150 H=H-1:N=N-1:B=B-1
5160 NEXT I
5170 GOSUB 9000
5180 SR=P*KB:GOSUB 310
5190 PRINT:PRINT"P(X=";A;")=";SR$
5200 RETURN
6000 REM *** EXPERIMENT 2,2 ***
6010 GOSUB 100:PRINT:PRINT
6020 PRINT"Hoeveel knikkers wilt u trekken ";:INPUT K
6030 PRINT:PRINT
6040 IF (K<0) OR (K>N) THEN 6000
6050 PRINT:PRINT"X=aantal witte knikkers."
6060 PRINT
6070 S=0:NN=N
6080 FOR A=0 TO K
6090 IF A>M THEN P(A)=0:GOTO 6180
6100 PP=1:H=M:B=A:N=NN
6110 FOR I=1 TO K
6120 IF B=0 THEN H=L
6130 PP=PP*H/N
6140 H=H-1:N=N-1:B=B-1
6150 NEXT I
6160 GOSUB 9000
6170 P(A)=PP*KB
6180 SR=P(A):GOSUB 310
6190 PRINT"P(X=";A;")=";SR$
6200 S=S+SR
6210 NEXT A
6220 SR=S:GOSUB 310
6230 PRINT:PRINT"De som van alle kansen is :";SR$
6240 PRINT:PRINT
6250 PRINT"Wilt u hiervan de grafiek zien (j/n)?";
6260 GOSUB 210:IF (IN<>74) AND (IN<>78) THEN 6260
6270 IF IN=74 THEN GOSUB 7000
6280 RETURN
7000 REM *** GRAFIEK ***
7010 GOSUB 600:CN=0:HO=0:VE=0:SR$="1.00":GOSUB 650
7020 VE=.19:SR$="0.75":GOSUB 650
7030 VE=.44:SR$="0.50":GOSUB 650
7040 VE=.69:SR$="0.25":GOSUB 650
7050 VE=.94:SR$="0.00":GOSUB 650
7060 H=.9/A:VE=.94:HO=.1:GOSUB 620
7070 FOR I=0 TO A
7080 VE=.94-P(I):GOSUB 630:HO=HO+H:GOSUB 630
7090 VE=.94:GOSUB 630:NEXT I
7100 VE=.94:HO=.1:GOSUB 620
7110 S=0
7120 FOR I=0 TO A
7130 S=S+P(I)
7140 HO=HO+H:VE=.94-S:GOSUB 630
7150 NEXT I
7160 HO=.1:VE=.94:GOSUB 620:HO=1:GOSUB 630
7170 IF A>=16 THEN RETURN
7180 HO=.097+.5*H:VE=.95
7190 FOR I=0 TO A
7200 SR=I:GOSUB 300:GOSUB 650
7210 HO=HO+H
7220 NEXT I
7230 RETURN
8000 REM *** MENU ***
8010 GOSUB 100:PRINT:PRINT
8020 PRINT"Wilt u"
8030 PRINT"       1) opnieuw beginnen"
8040 PRINT"       2) stoppen"
8050 GOSUB 210
8060 RETURN
9000 REM *** K BOVEN A BEREKENEN ***
9010 IF (A=0) OR (A=K) THEN KB=1:RETURN
9020 IF (A=1) OR (A=K-1) THEN KB=K:RETURN
9030 KF=0:FOR I=1 TO K:KF=KF+LOG(I):NEXT I
9040 AF=0:FOR I=1 TO A:AF=AF+LOG(I):NEXT I
9050 BF=0:FOR I=1 TO K-A:BF=BF+LOG(I):NEXT I
9060 KB=EXP(KF-AF-BF)
9070 RETURN
9080 REM toets
9090 VE=V-2:HO=H1-3:GOSUB 110:PRINT"=>";
9100 GOSUB 210:RETURN
32000 REM ************************ 
32010 REM *                      * 
32020 REM *   (C) T.H. STOOF     * 
32030 REM *                      * 
32040 REM *     RESEDALN 24      * 
32050 REM *                      * 
32060 REM *   5582 AB  WAALRE    * 
32070 REM *                      *
32080 REM *      NEDERLAND       * 
32090 REM *                      *
32100 REM *    GEMAAKT OP EEN    * 
32110 REM *  BBC MICRO, MODEL B  * 
32120 REM *    OP 29-12-1986     * 
32130 REM *                      * 
32140 REM ************************
32200 REM TROS-radio dd 870128

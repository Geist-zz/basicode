1000 A=500:GOTO 20:REM LIJNONDERZOEK
1010 REM *CONSTANTEN INITIEREN*
1020 DIM A(3),V$(3),A$(6),I$(3)
1030 R2$="":R3$="":R4$=""
1040 REM *MAX/MIN X-AS*
1050 MX=10:SR=MX:GOSUB 300:MX$=SR$
1060 XM=-10:SR=XM:GOSUB 300:XM$=SR$
1070 REM *OORSPRONG*
1080 X0=144:Y0=75:XA=288:YA=200
1090 REM *MAX/MIN Y-AS*
1100 MY=20:SR=MY:GOSUB 300:MY$=SR$
1110 YM=-20:SR=YM:GOSUB 300:YM$=SR$
1120 REM *VERDELINGSFACTOR Y-AS*
1130 SY=2*Y0/ABS(YM-MY)
1140 REM *VERDELINGSFACTOR X-AS*
1150 SX=2*X0/ABS(XM-MX)
1160 V$(1)=" a wordt:":V$(2)=" b wordt:":V$(3)=" c wordt:"
1170 REM TITELPAGINA AANROEPEN
1180 GOSUB 8010
1190 REM *PROGRAMMA TEKST*
1200 REM SCHERM SCHOON
1210 GOSUB 100
1220 PRINT"lijn - variabelen - onderzoek"
1230 PRINT:PRINT:P$="lijn : a.x + b.y = c":PRINT P$
1240 PRINT:PRINT"met dit programma kun je gaan onderzoe-"
1250 PRINT"ken welke gevolgen het veranderen van "
1260 PRINT"de variabelen a,b en c hebben voor de "
1270 PRINT"vorm van de grafiek van de functie."
1280 PRINT"je begint met het invoeren van de waar-"
1290 PRINT"den a,b en c van de beginlijn."
1300 PRINT"je kunt daarna deze waarden veranderen"
1310 PRINT"en de grafiek opnieuw laten tekenen."
1320 PRINT"de grafiek wordt steeds voor het inter-"
1330 PRINT"val -10 tot 10 getekend."
1340 PRINT"veel succes met je onderzoek!"
1350 PRINT:PRINT"doorgaan <return>"
1360 GOSUB 210:IF IN<>13 THEN 1360
1370 REM BEGININVOER SUBROUTINE
1380 REM SCHERM SCHOON
1390 GOSUB 100
1400 PRINT P$:PRINT
1410 PRINT"welke waarde krijgt a ";:INPUT A0:PRINT
1420 SR=A0:GOSUB 300:A0$=SR$
1430 PRINT"welke waarde krijgt b ";:INPUT B0:PRINT
1440 SR=B0:GOSUB 300:B0$=SR$
1450 PRINT"welke waarde krijgt c ";:INPUT C0:PRINT
1460 SR=C0:GOSUB 300:C0$=SR$
1470 PRINT:PRINT:PRINT"doorgaan <return>"
1480 GOSUB 210:IF IN<>13 THEN 1480
1490 REM *HOOFDPROGRAMMA*
1500 GOSUB 600:REM GRAFISCH BEDRIJF
1510 REM *X-AS+VERDELING*
1520 CN=0:HO=0:VE=Y0/YA:GOSUB 620
1530 HO=(2*X0-1)/XA:GOSUB 630
1540 FOR X=0 TO 2*X0 STEP SX
1550 CN=0:HO=X/XA:VE=(Y0-2)/YA:GOSUB 620
1560 VE=(Y0+2)/YA:GOSUB 630
1570 NEXT X
1580 REM TEKST X-AS
1590 SR$="x":HO=(2*X0-8)/XA:VE=(Y0+5)/YA:GOSUB 650
1600 REM *Y-AS+VERDELING*
1610 CN=0:HO=X0/XA:VE=0:GOSUB 620
1620 VE=2*Y0/YA:GOSUB 630
1630 FOR Y=0 TO 2*Y0 STEP 2*SY
1640 CN=0:HO=(X0-2)/XA:VE=Y/YA:GOSUB 620
1650 HO=(X0+2)/XA:GOSUB 630
1660 NEXT Y
1670 SR$="y":HO=(X0-8)/XA:VE=0:GOSUB 650
1680 SR$=MY$:HO=X0/XA:VE=0:GOSUB 650
1690 SR$=YM$:HO=X0/XA:VE=2*Y0/YA:GOSUB 650
1700 REM *BEGINTEKST+LIJN*
1710 A$(0)=A0$+".x + "+B0$+".y = "+C0$
1720 A$(0)="begin "+A$(0)
1730 CN=0:SR$=A$(0):HO=0:VE=160/YA:GOSUB 650
1740 A=A0:B=B0:C=C0
1750 REM FUNCTIE SUBROUTINE AANROEPEN
1760 GOSUB 2010
1770 REM KEUZE-SUBROUTINE SPRINGEN
1780 GOTO 3010
2000 REM FUNCTIE SUBROUTINE
2010 IF (A=0) AND (B=0) THEN 2110
2020 IF (A=0) AND (B<>0) AND (C=0) THEN 2210
2030 IF (A=0) AND (B<>0) AND (C<>0) THEN 2310
2040 IF (A<>0) AND (B=0) AND (C=0) THEN 2410
2050 IF (A<>0) AND (B=0) AND (C<>0) THEN 2510
2060 IF (A<>0) AND (B<>0) THEN 2610
2100 REM STRIJDIGSUBROUTINE
2110 R$="invoer kan niet -> strijdig"
2120 GOSUB 5010:REM TEKST
2130 RETURN
2200 REM X-AS
2210 R$="lijn is de x-as"
2220 GOSUB 5010:REM TEKST
2230 GOTO 2690:REM RETURN
2300 REM // X-AS
2310 R$="lijn evenwijdig x-as"
2320 CN=0:VE=(Y0-SY*C/B)/YA:HO=0:GOSUB 620
2330 HO=288/XA:GOSUB 630
2340 GOSUB 5010:REM TEKST
2350 GOTO 2690:REM RETURN
2400 REM Y-AS
2410 R$="lijn is de y-as"
2420 GOSUB 5010:REM TEKST
2430 GOTO 2690:REM RETURN
2500 REM // Y-AS
2510 R$="lijn evenwijdig y-as"
2520 CN=0:HO=(X0+SX*C/A)/XA:VE=0:GOSUB 620
2530 VE=150/YA:GOSUB 630
2540 GOSUB 5010:REM TEKST
2550 GOTO 2690:REM RETURN
2600 REM LIJN
2610 R$=""
2620 GOSUB 6010:REM RC SUBROUTINE
2630 IF R$<>"" THEN 2690:REM RETURN
2640 X1=X0+X1*SX:X2=X0+X2*SX
2650 Y1=Y0-Y1*SY:Y2=Y0-Y2*SY
2660 IF X2=XA THEN X2=XA-1
2670 CN=0:HO=X1/XA:VE=Y1/YA:GOSUB 620
2680 HO=X2/XA:VE=Y2/YA:GOSUB 630
2690 RETURN
3000 REM KEUZESUBROUTINE
3010 CN=1:HO=0:VE=180/YA:SR$=R3$:GOSUB 650
3020 CN=1:HO=0:VE=190/YA:SR$=R4$:GOSUB 650
3030 R3$="(v)eranderen"
3040 CN=0:HO=0:VE=180/YA:SR$=R3$:GOSUB 650
3050 R4$="(s)toppen"
3060 CN=0:HO=0:VE=190/YA:SR$=R4$:GOSUB 650
3070 GOSUB 210:IF (IN<>86) AND (IN<>83) THEN 3070
3080 IF IN=86 THEN GOSUB 4010:GOTO 3010
3090 REM SCHOON SCHERM
3100 GOSUB 100:PRINT"andere beginlijn (j/n)"
3110 GOSUB 210
3120 IF (IN<>74) AND (IN<>78) THEN 3110
3130 IF IN=74 THEN 1390
3140 GOTO 950
4000 REM VERANDEREN SUBROUTINE
4010 CN=1:HO=0:VE=180/YA:SR$=R3$:GOSUB 650
4020 CN=1:HO=0:VE=190/YA:SR$=R4$:GOSUB 650
4030 CN=1:HO=0:VE=170/YA:SR$=R2$:GOSUB 650
4040 R2$="a.x + b.y = c":
4050 CN=0:HO=0:VE=170/YA:SR$=R2$:GOSUB 650
4060 FOR V=1 TO 3
4070 R3$=V$(V)
4080 CN=0:HO=0:VE=180/YA:SR$=R3$:GOSUB 650
4090 GOSUB 210
4100 IF IN=13 THEN 4120
4110 I$(V)=I$(V)+IN$
4120 A(V)=VAL(I$(V))
4130 R5$=I$(V)
4140 CN=0:HO=90/XA:VE=180/YA:SR$=R5$:GOSUB 650
4150 IF IN<>13 THEN 4090
4160 CN=1:HO=0:VE=180/YA:SR$=R3$:GOSUB 650
4170 CN=1:HO=90/XA:VE=180/YA:SR$=R5$:GOSUB 650
4180 NEXT V
4190 CN=1:HO=0:VE=170/YA:SR$=R2$:GOSUB 650
4200 R2$=I$(1)+".x + "+I$(2)+".y = "+I$(3)
4210 CN=0:SR$=R2$:GOSUB 650
4220 A=A(1):B=A(2):C=A(3)
4230 FOR I=1 TO 3:I$(I)="":NEXT I
4240 REM FUNCTIE SUBROUTINE AANROEPEN
4250 GOSUB 2010
4260 GOTO 3010:REM NIEUWE KEUZE
5000 REM TEKSTSUBROUTINE
5010 CN=0:HO=0:VE=180/YA:SR$=R$:GOSUB 650
5020 SV=0:SD=30:GOSUB 400:REM PAUZE
5030 CN=1:GOSUB 650
5040 RETURN
6000 REM RC SUBROUTINE
6010 RC=-A/B
6020 IF RC<0 THEN 6050
6030 IF RC>0 THEN 6090
6040 REM NEGATIEF
6050 Y1=MY:X1=Y1/RC+C/A
6060 Y2=YM:X2=Y2/RC+C/A
6070 GOTO 6130:REM TEST
6080 REM POSITIEF
6090 Y1=YM:X1=Y1/RC+C/A
6100 Y2=MY:X2=Y2/RC+C/A
6110 GOTO 6130:REM TEST
6120 REM TEST
6130 IF (X2<=XM) OR (X1>=MX) THEN 7010
6140 IF X2>=MX THEN X2=MX:Y2=RC*MX+C/B
6150 IF X1<=XM THEN X1=XM:Y1=RC*XM+C/B
6160 GOTO 7030:REM RETURN
7000 REM BUITEN SCHERM
7010 R$="buiten scherm "
7020 GOSUB 5010:REM TEKST
7030 RETURN
8000 REM TITELPAGINA
8010 A$(0)=" w i s k u n d e "
8020 A$(1)="lijn-onderzoek"
8030 A$(2)="door: c.brokken"
8040 A$(3)="wijk bij duurstede"
8050 A$(4)="03435 - 75030"
8060 A$(5)="doorgaan <return>"
8070 GOSUB 600:REM GRAFISCH BEDRIJF
8080 FOR I=0 TO 5
8090 CN=0:SR$=A$(I):HO=0
8100 VE=(20+I*30)/YA:GOSUB 650
8110 NEXT I
8120 GOSUB 210
8130 RETURN
32000 REM *LIJN VARIABELEN ONDERZOEK*
32010 REM *DOOR C.BROKKEN*
32020 REM *ANTONIUS MAVO WIJK BIJ DUURSTEDE*
32030 REM *TEL. 03435-75030*
32040 REM *AANGEPAST VOOR BASICODE-3*
32050 REM *OP 8-4-1988*
32060 REM 
32070 REM TROS-RADIO dd 880629

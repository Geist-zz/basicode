1000 A=100:GOTO 20:REM FRAKTALE
1010 GOSUB 100:ZA=VE:SA=HO:GOTO 2000
1100 L=0:FOR K=1 TO N
1110 C=A*A-B*B+X:D=A*B:D=D+D+Y:A=C:B=D
1120 IF ABS(A)+ABS(B)>M THEN L=K:K=N
1130 NEXT K:IF L=0 THEN L=N
1140 RETURN
1200 L=0:FOR K=1 TO N
1210 C=A*A*A-3*A*B*B+X:D=3*A*B*B-B*B*B+Y
1220 A=C:B=D:IF ABS(A)+ABS(B)>M THEN L=K:K=N
1230 NEXT K:IF L=0 THEN L=N
1240 RETURN
1300 L=0:FOR K=1 TO N:D=A*B:E=A*A:F=B*B
1310 C=(E-F)*D*4+Y:D=E*E-6*D*D+F*F+X
1320 A=D:B=C:IF ABS(A)+ABS(B)>M THEN L=K:K=N
1330 NEXT K:IF L=0 THEN L=N
1340 RETURN
1500 Y=YU-DY:FOR I=0 TO ZG
1510 Y=Y+DY:X=XU-DX:FOR J=0 TO SG:X=X+DX
1520 A=0:B=0:ON MD GOSUB 1100,1200,1300
1530 IF L/2=INT(L/2) THEN HO=J/S:VE=I/Z:GOSUB 620
1540 NEXT J:NEXT I:RETURN
1600 XM=XU+.5*(XO-XU):Y=YU-DY:FOR I=0 TO ZG:MM=(ZG-.5*I)/ZG
1610 Y=Y+DY:X=XU-DX:FOR J=0 TO SG:X=X+DX
1620 A=0:B=0:ON MD GOSUB 1100,1200,1300
1630 H=INT(S*(.5+(X-XM)/(XO-XU)*MM))
1640 F=INT(Z-I-.65*MM*Z*L/N)
1650 S1=0:IF F>=O(H) THEN S1=1
1660 IF F<=U(H) THEN S1=-1
1670 IF J=0 THEN S0=0:IF S1<>0 THEN VE=F/Z:HO=H/S:GOSUB 620
1672 IF J=0 THEN 1710
1674 IF (S1=1) AND (S0<1) THEN VE=O(H)/Z:HO=H/S:GOSUB 620
1676 IF (S1=-1) AND (S0>=0) THEN VE=U(H)/Z:HO=H/S:GOSUB 620
1680 IF S0*S1>0 THEN HO=H/S:VE=F/Z:GOSUB 630:GOTO 1710
1690 IF S0=1 THEN HO=H/S:VE=O(H)/Z:GOSUB 630
1700 IF S0=-1 THEN HO=H/S:VE=U(H)/Z:GOSUB 630
1710 GOSUB 200:IF IN$="$" THEN 950
1720 IF F<U(H) THEN U(H)=F
1730 IF F>O(H) THEN O(H)=F
1740 S0=S1:NEXT J:NEXT I:RETURN
1900 GOSUB 100:SR$="FRAKTALE IN BASICODE - INFO":GOSUB 150
1902 PRINT
1905 PRINT:PRINT"WENN MAN BESTIMMTE, OFT KOMPLEXE  FUNK-"
1910 PRINT"TIONEN MIT UNTERSCHIEDLICHEN PARAMETERN"
1915 PRINT"ITERIERT, ERGIBT SICH IN DEREN ABHAENG-"
1920 PRINT"IGKEIT TEILS KONVERGENZ TEILS DIVERGENZ"
1925 PRINT"STELLT MAN DIESES  VERHALTEN  IN  EINER"
1930 PRINT"GRAFIK  DAR, ERHAELT  MAN  INTERESSANTE"
1935 PRINT"BILDER. SINNVOLLE WERTE FUER REAL-  UND"
1938 PRINT"IMAGINAERTEIL -4...+4, ITERATIONSANZAHL"
1940 PRINT"5..99,DIVERGENZGRENZE >2."
1945 PRINT"DAS  PROGRAMM ENTHAELT 2 DARSTELLUNGEN:"
1950 PRINT"PLANAR MODULO 2 UND 3-D MIT ITERATIONS-"
1955 PRINT"ANZAHL ALS HOEHE. ES GIBT 3 FUNKTIONEN."
1960 PRINT"AUF GRUND DER UMFANGREICHEN  RECHNUNGEN"
1965 PRINT"LAUFT DAS PROGRAMM VIELE  STUNDEN. WENN"
1970 PRINT"SIE EINEN COMPILER HABEN,NUTZEN SIE IHN"
1990 PRINT"VIEL SPASS!       <ENTER>":GOSUB 210
1995 GOSUB 100:RETURN
2000 GOSUB 600:Z=VG:S=HG:GOSUB 100
2010 SR$="FRAKTALE IN BASICODE-3 (C) BIDAT'89":GOSUB 150
2020 IF (Z>=80) AND (S>=80) THEN 2050
2030 PRINT:PRINT"UNERWARTETES GRAFIKFORMAT FESTGESTELLT:"
2032 PRINT"PIXEL HORIZONTAL:";S;" VERTIKAL:";Z
2034 INPUT"IST DAS KORREKT (J/N) ?";A$
2036 IF A$<>"N" THEN 2050
2040 INPUT"PIXEL HORIZONTAL:";S
2045 INPUT"      VERTIKAL  :";Z
2050 DIM U(S),O(S):Z=Z-1:S=S-1
2060 PRINT:PRINT"AUSWAHL DER FUNKTION:"
2065 PRINT:PRINT" <1> INFO"
2070 PRINT" <2> Z=Z^2+C":PRINT" <3> Z=Z^3+C"
2080 PRINT" <4> Z=Z^4+C":PRINT" <0> ENDE"
2090 PRINT:INPUT"IHRE AUSWAHL:";MD:IF MD>4 THEN 2090
2095 IF MD=1 THEN GOSUB 1900:GOTO 2060
2100 MD=MD-1:IF MD<1 THEN 950
2110 PRINT:PRINT"AUSWAHL DER DARSTELLUNGSART"
2120 PRINT:PRINT" <1> PLANAR":PRINT" <2> GEBIRGE"
2130 PRINT:INPUT"IHRE AUSWAHL:";DM:IF (DM<1) OR (DM>2) THEN 2130
2140 PRINT:PRINT"FESTLEGUNG DER PARAMETER:"
2150 PRINT:INPUT"REALTEIL MINIMUM:";XU
2160 INPUT"REALTEIL MAXIMUM:";XO
2170 INPUT"IMAGTEIL MINIMUM:";YU
2180 INPUT"IMAGTEIL MAXIMUM:";YO
2190 INPUT"DIVERGENZGRENZE :";M
2204 INPUT"ITERATIONSANZAHL:";N
2210 INPUT"SIND DIESE WERTE KORREKT (J/N) ?";A$
2220 IF A$="N" THEN 2150
2222 IF DM=2 THEN FOR I=0 TO S:U(I)=1E4:O(I)=0:NEXT I
2230 ZG=Z:SG=S:IF DM=2 THEN ZG=Z/2
2240 DX=(XO-XU)/SG:DY=(YO-YU)/ZG
2250 GOSUB 600:IF DM=1 THEN GOSUB 1500
2255 IF DM=2 THEN GOSUB 1600
2260 VE=.95:HO=.8:SR$="WEITER?":GOSUB 650
2270 GOSUB 210:IF IN$="N" THEN 950
2280 GOSUB 100:GOTO 2060
32000 REM        (C) BIDAT 13.6.89
32010 REM AUTOR: BERTHOLD BIENER
32020 REM        R.-EYERMANN-RING 4/0106
32030 REM        ERFURT
32040 REM        5063
32050 REM IN BASICODE-3 FUER RADIO DDR 2

1000 A=200:GOTO 20:REM "SINCOS"(GONIO)
1010 VT=VE
1020 GOSUB 1510:REM tekst 1
1030 GOSUB 3170:REM input hoek
1040 GOSUB 3010:REM schets figuur
1050 GOSUB 3220:REM teken hoek
1060 GOSUB 3460:IF IN=74 THEN 1030
1070 GOSUB 1810:REM tekst 2
1080 IF FO=1 THEN FO=0:GOTO 1030
1090 GOSUB 2210:REM tekst 3
1100 GOSUB 3170:REM input hoek
1110 GOSUB 3010:REM schets figuur
1120 GOSUB 3220:REM teken hoek
1130 GOSUB 3500:REM markeer de sinus
1140 GOSUB 3650:REM bereken sinus
1150 GOSUB 3460:IF IN=74 THEN 1100
1160 GOSUB 2610:REM tekst 4
1170 GOSUB 3170:REM input hoek
1180 GOSUB 3010:REM schets figuur
1190 GOSUB 3220:REM teken hoek
1200 GOSUB 3710:REM markeer de cosinus
1210 GOSUB 3860:REM bereken cosinus
1220 GOSUB 3460:IF IN=74 THEN 1170
1230 GOSUB 2910:REM tekst 5
1240 GOSUB 3170:REM input hoek
1250 GOSUB 3010:REM schets figuur
1260 GOSUB 3220:REM teken hoek
1270 GOSUB 3500:REM markeer de sinus
1280 GOSUB 3650:REM bereken sinus
1290 GOSUB 3710:REM markeer de cosinus
1300 GOSUB 3860:REM bereken cosinus
1310 GOSUB 3460:IF IN=74 THEN 1240
1320 GOTO 950
1500 REM === TEKST 1 ===
1510 GOSUB 100
1520 PRINT" GONIOMETRIE"
1530 PRINT" ==========="
1540 PRINT:PRINT" Goniometrie is hoekmeetkunde."
1550 PRINT:PRINT" Je kunt op verschillende manieren"
1560 PRINT:PRINT" aangeven hoe groot een hoek is."
1570 PRINT:PRINT" Een manier is op te geven hoeveel"
1580 PRINT:PRINT" graden een hoek groot is."
1590 PRINT:PRINT" Als je een paar keer een hoek in"
1600 PRINT:PRINT" graden opgeeft, dan zal de computer"
1610 PRINT:PRINT" die hoek voor je tekenen."
1620 GOSUB 1750
1630 GOSUB 100
1640 PRINT:PRINT" De tekening bestaat uit X-as en Y-as"
1650 PRINT:PRINT" die zijn onderverdeeld in tienden,"
1660 PRINT:PRINT" met daar op een cirkel met R = 1"
1670 PRINT:PRINT" die is onderverdeeld in boogjes van"
1680 PRINT:PRINT" tien graden."
1690 PRINT:PRINT" Eerst wordt een cirkelboog getekend"
1700 PRINT:PRINT" van het opgegeven aantal graden en"
1710 PRINT:PRINT" daarna wordt het tweede been van de"
1720 PRINT:PRINT" hoek getekend."
1750 HO=0:VE=VT:GOSUB 110
1760 PRINT" ---> Tik op een toets...";
1770 GOSUB 210:RETURN
1800 REM === TEKST 2 ===
1810 GOSUB 100:FO=0
1820 PRINT" EEN PAAR VRAGEN"
1830 PRINT" ==============="
1840 PRINT:PRINT" Een net vel papier heeft rechte"
1850 PRINT:PRINT" hoeken."
1860 PRINT:PRINT" Hoeveel graden is een rechte hoek";
1870 INPUT H
1880 IF H<>90 THEN FO=1:GOTO 1900
1890 PRINT:PRINT" Dat is goed beantwoord.":GOTO 1910
1900 PRINT:PRINT" NEE, een rechte hoek is 90 graden!"
1910 PRINT
1920 PRINT:PRINT" Hoeveel graden is een cirkel";
1930 INPUT H
1940 IF H<>360 THEN FO=1:GOTO 1960
1950 PRINT:PRINT" Dat is goed beantwoord.":GOTO 1970
1960 PRINT:PRINT" NEE, een cirkel is 360 graden."
1970 PRINT:IF FO=0 THEN 2000
1980 PRINT:PRINT" We gaan die hoeken nog een keer"
1990 PRINT:PRINT" bekijken, probeer 90 en 360!"
2000 GOSUB 1750
2010 RETURN
2200 REM === TEKST 3 ===
2210 GOSUB 100
2220 PRINT:PRINT" Om een hoek van 50 graden te kunnen"
2230 PRINT:PRINT" tekenen heb je een GRADENBOOG nodig."
2240 PRINT:PRINT" Met een PASSER en een LINIAAL kun"
2250 PRINT:PRINT" je ook een hoek uitzetten."
2260 PRINT:PRINT" Wat je daarvoor moet weten is de"
2270 PRINT:PRINT" VERHOUDING VAN 2 ZIJDEN VAN EEN"
2280 PRINT:PRINT" RECHTHOEKIGE DRIEHOEK die de"
2290 PRINT:PRINT" gezochte hoek bevat."
2300 PRINT:PRINT" De eerste verhouding die we leren"
2310 PRINT:PRINT" kennen is DE SINUS"
2320 GOSUB 1750
2330 GOSUB 100
2340 PRINT:PRINT" SINUS = OVERSTAANDE RECHTHOEKSZIJDE"
2350 PRINT:PRINT" GEDEELD DOOR SCHUINE ZIJDE."
2360 PRINT:PRINT" In onze tekening is de schuine zijde"
2370 PRINT:PRINT" tot aan de cirkel altijd precies 1"
2380 PRINT:PRINT" Dan is de SINUS van de hoek gelijk"
2390 PRINT:PRINT" aan de Y-WAARDE van het SNIJPUNT"
2400 PRINT:PRINT" van de SCHUINE ZIJDE en de CIRKEL."
2410 PRINT:PRINT" De sinus kan niet groter zijn dan 1"
2420 PRINT:PRINT" en niet kleiner dan -1"
2430 GOSUB 1750
2440 RETURN
2600 REM === TEKST 4 ===
2610 GOSUB 100
2620 PRINT:PRINT" De tweede verhouding die we leren"
2630 PRINT:PRINT" is de COSINUS."
2640 PRINT:PRINT" COSINUS = AANLIGGENDE RECHTHOEKSZIJDE"
2650 PRINT:PRINT" GEDEELD DOOR SCHUINE ZIJDE"
2660 PRINT:PRINT" In onze tekening is de schuine zijde"
2670 PRINT:PRINT" tot aan de cirkel =1, dus de cosinus"
2680 PRINT:PRINT" is gelijk aan de X-WAARDE van"
2690 PRINT:PRINT" het SNIJPUNT van de CIRKEL en de"
2700 PRINT:PRINT" SCHUINE ZIJDE."
2710 PRINT:PRINT" De cosinus kan niet groter zijn dan 1"
2720 PRINT:PRINT" en niet kleiner dan -1"
2730 GOSUB 1750:RETURN
2900 REM === TEKST 5 ===
2910 GOSUB 100
2920 PRINT:PRINT" We krijgen nu tegelijk de SINUS en"
2930 PRINT:PRINT" de COSINUS te zien."
2940 PRINT:PRINT" OPDRACHT":PRINT" ========"
2950 PRINT:PRINT" Maak nu op een vel papier een grafiek"
2960 PRINT:PRINT" van het verloop van de SINUS en de"
2970 PRINT:PRINT" COSINUS tussen 0 en 360 graden."
2980 PRINT:PRINT
2990 GOSUB 1750:RETURN
3000 REM === STIPPENSCHETS ===
3010 GOSUB 600:CN=0
3020 VE=0.5
3030 FOR N=-12 TO 12
3040 HO=0.5+(N*0.03)
3050 GOSUB 620:NEXT N
3060 HO=0.5
3070 FOR N=-12 TO 12
3080 VE=0.5+(N*0.04)
3090 GOSUB 620:NEXT N
3100 A=0.174533
3110 FOR N=1 TO 35
3120 HO=0.5+0.3*COS(N*A)
3130 VE=0.5-0.4*SIN(N*A)
3140 GOSUB 620:NEXT N
3150 RETURN
3160 REM === INPUT HOEK ===
3170 GOSUB 100
3180 PRINT:PRINT"HOEVEEL GRADEN";
3190 INPUT HK:HK=INT(HK)
3200 RETURN
3210 REM === TEKEN DE HOEK ===
3220 GD=.0174532
3230 TK=1:IF HK<0 THEN TK=-1
3240 HO=.5:VE=.5:GOSUB 620
3250 HO=0.9:GOSUB 630
3260 CN=1
3270 FOR N=1 TO 12
3280 HO=.5+(N*.03):GOSUB 620
3290 NEXT N
3300 CN=0
3310 SR=HK:GOSUB 300:L=LEN(SR$)
3320 HO=.82:VE=.49-TK*.05:GOSUB 650
3330 HO=HO+L*.025:VE=VE-.02
3340 SR$="o":GOSUB 650
3350 HO=.8:VE=.5:GOSUB 620
3360 FOR N=0 TO HK STEP TK
3370 HO=.5+.3*COS(N*GD)
3380 VE=.5-.4*SIN(N*GD)
3390 GOSUB 630:NEXT N
3400 HO=.5+.372*COS(HK*GD)
3410 VE=.5-.496*SIN(HK*GD)
3420 GOSUB 620
3430 HO=.5:VE=.5:GOSUB 630
3440 RETURN
3450 REM === NOG EENS? ===
3460 HO=.3:VE=.95:SR$="Nog een keer?"
3470 GOSUB 650:GOSUB 210
3480 RETURN
3490 REM === MARKEER SINUS ===
3500 SH=SIN(HK*GD):CH=COS(HK*GD)
3510 PH=.5+.3*CH:PV=.5-.4*SH
3520 VE=.5-.4*SH:CN=0
3530 K=-1:IF CH<0 THEN K=1
3540 FOR HO=PH TO .5 STEP K*.01
3550 GOSUB 620:NEXT HO
3560 FOR N=1 TO 10
3570 CN=1-CN
3590 HO=.49:GOSUB 620
3600 HO=.51:GOSUB 630
3610 SV=0:SP=100:SD=2:GOSUB 400
3620 NEXT N
3630 RETURN
3640 REM === BEREKEN SINUS ===
3650 SR=HK:GOSUB 300:HK$=SR$
3660 HO=0:VE=0:SR$="SIN "+HK$+" = ":GOSUB 650
3670 HO=HO+.025*LEN(SR$)
3680 SR=SH:CT=7:CN=4:GOSUB 310:CN=0:GOSUB 650
3690 RETURN
3700 REM === MARKEER COSINUS ===
3710 SH=SIN(HK*GD):CH=COS(HK*GD)
3720 PH=.5+.3*CH:PV=.5-.4*SH
3730 HO=.5+.3*CH:CN=0
3740 K=1:IF SH<0 THEN K=-1
3750 FOR VE=PV TO .5 STEP K*.01
3760 GOSUB 620:NEXT VE
3770 FOR N=1 TO 10
3780 CN=1-CN
3800 VE=.49:GOSUB 620
3810 VE=.51:GOSUB 630
3820 SV=0:SP=100:SD=2:GOSUB 400
3830 NEXT N
3840 RETURN
3850 REM === BEREKEN COSINUS ===
3860 SR=HK:GOSUB 300:HK$=SR$
3870 HO=.5:VE=0:SR$="COS "+HK$+" = ":GOSUB 650
3880 HO=HO+.025*LEN(SR$)
3890 SR=CH:CT=7:CN=4:GOSUB 310:CN=0:GOSUB 650
3900 RETURN
30000 REM ============================
30010 REM KENNISMAKING MET SINUS EN
30020 REM COSINUS VOOR DE BRUGKLAS.
30030 REM ============================
30040 REM GEMAAKT OP EEN BBC COMPUTER
30050 REM 2 MAART 1987
30060 REM DOOR
32000 REM Ch.W.Brederode
32010 REM Von Bonninghausenlaan 33
32020 REM 2161 ES  LISSE
32100 REM TROS-RADIO dd 870513
32110 REM incl corr. dd 870822

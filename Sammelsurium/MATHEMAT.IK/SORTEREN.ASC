1000 a=1000:GOTO 20:REM alpha-sort
1010 GOTO 2990:REM initialisatie
1020 aw=0:REM aantal woorden
1030 ho=0:ve=4+2*aw
1040 IF aw<5 THEN 1060
1050 ho=19:ve=2*aw-6
1060 GOSUB 110
1070 sr=aw+1:ct=2:cn=0:GOSUB 310
1080 PRINT sr$;" ";:ml=9:GOSUB 2750:IF a$="" THEN 1140
1090 GOSUB 1280:IF aw<10 THEN 1030
1100 GOSUB 2440
1110 ho=0:ve=23:GOSUB 110
1120 PRINT"Met <RETURN> start het sorteren...  ";
1130 GOSUB 210:IF ASC(in$)<>13 THEN 1130
1140 IF aw=0 THEN 3030
1150 GOSUB 1520:REM overzicht
1160 GOSUB 1410:REM snelsorteer
1170 ho=29:FOR i=1 TO aw
1180 ve=2*i+1:GOSUB 110:PRINT s$(i):NEXT i
1190 ho=0:ve=23:GOSUB 110
1200 PRINT"Wilt U dat nu wat langzamer zien ? ";:GOSUB 2920
1210 IF y=0 THEN 1270
1220 IF y<0 THEN 1190
1230 GOSUB 1520:REM overzicht
1240 GOSUB 1710:REM opschuiven
1250 GOSUB 1790:REM traagsorteer
1260 GOSUB 2590:REM spatie
1270 GOTO 950
1280 REM subr controleer  woord  a$
1290 le=0
1300 FOR vk=1 TO LEN(a$)
1310 vb=ASC(MID$(a$,vk,1))
1320 IF (vb>64) AND (vb<91) THEN 1350
1330 IF (vb>64+hl) AND (vb<91+hl) THEN 1350
1340 le=1
1350 NEXT vk
1360 IF le=0 THEN 1390
1370 ee$="Woord bevat niet alleen maar letters !"
1380 GOSUB 2490:GOTO 1400
1390 aw=aw+1:wo$(aw)=a$
1400 RETURN
1410 REM subr snelsorteer
1420 GOSUB 1590
1430 IF aw=1 THEN 1510
1440 FOR i=1 TO aw-1
1450 FOR j=i+1 TO aw
1460 IF w$(i)<=w$(j) THEN 1490
1470 ft$=w$(i):w$(i)=w$(j):w$(j)=ft$
1480 ft$=s$(i):s$(i)=s$(j):s$(j)=ft$
1490 NEXT j
1500 NEXT i
1510 RETURN
1520 REM subr overzicht
1530 GOSUB 100:PRINT"Ongesorteerd";
1540 ho=29:ve=0:GOSUB 110:PRINT"Gesorteerd"
1550 FOR i=1 TO aw
1560 ho=0:ve=2*i+1:GOSUB 110:PRINT wo$(i)
1570 NEXT i
1580 RETURN
1590 REM subr zet om in hoofdletters
1600 FOR i=1 TO aw
1610 a$=""
1620 FOR j=1 TO LEN(wo$(i))
1630 b=ASC(MID$(wo$(i),j,1))
1640 IF b<96 THEN b=b+hl
1650 a$=a$+chr$(b)
1660 NEXT j
1670 w$(i)=a$
1680 s$(i)=wo$(i)
1690 NEXT i
1700 RETURN
1710 REM subroutine shift
1720 FOR i=1 TO 29:ho=i-1
1730 FOR j=1 TO aw
1740 ve=2*j+1:GOSUB 110
1750 PRINT MID$(wo$(j)+LEFT$(le$,29),i,1);wo$(j)
1760 NEXT j
1770 NEXT i
1780 RETURN
1790 REM subr traagsorteer
1800 GOSUB 1590
1810 IF aw=1 THEN RETURN
1820 FOR x=1 TO aw-1
1830 b=x
1840 FOR y=x+1 TO aw
1850 IF w$(b)>w$(y) THEN b=y
1860 NEXT y
1870 IF b>x THEN GOSUB 1900
1880 NEXT x
1890 RETURN
1900 REM subr verwissel  x en  b
1910 ir=2*x+1:jr=2*b+1
1920 jc=28-LEN(s$(b))
1930 ic=jc-LEN(s$(x))-1
1940 IF x+1=b THEN ic=29
1950 REM -- schuIF  s$(b) eruit --
1960 FOR ho=29 TO jc STEP -1
1970 GOSUB 2300
1980 ve=jr:GOSUB 110:PRINT s$(b);" "
1990 NEXT ho
2000 REM -- schuif s$(x) eruit --
2010 IF ic>28 THEN 2060
2020 FOR ho=29 TO ic STEP -1
2030 GOSUB 2300
2040 ve=ir:GOSUB 110:PRINT s$(x);" "
2050 NEXT ho
2060 REM schuIF  w$(b) op, w$(x) neer
2070 ho=jc:FOR sk=jr TO ir+1 STEP -1
2080 GOSUB 2300
2090 ve=sk:GOSUB 110:PRINT LEFT$(le$,LEN(s$(b)))
2100 ve=sk-1:GOSUB 110:PRINT s$(b)
2110 NEXT sk
2120 ho=ic:FOR sk=ir TO jr-1
2130 GOSUB 2300
2140 ve=sk:GOSUB 110:PRINT LEFT$(le$,LEN(s$(x)))
2150 ve=sk+1:GOSUB 110:PRINT s$(x):NEXT sk
2160 REM schuif strings terug
2170 FOR ho=jc TO 28
2180 GOSUB 2300
2190 ve=ir:GOSUB 110:PRINT" ";s$(b)
2200 NEXT ho
2210 IF ic>28 THEN 2260
2220 FOR ho=ic TO 28
2230 GOSUB 2300
2240 ve=jr:GOSUB 110:PRINT" ";s$(x)
2250 NEXT ho
2260 t$=w$(x):w$(x)=w$(b):w$(b)=t$
2270 t$=s$(x):s$(x)=s$(b):s$(b)=t$
2280 xx=1:GOSUB 2560
2290 RETURN
2300 REM subr pauze
2310 xx=0.1:GOSUB 2560
2320 RETURN
2330 REM subr header
2340 GOSUB 100:ho=10:ve=1:GOSUB 110
2350 PRINT"ALFABETISCH SORTEREN"
2360 ho=10:ve=2:GOSUB 110
2370 PRINT"--------------------"
2380 RETURN
2390 REM subr instructies
2400 ho=0:ve=19:GOSUB 110
2410 PRINT"Druk op RETURN na elk ingevoerd woord"
2420 PRINT:PRINT"Druk weer op RETURN voor het sorteren."
2430 RETURN
2440 REM subr wis onderste regels
2450 ho=0:FOR ve=19 TO 23
2460 GOSUB 110:PRINT le$;
2470 NEXT ve
2480 RETURN
2490 REM subr foutmelding ee$
2500 GOSUB 2440:REM wis onderste regels
2510 ho=19-LEN(ee$)/2:ve=20:GOSUB 110:PRINT ee$
2520 GOSUB 2590:REM spatie
2530 GOSUB 2440:REM wis onderste regels
2540 GOSUB 2390:REM instructies
2550 RETURN
2560 REM proc wacht xx seconden
2570 FOR zz=1 TO 100*xx:NEXT zz
2580 RETURN
2590 REM subr spatie
2600 ho=2:ve=23:GOSUB 110
2610 PRINT"Druk op de SPATIEBALK voor vervolg";:GOSUB 2720
2620 GOSUB 210:IF in$<>" " THEN 2620
2630 RETURN
2640 REM subr titelpagina
2650 GOSUB 100:ho=14:ve=8:GOSUB 110
2660 PRINT"ALFABETISCH"
2670 ho=16:ve=11:GOSUB 110
2680 PRINT"SORTEREN"
2690 FOR ho=1 TO 1000:GOSUB 200:IF in$<>"" THEN ho=1000
2700 NEXT ho
2710 RETURN
2720 REM subr buffer leegmaken
2730 GOSUB 200:IF in$<>"" THEN 2730
2740 RETURN
2750 REM subr invoer van een string
2760 a$=""
2770 GOSUB 120:FOR oo=1 TO ml:PRINT" ";:NEXT oo
2780 GOSUB 110
2790 GOSUB 2720:REM buffer legen
2800 GOSUB 210:gb=ASC(in$):IF gb=13 THEN 2910
2810 IF (gb=dl) AND (a$="") THEN GOSUB 250:GOTO 2790
2820 IF gb<>dl THEN 2870
2830 IF LEN(a$)=1 THEN a$="":GOTO 2850
2840 a$=LEFT$(a$,LEN(a$)-1)
2850 GOSUB 120:ho=ho-1:GOSUB 110:PRINT" ";:GOSUB 110
2860 GOTO 2790
2870 IF (LEN(a$)=ml) OR (gb<33) THEN 2900
2880 IF (gb>127) AND (gb<160) THEN 2900
2890 PRINT in$;:a$=a$+in$:GOTO 2800
2900 GOSUB 250:GOTO 2800
2910 RETURN
2920 REM subr ja of nee
2930 ml=3:GOSUB 2750
2940 ob$=LEFT$(a$,1)
2950 y=-1
2960 IF (ob$="J") OR (ob$="j") THEN y=1
2970 IF (ob$="N") OR (ob$="n") THEN y=0
2980 RETURN
2990 REM initialisering
3000 DIM wo$(10),w$(10),s$(10)
3010 le$="                                       "
3020 hl=ASC("A")-ASC("a"):IF hl<0 THEN hl=-hl
3030 GOSUB 100
3040 GOSUB 2640:GOSUB 2330
3050 ho=0:ve=7:GOSUB 110
3060 PRINT"Tik hoogstens tien woorden in,"
3070 PRINT"elk met maximaal negen letters."
3080 PRINT:PRINT"Het maakt niet uit of het HOOFDletters"
3090 PRINT"of kleine letters zijn."
3100 PRINT:PRINT"Nadat U de woorden hebt ingetikt, zal"
3110 PRINT"de computer ze sorteren."
3120 PRINT:PRINT"Tik nu het karakter in wat U gebruikt"
3130 PRINT"om een karakter te wissen:";:GOSUB 210
3140 dl=ASC(in$)
3150 GOSUB 2590
3160 GOSUB 2330:GOSUB 2390
3170 GOTO 1020
30000 REM "ALPHABETISCH SORTEREN    "
30010 REM een programma van
30020 REM "    Andrew Chapman       "
30030 REM           &
30040 REM "   Jerry Temple-Fry      "
30050 REM   (netherhall school)
30060 REM           &
30070 REM "   Richard G Warner      "
30080 REM oorspronkelijk geschreven
30090 REM        voor de
30100 REM "BBC model A microcomputer"
30110 REM vertaald naar
30120 REM 'eenvoudig' basic
30130 REM en geschikt gemaakt voor
30140 REM "BASICODE-2      door:    "
30150 REM "       J. Haubrich       "
30160 REM  december 1982

1000 A=200:GOTO 20:REM  ENERGY CONSUMPTION
1010 DIM A(12)
1020 VM=VE
1030 GOSUB 100:T=0:MO=12:XX=0:SO=0:Q=0
1040 HO=2:VE=0:GOSUB 110
1050 SR$="==============================":GOSUB 150
1060 VE=1:GOSUB 110
1070 SR$="E N E R G Y  C O N S U M P T I O N":GOSUB 150
1080 VE=2:GOSUB 110
1090 SR$="==============================":GOSUB 150
1100 RESTORE :READ A$,B$,C$
1110 B=VAL(B$):C=VAL(C$)
1120 HO=3:VE=4:GOSUB 110
1130 PRINT A$
1140 HO=25:GOSUB 110
1150 PRINT B;"-";C
1160 PRINT:PRINT"  Start by filling in the year."
1170 PRINT"          ";:SR$="( DEMO 1985 )":GOSUB 150
1180 PRINT
1190 PRINT:PRINT"ELECTRICITY comes first and"
1200 PRINT"  then GAS and WATER, separated by"
1210 PRINT"        pressing the space bar."
1220 PRINT:PRINT:PRINT"      Enter 0000 to exit."
1230 PRINT:PRINT"     ";:SR$="Press ? for explanation."
1240 GOSUB 150
2000 REM  ENTER START.
2010 JA=0:FO=0:JA$=""
2020 HO=30:VE=22:GOSUB 110:PRINT"...."
2030 HO=5:VE=22:GOSUB 110
2040 PRINT"Which year do you want to see :  ";
2050 REM  CLEAR KEYBOARD BUFFER
2060 GOSUB 200:IF IN>0 THEN 2060
2070 GOSUB 210:J1$=IN$:PRINT J1$;
2080 IF IN=13 THEN 2010
2090 IF J1$="?" THEN 6000
2100 IF J1$="0" THEN 2120
2110 J1=VAL(J1$):IF J1=0 THEN 2010
2120 GOSUB 210:J2$=IN$:PRINT J2$;
2130 IF IN=13 THEN 2010
2140 IF J2$="0" THEN 2160
2150 J2=VAL(J2$):IF J2=0 THEN 2010
2160 GOSUB 210:J3$=IN$:PRINT J3$;
2170 IF IN=13 THEN 2010
2180 IF J3$="0" THEN 2200
2190 J3=VAL(J3$):IF J3=0 THEN 2010
2200 GOSUB 210:GOSUB 250:J4$=IN$:PRINT J4$;
2210 IF IN=13 THEN 2010
2220 IF J4$="0" THEN 2240
2230 J4=VAL(J4$):IF J4=0 THEN 2010
2240 JA$=J1$+J2$+J3$+J4$
2250 JA=VAL(JA$)
2260 DE$="I==I"
2270 IF JA=1985 THEN DE$="DEMO"
2280 IF JA$<>"0000" THEN 3000
2290 GOSUB 100:HO=9:VE=11:GOSUB 110
2300 SR$="END OF PROGRAM":GOSUB 150
2310 HO=7:VE=VM:GOSUB 110
2320 PRINT"Press a key to stop. ";
2330 GOSUB 210:GOTO 950
3000 IF (JA<B-1) OR (JA>C) THEN FO=1
3010 IF FO<>1 THEN 3090
3020 HO=5:VE=22:GOSUB 110
3030 PRINT"  ";:SR$="Wrong statement !"
3040 GOSUB 150;:PRINT"    ";
3050 SD=25:GOSUB 450:T=0
3060 GOSUB 110
3070 PRINT"                              "
3080 GOTO 2010
3090 HO=5:VE=22:GOSUB 110
3100 PRINT"  ";:SR$="Please wait."
3110 GOSUB 150:PRINT"     "
3120 GOTO 5000
4000 SO=A(1)+A(2)+A(3)+A(4)+A(5)+A(6)+A(7)
4010 SO=SO+A(8)+A(9)+A(10)+A(11)+A(12)
4020 T=T+1
4030 B1$="Kwh.":B2$="M3.":B3$="ELECTRICITY"
4040 B4$="    GAS.":B5$="  WATER."
4050 IF T=1 THEN A1$=B1$:A2$=B3$
4060 IF T=2 THEN A1$=B2$:A2$=B4$
4070 IF T=3 THEN A1$=B2$:A2$=B5$
4080 FOR I=1 TO 12
4090 IF I=1 THEN A=A(I)
4100 IF A(I)>A THEN A=A(I)
4110 NEXT I
4120 A=ABS(28/A)
4130 GOSUB 100
4140 PRINT"ENERGY CONSUMPTION"
4150 HO=17:VE=0:GOSUB 110:SR=JA:GOSUB 300:GOSUB 150
4160 HO=31:GOSUB 110:PRINT A2$
4170 BB=SO/MO:PRINT"Tot: "
4180 HO=4:SR=SO:CT=8:CN=1:GOSUB 310:VE=1:GOSUB 110
4190 PRINT SR$
4200 HO=14:VE=1:GOSUB 110:PRINT A1$
4210 HO=20:GOSUB 110:PRINT DE$
4220 HO=25:CT=6:CN=1:SR=BB:GOSUB 310
4230 VE=1:GOSUB 110:PRINT SR$
4240 HO=33:GOSUB 110:PRINT"/ month"
4250 HO=0:VE=3:GOSUB 110:PRINT"January"
4260 AX=A(1):HO=10:GOSUB 5530
4270 HO=20:VE=3:GOSUB 110:PRINT"July"
4280 AX=A(7):HO=31:GOSUB 5530
4290 HO=0:VE=4:GOSUB 110:PRINT"February"
4300 AX=A(2):HO=10:GOSUB 5530
4310 HO=20:VE=4:GOSUB 110:PRINT"August"
4320 AX=A(8):HO=31:GOSUB 5530
4330 HO=0:VE=5:GOSUB 110:PRINT"March"
4340 AX=A(3):HO=10:GOSUB 5530
4350 HO=20:VE=5:GOSUB 110:PRINT"September"
4360 AX=A(9):HO=31:GOSUB 5530
4370 HO=0:VE=6:GOSUB 110:PRINT"April"
4380 AX=A(4):HO=10:GOSUB 5530
4390 HO=20:VE=6:GOSUB 110:PRINT"October"
4400 AX=A(10):HO=31:GOSUB 5530
4410 HO=0:VE=7:GOSUB 110:PRINT"May"
4420 AX=A(5):HO=10:GOSUB 5530
4430 HO=20:VE=7:GOSUB 110:PRINT"November"
4440 AX=A(11):HO=31:GOSUB 5530
4450 HO=0:VE=8:GOSUB 110:PRINT"June"
4460 AX=A(6):HO=10:GOSUB 5530
4470 HO=20:VE=8:GOSUB 110:PRINT"December"
4480 AX=A(12):HO=31:GOSUB 5530
4490 HO=0:VE=10:GOSUB 110
4500 MN$="JanFebMarAprMayJunJulAugSepOctNovDec"
4510 FOR I=1 TO 12
4520 PRINT MID$(MN$,3*I-2,3);":";
4530 SR$="":ST$="*":FOR J=1 TO A(I)*A
4540 SR$=SR$+ST$
4550 NEXT J
4560 Q=LEN(SR$):SX$="":SP$=" "
4570 FOR QQ=1 TO (29-Q):SX$=SX$+SP$:NEXT QQ
4580 SR$=SR$+SX$
4590 GOSUB 150
4600 PRINT
4610 NEXT I
4620 HO=0:VE=VM:GOSUB 110
4630 SR$="Press spacebar to continue:"
4640 GOSUB 150
4650 GOSUB 210:IF IN$<>" " THEN GOSUB 250:GOTO 4650
4660 GOSUB 100:MO=12:RETURN
5000 RESTORE:READ A$,B$,C$
5010 READ A
5020 HO=16:VE=20:GOSUB 110:XX=XX+1:PRINT XX
5030 IF A<JA*10 THEN 5010
5040 Q=Q+1:FOR I=1 TO 12:READ A(I)
5050 IF A(I)>9999.9 THEN GOSUB 5560
5060 IF A(I)=0 THEN MO=MO-1
5070 NEXT I
5080 IF A(1)<>.01 THEN 5500
5090 HO=6:VE=20:GOSUB 110
5100 PRINT"  No DATA available ."
5110 PRINT
5120 IF T=0 THEN PRINT"       ";:SR$="***  Electricity  ***"
5130 IF T=0 THEN GOSUB 150
5140 IF T=1 THEN PRINT"       ";:SR$="***    Gas    ***"
5150 IF T=1 THEN GOSUB 150
5160 IF T=2 THEN PRINT"       ";:SR$="***   Water   ***"
5170 IF T=2 THEN GOSUB 150
5180 SD=10:GOSUB 450
5190 MO=12
5200 T=T+1:IF T=3 THEN 1030
5210 GOTO 5040
5500 GOSUB 4000
5510 IF T=3 THEN 1030
5520 GOTO 5040
5530 CT=7:CN=1:SR=AX:GOSUB 310
5540 GOSUB 110:PRINT SR$
5550 RETURN
5560 GOSUB 100
5570 HO=0:VE=2:GOSUB 110
5580 PRINT"    ******";JA;"*** ";
5590 IF Q=1 THEN PRINT"ELECTRICITY ******"
5600 IF Q=2 THEN PRINT"GAS ******"
5610 IF Q=3 THEN PRINT"WATER ******"
5620 PRINT:PRINT:PRINT
5630 PRINT"You entered DATA greater than"
5640 PRINT:PRINT"the allowed value of 9999.9"
5650 PRINT:PRINT"per month, change this or adjust it"
5660 PRINT:PRINT"in program at 10000 to 13070."
5670 PRINT:PRINT"This is because otherwise the numbers are no longer"
5680 PRINT:PRINT"neatly printed one after the other."
5690 HO=0:VE=VM:GOSUB 110
5700 PRINT"Press space bar to continue : ";
5710 GOSUB 210
5720 IF IN$=" " THEN RETURN
5730 GOTO 5690
6000 GOSUB 100
6010 PRINT"With this program you can visualize your energy"
6020 PRINT"consumption per month."
6030 PRINT:The total is kept at the top left,"
6040 PRINT"at the top right is the average"
6050 PRINT"consumption per month."
6060 PRINT:PRINT"In DATA line 25000 you can enter the text:"
6070 PRINT"DATA from 25000 to your own text,"
6080 PRINT"ex. your name or street + no."
6090 PRINT:PRINT"Don't make the text longer than"
6100 PRINT"the existing text."
6110 PRINT:PRINT"The 1986 / 2000 data does not change."
6120 HO=0:VE=VM:GOSUB 110
6130 SR$="Press space bar to continue."
6140 GOSUB 150
6150 GOSUB 210:IF IN$=" " THEN 7000
6160 GOTO 6120
7000 GOSUB 100
7010 PRINT"Build DATA rules."
7020 PRINT"==================="
7030 PRINT:PRINT"The program first searches for"
7040 PRINT"the given year*10."
7050 PRINT:PRINT"Then the program will read"
7060 PRINT"and process the first 12 data."
7070 PRINT:PRINT"The first series is ELECTRiCITY."
7080 PRINT"The second series is GAS."
7090 PRINT"The third series is then for the WATER."
7100 PRINT:PRINT"The . in the DATA lines is a decimal"
7110 PRINT"point, so pay attention to this when"
7120 PRINT"entering data."
7130 PRINT:PRINT"The number 0.01 indicates to the"
7140 PRINT"program that there is no DATA available,"
7150 PRINT"you just change this number to the desired"
7160 PRINT"value when you fill in the DATA lines."
7170 REM PRINT"gaat vullen."
7180 HO=0:VE=VM:GOSUB 110
7190 SR$="Press space bar to continue."
7200 GOSUB 150
7210 GOSUB 210:IF IN$=" " THEN 7230
7220 GOTO 7180
7230 GOSUB 100
7240 PRINT"The largest number you are allowed to enter"
7250 PRINT"is 9999.9 , and the smallest is 0.1"
7290 PRINT
7300 PRINT:PRINT:PRINT"Have fun with this program."
7310 HO=0:VE=VM:GOSUB 110
7320 SR$="Press space bar to start."
7330 GOSUB 150
7340 GOSUB 210:IF IN$=" " THEN 1030
7350 GOTO 7310
25000 REM  THE NUMBERS 1986 AND 2000 DO NOT CHANGE.
25010 DATA "DATA from 25000","1986","2000"
25020 REM  data demo series for 1985.
25030 DATA 19850
25040 DATA .2,.4,.6,.8,1,1.2,1.2,1,.8,.6,.4,.2
25050 DATA 90,70,50,30,10,3,3,10,30,50,70,90
25060 DATA 6,5,7,6,7,5,8,8,5,6,5,7
25070 REM 
25080 REM  data Electra/Gas/Water 1986.
25090 DATA 19860
25100 DATA 0.01,0,0,0,0,0,0,0,0,0,0,0
25110 DATA 0.01,0,0,0,0,0,0,0,0,0,0,0
25120 DATA 0.01,0,0,0,0,0,0,0,0,0,0,0
25130 REM 
25140 REM  data Electra/Gas/Water 1987.
25150 DATA 19870
25160 DATA 0.01,0,0,0,0,0,0,0,0,0,0,0
25170 DATA 0.01,0,0,0,0,0,0,0,0,0,0,0
25180 DATA 0.01,0,0,0,0,0,0,0,0,0,0,0
25190 REM 
25200 REM 
25210 REM  data Electra/Gas/Water 2000.
25220 DATA 20000
25230 DATA 0.01,0,0,0,0,0,0,0,0,0,0,0
25240 DATA 0.01,0,0,0,0,0,0,0,0,0,0,0
25250 DATA 0.01,0,0,0,0,0,0,0,0,0,0,0
32000 REM  *******************
32010 REM  ** AANGEPAST AAN **
32020 REM  ** BASICODE  III **
32030 REM  **  H. v.  Leen  **
32040 REM  **   HEEMSKERK   **
32050 REM  *******************
32060 REM  **   PROGRAMMA   **
32070 REM  **    ENERGIE    **
32080 REM  **  GESCHREVEN   **
32090 REM  **  OP     EEN   **
32100 REM  **  P2000T/102   **
32110 REM  *******************
32120 REM  **  21 12  1986  **
32130 REM  *******************
32200 REM TROS-radio dd 870204

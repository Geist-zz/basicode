1000 A=200:GOTO 20:REM VERZINSUNGII
1010 REM 
1020 REM  H & M FILLINGER
1030 REM  17.04.1984
1040 REM 
1050 REM TITEL
1060 GOSUB 100
1070 PRINT :PRINT :PRINT :PRINT 
1080 PRINT "     SCHULDZINSEN"
1090 PRINT :PRINT :PRINT 
1100 PRINT "  CO. H.& M.FILLINGER"
1110 PRINT "      MCMLXXXIV"
1120 PRINT 
1130 REM 
1200 REM VARIABLENLISTE
1210 REM   AN...FAELLIGKEIT
1220 REM   AW...AUSWAHL
1230 REM   D....AUSGABEWAHL
1240 REM   JA...ANFANGSJAHR
1250 REM   JE...ENDJAHR
1260 REM   JZ...FAELLIGKEIT
1270 REM   MA...ANFANGSMONAT
1280 REM   ME...ENDMONAT
1290 REM   MZ...FAELLIGKEIT
1300 REM   S....ZWISCHENSUMME
1310 REM   S0...ANFANGSSCHULD
1320 REM   S1...TILGUNG
1330 REM   S2...RATE
1340 REM   S3...ZINSEN
1350 REM   S4...HILFSVARIABLE ZU SP
1360 REM   SB...BEARBEITUNGSGEB.%
1370 REM   SD...DAMNUM
1380 REM   SG...BEARBEITUNGSGEB.DM
1390 REM   SE...ENDSCHULD
1400 REM   SP...ZINSSATZ
1410 REM   SZ...ZINSEN
1420 REM   X....HILFSVARIABLE ZU AN
1430 REM   Z....ZEILEN MONITOR
1440 REM   Z1...ZEILENZAEHLER
1450 REM   I,L..LAUFVARIABLE
1460 REM   SA...ABWEICHENDE RATE
1470 REM 
1480 REM 
1490 GOSUB 5000
1500 REM 
1510 FOR I=1 TO 2000:NEXT I
1520 REM 
1530 REM 
2000 REM MENUE
2010 GOSUB 100:AW=0
2020 PRINT "-1- RATENKREDIT"
2030 PRINT "-2- HYPOTHEKEN U.SONSTIGES"
2040 PRINT "-0- PROGRAMM BEENDEN"
2050 INPUT AW
2060 IF AW=0 THEN GOTO 950
2070 IF (AW<1) OR (AW>2) THEN 2010
2080 GOSUB 100
2090 PRINT "WIEVIEL ZEILEN HAT IHR MONITOR";
2100 INPUT Z
2110 GOSUB 100
2120 PRINT "-1- AUSGABE DRUCKER"
2130 PRINT "-2- AUSGABE MONITOR"
2140 INPUT D
2150 IF (D<1) OR (D>2) THEN 2120
2160 GOSUB 100
2170 ON AW GOSUB 3000,4000
2180 GOSUB 5000
2190 GOTO 2000
2200 REM 
2210 REM 
3000 REM MENUETEIL 1
3010 PRINT "BEGINN DER BERECHNUNG"
3020 PRINT "MONAT";:INPUT MA
3030 IF (MA<1) OR (MA>12) THEN 3020
3040 PRINT "JAHR";:INPUT JA
3050 IF JA<1000 THEN JA=JA+1900
3060 PRINT "KREDITBETRAG";:INPUT S0
3070 PRINT "ZINSEN PRO MONAT IN %";:INPUT SP
3080 PRINT "BEARBEITUNGSGEBUEHR IN %";:INPUT SB
3090 SG=S0/100*SB
3100 PRINT "1. RATE";:INPUT SA
3110 PRINT "FOLGERATEN";:INPUT S2
3120 PRINT "LETZTE RATE";:INPUT SL
3130 PRINT "LAUFZEIT IN MONATEN";:INPUT LZ
3140 MZ=MA
3150 JZ=JA
3160 S=S0+SG
3170 GOSUB 100
3180 SZ=(S0*SP*LZ)/100:S=S+SZ
3190 FOR I=1 TO LZ
3200 MZ=MZ+1
3210 IF MZ>12 THEN MZ=1:JZ=JZ+1
3220 CT=2:CN=0:SR=MZ:GOSUB 310:A$=SR$+"."
3230 CT=4::SR=JZ:GOSUB 310:A$=A$+SR$
3240 CT=19:CN=2:SR=S:GOSUB 310:SR$=A$+SR$+" DM"
3250 GOSUB 6000
3260 IF I=LZ THEN SR=SL
3270 A$="RATE   "
3280 IF I=1 THEN SR=SA
3290 IF NOT (I=1) THEN SR=S2
3300 GOSUB 310:SR$=A$+SR$+" DM"
3310 GOSUB 6000:SR$="":GOSUB 6000
3320 S=S-SR
3330 NEXT I
3340 SR=S:A$="       ":GOSUB 310:SR$=A$+SR$+" DM"
3350 GOSUB 6000:SR$="":GOSUB 6000
3360 SR$="ZINSEN UND":GOSUB 6000
3370 A$="GEBUEHREN ":SR=(SA+S2*(LZ-1))-S0
3380 GOSUB 310:SR$=A$+SR$+" DM"
3390 GOSUB 6000:SR$="":GOSUB 6000
3400 A$="EFFEKTIVER JAHRESZINS"
3410 SR=(24*(SB+SP*LZ))/(LZ+1)
3420 CT=9:CN=1:GOSUB 310:SR$=A$+SR$+" %"
3430 GOSUB 6000
3440 SR$="NAEHERUNGSFEHLER CA. 0.2 PROZENTPUNKTE"
3450 GOSUB 6000
3460 SR$="":D=2:Z1=Z:GOSUB 6000
3470 RETURN
3480 REM 
3490 REM 
4000 REM * MENUETEIL 2 *
4010 PRINT "BEGINN DER BERECHNUNG"
4020 PRINT "MONAT";:INPUT MA
4030 PRINT "JAHR";:INPUT JA
4040 PRINT "HOEHE DER SCHULD";:INPUT S0
4050 PRINT "AUSZAHLUNGSQUOTE IN %";:INPUT SS
4060 PRINT "BEARBEITUNGSGEBUEHR IN %";:INPUT SB
4070 PRINT "SONSTIGE GEBUEHREN IN DM";:INPUT SG
4080 S0=ABS(S0):SD=S0-((S0*SS)/100)
4090 SG=SG+SD+((S0*SB)/100)
4100 S=S0
4110 PRINT "ZINSSATZ P.JAHR IN %";:INPUT SP
4120 PRINT "IHRE ZAHLUNG ERFOLGT"
4130 PRINT "-1- MONATLICH"
4140 PRINT "-2- VIERTELJAEHRLICH"
4150 PRINT "-3- HALBJAEHRLICH"
4160 PRINT "-4- JAEHRLICH"
4170 INPUT AN
4180 IF (AN<1) OR (AN>4) THEN 4120
4190 S4=SP
4200 IF AN=1 THEN S4=SP/12
4210 IF AN=2 THEN S4=SP/4
4220 IF AN=3 THEN S4=SP/2
4230 PRINT "BETRAG";:INPUT S2
4240 PRINT "SOLL DIE BERECHNUNG ERFOLGEN BIS DER"
4250 PRINT "KAPITALSTAND 0 IST";:INPUT AW$
4260 AW$=LEFT$(AW$,1)
4270 IF AW$="J" THEN ME=MA:JE=JA+200:GOTO 4340
4280 IF AW$<>"N" THEN 4240
4290 PRINT "ENDE DER BERECHNUNG"
4300 PRINT "MONAT";:INPUT ME
4310 PRINT "JAHR";:INPUT JE
4320 IF JE<JATHEN 4290
4330 IF (JE-JA) AND (ME=MA) THEN 4290
4340 IF AN=1 THEN X=1
4350 IF AN=2 THEN X=3
4360 IF AN=3 THEN X=6
4370 IF AN=4 THEN X=12
4380 LZ=(JE-JA-1)*12:CT=18:CN=2
4390 LZ=LZ+12-MA+ME:GOSUB 100
4400 MZ=MA:JZ=JA
4410 FOR L=1 TO LZ STEP X
4420 SR=S:GOSUB 310:SR$="SCHULD :"+SR$+" DM"
4430 GOSUB 6000:SR$="":GOSUB 6000
4440 MZ=MZ+X
4450 IF MZ>12 THEN MZ=MZ-12:JZ=JZ+1
4460 CT=2:CN=0:SR=MZ:GOSUB 310:A$=SR$+"."
4470 CT=4:SR=JZ:GOSUB 310:SR$=A$+SR$:GOSUB 6000
4480 CT=18:CN=2
4490 S3=S*S4/100:SZ=SZ+S3
4500 S5=S2-S3:S1=S1+S5
4510 SR=S3:GOSUB 310:SR$="ZINSEN :"+SR$+" DM"
4520 GOSUB 6000:SR=S5:GOSUB 310
4530 SR$="TILGUNG:"+SR$+" DM":GOSUB 6000
4540 SR$="":GOSUB 6000
4550 S=S-S5
4560 IF S<=0 THEN 4580
4570 NEXT L
4580 GOSUB 6000:SR=SZ:GOSUB 310
4590 SR$="SUMME ZINSEN:"+SR$+" DM"
4600 GOSUB 6000:SR=SD:GOSUB 310
4610 SR$="DAMNUM      :"+SR$+" DM"
4620 GOSUB 6000:SR=SG-SD:GOSUB 310
4630 SR$="GEBUEHREN   :"+SR$+" DM"
4640 GOSUB 6000:SR=SZ+SG:GOSUB 310
4650 SR$="SUMME KOSTEN:"+SR$+" DM"
4660 GOSUB 6000
4670 CT=10:CN=1:SR=SR*100/S0:GOSUB 310
4680 SR$="IN % DER ANGFANGSSCHULD"+SR$+" %"
4690 GOSUB 6000
4700 SR$="":GOSUB 6000
4710 D=2:Z1=Z:GOSUB 6000
4720 RETURN
5000 REM VARIABLE=0 SETZEN
5010 S0=0:SS=0:SZ=0:S1=0:S2=0:S3=0:S4=0:S=0
5020 SE=0:SA=0:SD=0:SP=0:SB=0:SG=0:Z=0:Z1=0
5030 D=0:MA=0:ME=0:MZ=0:JA=0:JE=0:JZ=0
5040 LZ=0:X=0:AN=0
5050 RETURN
5060 REM 
5070 REM 
6000 REM AUSGABE
6010 PRINT SR$
6020 Z1=Z1+1
6030 IF D=1 THEN GOSUB 350:GOSUB 360:RETURN
6040 IF Z1<(Z-2) THEN RETURN
6050 PRINT "WENN WEITER, BITTE TASTE DRUECKEN"
6060 GOSUB 210
6070 Z1=0
6080 RETURN
6090 REM 
6100 REM 

1000 A=100:GOTO 20:REM berekening van e
1010 VT=VE:HT=HO:REM bewaar schermmaten
1020 CC=7:B=10000000:REM CC cijfers per getal
1030 IF B*B+1=B*B THEN CC=CC-1:B=B/10:GOTO 1030
1040 REM Bereken CC conform rekennauwkeurigheid
1050 REM Deze berekening werkt in MSX-machines
1060 REM alleen goed als in regel 21 de opdracht
1070 REM DEFSNGA-Z wordt verwijderd.
1080 REM Daarom de volgende regel voor alle zekerheid:
1090 CC=3:B=1000
1100 GOSUB 5000:REM toelichting
2000 PRINT"Hoeveel cijfers nauwkeurigheid ";:INPUT EC
2010 PRINT:PRINT"Even rekenen : ";
2020 GOSUB 120
2030 H=B/10
2040 AB=INT(16+.6*EC):IF EC<100 THEN 2060
2050 AB=INT(35+.4545*EC)
2060 L=INT(1+EC/CC):DIM E(L)
2070 FOR N=AB TO 1 STEP -1
2080 E(1)=E(1)+H:C=0
2090 FOR I=1 TO L
2100 C=C*B+E(I):E(I)=INT(C/N):C=C-E(I)*N:NEXT I
2110 GOSUB 110:SR=N:GOSUB 300:PRINT SR$;"  ";
2120 NEXT N
3000 REM klaar met de berekening
3010 PRINT:PRINT
3020 REM print het resultaat
3030 PRINT"De eerste ";EC;" decimalen van e :"
3040 PRINT:PRINT"2,";:RL=0
3050 SR=E(1)-H:GOSUB 300:A$=SR$:AD=EC
3060 FOR J=2 TO L
3070 SR=E(J)+B:GOSUB 300:A$=A$+RIGHT$(SR$,CC)
3080 IF LEN(A$)>HT THEN GOSUB 3110
3090 NEXT J:GOSUB 3110
3100 GOSUB 5500:GOTO 950
3110 IF AD<6 THEN PRINT LEFT$(A$,AD):AD=0:A$="":GOTO 3130
3120 PRINT LEFT$(A$,5);:AD=AD-5
3130 A$=MID$(A$,6):RL=RL+6
3140 IF RL<HT-5 THEN PRINT" ";:GOTO 3160
3150 RL=0:PRINT:PRINT"  ";
3160 IF LEN(A$)<5 THEN RETURN
3170 GOTO 3110
5000 SR$="Berekening van het getal e":GOSUB 150
5010 PRINT:PRINT
5020 PRINT"Het getal 'e', oorsronkelijk bedacht"
5030 PRINT"door Leonard Euler en dus in Duitsland"
5040 PRINT"nog altijd met 'EULERSCHE ZAHL' aange-"
5050 PRINT"duid, is met een eenvoudige formule te"
5060 PRINT"berekenen:":PRINT
5070 PRINT"          1    1    1    1"
5080 PRINT" e = 1 + -- + -- + -- + -- + ..."
5090 PRINT"         1!   2!   3!   4!"
5100 PRINT:PRINT"Voor de berekening in dit programma"
5110 PRINT"wordt de formule als volgt geschreven:"
5120 PRINT
5130 PRINT"      1      1      1      1"
5140 PRINT" e=1+ - *(1+ - *(1+ - *(1+ -*(...)))"
5150 PRINT"      1      2      3      4"
5160 PRINT:PRINT"en de formule wordt van binnen naar"
5170 PRINT"buiten uitgewerkt."
5180 GOSUB 5500
5190 PRINT"Het aardige van dit programma is, dat"
5200 PRINT"het 'e' kan berekenen in vrijwel elk"
5210 PRINT"gewenst aantal decimalen. De beperking"
5220 PRINT"zit hem in het geheugen van de computer"
5230 PRINT"en in uw geduld.":PRINT
5240 PRINT"Mijn gammele oude C-64 heeft voor 100"
5250 PRINT"decimalen al 80 seconden nodig terwijl"
5260 PRINT"de Archimedes van de buurman hetzelfde"
5270 PRINT"klusje in 1.37 seconde doet en voor"
5280 PRINT"1000 decimalen amper 75 seconden tijd"
5290 PRINT"nodig heeft. Overigens, dubbel zo veel"
5300 PRINT"decimalen kost viermaal zo veel tijd,"
5310 PRINT"dus om het echt leuk te maken moet u"
5320 PRINT"wel een weekje uittrekken."
5330 PRINT:PRINT"Desondanks: veel plezier!"
5500 HO=0:VE=VT:GOSUB 110
5510 PRINT"--- spatie voor vervolg ---";
5520 GOSUB 200:IF IN<>32 THEN 5520
5530 GOSUB 100:GOSUB 150:PRINT:PRINT:RETURN
32000 REM E-berekenen is in mei '88 geschreven door
32010 REM 
32020 REM  S.G. van Dijk
32030 REM  Amsterdam
32040 REM 
32050 REM TROS-RADIO dd 880831

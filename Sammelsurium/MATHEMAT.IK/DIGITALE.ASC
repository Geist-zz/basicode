1000 a=700:GOTO 20:REM digitale klok
1010 GOSUB 100
1020 DIM a$(6),b$(9,6),cr$(7),jj(9)
1030 FOR i=1 TO 6:READ a$(i):NEXT i
1040 FOR i=0 TO 7:READ cr$(i):NEXT i
1050 FOR i=1 TO 9:READ jj(i):NEXT i
1060 PRINT"     D I G I T A L E   K L O K":PRINT 
1070 PRINT"Het volgende programma is afkomstig van"
1080 PRINT:PRINT"een  D A I  personal computer"
1090 PRINT:PRINT"Dit programma is bedoeld als demo-"
1100 PRINT:PRINT"programma voor   NOS BASICODE-2"
1110 PRINT:PRINT:PRINT:PRINT 
1120 PRINT:PRINT"    DRUK OP EEN TOETS";:GOSUB 210
1130 GOSUB 100
1140 PRINT"Tijdvertraging proefondervindelijk vast"
1150 PRINT:PRINT"te stellen."
1160 PRINT 
1170 PRINT"Tijdvertraging (0-5000)";:INPUT t:PRINT 
1180 PRINT"Aantal seconden per stap (4-60) ";:INPUT c
1190 e$=" "
1200 PRINT:PRINT"Voer nu svp de tijd in:"
1210 PRINT:PRINT"                  uren ";:INPUT u
1220 PRINT:PRINT"               minuten ";:INPUT m
1230 PRINT:PRINT"              seconden ";:INPUT s
1240 GOSUB 100
1250 n=INT(u/10):j=1:IF n=0 THEN 1270
1260 GOSUB 1440
1270 n=u-n*10:j=2:GOSUB 1440
1280 n=INT(m/10):j=3:GOSUB 1440
1290 n=m-n*10:j=4:GOSUB 1440
1300 n=INT(s/10):j=5:GOSUB 1440
1310 n=s-n*10:j=6:GOSUB 1440
1320 ho=0:ve=4:GOSUB 110
1330 FOR i=1 TO 9
1340 j=jj(i):k=i:IF i>5 THEN k=6
1350 PRINT e$;b$(j,1);e$;b$(j,2);e$;e$;b$(j,3);
1360 PRINT e$;b$(j,4);e$;e$;b$(k,5);e$;b$(k,6)
1370 NEXT i
1380 FOR i=1 TO t:NEXT i
1390 s=s+c:IF s<60 THEN 1250
1400 s=s-60:m=m+1:IF m<60 THEN 1250
1410 m=m-60:u=u+1:IF u<24 THEN 1250
1420 u=0
1430 FOR i=1 TO 9:b$(i,1)="     ":NEXT i:GOTO 1250
1440 FOR i=1 TO 5
1450 b$(i,j)=cr$(VAL(MID$(a$(i),n+1,1)))
1460 NEXT i
1470 RETURN
25000 DATA "7277574777","5211544155","5277777377"
25010 DATA "5241115151","7277177177","0000000000"
25020 DATA "     ","    X","  X  ","   XX"
25030 DATA "X    ","X   X","x","XXXXX"
25040 DATA  1,2,2,2,3,4,4,4,5
30000 REM"auteur: TH.V.LIESHOUT"
30010 REM"        POSTGALEI 5"
30020 REM"        1687 VP WOGNUM (N.H.)"

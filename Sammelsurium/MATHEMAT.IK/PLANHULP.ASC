1000 a=1000:GOTO 20:REM **planhulp**
1010 REM ++stuurprogramma++
1020 GOSUB 1500
1030 GOSUB 1700
1040 GOSUB 100
1050 IF ws=0 THEN 1100
1060 PRINT"* PLANHULP EINDE *":PRINT 
1070 PRINT"GEGEVENS OPSLAAN? J/N";
1080 GOSUB 9000:GOSUB 100
1090 IF in$="J" THEN GOSUB 20000
1100 FOR i=1 TO 10
1110 PRINT:PRINT TAB(i*2);"TOT ZIENS"
1120 NEXT i
1130 GOTO 950
1500 REM --init--
1520 nl=12:nk=3:nr=0:nc=0
1530 ct=9:cn=2:ml=8:np=9
1540 ws=0:wm=0:rv=-1
1550 bl$="                   "
1560 DIM qp$(np),ru(99,2),rv$(99)
1570 FOR i=1 TO np
1580 READ qp$(i)
1590 NEXT i
1600 GOSUB 270:w1=fr-2150
1610 RETURN
1700 REM --menu--
1710 GOSUB 100:PRINT 
1720 PRINT"      ** PLANHULP **":PRINT 
1730 PRINT"   EEN ELECTRONISCHE LEI"
1740 PRINT 
1750 PRINT"           MENU"
1760 PRINT 
1770 PRINT"      ?  UITLEG"
1780 PRINT"      0  STOPPEN"
1790 w$="      1  MODEL/LOAD"
1800 IF wm<>0 THEN w$="      1  SAVE"
1810 PRINT w$
1820 PRINT"      2  GEGEVENS"
1830 PRINT"      3  WIJZIGEN"
1840 PRINT"      4  VOORBEELD"
1850 PRINT 
1870 w$="   KIES  ?-1,4: "
1880 IF wm<>0 THEN w$="   KIES  ?-3: "
1890 PRINT w$;
1900 GOSUB 250:GOSUB 210
1910 IF in$="0" THEN 1990
1920 IF in$="?" THEN GOSUB 10000
1930 IF in$="1" THEN GOSUB 20000
1940 IF in$="2" THEN GOSUB 4000
1950 IF in$="3" THEN GOSUB 2200
1960 IF in$="4" THEN GOSUB 17000
1980 GOTO 1710
1990 RETURN
2000 REM --model--
2010 GOSUB 2400
2020 PRINT nr;" X ";nc;"MODEL? ?/J/N";
2030 GOSUB 9000
2040 IF in$="?" THEN 15300
2050 IF in$<>"J" THEN RETURN
2060 GOSUB 2100
2070 GOSUB 2700
2080 GOSUB 4000
2090 RETURN
2100 REM --dimension--
2110 DIM da(nr,nc)
2120 DIM cn$(nc),rn$(nr)
2130 wm=1
2140 RETURN
2200 REM --wijzigen--
2205 IF wm=0 THEN 9700
2210 GOSUB 100
2220 PRINT"* PLANHULP WIJZIGEN *"
2230 PRINT 
2240 PRINT"REKENFUNKTIE WISSEN? J/N"
2250 GOSUB 9000
2260 IF in$="J" THEN rv=-1
2280 PRINT 
2290 PRINT"NIEUWE RIJ- EN KOLOMNAMEN? J/N"
2300 GOSUB 9000
2310 IF in$="N" THEN 2350
2320 GOSUB 2700
2350 RETURN
2400 REM --aantallen--
2410 GOSUB 100
2420 PRINT"* PLANHULP MODEL *"
2430 PRINT 
2450 nr=0:nc=0
2460 PRINT:PRINT"AANTAL RIJEN";
2470 INPUT nr
2480 PRINT:PRINT"AANTAL KOLOMMEN";
2490 INPUT nc
2500 IF (nc>0) AND (nr>0) THEN 2550
2510 PRINT 
2520 PRINT"ZINVOLLE GETALLEN SVP"
2530 ho=0:ve=3:GOSUB 110
2540 GOTO 2460
2550 ms=(nc+1)*(nr+1)*5+(nc+nr+2)*13
2560 IF ms<w1 THEN 2600
2570 PRINT:PRINT"GEHEUGEN TEKORT.     "
2580 GOTO 2530
2600 PRINT:PRINT bl$;bl$
2610 RETURN
2700 REM --namen--
2710 GOSUB 100
2720 PRINT"* PLANHULP MODEL *"
2730 PRINT 
2740 PRINT"GEEF DE NAMEN VAN RIJEN EN KOLOMMEN"
2750 PRINT"NAMEN MAX ";ml;" KARAKTERS LANG."
2760 PRINT:PRINT"RIJ           WAS      WORDT"
2770 FOR i=1 TO nr
2780 sr=i:GOSUB 300:w$=rn$(i)
2790 rn$(i)=LEFT$("R"+sr$+bl$,ml)
2800 PRINT i;"VAN";nr;TAB(11);w$;TAB(21);
2810 INPUT w$
2820 IF w$<>"" THEN rn$(i)=LEFT$(w$+bl$,ml)
2830 NEXT i
2840 PRINT:PRINT"KOLOM         WAS      WORDT"
2850 FOR i=1 TO nc
2860 sr=i:GOSUB 300:w$=cn$(i)
2870 cn$(i)=LEFT$("K"+sr$+bl$,ml)
2880 PRINT i;"VAN";nc;TAB(11);w$;TAB(21);
2890 INPUT w$
2900 IF w$<>"" THEN cn$(i)=LEFT$(w$+bl$,ml)
2910 NEXT i
2920 RETURN
4000 REM --tonen--
4010 IF wm=0 THEN 9700
4020 GOSUB 100
4030 PRINT"* PLANHULP GEGEVENS *"
4040 PRINT 
4100 k1=1:GOSUB 4750
4110 z1=1:GOSUB 4700
4120 ve=3
4130 REM kolomkoppen
4140 FOR j=1 TO nk
4150 h1=k1+j-1:IF h1>nc THEN 4190
4160 ho=j*10+1:GOSUB 110
4180 PRINT cn$(h1);
4190 NEXT j
4200 REM rijnamen en matrix
4210 FOR i=0 TO nl-1
4220 IF z1+i>nr THEN 4310
4230 ve=4+i:ho=0:GOSUB 110
4240 PRINT rn$(z1+i);
4250 FOR j=0 TO nk-1
4260 h1=k1+j:IF h1>nc THEN 4300
4270 ho=(j+1)*10:GOSUB 110
4280 sr=da(z1+i,h1):GOSUB 310:PRINT sr$;
4300 NEXT j
4310 NEXT i
4400 REM opdrachtregels
4410 GOSUB 9500
4420 PRINT"MATRIX I/B/O/N/L/R OF ?/M(ENU)";
4430 GOSUB 210:IF in$="?" THEN 15500
4440 IF in$="M" THEN RETURN
4450 IF in$="I" THEN 4600
4460 IF in$="B" THEN 4650
4470 i=0:j=0:w1=0:PRINT 
4480 PRINT"HOEVEEL ";:INPUT w1
4490 GOSUB 9500
4500 IF in$="N" THEN i=-w1
4510 IF in$="O" THEN i=w1
4520 IF in$="L" THEN j=w1
4530 IF in$="R" THEN j=-w1
4540 IF (i=0) AND (j=0) THEN 4400
4550 z1=z1+i:k1=k1+j
4560 IF i<>0 THEN GOSUB 4700
4570 IF j<>0 THEN GOSUB 4750
4590 GOTO 4120
4600 REM invoer
4610 GOSUB 5000
4620 IF rv>-1 THEN GOSUB 8000
4630 GOTO 4400
4650 REM bereken
4660 GOSUB 7000
4670 GOTO 4400
4700 REM --z1/z2 bepalen--
4710 h1=nl-1
4720 z2=z1+h1:IF z2>nr THEN z2=nr
4730 z1=z2-h1:IF z1<1 THEN z1=1
4740 RETURN
4750 REM --k1/k2 bepalen--
4760 h1=nk-1
4770 k2=k1+h1:IF k2>nc THEN k2=nc
4780 k1=k2-h1:IF k1<1 THEN k1=1
4790 RETURN
5000 REM --invoer--
5030 GOSUB 9500:ve=4:ho=19
5040 PRINT"WIJZER O/N/L/R OF ?/I/E(ND)";
5060 sr$="<":GOSUB 5600
5070 GOSUB 210:IF in$="?" THEN 15700
5090 h1=0:h2=0
5100 IF in$="O" THEN h1=-1
5110 IF in$="N" THEN h1=+1
5120 IF in$="L" THEN h2=-10
5130 IF in$="R" THEN h2=+10
5140 IF in$="E" THEN 5550
5150 IF in$="I" THEN 5400
5160 sr$=" ":GOSUB 5600
5170 ve=ve+h1:ho=ho+h2
5180 IF ve<4 THEN ve=4
5190 IF ve>4+z2-z1 THEN ve=4+z2-z1
5200 IF ho<10 THEN ho=19
5210 IF ho>nk*10+10 THEN ho=nk*10+9
5220 GOTO 5060
5400 REM getal
5410 ws=1:i=z1+ve-4:j=k1+(ho-19)/10
5420 w2=ve:w3=ho
5430 GOSUB 9500
5440 PRINT"GETAL=";
5450 INPUT sr:da(i,j)=sr
5460 GOSUB 9500
5470 PRINT"WIJZER O/N/L/R OF I/E(ND)";
5480 ve=w2:ho=w3-9:GOSUB 110
5490 GOSUB 310:PRINT sr$;
5500 ho=w3
5510 GOTO 5060
5550 sr$=" "
5600 REM --wijzer--
5610 GOSUB 110:PRINT sr$;
5620 RETURN
7000 REM --rekenen--
7010 GOSUB 9500
7020 PRINT"KOLOM- OF RIJGEWIJS? ?/K/R OF E(ND) ";
7030 GOSUB 210:IF in$="?" THEN 15900
7040 GOSUB 9500
7050 IF in$="K" THEN 7500
7060 IF in$<>"R" THEN RETURN
7100 REM ++rij++
7110 GOSUB 9500
7120 PRINT"1E RIJNAAM OF  END ";
7130 GOSUB 7400:r1=r
7140 IF w$="END" THEN RETURN
7150 IF r<0 THEN 7110
7160 GOSUB 9100
7170 GOSUB 9500
7180 PRINT"2E RIJNAAM ";
7190 GOSUB 7400:r2=r
7200 IF w$="END" THEN GOSUB 7470
7210 IF r<0 THEN 7170
7230 GOSUB 9500
7240 PRINT"RIJNAAM WAARHEEN ";
7250 GOSUB 7400:r3=r
7260 IF w$="END" THEN GOSUB 7470
7270 IF r<0 THEN 7230
7300 REM bereken rij
7310 rv=rv+1:IF rv>99 THEN 7350
7320 ru(rv,1)=100+r1:REM 100=rijgew.
7330 ru(rv,2)=r2*100+r3
7340 rv$(rv)=in$
7350 FOR i=1 TO nc
7360 c3=i:c2=i:c1=i
7370 GOSUB 9300
7380 NEXT i
7390 GOTO 7100
7400 REM --rij-index--
7410 r=-1:w$="":INPUT w$
7420 IF w$="END" THEN 7490
7430 w$=LEFT$(w$+bl$,ml)
7440 FOR i=1 TO nr
7450 IF rn$(i)=w$ THEN r=i:GOTO 7490
7460 NEXT i
7470 PRINT"RIJ ";w$;" ONBEKEND";
7480 PRINT" GEEF  RETURN";:GOSUB 210
7490 RETURN
7500 REM ++kolom++
7510 GOSUB 9500
7520 PRINT"1E KOLOMNAAM OF  END ";
7530 GOSUB 7800
7540 IF w$="END" THEN RETURN
7550 IF c<0 THEN 7510
7560 c1=c:GOSUB 9100
7570 GOSUB 9500
7580 PRINT"2E KOLOMNAAM ";
7590 GOSUB 7800
7600 IF w$="END" THEN GOSUB 7870
7610 IF c<0 THEN 7570
7620 c2=c
7630 GOSUB 9500
7640 PRINT"KOLOMNAAM WAARHEEN ";
7650 GOSUB 7800
7660 IF w$="END" THEN GOSUB 7870
7670 IF c<0 THEN 7630
7680 c3=c
7700 REM bereken kolom
7710 rv=rv+1:IF rv>99 THEN 7750
7720 ru(rv,1)=200+c1:REM 200=kolgew.
7730 ru(rv,2)=c2*100+c3
7740 rv$(rv)=in$
7750 FOR i=1 TO nr
7760 r3=i:r2=i:r1=i
7770 GOSUB 9300
7780 NEXT i
7790 GOTO 7500
7800 REM --kolomindex--
7810 c=-1:w$="":INPUT w$
7820 IF w$="END" THEN 7890
7830 w$=LEFT$(w$+bl$,ml)
7840 FOR i=1 TO nc
7850 IF cn$(i)=w$ THEN c=i:GOTO 7890
7860 NEXT i
7870 PRINT"KOLOM ";w$;" ONBEKEND";
7880 PRINT" GEEF  RETURN";:GOSUB 210
7890 RETURN
8000 REM --funktie afhandelen--
8010 IF rv<0 THEN 9600
8020 GOSUB 9500:PRINT"REKENT";
8030 FOR i=0 TO rv
8040 IF i>99 THEN 8090
8050 w1=INT(ru(i,1)/100)
8060 w2=nc:IF w1=2 THEN w2=nr
8070 FOR j=1 TO w2
8080 r1=ru(i,1)-w1*100
8090 r2=INT(ru(i,2)/100)
8100 r3=ru(i,2)-r2*100
8110 in$=rv$(i)
8120 c3=j:c2=j:c1=j
8130 IF w1=1 THEN 8160
8140 c3=r3:c2=r2:c1=r1
8150 r3=j:r2=j:r1=j
8160 GOSUB 9300
8170 NEXT j
8180 NEXT i
8190 RETURN
9000 REM --j/n vragen--
9010 GOSUB 210
9030 IF (in$="J") or(in$="N") THEN 9070
9040 IF in$="?" THEN 9070
9050 PRINT:PRINT"GEEF  J  OF  N";
9060 GOTO 9010
9070 RETURN
9100 REM --operator--
9110 GOSUB 110
9120 FOR i=1 TO np
9130 PRINT qp$(i);", ";
9140 NEXT i
9150 in$="":INPUT in$
9160 FOR i=1 TO np
9170 IF qp$(i)=in$ THEN RETURN
9180 NEXT i
9190 PRINT" OPNIEUW";:GOTO 9110
9300 REM --reken--
9310 h2=da(r2,c2):h1=da(r1,c1):sr=1e15
9320 IF in$="+" THEN sr=h1+h2
9330 IF in$="-" THEN sr=h1-h2
9340 IF in$="*" THEN sr=h1*h2
9350 IF in$="%" THEN sr=h1*h2/100
9360 IF (in$="/") AND (h2<>0) THEN sr=h1/h2
9370 IF in$="+%" THEN sr=h1+(h2*h1)/100
9380 IF in$="-%" THEN sr=h1-(h2*h1)/100
9390 IF (in$="A%") AND (h1<>0) THEN sr=(h2-h1)*100/h1
9400 IF (in$="^") AND (h1>=0) THEN sr=h1^h2
9410 da(r3,c3)=sr
9420 ve=r3-z1+4:ho=(c3-k1)*10+10
9430 IF (ho<10) or(ho>nk*10+9) THEN RETURN
9440 IF (ve<4) or(ve>z2-z1+4) THEN RETURN
9450 GOSUB 110:GOSUB 310:PRINT sr$;
9460 RETURN
9500 REM --regel20--
9510 ho=0:ve=19:GOSUB 110
9520 PRINT bl$;bl$:PRINT bl$;bl$
9530 GOSUB 110
9540 RETURN
9700 REM --nog model--
9720 FOR i=1 TO 25
9730 PRINT"    EERST MODEL/LOAD"
9740 NEXT i
9750 PRINT:PRINT"VERDER MET  V";
9760 GOSUB 250:GOSUB 210
9770 IF in$<>"V" THEN 9760
9780 RETURN
10000 REM --uitleg--
10005 GOSUB 100
10010 PRINT"* PLANHULP UITLEG  BLZ 1 *"
10015 PRINT 
10020 PRINT"  PLANHULP IS EEN PLANNINGSPROGRAMMA,"
10030 PRINT"WAARMEE ZGN 'WAT ALS'-PROBLEMEN KUNNEN"
10040 PRINT"WORDEN BEREKEND. HET PROGRAMMA REKENT"
10050 PRINT"OP RIJEN OF KOLOMMEN. RIJEN ZIJN (HORI-"
10060 PRINT"ZONTALE) REGELS, KOLOMMEN STAAN VERTI-"
10070 PRINT"KAAL.":PRINT 
10080 PRINT"  PLANHULP BEZIT EEN HULPFUNKTIE, DIE"
10090 PRINT"DAAR WAAR DIT IN EEN KEUS DMV EEN '?'"
10100 PRINT"STAAT GEGEVEN OPGEROEPEN KAN WORDEN.":PRINT 
10110 PRINT"  EERST MOET MET KEUS 1 (MODEL/LOAD) 'N"
10120 PRINT"MATRIXMODEL GEDEFINIEERD WORDEN, DWZ"
10130 PRINT"DAT HET AANTAL KOLOMMEN EN RIJEN EN HUN"
10140 PRINT"NAMEN MOET WORDEN GEGEVEN. DIT KAN DOOR"
10150 PRINT"'N BESTAAND MODEL VAN CASSETTE TE LADEN"
10160 PRINT"OF DOOR EEN NIEUW PROBLEEM OP TE GEVEN."
10170 PRINT:PRINT"VERDER MET  V  ";
10180 GOSUB 210:IF in$<>"V" THEN 10180
10190 GOSUB 100
10195 PRINT"* PLANHULP UITLEG  BLZ 2 *":PRINT 
10200 PRINT"NA HET MODELLEREN VERSCHIJNEN MAXIMAAL"
10210 PRINT"12 RIJEN EN 3 KOLOMMEN OP HET SCHERM."
10220 PRINT"(DIT IS KEUS 2 UIT HET HOOFDMENU.)"
10230 PRINT"  ONDERAAN OP 'T SCHERM STAAN DE KEUZES"
10240 PRINT"WAARUIT GEKOZEN MOET WORDEN."
10250 PRINT 
10260 PRINT"DE LETTERS STAAN VOOR:"
10270 PRINT"  R=RECHTS  L=LINKS    O=OP   N=NEER"
10280 PRINT"  I=INVOER  B=BEREKEN  E=END  M=MENU"
10290 PRINT 
10300 PRINT"  BIJ I(NVOER) VERSCHIJNT ER EEN WIJZER"
10310 PRINT"'<' IN DE MATRIX DIE MET O,N,L,R-KEUZES"
10320 PRINT"NAAR DE TE VULLEN PLAATS KAN WORDEN GE-"
10330 PRINT"STUURD. DAN 'I' EN GETAL GEVEN."
10340 PRINT"  ALLEEN HET GETOONDE DEEL KAN WORDEN"
10350 PRINT"GEVULD. ANDERE MATRIX-ELEMENTEN KUNNEN"
10360 PRINT"WORDEN BEREIKT VIA 'E' EN O,N,L OF R."
10400 PRINT:PRINT"VERDER MET  V  ";
10410 GOSUB 210:IF in$<>"V" THEN 10410
10420 GOSUB 100
10425 PRINT"* PLANHULP UITLEG  BLZ 3 *":PRINT 
10430 PRINT"  BIJ B(EREKEN) MOET WORDEN GEKOZEN UIT"
10440 PRINT"KOLOM- OF RIJGEWIJS REKENEN. RIJGEWIJS"
10450 PRINT"BIJV. BEREKENT UIT 1 OF 2 RIJEN EEN"
10460 PRINT"ANDERE. IDEM VOOR KOLOMGEWIJS."
10470 PRINT 
10480 PRINT"  DE REKENOPDRACHTEN WORDEN IN EEN ZGN"
10490 PRINT"REKENFUNKTIE BEWAARD. DE ZIN HIERVAN IS:"
10500 PRINT"A) VUL EERST DE STARTWAARDES IN"
10510 PRINT"B) BEREKEN VANUIT DE STARTWAARDES DE"
10520 PRINT"   REST VAN DE MATRIX"
10530 PRINT"C) ALS NU EEN STARTWAARDE WORDT GEWIJ-"
10540 PRINT"   ZIGD, WORDT DE REST AUTOMATISCH WEER"
10550 PRINT"   BEREKEND. ALDUS WERKT HET PLANNEN."
10570 PRINT:PRINT"TERUG MET  V  "
10580 GOSUB 210:IF in$<>"V" THEN 10580
10590 RETURN
15000 REM ++loadhulp++
15010 PRINT:PRINT 
15020 PRINT"ANTWOORD 'J' ALS HET MODEL AL EENS IN"
15030 PRINT"EEN VORIGE PLANHULP-RUN IS GEMAAKT EN"
15040 PRINT"OP CASSETTE IS OPGESLAGEN. DE MODELNAAM"
15050 PRINT"MOET NOG BEKEND ZIJN."
15060 PRINT 
15070 PRINT"ANTWOORD 'N' ALS EEN NIEUW PROBLEEM"
15080 PRINT"GEDEFINIEERD MOET WORDEN."
15090 PRINT"AANTAL KOLOMMEN EN RIJEN MOETEN BEKEND"
15100 PRINT"ZIJN, DAAR HERDIMENSIONEREN NIET GAAT."
15110 PRINT"KOLOMNAMEN EN RIJNAMEN HOEVEN NOG NIET"
15120 PRINT"DEFINITIEF GEKOZEN TE WORDEN, DAAR ZE"
15130 PRINT"MET MENUKEUS 4 LATER KUNNEN WORDEN"
15150 PRINT"HERZIEN. DOOR DE NAAMVRAGEN TE BEANT-"
15160 PRINT"WOORDEN MET <RETURN> KRIJGEN KOLOMMEN"
15170 PRINT"DE NAMEN K1, K2 ENZ., RIJEN R1, R2 ENZ."
15180 PRINT:PRINT"KIES J OF N ";
15190 GOSUB 9000:GOTO 20230
15300 REM ++dimensiehulp++
15310 PRINT:PRINT 
15320 PRINT"DIT IS EEN VOOR DEZE PLANHULP-RUN DEFI-"
15330 PRINT"TIEVE KEUS. MET 'J' LIGT HET MODEL VAST"
15340 PRINT"EN HET KAN DAN NIET MEER WORDEN VER-"
15350 PRINT"ANDERD. MET 'N' KOMT HET MENU TERUG EN"
15360 PRINT"IS ER NIETS GEBEURD."
15370 PRINT:PRINT"KIES J OF N ";
15380 GOSUB 9000:GOTO 2050
15500 REM ++gegevenshulp++
15510 GOSUB 9500
15520 PRINT"I IS GEGEVENSINVOER"
15530 PRINT"B IS BEREKENEN    (TIK TOETS)";
15540 GOSUB 210:GOSUB 9500
15550 PRINT"O SCHUIFT MATRIX OP, N NEER"
15560 PRINT"R SCHUIFT RECHTS, L LINKS (TIK TOETS)";
15570 GOSUB 210:GOSUB 9500
15580 PRINT"O,N,L,R VRAGEN HOEVEEL VERSCHUIVINGEN"
15590 PRINT"NIET-B/I/M OOK. GEEF NUL. (TIK TOETS)";
15600 GOSUB 210:GOTO 4400
15700 REM ++invoerhulp++
15710 h1=ve:h2=ho:GOSUB 9500
15720 PRINT"PLAATS WIJZER '<' OP INVOERPLAATS MET"
15730 PRINT"OP/NEER/LINKS/RECHTS   (TIK TOETS)";
15740 GOSUB 210:GOSUB 9500
15750 PRINT"GEEF  I  EN DAN GETAL MET <RETURN>"
15760 PRINT" E  VOOR ANDERE SUBMATRIX (TIK TOETS)";
15770 GOSUB 210:GOSUB 9500
15780 PRINT"ALS ER REKENWERK IS GEWEEST, DAN WORDT"
15790 PRINT"'T HERHAALD NA  E(ND).   (TIK TOETS)";
15800 GOSUB 210:GOSUB 9500
15810 ve=h1:ho=h2:GOTO 5040
15900 REM ++rekenhulp++
15910 GOSUB 9500
15920 PRINT"BIJ KOLOMGEWIJS WORDT EEN KOLOM UIT"
15930 PRINT"1 OF 2 ANDERE BEREKEND  (TIK TOETS)";
15940 GOSUB 210:GOSUB 9500
15950 PRINT"BIJV. K3=K1 + K1 BEANTWOORDEN MET"
15960 PRINT"K1, +, K1, K3. RIJEN IDEM  (TIK TOETS)";
15970 GOSUB 210:GOSUB 9500
15980 PRINT"DE REKENOPDRACHTEN WORDEN BEWAARD EN"
15990 PRINT"NA INVOER HERHAALD.       (TIK TOETS)";
16000 GOSUB 210:GOSUB 9500
16010 PRINT"DEZE REKENFUNKTIE IS MET MENUKEUS 4"
16020 PRINT"DESGEWENST TE WISSEN.   (TIK TOETS)";
16030 GOSUB 210:GOSUB 9500
16040 PRINT"REKENKEUS: +, -, *, /, ^ (BEKEND)"
16050 PRINT"%=PERCENTAGE: R3=R1*R2/100 (TIK TOETS)";
16060 GOSUB 210:GOSUB 9500
16070 PRINT"+%=PLUS PROCENT: R3=R1+R2*R1/100"
16080 PRINT"-%=IDEM. A% IS %AFWIJKING (TIK TOETS)";
16090 GOSUB 210:GOTO 7000
17000 REM --voorbeeld--
17010 GOSUB 100
17015 IF wm=0 THEN 17020
17016 PRINT:PRINT"AL EEN MODEL BEKEND"
17017 PRINT:PRINT"TERUG MET  V";
17018 GOSUB 210:IF in$<>"V" THEN 17018
17019 RETURN
17020 PRINT"* PLANHULP VOORBEELD *"
17030 PRINT 
17040 PRINT"IK KOOP IN 1983 EEN AUTO VAN F 20000,-."
17050 PRINT"WAT MOET IK JAARLIJKS SPAREN ALS IK"
17060 PRINT"OVER 6 JAAR EEN NIEUWE WIL KOPEN BIJ:"
17070 PRINT"A) GEMIDDELD 7% RENTE PER JAAR"
17080 PRINT"B) AUTO-PRIJSSTIJGING VAN 3% PER JAAR"
17100 PRINT"C) AFNEMING VAN DE INRUILWAARDE MET 25%"
17110 PRINT"   VAN DE RESTWAARDE"
17120 PRINT:PRINT"MODEL:"
17130 PRINT"9 RIJEN: 1983, SPAREN/J, MUTATIE, 1984,"
17140 PRINT"         1985, 1986, 1987, 1988 EN 1989"
17150 PRINT"4 KOL. : INRUIL, SPAREN, NIEUWPR, SALDO"
17160 PRINT 
17180 PRINT"REKENEN:1983+%MUTATIE=1984"
17190 PRINT"        1984+SPAREN/J=1984"
17200 PRINT"        IDEM 1985 T/M 1989"
17210 PRINT"        SPAREN+INRUIL-NIEUWPR=SALDO"
17220 PRINT"1E DRIE RIJEN VORMEN START. VERANDER"
17230 PRINT"SPAARBEDRAG VAN 1800 IN 2850 EN SPEEL"
17240 PRINT"WAT MET DE OVERIGE STARTWAARDES."
17300 nc=4:nr=9:rv=13:GOSUB 2100
17310 da(1,1)=20000:da(1,3)=20000
17320 da(2,2)=1800:da(3,1)=-25
17330 da(3,2)=7:da(3,3)=3
17340 FOR i=1 TO nr
17350 READ w$:rn$(i)=LEFT$(w$+bl$,ml)
17360 NEXT i
17370 FOR i=1 TO nc
17380 READ w$:cn$(i)=LEFT$(w$+bl$,ml)
17390 NEXT i
17400 FOR i=0 TO rv
17410 READ ru(i,1):READ ru(i,2)
17420 READ rv$(i)
17430 NEXT i
17500 PRINT:PRINT"VERDER MET  V  , DAN  1 KEER MET  M  ";
17520 GOSUB 210:IF in$<>"V" THEN 17520
17530 GOSUB 4000:GOSUB 8000:GOTO 4400
17540 RETURN
20000 REM --load/save--
20010 REM in regels 20200-20400 wordt
20020 REM het model geladen, in 20500-
20030 REM 20700 gesaved. het gaat erom
20040 REM navolgende gegevens te kunnen
20050 REM laden of bewaren:
20060 REM rv : rekenfunktie-teller
20070 REM nr : aantal rijen in model
20075 REM nc : aantal kolommen in model
20080 REM ru : dim 99 x 2; vol tot rv
20085 REM rv$: dim 99; vol tot rv
20090 REM rn$: dim nr: rijnamen
20095 REM cn$: dim nc: kolomnamen
20100 REM da : dim nr x nc: tabelgeg.
20110 IF wm=1 THEN 20500
20120 REM load
20130 GOSUB 100
20140 PRINT"* PLANHULP MODEL/LOAD *"
20150 PRINT 
20200 PRINT"INVOER VAN BESTAND? ?/J/N";
20210 GOSUB 9000
20220 IF in$="?" THEN 15000
20230 IF in$<>"J" THEN 2000
20240 PRINT:PRINT 
20250 PRINT"GEEF DE BESTANDSNAAM";
20260 w$="":INPUT w$
20270 IF w$="" THEN w$="BSTHULP"
20300 OPEN 1,1,0,w$
20310 INPUT #1,rv,nr,nc:GOSUB 2100
20320 FOR i=0 TO rv
20330 INPUT #1,ru(i,1),ru(i,2),rv$(i)
20340 NEXT i
20350 FOR i=1 TO nr
20360 INPUT #1,rn$(i)
20370 NEXT i
20380 FOR i=1 TO nc
20390 INPUT #1,cn$(i)
20400 FOR j=1 TO nr
20410 INPUT #1,da(j,i)
20420 NEXT j
20430 NEXT i
20440 CLOSE 1
20450 GOSUB 4000:RETURN
20500 REM save
20530 GOSUB 100
20540 PRINT"* PLANHULP SAVE *":PRINT 
20550 PRINT"GEEF EEN BESTANDSNAAM";
20560 w$="":INPUT w$
20570 ws=0:IF w$="" THEN w$="BSTHULP"
20600 OPEN 1,1,1,w$
20610 PRINT #1,rv;",";nr;",";nc
20620 FOR i=0 TO rv
20630 PRINT #1,ru(i,1);",";ru(i,2);",";rv$(i)
20640 NEXT i
20650 FOR i=1 TO nr
20660 PRINT #1,rn$(i)
20670 NEXT i
20680 FOR i=1 TO nc
20690 PRINT #1,cn$(i)
20700 FOR j=1 TO nr
20710 PRINT #1,da(j,i)
20720 NEXT j
20730 NEXT i
20740 CLOSE 1
20750 RETURN
25000 DATA "+","-","*","/","%","+%","-%","A%","^"
25500 DATA "1983","SPAREN/J","MUTATIE","1984"
25510 DATA "1985","1986","1987","1988","1989"
25520 DATA "INRUIL","SPAREN","NIEUWPR","SALDO"
25530 DATA 101,304,"+%",104,204,"+",104,305
25540 DATA "+%",105,205,"+",105,306,"+%",106
25550 DATA 206,"+",106,307,"+%",107,207,"+"
25560 DATA 107,308,"+%",108,208,"+",108,309
25570 DATA "+%",109,209,"+",201,204,"+",204
25580 DATA 304,"-"
30000 REM --planhulp--
30010 REM een programma van ca 16k.
30020 REM planhulp is ontwikkeld uit 't
30030 REM programma 'tiny plan' van
30050 REM cj flynn, compute! van dec 82
31000 REM ******************************
31100 REM w.j. de boer
31110 REM vr. cecilialaan 8 (na 1/1/84)
31120 REM 5673 mx  nuenen
31130 REM 
31140 REM met hartelijke dank aan:
31150 REM fam. valk en fam. van meurs
31170 REM ******************************
31180 REM 
31190 REM copyright  nos hilversum

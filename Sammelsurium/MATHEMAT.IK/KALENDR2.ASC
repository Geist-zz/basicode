1000 a=500:GOTO 20:REM kalender
1010 z=0:lj=0:GOSUB 100:REM wissen
1020 GOTO 3140:REM initialisaties
1030 REM
1040 REM ** hoofdprogramma **
1050 REM 
1060 GOSUB 2140:REM uitleg
1070 GOSUB 2590:REM uitleg(2)+keuze
1080 IF z=1 THEN GOTO 950
1090 GOSUB 1500:REM schrikkeljaar?
1100 m(2)=m(0)+s
1110 de=0
1120 FOR t=1 TO m:db=de:de=db+m(t):NEXT t
1130 a$="":b$=""
1140 FOR t=1 TO 31:a$(t)="  ":NEXT t
1150 IF j=lj THEN 1180
1160 GOSUB 1600:REM paasdatum
1170 lj=j
1180 i=j-1
1190 u=j+INT(i/4)-INT(i/100)+INT(i/400)
1200 t=u+db:n=7:GOSUB 1380
1210 y=-r
1220 FOR n=0 TO 5:k(n)=0:NEXT n
1230 IF m>6 THEN 1250
1240 GOSUB 1760:REM feestdagen?
1250 IF m<>12 THEN 1270
1260 GOSUB 1410:REM kerstdagen
1270 GOSUB 100:PRINT"         ";b$(m);" ";j:PRINT 
1280 PRINT"--------------------------------------"
1290 GOSUB 2450:REM printen
1300 PRINT 
1310 PRINT"--------------------------------------"
1320 PRINT:PRINT a$;" ";b$:PRINT 
1330 GOSUB 2690:REM keuze
1340 GOTO 1080
1350 REM **bepaling quotient +
1360 REM **rest van de breuk
1370 REM 
1380 q=INT(t/n):r=t-q*n:RETURN
1390 REM **kerstdagen
1400 REM 
1410 a$="K = Kerstmis"
1420 a$(25)=":K"
1430 a$(26)=":K"
1440 t=24-y:n=7:GOSUB 1380
1450 k(q)=1:t=25-y:GOSUB 1380
1460 k(q)=1
1470 RETURN
1480 REM **schrikkeljaar ?
1490 REM 
1500 s=0:t=j:n=4:GOSUB 1380
1510 IF r<>0 THEN 1570
1520 s=1:n=100:GOSUB 1380
1530 IF r<>0 THEN 1570
1540 s=0:n=400:GOSUB 1380
1550 IF r<>0 THEN 1570
1560 s=1
1570 RETURN
1580 REM **paasdatum ?
1590 REM 
1600 t=j:n=19:GOSUB 1380
1610 a=r:t=j:n=100:GOSUB 1380
1620 b=q:c=r:t=b:n=4:GOSUB 1380
1630 d=q:e=r:t=b+8:GOSUB 1380
1640 f=q:t=b-f+1:n=3:GOSUB 1380
1650 g=q:t=19*a+b-d-g+15:n=30:GOSUB 1380
1660 h=r:t=c:n=4:GOSUB 1380
1670 i=q:k=r:t=32+e+e+i+i-h-k:n=7:GOSUB 1380
1680 l=r:t=a+11*h+22*l:n=451:GOSUB 1380
1690 o=q:t=h+l-7*o+114:n=31:GOSUB 1380
1700 ma=q:da=r+1:RETURN
1710 REM **bepaling carnaval-,
1720 REM **paas-, hemelvaart-
1730 REM **of pinksterdag in de
1740 REM **af te drukken maand
1750 REM 
1760 c(4)=59+s+da+(ma-3)*31
1770 c(1)=c(4)-49:c(2)=c(4)-48
1780 c(3)=c(4)-47:c(5)=c(4)+1
1790 c(6)=c(4)+39:c(7)=c(4)+49
1800 c(8)=c(4)+50
1810 FOR t=1 TO 3:x=c(t)-db
1820 IF x<1 THEN 1860
1830 IF x>m(m) THEN 1860
1840 a$(x)=":C"
1850 a$="C = Carnaval"
1860 NEXT t
1870 FOR t=4 TO 5:x=c(t)-db
1880 IF x<1 THEN 1920
1890 IF x>m(m) THEN 1920
1900 a$(x)=":P"
1910 a$="P = Pasen"
1920 NEXT t
1930 x=c(6)-db
1940 IF x<1 THEN 1980
1950 IF x>m(m) THEN 1980
1960 a$(x)=":H"
1970 a$="H = Hemelvaart"
1980 FOR t=7 TO 8
1990 x=c(t)-db
2000 IF x<1 THEN 2040
2010 IF x>m(m) THEN 2040
2020 a$(x)=":P"
2030 b$="P = Pinksteren"
2040 NEXT t
2050 REM *feestdag in kolom?
2060 REM 
2070 FOR x=1 TO m(m)
2080 IF a$(x)="  " THEN 2110
2090 t=x-y-1:n=7:GOSUB 1380
2100 k(q)=1
2110 NEXT x:RETURN
2120 REM **subr.uitleg
2130 REM 
2140 PRINT"****************************"
2150 PRINT"*                          *"
2160 PRINT"*         KALENDER         *"
2170 PRINT"*                          *"
2180 PRINT"****************************"
2190 PRINT 
2200 PRINT:PRINT"Dit programma geeft steeds"
2210 PRINT:PRINT"per maand een kalenderblad."
2220 PRINT:PRINT"Carnaval, Pasen, Hemelvaart,"
2230 PRINT:PRINT"Pinksteren en Kerstmis wor-"
2240 PRINT:PRINT"den hierbij aangegeven."
2250 PRINT 
2260 PRINT 
2270 GOSUB 3100
2280 GOSUB 100:REM *wissen
2290 PRINT"U moet eerst de maand en het"
2300 PRINT:PRINT"jaar opgeven."
2310 PRINT:PRINT"Daarna kunt U een volgende"
2320 PRINT:PRINT"maand kiezen met een enkele"
2330 PRINT:PRINT"toets, te weten:"
2340 PRINT 
2350 PRINT:PRINT"'N' als U een nieuwe maand wilt"
2360 PRINT:PRINT"'T' als U terug wilt (vorige)"
2370 PRINT:PRINT"'V' als U verder wilt"
2380 PRINT:PRINT"'S' als U wilt stoppen"
2390 PRINT 
2400 PRINT 
2410 GOSUB 3100
2420 RETURN
2430 REM **PRINT en
2440 REM 
2450 ct=2:cn=0
2460 FOR a=1 TO 7
2470 PRINT:PRINT d$(a)
2480 NEXT a
2490 ve=2-2*y:ho=4
2500 FOR sr=1 TO m(m)
2510 ve=ve+2
2520 IF ve>17 THEN ve=ve-14:ho=ho+6
2530 GOSUB 110:GOSUB 310:PRINT sr$;a$(sr)
2540 NEXT sr
2550 ve=17:ho=0:GOSUB 110
2560 RETURN
2570 REM **subr. uitleg(2)
2580 REM 
2590 GOSUB 100:REM *wissen
2600 PRINT"Geef maandnummer ";:INPUT m
2610 PRINT 
2620 PRINT"Geef jaar        ";:INPUT j
2630 PRINT 
2640 m=INT(m):IF m>12 orm<1 THEN 2750
2650 j=INT(j)
2660 IF j>9999 THEN 2790
2670 IF j<1583 THEN 2830
2680 RETURN
2690 PRINT"   N , V , T , S : ";:GOSUB 210
2700 GOSUB 100
2710 IF (in$="T") OR (in$="t") THEN 2900
2720 IF (in$="V") OR (in$="v") THEN 2970
2730 IF (in$="S") OR (in$="s") THEN 3040
2740 IF (in$="N") OR (in$="n") THEN 2590
2750 PRINT 
2760 PRINT"FOUTIEVE INVOER !!!!!!"
2770 PRINT:PRINT:PRINT 
2780 GOTO 2600
2790 PRINT:PRINT"Jaartallen boven 9999 worden"
2800 PRINT:PRINT"NIET verwerkt !"
2810 FOR t=1 TO 4000:NEXT t
2820 GOTO 2590
2830 PRINT:PRINT"Tot 1582 gold de Juliaanse"
2840 PRINT:PRINT"kalender. Schrijf het programma"
2850 PRINT:PRINT"daarvoor maar zelf !"
2860 FOR t=1 TO 4000:NEXT t
2870 GOTO 2590
2880 REM **terug
2890 REM 
2900 m=m-1
2910 IF m>0 THEN 2660:REM *input(2)
2920 m=12
2930 j=j-1
2940 GOTO 2660:REM *input(2)
2950 REM **volgende
2960 REM 
2970 m=m+1
2980 IF m<13 THEN 2660:REM *input(2)
2990 m=1
3000 j=j+1
3010 GOTO 2660:REM *input(2)
3020 REM **stoppen
3030 REM 
3040 GOSUB 100:REM *wissen
3050 PRINT:PRINT"U Koos voor stoppen"
3060 PRINT:PRINT"Voor opnieuw beginnen 'RUN' intypen"
3070 z=1:RETURN
3080 REM **wacht op input
3090 REM
3100 PRINT"                     >RETURN";
3110 GOSUB 210:RETURN
3120 REM **initialisaties
3130 REM
3140 DIM a$(31),b$(12),d$(7),c$(8),m(12),k(5),c(8)
3150 FOR t=1 TO 12:READ b$(t):NEXT t
3160 FOR t=1 TO 7:READ d$(t):NEXT t
3170 FOR t=0 TO 12:READ m(t):NEXT t
3180 GOTO 1060
25000 DATA "Januari","Februari","Maart"
25010 DATA "April","Mei","Juni","Juli"
25020 DATA "Augustus","September","OkTO ber"
25030 DATA "November","December"
25040 DATA "Zo","Ma","Di"
25050 DATA "Wo","Do","Vr","Za"
25060 DATA 28,31,00,31,30,31,30
25070 DATA    31,31,30,31,30,31
30000 REM ********************
30010 REM *                  *
30020 REM *     kalender     *
30030 REM *                  *
30040 REM *  incl.feestdagen *
30050 REM *                  *
30060 REM * maker:e. ivens,  *
30070 REM *       doetinchem *
30080 REM *                  *
30090 REM ********************
30100 REM
30110 REM  gemaakt op apple ii
30120 REM
30130 REM  versie: 17-5-1982
30140 REM
30150 REM aangepast voor basicode-2
30160 REM
30170 REM
30180 REM  zie voor de bepaling
30190 REM  van de paasdatum:
30200 REM  hemel en dampkring,
30210 REM  april 1973; of het
30220 REM  boek van j. meeus.
30230 REM

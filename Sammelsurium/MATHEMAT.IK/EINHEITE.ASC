1000 A=200:GOTO 20:REM * REKENEN MET EENHEDEN *
1010 L$="                                       "
1020 DIM A$(20),A(20):REM bovengrens
1030 PRINT "wil je uitleg? (j/n) ";:INPUT V$
1040 GOSUB 3060:GOSUB 3100:REM   wist regel
1050 IF (V$="j") OR (V$="J") THEN GOSUB 3150
1060 AA=5
1070 NR=0:G=0
1080 GOSUB 3060
1100 GOSUB 100:PRINT "M E N U":PRINT 
1110 PRINT "Druk          1"
1120 PRINT "Lengtematen   2"
1130 PRINT "Inhoudsmaten  3"
1140 PRINT "Kracht        4"
1150 PRINT "Warmte        5"
1160 PRINT "Vermogen      6"
1170 PRINT "Frekwentie    7"
1180 GOSUB 3080:PRINT "Wat kies je?":INPUT V
1190 IF (V<1) OR (V>7) THEN 1100
1200 GOSUB 3080:GOSUB 3100
1210 REM   leest aantal eenheden
1220 GOSUB 5000
1230 READ AA:GOSUB 100
1240 GOSUB 260:GE=(INT(1000*RV))/100
1250 GOSUB 3080:PRINT "wil je een spiekbriefje? j/n ";
1260 INPUT V$:GOSUB 3100
1270 GOSUB 5000:GOSUB 2510
1280 IF (V$="j") OR (V$="J") THEN GOSUB 2000
1290 GOSUB 260:EX=INT(3*RV):REM   verschuift punt
1300 GOSUB 260:GE=INT(1000*RV):REM   kiest getal
1310 CN=2-EX
1320 IF EX=1 THEN GE=GE/10
1330 IF EX=0 THEN GE=GE/100
1340 CT=7:SR=GE:GOSUB 310:GE$=SR$
1350 GOSUB 260:K1=1+INT(RV*AA):REM   kiest eenheid
1360 GOSUB 260:K2=1+INT(RV*AA):REM   kiest 2de eenheid
1370 IF K2=K1 THEN 1360
1380 GOSUB 3080:PRINT L$
1390 REM   print vraag
1400 GOSUB 3060:PRINT GE$;" ";A$(K1);" = ";
1410 GOSUB 3120:PRINT "  ????   ";A$(K2)
1420 AG=GE*10^(A(K2)-A(K1)):REM   antwoord
1430 CN=CN-A(K2)+A(K1):CT=7-CN:IF CN<0 THEN CN=0
1440 IF CT<CN+2 THEN CT=CN+4
1450 GOSUB 3080:PRINT "wat is het antwoord?   "
1460 INPUT AW:HO=0:VE=21:GOSUB 110:PRINT L$
1470 HO=PH:VE=PV:GOSUB 110:PRINT "      "
1480 HO=0:VE=2:GOSUB 110
1490 SR=AW:GOSUB 310:AW$=SR$
1500 PRINT "je geeft als antwoord ";AW$
1510 HO=0:VE=3:GOSUB 110
1520 SR=AG:GOSUB 310:AG$=SR$
1530 PRINT "het antwoord is       ";AG$
1540 VE=3:HO=16:GOSUB 110
1550 IF AW$=AG$ THEN PRINT "goed":G=G+1:REM  controle antw
1560 NR=NR+1
1570 HO=0:VE=19:GOSUB 110
1580 PRINT NR;" sommen: ";G;" goed"
1590 GOSUB 3000
1600 GOSUB 3060
1610 FOR M=1 TO 4:REM   wist vier regels
1620 PRINT L$
1630 NEXT M
1640 GOTO 1290
2000 GOSUB 100:FOR M=2 TO AA:REM   print spiekbriefje
2010 HO=0:VE=M+5:GOSUB 110
2020 CT=10:CN=3:SR=10^A(M)
2030 IF A(M)>=0 THEN CT=6:CN=0
2040 GOSUB 310
2050 IF A(M)<0 THEN PRINT 10^A(1);" ";A$(1);" = ";SR$;" ";A$(M)
2060 IF A(M)>=0 THEN PRINT 10^A(1);" ";A$(1);
2070 IF A(M)>=0 THEN PRINT " = ";SR$;"     ";A$(M)
2080 NEXT M
2090 GOSUB 5000
2500 RETURN
2510 READ AA:FOR M=1 TO AA
2520 READ A$(M):REM   leest eenheden
2530 READ A(M):REM   leest waarden van eenheden
2540 NEXT M
2550 RETURN
3000 GOSUB 3080
3010 PRINT "druk op toets; met S stoppen"
3020 SD=10000:GOSUB 450
3030 IF IN=83 THEN 950:REM stoppen
3040 GOSUB 3080:GOSUB 3100
3050 RETURN
3060 HO=0:VE=0:GOSUB 110:REM   cursor op 0.0
3070 RETURN
3080 HO=0:VE=20:GOSUB 110:REM   cursor op 20.0
3090 RETURN
3100 PRINT L$
3110 RETURN
3120 GOSUB 120
3130 PH=HO:PV=VE
3140 RETURN
3150 GOSUB 100:V=0:GOSUB 5000
3160 FOR M=1 TO 9
3170 READ V$
3180 PRINT V$:PRINT 
3190 NEXT M
3200 GOSUB 3080:GOSUB 3010
3210 GOSUB 100
3220 RETURN
5000 RESTORE 
5010 READ VV$:IF VAL(RIGHT$(VV$,1))=V THEN RETURN
5020 READ AA:FOR M=1 TO AA:READ D$,D:NEXT M:GOTO 5010
25000 REM nu DATA voor DRUK
25010 DATA "*1"
25020 DATA 5
25030 DATA "bar",0
25040 DATA "N/cm2",1
25050 DATA "mbar",3
25060 DATA "pascal",5
25070 DATA "N/m2",5
25080 REM nu DATA voor LENGTEMATEN
25090 DATA "*2"
25100 DATA 5
25110 DATA "m",0
25120 DATA "km",-3
25130 DATA "dm",1
25140 DATA "cm",2
25150 DATA "mm",3
25160 REM nu DATA voor INHOUDSMATEN
25170 DATA "*3"
25180 DATA 7
25190 DATA "cm3",0
25200 DATA "mm3",3
25210 DATA "ml",0
25220 DATA "cl",-1
25230 DATA "dl",-2
25240 DATA "dm3",-3
25250 DATA "l",-3
25260 REM nu DATA voor KRACHT
25270 DATA "*4"
25280 DATA 2
25290 DATA "N",0
25300 DATA "kN",-3
25310 REM nu DATA voor WARMTE
25320 DATA "*5"
25330 DATA 2
25340 DATA "J",0
25350 DATA "kJ",-3
25360 REM nu DATA voor VERMOGEN
25370 DATA "*6"
25380 DATA 2
25390 DATA "W",0
25400 DATA "kW",-3
25410 REM nu DATA voor FREKWENTIE
25420 DATA "*7"
25430 DATA 2
25440 DATA "Hz",0
25450 DATA "kHz",-3
25460 REM nu DATA voor UITLEG
25470 DATA "*0"
25480 DATA "Met dit programma kun je rekenen met"
25490 DATA "eenheden oefenen: van meters naar"
25500 DATA "centimeters, Pascal naar bar, etc."
25510 DATA "Je moet de eenheden wel kennen, maar..."
25520 DATA "er is een spiekbriefje."
25530 DATA "Op regel 30000 staat hoe je  eenheden"
25540 DATA "kunt invoeren, die nog niet in dit"
25550 DATA "programma staan."
25560 DATA "Succes ermee."
30000 REM   Met dit programma kan een
30010 REM   leerling in de lagere
30020 REM   klassen van het VO oefenen
30030 REM   met het omrekenen van
30040 REM   eenheden.
30050 REM   Nieuwe eenheden kunnen in DATA
30060 REM   regels vanaf 25000 worden
30070 REM   ingevoerd.
30080 REM   Op de honderd tallen staan de
30090 REM   aantallen eenheden die volgen.
30100 REM   Daarna de eenheden en het
30110 REM   exponent van 10, dat de
30120 REM    verhouding met de bovenste
30130 REM   eenheid in het rijtje geeft
30140 REM 
30150 REM   Ook moet het menu vanaf
30160 REM   1100 worden aangepast.
30170 REM 
30180 REM   Ik heb dit programma gemaakt
30190 REM   voor mijn dochter in 4-LTS
30200 REM   op C-D niveau.
32000 REM   ***************************
32010 REM 
32020 REM   REKENEN MET EENHEDEN
32030 REM 
32040 REM   E.W.B. Ruitenberg
32050 REM   Prof. Meijerslaan 57
32060 REM   2285 HD Rijswijk
32070 REM 
32080 REM   januari 1988
32090 REM 
32110 REM   ***************************
32120 REM TROS-RADIO dd 880323

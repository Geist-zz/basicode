1000 a=100:GOTO 20:REM  'micromuf'  date:23-5-1984
1010 a=0:n=0:yt=0:xt=0:yr=0:xr=0
1020 r=0:m=0:x=0:y=0:la=0:ls=0:hp=0
1030 sf=0:fe=0:se=0:re=0:hp=0:cp=0
1040 k=0:ho=0:ve=0:l=0:xz=0:mf=0:ff=0
1050 xh=0:sx=0:wx=0:lh=0:lm=0:ab=0:t=0
1060 ty=0:tl=0:mh=0:fo=0:yg=0:zo=0:z=0
1070 t$="":r$="":a$=""
1080 p=3.14159265
1090 i$="INVALID"
1100 rd=p/180:d=180/p
1110 GOSUB 100
1120 PRINT"          *** MICROMUF ***"
1130 PRINT 
1140 PRINT"THIS PROGRAM CALCULATES THE:"
1150 PRINT 
1160 PRINT" * M.U.F.(MAXIMUM USABLE FREQUENCY)"
1170 PRINT 
1180 PRINT" * L.U.F.(LOWEST USABLE  FREQUENCY)"
1190 PRINT 
1200 PRINT"OF ANY SHORTWAVE SKYWAVE-PATH."
1210 PRINT:PRINT 
1220 PRINT"CALCULATIONS CAN BE DONE FOR"
1230 PRINT 
1240 PRINT"ANY MONTH AND ANY SUNSPOTNUMBER."
1250 PRINT:PRINT 
1260 PRINT"NAME TRANSMITTER LOCATION"
1270 INPUT t$
1280 PRINT 
1290 PRINT"TRANSMITTER LONGITUDE"
1300 PRINT"IN DEGR.  ( W=+ E=- )";
1310 INPUT yt
1320 IF yt<-180 OR yt>180 THEN PRINT i$:GOTO 1310
1330 PRINT 
1340 PRINT"TRANSMITTER LATITUDE"
1350 PRINT"IN DEGR. ( N=+ S=- )";
1360 INPUT xt
1370 IF xt<-90 OR xt>90 THEN PRINT i$:GOTO 1360
1380 PRINT 
1390 PRINT"NAME RECEIVER LOCATION"
1400 INPUT r$
1410 PRINT 
1420 PRINT"RECEIVER LONGITUDE"
1430 PRINT"IN DEGR.  ( W=+ E=-)";
1440 INPUT yr
1450 IF yr<-180 OR yr>180 THEN PRINT i$:GOTO 1440
1460 PRINT 
1470 PRINT"RECEIVER LATITUDE"
1480 PRINT"IN DEGR.(N=+ S=-)";
1490 INPUT xr
1500 IF xr<-90 OR xr>90 THEN PRINT i$:GOTO 1490
1510 PRINT 
1520 PRINT"SUNSPOTNUMBER";
1530 INPUT r
1540 IF r<1 OR r>180 THEN PRINT i$:GOTO 1530
1550 PRINT 
1560 PRINT"MONTH";
1570 INPUT m
1580 IF m<1 OR m>12 THEN PRINT i$:GOTO 1570
1590 REM geometry calc.
1600 q=SIN(xt*rd)*SIN(xr*rd)
1610 x=q+COS(xt*rd)*COS(xr*rd)*COS(yt*rd-yr*rd)
1620 la=-ATN(x/SQR(-x*x+1+1e-12))+(p/2):la=la*d
1630 l=111.1*la
1640 q=(SIN(xr*rd)-SIN(xt*rd)*COS(la*rd))
1650 x=q/COS(xt*rd)/SIN(la*rd)
1660 u=-ATN(x/SQR(-x*x+1+1e-12))+(p/2):u=u*d
1670 IF yt-yr<=0 THEN u=360-u
1680 IF ABS(yt-yr)>=180 THEN u=360-u
1690 h=275+r/2
1700 xs=23.4*COS(30*(m-6.25)*rd)
1710 n=n+1
1720 lh=l/n
1730 IF lh>4500 THEN 1710
1740 lm=la/n
1750 a=ATN((COS(.5*lm*rd)-6367/(h+6367))/SIN(.5*lm*rd))
1760 a=a*d
1770 IF a<1.5 THEN 1710
1780 GOSUB 100
1790 PRINT"FROM:";t$;"  TO:";r$
1800 PRINT"MONTH:";m;
1810 PRINT" SSN:";r;:PRINT" DIST:";INT(l+.5);"KM"
1820 PRINT"AZIM:";INT(u+.5);"DEGR. F-HOPS:";n
1830 ve=3:ho=0:GOSUB 110
1840 FOR q=34 TO 2 STEP -2
1850 PRINT"I                        I ";q
1860 NEXT q
1870 PRINT"--------------------------"
1880 PRINT" 0 2 4 6 8 10  14  18  22 H(UTC)"
1890 PRINT"       +:MUF -:LUF"
1895 ve=3:ho=30:GOSUB 110:PRINT"MHZ"
1900 FOR t=0 TO 23
1910 ab=0
1920 k=.5:GOSUB 2120:GOSUB 2240:mf=ff
1930 k=n-.5:GOSUB 2120:GOSUB 2240
1940 IF ff<mf THEN mf=ff
1950 ve=20-INT(mf/2+.5):ho=t+1
1960 IF ve<3 THEN ve=3
1970 GOSUB 110
1980 PRINT"+"
1990 FOR k=.25 TO n-.25 STEP .5
2000 GOSUB 2120
2010 GOSUB 2490
2020 ab=ab+ls
2030 NEXT k
2040 ve=19-INT(ab+.5)
2050 IF ve<3 THEN ve=3
2060 IF ve>19 THEN ve=19
2070 GOSUB 110
2080 PRINT"-"
2090 NEXT t
2100 ve=22:ho=27:GOSUB 110:PRINT"HIT ANY KEY!"
2110 GOSUB 250:GOSUB 210:run
2120 REM interm. lat.& long.
2130 q=COS(u*rd)*COS(xt*rd)*SIN(k*lm*rd)
2140 x=q+SIN(xt*rd)*COS(k*lm*rd)
2150 xn=ATN(x/SQR(-x*x+1+1e-12)):xn=xn*d
2160 q=(COS(k*lm*rd)-SIN(xt*rd)*SIN(xn*rd))
2170 x=q/(COS(xt*rd)*COS(xn*rd))
2180 yi=-ATN(x/SQR(-x*x+1+1e-12))+(p/2):yi=yi*d
2190 IF u<180 THEN yi=-yi
2200 yn=yt+yi
2210 IF yn>180 THEN yn=yn-360
2220 IF yn<-180 THEN yn=yn+360
2230 RETURN
2240 yz=yn:REM 'mini-f2' f-layer calc.
2250 IF yn<-160 THEN yz=yn+360
2260 yg=(20-yz)/50
2270 zo=20*yg/(1+yg+yg^2)+5*(1-yg/7)^2
2280 z=xn-zo: REM magn. dip equator correct.
2290 tl=t-yn/15
2300 IF tl>24 THEN tl=tl-24
2310 IF tl<0 THEN tl=tl+24
2320 mh=m
2330 IF z>0 THEN 2360
2340 z=-z
2350 mh=m+6
2360 xh=COS(30*(mh-6.5)*rd):REM 1 week delay on equinoxes
2370 sx=(ABS(xh)+xh)/2:REM f layer local summer var.
2380 wx=(ABS(xh)-xh)/2:REM f layer local winter var.
2390 IF z>77.5 THEN z=77.5
2400 ty=tl
2410 IF ty<5 THEN ty=tl+24
2420 yf=(ty-14-sx*2+wx*2-r/175)*(7-sx*3+wx*4-r/(150-wx*75))
2430 IF ABS(yf)>60 THEN yf=60
2440 x=(1+r/(175+sx*175))
2450 fo=6.5*x*COS(yf*rd)*COS((z-sx*5+wx*5)*rd)^.5
2460 sf=(1-(COS(a*rd)*6367/(6367+h))^2)^-.5
2470 ff=fo*sf:REM muf at control point
2480 RETURN
2490 REM e-layer & absorpt.calc.
2500 q=SIN(xn*rd)*SIN(xs*rd)
2510 x=q+COS(xn*rd)*COS(xs*rd)*COS((yn-15*(t-12))*rd)
2520 xz=(-ATN(x/SQR(-x*x+1+1e-12))+p/2)*d
2530 IF xz>85 THEN 2560
2540 fe=3.4*(1+.0016*r)*COS(xz*rd)^.3
2550 GOTO 2570
2560 fe=3.4*(1+.0016*r)*(xz-80)^-.5
2570 se=(1-.965*COS(a*rd)^2)^-.5
2580 ls=.028*fe^2*se
2590 RETURN
30000 REM this program uses 'mini-f2'
30010 REM devised by r.fricker (bbc
30020 REM external services) for fo-f
30030 REM calculations and
30040 REM l.m.muggleton's formula for 
30050 REM fo-e calculations.
30060 REM for the l.u.f. a min.useable
30070 REM fieldstrength of 30dbuv at
30080 REM the receiver and 250kw of
30090 REM transmitter-power (aerial gain:
30100 REM 18dbi) are assumed.
30110 REM the l.u.f. is derived
30120 REM from absorption calculations
30130 REM based on the work of piggot,
30140 REM george,samuel qnd bradley.
30150 REM in spite of the program's
30160 REM charming simplicity it gives
30170 REM a good impression of the
30180 REM ionosphere's behaviour and
30190 REM can be used for propagation
30200 REM predictions.
30210 REM
30220 REM      hans bakhuizen
30230 REM      propagation unit;frequency bureau
30240 REM      radio netherlands
30250 REM      p.o.box 222
30260 REM      1200 jg hilversum holland
30270 REM (c) copyright media network june 1984

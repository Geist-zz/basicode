1000 a=100:GOTO 20:REM timer fototrocknung
1010 DIM l$(16)
1020 GOSUB 100:sr$="    TIMER FOTOTROCKNUNG     ":GOSUB 150
1030 ve=5:ho=10:GOSUB 110:PRINT"M E N U E"
1040 ve=6:ho=10:GOSUB 110:PRINT"========="
1050 ve=9:GOSUB 110:PRINT"E = Eingabe der Parameter"
1060 ve=11:GOSUB 110:PRINT"A = Anwenderinformationen"
1070 ve=13:GOSUB 110:PRINT"S = Start des Timers"
1080 ve=15:GOSUB 110:PRINT"P = Programm beenden"
1090 ve=18:ho=10:GOSUB 110:PRINT"BITTE WAEHLEN !"
1100 GOSUB 200:IF in$="" THEN 1100
1110 IF in=69 THEN GOSUB 2010
1120 IF in=65 THEN GOSUB 2510
1130 IF in=83 THEN GOSUB 2620
1140 IF in=80 THEN 950
1150 GOTO 1020
1160 REM================================
2000 REM  <<<<  eingabe parameter >>>>
2010 GOSUB 100:sr$="       Eingabe der Parameter   "
2020 GOSUB 150
2030 t=0:an=0
2040 ve=5:ho=3:GOSUB 110
2050 INPUT"Trockenzeit in Minuten     :";t
2060 ve=7:GOSUB 110
2070 INPUT"Anzahl der Fotos je Zyklus :";an
2080 ve=12:GOSUB 110
2090 PRINT"Trockenzeit = ";t;" Minuten"
2100 ve=15:ho=2:GOSUB 110
2110 PRINT an;" Fotos je Zyklus"
2120 ve=22:ho=3:GOSUB 110
2130 PRINT"Sind die Angaben richtig ? (J/N)"
2140 ho=36:GOSUB 110
2150 GOSUB 200
2160 IF in=74 THEN RETURN
2170 IF in=78 THEN 2010
2180 GOTO 2150
2500 REM  <<<< anwenderinformationen >>>>
2510 RESTORE:b=0
2520 GOSUB 100:b=b+1
2530 sr$="     ANWENDERINFORMATIONEN        ":GOSUB 150
2540 FOR i=1 TO 16
2550 READ l$(i):PRINT l$(i)
2560 NEXT i
2570 ho=18:ve=23:GOSUB 110:PRINT"WEITER --> eine Taste"
2580 GOSUB 200:IF in$="" THEN 2580
2590 IF b=2 THEN GOSUB 100:GOTO 1020
2600 GOTO 2520
2610 REM < starten timer & darstellung >
2620 GOSUB 100
2630 sr$="        TIMER & DARSTELLUNG       ":GOSUB 150
2640 PRINT:PRINT"  Trockenzeit je Zyklus ";t;" Minuten"
2650 PRINT:PRINT"  Je  Durchgang  werden ";an;" Aufnahmen"
2660 PRINT"  getrocknet."
2670 ve=21:ho=15:GOSUB 110:PRINT"MENUE --> eine Taste"
2680 gs=0:sd=0:in=0
2690 ve=13:ho=0:GOSUB 110:PRINT"    Zeit laeuft !"
2700 sd=t*600:sv=0:sp=0:GOSUB 400:REM zeitschleife
2710 gs=gs+an:REM gesamtzahl der fotos
2720 ve=13:ho=0:GOSUB 110
2730 PRINT gs;" Fotos getrocknet."
2740 sv=15:sd=5:sp=60:GOSUB 400
2750 sv=15:sd=5:sp=69:GOSUB 400
2760 sv=15:sd=5:sp=78:GOSUB 400
2770 GOSUB 200:IF in<>0 THEN GOSUB 3020
2780 IF in=0 THEN 2700
2790 GOSUB 100:GOTO 1020
3000 REM================================
3010 REM <<< abbruch timerprogramm >>>
3020 ve=21:ho=0:GOSUB 110
3030 sr$="ABBRUCH        Menue --> eine Taste":GOSUB 150
3040 GOSUB 200
3050 IF in$="" THEN 3040
3060 RETURN
25000 DATA "","Bestimmt  haben  Sie   als  Fotoamateur"
25010 DATA "schon  einmal vor dem Problem der exak-"
25020 DATA "ten  Trocknungszeit  Ihrer  Papierposi-"
25030 DATA "tive gestanden. Schliesslich werden Sie"
25040 DATA "wohl  kaum staendig  neben der Trocken-"
25050 DATA "presse  stehen."
25060 DATA "",""
25070 DATA "Dieses   kleine   Programm laesst  nach"
25080 DATA "einer  vom  Anwender einzugebenen Zeit-"
25090 DATA "vorgabe   einen   Signalton   ertoenen."
25100 DATA "Gleichzeitig  wird  auf  dem Bildschirm"
25110 DATA "die  Anzahl der bis zu diesem Zeitpunkt"
25120 DATA "getrockneten   Aufnahmen   dargestellt."
25130 DATA "","","Natuerlich  ist  deshalb  ebenfalls die"
25140 DATA "Eingabe der Anzahl der in einem  Zyklus"
25150 DATA "zu bearbeitenden Fotografien notwendig."
25160 DATA "",""
25170 DATA "Vor  dem  Starten  des  Timers ist also"
25180 DATA "der Eingabeteil  zu belegen.",""
25190 DATA "Wollen  Sie das Programm verlassen,  so"
25200 DATA "druecken Sie eine beliebige Taste. Nach"
25210 DATA "Ablauf des begonnenen Zyklus erscheint "
25220 DATA "eine Information ueber den Abbruch. Mit"
25230 DATA "einem  weiteren  Tastendruck kehren Sie"
25240 DATA "ins Menue zurueck."
25250 DATA "                           VIEL SPASS !"
32000 REM  ****************************
32010 REM  *                          *
32020 REM  * autor: burkhardt  preuss *
32030 REM  *        h.-beimler-str.20 *
32040 REM  *        frankfurt(o)      *
32050 REM  *        1200              *
32060 REM  *                          *
32070 REM  *                          *
32080 REM  * erstellt am 12.01.1989   *
32090 REM  * auf einem  c64           *
32100 REM  *                          *
32110 REM  ****************************
32120 REM
32130 REM erstausstrahlung: radio ddr
32140 REM -spezial 900216

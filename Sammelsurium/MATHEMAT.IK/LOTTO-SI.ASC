1000 a=100:GOTO 20:REM  ## tele-lotto ##
1010 GOSUB 100
1020 DIM t1(5):DIM zu(5):DIM ri(5)
1030 GOSUB 260:zz=INT(35*rv+1):IF zz=0 THEN 1030
1040 DEF FNz(x)=zz
1050 sr$="TELE-LOTTO-SIMULATION von A.Thoemke"
1060 ho=INT(ho/2)-19:ve=1
1070 GOSUB 110:GOSUB 150
1080 PRINT 
1090 PRINT"Wieviel Tele-Lotto-Spiele"
1100 INPUT"wollen Sie simulieren   ";an:PRINT 
1110 FOR i=1 TO 5
1120 PRINT i;:INPUT". Tippzahl =";t1(i)
1130 NEXT i:PRINT 
1140 FOR i=1 TO 5
1150 IF (t1(i)<1) OR (t1(i)>35) THEN 5010
1160 NEXT i
1170 FOR i=1 TO 4:FOR j=i+1 TO 5
1180 IF t1(i)=t1(j) THEN 5010
1190 NEXT j:NEXT i
2000 REM ------------------zufallszahlen
2010 FOR k=1 TO an
2020 GOSUB 260:zz=INT(35*rv+1):IF zz=0 THEN 2020
2030 zu(1)=FNz(1)
2040 FOR i=2 TO 5
2050 zu(i)=FNz(1)
2060 GOSUB 260:zz=INT(35*rv+1):IF zz=0 THEN 2060
2070 FOR j=1 TO i-1
2080 IF zu(i)=zu(j) THEN 2050
2090 NEXT j:NEXT i
3000 REM ------------------------anzeige
3010 FOR i=1 TO 5
3020 PRINT CHR$(20);zu(i);
3030 NEXT i
3040 GOSUB 250
4000 REM ---------------------auswertung
4010 ho=0:ve=12:GOSUB 110
4020 PRINT:PRINT:PRINT"Anzahl gespielter Spiele = ";k
4030 PRINT:su=0
4040 FOR i=1 TO 5:FOR j=1 TO 5
4050 IF t1(i)=zu(j) THEN su=su+1
4060 NEXT j:NEXT i
4070 ri(su)=ri(su)+1
4080 FOR i=0 TO 5
4090 PRINT i;" Richtige  = ";ri(i)
4100 NEXT i:PRINT 
4110 NEXT k
4120 PRINT 
4130 sd=10000:GOSUB 450
4140 GOTO 950:REM -----------programmende
5000 REM  --------up fehlerhafte eingabe
5010 PRINT"FALSCHE EINGABE !!!":sd=9:GOSUB 450
5020 GOSUB 100:GOTO 1000
30000 REM  -----------------------------
30010 REM  kredit:h.gutzer , spiel+spass
30020 REM         mit    dem    computer
30030 REM         urania-vlg.1987,s.52ff
30035 REM 
30040 REM  anpassung in  basicode  durch
30050 REM  andreas  thoemke  am 27.08.89
30055 REM  -----------------------------
30060 REM 
32000 REM  #############################
32010 REM  ##                         ##
32020 REM  ##  tele-lotto-simulation  ##
32030 REM  ##  ---------------------  ##
32040 REM  ##  (c) 1989 by  ata-soft  ##
32050 REM  ##  ---------------------  ##
32060 REM  ##  auf  commodore 64 von  ##
32065 REM  ##                         ##
32070 REM  ##   andreas   thoemke     ##
32080 REM  ##   glueck-auf-ring 1     ##
32090 REM  ##   lu.-eisleben 4250     ##
32100 REM  ##                         ##
32110 REM  #############################
32120 REM 
32130 REM  erstausstrahlung: radio ddr
32140 REM  900124

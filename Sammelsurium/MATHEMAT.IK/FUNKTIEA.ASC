1000 a=100:GOTO 20:REM function properties
1010 GOSUB 100:PRINT:PRINT"FUNCTION PROPERTIES"
1100 PRINT:PRINT"1) INTEGRATION"
1110 PRINT:PRINT"2) DIFFERENTIATION"
1120 PRINT:PRINT"3) ZERO POINT"
1130 PRINT:PRINT"4) ARC LENGTH"
1140 PRINT:PRINT"5) INSTRUCTIONS"
1150 PRINT:PRINT"6) END"
1160 PRINT:PRINT 
1170 PRINT"Your choice";:GOSUB 210:k=val(in$)
1180 IF (k<1) OR (k>6) THEN 1010
1190 ON k GOSUB 2000,3000,4000,5000,6000,1999
1200 PRINT:PRINT"PRESS";:GOSUB 210:GOTO 1010
1500 REM   y=f(x) ******
1510 y=sin(x)+x*x
1520 REM   *************
1530 RETURN
1999 end
2000 REM   integration
2005 REM   (simpson)
2010 GOSUB 100:PRINT:PRINT"INTEGRATION"
2020 PRINT:PRINT"Lower limit";:INPUT x1:PRINT"Upper limit";
2030 INPUT x2:IF x1>x2 THEN 2020
2040 PRINT:PRINT"Number of decimals ";
2050 INPUT e:e=10^-(INT(e+.5))
2100 PRINT:PRINT"   STEP   INTEGRAL":PRINT 
2110 h=x2-x1:x=x1:GOSUB 1500:t1=y
2120 x=x2:GOSUB 1500:t1=(t1+y)*h/2
2130 x=x1+h/2:GOSUB 1500:m1=y*h
2140 h=h/2:a=2:t2=(t1+m1)/2:m2=0
2150 FOR i=.5 TO 2:x=i*h+x1:GOSUB 1500
2160 m2=m2+y*h:NEXT i:s1=t2
2200 h=h/2:a=a*2:t3=(t2+m2)/2:m3=0
2210 FOR j=.5 TO a:x=j*h+x1:GOSUB 1500
2220 m3=m3+y*h:NEXT j
2230 s2=(t1+2*m1)/3:GOSUB 2500:t1=t2:t2=t3:m1=m2:m2=m3
2240 s0=s1:s1=s2:IF ABS(s1-s0)>3*e THEN 2200
2300 REM   slot int.
2310 h=h/2:s2=(t1+2*m1)/3:GOSUB 2500
2320 PRINT:PRINT"The integral from ":PRINT x1;" TO  ";x2
2330 PRINT"is ";:GOSUB 2530
2340 RETURN
2500 REM   PRINT  table
2510 sr=INT(2*h*1e5+.5)/1e5
2520 cn=5:ct=9:GOSUB 310:PRINT sr$;" ";
2530 sr=INT(s2/e+.5)*e
2540 cn=INT(log(1/e)/log(10)+.5)
2550 ct=INT(7+cn/2):GOSUB 310:PRINT sr$
2560 RETURN
3000 REM  dIF ferentiation
3010 GOSUB 100:PRINT:PRINT"DERIVATIVE"
3020 PRINT:PRINT"For which X ";:INPUT x1
3030 PRINT:PRINT"Number of decimals ";
3040 INPUT e:e=10^-(INT(e+.5))
3100 PRINT:PRINT"   STEP    DIFFER.   ":PRINT 
3110 s=.1:i=0
3200 x=x1-s:GOSUB 1500:y1=y:x=x1+s:GOSUB 1500:y2=y:iv=i
3210 i=(y2-y1)/(s*2)
3220 h=s:s2=i:GOSUB 2500:s=s/2
3230 IF ABS(iv-i)>e THEN 3200
3300 PRINT:PRINT"The derivative in"
3310 PRINT x1;" is ";:GOSUB 2530
3320 RETURN
4000 REM zero point
4010 REM   regula falsi
4020 GOSUB 100:PRINT:PRINT"ZERO POINT":PRINT 
4030 PRINT"Lower limit";:INPUT x1:PRINT"Upper limit";
4040 INPUT x2
4050 x=x1:GOSUB 1500:y1=y:x=x2:GOSUB 1500:y2=y
4060 IF (y1>0) AND (y2>0) THEN PRINT"Error":GOTO 4030
4070 IF (y1<0) AND (y2<0) THEN PRINT"Error":GOTO 4030
4080 PRINT:PRINT"Number of decimals ";
4090 INPUT e:e=10^-(INT(e+.5))
4100 PRINT:PRINT"   STEP       ZERO POINT":PRINT 
4110 x=x1:GOSUB 1500:z=sgn(y)
4120 x4=(x1+x2)/2
4200 x=x1:GOSUB 1500:y1=y:x=x2:GOSUB 1500:y2=y
4210 x3=(x1*y2-x2*y1)/(y2-y1)
4220 h=(x3-x4)/2:s2=x3:GOSUB 2500
4230 x=x3:GOSUB 1500
4240 IF y*z<0 THEN x2=x3:GOTO 4260
4250 x1=x3
4260 IF ABS(x3-x4)>e THEN x4=x3:GOTO 4200
4300 h=(x3-x4)/2:s2=x3:GOSUB 2500
4310 PRINT:PRINT"The zero point is":GOSUB 2530:RETURN
5000 REM   arc length
5010 REM   trapezium
5020 GOSUB 100:PRINT:PRINT"ARC LENGTH":PRINT 
5030 PRINT"Lower limit";:INPUT x1:PRINT"Upper limit";
5040 INPUT x2:IF x1>x2 THEN 5030
5050 PRINT:PRINT"Number of decimals ";
5060 INPUT e:e=10^-(INT(e+.5))
5100 PRINT:PRINT"   STEP     ARC":PRINT 
5110 h=x2-x1:l=0
5200 l1=l:l=0:FOR i=x1 TO x2-h STEP h:x=i
5210 GOSUB 1500:ya=y:x=i+h:GOSUB 1500:yb=y
5220 l=l+SQR(h^2+(yb-ya)^2):NEXT i
5230 h=h/2:s2=l:GOSUB 2500
5240 IF ABS(l-l1)>e THEN 5200
5300 PRINT:PRINT"The arc length"
5310 PRINT"from ";x1;" to ";x2
5320 PRINT"is";:GOSUB 2530:RETURN
6000 REM   explanation
6010 GOSUB 100:PRINT:PRINT"FUNCTION PROPERTIES"
6020 PRINT:PRINT:PRINT"INSTRUCTIONS"
6030 PRINT"This program calculates":PRINT"some properties"
6040 PRINT"of a given function.":PRINT 
6050 PRINT"In this case, the function":PRINT"is stored";
6060 PRINT"in line 1510."
6070 PRINT:PRINT"Line 1510 Y=SIN(X)+X*X"
6080 PRINT:PRINT"You can obviously change"
6090 PRINT"the function to suit your needs."
6100 RETURN
30000 REM
30010 REM  function analyser
30020 REM  
30030 REM  h.van deelen
30040 REM  
30050 REM  rudolphln.30
30060 REM  
30070 REM  de glind gld
30080 REM  
30090 REM  the netherlands
30100 REM  
30110 REM  written on a vic 20
30120 REM 
30130 REM  translated FOR 
30135 REM  radio netherlands
30140 REM  by jonathan marks

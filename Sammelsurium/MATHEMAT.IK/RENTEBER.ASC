1000 a=100:GOTO 20:REM rente berekeningen
1010 GOSUB 100:ct=9:cn=2
1020 PRINT"  HOOFDMENU INTRESTBEREKENINGEN"
1030 PRINT"  ============================="
1040 PRINT:PRINT 
1050 PRINT:PRINT" 1 ENKELVOUDIGE INTREST"
1060 PRINT:PRINT" 2 SAMENGESTELDE INTREST"
1070 PRINT:PRINT" 3 ANNUITEITEN"
1080 PRINT:PRINT" 4 EINDE PROGRAMMA"
1090 PRINT:PRINT:PRINT"MAAK UW KEUZE A.U.B"
1100 GOSUB 210:r$=in$
1110 IF (r$<"1") OR (r$>"4") THEN 1090
1120 ON VAL(r$) GOTO 1130,1930,2900,30060
1130 GOSUB 100:kp=0:t=0:p=0:c=0:a=0:r=0
1140 g=0:k=0
1150 PRINT"ENKELVOUDIGE INTRESTBEREKENINGEN"
1160 PRINT"================================":PRINT 
1170 PRINT" 1 BEREKENING RENTE"
1180 PRINT:PRINT" 2 BEREKENING LOOPTYD"
1190 PRINT:PRINT" 3 BEREKENING RENTEPERCENTAGE"
1200 PRINT:PRINT" 4 BEREKENING BEGINKAPITAAL"
1210 PRINT:PRINT" 5 KOOP OP AFBETALING"
1220 PRINT:PRINT" 6 NAAR HOOFDMENU"
1230 PRINT:PRINT:PRINT"MAAK UW KEUZE A.U.B"
1240 GOSUB 210:r$=in$
1250 IF (r$<"1") OR (r$>"6") THEN 1230
1260 ON VAL(r$) GOTO 1270,1380,1460,1550,1630,1010
1270 GOSUB 100:GOSUB 1750
1280 PRINT"INDIEN U DEZE RENTE NIET OPNEEMT EN HET"
1285 PRINT"BEDRAG LANGER DAN 1 JAAR LAAT STAAN"
1290 PRINT"KUNT U BETER REKENEN MET SAMENGESTELDE"
1300 PRINT"INTREST"
1310 PRINT:PRINT"GEEF BEDRAG     ";:INPUT kp
1320 PRINT:PRINT"RENTE % PER JAAR";:INPUT p:PRINT 
1330 PRINT"HOEVEEL ";k$;" STAAT HET BEDRAG UIT";
1340 INPUT t:r=(kp*t*p)/c
1350 sr=r:GOSUB 310
1360 PRINT:PRINT"UW RENTE NA ";t;k$;" IS f";sr$
1370 GOTO 1900
1380 GOSUB 100:GOSUB 1750
1390 PRINT:PRINT"GEEF BEDRAG     ";:INPUT kp
1400 PRINT:PRINT"RENTE % PER JAAR";:INPUT p
1410 PRINT:PRINT"GEEF RENTEBEDRAG";:INPUT r
1420 t=(r*c)/(kp*p)
1430 sr=t:GOSUB 310
1440 PRINT:PRINT"HET GELD HEEFT ";sr$;" ";k$;" UITGESTAAN"
1450 GOTO 1900
1460 GOSUB 100:GOSUB 1750
1470 PRINT:PRINT"GEEF BEDRAG     ";:INPUT kp
1480 PRINT:PRINT"GEEF RENTEBEDRAG";:INPUT r
1490 PRINT 
1500 PRINT"HOEVEEL ";k$;" STOND HET BEDRAG UIT"
1510 INPUT t:p=(r*c)/(kp*t):PRINT 
1520 sr=p:GOSUB 310
1530 PRINT"RENTE PER JAAR IS";sr$;"%"
1540 GOTO 1900
1550 GOSUB 100:GOSUB 1750
1560 PRINT:PRINT"RENTE % PER JAAR";:INPUT p
1570 PRINT:PRINT"GEEF RENTEBEDRAG";:INPUT r:PRINT 
1580 PRINT"HOEVEEL ";k$;" STAAT HET BEDRAG UIT"
1590 INPUT t:kp=(r*c)/(t*p):
1600 sr=kp:GOSUB 310
1610 PRINT:PRINT"UW STARTBEDRAG WAS f";sr$
1620 GOTO 1900
1630 GOSUB 100
1640 PRINT"WAT KOST HET ARTIKEL ZONDER AFBETALING";:INPUT kp
1650 PRINT 
1660 PRINT"WAT IS DE AANBETALING";:INPUT a:PRINT 
1670 PRINT"HOEVEEL MAANDEN MOET U BETALEN";:INPUT t
1680 PRINT 
1690 PRINT"HOEVEEL MOET U PER MAAND BETALEN";:INPUT r
1700 g=(t+1)/2:r=(r*t)-(kp-a)
1710 p=((r/g)*12)/((kp-a)/100):PRINT 
1720 sr=p:GOSUB 310
1730 PRINT"U BETAALT AAN RENTE";sr$;"% PER JAAR"
1740 GOTO 1900
1750 REM **INPUT **
1760 PRINT:PRINT"BEREKENING IN DAGEN,WEKEN"
1770 PRINT"MAANDEN OF JAREN <D/W/M/J> ?"
1780 GOTO 1810
1790 PRINT:PRINT"PERCENTAGE S.I. PER MAAND,"
1800 PRINT"KWARTAAL,HALFJAAR OF JAAR <M/K/H/J>?"
1810 GOSUB 210
1820 IF (in$="D") OR (in$="d") THEN c=36500:k$="DAGEN"
1830 IF (in$="W") OR (in$="w") THEN c=5200:k$="WEKEN"
1840 IF (in$="M") OR (in$="m") THEN c=1200:k$="MAANDEN"
1850 IF (in$="J") OR (in$="j") THEN c=100:k$="JAAR"
1860 IF (in$="K") OR (in$="k") THEN k$="KWARTAAL"
1870 IF (in$="H") OR (in$="h") THEN k$="HALFJAAR"
1880 s$=in$
1890 GOSUB 100:RETURN
1900 PRINT:PRINT"TOETS <S>":GOSUB 210
1910 IF (in$="s") OR (in$="S") THEN 1130
1920 GOTO 1900
1930 GOSUB 100:kp=0:t=0:p=0:c=0:a=0
1940 k=0:r=0:ke=0:n=0
1950 PRINT"SAMENGESTELDE INTRESTBEREKENINGEN"
1960 PRINT"================================="
1970 PRINT:PRINT" 1 BEREKENING RENTE MET S.I."
1980 PRINT:PRINT" 2 CONTANTE WAARDE EEUWIGDURENDE RENTE"
1990 PRINT:PRINT" 3 CONTANTE WAARDE TYDELIJKE RENTE"
2000 PRINT:PRINT" 4 EINDWAARDE TYDELYKE RENTE"
2010 PRINT:PRINT" 5 EINDWAARDE STORTING MET OPNAMES"
2020 PRINT:PRINT" 6 NAAR HOOFDMENU"
2030 PRINT:PRINT:PRINT"MAAK UW KEUZE A.U.B"
2040 GOSUB 210:r$=in$
2050 IF (r$<"1") OR (r$>"6") THEN 2030
2060 ON VAL(r$) GOTO 2070,2180,2350,2530,2710,1010
2070 GOSUB 100:k=0:GOSUB 1790
2080 PRINT:PRINT"GEEF BEDRAG     ";:INPUT kp
2090 PRINT:PRINT"RENTE % PER ";k$;
2100 INPUT p:PRINT 
2110 PRINT"HOEVEEL JAAR STAAT HET BEDRAG UIT";:INPUT t
2120 GOSUB 4160
2130 ke=kp*(p^t)
2140 r=ke-kp
2150 sr=r:GOSUB 310
2160 PRINT:PRINT"UW RENTE NA ";t;" JAAR IS f";sr$
2170 GOTO 4210
2180 GOSUB 100:k=0
2190 PRINT"DE C.W. V/E EEUWIGDURENDE RENTE IS HET"
2200 PRINT"BEDRAG WAT MEN NU KAN BETALEN OM DE"
2210 PRINT"EEUWIGDURENDE VERPLICHTING AF TE KOPEN."
2220 PRINT 
2230 PRINT"VERVALLEN DE TERMIJNEN AAN HET BEGIN OF"
2240 PRINT"OF AAN HET EINDE VAN ELK JAAR <B/E> ?";
2250 GOSUB 210:s$=in$:PRINT:PRINT 
2260 PRINT"OVER HOEVEEL JAAR BEGINT U TE BETALEN";:INPUT t
2270 PRINT:PRINT"BEDRAG PER  JAAR";:INPUT r
2280 PRINT:PRINT"RENTE % PER JAAR";:INPUT p:PRINT 
2290 p=p/100:c=(1+p)^t
2300 c=1/c:c=c/p:kp=r*c
2310 IF (s$="b") OR (s$="B") THEN kp=kp*(1+p):
2320 sr=kp:GOSUB 310
2330 PRINT"DE CONTANTE WAARDE IS f";sr$
2340 GOTO 4210
2350 GOSUB 100:k=0
2360 PRINT"DE C.W. V/E TYDELIJKE RENTE IS HET"
2370 PRINT"BEDRAG WAT MEN NU KAN BETALEN OM DE"
2380 PRINT"TYDELIJKE VERPLICHTING AF TE KOPEN":PRINT 
2390 PRINT"VERVALLEN DE TERMIJNEN AAN HET BEGIN"
2400 PRINT"OF EINDE VAN ELK JAAR <B/E> ?";
2410 GOSUB 210:s$=in$:PRINT 
2420 PRINT"OVER HOEVEEL JAAR BEGINT DE BETALING";:INPUT n
2430 PRINT:PRINT"HOEVEEL JAAR MOET U BETALEN";:INPUT t
2440 PRINT:PRINT"BEDRAG PER  JAAR";:INPUT r
2450 PRINT:PRINT"RENTE % PER JAAR";:INPUT p:PRINT 
2460 p=p/100:c=1-(1/((1+p)^t))
2470 kp=(r/p)*c
2480 IF (s$="b") OR (s$="B") THEN kp=kp*(1+p)
2490 kp=kp*(1/((1+p)^n))
2500 sr=kp:GOSUB 310
2510 PRINT"DE CONTANTE WAARDE IS f";sr$
2520 GOTO 4210
2530 GOSUB 100:k=0
2540 PRINT"U STORT JAARLIJKS EEN BEDRAG,WAT IS UW"
2550 PRINT"TEGOED NA EEN BEPAALD AANTAL JAREN":PRINT 
2560 PRINT"STORT U DE BEDRAGEN AAN HET BEGIN"
2570 PRINT"OF AAN HET EINDE VAN ELK JAAR <B/E> ?";
2580 GOSUB 210:s$=in$:PRINT 
2590 PRINT:PRINT"HOEVEEL STORT U JAARLIJKS";:INPUT r
2600 PRINT:PRINT"HOEVEEL JAAR STORT U";:INPUT t
2610 PRINT:PRINT"WAT IS HET RENTE %";:INPUT p
2620 p=p/100:IF (s$="e") OR (s$="E") THEN 2650
2630 FOR i=1 TO t:c=c+((1+p)^i):NEXT i:kp=r*c
2640 GOTO 2670
2650 FOR i=1 TO (t-1):c=c+((1+p)^i):NEXT i
2660 c=c+1:kp=r*c
2670 sr=kp:GOSUB 310:PRINT 
2680 PRINT"UW TEGOED NA ";t;" STORTINGEN AAN HET"
2690 PRINT"EIND VAN HET JAAR IS f";sr$
2700 GOTO 4210
2710 GOSUB 100:k=0
2720 PRINT"U STORT EEN BEPAALD BEDRAG OP DE BANK"
2730 PRINT"EN NEEMT OP HET EIND VH JAAR STEEDS EEN"
2740 PRINT"BEPAALD BEDRAG OP. HOEVEEL HEEFT U DAN"
2750 PRINT"NOG OVER NA EEN BEPAALD AANTAL JAREN"
2770 PRINT:PRINT"HOEVEEL STORT U IN HET BEGIN";:INPUT kp
2780 PRINT:PRINT"NA HOEVEEL JAAR RESTWAARDE";:INPUT t
2790 PRINT:PRINT"WAT IS HET RENTE %";:INPUT p
2800 PRINT:PRINT"HOEVEEL NEEMT U OP HET EIND VAN ELK "
2810 PRINT"JAAR OP";:INPUT a:PRINT 
2820 p=p/100:kp=kp*((1+p)^t)
2830 i=0:c=0:FOR i=1 TO (t-1)
2840 c=c+((1+p)^i):NEXT i
2850 c=c+1:a=a*c:kp=kp-a
2860 sr=kp:GOSUB 310
2870 PRINT"UW SALDO NA ";t;" OPNAMEN OP HET EIND"
2880 PRINT"VAN HET JAAR IS f";sr$
2890 GOTO 4210
2900 GOSUB 100:kp=0:t=0:p=0:c=0:a=0
2910 k=0:r=0:ke=0:n=0:j=0:
2920 PRINT"     ANNUITEITEN"
2930 PRINT"     ==========="
2940 PRINT:PRINT" 1 BEREKENING ANNUITEIT"
2950 PRINT:PRINT" 2 BEREKENING HYPOTHEEKBEDRAG"
2960 PRINT:PRINT" 3 TABELLEN HYPOTHEEK VASTE ANNUITEIT"
2970 PRINT:PRINT" 4 ANNUITEIT MET VASTE AFLOSSING"
2980 PRINT:PRINT" 5 ANNUITEIT MET VASTE TOE OF AFNAME"
2990 PRINT:PRINT" 6 NAAR HOOFDMENU"
3000 PRINT:PRINT:PRINT"MAAK UW KEUZE A.U.B"
3010 GOSUB 210:r$=in$
3020 IF (r$<"1") OR (r$>"7") THEN 3000
3030 ON VAL(r$) GOTO 3040,3200,3720,3310,3470,1010
3040 GOSUB 100:k=0
3050 PRINT"U NEEMT EEN HYPOTHEEK EN WILT WETEN WAT"
3060 PRINT" U DEZE PER JAAR EN MAAND GAAT KOSTEN":PRINT 
3070 PRINT"WELK BEDRAG WILT U LENEN";:INPUT kp
3080 PRINT:PRINT"WAT IS HET RENTE %";:INPUT p
3090 PRINT:PRINT"HOELANG IS DE LOOPTYD";:INPUT t
3100 p=p/100
3110 a=kp*p/(1-(1/(1+p))^t)
3120 sr=a:GOSUB 310
3130 PRINT:PRINT"DE JAARANNUITEIT IS f";sr$
3140 sr=(a/12):GOSUB 310
3150 PRINT:PRINT"DE MAANDANNUITEIT   f";sr$
3160 v=t:PRINT:PRINT 
3170 PRINT"NU KUNT U NAAR DE TABELLEN GAAN OM HET"
3180 PRINT"HYPOTHEEKVERLOOP TE BEKIJKEN"
3190 GOSUB 3640:GOTO 4240
3200 GOSUB 100:k=0
3210 PRINT"HOEVEEL HYPOTHEEK KUNT U VOOR EEN BEP."
3220 PRINT"BEDRAG PER MAAND KRIJGEN":PRINT 
3230 PRINT"WAT WILT U PER MAAND BETALEN";:INPUT a
3240 PRINT:PRINT"WAT IS HET RENTE %";:INPUT p
3250 PRINT:PRINT"HOELANG IS DE LOOPTYD";:INPUT t
3260 p=p/100
3270 kp=((12*a)*(1-(1/(1+p))^t))/p
3280 sr=kp:GOSUB 310
3290 PRINT:PRINT"U KUNT f";sr$;" HYPOTHEEK NEMEN"
3300 GOTO 4240
3310 GOSUB 100:k=0
3320 PRINT"WELK BEDRAG WILT U LENEN";:INPUT kp
3330 PRINT:PRINT"WAT IS HET RENTE %";:INPUT p
3340 PRINT:PRINT"HOELANG IS DE LOOPTYD";:INPUT t:PRINT 
3350 v=kp/t:PRINT 
3360 a=v+((p/100)*kp)
3370 sr=v:GOSUB 310
3380 PRINT"DE VASTE AFLOSSING IS  f";sr$;" PER JR"
3390 PRINT:sr=a:GOSUB 310
3400 PRINT"DE EERSTE ANNUITEIT IS f";sr$;" PER JR"
3410 d=(kp-v)*(p/100)
3420 r=(p/100)*kp-d
3430 sr=r:GOSUB 310
3440 PRINT:PRINT"VOLGENDE ANNUITEITEN f";sr$
3450 PRINT"PER JAAR LAGER"
3460 GOTO 4240
3470 GOSUB 100:k=0
3480 PRINT"WELK BEDRAG WILT U LENEN";:INPUT kp
3490 PRINT:PRINT"WAT IS HET RENTE %";:INPUT p
3500 PRINT:PRINT"HOELANG IS DE LOOPTYD";:INPUT t:PRINT 
3510 PRINT"HOEVEEL NEEMT ANNUITEIT TOE OF AF +/-";
3520 INPUT d
3530 PRINT 
3540 FOR i=1 TO t:c=c+(1/((1+p/100)^i)):NEXT i
3550 c=1/c:a=(kp+(t*d/(p/100)))*c
3560 a=a-(d/(p/100)+(t*d))
3570 sr=a:GOSUB 310
3580 PRINT:PRINT"ANNUITEIT EERSTE JAAR  IS f";sr$
3590 s=a-((p/100)*kp):PRINT:sr=s:GOSUB 310
3600 PRINT"DE EERSTE AFLOSSING IS    f";sr$
3610 sr=d:GOSUB 310
3620 PRINT:PRINT"VOLGENDE ANNUITEITEN VERSCHIL f";sr$
3630 GOTO 4240
3640 REM **tabellen**
3650 DIM a(t),l(t),r(t),k(t),s(t),e(t)
3660 an=a-kp*p
3670 FOR i=1 TO t:a(i)=an*(1+p)^(i-1)
3680 l(i)=l(i-1)+a(i):r(i)=a-a(i)
3690 k(i)=k(i-1)+r(i):s(i)=kp-l(i-1)
3700 e(i)=kp-l(i):NEXT i
3710 RETURN
3720 GOSUB 100:PRINT:PRINT 
3730 PRINT"MENU TABELLEN HYPOTHEEK VASTE ANNUITEIT"
3740 PRINT 
3750 PRINT:PRINT" 1 AFLOSSING EN RENTE"
3760 PRINT:PRINT" 2 AFLOSSING/RENTE CUMULATIEF"
3770 PRINT:PRINT" 3 SCHULD BEGIN/EINDE"
3780 PRINT:PRINT" 4 NAAR MENU ANNUITEITEN"
3790 PRINT:PRINT:PRINT"MAAK UW KEUZE A.U.B"
3800 GOSUB 210:r$=in$
3810 IF (r$<"1") OR (r$>"4") THEN 3790
3820 ON VAL(r$) GOTO 3830,3930,4030,2900
3830 GOSUB 100
3840 PRINT"JAAR    RENTE    AFLOSSING   TOTAAL":PRINT 
3850 FOR i=1 TO v:sr=a(i):GOSUB 310:a$=sr$
3860 sr=r(i):GOSUB 310:r$=sr$
3870 sr=a(i)+r(i):GOSUB 310:tt$=sr$
3880 ct=2:cn=0:sr=i:GOSUB 310:i$=sr$
3890 PRINT" ";i$;"  ";r$;"  ";a$;"  ";tt$
3900 ct=9:cn=2
3910 NEXT i
3920 GOTO 4130
3930 GOSUB 100
3940 PRINT"JAAR    RENTE    AFLOSSING   TOTAAL":PRINT 
3950 FOR i=1 TO v:sr=k(i):GOSUB 310:a$=sr$
3960 sr=l(i):GOSUB 310:r$=sr$
3970 sr=k(i)+l(i):GOSUB 310:tt$=sr$
3980 ct=2:cn=0:sr=i:GOSUB 310:i$=sr$
3990 PRINT" ";i$;"  ";a$;"  ";r$;"  ";tt$
4000 ct=9:cn=2
4010 NEXT i
4020 GOTO 4130
4030 GOSUB 100
4040 PRINT"JAAR    SCHULD   AFLOSSING   SCHULD":PRINT 
4050 FOR i=1 TO v:sr=a(i):GOSUB 310:a$=sr$
4060 sr=s(i):GOSUB 310:r$=sr$
4070 sr=e(i):GOSUB 310:tt$=sr$
4080 ct-2:cn=0:sr=i:GOSUB 310:i$=sr$
4090 PRINT" ";i$;"  ";r$;"  ";a$;"  ";tt$
4100 ct=9:cn=2
4110 NEXT i
4120 GOTO 4130
4130 PRINT:PRINT"TOETS <S>":GOSUB 210
4140 IF (in$="s") OR (in$="S") THEN 3720
4150 GOTO 4130
4160 IF (s$="m") OR (s$="M") THEN p=(1+(p/100))^12
4170 IF (s$="k") OR (s$="k") THEN p=(1+(p/100))^4
4180 IF (s$="h") OR (s$="H") THEN p=(1+(p/100))^2
4190 IF (s$="j") OR (s$="J") THEN p=1+(p/100)
4200 RETURN
4210 PRINT:PRINT"TOETS <S>":GOSUB 210
4220 IF (in$="s") OR (in$="S") THEN 1930
4230 GOTO 4210
4240 PRINT:PRINT"TOETS <S>":GOSUB 210
4250 IF (in$="s") OR (in$="S") THEN 2900
4260 GOTO 4240
30000 REM ***********************
30010 REM ** p2000 16k jan.1984**
30020 REM ** p.kuepers         **
30030 REM ** lindenstraat 26   ** 
30040 REM ** 6031xv nederweert **
30050 REM ***********************
30060 REM
30070 REM copyright nos hilversum

1000 A=300:GOTO 20:REM BREUKEN-2
1010 D$="/":S$=" ":CN=0:S=0:H=0:HT=HO:VT=VE:VF=0
1011 FR=1:GOSUB 280
1012 SP$="          ":SP$=SP$+SP$+SP$+SP$
1020 REM STARTSCHERM
1030 HO=12:VE=3:GOSUB 110:PRINT "//BREUKEN II//"
1040 HO=5:VE=6:GOSUB 110:PRINT "vermenigvuldigen + delen"
1050 HO=2:VE=8:GOSUB 110
1060 PRINT "worteltrekken + machtsverheffen"
1070 HO=2:VE=15:GOSUB 110
1080 PRINT "EDUCATIEF PROGRAMMA VAN D.MAASKANT"
1090 HO=26:VE=20:GOSUB 110:PRINT "APRIL 1987":GOSUB 4340
1100 REM MENU
1110 GOSUB 100:HO=4:VE=0:GOSUB 110
1120 PRINT "MAAK EEN KEUZE UIT DIT MENU"
1130 PRINT :PRINT :PRINT "    1 = UITLEG VERMENIGVULDIGEN"
1140 PRINT :PRINT "    2 = UITLEG DELEN"
1150 PRINT :PRINT "    3 = OPGAVEN VERM/DELEN"
1160 PRINT :PRINT "    4 = UITLEG MACHTSVERHEFFEN"
1170 PRINT :PRINT "    5 = UITLEG WORTELTREKKEN"
1180 PRINT :PRINT "    6 = EINDTOETS"
1190 PRINT :PRINT "    7 = STOPPEN":GOSUB 4340
1200 IF (IN<49) OR (IN>56) THEN 1110
1210 ON IN-48 GOTO 1230,2150,2830,2850,3500,4200,4220
1220 REM VERMENIGVULDIGEN
1230 GOSUB 600:HO=.2:VE=0:SR$="UITLEG VERMENIGVULDIGEN"
1240 GOSUB 650:H=0:V=.08:H1=.5:GOSUB 4520
1250 SR$="Als dit een hele balk is en we"
1260 HO=0:VE=.16:GOSUB 650
1270 SR$="vermenigvuldigen die met 2 ":VE=.24:GOSUB 650
1290 SD=4:GOSUB 450:H=0:V=.32:H1=.9999:GOSUB 4520
1300 SR$="dan wordt hij twee keer zo lang.":HO=0:VE=.4
1310 GOSUB 650:SD=8:GOSUB 450
1320 SR$="Als we hem met 1/2 vermenigvuldigen":VE=.52
1330 GOSUB 650:SD=4:GOSUB 450:H=0:V=.6:H1=.25:GOSUB 4520
1350 SR$="wordt hij de helft van de 1e balk.":HO=0:VE=.68
1360 GOSUB 650:SD=8:GOSUB 450:GOSUB 4300:GOSUB 210
1370 GOSUB 600:H=0:V=0:H1=.5:GOSUB 4520
1380 SR$="x 2 =":VE=0:HO=.6:GOSUB 650:SD=4:GOSUB 450
1390 H=0:V=.08:H1=.9999:GOSUB 4520:SD=8:GOSUB 450
1400 H=0:V=.2:H1=.5:GOSUB 4520
1410 SR$="x 1 =":HO=.6:VE=.2:GOSUB 650:SD=4:GOSUB 450
1420 H=0:V=.28:H1=.5:GOSUB 4520:SD=8:GOSUB 450
1430 H=0:V=.4:H1=.5:GOSUB 4520
1440 SR$="x 1/2 =":HO=.6:VE=.4:GOSUB 650:SD=4:GOSUB 450
1450 H=0:V=.48:H1=.25:GOSUB 4520:SD=8:GOSUB 450
1460 SR$="VERMENIGVULDIGEN MET EEN BREUK DIE"
1470 HO=0:VE=.55:GOSUB 650
1480 SR$="KLEINER IS DAN 1 IS DELEN":VE=.65:GOSUB 650
1490 SR$="balk x 1/2 =":HO=.2:VE=.75:GOSUB 650
1500 SR$="balk x 1 ":HO=.6:VE=.7:GOSUB 650
1510 SR$="-------- ":VE=.75:GOSUB 650
1520 SR$="   2     ":VE=.8:GOSUB 650
1530 SD=4:GOSUB 450:GOSUB 4300:GOSUB 210
1540 GOSUB 100:HO=0:VE=2:GOSUB 110
1550 PRINT "Je kunt alleen BREUKEN met elkaar"
1560 PRINT :PRINT "vermenigvuldigen. Van hele getallen"
1570 PRINT :PRINT "moet je dus eerst een breuk maken."
1580 PRINT :PRINT "Van een heel getal maak je een"
1590 PRINT :PRINT "breuk met noemer 1, de waarde"
1600 PRINT :PRINT "van het getal blijft gelijk."
1610 HO=29:VE=14:GOSUB 110:PRINT "2   3"
1620 PRINT "   2 x 3/4 is hetzelfde als ---x---"
1630 HO=29:VE=16:GOSUB 110:PRINT "1   4"
1640 GOSUB 4340:GOSUB 100:HO=0:VE=2:GOSUB 110
1650 PRINT "Bestaat een getal uit een heel getal"
1660 PRINT :PRINT "en een breuk b.v  6 2/3  dan kun je"
1670 PRINT :PRINT "ook niet direkt vermenigvuldigen."
1680 PRINT :PRINT "Je moet er eerst een breuk van maken"
1690 PRINT :PRINT 
1700 PRINT "Dat doe je door van het hele getal een"
1710 PRINT :PRINT "breuk te maken met dezelfde noemer."
1720 PRINT :PRINT "Dan tel je beide breuken bij elkaar op"
1730 PRINT :PRINT "Als voorbeeld  6 2/3"
1740 PRINT :PRINT "               6 = 18/3"
1750 PRINT :PRINT "               18/3 + 2/3 = 20/3"
1760 GOSUB 4340
1770 GOSUB 4450:HO=0:VE=5:GOSUB 110:GOSUB 4560
1780 SR$="VERMENIGVULDIGEN":GOSUB 4555
1790 PRINT :GOSUB 4560
1800 SR$="EERST BREUKEN MAKEN":GOSUB 4555
1820 SR$="TELLER x TELLER":GOSUB 4555
1840 SR$="NOEMER x NOEMER":GOSUB 4555
1860 SR$="VEREENVOUDIGEN":GOSUB 4555
1870 GOSUB 4340
1880 REM VOORBEELDEN
1890 GOSUB 100:HO=0:VE=2:GOSUB 110
1900 PRINT " We gaan nu een voorbeeld uitwerken "
1910 HO=5:VE=4:GOSUB 110:SR$=" 2 1/3 x 3 1/4 =":GOSUB 150
1920 HO=0:VE=6:GOSUB 110
1930 PRINT " 1e getal wordt  6/3 + 1/3 = 7/3"
1940 PRINT :PRINT " 2e getal wordt 12/4 + 1/4 = 13/4"
1950 PRINT :PRINT " De opgave wordt "
1960 HO=5:VE=12:GOSUB 110:SR$="   7/3 x 13/4  = ":GOSUB 150
1970 HO=0:VE=14:GOSUB 110
1980 PRINT "                7 x 13     = 91"
1990 PRINT :PRINT "                3 x  4     = 12"
2000 PRINT :PRINT " het antwoord is"
2010 HO=5:VE=20:GOSUB 110:SR$="        91/12  = 7 7/12"
2020 GOSUB 150:GOSUB 4340
2030 SR$="   voorbeelden vermenigvuldigen   ":HO=0:VE=0
2040 GOSUB 100:GOSUB 150:V=6:GOSUB 5070
2050 HO=0:VE=5:GOSUB 110
2060 PRINT "  2  x  5/6=          10/6 =  1 2/3"
2070 PRINT :PRINT 
2075 PRINT "  3/4x  7/8=          21/32=  21/32"
2080 PRINT :PRINT 
2085 PRINT "1 3/5x2 1/3= 8/5x7/3= 56/15= 3 11/15"
2090 PRINT :PRINT 
2095 PRINT "2 1/2x2 1/4= 5/2x9/4= 45/8 = 5  5/8"
2100 GOSUB 4340
2110 REM OEFENOPGAVEN
2120 SR$="  oefenopgaven vermenigvuldigen   ":V=6:R=3
2130 GOSUB 5130
2140 REM UITLEG DELEN
2150 GOSUB 600:HO=.3:VE=0:SR$="UITLEG DELEN":GOSUB 650
2160 H=0:V=.08:H1=.5:GOSUB 4520
2170 SR$="Als dit een hele balk is en we"
2180 HO=0:VE=.16:GOSUB 650
2190 SR$="delen die door 2 ":VE=.24:GOSUB 650:SD=4:GOSUB 450
2210 H=0:V=.32:H1=.25:GOSUB 4520
2220 SR$="wordt hij de helft van de 1e balk":HO=0:VE=.4
2230 GOSUB 650:SD=8:GOSUB 450
2240 SR$="Als we hem door 1/2 delen":VE=.52:GOSUB 650
2250 SD=4:GOSUB 450:H=0:V=.6:H1=.9999:GOSUB 4520
2260 SR$="wordt hij twee keer zo lang":HO=0:VE=.68
2270 GOSUB 650:SD=8:GOSUB 450:GOSUB 4300:GOSUB 210
2280 GOSUB 600:H=0:V=0:H1=.5:GOSUB 4520
2290 SR$=" :2 =":VE=0:HO=.6:GOSUB 650:SD=4:GOSUB 450
2300 H=0:V=.08:H1=.25:GOSUB 4520:SD=8:GOSUB 450
2310 H=0:V=.2:H1=.5:GOSUB 4520
2320 SR$=" :1 =":HO=.6:VE=.2:GOSUB 650:SD=4:GOSUB 450
2330 H=0:V=.28:H1=.5:GOSUB 4520:SD=8:GOSUB 450
2340 H=0:V=.4:H1=.5:GOSUB 4520
2350 SR$=" :1/2 =":HO=.6:VE=.4:GOSUB 650:SD=4:GOSUB 450
2360 H=0:V=.48:H1=.9999:GOSUB 4520:SD=8:GOSUB 450
2370 SR$="DELEN DOOR EEN BREUK DIE KLEINER"
2380 HO=0:VE=.55:GOSUB 650
2390 SR$="IS DAN 1 IS VERMENIGVULDIGEN.":VE=.65:GOSUB 650
2400 SR$="balk : 1/2 = balk x 2/1 ":HO=.2:VE=.75:GOSUB 650
2410 SD=4:GOSUB 450:GOSUB 4300:GOSUB 210
2420 GOSUB 100:HO=0:VE=2:GOSUB 110
2430 PRINT "Over delen door een breuk kunnen we"
2440 PRINT :PRINT "heel kort zijn."
2450 PRINT :PRINT "Delen is het omgekeerde van"
2460 PRINT :PRINT "vermenigvuldigen.":PRINT 
2470 PRINT :PRINT "Als je een getal deelt door een getal:"
2480 PRINT :PRINT " groter  dan 1, dan wordt het kleiner"
2490 PRINT :PRINT "   precies   1, dan blijft het gelijk"
2500 PRINT :PRINT " kleiner dan 1, dan wordt het groter"
2510 GOSUB 4340:GOSUB 100
2520 PRINT "Als het getal waardoor je moet delen"
2530 PRINT :PRINT "bestaat uit een heel getal en een"
2540 PRINT :PRINT "breuk, dan moet je er eerst een breuk"
2550 PRINT :PRINT "van maken,zoals bij vermenigvuldigen."
2560 PRINT :PRINT :PRINT "Daarna keer je de tweede breuk om"
2570 PRINT :PRINT "en ga je vermenigvuldigen.":GOSUB 4340
2580 GOSUB 4450:HO=0:VE=5:GOSUB 110:GOSUB 4560
2590 SR$="DELEN":GOSUB 4555
2600 PRINT :GOSUB 4560
2610 SR$="EERST BREUKEN MAKEN":GOSUB 4555
2630 SR$="TWEEDE BREUK OMDRAAIEN":GOSUB 4555
2650 SR$="TELLER x TELLER":GOSUB 4555
2670 SR$="NOEMER x NOEMER":GOSUB 4555
2690 SR$="VEREENVOUDIGEN":GOSUB 4555
2700 GOSUB 4340
2710 GOSUB 100:HO=0:VE=0:GOSUB 110
2720 SR$="Voorbeeld  delen door een breuk  ":GOSUB 150
2730 V=7:GOSUB 5070:VE=3:GOSUB 110
2740 PRINT "   2/3 : 1/4  = 2/3 x 4/1 =  8/3 = 2 2/3":PRINT 
2750 PRINT :PRINT " 1 3/4 : 2/5  = 7/4 x 5/2 = 35/8 = 4 3/8"
2760 PRINT :PRINT :PRINT "   1/3 : 2 1/4=1/3 : 9/4  ="
2770 PRINT :PRINT "              =1/3 x 4/9  = 4/27"
2780 GOSUB 4340
2790 REM OEFENOPGAVEN
2800 SR$="      oefenopgaven delen          "
2810 V=7:R=4:GOSUB 5130
2820 REM OPGAVEN VERMENIGV. + DELEN
2830 R1=0:R=4:GOSUB 5780
2840 REM UITLEG MACHTSVERHEFFEN
2850 GOSUB 600:HO=.2:VE=0
2855 SR$="UITLEG MACHTSVERHEFFEN":GOSUB 650
2860 H=0:V=.08:H1=.4:N=4:GOSUB 4590:P=0:GOTO 2990
2870 HO=.45:FOR VE=.12 TO .42 STEP .1
2880 SR$="1":GOSUB 650
2900 NEXT VE
2910 FOR HO=H TO H+(N-1)/N*H1 STEP H1/N:VE=.53
2915 SR$=" 1":GOSUB 650:NEXT HO:HO=0:VE=.6
2920 SR$="Met dit vierkant kunnen we laten  ":GOSUB 650
2930 VE=.68:P=P+1
2940 SR$="zien wat een kwadraat is.":GOSUB 650:VE=.75
2950 SR$=" 2 ":GOSUB 650:VE=.8
2960 SR$="4  = 4 x 4 = 16. Er zijn 16 vakjes":GOSUB 650
2970 IF P=2 THEN RETURN
2980 GOSUB 4300:GOSUB 210:RETURN
2990 GOSUB 2870:CN=1:GOSUB 2870:CN=0:P=0:GOTO 3110
3000 HO=.45:FOR VE=.13 TO .43 STEP .1
3010 SR$="1/4":GOSUB 650
3030 NEXT VE
3040 VE=.51:FOR HO=H TO H+(N-1)/N*H1 STEP H1/N
3045 SR$=" 1/4":GOSUB 650:NEXT HO
3050 HO=0:VE=.6:P=P+1
3060 SR$="Als elk lijnstukje 1/4 voorsteld":GOSUB 650:VE=.7
3070 SR$="is dit de tekening van het ":GOSUB 650:VE=.8
3080 SR$="kwadraat van de breuk 4/4         ":GOSUB 650
3090 IF P=2 THEN RETURN
3100 GOSUB 210:RETURN
3110 GOSUB 3000:CN=1:GOSUB 3050:CN=0:P=0
3120 HO=0:VE=.56:SR$="     2":GOSUB 650:VE=.58
3130 SR$="(4/4)   = 4/4 x 4/4 = 16/16":GOSUB 650:VE=.66
3140 SR$="Machtsverheffen is ook bij breuken":GOSUB 650
3150 VE=.74
3160 SR$="eigenlijk gewoon vermenigvuldigen ":GOSUB 650
3170 VE=.82
3180 SR$="teller x teller en noemer x noemer":GOSUB 650
3190 GOSUB 4300:GOSUB 210
3200 GOSUB 4450:HO=0:VE=5:GOSUB 110:GOSUB 4560
3210 SR$="MACHTSVERHEFFEN":GOSUB 4555
3220 PRINT :GOSUB 4560
3230 SR$="EERST BREUKEN MAKEN":GOSUB 4555
3250 SR$="TELLER x TELLER":GOSUB 4555
3270 SR$="NOEMER x NOEMER":GOSUB 4555
3290 SR$="VEREENVOUDIGEN":GOSUB 4555
3300 GOSUB 4340:GOSUB 100:HO=0:VE=5:GOSUB 110
3310 PRINT "Op de computer gebruiken we een teken"
3320 PRINT :PRINT "voor een kwadraat en wel ^"
3330 PRINT :PRINT "dus 4^2 wil zeggen 4 tot de 2e macht"
3340 PRINT :PRINT "4^3 is 4 tot de derde macht"
3350 PRINT :PRINT "4^4 is 4 tot de vierde macht"
3360 GOSUB 4340:GOSUB 100
3370 SR$="   voorbeelden machtsverheffen   ":GOSUB 150
3380 V=6:GOSUB 5070:VE=3:GOSUB 110
3390 PRINT " 4^2     = 4  x  4 = 16"
3400 PRINT :PRINT "(1/4)^2  =1/4 x 1/4= 1/16"
3410 PRINT :PRINT "(2 1/3)^2=(7/3)^2=7/3x7/3=49/9 = 5 4/9"
3420 PRINT :PRINT " 4+2^2/5 = 4+2x2/5 = 4 4/5"
3430 PRINT :PRINT " 3^2+1/6 = 3x3+1/6 = 9 1/6"
3440 PRINT :PRINT " 2+2/3^2 = 2+2/(3x3) = 2  2/9"
3450 GOSUB 4340:GOSUB 100
3460 REM oefenopgaven machtsverheffen
3470 SR$="  oefenopnamen machtsverheffen    "
3480 V=6:R=5:GOSUB 5130
3490 REM uitleg worteltrekken
3500 GOSUB 600:HO=.2:VE=0:GOSUB 110
3505 SR$="UITLEG WORTELTREKKEN "
3510 GOSUB 650
3520 H=0:V=.1:H1=.4:N=3:GOSUB 4590
3530 HO=.2:VE=.3:SR$="9":GOSUB 650
3540 HO=.45:VE=.15:SR$="1":GOSUB 650
3550 VE=.28:SR$="1":GOSUB 650:VE=.41:SR$="1":GOSUB 650
3570 VE=.55:FOR HO=H TO H+(N-1)/N*H1 STEP H1/N
3575 SR$=" 1":GOSUB 650:NEXT HO
3580 P=0:GOTO 3640
3590 HO=0:VE=.65:P=P+1
3600 SR$="Dit vierkant stelt het getal 9 voor":GOSUB 650
3610 VE=.75:SR$="er zijn 9 vakjes van 1 bij 1      "
3620 GOSUB 650:IF P=2 THEN RETURN
3630 GOSUB 4300:GOSUB 210:RETURN
3640 GOSUB 3590:CN=1:GOSUB 3590:CN=0:P=0:GOTO 3690
3650 VE=.65:SR$="De wortel uit 9 is dus 3 ":GOSUB 650
3660 VE=.75:SR$="want 3 x 3 = 9":GOSUB 650
3670 IF P=2 THEN RETURN
3680 GOSUB 4300:GOSUB 210:RETURN
3690 GOSUB 3650:GOSUB 600
3700 H=0:V=.1:H1=.4:N=4:GOSUB 4590:GOTO 3820
3710 HO=.21:VE=.35:SR$="16":GOSUB 650
3720 HO=.45:VE=.12:SR$="1":GOSUB 650
3730 VE=.25:SR$="1":GOSUB 650:VE=.35:SR$="1":GOSUB 650
3750 VE=.45:SR$="1":GOSUB 650
3760 VE=.55:FOR HO=H TO H+(N-1)/N*H1 STEP H1/N
3765 VE=.53:SR$=" 1":GOSUB 650:NEXT HO
3770 HO=0:VE=.65:P=P+1
3780 SR$="Dit vierkant stelt 16 voor 4x4=16 ":GOSUB 650
3790 VE=.75:SR$="De wortel van 16 is dus 4":GOSUB 650
3800 IF P=2 THEN RETURN
3810 GOSUB 4300:GOSUB 210:RETURN
3820 GOSUB 3710:CN=1:GOSUB 3710:CN=0:P=0:
3830 HO=.45:VE=.12:SR$="1/4":GOSUB 650
3840 VE=.25:SR$="1/4":GOSUB 650:VE=.35:SR$="1/4":GOSUB 650
3860 VE=.45:SR$="1/4":GOSUB 650
3870 VE=.55:FOR HO=H TO H+(N-1)/N*H1 STEP H1/N
3875 SR$=" 1/4":GOSUB 650:NEXT HO
3880 HO=0:VE=.62
3890 SR$="Nu stelt het vierkant 16/16 voor  ":GOSUB 650
3900 VE=.7:SR$=" 4/4 x 4/4 = 16/16":GOSUB 650
3910 VE=.78:SR$="De wortel uit 16/16 is dus 4/4":GOSUB 650
3920 GOSUB 4300:GOSUB 210
3930 GOSUB 4450:HO=0:VE=5:GOSUB 110:GOSUB 4560
3940 SR$="WORTELTREKKEN":GOSUB 4555
3950 PRINT :GOSUB 4560
3960 SR$="WORTEL UIT DE TELLER":GOSUB 4555
3980 SR$="WORTEL UIT DE NOEMER":GOSUB 4555
4000 SR$="VEREENVOUDIGEN":GOSUB 4555
4010 GOSUB 4340
4020 GOSUB 100:HO=0:VE=5:GOSUB 110
4030 PRINT "Omdat we in basicode het wortelteken"
4040 PRINT :PRINT "niet kunnen gebruiken nemen we hiervoor"
4050 PRINT :PRINT "de V. Dus V16 = wortel 16 = 4 "
4060 PRINT :PRINT "          V25 = wortel 25 = 5 "
4070 GOSUB 4340
4080 REM voorbeelden worteltrekken
4090 GOSUB 100:HO=0:VE=0:GOSUB 110
4100 SR$="    Voorbeelden worteltrekken    ":GOSUB 150
4110 V=8:GOSUB 5070:VE=3:GOSUB 110
4120 PRINT " V(4/16)  = V4/V16   = 2/4"
4130 PRINT :PRINT " V25/6    =          = 5/6"
4140 PRINT :PRINT "   5/V36  =          = 5/6"
4150 GOSUB 4340:GOSUB 100
4160 REM oefenopgaven worteltrekken
4170 SR$="    oefenopgaven worteltrekken    "
4180 V=8:R=6:GOSUB 5130
4190 REM EINDTOETS
4200 R1=1:GOSUB 5780
4210 REM EINDROUTINE
4220 GOSUB 100:HO=0:VE=4:GOSUB 110:SR$="":GOSUB 4570
4230 SR$="DIT IS HET EINDE VAN BREUKEN II":GOSUB 4555
4270 HO=23:VE=20:GOSUB 110:SR$="D.MAASKANT":GOSUB 150
4280 SD=40:GOSUB 450:GOTO 950
4290 REM SUBROUTINES
4300 SR$="================================="
4310 HO=0:VE=.85:GOSUB 650:SR$="druk op een toets    "
4330 HO=0:VE=.9:GOSUB 650:RETURN
4340 HO=0:VE=VT-1:GOSUB 110
4350 PRINT "======================================"
4360 PRINT "  druk op een toets.<M> is menu       ";
4370 GOSUB 210:IF IN=77 THEN 1110
4380 RETURN
4390 HO=1:VE=VT:GOSUB 110
4400 PRINT "druk op een toets.<B> is meer opgaven ";
4410 GOSUB 210:IF IN=66 THEN 5790
4420 IF (IN<>77) THEN RETURN
4430 HO=1:VE=VT:GOSUB 110
4440 PRINT "geef eindantwoord en druk op <RETURN>";:RETURN
4450 GOSUB 100:PRINT 
4460 SR$="---------------------------------":GOSUB 4570
4480 SR$="  LEER DEZE REGELS UIT JE HOOFD  ":GOSUB 4570
4490 SR$="---------------------------------":GOSUB 4570
4500 RETURN
4510 REM HELE BALK
4520 HO=H:VE=V:GOSUB 620:HO=H1:GOSUB 630:VE=V+.05:GOSUB 630
4540 HO=H:GOSUB 630:VE=V:GOSUB 630:RETURN
4550 REM LEGE REGEL
4555 GOSUB 4570
4560 SR$="  "
4570 LS$=LEFT$(SP$,INT((HT+1-LEN(SR$))/2)-3)
4575 SR$=LEFT$(LS$+SR$+LS$,HT-6)
4579 GOSUB 150:PRINT :RETURN
4580 REM vierkanten
4590 HO=H:VE=V:GOSUB 620:HO=H1:GOSUB 630:VE=VE+H1:GOSUB 630
4600 HO=H:GOSUB 630:VE=V:GOSUB 630
4610 FOR I=1 TO N
4620 HO=HO+H1/N:VE=V:GOSUB 620:VE=VE+H1:GOSUB 630
4630 NEXT I:VE=V
4640 FOR I=1 TO N
4650 HO=H:VE=VE+H1/N:GOSUB 620:HO=HO+H1:GOSUB 630
4660 NEXT I:RETURN
4680 REM GETALLEN KIEZEN
4690 GOSUB 260:T1=INT(RV*10):IF T1=0 THEN 4690
4700 SR=T1:GOSUB 300:T1$=SR$
4710 GOSUB 260:T2=INT(RV*10):IF T2=0 THEN 4710
4720 SR=T2:GOSUB 300:T2$=SR$
4750 GOSUB 260:N1=INT(RV*10):IF N1=0 THEN 4750
4760 SR=N1:GOSUB 300:N1$=SR$
4770 GOSUB 260:N2=INT(RV*10):IF N2=0 THEN 4770
4780 SR=N2:GOSUB 300:N2$=SR$
4810 GOSUB 260:G1=(INT(RV*5))+1
4820 SR=G1:GOSUB 300:G1$=SR$
4830 GOSUB 260:G2=(INT(RV*5))+1
4840 SR=G2:GOSUB 300:G2$=SR$
4850 G3=G1*G1:SR=G3:GOSUB 300:G3$=SR$
4860 G4=G2*G2:SR=G4:GOSUB 300:G4$=SR$
4870 GOSUB 260:M1=INT(RV*2):SR=M1:GOSUB 300:M1$=SR$
4880 M1$="":IF M1=1 THEN M1$="^2"
4900 GOSUB 260:M2=INT(RV*2):SR=M2:GOSUB 300:M2$=SR$
4910 M2$="":IF M2=1 THEN M2$="^2"
4930 GOSUB 260:M3=INT(RV*2):SR=M3:GOSUB 300:M3$=SR$
4940 M3$="":IF M3=1 THEN M3$="^2"
4960 GOSUB 260:W1=INT(RV*2):SR=W1:GOSUB 300:W1$=SR$
4970 W1$="":IF W1=1 THEN W1$="V"
4990 GOSUB 260:W2=INT(RV*2):SR=W2:GOSUB 300:W2$=SR$
5000 W2$="":IF W2=1 THEN W2$="V"
5020 GOSUB 260:W3=INT(RV*2):SR=W3:GOSUB 300:W3$=SR$
5030 W3$="":IF W3=1 THEN W3$="V"
5050 RETURN
5060 REM VOETNOTEN
5070 IF V=5 THEN SR$="        goed             fout    "
5080 IF V=6 THEN SR$="teller x teller , noemer x noemer "
5090 IF V=7 THEN SR$="2e breuk omdraaien, t x t en n x n"
5100 IF V=8 THEN SR$="       V(x/y) = Vx/Vy             "
5110 HO=0:VE=21:GOSUB 110:GOSUB 150:RETURN
5120 REM OPGAVEN-GENERATOR
5130 GOSUB 100:HO=0:VE=0:GOSUB 110:GOSUB 150:GOSUB 5070
5140 GOSUB 4430:VE=0:S=0
5160 FOR I=1 TO 10:H=0:HO=2:VE=VE+2:GOSUB 110
5180 GOSUB 4690
5190 ON R GOTO 5210,5260,5320,5370,5450,5570
5200 REM OPTELLEN+++++
5210 PRINT T1$+D$+N1$;" + ";T2$+D$+N2$;" = ";:INPUT A$
5220 X=T1*N2+T2*N1:Y=N1*N2
5230 GOSUB 5690:IF S>0 THEN RETURN
5240 GOTO 5670
5250 REM AFTREKKEN-----
5260 IF T1*N2<T2*N1 THEN 5180
5270 PRINT T1$+D$+N1$;" - ";T2$+D$+N2$;" = ";:INPUT A$
5280 X=T1*N2-T2*N1:Y=N1*N2:H=0
5290 GOSUB 5690:IF S>0 THEN RETURN
5300 GOTO 5670
5310 REM VERMENIGVULDIGEN *****
5320 PRINT G1$+S$+T1$+D$+N1$;" x ";G2$+S$+T2$+D$+N2$;" =";
5330 INPUT A$:X=(G1*N1+T1)*(G2*N2+T2):Y=N1*N2
5340 GOSUB 5690:IF S>0 THEN RETURN
5350 GOTO 5670
5360 REM DELEN /////
5370 PRINT G1$+S$+T1$+D$+N1$;" : ";G2$+S$+T2$+D$+N2$;" =";
5380 INPUT A$
5390 IF G1=0 THEN G1=1
5400 IF G2=0 THEN G2=1
5410 X=(G1*N1+T1)*N2:Y=N1*(G2*N2+T2)
5420 GOSUB 5690:IF S>0 THEN RETURN
5430 GOTO 5670
5440 REM MACHTSVERHEFFEN ^^^^^
5450 IF (M1=0) AND (M2=0) AND (M3=0) THEN GOSUB 4870:GOTO 5450
5460 IF M1=1 THEN M1=G1
5470 IF M1=0 THEN M1=1
5480 PRINT G1$+M1$;"+";T1$+M2$+D$+N1$+M3$;" = ";:INPUT A$
5490 IF M2=1 THEN M2=T1
5500 IF M2=0 THEN M2=1
5510 IF M3=1 THEN M3=N1
5520 IF M3=0 THEN M3=1
5530 X=G1*M1*N1*M3+T1*M2:Y=N1*M3
5540 GOSUB 5690:IF S>0 THEN RETURN
5550 GOTO 5670
5560 REM WORTELTREKKEN VVVVV
5570 IF W1=0 THEN 5600
5580 PRINT W1$;"(";G3$+D$+G4$;") =";:INPUT A$
5590 X=G1:Y=G2:GOTO 5660
5600 IF (W2=0) AND (W3=0) THEN GOSUB 4990:GOTO 5600
5610 PRINT W2$+G3$+D$+W3$+G4$;" = ";:INPUT A$
5620 IF W2=1 THEN X=G1
5630 IF W2=0 THEN X=G3
5640 IF W3=1 THEN Y=G2
5650 IF W3=0 THEN Y=G4
5660 GOSUB 5690:IF S>0 THEN RETURN
5670 NEXT I
5680 GOSUB 4390:RETURN
5690 IF X>=Y THEN H=INT(X/Y):X=X-H*Y
5700 FOR J=0 TO 3
5710 IF (X/7=INT(X/7)) AND (Y/7=INT(Y/7)) THEN X=X/7:Y=Y/7
5720 IF (X/5=INT(X/5)) AND (Y/5=INT(Y/5)) THEN X=X/5:Y=Y/5
5730 IF (X/3=INT(X/3)) AND (Y/3=INT(Y/3)) THEN X=X/3:Y=Y/3
5740 IF (X/2=INT(X/2)) AND (Y/2=INT(Y/2)) THEN X=X/2:Y=Y/2
5750 NEXT J:GOTO 5960
5770 REM VARIABELE OPGAVEN en EINDTOETS
5780 G=0:F=0
5785 IF R1>0 THEN 5800
5790 IF R<7 THEN SR$="       variabele opgaven          "
5800 IF R1=1 THEN SR$="       eindtoets breuken          "
5810 GOSUB 100:HO=0:VE=0:GOSUB 110:GOSUB 150:V=5:GOSUB 5070
5820 GOSUB 4430:GOSUB 5930:VE=0
5830 FOR I=1 TO 10
5840 H=0:HO=1:VE=VE+2:GOSUB 110
5850 GOSUB 4690:GOSUB 260:S=INT(RV*4)+1
5860 IF (R=3) OR (R=4) THEN 5890
5870 GOSUB 260:S=INT(RV*6)+1
5880 ON S GOSUB 5210,5260,5320,5370,5450,5570:GOTO 5900
5890 GOSUB 260:S=INT(RV*2)+1:ON S GOSUB 5320,5370
5900 VF=VE:GOSUB 5930
5910 NEXT I
5920 GOSUB 4390:RETURN
5930 HO=6:VE=21:GOSUB 110:PRINT G:HO=23:GOSUB 110:PRINT F
5940 VE=VF:RETURN
5950 REM ANTWOORDCONTROLE
5960 IF X<1 THEN K$="":GOTO 5990
5970 SR=X:GOSUB 300:K$=SR$+D$
5980 SR=Y:GOSUB 300:K$=K$+SR$
5990 IF H>=1 THEN SR=H:GOSUB 300:K$=SR$+S$+K$
6000 IF X=0 THEN SR=H:GOSUB 300:K$=SR$
6010 IF A$<>K$ THEN 6040
6020 HO=30:GOSUB 110:PRINT "GOED":G=G+1:HO=2:RETURN
6040 HO=30:GOSUB 110:PRINT K$:F=F+1:HO=2:RETURN
30000 REM BREUKEN DEEL 2
30010 REM GEMAAKT OP EEN SONY HB201P
30020 REM MSX-COMPUTER
30030 REM D.MAASKANT
30040 REM RUYSDAELLAAN 5
30050 REM 3931 TR WOUDENBERG
31000 REM TROS-RADIO dd. 870617

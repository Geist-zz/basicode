1000 a=200:GOTO 20:REM getalconversie
1010 GOSUB 100:PRINT 
1015 PRINT"   ***** GETALCONVERSIE *****"
1020 PRINT 
1030 PRINT"U heeft de volgende mogelijkheden:"
1040 PRINT 
1050 PRINT"1. van DECIMAAL     naar BINAIR."
1060 PRINT"2. van BINAIR       naar DECIMAAL.
1063 PRINT"3. van DECIMAAL     naar OKTAAL."
1065 PRINT"4. van OKTAAL       naar DECIMAAL."
1070 PRINT"5. van DECIMAAL     naar HEXADECIMAAL.
1080 PRINT"6. van HEXADECIMAAL naar DECIMAAL.
1083 PRINT"7. van DECIMAAL     naar ROMEINS."
1085 PRINT"8. van ROMEINS      naar DECIMAAL."
1090 PRINT:PRINT:PRINT"MAAK UW KEUZE! (1-8)";:INPUT aa
1100 IF (aa>=1) AND (aa<=8) THEN 1110
1105 RUN
1110 ON aa GOTO 1130,1400,3105,3400,1550,1980,2630,2320
1120 REM *** dec-bin ***
1130 GOSUB 100:PRINT:PRINT 
1140 PRINT"Geef het DECIMALE getal   (max. 131071)":INPUT b
1145 IF b>131071 THEN PRINT"GETAL TE GROOT!!!":GOTO 1140
1150 GOSUB 100:PRINT:PRINT:PRINT 
1160 PRINT"DECIMAAL:        BINAIR:"
1170 PRINT"*************************"
1180 PRINT 
1190 w=b:w$="":x$="":x=65536
1200 y=w-x
1210 IF (y<0) THEN w$="0":GOTO 1230
1220 w=y:w$="1"
1230 x$=x$+w$
1240 x=x/2
1250 IF (x<1) THEN 1270
1260 GOTO 1200
1270 IF (LEFT$(x$,1)="0") THEN 1290
1280 GOTO 1350
1290 z$=""
1300 FOR i=2 TO LEN(x$)
1310 z$=z$+MID$(x$,i,1)
1320 NEXT i
1330 x$=z$
1340 GOTO 1270
1350 PRINT b;TAB(18);x$
1360 PRINT:PRINT:PRINT:PRINT"Wilt U stoppen ?"
1370 GOSUB 210
1380 IF (in$="j") OR (in$="J") THEN GOTO 950
1390 RUN
1400 GOSUB 100:PRINT:PRINT 
1410 PRINT"Geef het BINAIRE getal";:INPUT y$
1420 GOSUB 100:PRINT:PRINT:PRINT 
1430 e=0:i=LEN(y$):j=1
1440 IF (i=0) THEN 1480
1450 IF (MID$(y$,i,1)="1") THEN e=e+j
1460 i=i-1:j=j*2
1470 GOTO 1440
1480 PRINT"BINAIR:           DECIMAAL:"
1490 PRINT"****************************"
1500 PRINT 
1510 PRINT y$;TAB(18);e
1520 PRINT:PRINT:PRINT:PRINT"Wilt U stoppen"
1530 GOSUB 210:IF (in$="j") OR (in$="J") THEN GOTO 950
1540 RUN
1550 REM *** dec-hex ***
1560 GOSUB 100:PRINT:PRINT 
1570 PRINT"Geef het DECIMALE getal";:INPUT f
1580 f$=""
1590 i=1
1600 b=f/i
1610 IF (b>=16) THEN i=i*16:GOTO 1600
1620 e=f
1630 c=e/i
1640 d=INT(c)+1
1650 IF (d>8) THEN 1680
1660 ON d GOSUB 1820,1830,1840,1850,1860,1870,1880,1890
1670 GOTO 1700
1680 l=d-8
1690 ON l GOSUB 1900,1910,1920,1930,1940,1950,1960,1970
1700 f$=f$+g$
1710 e=e-i*(d-1)
1720 i=i/16
1730 IF (i>=1) THEN 1630
1740 GOSUB 100:PRINT:PRINT:PRINT 
1750 PRINT"DECIMAAL:     HEXADECIMAAL:"
1760 PRINT"****************************"
1770 PRINT 
1780 PRINT f;TAB(15);f$
1790 PRINT:PRINT:PRINT:PRINT"Wilt U stoppen ?"
1800 GOSUB 210:IF (in$="j") OR (in$="J") THEN GOTO 950
1810 RUN
1820 g$="0":RETURN
1830 g$="1":RETURN
1840 g$="2":RETURN
1850 g$="3":RETURN
1860 g$="4":RETURN
1870 g$="5":RETURN
1880 g$="6":RETURN
1890 g$="7":RETURN
1900 g$="8":RETURN
1910 g$="9":RETURN
1920 g$="A":RETURN
1930 g$="B":RETURN
1940 g$="C":RETURN
1950 g$="D":RETURN
1960 g$="E":RETURN
1970 g$="F":RETURN
1980 REM *** hex-dec ***
1990 GOSUB 100:PRINT:PRINT:PRINT 
2000 PRINT"Geef het HEXADECIMALE getal";:INPUT z$
2010 GOSUB 100:PRINT:PRINT:PRINT 
2020 PRINT"HEXADECIMAAL:     DECIMAAL:"
2030 PRINT"****************************"
2040 PRINT 
2050 i=LEN(z$)
2060 j=1
2070 q$=MID$(z$,i,1)
2080 IF (q$="0") THEN q=0
2090 IF (q$="1") THEN q=1
2100 IF (q$="2") THEN q=2
2110 IF (q$="3") THEN q=3
2120 IF (q$="4") THEN q=4
2130 IF (q$="5") THEN q=5
2140 IF (q$="6") THEN q=6
2150 IF (q$="7") THEN q=7
2160 IF (q$="8") THEN q=8
2170 IF (q$="9") THEN q=9
2180 IF (q$="A") OR (q$="a") THEN q=10
2190 IF (q$="B") OR (q$="b") THEN q=11
2200 IF (q$="C") OR (q$="c") THEN q=12
2210 IF (q$="D") OR (q$="d") THEN q=13
2220 IF (q$="E") OR (q$="e") THEN q=14
2230 IF (q$="F") OR (q$="f") THEN q=15
2240 r=r+j*q
2250 j=j*16
2260 i=i-1
2270 IF (i>0) THEN 2070
2280 PRINT z$;TAB(18);r
2290 PRINT:PRINT:PRINT:PRINT"Wilt U stoppen ?"
2300 GOSUB 210:IF (in$="j") OR (in$="J") THEN GOTO 950
2310 RUN
2320 REM *** romeins-dec ***
2330 GOSUB 100
2340 PRINT 
2350 PRINT"I=1:V=5:X=10:L=50:C=100:D=500:M=1000"
2360 PRINT:PRINT 
2370 PRINT"Geef het ROMEINSE getal";:INPUT r$
2380 GOSUB 100
2390 k=LEN(r$)
2400 DIM a(22)
2410 FOR j=1 TO k
2415 b$=MID$(r$,j,1)
2420 IF (b$="I") OR (b$="i") THEN a(j)=1
2430 IF (b$="V") OR (b$="v") THEN a(j)=5
2440 IF (b$="X") OR (b$="x") THEN a(j)=10
2450 IF (b$="L") OR (b$="l") THEN a(j)=50
2460 IF (b$="C") OR (b$="c") THEN a(j)=100
2470 IF (b$="D") OR (b$="d") THEN a(j)=500
2480 IF (b$="M") OR (b$="m") THEN a(j)=1000
2490 NEXT j
2500 r=0
2510 FOR j=1 TO k
2520 IF (a(j)>=a(j+1)) THEN r=r+a(j)
2530 IF (a(j)<a(j+1)) THEN r=r-a(j)
2540 NEXT j
2550 PRINT:PRINT 
2560 PRINT"ROMEINS:            DECIMAAL:"
2570 PRINT"******************************"
2580 PRINT 
2585 IF (r>=4000) THEN PRINT r$;TAB(20);"groter dan 3999 =>"
2587 IF (r>=4000) THEN PRINT TAB(20);"GEEN ROMEINS GETAL!!"
2588 IF (r>=4000) THEN 2600
2590 PRINT r$;TAB(20);r
2600 PRINT:PRINT:PRINT:PRINT"Wilt U stoppen ?"
2610 GOSUB 210:IF (in$="j") OR (in$="J") THEN GOTO 950
2620 RUN
2630 REM *** dec-romeins ***
2640 GOSUB 100:PRINT:PRINT 
2650 PRINT"Geef het DECIMALE getal";:INPUT a$
2660 IF (VAL(a$)>=4000) THEN 2680
2670 GOTO 2710
2680 PRINT"Romeinse getallen moeten kleiner "
2690 PRINT"dan 4000 zijn!"
2700 GOTO 2650
2710 GOSUB 100:PRINT:PRINT 
2720 PRINT"DECIMAAL:    ROMEINS:"
2730 PRINT"**********************"
2740 PRINT 
2750 DIM c(5)
2760 b=LEN(a$)
2770 j=4
2780 FOR i=b TO 1 STEP -1
2790 c$=MID$(a$,i,1)
2800 c(j)=VAL(c$)
2810 j=j-1
2820 NEXT i
2830 u=1
2835 DIM v(22)
2840 FOR j=1 TO b
2850 p=2*(b-j)+1:d=c(4-b+j)
2860 IF (d=4) OR (d=9) THEN 2930
2870 IF (d>=5) THEN r=p+2:v(u)=r:u=u+1:d=d-5
2880 r=p+1
2885 IF (d=0) THEN 2980
2890 FOR e=1 TO d
2900 v(u)=r:u=u+1
2910 NEXT e
2920 GOTO 2980
2930 r=p+1
2940 v(u)=r:u=u+1
2950 IF (d=4) THEN r=p+2
2960 IF (d=9) THEN r=p+3
2970 v(u)=r:u=u+1
2980 NEXT j
2990 FOR k=u TO 20
3000 v(k)=1
3010 NEXT k
3020 z$=" IVXLCDM":y$=""
3030 FOR n=1 TO 20
3040 k$=MID$(z$,v(n),1)
3050 y$=y$+k$
3060 NEXT n
3070 PRINT a$;TAB(14);y$
3080 PRINT:PRINT:PRINT:PRINT"Wilt U stoppen ?"
3090 GOSUB 210:IF (in$="j") OR (in$="J") THEN GOTO 950
3100 RUN
3105 REM *** dec-okt ***
3110 GOSUB 100:PRINT:PRINT 
3120 PRINT"Geef het DECIMALE getal";:INPUT f
3130 GOSUB 100:PRINT:PRINT:PRINT 
3140 PRINT"DECIMAAL:       OKTAAL:"
3150 PRINT"************************"
3160 f$="":i=1
3170 b=f/i
3180 IF (b>=8) THEN i=i*8:GOTO 3170
3190 e=f
3200 c=e/i
3210 d=INT(c)+1
3220 ON d GOSUB 3270,3280,3290,3300,3310,3320,3330,3340
3230 f$=f$+g$
3240 e=e-i*(d-1)
3250 i=i/8
3260 IF (i>=1) THEN 3200
3265 GOTO 3350
3270 g$="0":RETURN
3280 g$="1":RETURN
3290 g$="2":RETURN
3300 g$="3":RETURN
3310 g$="4":RETURN
3320 g$="5":RETURN
3330 g$="6":RETURN
3340 g$="7":RETURN
3350 PRINT 
3360 PRINT f;TAB(17);f$
3370 PRINT:PRINT:PRINT:PRINT"Wilt U stoppen ?"
3380 GOSUB 210:IF (in$="j") OR (in$="J") THEN GOTO 950
3390 RUN
3400 REM *** okt-dec ***
3405 GOSUB 100:PRINT:PRINT 
3410 PRINT"Geef het OKTALE getal";:INPUT z$
3420 GOSUB 100:PRINT:PRINT 
3430 PRINT"OKTAAL:          DECIMAAL:"
3440 PRINT"***************************"
3450 PRINT 
3460 i=LEN(z$):j=1
3470 q$=MID$(z$,i,1)
3480 q=VAL(q$)
3490 r=r+j*q
3500 j=j*8
3510 i=i-1
3520 IF (i>0) THEN 3470
3530 PRINT z$;TAB(17);r
3540 PRINT:PRINT:PRINT:PRINT"Wilt U stoppen ?"
3550 GOSUB 210:IF (in$="j") OR (in$="J") THEN GOTO 950
3560 RUN
30000 REM ***********************
30010 REM *                     *
30020 REM * geschreven op een   *
30030 REM *                     *
30040 REM * commodore-64 door:  *
30050 REM *                     *
30060 REM * edward vermaas,     *
30070 REM *                     *
30080 REM * patrijshoek 30,     *
30090 REM *                     *
30100 REM * 3201 jk spijkenisse.*
30110 REM *                     *
30120 REM ***********************
30130 REM
30140 REM copyright nos hilversum

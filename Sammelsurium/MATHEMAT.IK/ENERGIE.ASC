1000 A=200:GOTO 20:REM ## ENERGY COSTS ##
1010 SR$="ELECTRICAL ENERGY COST CALCULATION"
1020 GOTO 3000
1030 HO=1:VE=2:GOSUB 110:GOSUB 150
1040 HO=0:VE=4:GOSUB 110
1050 PRINT" Entering the performance parameters"
1060 PRINT"--------------------------------"
1070 PRINT
1200 INPUT" working price ($/kWh) =";AP
1210 INPUT" fixed power price ($) =";FL
1220 INPUT" variable power price ($/kWh) =";VL
1230 INPUT" Settlement rate f.counter ($) =";VP
1240 PRINT" Average"
1250 INPUT" maximum price ($/kWh) =";DH
1260 INPUT" minimum price ($/kWh) =";DM:PRINT
1270 INPUT" value added tax (VAT)(%) =";US:PRINT
1280 INPUT" Power consumption (kWh/year) =";JV
2000 REM
2010 JK=JV*(AP+VL)
2020 GK=JK+FL
2030 DK=GK/JV:C=US*.01+1
2040 IF DK>DH THEN 2400
2050 IF DK<DM THEN 2600
2060 GS=(GK+VP)*C
2200 PRINT
2210 PRINT" Total cost = ";INT(GS*100+.5)/100;
2220 PRINT" $":PRINT
2230 PRINT" Average price/kWh =";
2240 PRINT INT(GK*C*1000/JV+.5)/1000;" $"
2250 PRINT" (incl. VAT)"
2300 GOSUB 210:IF JV=1500 THEN 4100:GOTO 3000
2310 GOTO 3000
2400 GS=DH*JV*C+VP:PRINT
2410 PRINT" Total cost = ";INT(GS*100+.5)/100;
2420 PRINT" $":PRINT
2430 PRINT" Average price/kWh =";
2440 PRINT DH*C;" $":PRINT
2450 PRINT"(incl. VAT)"
2460 GOSUB 210:IF JV=1500 THEN 4100:GOTO 3000
2470 GOTO 3000
2600 GS=DM*JV*C+VP:PRINT
2610 PRINT" Total cost = ";INT(GS*100+.5)/100;
2620 PRINT" $":PRINT
2630 PRINT" Average price/kWh =";
2640 PRINT DM*C;" $"
2650 PRINT"(incl. VAT)"
2660 GOSUB 210:IF JV=1500 THEN 4100:GOTO 3000
2680 GOTO 3000
3000 GOSUB 100:HO=1:VE=2:GOSUB 110:GOSUB 150
3010 HO=0:VE=4:GOSUB 110
3020 PRINT" M E N U "
3030 PRINT" ---------"
3040 PRINT" -1- Explanation and example parameters"
3050 PRINT:PRINT" -2- Entering the parameters"
3060 PRINT:PRINT" -3- Recalculation with new"
3070 PRINT"     energy consumption (kWh/year)":PRINT
3080 PRINT" -4- Graphical representation":PRINT
3090 PRINT" -5- Finish calculation"
3100 GOSUB 210
3110 IF IN$="1" THEN GOSUB 100:GOTO 6010
3120 IF IN$="2" THEN PRINT:GOSUB 100:GOTO 1030
3130 IF IN$="3" THEN GOSUB 100:GOTO 1280
3140 IF IN$="4" THEN 4010
3150 IF IN$="5" THEN 950
3160 GOTO 3000
4000 REM
4010 GOSUB 100
4020 PRINT"Enter the maximum energy consumption "
4030 PRINT"(kWh/year)":PRINT
4040 INPUT N
4050 INPUT"Enter the maximum energy costs ($)";MK
4100 GOSUB 600:CN=0
4110 HO=0:VE=.99:GOSUB 620
4120 HO=.99:VE=.99:GOSUB 630
4130 HO=0:VE=0:GOSUB 620
4140 HO=0:VE=.99:GOSUB 630
4150 FOR F=1 TO 3
4160 FOR E=0 TO 1 STEP .05
4170 HO=F/300:VE=E:GOSUB 620
4180 NEXT E
4190 NEXT F
4200 FOR F=1 TO 4
4210 FOR E=0 TO 1 STEP .05
4220 HO=E:VE=.99-F/200:GOSUB 620
4230 NEXT E
4240 NEXT F
4300 REM
4310 FOR M=0.01 TO N-100 STEP N/300
4320 JK=M*(AP+VL):GK=JK+FL:DK=GK/M:C=US*.01+1
4330 IF DK>DH THEN 4600
4340 IF DK<DM THEN 4800
4350 GS=(GK+VP)*C:DP=GK*C/M
4400 HO=M/N:VE=1-GS/MK:GOSUB 620
4410 VE=1-DP:GOSUB 620
4420 GOTO 5000
4600 GS=DH*M*C+VP:DP=DH*C
4610 HO=M/N:VE=1-GS/MK:GOSUB 620
4620 VE=1-DP:GOSUB 620
4700 GOTO 5000
4800 GS=DM*M*C+VP:DP=DM*C
4810 HO=M/N:VE=1-GS/MK:GOSUB 620
4820 VE=1-DP:GOSUB 620
4900 GOTO 5000
5000 NEXT M:HO=0:VE=0
5010 SR=MK:GOSUB 300:SR$=SR$+" $ (1.-$/kWh)"
5020 GOSUB 650
5030 HO=.8:VE=.93:SR=N:GOSUB 300:SR$=SR$+" kWh"
5040 GOSUB 650
5050 HO=.3:VE=.55:SR$="(average price/kWh)"
5060 GOSUB 650
5070 HO=.45:VE=.25:SR$="energy costs/year"
5080 GOSUB 650
5090 SR$=" ELECTRICAL ENERGY COST CALCULATION"
5100 GOSUB 210:GOTO 3000
6000 REM EXPLANATION
6010 HO=4:VE=2:GOSUB 110:SR$="EXPLANATION":GOSUB 150
6020 HO=0:VE=4:GOSUB 110
6030 PRINT"In the 5 new federal states,"
6040 PRINT"the introduction of a new energy cost"
6050 PRINT"calculation has caused some confusion."
6060 PRINT
6070 PRINT"This program enables the energy"
6080 PRINT"cost calculation for any type"
6090 PRINT"of tariff.":PRINT
6100 PRINT"An example with graph. Representation"
6110 PRINT"for a medium-sized household in"
6120 PRINT"the ESAG tariff area (Saxony)"
6130 PRINT"certainly helps to understand the"
6140 HO=30:VE=24:GOSUB 110:PRINT"KEY=>":GOSUB 210
6150 GOSUB 100:PRINT" Example:"
6160 PRINT" --------"
6170 PRINT" working price/kWh = 0.14 $/kWh":PRINT
6180 PRINT" fixed performance price = 89.40 $":PRINT
6190 PRINT" variable performance price = .02 $/kWh"
6200 PRINT
6210 PRINT" transfer price = 31.80 $":PRINT
6220 PRINT" average-":PRINT
6230 PRINT" maximum price      = .42 $/kWh"
6240 PRINT" minimum price      = .171 $/kWh"
6250 PRINT:PRINT" Value Added Tax (VAT) = 14%"
6260 PRINT:PRINT" annual consumption = 1500 kWh"
6300 AP=.14:FL=89.4:VL=.02:VP=31.8
6310 DH=.42:DM=.171:US=14:JV=1500
6320 N=2000:MK=500
6330 SR$="ELECTRICAL ENERGY COST CALCULATION"
6400 GOTO 2010
7000 GOSUB 210
30130 REM -----------------------------
30140 REM ENERGY COST CALCULATION
32000 REM ----------------------------
32010 REM -    (C) 03.01.91          -
32020 REM -    MANFRED STEUDNER      -
32030 REM -    HEIDESTRASSE 166      -
32040 REM -    O-8142 RADEBERG       -
32050 REM -    ON COMMODORE C64     -
32060 REM ----------------------------
32070 REM
32080 REM DS-KULTUR
32090 REM -spezial 911113

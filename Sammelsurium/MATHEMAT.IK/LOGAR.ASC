1000 a=500:GOTO 20:REM --- log. dualis
1010 t$="Logarithmus dualis (ld)"
1020 f$=" F e h l e i n g a b e "
1030 ue$="Numerus      ld":u$=ue$+"  !  "+ue$
1040 st$="---------------":s1$=st$+"     "+st$
1050 s2$="-----------------!-----------------"
1060 s3$="          "
1070 t1$="  !  ":t2$="      0    ----  !"
1080 a=0:e=0:d=0:d1=0:d2=0:d3=0
1090 z=0:z1=0:ze=0:s=0:n=0:en=0
1100 p$="":p1$="":p2$="":p3$="":p4$=""
1110 pp=0:sr=0:sr$=""
1120 ct=8:cn=0:lg=LOG(2):l1=0:l2=0
1130 REM
1140 GOSUB 2040
1150 REM
1160 ho=0:ve=2:GOSUB 110
1170 PRINT "Das Programm erlaubt - ueber Bildschirm"
1180 PRINT "oder Drucker - den Ausdruck der  Log-"
1190 PRINT "arithmen zur Basis b=2 im Bereich":PRINT 
1200 PRINT "        0 ... 999 999":PRINT 
1210 PRINT "fuer ganzzahlige positive Numeri n :":PRINT 
1220 PRINT "- Bereich und Schrittweite (1 oder"
1230 PRINT "  gerade Zahl) sind beliebig waehlbar,":PRINT 
1240 PRINT "- der Logarithmus fuer n=0 ist nicht"
1250 PRINT "  definiert."
1260 GOSUB 2070:GOSUB 100:GOSUB 2040
1270 ho=0:ve=2:GOSUB 110
1280 PRINT "Soll der Ausdruck ueber den Drucker er-"
1290 PRINT "folgen, ist zu beachten, dass je Seite"
1300 PRINT "nur 120 Positionen Platz finden.":PRINT 
1310 PRINT "Der weiter auszudruckende Bereich ist"
1320 PRINT "dann erneut festzulegen.":PRINT 
1330 PRINT "Drucker vorhanden / betriebsbereit ?":PRINT 
1340 PRINT "Taste - (J)a / (N)ein - ";:GOSUB 210:PRINT in$
1350 pp=0:IF in=74 THEN pp=1
1360 REM
1370 GOSUB 2070:GOSUB 100:GOSUB 2040
1380 REM
1390 REM
1400 ho=0:ve=2:GOSUB 110
1410 a$="":INPUT"Untere Grenze ";a$:a=VAL(a$):IF a=1 THEN a=0
1420 e$="":INPUT"Obere  Grenze ";e$:e=VAL(e$)
1430 IF (INT(a)<>a) OR (INT(e)<>e) THEN  GOSUB 1970:GOTO 1400
1440 IF (a<0) OR (e<0) THEN  GOSUB 1970:GOTO 1400
1450 IF e<=a THEN  GOSUB 1970:GOTO 1400
1460 IF e>999999 THEN  GOSUB 1970:GOTO 1400
1470 ho=0:ve=5:GOSUB 110
1480 s$="":INPUT"Schrittweite  ";s$:s=VAL(s$)
1490 IF s<1 THEN  GOSUB 1970:GOTO 1400
1500 IF (s>1) AND (s/2<>INT(s/2)) THEN  GOSUB 1970:GOTO 1400
1510 IF (pp=1) AND (e-a)/(s*2)>60 THEN  GOSUB 1970:GOTO 1400
1520 ho=5:ve=9:GOSUB 110
1530 PRINT "                           "
1540 REM
1550 d=e-a:d1=d/2:d2=INT(d/2)
1560 IF d1<>d2 THEN d=d+1
1570 z1=(d/s)/2:z=INT(z1)+1
1580 IF a=0 THEN d3=z*s-s:en=d3:GOTO 1610
1590 d3=z*s:en=d3-1
1600 REM
1610 ho=0:ve=9:GOSUB 110
1620 IF pp=0 THEN PRINT u$
1630 IF pp=0 THEN PRINT s1$
1640 REM
1650 IF pp=1 THEN sr$=s3$+t$:GOSUB 350:GOSUB 360:GOSUB 360
1660 IF pp=1 THEN sr$=s3$+u$:GOSUB 350:GOSUB 360
1670 IF pp=1 THEN sr$=s3$+s2$:GOSUB 350:GOSUB 360
1680 FOR n=0 TO en STEP s
1690 REM
1700 IF (a=0) AND (n=0) THEN 1870
1710 l1=LOG(a+n)/lg
1720 l2=LOG(d3+a+n)/lg
1730 REM
1740 sr=a+n:ct=7:cn=0:GOSUB 310:p1$=sr$
1750 sr=l1:ct=8:cn=2:GOSUB 310:p2$=sr$
1760 sr=d3+a+n:ct=7:cn=0:GOSUB 310:p3$=sr$
1770 sr=l2:ct=8:cn=2:GOSUB 310:p4$=sr$
1780 REM
1790 p$=p1$+p2$+t1$+p3$+p4$
1800 IF pp=0 THEN PRINT p$
1810 GOSUB 120
1820 IF pp=1 THEN sr$=s3$+p$:GOSUB 350:GOSUB 360
1830 REM
1840 IF ve=21 THEN  GOSUB 2070:GOSUB 2130
1850 IF ve<>21 THEN 1900
1860 REM
1870 IF pp=0 THEN PRINT t2$
1880 IF pp=1 THEN sr$=s3$+t2$:GOSUB 350:GOSUB 360
1890 REM
1900 NEXT n
1910 REM
1920 IF pp=1 THEN  GOSUB 360:GOSUB 360
1930 REM
1940 GOSUB 2070
1950 GOSUB 2040:GOTO 1400
1960 REM
1970 ho=5:ve=9:GOSUB 110
1980 sr$=f$:GOSUB 150
1990 FOR ve=2 TO 5
2000 ho=15:GOSUB 110:PRINT "         "
2010 NEXT ve
2020 RETURN
2030 REM
2040 GOSUB 100:ho=(39-LEN(t$)-6)/2:ve=0
2050 GOSUB 110:sr$=t$:GOSUB 150:RETURN
2060 REM
2070 ho=10:ve=23:GOSUB 110
2080 PRINT "Taste - (W)eiter / (E)nde - ";:GOSUB 210
2090 IF in=69 THEN 950
2100 IF in<>87 THEN 2070
2110 RETURN
2120 REM
2130 GOSUB 100:GOSUB 2040
2140 ho=0:ve=2:GOSUB 110
2150 PRINT u$
2160 PRINT s1$
2170 RETURN
2180 REM
32000 REM ------------------------
32010 REM friedrich dormeier
32020 REM d-1000 berlin 39
32030 REM
32040 REM c64/c128 - 1/1990
32050 REM ------------------------
32060 REM
32070 REM erstausstrahlung: radio ddr
32080 REM -spezial 900302

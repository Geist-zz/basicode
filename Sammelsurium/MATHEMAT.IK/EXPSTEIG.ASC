1000 a=200:GOTO 20:REM == exponentielle steigerung
1010 s=0:GOSUB 3240
1020 PRINT:PRINT" === OELHANDEL ==="
1030 ho=0:ve=5:GOSUB 110
1040 PRINT"BEGINN: IM JAHR NULL"
1050 PRINT:PRINT"UMSATZ ERSTES JAHR: 1 LITER"
1060 PRINT:PRINT"STEIGERUNG 5% PRO JAHR" 
1070 ho=25:ve=10:GOSUB 110:PRINT" N"
1080 PRINT"UMSATZ IM JAHR N= 1 X 1,05 LITER"
1090 n=1:e=1:p=1:p$="1,000"
2000 REM --- hauptschleife ---
2010 p=p*1.05:IF p<10 THEN 2030
2020 p=p/10:e=e+1
2030 sr=p:ct=5:cn=3:GOSUB 310
2040 p$=LEFT$(sr$,1)+","+RIGHT$(sr$,3)
2050 ho=0:ve=15:GOSUB 110
2060 PRINT"IM JAHR";n;"BETRAEGT DER UMSATZ"
2070 ho= 9:ve=17:GOSUB 110:IF e>1 THEN PRINT e;
2080 PRINT:PRINT p$;:IF e>0 THEN PRINT" x 10  ";
2085 PRINT"   LITER ";
2090 n=n+1
2100 IF e<2 THEN 2170
2110 IF e=2 THEN GOSUB 3030
2120 IF e=4 THEN GOSUB 3050
2130 IF e=8 THEN GOSUB 3080
2140 IF e=22 THEN GOSUB 3110
2150 IF e=24 THEN GOSUB 3140
2160 IF e=30 THEN GOSUB 3170
2170 IF n>1990 THEN GOSUB 3200:GOSUB 210:GOTO 950
2175 IF s=0 THEN s=1:GOSUB 210
2180 GOSUB 200:IF in$=" " THEN GOSUB 3010
2190 IF (in$="S") OR (in$="s") THEN  GOTO 950
2200 GOTO 2000
3000 REM --- unterprogramme ---
3010 GOSUB 210
3020 RETURN
3030 IF s=2 THEN RETURN
3035 ho=0:ve=21:GOSUB 110:PRINT"(ein Fass OEL = 169 Liter)"
3040 s=2:RETURN
3050 IF s=3 THEN RETURN
3055 ho=26:ve=20:GOSUB 110
3060 PRINT"   4":PRINT"(ein Laster hat ca 2 x 10   Liter)"
3070 s=3:RETURN
3080 IF s=4 THEN RETURN
3085 ho=24:ve=20:GOSUB 110:PRINT"      8"
3090 PRINT"(ein Supertanker fasst ca 2x10  Liter)"
3100 s=4:RETURN
3110 IF s=5 THEN RETURN
3115 ho=26:ve=20:GOSUB 110:PRINT"   22"
3120 PRINT"(Volumen des Mondes= 2,2 x 10   Liter)"
3130 s=5:RETURN
3140 IF s=6 THEN RETURN
3145 ho=22:ve=20:GOSUB 110:PRINT"24       "
3150 PRINT"(Volumen der Erde = 10   Liter)       "
3160 s=6:RETURN
3170 IF s=7 THEN RETURN
3175 ho=26:ve=20:GOSUB 110:PRINT"   30"
3180 PRINT"(Volumen der Sonne = 1,4 x 10   Liter)"
3190 s=7:RETURN
3200 ho=26:ve=20:GOSUB 110:PRINT"     "
3210 PRINT"(in eine solche Menge passt uns-"
3220 PRINT"er Sonnensystem bis zum Pluto!)";
3230 RETURN
3240 GOSUB 100:PRINT"EXPONENTIELLE STEIGERUNG":PRINT
3250 PRINT"Exponentielle Steigerung ist die"
3260 PRINT"Prozentuale Zunahme.":PRINT
3270 PRINT"Jeder begreift, dass eine Stei-"
3280 PRINT"gerung von 100%  pro  Jahr sehr schnell"
3290 PRINT"zu unmoeglicher Groesse fuehrt."
3300 PRINT"Denken wir uns, in jedem Jahr 2 x"
3310 PRINT"so viele Autos auf der Strasse, "
3320 PRINT"als das Jahr zuvor!":PRINT:GOSUB 210
3330 PRINT"Dieses Programm zeigt, wie ein"
3340 PRINT"denkbarer Oelhandel sich mit"
3350 PRINT"einer konstanten Umsatzzunahme"
3360 PRINT"von 5%  pro Jahr entwickelt.":PRINT
3370 PRINT"Solche Steigerung wird allgemein"
3380 PRINT"'gesunde Steigerung' genannt"
3390 PRINT" - aber wie lange geht das gut?":PRINT
3400 PRINT"   Druecke eine Taste ";:GOSUB 210
3410 GOSUB 100
3420 PRINT"Wir gehen 2000 Jahre zureck in"
3430 PRINT"die Geschichte der Menschheit.":PRINT
3440 PRINT"Hier beginnt unsere Jahreszaehlung."
3450 PRINT"Es beginnt jemand einen Oelhandel mit"
3460 PRINT"einem Umsatz von 1 Liter pro Jahr."
3470 PRINT"Jedes nachfolgende Jahr verkauft er"
3480 PRINT"5% mehr.":PRINT:GOSUB 210
3490 PRINT"Der Computer rechnet das also auf"
3500 PRINT"und zeigt dir jeweils"
3510 PRINT"ein Objekt mit einer"
3520 PRINT"vergleichbaren Groesse.":PRINT
3530 PRINT"Du kannst das Programm starten und"
3540 PRINT"anhalten mit der Space-Taste.":PRINT
3550 PRINT"Mit S brichst du das Programm ab.":PRINT
3560 PRINT"       Druecke eine Taste!";:GOSUB 210
3570 GOSUB 100
3580 RETURN
3590 REM----------------------------------
30000 REM ch.w.brederode
30010 REM von bonninghausenlaan 33
30020 REM 2161 es  lisse
30030 REM copyright nos hilversum
30050 REM fehlerfrei aufgenommen 
30060 REM am 5.2.90 von tros-radio
30070 REM durch  b.schneider
30080 REM und uebersetzt.
30090 REM

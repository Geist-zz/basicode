1000 A=100:GOTO 20:REM VREEMD GELD
1010 VT=VE:GOSUB 100:REM * SCHERM SCHOON *
1020 DIM NA$(18),BI(18),LA(18)
1030 PRINT"even geduld...ik lees de data."
1040 FOR I=1 TO 18
1050 READ NA$(I),BI(I),LA(I)
1060 NEXT I
1070 GOSUB 100:VE=3:HO=0:GOSUB 110
1080 PRINT"1. wilt u 'vreemd geld' KOPEN":PRINT
1090 PRINT"2. 'vreemd geld' VERKOPEN":PRINT
1100 PRINT"3. onderlinge KOERS bepalen":PRINT
1110 PRINT"4. de koers-LIJST"
1120 PRINT"             (event.wijzigen in data"
1130 PRINT"              vanaf regel 25000.)":PRINT
1140 PRINT"5. STOPPEN":PRINT:PRINT
1150 PRINT"** Geef het NUMMER van Uw keuze **"
1160 GOSUB 210
1170 IF IN$="1" THEN 1250
1180 IF IN$="2" THEN 1880
1190 IF IN$="3" THEN 2100
1200 IF IN$="4" THEN 2350
1210 IF IN$="5" THEN 950
1220 VE=VT-2:GOSUB 110:GOSUB 250
1230 PRINT"** kies opnieuw tussen 1 t.m.5 **"
1240 PRINT"druk toets";:GOSUB 210:GOTO 1070
1250 REM ** VREEMD GELD KOPEN **
1260 GOSUB 100:VE=3:GOSUB 110
1270 SR$=" 'VREEMD GELD' KOPEN ":GOSUB 150
1280 VE=6:GOSUB 110
1290 PRINT"1. wenst U een bepaald bedrag"
1300 PRINT"   aan 'VREEMD GELD'te kopen"
1310 VE=9:GOSUB 110
1320 PRINT"2. wenst U een bepaald bedrag "
1330 PRINT"   aan NED.GELD om te zetten"
1340 VE=12:GOSUB 110
1350 PRINT"** geef het NUMMER van Uw keuze **"
1360 GOSUB 210
1370 IF IN$="1" THEN 1430
1380 IF IN$="2" THEN 1640
1390 VE=18:GOSUB 110
1400 PRINT"** U kon kiezen tussen 1 en 2 **"
1410 PRINT"-- Druk toets --"
1420 GOSUB 250:GOSUB 210:GOTO 1250
1430 REM **VAST BEDRAG'VREEMD GELD'KOPEN **""
1440 GOSUB 100:VE=1:GOSUB 110
1450 SR$=" Bedrag'VREEMD GELD' kopen ":GOSUB 150
1460 GOSUB 5070
1470 GOSUB 5180
1480 GOSUB 100:VE=3:GOSUB 110
1490 PRINT"hoeveel ";NA$;" wilt U kopen?"
1500 INPUT B
1510 VE=5:GOSUB 110
1520 CT=10:CN=5:SR=LA:GOSUB 310
1530 PRINT"De laat-koers (per eenheid): ";SR$
1540 VE=7:GOSUB 110
1550 PRINT"Bank-kosten:";
1560 INPUT D
1570 REM ** BEREKENING TEGENWAARDE **
1580 TW=INT(100*B*LA+.5)/100+D
1590 CT=10:CN=2:SR=TW:GOSUB 310
1600 VE=14:GOSUB 110
1610 PRINT"**             ";B;" ";NA$
1620 PRINT"** kosten U  ";SR$;" gulden."
1630 GOTO 5000
1640 REM ** VOOR BEPAALD BEDRAG NED.GELD**
1650 REM ** 'VREEMD GELD'KOPEN **
1660 GOSUB 100:VE=1:GOSUB 110
1670 SR$=" Ned.geld ->'vreemd geld' ":GOSUB 150
1680 GOSUB 5070
1690 GOSUB 5180
1700 GOSUB 100:VE=3:GOSUB 110
1710 PRINT"U wenst ";NA$
1720 CT=10:CN=5:SR=LA:GOSUB 310
1730 PRINT"de laat-koers is: ";SR$
1740 VE=6:GOSUB 110
1750 PRINT"Welk bedrag aan ned.geld wilt u"
1760 PRINT"omzetten in ";NA$
1770 INPUT B
1780 VE=9:GOSUB 110
1790 PRINT"wat berekent de bank aan kosten:"
1800 INPUT D
1810 REM ** BEREKENING TEGENWAARDE **
1820 TW=INT(100*(B-D)/LA+.5)/100
1830 CT=10:CN=2:SR=TW:GOSUB 310
1840 VE=18:GOSUB 110
1850 PRINT"voor     ";B;" ";"gulden krijgt U"
1860 PRINT"     ";SR$;" ";NA$
1870 GOTO 5000
1880 REM ** VREEMD GELD VERKOPEN **
1890 GOSUB 100:VE=1:GOSUB 110
1900 SR$=" VERKOOP 'VREEMD GELD' ":GOSUB 150
1910 GOSUB 5070
1920 GOSUB 5180
1930 GOSUB 100:VE=3:GOSUB 110
1940 PRINT"hoeveel ";NA$;" verkopen:"
1950 INPUT B
1960 VE=6:GOSUB 110
1970 SR=BI:CT=10:CN=5:GOSUB 310
1980 PRINT"De biedkoers (per eenheid): ";SR$
1990 VE=9:GOSUB 110
2000 PRINT"bank-kosten:"
2010 INPUT D
2020 REM ** BEREKENING TEGENWAARDE **
2030 TW=INT(100*B*BI+.5)/100-D
2040 CT=10:CN=2:SR=TW:GOSUB 310
2050 VE=15:GOSUB 110
2060 PRINT"voor Uw    ";B;" ";NA$
2070 PRINT"krijgt U ";SR$;" ";"gulden."
2080 GOTO 5000
2090 REM ** ONDERLINGE KOERS BEPALEN **
2100 GOSUB 100:VE=1:GOSUB 110
2110 SR$=" ONDERLINGE KOERS ":GOSUB 150
2120 VE=4:GOSUB 110
2130 PRINT"geef NAAM eerste valuta:"
2140 INPUT F$
2150 VE=6:GOSUB 110
2160 PRINT"geef BEDRAG eerste valuta:"
2170 INPUT F
2180 VE=10:GOSUB 110
2190 PRINT"geef NAAM tweede valuta:"
2200 INPUT G$
2210 VE=12:GOSUB 110
2220 PRINT"geef BEDRAG tweede valuta:"
2230 INPUT G
2240 REM ** KOERS-BEREKENING **
2250 KW=INT(100*G/F+.5)/100
2260 CT=10:CN=2:SR=KW:GOSUB 310
2270 VE=18:GOSUB 110
2280 PRINT"**  1 ";F$;"=";" ";SR$;" ";G$
2290 LW=INT(100*F/G+.5)/100
2300 CT=10:CN=2:SR=LW:GOSUB 310
2310 VE=20:GOSUB 110
2320 PRINT"**  1 ";G$;"=";" ";SR$;" ";F$
2330 GOTO 5000
2340 REM ** KOERS-LIJST AFDRUKKEN **
2350 GOSUB 100:VE=1:GOSUB 110
2360 SR$=" KOERS-LIJST ":GOSUB 150
2370 PRINT:PRINT"valuta";TAB(16);"bieden";TAB(30);
2380 PRINT"laten":PRINT
2390 FOR I=1 TO 18
2400 PRINT NA$(I);TAB(10);
2410 SR=BI(I):CT=13:CN=5:GOSUB 310:PRINT SR$;TAB(24);
2420 SR=LA(I):GOSUB 310:PRINT SR$
2430 NEXT I
2440 GOTO 5000
5000 REM ** HERHAAL-VRAAG **
5010 VE=VT:GOSUB 110
5020 PRINT"Nog een berekening? (j/n)";
5030 GOSUB 210
5040 IF IN=74 THEN 1070
5050 IF IN<>78 THEN 5010
5060 GOTO 950
5070 REM ** VALUTA KEUZE **
5080 VE=3:GOSUB 110
5090 PRINT"-- valuta soorten --"
5100 FOR I=1 TO 18
5110 PRINT I;". ";NA$(I)
5120 NEXT I
5130 VE=VT-1:GOSUB 110
5140 PRINT"** welk NUMMER kiest U **";
5150 INPUT N
5160 NA$=NA$(N):BI=BI(N):LA=LA(N)
5170 RETURN
5180 REM ** DRUK-TOETS SUBROUTINE **
5190 VE=VT:GOSUB 110
5200 PRINT"--Druk toets --";
5210 GOSUB 210:RETURN
25000 DATA "u.s.dollar",2.03,2.15
25010 DATA "brits pond",3.16,3.46
25020 DATA "belg.frank",0.0528,0.0558
25030 DATA "d.mark    ",1.1075,1.1475
25040 DATA "it.lire   ",0.00149,0.00169
25050 DATA "port.esc. ",0.0125,0.0175
25060 DATA "can.dollar",1.52,1.64
25070 DATA "fr.franc  ",0.325,0.355
25080 DATA "zw.franc  ",1.325,1.37
25090 DATA "zw.kroon  ",0.3075,0.3375
25100 DATA "noorse kr.",0.285,0.315
25110 DATA "deense kr.",0.2825,0.3125
25120 DATA "oost.schil",0.1583,0.1633
25130 DATA "sp.pesos  ",0.0151,0.0176
25140 DATA "griekse dr",0.0125,0.0185
25150 DATA "finse mark",0.445,0.475
25160 DATA "joeg.dinar",0.002,0.006
25170 DATA "Iers pond ",2.86,3.16
25180 REM ** KOERS-13/03/087 **
25190 REM ** GEBRUIK OP UW EIGEN **
25200 REM ** VERANTWOORDELIJKHEID**
25210 REM 
32000 REM **********************
32010 REM *  HARRY EYCK  /NEER *
32020 REM *  COMMODORE 64      *
32030 REM *  16-03-1987        *
32040 REM **********************

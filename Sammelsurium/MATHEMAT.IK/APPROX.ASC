1000 a=256:GOTO 20:REM approx
1010 qq=0:r1=0:GOTO 2510
1020 REM ===gausscher algorithmus===
1030 FOR i=0 TO r:sv(i)=i:NEXT i
1040 FOR i=0 TO r-1:m=0
1050 FOR j=i TO r:FOR k=i TO r
1060 w=ABS(a(j,k)):IF w>m THEN m=w:s=k:z=j
1070 NEXT k:NEXT j
1080 v=sv(i):sv(i)=sv(s):sv(s)=v
1090 FOR j=0 TO r:v=a(j,i):a(j,i)=a(j,s):a(j,s)=v:NEXT j
1100 IF m=0 THEN PRINT"KEINE LOESUNG":RETURN
1110 FOR j=0 TO r+1
1120 v=a(i,j):a(i,j)=a(z,j):a(z,j)=v
1130 NEXT j
1140 FOR j=i+1 TO r:u=a(j,i)/a(i,i)
1150 FOR k=i TO r+1:a(j,k)=a(j,k)-a(i,k)*u:NEXT k
1160 NEXT j:NEXT i
1170 FOR i=r TO 0 STEP -1:u=a(i,r+1)
1180 IF i<r THEN FOR j=r TO i+1 STEP -1:u=u-a(i,j)*a(j,r+1):NEXT j
1190 a(i,r+1)=u/a(i,i):NEXT i
1200 FOR i=0 TO r:FOR j=i TO r
1210 IF sv(j)<>i THEN 1230
1220 v=a(i,r+1):a(i,r+1)=a(j,r+1):a(j,r+1)=v:sv(j)=sv(i):j=r
1230 NEXT j:NEXT i:RETURN
1500 REM ===gleichungssystem fuer approx
1510 FOR i=0 TO r:FOR j=i TO r:s=0
1520 FOR k=0 TO n:s=s+u(k)^(i+j):NEXT k
1530 a(i,j)=s:a(j,i)=s:NEXT j:s=0
1540 FOR k=0 TO n:s=s+v(k)*u(k)^i:NEXT k
1550 a(i,r+1)=s:NEXT i:RETURN
1600 REM ===gleichungssystem fuer inpol
1610 r=n:r2=n:FOR i=0 TO n:FOR j=0 TO n
1620 a(i,j)=u(i)^j:NEXT j:a(i,r+1)=v(i)
1630 NEXT i:RETURN
1700 REM === normierung ===
1710 qq=0:FOR i=0 TO n:s=ABS(u(i))
1720 IF s>qq THEN qq=s
1730 NEXT i:FOR i=0 TO n:u(i)=u(i)/qq:NEXT i
1740 RETURN
1800 REM === entnormierung ===
1810 FOR i=1 TO r:a(i,r+1)=a(i,r+1)/qq^i:NEXT i:RETURN
1820 FOR i=0 TO n:u(i)=u(i)*qq:NEXT i:RETURN
2000 REM === approximaton ===
2010 PRINT:PRINT"Wurde in Zeile 2130 Funktion eingegeben?"
2020 GOSUB 210:IF in=74 THEN 2040:REM j
2030 sr$="Funktion eingeben u.neu starten":GOSUB 150:GOTO 950
2040 GOSUB 100:INPUT"ANZAHL DER PUNKTE:";n:n=n-1
2050 IF n>100 THEN PRINT"PUNKTZAHL ZU GROSS!":GOTO 2040
2060 PRINT"EINGABE DEFINITIONSBEREICH: "
2070 INPUT"MINIMUM X:";xu:b1$="N":b3$="J"
2080 INPUT"MAXIMUM X:";xo
2090 xd=(xo-xu)/(n-1)
2100 INPUT"GRAD DER FUNKTION:";r1
2110 IF r1>9 THEN :PRINT"GRAD ZU GROSS !":GOTO 2100
2120 r=r1:x=xu:FOR i=0 TO n
2130 y=x:REM **> hier anwenderfunktion eingeben <**
2140 u(i)=x:v(i)=y:x=x+xd:NEXT i:GOTO 3000
2500 REM ** hauptprogramm start **
2510 DIM a(10,11),sv(10),u(100),v(100),b(10),w(10,1)
2520 GOSUB 100:PRINT"======================================="
2530 PRINT"    *   ****  ****  ****   ***   *   *"
2540 PRINT"   * *  *   * *   * *   * *   *   * *"
2550 PRINT"  *   * *   * *   * ****  *   *    *"
2560 PRINT"  ***** ****  ****  * *   *   *   * *"
2570 PRINT"  *   * *     *     *  *   ***   *   *":PRINT 
2580 PRINT"      ";:sr$="(C) bidat'78/86/90":GOSUB 150:PRINT 
2590 PRINT:PRINT" INTERPOLATION/REGRESSION/APPROXIMATION"
2600 PRINT"======================================="
2610 PRINT"Fuer eine Menge von Punkten wird ein"
2620 PRINT"Polynom errechnet, dass bei Interpo-"
2630 PRINT"lation durch diese Punkte verlaeuft,"
2640 PRINT"bei Regression  mit  minimalem qua-"
2650 PRINT"dratischen Fehler vorbeifuehrt.  Bei"
2660 PRINT"der Approximation  wird eine vorher"
2670 PRINT"eingegebene Funktion in einem Inter-"
2680 PRINT"vall durch ein Polynom ersetzt,wobei"
2690 PRINT"wiederum der quadratische Fehler mi-"
2700 PRINT"nimiert wird. Das zu berechnende Po-"
2710 PRINT"lynom kann maximal 9.Grades sein,d.h."
2720 PRINT"bei Interpolation maximal 10 Punkte."
2730 sr$="AUSWAHL DER BETRIEBSART:":GOSUB 150:PRINT 
2740 INPUT"INTERPOLATION (J/N)";sr$:GOSUB 330:b1$=sr$
2750 INPUT"REGRESSION    (J/N)";sr$:GOSUB 330:b2$=sr$
2760 INPUT"APPROXIMATION (J/N)";sr$:GOSUB 330:b3$=sr$
2770 IF (b3$="J") AND ((b1$<>"N") OR (b2$<>"N")) THEN 2740
2780 IF b3$="J" THEN 2010
2790 PRINT:INPUT"ANZAHL DER PUNKTE:";n
2800 n=n-1:nm=100:IF b1$="J" THEN nm=10
2810 IF n>nm THEN PRINT"PUNKTZAHL ZU GROSS !":GOTO 2790
2820 IF (b2$="J") OR (b3$="J") THEN INPUT"GRAD DER FUNKTION:";r1
2830 IF r1>9 THEN PRINT"GRAD ZU GROSS !":GOTO 2820
2840 i=0:IF b1$="J" THEN r0=n
2850 GOSUB 100:sr$="APPROX-Eingabe der Punktkoordinaten"
2860 GOSUB 150:ho=0:ve=1:GOSUB 110
2870 PRINT"= I ==== X ========= Y ======"
2880 j=i+1:ve=2+i-20*int(i/20):ho=2:GOSUB 110
2890 PRINT j;:ho=6:GOSUB 110:INPUT"";u(i)
2900 ho=18:GOSUB 110:INPUT"";v(i):IF i>=n THEN 2930
2910 i=i+1:IF i=20*int(i/20) THEN 2840
2920 GOTO 2880
2930 PRINT:PRINT"KORREKTUREN (J/N)?";:GOSUB 210:PRINT 
2940 IF in<>78 THEN 3000:REM n
2950 GOSUB 120:INPUT" ";i:i=i-1
2960 ho=6:GOSUB 110:INPUT"";u(i)
2970 ho=18:GOSUB 110:INPUT"";v(i)
2980 ho=30:GOSUB 110:PRINT"Forts?";:GOSUB 210
2990 PRINT:IF in=74 THEN 2950:REM j
3000 PRINT:sr$="Bitte warten - Ich rechne":GOSUB 150:PRINT 
3010 REM === min/max ermittlung ===
3020 xu=1e38:xo=-1e38:FOR i=0 TO n
3030 IF u(i)<xu THEN xu=u(i)
3040 NEXT i:FOR i=0 TO n:IF u(i)>xo THEN xo=u(i)
3050 NEXT i
3060 yu=1e38:yo=-1e38:FOR i=0 TO n
3070 IF v(i)<yu THEN yu=v(i)
3080 NEXT i:FOR i=0 TO n:IF v(i)>yo THEN yo=v(i)
3090 NEXT i
3100 x1=ABS(1.4*xu):x2=ABS(1.4*xo)
3110 y1=ABS(1.4*yu):y2=ABS(1.4*yo)
3120 x3=x1:IF x1<x2 THEN x3=x2
3130 y3=y1:IF y1<y2 THEN y3=y2
3140 IF (xu<0) AND (xo>0) THEN x1=-x3:x2=x3:GOTO 3170
3150 IF (xu<0) AND (xo<0) THEN x1=-x3:x2=0:GOTO 3170
3160 IF (xu>=0) AND (xo>0) THEN x1=0:x2=x3
3170 IF (yu<0) AND (yo<0) THEN y1=-y3:y2=0:GOTO 5020
3180 IF (yu>=0) AND (yo>0) THEN y1=0:y2=y3:GOTO 3210
3190 y1=-y3:y2=y3
3200 REM === gleichungssystem bearbeiten
3210 IF b1$<>"J" THEN 3250
3220 r=r0:GOSUB 1710:GOSUB 1610:GOSUB 1030:GOSUB 1810
3230 FOR i=0 TO r:w(i,0)=a(i,r+1):NEXT i
3240 sr$="Ergebnis der Interpolation":GOSUB 3440
3250 IF (b2$="N") AND (b3$="N") THEN 3320
3260 r=r1:IF b1$<>"J" THEN  GOSUB 1710
3270 GOSUB 1510:GOSUB 1030:GOSUB 1810
3280 FOR i=0 TO r:w(i,1)=a(i,r+1):NEXT i
3290 IF b2$="J" THEN sr$="Ergebnis der Regression"
3300 IF b3$="J" THEN sr$="Ergebnis der Approximation"
3310 GOSUB 3440
3320 sr$="Grafik (J/N) ?":GOSUB 150:GOSUB 210
3330 IF in<>78 THEN  GOSUB 5010
3340 PRINT:PRINT 
3350 sr$="Funktionstabelle (J/N) ?":GOSUB 150:GOSUB 210
3360 IF in<>78 THEN  GOSUB 6510
3370 PRINT:PRINT 
3380 sr$="Berechnung von Funktionswerten(J/N) ?":GOSUB 150
3390 GOSUB 210:IF in<>78 THEN  GOSUB 7000
3400 PRINT:PRINT 
3410 sr$="Fortsetzung (J/N)":GOSUB 150:GOSUB 210
3420 IF in<>78 THEN 2520:REM j
3430 GOTO 950
3440 GOSUB 150:PRINT:PRINT 
3450 PRINT"Funktion Y=A0+A1*X+A2*x^2+...+An*x^n":PRINT 
3460 FOR i=0 TO r:PRINT"A";CHR$(48+i);" =";a(i,r+1):NEXT i
3470 PRINT:RETURN
5000 REM === grafik der anfangspunkte
5010 GOSUB 600:cn=0:ho=0:ve=0:GOSUB 620:ho=.999:GOSUB 630
5020 ve=.999:GOSUB 630:ho=0:GOSUB 630:ve=0:GOSUB 630
5030 REM === koordinatensystem zeichnen
5040 IF y1=0 THEN y4=.999
5050 IF y2=0 THEN y4=0
5060 IF y1*y2<0 THEN y4=.5
5070 ho=0:ve=y4:GOSUB 620:ho=.999:GOSUB 630
5080 FOR i=0 TO 1 STEP .05:ho=i:ve=ABS(y4-.005):GOSUB 620:NEXT i
5090 IF x1=0 THEN x4=0
5100 IF x2=0 THEN x4=.999
5110 IF x1*x2<0 THEN x4=.5
5120 ve=0:ho=x4:GOSUB 620:ve=.999:GOSUB 630
5130 FOR i=0 TO 1 STEP .05:ho=ABS(x4-.005):ve=i:GOSUB 620:NEXT i
5500 REM === punkte einzeichnen
5510 GOSUB 1820:x3=x2-x1:y3=y2-y1
5520 FOR i=0 TO n:x=u(i):y=v(i)
5530 GOSUB 7510:GOSUB 8010:NEXT i
5540 IF b1$<>"J" THEN 6040
6000 REM === kurven zeichnen ===
6010 j=0:r=r0:h0=0:dx=(x2-x1)/100
6020 FOR q=x1 TO x2 STEP dx:x=q:GOSUB 8110
6030 GOSUB 7510:GOSUB 8070:NEXT q
6040 IF (b2$="N") AND (b3$="N") THEN 6080
6050 j=1:r=r1:dx=(x2-x1)/320
6060 FOR q=x1 TO x2 STEP dx:x=q:h0=0:GOSUB 8110
6070 GOSUB 7510:GOSUB 8070:NEXT q
6080 GOSUB 210:GOTO 100
6500 REM === funktionstabelle ===
6510 PRINT:INPUT"ANFANGSWERT :";xu
6520 INPUT"ENDWERT     :";xo
6530 INPUT"SCHRITTWEITE:";dx
6540 GOSUB 100
6550 PRINT"= X ======== Y INTERPOL == Y APPROX =="
6560 i=0:FOR x=xu TO xo STEP dx
6570 y=0:IF b1$="J" THEN j=0:GOSUB 8110:q=y
6580 y=0:IF (b2$="J") OR (b3$="J") THEN j=1:GOSUB 8110
6590 PRINT x;:IF b1$="J" THEN PRINT TAB(12);q;
6600 IF (b2$="J") OR (b3$="J") THEN PRINT TAB(26);y;
6610 PRINT:i=i+1:IF i>=20 THEN i=0:GOSUB 210
6620 NEXT x:RETURN
7000 GOSUB 100:PRINT"= X == Y INTERPOL == Y APPROX == weiter"
7010 GOSUB 120:INPUT"";x:j=0
7020 IF b1$="J" THEN ho=6:GOSUB 110:GOSUB 8110:PRINT y;
7030 IF (b2$="N") AND (b3$="N") THEN 7050
7040 ho=20:GOSUB 110:j=1:GOSUB 8110:PRINT y;
7050 ho=34:GOSUB 110:PRINT"J/N?";:GOSUB 210
7060 IF in=78 THEN RETURN
7070 PRINT:GOTO 7010
7080 REM end
7500 REM === up punktkoordinaten ===
7510 x=(x-x1)/x3
7520 y=1-(y-y1)/y3:RETURN
8000 REM === up punkt zeichnen ===
8010 IF (x<0) OR (x>=1) OR (y<0) OR (y>=1) THEN RETURN
8020 h1=ABS(x-.005):h2=ABS(y-.005)
8030 h3=1-ABS(.995-x):h4=1-ABS(.995-y)
8040 ho=h1:ve=h2:GOSUB 620:ho=h3:ve=h4:GOSUB 630
8050 ho=h1:GOSUB 620:ho=h3:ve=h2:GOTO 630
8060 REM === up verbindung zu punkt ===
8070 IF (x<0) OR (x>=1) OR (y<0) OR (y>=1) THEN h0=0:RETURN
8080 ho=x:ve=y:IF h0=0 THEN h0=1:GOTO 620
8090 GOTO 630
8100 REM === berechnung eines punktes
8110 y=0:FOR k=r TO 0 STEP -1:y=y*x+w(k,j)
8120 NEXT k:RETURN
8130 y=0:FOR k=r2 TO 0 STEP -1:y=y*p+b(k)
8140 NEXT k:RETURN
32000 REM        (c) bidat
32010 REM autor: berthold biener
32020 REM        r.-eyermann-ring 4/0106
32030 REM        5063 erfurt
32040 REM entwickelt auf commodore+4
32050 REM
32060 REM erstausstrahlung: ds-kultur
32070 REM -spezial 900822

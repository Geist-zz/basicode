1000 a=1000:GOTO 20:REM wahl90
1010 DIM p$(20),p(20,1),d(20),e(20)
1020 sg=400:REM sitze
1030 sk=0:REM sperrklausel(%)
1040 GOTO 1840
1050 REM up sortierung
1060 FOR i=1 TO n-1:m=p(i,0):k=i:FOR j=i+1 TO n
1070 IF p(j,0)>m THEN m=p(j,0):k=j
1080 NEXT j:IF k=i THEN 1110
1090 t$=p$(i):p$(i)=p$(k):p$(k)=t$
1100 m=p(i,0):p(i,0)=p(k,0):p(k,0)=m
1110 NEXT i:RETURN
1120 REM anzahl darzustellender parteien
1130 q=1:s=100*v
1140 IF (p(q,u)>=1) AND (q<n) THEN s=s-v*p(q,u):q=q+1:GOTO 1140
1150 GOSUB 600
1160 sr$="* *  WAHL'90 Stimmenverteilung * *"
1170 IF u=1 THEN sr$="* * WAHL'90 Sitzverteilung * *"
1180 ho=0.:ve=.05:GOSUB 650
1190 REM berechnung der sitze
1200 q=1:s=0:t=0:sr$="Bitte warten - Sitzberechnung"
1210 ho=5:ve=23:GOSUB 110:GOSUB 150
1220 IF (p(q,0)>=sk) AND (q<n) THEN s=s+p(q,0):q=q+1:GOTO 1220
1230 FOR i=1 TO q-1:p(i,1)=INT(v*100*p(i,0)/s)/4:NEXT i
1240 m=0:FOR i=1 TO q-1:m=m+v*p(i,1):NEXT i
1250 IF m>=sg THEN RETURN
1260 FOR i=1 TO q-1:d(i)=p(i,0)-p(i,1):e(i)=0:NEXT i
1270 FOR i=1 TO q-1:t=-20:s=0:FOR j=1 TO q-1
1280 IF (d(j)>t) AND (e(j)=0) THEN t=d(j):s=j
1290 NEXT j:e(s)=i:NEXT i:m=sg-m:IF m>=q THEN m=q-1
1300 FOR i=1 TO q-1:IF e(i)<=m THEN p(i,1)=p(i,1)+1/v
1310 NEXT i:GOTO 1240
1320 REM balkendiagramm
1330 GOSUB 1130:ho=0:ve=.1:GOSUB 620
1340 ho=.99:GOSUB 630:ve=.8:GOSUB 630
1350 ho=0:GOSUB 630:ve=.1:GOSUB 630
1360 w=.99/q:h=.6/p(1,u)
1370 FOR i=1 TO q-u:IF i<q THEN t=p(i,u):t$=p$(i)
1380 IF i=q THEN t=s:t$="Rest"
1390 GOSUB 1400:NEXT i:GOSUB 210
1400 ho=(i-.9)*w:ve=.8:GOSUB 620
1410 ve=.8-h*t:GOSUB 630
1420 ho=(i-.3)*w:GOSUB 630:ve=.8:GOSUB 630
1430 ho=(i-.9)*w:ve=.8-h*t:GOSUB 620
1440 ho=(i-.7)*w:ve=.77-h*t:GOSUB 630
1450 ho=(i-.1)*w:GOSUB 630:ve=.77:GOSUB 630
1460 ho=(i-.3)*w:ve=.8:GOSUB 630
1470 ho=(i-.3)*w:ve=.8-h*t:GOSUB 620
1480 ho=(i-.1)*w:ve=.77-h*t:GOSUB 630
1490 IF q<9 THEN 1540
1500 k=LEN(t$):IF k>4 THEN k=4
1510 FOR j=1 TO k:sr$=MID$(t$,j,1)
1520 ho=(i-.9)*w:ve=.78+j*.042
1530 GOSUB 650:NEXT j:GOTO 1550
1540 ho=(i-.9)*w:ve=.82:sr$=t$:GOSUB 650
1550 sr=v*t:ct=4:cn=1-u:GOSUB 310:cn=0
1560 ho=(i-.9)*w:ve=.72-h*t:GOSUB 650
1570 REM tortendiagramm
1580 GOSUB 1130:ho=.1:ve=.4:GOSUB 620:k=0
1590 FOR i=0 TO 6.3 STEP .05:ve=.4-.25*SIN(i)
1600 ho=.5-.4*COS(i):GOSUB 630:NEXT i
1610 ho=.1:ve=.5:GOSUB 630:FOR i=0 TO 3.15 STEP .05
1620 ho=.5-.4*COS(i):ve=.5+.25*SIN(i)
1630 GOSUB 630:NEXT i:ho=.9:ve=.4:GOSUB 630
1640 ho=.5:ve=.4:GOSUB 620:ho=.1:GOSUB 630
1650 w=0:FOR i=1 TO q-1:t=p(i,u)*.0628:w=w+t
1660 ho=.5:ve=.4:GOSUB 620:ho=.5-.4*COS(w)
1670 ve=.4-.25*SIN(w):GOSUB 630
1680 IF w>3.14 THEN ve=ve+.1:GOSUB 630
1690 sr=v*p(i,u):ct=4:cn=1-u:GOSUB 310:t$=sr$:s$=p$(i)
1700 cn=0:IF t<.6 THEN 1760
1710 j=w-.7*t:IF (w-t>3.5) THEN j=w-.1*t
1720 ho=.5-.25*COS(j):ve=.4-.15*SIN(j)
1730 IF w-t>3.5 THEN ve=ve-.042
1740 sr$=s$:GOSUB 650:ve=ve+.042:sr$=t$:GOSUB 650
1750 GOTO 1820
1760 IF w>3.14 THEN ho=.505-.4*COS(w):ve=.43-.25*SIN(w)
1770 IF w<=3.14 THEN j=w-.7*t:ho=.5-.3*COS(j):ve=.4-.2*SIN(j)
1780 sr$=CHR$(96+i):GOSUB 650
1790 ho=.5*INT(k/6):ve=.76+.042*(k-6*INT(k/6))
1800 sr$=CHR$(96+i)+"="+LEFT$(s$+"  ",4)+":"+t$
1810 GOSUB 650:k=k+1
1820 NEXT i:GOSUB 210:RETURN
1830 REM hauptprogramm
1840 GOSUB 100
1850 PRINT"======================================="
1860 PRINT:PRINT"    *   *  **  *  * *   *  **   **"
1870 PRINT"    *   * *  * *  * *   * *  * *  *"
1880 PRINT"    *   * *  * *  * *     *  * *  *"
1890 PRINT"    *   * **** **** *      *** *  *"
1900 PRINT"======================================="
1910 PRINT"     * *  *  * *  * ****   **   **"
1920 PRINT:PRINT"           ";:sr$="(C) bidat'90"
1930 GOSUB 150:PRINT:PRINT
1950 PRINT"Dieses Programm soll die Stimmen- und"
1960 PRINT"Sitzverteilung bei der Volkskammerwahl"
1970 PRINT"am 18.3.90 grafisch darstellen. Dazu"
1980 PRINT"werden dreidimensionale Balken- und"
1990 PRINT"Tortendiagramme verwendet. Da wir keine"
2000 PRINT"Direktverbindung zum Wahlstudio haben,"
2010 PRINT"muessen die Daten von Hand eingegeben"
2020 PRINT"werden. Auch die Parteien muessen zu"
2030 PRINT"Beginn der Arbeit eingegeben werden,"
2040 PRINT"da die gespeicherte Liste veraltet ist."
2050 PRINT"            ";:sr$="<Enter>":GOSUB 150:GOSUB 210
2060 GOSUB 100:sr$="* * * Wahl'90 Hauptmenue * * *"
2070 GOSUB 150:PRINT:PRINT
2080 PRINT"<1> Eingabe der Parteien"
2090 PRINT:PRINT"<2> Eingabe der Stimmen (%)"
2100 PRINT:PRINT"<3> Stimmenverteilung Balkendiagramm"
2110 PRINT:PRINT"<4> Stimmenverteilung Tortendiagramm"
2120 PRINT:PRINT"<5> Sitzverteilung Balkendiagramm"
2130 PRINT:PRINT"<6> Sitzverteilung Tortendiagramm"
2140 PRINT:PRINT"<7> Numerischer Ergebnisausdruck":PRINT
2150 PRINT"<0> Ende":PRINT:sr$="Ihre Auswahl:":GOSUB 150
2160 GOSUB 210:IF (in<48) OR (in>55) THEN 2160
2170 in=in-48:IF in=0 THEN  GOSUB 100:GOTO 950
2180 ONin GOSUB 2200,2500,2670,2680,2690,2700,2710
2190 GOTO 2060
2200 GOSUB 100:sr$="* * * WAHL'90 Parteien * * *":GOSUB 150
2210 PRINT:PRINT:PRINT"Folgende Parteien sind gespeichert:"
2220 PRINT:p$(0)="Sonstige":restore:n=1
2230 READ p$(n):IF p$(n)="$" THEN 2260
2240 READ t$:PRINT p$(n);TAB(5);t$
2250 n=n+1:GOTO 2230
2260 PRINT:sr$="<N>eueingabe <K>orrektur <M>enue":GOSUB 150
2270 GOSUB 210:IF (in=77) OR (in=109) THEN RETURN
2280 IF (in=75) OR (in=107) THEN 2380
2290 IF (in<>78) AND (in<>110) THEN 2270
2300 GOSUB 100:sr$="* * * WAHL'90 Parteien * * *":GOSUB 150
2310 PRINT:PRINT:PRINT"Geben Sie bitte die Kurzbezeichnung"
2320 PRINT"von maximal 19 Parteien ein. Wenn Sie"
2330 PRINT"sich vertippen, besteht noch eine Kor-"
2340 PRINT"rekturmoeglichkeit. Die Reihenfolge"
2350 PRINT"ist unwichtig. Ende ist bei $":PRINT:n=1
2360 PRINT"Nr.:";n;:INPUT" Partei:";p$(n)
2370 IF (p$(n)<>"$") AND (n<24) THEN n=n+1:GOTO 2360
2380 GOSUB 100:sr$="* * * WAHL'90 Parteien * * *":GOSUB 150
2390 GOSUB 2450
2400 ho=0:ve=20:GOSUB 110
2410 INPUT"Auswahl Partei-Nr. (0=Ende):";j:i=j-1
2420 IF i<0 THEN RETURN
2430 INPUT"Parteiname(Kurzbezeichnung):";p$(j)
2440 GOTO 2380
2450 i=0:ct=2:cn=0
2460 ho=20*INT(i/10):ve=3+i-10*INT(i/10)
2470 sr=i+1:GOSUB 310:sr$=sr$+" = "+p$(i+1):GOSUB 110:PRINT sr$
2480 IF (p$(i+1)="$") OR (p$(i+1)="") THEN RETURN
2490 i=i+1:GOTO 2460
2500 GOSUB 100:sr$="* * WAHL'90 Eingabe der Stimmen * *"
2510 GOSUB 150:GOSUB 2450:n=i+1
2520 ho=0:ve=20:GOSUB 110
2530 INPUT"Parteinr.od.Name(0=sonst,X=Ende):";t$
2540 j=ASC(t$):IF (j=88) OR (j=120) THEN 2650
2550 INPUT"Stimmen in Prozent : ";s
2560 IF j<58 THEN j=VAL(t$):p(j,0)=s:GOTO 2610
2570 i=1:k=LEN(t$):j=0:sr$=t$:GOSUB 330
2580 IF sr$=LEFT$(p$(i),k) THEN j=i:p(j,0)=s:GOTO 2610
2590 IF (j=0) AND (i<n) THEN i=i+1:GOTO 2580
2600 GOSUB 250:p(0,0)=p(0,0)+s
2610 i=j-1:ho=10+20*INT(i/10):ve=3+i-10*INT(i/10):GOSUB 110
2620 IF j>0 THEN PRINT":";p(j,0);"%":GOTO 2520
2630 ho=20:ve=14:GOSUB 110
2640 PRINT"Sonstige:";p(j,0);"%":GOTO 2520
2650 ho=5:ve=23:GOSUB 110:sr$="Bitte warten - Sortierung"
2660 GOSUB 150:GOTO 1060
2670 v=1:u=0:GOTO 1330
2680 v=1:u=0:GOTO 1580
2690 v=sg/100:u=1:GOSUB 1200:GOTO 1330
2700 v=sg/100:u=1:GOSUB 1200:GOTO 1580
2710 v=sg/100:u=1:GOSUB 1200
2720 ve=21:ho=5:sr$="      Ausdruck       "
2730 GOSUB 150
2740 sr$="WAHL'90 Ausdruck Stimmen und Mandate"
2750 GOSUB 350:GOSUB 360:GOSUB 360
2760 sr$="Partei      Stimmen(%)  Mandate"
2770 GOSUB 350:GOSUB 360:sr$="============"
2780 FOR i=1 TO 3:GOSUB 350:NEXT i:GOSUB 360:i=1
2790 sr=p(i,0):ct=4:cn=1:GOSUB 310:t$=sr$
2800 sr=p(i,1)*sg/100:ct=3:cn=0:GOSUB 310:s$=sr$
2810 sr$=LEFT$(p$(i)+"          ",12)
2820 sr$=sr$+t$+"        "+s$:GOSUB 350:GOSUB 360
2830 i=i+1:IF (i<=n) AND (p(i,0)>0) THEN 2790
2840 IF p(0,0)=0 THEN 2890
2850 sr=p(0,0):ct=4:cn=1:GOSUB 310:t$=sr$
2860 sr=p(0,1)*sg/100:ct=3:cn=0:GOSUB 310:s$=sr$
2870 sr$="Sonstige    "+t$+"        "+s$:GOSUB 350:GOSUB 360
2880 GOSUB 350:NEXT i:GOSUB 360
2890 sr$="============":FOR i=1 TO 3
2900 GOSUB 350:NEXT i:GOSUB 360
2910 sr$="Sitzverteilung ohne Gewaehr"
2920 GOSUB 350:GOSUB 360
25000 DATA"CDU","Christlich Demokratische Union"
25010 DATA"DA","Demokratischer Aufbruch"
25020 DATA"DBD","Demokratische Bauernpartei Dtl."
25030 DATA"DFP","Deutsche Forumpartei"
25040 DATA"DJ","Demokratie jetzt"
25050 DATA"DSU","Deutsche Soziale Union"
25060 DATA"FDP","Freie Demokratische Partei"
25070 DATA"GP","Gruene Partei"
25080 DATA"IFM","Initiative f.Frieden u.Menschenr."
25090 DATA"LDP","Liberaldemokratische Partei"
25100 DATA"NDPD","Nationaldemokratische Partei Dtl"
25110 DATA"NF","Neues Forum"
25120 DATA"PDS","Partei d.demokratischen Soz.(SED)"
25130 DATA"SPD","Sozialdemokratische Partei Dtl."
25140 DATA"VL","Vereinigte Linke","$"
30000 REM die sitzverteilung ist als
30010 REM richtwert anzusehen,da die
30020 REM mandate auf basis der ergeb-
30030 REM nisse der wahlkreise vergeben
30040 REM werden, hier aber nur eine
30050 REM globale berechnung auf basis
30060 REM der stimmprozente erfolgt.
30070 REM ------------------------------
32000 REM (c) bidat 1990
32010 REM autor: berthold biener
32020 REM        r.-eyermann-ring 4/06
32030 REM        ddr 5063 erfurt
32040 REM entwickelt auf commodore+4
32050 REM ------------------------------
32060 REM erstausstrahlung: radio ddr
32070 REM 900307

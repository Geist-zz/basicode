1000 A=1000:GOTO 20:REM MENU'S
1010 GOSUB 100
1020 PRINT :PRINT "VAN WELK MEDIUM LADEN ?"
1030 PRINT :PRINT " 0 = CASSETTE (BASICODEFORMAAT)"
1040 PRINT :PRINT " 2 = EIGEN OPSLAGSYSTEEM"
1050 PRINT :PRINT " 4 = DISKETTE"
1060 GOSUB 200:NF=IN-48
1070 IF (NF<>0) AND (NF<>2) AND (NF<>4) THEN 1060
1080 IF IN=0 THEN 1140
1090 PRINT :PRINT "WELK MENU LADEN ?"
1100 PRINT :PRINT "(VOORSTEL: CHINEES)":PRINT 
1110 INPUT SR$:IF SR$="" THEN 1110
1120 IF LEN(SR$)>7 THEN 1110
1130 GOSUB 330:NF$=SR$
1140 GOSUB 500
1150 GOSUB 540:MX=VAL(IN$):REM MAXIMUM AANTAL GANGEN
1160 DIM GA$(MX),ME$(MX)
1170 FOR I=1 TO MX
1180 GOSUB 540:GA$(I)=IN$:REM LEES GANGEN IN
1190 NEXT I
1200 GOSUB 540:GX=VAL(IN$):REM AANTAL GERECHTEN PER GANG
1210 DIM GE$(MX*GX)
1220 FOR I=1 TO MX*GX
1230 GOSUB 540:GE$(I)=IN$:REM LEES GERECHTEN IN
1240 NEXT I
1250 GOSUB 540:ZI$=IN$:REM NAAM INGREDIENTEN-FILE
1260 GOSUB 540:ZB$=IN$:REM NAAM BEREIDINGS-FILE
1270 GOSUB 580
1280 PR=0:REM PRINTER UIT
1500 REM HOOFDPROGRAMMA
1510 GOSUB 100
1520 TL=0:FOR I=1 TO MX
1530 IF ME$(I)<>"" THEN TL=TL+1
1540 NEXT I
1550 PRINT :PRINT :PRINT "    MENU":PRINT 
1560 PRINT "    1 SHOW DE SPIJSKAART":PRINT 
1570 PRINT "    2 COMPUTER STELT MENU SAMEN":PRINT 
1580 PRINT "    3 PRINTER ";
1590 IF PR=1 THEN PRINT "UIT";
1600 IF PR=0 THEN PRINT "AAN";
1610 PRINT "ZETTEN":PRINT 
1620 PRINT "    4 TOELICHTING":PRINT 
1630 IF TL=0 THEN 1660
1640 PRINT "    5 SHOW HET GEKOZEN MENU":PRINT 
1650 PRINT "    6 TOON INGREDIENTEN EN BEREIDING":PRINT 
1660 PRINT "    0 VANDAAG GEEN TREK ";
1670 IF TL>0 THEN PRINT "MEER"
1680 HO=10:VE=23:GOSUB 110:PRINT "MAAK UW KEUZE";
1690 MO=4:IF TL>0 THEN MO=6
1700 GOSUB 210
1710 CH=VAL(IN$)
1720 IF CH>MO THEN GOSUB 250:GOTO 1700
1730 GOSUB 100
1740 IF CH=0 THEN 9010
1750 ON CH GOSUB 3010,5010,2010,6010,4010,7010
1760 GOTO 1510
2000 REM    PRINTER AAN/UIT
2010 PR=1-PR:RETURN
3000 REM SPIJSKAART
3010 PRINT "+                                    +++"
3020 FOR VE=1 TO 20:HO=0:GOSUB 110
3030 PRINT "!                                      !"
3040 IF VE<6 THEN HO=38-VE*5:GOSUB 110:PRINT "+++++"
3050 IF VE>18 THEN HO=(VE-17)*9-5:GOSUB 110
3060 IF VE>18 THEN PRINT "+++++++++"
3070 IF VE>17 THEN HO=(21-VE)*4-3:GOSUB 110:PRINT "++++"
3080 IF VE<7 THEN HO=VE*2:GOSUB 110:PRINT "++"
3090 IF (VE>6) AND (VE<18) THEN HO=12:GOSUB 110:PRINT "!"
3100 NEXT VE
3110 PRINT "+                             ++++++++++"
3120 I=1:J=1:SR=0:CT=2:CN=0
3130 HO=2:VE=7:GOSUB 110:PRINT GA$(I)
3140 NL=INT(13/GX):RG=6+INT((13-GX*NL)/2/NL)
3150 HO=13:VE=RG+NL:GOSUB 110
3160 SR=SR+1:GOSUB 310
3170 PRINT SR$;" ";GE$(J)
3180 J=J+1:RG=RG+NL
3190 IF (J-1)/GX=INT((J-1)/GX) THEN 3210
3200 GOTO 3150
3210 HO=2:VE=13:GOSUB 110:PRINT "[K]IES"
3220 VE=15:GOSUB 110:PRINT "[V]ERDER"
3230 VE=17:GOSUB 110:PRINT "[M]ENU";
3240 GOSUB 200:IF IN=0 THEN 3240
3250 IF (IN<>75) AND (IN<>77) AND (IN<>86) THEN 3240
3260 GOSUB 3510
3270 IF IN=75 THEN GOSUB 3710:IN=86
3280 IF IN=77 THEN 1280
3290 IF IN<>86 THEN 3210
3300 GOSUB 3510:SR=0
3310 IF I<MX THEN I=I+1:GOTO 3130
3320 GOTO 3120
3500 REM  WIS SPIJSKAART
3510 FOR VE=7 TO 17 STEP 2
3520 HO=2:GOSUB 110
3530 IF VE<18 THEN PRINT "          "
3540 IF IN=75 THEN 3570
3550 HO=13:GOSUB 110
3560 PRINT "                          "
3570 NEXT VE
3580 RETURN
3700 REM    KIES GERECHT
3710 VE=11:GOSUB 110:PRINT "DRUK"
3720 VE=13:GOSUB 110:PRINT "NUMMER"
3730 VE=15:GOSUB 110:PRINT "VAN UW"
3740 VE=17:GOSUB 110:PRINT "KEUZE "
3750 GOSUB 200:K=VAL(IN$)
3760 IF (K<1) OR (K>5) THEN 3750
3770 ME$(I)=GE$((I-1)*GX+K)
3780 RETURN
4000 REM    SHOW GEKOZEN MENU
4010 SR$="GEKOZEN MENU:":GOSUB 6110
4020 VE=5:HO=3:GOSUB 110
4030 FOR SR=1 TO MX:GOSUB 300:SR$=SR$+" "+ME$(SR)
4040 VE=5+2*SR:GOSUB 110:GOSUB 6110
4050 NEXT SR
4060 GOTO 8010
5000 REM    MENUKEUZE COMPUTER
5010 PRINT "EEN MOMENTJE";
5020 FOR I=1 TO MX*GX STEP GX
5030 GOSUB 260:SR=I+INT(GX*RV)
5040 ME$(I/GX+1)=GE$(SR)
5050 PRINT ".";:NEXT I
5060 GOSUB 100
5070 GOTO 4010
6000 REM    TOELICHTING
6010 PRINT "MET DE RECEPTEN UIT DIT PROGRAMMA":PRINT 
6020 PRINT "ZIJN VELE MIN OF MEER VERSCHILLENDE":PRINT 
6030 PRINT "MENU'S SAMEN TE STELLEN.":PRINT :PRINT :PRINT 
6040 PRINT "DE RECEPTEN ZIJN BESTEMD VOOR 1 PERSOON.":PRINT 
6050 PRINT "GROTERE GEZELSCHAPPEN KUNNEN MEE-ETEN":PRINT 
6060 PRINT "DOOR DE HOEVEELHEDEN TE VERMENIGVULDI-":PRINT 
6070 PRINT "GEN":PRINT :PRINT :PRINT 
6080 PRINT "EET SMAKELIJK !"
6090 GOTO 8010
6100 REM  PRINT SUBROUTINES
6110 PRINT SR$:IF PR=1 THEN GOSUB 350:GOSUB 360
6120 RETURN
6130 PRINT :IF PR=1 THEN GOSUB 360
6140 RETURN
7000 REM    PRINT INGRED./BEREID.
7010 PRINT "Ingredienten of Bereiding ?"
7020 PRINT :PRINT "    (Kies I of B)"
7030 GOSUB 210:IF IN=0 THEN 7030
7040 IF (IN<>66) AND (IN<>73) THEN 7030
7050 NF$=ZB$:IF IN=73 THEN NF$=ZI$
7060 GOSUB 100
7070 GOSUB 500:REM OPEN FILE
7080 FOR I=1 TO MX
7090 IF ME$(I)="" THEN 7170
7100 GOSUB 540:IF IN$<>ME$(I) THEN 7100
7110 SR$=IN$:GOSUB 150:PRINT 
7120 IF PR=1 THEN GOSUB 350:GOSUB 360:GOSUB 360
7130 GOTO 7150
7140 SR$=IN$:GOSUB 6110
7150 GOSUB 540:IF (IN$<>"@@") AND (IN$<>"--") THEN 7140
7160 GOSUB 8010
7170 NEXT I:GOSUB 580:RETURN
8000 REM    VERVOLG NA ENTER
8010 VE=23:HO=0:GOSUB 110
8020 PRINT "                    DRUK OP [ENTER]";
8030 GOSUB 210:IF IN<>13 THEN 8030
8040 GOSUB 100:RETURN:REM    TERUG MET SCHOON SCHERM
9000 REM    STOP
9010 GOSUB 100:VE=9:HO=0:GOSUB 110
9020 PRINT "OK. TOT ZIENS EN VERGEET NIET..........."
9030 SD=50:GOSUB 450:PRINT 
9040 PRINT "COMPUTERFREAKS DOEN HET VINGERVLUG..!!!!"
9050 SD=50:GOSUB 450:PRINT 
9060 GOTO 950
32000 REM    * K. v.d. WISSEL
32010 REM    * NIEUWSTRAAT 306
32020 REM    * 3201 EG SPIJKENISSE
32030 REM    * 01880-13398
32040 REM 
32050 REM    * OP ZX-SPECTRUM
32060 REM    * 1987
32070 REM 
32080 REM TROS-RADIO dd 890621
32090 REM verbeteringen november 1989

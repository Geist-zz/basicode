1000 A=5000:GOTO 20:REM ****ADRESSEN-VERWALTUNG****
1010 REM 
1020 REM (C) HEIKE UND MANFRED FILLINGER , 20.5.1983
1030 REM UEBERARBEITET FUER BASICODE-2 B7.5.84
1040 REM 
1050 REM INITIALISIERUNG
1060 REM 
1070 REM 
1080 DIM I1$(100),I2$(100),I3$(100),I4$(100)
1090 REM 
1100 REM       KONSTANTEN
1110 M=8:REM ADRESSENPROBLATT
1120 R=8:REM LINKERPAPIERRAND
1130 REM 
1140 REM       L,Z$,ZZ$..ZEICHENVERVIELFACHER
1150 REM       A$,A,E$,E..-VON,BIS-ADRESSEN DRUCKEN
1160 REM 
1170 REM 
1180 REM 
1500 REM       VARIABLEN
1510 REM       A1$....FAMILIENNAME BEI EINGABE
1520 REM       A2$....VORNAME BEI EINGABE
1530 REM 
1540 REM       I1$()..NAMEN
1550 REM       I2$()..STRASSE BZW. POSTFACH
1560 REM       I3$()..PLZ , ORT
1570 REM       I4$()..TELEFONNUMMER
1580 REM 
1590 REM       C$.....DIALOGPUFFER
1600 REM       CU$....UEBERGABEPUFFER
1610 REM       SM.....MAXIMALE SCHREIBLAENGE
1620 REM       AW.....AUSWAHL
1630 REM       N......AKTUELLE ADRESSENPOS.
1640 REM       NM.....MAXIMALE ADRESSENPOS.
1650 REM       T......TREFFERKENNZ. BEI SUCHEN
1660 REM       RC.....RETURNCODE
1670 REM       LL.....STRINGLAENGE
1680 REM 
1690 REM       X......HILFSVAR. FUER WARTESCHLEIFE
1700 REM       H1$....HILFSVAR.SORT.
1710 REM       H2$....HILFSVAR.SORT.
1720 REM       I,II...LAUFVAR.
1730 REM       J......LAUFVAR.
1740 REM 
1750 REM 
1760 REM 
2000 REM ---AUSWAHL-MODUL---
2010 GOSUB 100
2020 PRINT 
2030 PRINT "BITTE NAECHSTEN ARBEITSGANG WAEHLEN:"
2040 PRINT "------------------------------------"
2050 PRINT "ADRESSEN EINGEBEN.....................1"
2060 PRINT "ADRESSEN SUCHEN(AENDERN BZW LOESCHEN).2"
2070 PRINT 
2080 PRINT "GESPEICHERTE ADRESSEN LISTEN..........3"
2090 PRINT "GESPEICHERTE ADRESSEN DRUCKEN.........4"
2100 PRINT 
2110 PRINT "ADRESSEN VON KASSETTE LESEN...........5"
2120 PRINT "ADRESSEN AUF KASSETTE SCHREIBEN.......6"
2130 PRINT "KASSETTENDATEN PRUEFEN................7"
2140 PRINT 
2150 PRINT "PROGRAMM BEENDEN......................8"
2160 PRINT 
2170 PRINT ":";:INPUT AW
2180 ON AW GOSUB 3000,4000,5000,6000,7000,8000,9000,9999
2190 N=NM
2200 GOTO 2010
2210 REM 
2220 REM 
2230 REM 
3000 REM ---MODUL ADRESSEN EINGEBEN---
3010 REM 
3020 GOSUB 100
3030 PRINT :PRINT :PRINT :PRINT 
3040 N=NM+1
3050 SM=32
3060 CU$="":PRINT "FAMILIEN-,VORNAME:":GOSUB 15030
3070 IF CU$="" THEN GOSUB 16000:RETURN
3080 I1$(N)=CU$
3090 PRINT 
3100 CU$="":PRINT "STR. BZW.POSTF.:":GOSUB 15030:I2$(N)=CU$
3110 PRINT 
3120 CU$="":PRINT "PLZ , ORT:":GOSUB 15030:I3$(N)=CU$
3130 PRINT 
3140 CU$="":PRINT "TELEFONNR.:":GOSUB 15030:I4$(N)=CU$
3150 PRINT 
3160 N=N+1:NM=N-1
3170 L=39:Z$=".":GOSUB 16500:PRINT ZZ$
3180 GOTO 3050
3190 REM 
3200 REM 
3210 REM 
4000 REM ---MODUL ADRESSEN SUCHEN---
4010 REM 
4020 GOSUB 100
4030 PRINT :PRINT :PRINT 
4040 PRINT 
4050 PRINT "BITTE GESUCHTEN FAMILIENNAMEN EINGEBEN :";
4060 INPUT S$
4070 T=0:LL=LEN(S$)
4080 N=0
4090 N=N+1
4100 IF LEFT$(I1$(N),LL)=S$ THEN GOSUB 4180
4110 IF (LEFT$(I1$(N),LL)<>S$)AND (T=1) THEN 4130
4120 IF NOT (N>NM) THEN 4090
4130 PRINT "---ENDE---"
4140 GOSUB 15500
4150 IF RC=1 THEN GOSUB 16000:REM SORTIEREN
4160 RETURN
4170 REM 
4180 REM AUSWAHL NACH FINDEN
4190 T=1
4200 PRINT I1$(N)
4210 PRINT I2$(N)
4220 PRINT I3$(N)
4230 PRINT I4$(N)
4240 L=39:Z$=".":GOSUB 16500:PRINT ZZ$
4250 PRINT "(W)EITER-(A)ENDERN-(LOE)SCHEN :";:INPUT C$
4260 RC=0
4270 IF C$="W" THEN L=39:Z$=".":GOSUB 16500:PRINT ZZ$:RETURN
4280 IF C$="LOE" THEN GOSUB 4470:RETURN
4290 GOSUB 4320
4300 RETURN
4310 REM 
4320 REM AENDERN
4330 GOSUB 100
4340 PRINT :PRINT :PRINT 
4350 SM=32
4360 CU$=I1$(N):PRINT "NAME:":GOSUB 15030:I1$(N)=CU$
4370 PRINT 
4380 CU$=I2$(N):PRINT "STR. BZW POSTF.:":GOSUB 15030:I2$(N)=CU$
4390 PRINT 
4400 CU$=I3$(N):PRINT "PLZ , ORT:":GOSUB 15030:I3$(N)=CU$
4410 PRINT 
4420 CU$=I4$(N):PRINT "TELEFONNR.:":GOSUB 15030:I4$(N)=CU$
4430 PRINT 
4440 RC=1:L=39:Z$=".":GOSUB 16500:PRINT ZZ$
4450 RETURN
4460 REM 
4470 REM GEFUNDENE ADRESSE LOESCHEN
4480 NM=NM-1
4490 FOR I=N TO NM
4500 I1$(I)=I1$(I+1)
4510 I2$(I)=I2$(I+1)
4520 I3$(I)=I3$(I+1)
4530 I4$(I)=I4$(I+1)
4540 NEXT I
4550 N=N-1
4560 RETURN
4570 REM 
4580 REM 
4590 REM 
5000 REM ---MODUL ALLE GESPEICHERTEN ADRESSEN LISTEN---
5010 REM 
5020 GOSUB 100
5030 PRINT :PRINT :PRINT 
5040 FOR N=1 TO NM
5050 PRINT I1$(N)
5060 PRINT I2$(N)
5070 PRINT I3$(N)
5080 PRINT I4$(N)
5090 L=39:Z$=".":GOSUB 16500:PRINT ZZ$
5100 IF (N/2)=INT(N/2) THEN GOSUB 15500:PRINT ZZ$
5110 NEXT N
5120 PRINT "---ENDE---"
5130 GOSUB 15500
5140 RETURN
5150 REM 
5160 REM 
5170 REM 
6000 REM ---MODUL GESPEICHERTEN ADRESSEN DRUCKEN---
6010 REM 
6020 GOSUB 100:L=R:Z$=" ":GOSUB 16500
6030 PRINT "ALLE ADRESSEN DRUCKEN..........1"
6040 PRINT "ADRESSENBEREICHE DRUCKEN.......2"
6050 PRINT "DRUCKEN BEENDEN................3"
6060 PRINT ":";:INPUT AW
6070 ON AW GOTO 6090,6500,6820
6080 GOTO 6000
6090 FOR N=1 TO NM
6100 SR$=ZZ$+I1$(N):GOSUB 350:GOSUB 360
6110 SR$=ZZ$+I2$(N):GOSUB 350:GOSUB 360
6120 SR$=ZZ$+I3$(N):GOSUB 350:GOSUB 360
6130 SR$=ZZ$+I4$(N):GOSUB 350:GOSUB 360
6140 IF (N/M)=INT(N/M) THEN GOSUB 6190
6150 GOSUB 360:GOSUB 360
6160 NEXT N
6170 RETURN
6180 REM 
6190 REM SEITENWECHSEL
6200 GOSUB 250
6210 PRINT "BITTE NEUES BLATT EINLEGEN !"
6220 GOSUB 15500:GOSUB 100
6230 GOSUB 360
6240 RETURN
6250 REM 
6260 REM 
6270 REM 
6500 REM ADRESSENBEREICH DRUCKEN
6510 PRINT 
6520 PRINT "VON-ANFANGSBUCHSTABE :";:INPUT A$
6530 PRINT "BIS-ANFANGSBUCHSTABE :";:INPUT E$
6540 A=ASC(A$):E=ASC(E$):I=1
6550 IF (A>E) OR (A<ASC("A")) OR (E>ASC("Z")) THEN 6500
6560 FOR N=1 TO NM
6570 T=ASC(LEFT$(I1$(N),1))
6580 IF (T>=A)AND (T<=E) THEN GOSUB 6700
6590 NEXT N
6600 GOTO 6000
6700 REM GEFUNDENE ADRESSE DRUCKEN
6710 IF I>M THEN GOSUB 6190:I=1
6720 SR$=ZZ$+I1$(N):GOSUB 350:GOSUB 360
6730 SR$=ZZ$+I2$(N):GOSUB 350:GOSUB 360
6740 SR$=ZZ$+I3$(N):GOSUB 350:GOSUB 360
6750 SR$=ZZ$+I4$(N):GOSUB 350:GOSUB 360
6760 GOSUB 360:GOSUB 360
6770 I=I+1
6780 RETURN
6790 REM 
6800 REM 
6810 REM 
6820 RETURN:REM MODUL-ENDE
7000 REM ---MODUL ADRESSEN VON KASSETTE LESEN---
7010 REM 
7200 PRINT "DATEINAME :";:INPUT DN$
7210 GOSUB 21200
7220 GOSUB 21500
7230 NM=VAL(S1$)
7240 FOR N=1 TO NM
7250 GOSUB 21500:I1$(N)=S1$:I2$(N)=S2$
7260 GOSUB 21500:I3$(N)=S1$:I4$(N)=S2$
7270 NEXT N
7280 GOSUB 21800
7300 RETURN
7310 REM 
7320 REM 
7330 REM 
8000 REM ---MODUL ADRESSEN AUF KASSETTE SCHREIBEN---
8010 REM 
8200 SR=NM:GOSUB 300
8210 PRINT "DATEI-NAME :";:INPUT DN$
8220 GOSUB 21000
8230 S1$=SR$:S2$=""
8240 GOSUB 21300
8250 FOR N=1 TO NM
8260 S1$=I1$(N):S2$=I2$(N):GOSUB 21300
8270 S1$=I3$(N):S2$=I4$(N):GOSUB 21300
8280 NEXT N
8290 GOSUB 21700
8300 RETURN
8310 REM 
8320 REM 
8330 REM 
9000 REM ---MODUL KASSETTENDATENPRUEFEN ---
9010 PRINT "DATEINAME :";:INPUT DN$
9020 GOSUB 21200
9030 GOSUB 21500
9040 IF VAL(S1$)<>NM THEN 9500
9050 N=0
9060 N=N+1
9070 GOSUB 21500
9080 IF I1$(N)<>S1$ THEN 9500
9090 IF I2$(N)<>S2$ THEN 9500
9100 GOSUB 21500
9110 IF I3$(N)<>S1$ THEN 9500
9120 IF I4$(N)<>S2$ THEN 9500
9130 IF NOT (N>=NM) THEN 9060
9140 GOSUB 21800
9150 RETURN
9500 REM LESEFEHLER
9510 PRINT "LESEFEHLER !"
9520 GOSUB 21800
9530 GOSUB 15500
9540 RETURN
9550 REM 
9560 REM 
9570 REM 
9999 GOTO 950
15000 REM ---UNTERPROGRAMM ZEILEN-EDITOR---
15010 REM 
15020 REM EINGABEPARAMETER - SM (ZUL. TEXT-LAENGE)
15030 REM EINGABEPARAMETER - CU$ (ALT-TEXT)
15040 REM AUSGABEPARAMETER - CU$ (NEU-TEXT)
15050 REM 
15060 PRINT ">";:GOSUB 120:HO=HO+SM:GOSUB 110:PRINT "<";
15070 HO=HO-SM:GOSUB 110:PRINT CU$;:PRINT "<";
15080 GOSUB 120:GOSUB 210
15090 IF (IN$="<")AND (HO>2) THEN GOSUB 15170
15100 IF IN$=CHR$(13) THEN RETURN
15110 IF IN$="<" THEN 15080
15120 IF HO>=(SM+2) THEN 15080
15130 CU$=CU$+IN$
15140 GOSUB 120:HO=HO-1:GOSUB 110
15150 PRINT IN$;:PRINT "<";
15160 GOTO 15080
15170 GOSUB 120:HO=HO-1:GOSUB 110
15180 IF HO<>(SM+1) THEN PRINT " ";
15190 HO=HO-1:GOSUB 110:PRINT "<";
15200 IF HO<=1 THEN CU$="":RETURN
15210 CU$=LEFT$(CU$,HO-1)
15220 RETURN
15500 REM ---UNTERPROGRAMM WARTESCHLEIFE---
15510 REM 
15520 PRINT "WENN WEITER DANN 'RETURN' DRUECKEN !"
15530 GOSUB 210
15540 RETURN
15550 REM 
15560 REM 
15570 REM 
16000 REM ---UNTERPROGRAMM LINSORT---
16010 REM GEMAESS MC-ZEITSCHRIFT MAI/JUNI 1981 SEITE 26
16020 REM SORTIEREN DER FELDER I1..4$ NACH DEM SCHLUESSEL
16030 REM IN DEM FELD I1$ .
16040 REM DIE UNTERE FELDGRENZE IST 1, DIE OBERE FELD-
16050 REM GRENZE STEHT IN DER VARIABLEN NM .
16060 REM 
16070 IF NM<2 THEN RETURN
16080 GOSUB 100
16090 PRINT "-BITTE WARTEN-"
16100 FOR I=2 TO NM
16110 H1$=I1$(I)
16120 H2$=I2$(I):H3$=I3$(I):H4$=I4$(I)
16130 J=I-1
16140 IF H1$>=I1$(J) THEN 16190
16150 I1$(J+1)=I1$(J)
16160 I2$(J+1)=I2$(J):I3$(J+1)=I3$(J):I4$(J+1)=I4$(J)
16170 J=J-1
16180 IF J<>0 THEN 16140
16190 I1$(J+1)=H1$
16200 I2$(J+1)=H2$:I3$(J+1)=H3$:I4$(J+1)=H4$
16210 NEXT I
16220 RETURN
16230 REM 
16240 REM 
16250 REM 
16500 REM ZEICHENVERVIELFACHER
16510 ZZ$="":IF L<1 THEN RETURN
16520 FOR II=1 TO L
16530 ZZ$=ZZ$+Z$
16540 NEXT II
16550 RETURN
21000 REM DATEIVERWALTUNG
21010 REM  GILT FUER CPC 464
21100 REM  DATEI OEFFNEN ZUM SCHREIBEN
21110 OPENOUT DN$:RETURN
21200 REM  DATEI OEFFNEN ZUM LESEN
21210 OPENIN DN$:RETURN
21300 QP$=S1$:GOSUB 21400:S1$=QZ$:QP$=S2$:GOSUB 21400
21310 S2$=QZ$
21320 REM DATEN (S1$,S2$) AUF KASSETTE SCHREIBEN
21330 PRINT#9,S1$:PRINT#9,S2$:RETURN
21400 REM UMWANDELN VON DOPPELPUNKT,KOMMA
21410 REM ANFUEHRUNGSZEICHEN USW.
21420 QZ$="":IF QP$="" THEN QP$=" "
21430 FOR QP=1 TO LEN(QP$)
21440 QQ$=MID$(QP$,QP,1):IF QQ$=" " THEN QQ$=CHR$(252)
21450 IF QQ$="," THEN QQ$=CHR$(250)
21460 IF QQ$=CHR$(34) THEN QQ$=CHR$(253)
21470 IF QQ$=":" THEN QQ$=CHR$(251)
21480 QZ$=QZ$+QQ$:NEXT QP:RETURN
21500 REM  DATEN (S1$,S2$) VON KASSETTE LESEN
21510 INPUT#9,S1$:INPUT#9,S2$
21520 QP$=S1$:GOSUB 21600:S1$=QZ$:QP$=S2$
21530 GOSUB 21600:S2$=QZ$:RETURN
21600 REM UMWANDELN VON DOPPELPUNKT,KOMMA
21610 REM ANFUEHRUNGSZEICHEN USW.
21620 QZ$=""
21630 FOR QP=1 TO LEN(QP$)
21640 QQ$=MID$(QP$,QP,1):IF QQ$=CHR$(252) THEN QQ$=" "
21650 IF QQ$=CHR$(250) THEN QQ$=","
21660 IF QQ$=CHR$(251) THEN QQ$=":"
21670 IF QQ$=CHR$(253) THEN QQ$=CHR$(34)
21680 QZ$=QZ$+QQ$
21690 NEXT QP:RETURN
21700 REM  DATEI ZUM SCHREIBEN SCHLIESSEN 
21710 CLOSEOUT:RETURN
21800 REM  DATEI ZUM LESEN SCHLIESSEN
21810 CLOSEIN:RETURN

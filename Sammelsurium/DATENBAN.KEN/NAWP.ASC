1000 GOSUB 270:A=0.9*FR-350:GOTO 20:REM  NAWP-BESTAND
1010 GOSUB 270
1020 MR=INT((FR-1000)/150):REM  MAXIMUM AANTAL RECORDS
1030 MV=5:REM  MAXIMUM AANTAL VELDEN
1040 LR$="":FOR I=1 TO HO:LR$=LR$+" ":NEXT I
1050 DIM SO(40),RI$(MV),DE$(MV),BE$(MR,MV)
1060 AR=0:REM  0 RECORDS AANWEZIG
1070 VS=0:REM  SORTEER OP NAAM
1080 NU=1:REM  HUIDIG RECORD
1090 READ MV
1100 FOR J=0 TO MV:READ DE$(J):BE$(0,J)="":NEXT J
1110 VT=VE:HT=HO
1500 REM 
1510 REM  HOOFDCYCLUS
1520 REM 
1530 GOSUB 100
1540 SR$="A D R E S B E S T A N D":GOSUB 10130
1550 PRINT :PRINT 
1560 PRINT "Uw keuzemogelijkheden:"
1570 PRINT :PRINT 
1580 PRINT :PRINT "1  Oud bestand inlezen"
1590 PRINT :PRINT "2  Bestand raadplegen/muteren"
1600 PRINT :PRINT "3  Bestand wegschrijven"
1610 PRINT :PRINT "4  Einde programma"
1620 MK=5:GOSUB 1830
1630 GOSUB 100
1640 ON KZ GOTO 2030,3030,7030,9030,8030
1800 REM 
1810 REM  KEUZE-OPVRAGEN-SUBROUTINE
1820 REM 
1830 HO=0:VE=VT-1:GOSUB 110
1840 PRINT "Wat is uw keuze      ";
1850 HO=17:VE=VT-1:GOSUB 110
1860 INPUT KZ$:KZ=VAL(KZ$)
1870 IF (KZ<1)OR(KZ>MK)OR(KZ<>INT(KZ))THEN GOSUB 250:GOTO 1830
1880 RETURN
2000 REM 
2010 REM  MENUKEUZE 1=OUD BESTAND INLEZEN
2020 REM 
2030 SR$="OUD BESTAND INLEZEN":GOSUB 10130
2040 PRINT :PRINT 
2050 PRINT "U kunt inlezen van:"
2060 PRINT :PRINT 
2070 PRINT :PRINT "1  BASICODE-3 cassette"
2080 PRINT :PRINT "2  cassette van uw eigen computer"
2090 PRINT :PRINT "3  diskette van uw eigen computer"
2100 PRINT :PRINT "4  Toch geen bestand inlezen"
2110 MK=4:GOSUB 1830
2120 IF KZ=4 THEN 1530
2130 NF=2*KZ-2:NF$="NAWP"
2140 GOSUB 500:REM  OPENEN
2150 GOSUB 540:AR=VAL(IN$):REM  INVOER AANTAL RECS
2160 GOSUB 540:MV=VAL(IN$):REM  INVOER AANTAL VELDEN
2170 GOSUB 540:VS=VAL(IN$):REM  INVOER SORTEERVELD
2180 HO=0:VE=VT-1
2190 FOR J=0 TO MV
2200 GOSUB 540:DE$(J)=IN$
2210 FOR I=1 TO AR
2220 GOSUB 110:PRINT "Veld ";:SR=J:GOSUB 300
2230 PRINT SR$;", Record ";:SR=I:GOSUB 300
2240 PRINT SR$;"    ";
2250 GOSUB 540:BE$(I,J)=IN$
2260 NEXT I
2270 NEXT J
2280 GOSUB 580:REM  SLUIT BESTAND
2290 NU=1
2300 GOTO 1530
3000 REM 
3010 REM  MENUKEUZE 2 = BESTAND RAADPLEGEN/MUTEREN
3020 REM 
3030 GOSUB 100
3040 SR$="BESTAND RAADPLEGEN / MUTEREN":GOSUB 10130
3050 PRINT :PRINT "U hebt nu ";:SR=AR:GOSUB 300
3060 PRINT SR$;" records,"
3070 PRINT " gesorteerd op ";DE$(VS)
3080 PRINT :PRINT 
3090 PRINT :PRINT "U kunt nu:"
3100 PRINT :PRINT "1  Bladeren in het bestand"
3110 PRINT :PRINT "2  Zoeken naar een ";DE$(VS)
3120 PRINT :PRINT "3  Een nieuw record invoeren"
3130 PRINT :PRINT "4  Bestand opnieuw sorteren"
3140 PRINT :PRINT "5  Terug naar het hoofdmenu"
3150 MK=6:GOSUB 1830
3160 IF KZ=5 THEN 1530
3170 ON KZ GOSUB 3330,4530,5530,6030
3180 IF KZ=2 THEN GOSUB 3330
3190 GOTO 3030
3300 REM 
3310 REM  BLADEREN
3320 REM 
3330 IF AR=0 THEN GOSUB 250:RETURN
3340 IF NU<1 THEN NU=1
3350 IF NU>AR THEN NU=AR
3360 GOSUB 4030
3370 HO=0:VE=VT-1:GOSUB 110
3380 PRINT "(V)ooruit,  (T)erug,  (W)ijzigen,"
3390 PRINT "(P)rinten,  (S)chrappen,  (M)enu";
3400 GOSUB 210
3410 IF IN=86 THEN NU=NU+1:GOTO 3340:REM  V
3420 IF IN=83 THEN 3830:REM  S
3430 IF IN=84 THEN NU=NU-1:GOTO 3340:REM  T
3440 IF IN=80 THEN GOSUB 3630:GOTO 3340:REM P
3450 IF IN=87 THEN 5030:REM  W
3460 IF IN=77 THEN RETURN:REM  M
3470 GOSUB 250:GOTO 3400
3600 REM 
3610 REM  PRINTEN
3620 REM 
3630 GOSUB 360
3640 SR=NU:GOSUB 300
3650 SR$="Nummer "+SR$:GOSUB 350:GOSUB 360:GOSUB 360
3660 FOR J=0 TO MV
3670 SR$=DE$(J):GOSUB 350:SR$=" : ":GOSUB 350
3680 SR$=BE$(NU,J):GOSUB 350:GOSUB 360
3690 NEXT J:GOSUB 360
3700 RETURN
3800 REM 
3810 REM  SCHRAPPEN
3820 REM 
3830 HO=0:VE=0:GOSUB 110
3840 SR$="DIT RECORD SCHRAPPEN":GOSUB 10130
3850 VE=VT-1:GOSUB 110
3860 SR$="HERHAAL OF HERROEP OPDRACHT":GOSUB 10140
3870 GOSUB 250:REM  PIEP
3880 VE=VT:GOSUB 110
3890 PRINT "   (S)chrappen of (B)laderen    ";
3900 GOSUB 210
3910 IF IN<>83 THEN 3340:REM  <>S
3920 FOR I=NU TO AR-1
3930 FOR J=0 TO MV:BE$(I,J)=BE$(I+1,J):NEXT J
3940 NEXT I
3950 AR=AR-1
3960 GOTO 3340
4000 REM 
4010 REM  PRINT RECORD(NU)
4020 REM 
4030 GOSUB 100
4040 PRINT "RECORD NUMMER ";NU
4050 PRINT 
4060 FOR J=0 TO MV
4070 HO=0:VE=5+3*J:GOSUB 110:PRINT DE$(J)
4080 HO=10:GOSUB 110:PRINT ": ";BE$(NU,J)
4090 NEXT J
4100 RETURN
4500 REM 
4510 REM  ZOEKEN
4520 REM 
4530 GOSUB 100
4540 PRINT "Welke ";DE$(VS);" wilt u "
4550 PRINT "    ";:INPUT Z$
4560 GOSUB 4610
4570 RETURN
4580 REM 
4590 REM  ZOEKROUTINE Z$
4600 REM 
4610 PRINT :PRINT "Even geduld a.u.b.";
4620 V1=1:V2=AR+1
4630 V3=INT((V1+V2)/2)
4640 PRINT ".";
4650 IF BE$(V3,VS)>Z$ THEN V2=V3:GOTO 4670
4660 V1=V3
4670 IF V2>V1+1 THEN 4630
4680 NU=V1
4690 RETURN
5000 REM 
5010 REM  WIJZIGEN
5020 REM 
5030 GOSUB 100
5040 SR$="DIT RECORD WIJZIGEN":GOSUB 10130
5050 FOR J=0 TO MV
5060 HO=0:VE=3+4*J:GOSUB 110
5070 PRINT "De ";DE$(J);" is nu:"
5080 PRINT "   ";BE$(NU,J)
5090 PRINT "Wijzigen (J/N) ";:GOSUB 210
5100 IF IN<>74 THEN 5140:REM  <>J
5110 VE=VE+2:GOSUB 110:PRINT LR$
5120 GOSUB 110:PRINT "Nieuwe ";DE$(J);" ";
5130 INPUT BE$(NU,J)
5140 NEXT J
5150 RETURN
5500 REM 
5510 REM  NIEUW RECORD INVOEREN
5520 REM 
5530 GOSUB 100
5540 IF AR=MR THEN GOSUB 250:RETURN
5550 PRINT "NIEUW RECORD INVOEREN"
5560 FOR J=0 TO MV
5570 HO=0:VE=7+J*3:GOSUB 110
5580 PRINT DE$(J)
5590 HO=11:GOSUB 110:PRINT ":";
5600 INPUT RI$(J)
5610 NEXT J
5620 PRINT :PRINT "Tik op A voor accoord,"
5630 PRINT "    op C voor correctie,"
5640 PRINT "    op V voor 'Vergeet deze invoer'";
5650 GOSUB 210
5660 IF IN=86 THEN RETURN:REM  V
5670 IF IN=67 THEN 5530:REM  C
5680 IF IN<>65 THEN 5650:REM  <>A
5690 IF AR=0 THEN NU=1:GOTO 5750
5700 Z$=RI$(VS):GOSUB 4610:REM  ZOEK
5710 NU=NU+1
5720 FOR I=AR+1 TO NU+1 STEP -1
5730 FOR J=0 TO MV:BE$(I,J)=BE$(I-1,J):NEXT J
5740 NEXT I
5750 FOR J=0 TO MV:BE$(NU,J)=RI$(J):NEXT J
5760 AR=AR+1
5770 RETURN
6000 REM 
6010 REM  OPNIEUW SORTEREN
6020 REM 
6030 GOSUB 100
6040 PRINT :PRINT 
6050 PRINT "U kunt het bestand laten sorteren op:"
6060 PRINT :PRINT 
6070 FOR J=0 TO MV
6080 PRINT :PRINT J+1;" ";DE$(J)
6090 NEXT J
6100 MK=MV+1:GOSUB 1830
6110 VS=KZ-1:HO=0
6120 GOSUB 110:PRINT LR$:VE=VE-4
6130 GOSUB 110:PRINT "Ik sorteer op ";DE$(VS)
6140 PRINT "Ogenblikje ...";
6500 REM 
6510 REM  SORTEREN MET QUICKSORT
6520 REM 
6530 V1=2:SO(1)=1:SO(21)=AR
6540 V1=V1-1:V2=SO(V1):V3=SO(V1+20)
6550 V4=V2:V5=V3:V6$=BE$(.5*(V2+V3),VS)
6560 IF BE$(V4,VS)<V6$ THEN V4=V4+1:GOTO 6560
6570 IF BE$(V5,VS)>V6$ THEN V5=V5-1:GOTO 6570
6580 IF V4<=V5 THEN GOSUB 6660:V4=V4+1:V5=V5-1
6590 IF V4<=V5 THEN 6560
6600 IF V4<V3 THEN SO(V1)=V4:SO(V1+20)=V3:V1=V1+1
6610 V3=V5:IF V2<V3 THEN 6550
6620 IF V1>1 THEN 6540
6630 PRINT :PRINT "Gesorteerd op ";DE$(VS)
6640 GOSUB 10030
6650 RETURN
6660 FOR J=0 TO MV
6670 H$=BE$(V4,J):BE$(V4,J)=BE$(V5,J):BE$(V5,J)=H$
6680 NEXT J
6690 RETURN
7000 REM 
7010 REM  MENUKEUZE 3 = BESTAND WEGSCHRIJVEN
7020 REM 
7030 SR$="BESTAND WEGSCHRIJVEN":GOSUB 10130
7040 PRINT :PRINT 
7050 PRINT "U kunt wegschrijven naar:"
7060 PRINT :PRINT 
7070 PRINT :PRINT "1  BASICODE-3 cassette"
7080 PRINT :PRINT "2  cassette van uw eigen computer"
7090 PRINT :PRINT "3  diskette van uw eigen computer"
7100 PRINT :PRINT "4  Toch geen bestand wegschrijven"
7110 MK=4:GOSUB 1830
7120 IF KZ=4 THEN 1530
7130 NF=2*KZ-1:NF$="NAWP"
7140 GOSUB 500:REM  OPENEN
7150 SR=AR:GOSUB 300:GOSUB 560
7160 SR=MV:GOSUB 300:GOSUB 560
7170 SR=VS:GOSUB 300:GOSUB 560
7180 HO=0:VE=VT-1
7190 FOR J=0 TO MV
7200 SR$=DE$(J):GOSUB 560
7210 FOR I=1 TO AR
7220 GOSUB 110:PRINT "Veld ";:SR=J:GOSUB 300
7230 PRINT SR$;", Record ";:SR=I:GOSUB 300
7240 PRINT SR$;"    ";
7250 SR$=BE$(I,J):GOSUB 560
7260 NEXT I
7270 NEXT J
7280 GOSUB 580:REM  SLUIT BESTAND
7290 GOTO 1530
8000 REM 
8010 REM  MENUKEUZE 5 = NIEUW BESTAND
8020 REM 
8030 GOSUB 100
8040 PRINT "Aantal velden (1...5)";
8050 INPUT MV
8060 MV=INT(MV):IF (MV<1)OR(MV>5)THEN GOSUB 250:GOTO 8040
8070 MV=MV-1:FOR J=0 TO MV
8080 PRINT "Naam veld ";J+1;" ";
8090 INPUT DE$(J)
8100 NEXT J
8110 GOTO 1530
9000 REM 
9010 REM  MENUKEUZE 4 = EINDE
9020 REM 
9030 GOSUB 100
9040 PRINT :PRINT "EINDE PROGRAMMA"
9050 PRINT :PRINT "Weet u het zeker ";
9060 INPUT SR$:GOSUB 330
9070 IF SR$<>"JA" THEN 1530
9080 GOTO 950
10000 REM 
10010 REM  WACHTROUTINE
10020 REM 
10030 HO=INT((HT-30)/2):VE=VT
10040 SR$="Druk op de spatie voor vervolg"
10050 GOSUB 10140
10060 GOSUB 210
10070 IF IN$<>" " THEN 10060
10080 RETURN
10100 REM 
10110 REM  GEACCENTUEERD CENTRAAL
10120 REM 
10130 VE=0
10140 HO=0:GOSUB 110
10150 FOR S=1 TO INT((HT-LEN(SR$))/2-3)
10160 PRINT " ";
10170 NEXT S
10180 GOSUB 150
10190 RETURN
25000 DATA 3
25010 DATA "Naam","Adres","Woonplaats","Postcode"
30000 REM *****************************
30010 REM 
30020 REM   ADRESBESTAND IS EEN
30030 REM   DEMONSTRATIE VAN DE
30040 REM   MOGELIJKHEDEN   VAN
30050 REM   B A S I C O D E - 3
30060 REM 
30070 REM 
30080 REM   COPYRIGHT (C)
30090 REM   STICHTING  BASICODE
30100 REM             1986,1987
30110 REM 
30120 REM ******************************
30130 REM  versie 1.5 dd 871212

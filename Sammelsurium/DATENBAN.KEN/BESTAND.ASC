1000 A=10000:GOTO 20:REM Bestaende in BASICODE 3
1010 MX=600:DIM I$(MX),S(INT(LN(MX)/LN(2))+1,1)
1020 DIM A$(14),N$(3),N(3),I(3)
1030 A$(1)="Basicode  Kassette"
1040 A$(2)="Standart Kassette"
1050 A$(3)="Disk ( standart )"
1060 A$(4)="Disk   unit Nr 2  "
1070 A$(5)="Computerspeicher "
1080 A$(6)="DATA - Zeilen     "
1090 REM MENU
1100 A$(7)="Einlesen       "
1110 A$(8)="Drucken        "
1120 A$(9)="Kopieren       "
1130 A$(10)="Zusammenfuegen "
1140 A$(11)="Vergleichen    "
1150 A$(12)="Sortieren      "
1160 A$(13)="Hilfe          "
1170 A$(14)="Ende           "
1180 I=0:J=-1:S$="                    "
1190 REM MENU
1200 GOSUB 100
1210 PRINT"            B E S T A E N D E "
1220 PRINT"            -----------------":PRINT
1230 GOSUB 120:V=VE:PRINT"Waehle .....":PRINT
1240 FOR M=7 TO 13
1250 PRINT A$(M);" ";CHR$(58+M)
1260 NEXT M
1270 PRINT A$(14);" ";CHR$(72);" oder  ";CHR$(88):PRINT
1280 GOSUB 120:M=VE:HO=0:VE=V:GOSUB 110
1290 REM KEUZE
1300 GOSUB 210:K=IN-64:IF IN=88 THEN K=8
1310 IF (K<1) OR (K>8) THEN 1300
1320 VE=V:GOSUB 110:PRINT A$(K+6)
1330 FOR VE=M TO V+2 STEP-1:GOSUB 110:PRINT S$:NEXT VE
1340 VE=V:GOSUB 110
1350 IF K=8 THEN 950
1360 IF K=6 THEN GOSUB 4010:GOTO 1200
1370 IF K=7 THEN GOSUB 5010:GOTO 1200
1380 N(1)=97:N(2)=98:N(3)=99
1390 GOSUB 3005:GOTO 1200
2000 REM BESTANDSNAMEN
2010 GOSUB 120:V=VE:HO=12:GOSUB 110
2020 IF N=1 THEN PRINT"Einlesen...";
2030 IF N=2 THEN PRINT"Einlesen2 ";
2040 IF N=3 THEN PRINT"Auslesen .";
2050 PRINT".........":PRINT
2060 FOR M=1 TO 6
2070 PRINT"            ";A$(M);" ";CHR$(M+48):NEXT M:PRINT
2080 PRINT"            Abgebrochen        ";CHR$(88)
2090 GOSUB 120:M=VE:VE=V:HO=12:GOSUB 110
2100 GOSUB 210:N(N)=IN-48
2110 IF IN=88 THEN FT=1:RETURN
2120 IF (IN<49) OR (IN>54) THEN 2100
2130 FOR VE=M TO V STEP-1:GOSUB 110:PRINT S$:NEXT VE
2140 VE=V:GOSUB 110:PRINT A$(N(N));" ";
2150 REM
2160 IF (IN<50) OR (IN>52) THEN N$(N)="":PRINT:RETURN
2170 N$="":GOSUB 120
2180 PRINT"'";N$;"' ";:GOSUB 110
2190 GOSUB 210:IF IN>31 THEN 2220
2200 IF LEN(N$)>7 THEN GOSUB 250:GOTO 2180
2210 N$(N)=N$:GOTO 2510
2220 IF IN<>127 THEN N$=N$+IN$:GOTO 2180
2230 IF LEN(N$)=1 THEN N$=""
2240 IF LEN(N$)>1 THEN N$=LEFT$(N$,LEN(N$)-1)
2250 GOTO 2180
2500 REM EINDE BESTANDSNAAM
2510 PRINT:RETURN
2600 REM AFBREKEN
2610 HO=0:VE=M:GOSUB 110
2620 PRINT:PRINT"Abgebrochen"
2630 GOSUB 210:RETURN
2700 REM FOUT
2710 PRINT:PRINT"2 x Dasselbe !!"
2720 GOSUB 210:RETURN
3000 REM INIT
3005 FT=0:N=1:GOSUB 2010
3010 IF FT<>0 THEN 2610
3015 IF (K=4) OR (K=5) THEN N=2:GOSUB 2010
3020 IF FT<>0 THEN 2610
3025 IF (K=3) OR (K=4) THEN N=3:GOSUB 2010
3030 IF FT<>0 THEN 2610
3035 IF (N(1)<3) AND (N(2)<3) THEN 2710
3040 IF (N(1)<3) AND (N(3)<3) THEN 2710
3045 IF (N(2)<3) AND (N(3)<3) THEN 2710
3050 IF N(1)=N(2) THEN 2710
3055 IF N(1)=N(3) THEN 2710
3060 IF N(2)=N(3) THEN 2710
3100 REM INIT 2
3110 PRINT
3120 N(1)=(N(1)-1)*2:REM INPUT
3130 N(2)=(N(2)-1)*2:REM INPUT
3140 N(3)=(N(3)-1)*2+1:REM OUTPUT
3150 I(1)=0:I(2)=0:I(3)=0
3200 REM OPENEN FILES
3205 FT=0:H$="":RESTORE:IF N(3)=9 THEN J=-1
3210 FOR N=1 TO 3
3215 IN=0:NF=N(N):NF$=N$(N)
3220 IF N(N)<8 THEN GOSUB 500
3225 IF (N(N)=8) AND (J=-1) THEN IN=-1
3230 IF N(N)=10 THEN READD$:IF D$="$$$$$" THEN IN=-1
3235 I(N)=IN:IF IN<>0 THEN PRINT"falsche Eroeffnung":FT=N:N=3
3240 NEXT N:IF FT<>0 THEN N=FT:GOTO 3810
3245 N=3:I=0:IJ=0:L=0
3250 IF K<>1 THEN GOSUB 120:PRINT"0";
3300 REM LEES FILE 1
3310 IF K<>1 THEN GOSUB 110:SR=I:GOSUB 300:PRINT SR$;
3320 IF N(1)<8 THEN NF=N(1):GOSUB 540:SR$=IN$:I(1)=IN
3330 IF N(1)=8 THEN SR$=I$(IJ):IJ=IJ+1:IF IJ>J THEN I(1)=1
3340 IF N(1)=10 THEN SR$=D$:READD$:IF D$="$$$$$" THEN I(1)=1
3400 REM LEES FILE 2
3410 IF K<>5 THEN 3510
3420 IF N(2)<8 THEN NF=N(2):GOSUB 540:H$=IN$:I(2)=IN
3430 IF N(2)=8 THEN H$=I$(IJ):IJ=IJ+1:IF IJ>J THEN I(2)=1:H$=""
3440 IF N(2)=10 THEN H$=D$:READD$:IF D$="$$$$$" THEN I(2)=1
3500 REM SCHRIJF FILE 3
3510 IF N(3)<8 THEN NF=N(3):GOSUB 560:I(3)=IN
3520 IF N(3)=9 THEN I$(I)=SR$:IF I>=MX THEN I(3)=-1
3530 IF K=1 THEN PRINT SR$
3540 IF K=2 THEN GOSUB 350:GOSUB 360
3550 IF (K=5) AND (SR$<>H$) THEN FT=1:I(1)=1:I(2)=1
3560 IF (K=5) AND (I(1)<>I(2)) THEN FT=1
3600 REM CONTROLE INTERRUPT
3610 IN=0:GOSUB 200
3620 IF IN=88 THEN I(1)=2:PRINT:PRINT:PRINT"Abgebrochen":IN=0
3630 IF IN<>0 THEN GOSUB 210
3640 IF IN=88 THEN I(1)=2:PRINT:PRINT:PRINT"Abgebrochen"
3700 REM EINDE BESTAND ?
3710 IF ABS(I(1))+ABS(I(2))+ABS(I(3))=0 THEN I=I+1:GOTO 3310
3720 IF I(1)<>1 THEN 3770
3730 IF (K<>4) OR (L=1) THEN 3770
3740 H$=N$(1):N$(1)=N$(2):N$(2)=H$
3750 H=N(1):N(1)=N(2):N(2)=H
3760 L=1:I(1)=0:I=I+1:GOTO 3310
3770 IF N(3)=9 THEN J=I
3780 IF K<>1 THEN SR=I+1:GOSUB 110:GOSUB 300:PRINT SR$:PRINT
3800 REM SLUITEN BESTANDEN
3810 PRINT
3820 FOR M=1 TO N
3830 IF N(M)<8 THEN NF=N(M):GOSUB 580
3840 NEXT M
3900 REM EINDE
3910 IF (K=5) AND (FT=0) THEN PRINT"Bestaende sind ungleich"
3920 IF (K=5) AND (FT<>0) THEN PRINT"Bestaende sind ungleich"
3930 FT=0:IF I(1)<0 THEN FT=1
3940 IF I(2)<0 THEN FT=2
3950 IF I(3)<0 THEN FT=3
3960 IF FT=0 THEN PRINT"Ok.":GOTO 3990
3970 PRINT"Fehler in :   ";A$(INT(N(FT)/2)+1);
3980 IF N$(FT)<>"" THEN PRINT" '";N$(FT);"'";
3990 PRINT:GOSUB 210:RETURN
4000 REM SORTEREN QUICKSORT
4010 IF J<1 THEN RETURN
4020 PRINT:PRINT:SR=0:GOSUB 120:PRINT"0"
4030 S(1,0)=0:S(1,1)=J:S=1:F=1
4040 L=S(S,0):R=S(S,1):S=S-1:P=L:Q=R
4050 X$=I$(INT((L+R)/2))
4060 IF I$(P)<X$ THEN P=INT(P+1.5):GOTO 4060
4070 IF X$<I$(Q) THEN Q=INT(Q-0.5):GOTO 4070
4080 IF P>Q THEN 4140
4090 W$=I$(P):I$(P)=I$(Q):I$(Q)=W$
4100 SR=SR+1:GOSUB 300:GOSUB 110:PRINT SR$
4110 P=P+1:P=J-(J-P+ABS(J-P))/2
4120 Q=Q-1:Q=(Q+ABS(Q))/2
4130 GOTO 4060
4140 IF L<Q THEN S=S+1:S(S,0)=L:S(S,1)=Q
4150 IF P<R THEN S=S+1:S(S,0)=P:S(S,1)=R
4160 IF S<>0 THEN 4040
4170 PRINT:PRINT:PRINT"Ok.":GOSUB 210:RETURN
5000 REM HELP
5010 GOSUB 100
5020 PRINT"            B E S T A E N D E "
5030 PRINT"            -----------------  ":PRINT
5040 GOSUB 120:V=VE:PRINT"Hilfe":PRINT
5050 PRINT"Mit Hilfe dieses Programms koennen Sie"
5060 PRINT"BASICODE - 3 Bestaende kopieren. Sie "
5070 PRINT"koennen Bestaende ausdrucken( Bild"
5080 PRINT"u. Drucker), Bestaende kopieren, zusam-"
5090 PRINT"menfuegen und vergleichen."
5100 REM
5110 PRINT:PRINT"Die Bestaende koennen auf Kassette(auch"
5120 PRINT"Basicode) und auf Disk stehen. Auch "
5130 PRINT"kann ein 'Bestand' aus DATA-Zeilen be-"
5140 PRINT"stehen. Als Zwischespeicher kann"
5150 PRINT"der Computerspeicher genutzt werden."
5160 PRINT"Das kann maximal 500 Zeilen betreffen,"
5170 PRINT"die auch sortiert werden koennen."
5180 PRINT:PRINT"Mit einer Taste wird die Bearbeitung"
5190 PRINT"unterbrochen, mit der Taste 'x' beendet."
5200 REM
5210 PRINT:PRINT"                          <Taste>";
5220 GOSUB 210:HO=0
5230 FOR VE=23 TO 4 STEP-1:GOSUB 110
5240 PRINT"                                       ";
5250 NEXT VE:RETURN
25000 REM EVT DATA-REGELS
25010 DATA"Vorlage DATA - Zeilen"
25020 DATA":"
25030 DATA"siehe Programm 25000-29000"
29000 DATA"$$$$$"
30000 REM BESTANDEN 1.8
30010 REM
30020 REM UTILITIES VOOR BESTANDEN
30030 REM
30040 REM IN BASICODE 3
30050 REM
31000 REM NF= 0 1 BASICODE CASSETTE
31010 REM     2 3 STANDAARD CASSETTE
31020 REM     4 5 DISK (STANDAARD)
31030 REM     6 7 DISK (UNIT NR 2)
31040 REM     8 9 COMPUTER GEHEUGEN
31050 REM    10   DATA-REGELS
31060 REM
31070 REM N$(3) N(3) I(3)
31080 REM
31090 REM 1 = INPUT FILE 1
31100 REM 2 = EVT INPUT  FILE 2
31110 REM 3 = EVT OUTPUT FILE
31120 REM
32000 REM GESCHREVEN  DOOR
32010 REM
32020 REM HENK VAN  DEELEN
32030 REM RUDOLPHLAAN   28
32040 REM 3794 MZ DE GLIND
32050 REM
32060 REM (C) H VAN DEELEN 1988
32070 REM
32080 REM TROS-RADIO DD 890405
32090 REM
32100 REM Uebersetzt von
32110 REM Ronald Knak
32120 REM Dr.-H.-Wolf-Str.1
32130 REM Schwerin
32140 REM 2 7 5 8
32150 REM
32160 REM am 14.12.90
32170 REM 
32180 REM Erstausstrahlung: Deutschlandsender Kultur
32190 REM-spezial 910220

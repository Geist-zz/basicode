1000 A=3000:GOTO 20:REM *BUNDESLIGA*
1010 REM 
1020 REM * CO. H+M FILLINGER *
1030 REM * 3.4.1984          *
1040 GOSUB 100
1100 REM * TITEL *
1110 VE=5:HO=0:GOSUB 110
1120 PRINT "BUNDESLIGATABELLE"
1130 PRINT 
1140 PRINT "CO. H+M FILLINGER"
1150 PRINT "  MCMLXXXIV"
1160 PRINT 
1200 REM * VARIABLENLISTE *
1210 REM AW,AW$.............AUSWAHL
1220 REM D..................DRUCKER
1230 REM DA.................DATEN LESEN
1240 REM DS.................DATEN SPEICHERN
1250 REM E,E$...............EDITIERHILFE
1260 REM I,J,K,L............LAUFVARIABLE
1270 REM SP(I)..............SPIELTAG
1280 REM T(I),T1(I),T2(I)...TORE
1290 REM P(I),P1(I).........PUNKTE
1300 REM V$(I)..............VEREIN
1310 REM V1$(I).............ABKUERZUNG
1320 REM VA$(I),VB$(I)......1.2.VEREIN
1330 REM 
1500 DIM T(17),T1(17),T2(30),T3(30),P1(17),P(17)
1510 DIM V$(17),V1$(17),VA$(30),VB$(30),S$(17),TP(17),SP(17)
1520 REM 
1530 DA=0:DS=0:SP=0
1540 REM 
1550 FOR I=1 TO 1000:NEXT I
1560 REM 
2000 REM * MENUE *
2010 GOSUB 100:AW=0
2020 PRINT "-1- ERGEBNISSE EINGEBEN"
2030 PRINT "-2- TABELLE AUSGEBEN "
2040 PRINT "-3- TABELLENAUFBAU WAEHREND DER SPIELZEIT"
2050 PRINT "-0- PROGRAMM BEENDEN"
2060 INPUT AW:IF AW=0 THEN 2130
2070 GOSUB 100
2080 IF DA=0 THEN GOSUB 7000
2090 GOSUB 100
2100 ON AW GOSUB 3000,5000,6000
2110 GOSUB 9500
2120 GOTO 2010
2130 IF DS=1 THEN GOSUB 7500
2140 GOTO 950
3000 REM * EINGABE *
3010 DS=1:IF AW$="J" THEN 3240
3020 AW$="J"
3030 PRINT "BITTE GEBEN SIE ZUERST ALLE"
3040 PRINT "VEREINSNAMEN EIN"
3050 PRINT "DURCH EINGABE VON 'HILFE' WERDEN ALLE"
3060 PRINT "BISHER GESPEICHERTEN VEREINSNAMEN "
3070 PRINT "UND ABKUERZUNGEN AUSGEGEBEN"
3080 FOR I=0 TO 17
3090 PRINT "VEREINSNAME";:INPUT V$(I)
3100 IF V$(I)="HILFE" THEN GOSUB 8000:GOTO 3090
3110 L=LEN(V$(I)):IF L<16 THEN 3150
3120 IF L=16 THEN 3180
3130 V$(I)=LEFT$(V$(I),16)
3140 PRINT "EINGABE GEKUERZT AUF :";V$(I):GOTO 3180
3150 FOR J=L TO 15
3160 V$(I)=V$(I)+" "
3170 NEXT J
3180 PRINT "ABKUERZUNG (4 BUCHSTABEN)";:INPUT V1$(I)
3190 IF V1$(I)="HILFE" THEN GOSUB 8000:GOTO 3180
3200 L=LEN(V1$(I)):IF L<>4 THEN PRINT "FALSCH":GOTO 3180
3210 TP(I)=I+1
3220 NEXT I
3230 IF AW=3 THEN RETURN
3240 REM 
3250 GOSUB 100
3260 J=-1
3270 PRINT "-=EINGABE BEENDEN"
3280 PRINT "+=LETZTE EINGABE LOESCHEN"
3290 PRINT "/=ALLE EINGABEN LOESCHEN"
3300 PRINT "HILFE=AUSGABE ALLER VEREINE UND ABKUERZUNGEN"
3310 J=J+1
3320 PRINT "1.VEREIN";:INPUT E$:GOSUB 8500
3330 IF E=2 THEN 3480
3340 IF E=1 THEN 3270
3350 VA$(J)=E$:GOTO 4000
3360 PRINT "2.VEREIN";:INPUT E$:GOSUB 8500
3370 IF E=2 THEN 3480
3380 IF E=1 THEN 3270
3390 VB$(J)=E$:GOTO 4060
3400 PRINT "TORE VEREIN ";VA$(J);:INPUT E$:GOSUB 8500
3410 IF E=2 THEN 3480
3420 IF E=1 THEN 3270
3430 T2(J)=VAL(E$)
3440 PRINT "TORE VEREIN ";VB$(J);:INPUT E$:GOSUB 8500
3450 IF E=2 THEN 3480
3460 IF E=1 THEN 3270
3470 T3(J)=VAL(E$):GOTO 3270
3480 J=J-1
3490 LL=-1
3500 LL=LL+1
3510 IF T2(LL)>T3(LL) THEN 3580
3520 IF T2(LL)<T3(LL) THEN 3630
3530 FOR K=0 TO 17
3540 IF VA$(LL)=V1$(K) THEN P(K)=P(K)+1:P1(K)=P1(K)+1
3550 IF VB$(LL)=V1$(K) THEN P(K)=P(K)+1:P1(K)=P1(K)+1
3560 NEXT K
3570 GOTO 3670
3580 FOR K=0 TO 17
3590 IF VA$(LL)=V1$(K) THEN P(K)=P(K)+2
3600 IF VB$(LL)=V1$(K) THEN P1(K)=P1(K)+2
3610 NEXT K
3620 GOTO 3670
3630 FOR K=0 TO 17
3640 IF VB$(LL)=V1$(K) THEN P(K)=P(K)+2
3650 IF VA$(LL)=V1$(K) THEN P1(K)=P1(K)+2
3660 NEXT K
3670 FOR K=0 TO 17
3680 IF VA$(LL)=V1$(K) THEN T(K)=T(K)+T2(LL)
3690 IF VA$(LL)=V1$(K) THEN T1(K)=T1(K)+T3(LL)
3700 IF VB$(LL)=V1$(K) THEN T(K)=T(K)+T3(LL)
3710 IF VB$(LL)=V1$(K) THEN T1(K)=T1(K)+T2(LL)
3720 NEXT K
3730 IF LL<=J THEN 3500
3740 RETURN
3750 REM 
4000 REM * EINGABE PRUEFEN *
4010 K=-1
4020 K=K+1
4030 IF VA$(J)=V1$(K) THEN SP(K)=SP(K)+1:GOTO 3360
4040 IF NOT (K>=17) THEN 4020
4050 PRINT "FALSCHE EINGABE":GOTO 3320
4060 K=-1
4070 K=K+1
4080 IF VB$(J)=V1$(K) THEN SP(K)=SP(K)+1:GOTO 3400
4090 IF NOT (K>=17) THEN 4070
4100 PRINT "FALSCHE EINGABE":GOTO 3360
4110 REM 
5000 REM * AUSGABE *
5010 GOSUB 9000
5020 GOSUB 100
5030 PRINT "-1- AUSGABE DRUCKER"
5040 PRINT "-2- AUSGABE MONITOR"
5050 INPUT D
5060 IF (D<1) OR (D>2) THEN 5020
5070 GOSUB 100
5080 SR$="BUNDESLIGATABELLE       SP TORE PUNKTE"
5090 IF D=1 THEN GOSUB 350:GOSUB 360
5100 FOR I=0 TO 17
5110 SR$=S$(I):PRINT SR$
5120 IF D=1 THEN GOSUB 350:GOSUB 360:GOTO 5150
5130 IF I=8 THEN PRINT "WENN WEITER, BITTE TASTE DRUECKEN"
5140 IF I=8 THEN GOSUB 210
5150 NEXT I
5160 PRINT "WENN WEITER, BITTE TASTE DRUECKEN"
5170 GOSUB 210
5180 RETURN
5190 REM 
6000 REM * TABELLENAUFBAU *
6010 GOSUB 3000
6020 GOSUB 100
6030 FOR I=0 TO 17
6040 PRINT "VEREIN ";V$(I)
6050 PRINT "KUERZEL ";V1$(I)
6060 PRINT "PLATZ ";:INPUT TP(I)
6070 PRINT "TORE GESCHOSSEN ";:INPUT T(I)
6080 PRINT "TORE ERHALTEN ";:INPUT T1(I)
6090 PRINT "GEWINNPUNKTE ";:INPUT P(I)
6100 PRINT "VERLUSTPUNKTE ";:INPUT P1(I)
6110 PRINT "SPIELTAG ";:INPUT SP(I)
6120 NEXT I
6130 GOSUB 9000
6140 GOSUB 7500
6150 RETURN
6160 REM 
7000 REM * EINLESEN *
7010 PRINT "WURDEN SCHON DATEN GESPEICHERT";:INPUT AW$
7020 DA=1:PRINT "BITTE WARTEN"
7030 AW$=LEFT$(AW$,1)
7040 IF AW$="N" THEN GOSUB 7200:GOTO 7160
7050 IF AW$<>"J" THEN 7010
7060 DN$="BUNDESLIGA"
7070 GOSUB 21200
7080 GOSUB 21500
7090 DN$=S1$:SP=VAL(S2$)
7100 FOR I=0 TO 17 STEP 2
7110 GOSUB 21500:S$(I)=S1$:S$(I+1)=S2$
7120 NEXT I
7130 GOSUB 21700
7140 PRINT "BITTE WARTEN "
7150 GOSUB 9200
7160 GOSUB 100
7170 RETURN
7180 REM 
7190 REM 
7200 FOR I=0 TO 17
7210 S$(I)=""
7220 FOR L=0 TO 39
7230 S$(I)=S$(I)+" "
7240 NEXT L
7250 NEXT I
7260 RETURN
7270 REM 
7500 REM * DATEN SPEICHERN *
7510 DS=0:GOSUB 9000
7520 GOSUB 100
7530 DN$="BUNDESLIGA":SR=SP:CT=3:CN=0:GOSUB 310
7540 S1$=DN$:S2$=SR$
7550 GOSUB 21100
7560 GOSUB 21300
7570 FOR I=0 TO 17 STEP 2
7580 S1$=S$(I):S2$=S$(I+1):GOSUB 21300
7590 NEXT I
7600 GOSUB 21700
7610 GOSUB 100
7620 RETURN
7630 REM 
8000 REM * HILFE
8010 GOSUB 100
8020 FOR L=0 TO 17
8030 PRINT V$(L)+" "+V1$(L)
8040 IF (L<>8) OR (L<>17) THEN 8070
8050 PRINT "WENN WEITER, BITTE TASTE DRUECKEN"
8060 GOSUB 210
8070 NEXT L
8075 PRINT "WENN WEITER, BITTE TASTE DRUECKEN"
8076 GOSUB 210
8080 GOSUB 100
8090 RETURN
8100 REM 
8500 REM * EDITIERHILFE *
8510 IF E$="HILFE" THEN GOSUB 8000:RETURN
8520 IF E$="-" THEN E=2:RETURN
8530 IF E$="/" THEN 8570
8540 IF E$="+" THEN 8660
8550 E=0
8560 RETURN
8570 FOR X=0 TO J
8580 VA$(X)="":VB$(X)=""
8590 FOR XX=0 TO 17
8600 IF V1$(XX)=VA$(X) THEN SP(XX)=SP(XX)-1
8610 IF V1$(XX)=VB$(X) THEN SP(XX)=SP(XX)-1
8620 NEXT XX
8630 T2(X)=0:T3(X)=0
8640 NEXT X
8650 J=-1:E=1:RETURN
8660 VA$(J)="":VB$(J)="":T2(X)=0:T3(X)=0
8670 FOR XX=0 TO 17
8680 IF V1$(XX)=VA$(X) THEN SP(XX)=SP(XX)-1
8690 IF V1$(XX)=VB$(X) THEN SP(XX)=SP(XX)-1
8700 NEXT XX
8710 J=J-1:E=1
8720 RETURN
8730 REM 
9000 REM * ZUSAMMENFUEGEN *
9010 GOSUB 100
9020 PRINT "BITTE WARTEN":CN=0
9030 FOR I=0 TO 17
9040 S$(I)=""
9050 CT=2:SR=TP(I):GOSUB 310:S$(I)=SR$+"."
9060 S$(I)=S$(I)+V$(I)+" "+V1$(I)
9070 CT=3:SR=SP(I):GOSUB 310:S$(I)=S$(I)+SR$+" "
9080 CT=2:SR=T(I):GOSUB 310:S$(I)=S$(I)+SR$+":"
9090 SR=T1(I):GOSUB 310:S$(I)=S$(I)+SR$+" "
9100 SR=P(I):GOSUB 310:S$(I)=S$(I)+SR$+":"
9110 SR=P1(I):GOSUB 310:S$(I)=S$(I)+SR$
9120 NEXT I
9130 RETURN
9140 REM 
9200 REM * ENTSCHLUESSELN *
9210 FOR I=0 TO 17
9220 TP(I)=VAL(LEFT$(S$(I),2))
9230 V$(I)=MID$(S$(I),4,16)
9240 V1$(I)=MID$(S$(I),21,4)
9250 SP(I)=VAL(MID$(S$(I),25,3))
9260 T(I)=VAL(MID$(S$(I),29,2))
9270 T1(I)=VAL(MID$(S$(I),32,2))
9280 P(I)=VAL(MID$(S$(I),35,2))
9290 P1(I)=VAL(RIGHT$(S$(I),2))
9300 NEXT I
9310 RETURN
9320 REM 
9500 REM * SORTIEREN *
9510 PRINT "BITTE WARTEN"
9520 FOR I=1 TO 17
9530 FOR L=17 TO I STEP -1
9540 IF P(L)<P(L-1) THEN 9620
9550 IF P(L)>P(L-1) THEN 9630
9560 IF P1(L)>P1(L-1) THEN 9620
9570 IF P1(L)<P1(L-1) THEN 9630
9580 IF (T(L)-T1(L))<(T(L-1)-T1(L-1)) THEN 9620
9590 IF (T(L)-T1(L))>(T(L-1)-T1(L-1)) THEN 9630
9600 IF T(L)<T(L-1) THEN 9620
9610 IF T(L)>T(L-1) THEN 9630
9620 GOTO 9700
9630 TP=L:V$=V$(L):V1$=V1$(L):T=T(L):T1=T1(L):SP=SP(L)
9640 P=P(L):P1=P1(L):TP(L)=L+1:V$(L)=V$(L-1):SP(L)=SP(L-1)
9650 V1$(L)=V1$(L-1):T(L)=T(L-1):T1(L)=T1(L-1)
9660 P(L)=P(L-1):P1(L)=P1(L-1):V1$(L-1)=V1$
9670 TP(L-1)=TP:V$(L-1)=V$:T(L-1)=T
9680 T1(L-1)=T1:P(L-1)=P
9690 P1(L-1)=P1:SP(L-1)=SP
9700 NEXT L
9710 NEXT I
9720 GOSUB 100
9730 RETURN
9740 REM 
21000 REM DATEIVERWALTUNG
21010 REM  GILT FUER C64
21100 REM  DATEI OEFFNEN ZUM SCHREIBEN
21110 REM OPEN 1,1,1,DN$:RETURN
21200 REM  DATEI OEFFNEN ZUM LESEN
21210 REM OPEN 1,1,0,DN$:RETURN
21300 QP$=S1$:GOSUB 21400:S1$=QZ$:QP$=S2$:GOSUB 21400
21310 S2$=QZ$
21320 REM DATEN (S1$,S2$) AUF KASSETTE SCHREIBEN
21330 REM PRINT#1,S1$:REM PRINT#1,S2$:RETURN
21400 REM UMWANDELN VON DOPPELPUNKT,KOMMA
21410 REM ANFUEHRUNGSZEICHEN USW.
21420 QZ$="":IF QP$="" THEN QP$=" "
21430 FOR QP=1 TO LEN(QP$)
21440 QQ$=MID$(QP$,QP,1):IF QQ$=" " THEN QQ$=CHR$(252)
21450 IF QQ$="," THEN QQ$=CHR$(250)
21460 IF QQ$=CHR$(34) THEN QQ$=CHR$(253)
21470 IF QQ$=":" THEN QQ$=CHR$(251)
21480 QZ$=QZ$+QQ$:NEXT QP:RETURN
21500 REM  DATEN (S1$,S2$) VON KASSETTE LESEN
21510 REM INPUT#1,S1$:REM INPUT#1,S2$
21520 QP$=S1$:GOSUB 21600:S1$=QZ$:QP$=S2$
21530 GOSUB 21600:S2$=QZ$:RETURN
21600 REM UMWANDELN VON DOPPELPUNKT,KOMMA
21610 REM ANFUEHRUNGSZEICHEN USW.
21620 QZ$="":IF OP$="" THEN OP$=" "
21630 FOR QP=1 TO LEN(QP$)
21640 QQ$=MID$(QP$,QP,1):IF QQ$=CHR$(252) THEN QQ$=" "
21650 IF QQ$=CHR$(250) THEN QQ$=","
21660 IF QQ$=CHR$(251) THEN QQ$=":"
21670 IF QQ$=CHR$(253) THEN QQ$=CHR$(34)
21680 QZ$=QZ$+QQ$
21690 NEXT QP:RETURN
21700 REM  DATEI SCHLIESSEN
21710 REM CLOSE 1:RETURN
30000 REM DIE SORTIERUNG DER TABELLE ERFOLGT
30010 REM ZUNAECHST NACH GEWINNPUNKTEN.
30020 REM SIND DIESE GLEICH, WERDEN DIE VER-
30030 REM LUSTPUNKTE VERGLICHEN. SIND DIESE
30040 REM EBENFALLS GLEICH, DANN ENTSCHEIDET
30050 REM DIE TORDIFFERENZ.
30060 REM 

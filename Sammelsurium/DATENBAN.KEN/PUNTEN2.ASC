1000 A=3000:GOTO 20:REM PUNTENBOEKJE
1010 HT=HO:VT=VE
1020 LM=32:REM maximum aantal leerlingen
1030 PM=10:REM maximum aantal punten
1040 VS=-1:AP=0:AL=0:SL$="":BL$=""
1050 FOR I=1 TO HT:SL$=SL$+"-":BL$=BL$+" ":NEXT I
1070 DIM N$(LM),P(LM+1,PM),SD(PM),K$(5)
1080 GOSUB 100
1090 PRINT"Hoe is uw 3-lettercode ";:INPUT DC$
1100 IF LEN(DC$)<>3 THEN 1090
1110 GOSUB 100
1120 SR$="P U N T E N B O E K J E":GOSUB 150
1130 PRINT:PRINT:PRINT"M E N U":PRINT 
1140 PRINT:PRINT" 1 Oude gegevens opnieuw inlezen"
1150 PRINT:PRINT" 2 Punten op beeldscherm tonen"
1160 PRINT:PRINT" 3 Nieuwe punten invoeren"
1170 PRINT:PRINT" 4 Uitvoer naar printer"
1180 PRINT:PRINT" 5 Gegevens in bestand opslaan"
1190 PRINT:PRINT" 6 Mutaties aanbrengen"
1200 PRINT:PRINT" 7 Nieuw klassebestand vormen"
1210 PRINT:PRINT" 8 Stoppen met dit programma":PRINT 
1220 PRINT:PRINT"  U KIEST ? ";:GOSUB 210:IN=IN-48
1230 IF (IN<1) OR (IN>8) THEN GOSUB 250:GOTO 1110
1240 GOSUB 100
1250 ON IN GOSUB 1510,2010,3010,3510,5010,6010,7010,8010
1260 IF VS<>2 THEN 1110
1280 GOTO 950
1290 REM ---------
1500 REM *** inlezen ***
1510 SR$="Gegevens van bestand inlezen":GOSUB 150
1520 GOSUB 5200
1530 PRINT:PRINT:PRINT"Welke klas ";:INPUT TK$
1540 LE=3:GOSUB 7580:K$(0)=LE$:PRINT 
1550 PRINT"Ik zoek ":NF$=K$(0)+"p"+DC$
1560 GOSUB 500:IF IN=0 THEN 1590
1570 GOSUB 580:PRINT:PRINT:PRINT"Onbekend !"
1580 TE$="MENU":GOTO 2520
1590 PRINT:PRINT"Ik lees"
1600 GOSUB 540:AL=VAL(IN$):GOSUB 540:AP=VAL(IN$)
1610 FOR SI=0 TO 5:GOSUB 540:K$(SI)=IN$:NEXT SI
1620 FOR L=1 TO AL+1:GOSUB 540:N$(L)=IN$:NEXT L
1630 FOR L=0 TO AL+1:FOR P=0 TO AP-1:GOSUB 540
1640 P(L,P)=VAL(IN$):NEXT P:NEXT L:VS=0:GOTO 580
1650 REM ------------
2000 REM *** tonen ***
2010 GOSUB 7440:IF LG=0 THEN RETURN
2020 KO=1:IF 3*AP+22>HT THEN GOSUB 2080
2030 FOR L=1 TO AL:IF (L-15*INT(L/15)=1) THEN GOSUB 2160
2040 GOSUB 2340:IF (L/15<>INT(L/15)) THEN 2060
2050 IF L<AL THEN TE$="volgende leerlingen":GOSUB 2520
2060 NEXT L:TE$="menukeuze":GOTO 2520
2070 REM ** vanaf **
2080 SR$="Cijfers op beeldscherm":GOSUB 150
2090 PRINT:PRINT:PRINT:PRINT"Ik heb ";AP;" punten."
2100 PRINT:PRINT"Ik kan er ";INT((HT-22)/3);
2110 PRINT" tegelijk":PRINT"tonen."
2120 PRINT:PRINT"Vanaf welk nummer wilt u ze ";:INPUT KO
2130 IF (KO<=0) OR (KO>AP) THEN 2080
2140 RETURN
2150 REM ** schermkop **
2160 GOSUB 100:KP=KO+INT((HT-25)/3)
2170 SR$=K$(0)+"  "+K$(1)+"  "+K$(2):GOSUB 150:PRINT 
2180 HO=9:VE=1:GOSUB 110:PRINT" Nummer :";:J=KO:CT=2:CN=0
2190 PRINT" ";:SR=J:GOSUB 9800
2200 J=J+1:IF (J<=AP) AND (J<=KP) THEN 2190
2210 HO=10:VE=2:GOSUB 110:PRINT" Dag   :";:J=KO
2220 PRINT" ";:SR=P(0,J-1)-100*INT(P(0,J-1)/100)
2230 GOSUB 9800:J=J+1:IF (J<=AP) AND (J<=KP) THEN 2220
2240 HO=10:VE=3:GOSUB 110:PRINT" Maand :";:J=KO
2250 PRINT" ";:SR=P(0,J-1)/100:GOSUB 9800
2260 J=J+1:IF (J<=AP) AND (J<=KP) THEN 2250
2270 HO=0:VE=4:GOSUB 110:PRINT" kolom telt voor :";:J=KO
2280 PRINT" ";:SR=P(1,J-1):GOSUB 9800
2290 J=J+1:IF (J<=AP) AND (J<=KP) THEN 2280
2300 HO=HT-1:VE=2:GOSUB 110:PRINT"G"
2310 VE=3:GOSUB 110:PRINT"e":VE=4:GOSUB 110:PRINT"m"
2320 RETURN
2330 REM ** print regel(l) **
2340 CT=2:CN=0:HO=0:VE=L+5
2350 IF VE>20 THEN VE=VE-15:GOTO 2350
2360 IF VE>10 THEN VE=VE+1:IF VE>16 THEN VE=VE+1
2370 GOSUB 110:SR=L:GOSUB 9800
2380 PRINT" ";LEFT$(N$(L),1);" ";MID$(N$(L),11,13);
2390 IF P(L+1,0)<0 THEN PRINT"   vervallen":RETURN
2400 TJ=KO:CT=3
2410 SR=P(L+1,TJ-1):SR$="  -":IF SR>0 THEN GOSUB 310
2420 PRINT SR$;
2430 TJ=TJ+1:IF (TJ<=AP) AND (TJ<=KP) THEN 2410
2440 GOSUB 120:HO=HT-3:GOSUB 110:GOSUB 2470
2450 SR=GL:GOSUB 9800:PRINT:RETURN
2460 REM ** gem van nr l **
2470 SI=0:SK=0:GL=0:FOR SJ=0 TO AP-1:IF P(L+1,SJ)<=0 THEN 2490
2480 SI=SI+P(1,SJ):SK=SK+P(1,SJ)*P(L+1,SJ)
2490 NEXT SJ:IF SI>0 THEN GL=SK/SI
2500 RETURN
2510 REM ** RETURN voor TE$ **
2520 HO=0:VE=VT:GOSUB 110:SR$="RETURN voor "+TE$:GOSUB 150
2530 GOSUB 210:IF IN<>13 THEN 2530
2540 RETURN
2550 REM ---------
3000 REM *** invoeren ***
3010 SR$="Cijfers invoeren":GOSUB 150:PRINT 
3020 GOSUB 7440:IF LG=0 THEN RETURN
3030 PRINT:PRINT"Aanwezig van klas ";K$(0)
3040 PRINT:PRINT" zijn ";AP;" punten":AP=AP+1:VS=1:PRINT 
3050 PRINT" Dit wordt dus punt nr. ";AP:PRINT:PRINT 
3060 PRINT"Datum van dit werk (MMDD) ";:INPUT P(0,AP-1)
3070 PRINT:PRINT:PRINT"gewicht ";:INPUT P(1,AP-1)
3080 GOSUB 100:PRINT:PT=0:CT=2:CN=0:FOR L=1 TO AL
3090 SR=L:GOSUB 9800:PRINT" ";LEFT$(N$(L),10);
3100 PRINT" ";MID$(N$(L),11,13);"  ";
3110 IF P(L+1,0)<0 THEN PRINT"vervallen":GOTO 3130
3120 IP=AP-1:GOSUB 8910
3130 IF L/5=INT(L/5) THEN PRINT 
3140 NEXT L:VS=1:GOSUB 250:TE$="MENU":GOSUB 2520
3150 GOTO 2530
3160 REM ----------------
3500 REM *** printen  ***
3510 GOSUB 7440:IF LG=0 THEN RETURN
3520 GOSUB 100
3530 SR$="Uitvoer op de printer":GOSUB 150:PRINT 
3540 PRINT:PRINT:PRINT" Wilt u:":PRINT 
3550 PRINT" A Een rapportlijst;":PRINT 
3560 PRINT" B Een puntenboekje-pagina;":PRINT:PRINT 
3570 PRINT" C Terug naar het hoofdmenu.":PRINT:PRINT 
3580 PRINT" Uw keuzeletter ?";
3590 GOSUB 210:PRINT IN$:IF IN<65 THEN 3520
3600 IF IN>66 THEN RETURN
3610 ON IN-64 GOSUB 3710,4010
3620 GOTO 3520
3700 REM ** rapportlijstje **
3710 GOSUB 100
3720 SR$="Rapportlijst afdrukken":GOSUB 150:PRINT:PRINT:PRINT 
3730 PRINT"U kunt kiezen uit afronding op :":PRINT:PRINT 
3740 PRINT" A gehelen,":PRINT:PRINT" B tienden,":PRINT 
3750 PRINT" C helen of het punt 5.5":PRINT:PRINT 
3760 PRINT"U kiest :";:GOSUB 210:PRINT IN$:AR=IN-64
3770 FD=170:LT=0:GOSUB 20080:KL$=K$(0):GOSUB 9210
3780 FOR L=1 TO AL:GOSUB 3800:NEXT L:GOSUB 9020:GOTO 20120
3790 REM ** print deze **
3800 CT=2:CN=0:SR=L:GOSUB 310
3810 SR$=SR$+" "+RIGHT$(N$(L),3)+" "+LEFT$(N$(L),10)+" "
3820 SR$=SR$+MID$(N$(L),11,13)
3830 GOSUB 350:IF P(L+1,0)<0 THEN SR$=" verv.":GOTO 9700
3840 GOTO 9110
4000 REM ** pagina **
4010 PRINT"Puntenboekjepagina...":FD=128:LT=1:GOSUB 20050
4020 SR$="Klas: ":GOSUB 350:DT$=K$(0):GOSUB 20010
4030 SR$="     Vak: ":GOSUB 350:DT$=K$(1):GOSUB 20010
4040 GOSUB 360:GOSUB 360:FOR L=1 TO AL:CT=2:CN=0:SR=L:GOSUB 310
4050 SR$=SR$+" "+RIGHT$(N$(L),3)+" "+LEFT$(N$(L),10)
4060 SR$=SR$+" "+MID$(N$(L),11,13)+"  ":GOSUB 350:CT=4:P=1
4070 PT=P(L+1,P-1):SR=PT:GOSUB 310:IF PT=0 THEN SR$="   -"
4080 IF PT<0 THEN SR$="-- vervallen --":GOTO 4110
4090 GOSUB 350:P=P+1:IF (P<=AP) AND (PT>=0) THEN 4070
4100 CT=5:CN=1:GOSUB 2470:SR$=" ":GOSUB 350:SR=GL/10:GOSUB 310
4110 GOSUB 9700:NEXT L:CT=4:CN=0
4120 GOSUB 360:SR$=LEFT$(BL$,18)+"Gewicht :   ":GOSUB 350
4130 FOR P=1 TO AP:SR=P(1,P-1):GOSUB 310:GOSUB 350:NEXT P
4140 GOSUB 360:SR$="         DATUM :      Dag :   ":GOSUB 350
4150 FOR P=1 TO AP:SR=P(0,P-1)-100*INT(P(0,P-1)/100):GOSUB 310
4160 GOSUB 350:NEXT P:GOSUB 360:SR$=LEFT$(BL$,20)+"Maand :   "
4170 GOSUB 350:FOR P=1 TO AP:SR=P(0,P-1)/100:GOSUB 310:GOSUB 350
4180 NEXT P:GOSUB 360:SR$="       Klassegemiddelde   :   "
4190 GOSUB 350:FOR P=1 TO AP:SN=0:MU=0:SM=0:FOR L=1 TO AL
4200 IF P(L+1,P-1)<=0 THEN 4220
4210 SN=SN+1:MU=MU+P(L+1,P-1):SM=SM+P(L+1,P-1)*P(L+1,P-1)
4220 NEXT L:IF SN>0 THEN MU=MU/SN:SD(P-1)=SQR(SM/SN-MU*MU)
4230 SR=MU:GOSUB 310:GOSUB 350:NEXT P:GOSUB 360
4240 SR$="      Standaarddeviatie   :   ":GOSUB 350
4250 FOR P=1 TO AP:SR=SD(P-1):GOSUB 310:GOSUB 350:NEXT P
4260 GOSUB 360:GOTO 20120
4270 REM --------
5000 REM *** opslaan ***
5010 SR$="Gegevens naar bestand schrijven":GOSUB 150:PRINT 
5020 GOSUB 7440:IF LG=0 THEN RETURN
5025 GOSUB 5200:REM disk of tape
5030 NF$=K$(0)+"p"+DC$:NF=NF+1:GOSUB 500:REM open uitvoer
5040 PRINT:PRINT"Ik schrijf gegevens ..."
5050 SR=AL:GOSUB 300:GOSUB 560:SR=AP:GOSUB 300:GOSUB 560
5060 FOR SI=0 TO 5:SR$=K$(SI):GOSUB 560:NEXT SI
5070 FOR L=1 TO AL+1:SR$=N$(L):GOSUB 560:NEXT L
5080 FOR L=0 TO AL+1:FOR P=0 TO AP-1:SR=P(L,P):GOSUB 300
5090 GOSUB 560:NEXT P:NEXT L:VS=0:GOTO 580
5120 REM --------
5200 PRINT:PRINT:PRINT"Wilt u een bestand op"
5210 PRINT:PRINT"1 diskette"
5220 PRINT:PRINT"2 cassette"
5230 PRINT:PRINT"keuzenummer ";:GOSUB 210:NF=IN-48:PRINT NF
5240 IF NF=1 THEN NF=4:RETURN
5250 IF NF=2 THEN NF=0:RETURN
5260 GOTO 5200
5270 REM -----------
6000 REM *** muteren ***
6010 GOSUB 7440:IF LG=0 THEN RETURN
6020 GOSUB 100:SR$="Mutaties":GOSUB 150
6030 PRINT:PRINT"U kunt kiezen uit :":PRINT 
6040 PRINT:PRINT" A Een naam verbeteren"
6050 PRINT:PRINT" B Een leerling toevoegen"
6060 PRINT:PRINT" C Een leerling weghalen"
6070 PRINT:PRINT" D Klassegegevens veranderen"
6080 PRINT:PRINT" E Een cijfer veranderen"
6090 PRINT:PRINT" F Het gemiddelde als cijfers opnemen"
6100 PRINT:PRINT" G Een kolom punten verwijderen"
6110 PRINT:PRINT" H Een gewichtfactor veranderen"
6120 PRINT:PRINT" X Terug naar het hoofdmenu"
6130 PRINT:PRINT"De letter van uw keuze is:";
6140 GOSUB 210:IN=IN-64:IF IN<=0 THEN 6140
6150 IF IN>8 THEN RETURN
6160 GOSUB 100
6170 ON IN GOSUB 6200,6240,6310,6410,6440,6610,6700,6780
6180 TE$="mutatiemenu":GOSUB 2520:GOTO 6020
6190 REM ** MUT-A **
6200 TT$="Een naam verbeteren.":PRINT TT$
6210 PRINT:PRINT"Volgnummer van de betreffende leerling  ";
6220 INPUT L:GOTO 8510
6230 REM ** MUT-B **
6240 GOSUB 100:TT$="Een leerling toevoegen;":PRINT TT$:PRINT 
6250 PRINT"Ik heb ";AL;" leerlingen. Op welk nummer"
6260 PRINT"moet een leerling worden toegevoegd ?"
6270 PRINT"Nummer ";:INPUT NT:IF NT>AL+1 THEN 6250
6280 GOSUB 8810:N$(NT)=LEFT$(BL$,19)
6290 L=NT:GOSUB 8510:FOR P=0 TO AP-1:P(NT+1,P)=0:NEXT P:RETURN
6300 REM ** weghaal **
6310 PRINT 
6320 PRINT"Ik heb ";AL;" leerlingen, welke moet er weg?"
6330 PRINT 
6340 PRINT"Nummer ";:INPUT WL:IF (WL<1) OR (WL>AL) THEN RETURN
6350 PRINT:PRINT:PRINT"Leerling ";LEFT$(N$(WL),10);
6360 PRINT" ";MID$(N$(WL),11,13):PRINT"wordt nu geschrapt."
6370 FOR L=WL TO AL-1:N$(L)=N$(L+1)
6380 FOR P=0 TO AP-1:P(L+1,P)=P(L+2,P):NEXT P
6390 NEXT L:AL=AL-1:VS=1:RETURN
6400 REM ** verklasgeg **
6410 GOSUB 100:PRINT"Veranderen van de klassegegevens;"
6420 NW=1:VS=1:GOTO 7230
6430 REM ** verbcyfer **
6440 PRINT 
6450 PRINT:PRINT"Bij welke leerling moet er veranderd"
6460 PRINT"worden ?":PRINT:PRINT"Leerling nummer ";:INPUT L
6470 IF P(L+1,0)>=0 THEN 6500
6480 PRINT:PRINT"Deze leerling is vervallen !"
6490 TE$="menu":GOTO 2520
6500 PRINT:PRINT:PRINT:PRINT:PRINT 
6510 PRINT"U wenst wijziging in het werk met"
6520 PRINT"volgnummer ";:INPUT P:IF P>AP THEN RETURN
6530 PRINT:PRINT"Cijfer van werk ";P;" is: ";
6540 IF P(L+1,P-1)=0 THEN PRINT" - ":GOTO 6560
6550 PRINT P(L+1,P-1)
6560 PRINT:PRINT:PRINT"Geef nu het nieuwe punt,"
6570 PRINT" of V als deze leerling is vervallen,"
6580 PRINT" of - als niet meegemaakt.":PRINT 
6590 PRINT"Nieuw punt is";:IP=P-1:GOSUB 8910:VS=1:RETURN
6600 REM ** gemiscyfer **
6610 PRINT"Gemiddelden als cijfers opnemen;":PRINT 
6620 PRINT"Ik reken even ...":PRINT 
6630 FOR L=1 TO AL:GOSUB 2470:P(L+1,AP)=INT(GL+.5):NEXT L
6640 AP=AP+1:PRINT:PRINT:PRINT"Dat is toegevoegd."
6650 PRINT:PRINT"Op welke datum moet deze stand worden"
6660 PRINT"genoteerd (MMDD)";:INPUT P(0,AP-1)
6670 PRINT:PRINT"Welk gewicht dient dit nieuwe punt te"
6680 PRINT"krijgen ";:INPUT P(1,AP-1):VS=1:RETURN
6690 REM ** puntweg **
6700 PRINT"Volgnummer van de te verwijderen"
6710 PRINT"punten ";:INPUT P:IF (P<1) OR (P>AP) THEN RETURN
6720 PRINT:PRINT:PRINT 
6730 PRINT"Momentje....":FOR L=0 TO AL+1:SI=P(L,0)
6740 FOR SP=P TO AP-1:P(L,SP-1)=P(L,SP):NEXT SP
6750 P(L,AP-1)=0:IF SI<0 THEN P(L,0)=-1
6760 NEXT L:AP=AP-1:VS=1:RETURN
6770 REM ** factor **
6780 PRINT"Volgnummer van het werk waar de factor"
6790 PRINT"veranderd moet worden ";:INPUT P
6800 IF (P<1) OR (P>AP) THEN RETURN
6810 P=P-1
6820 PRINT:PRINT"Die factor is nu ";P(1,P)
6830 PRINT:PRINT"en die moet worden ";:INPUT P(1,P)
6840 IF (P(1,P)<>INT(P(1,P))) OR (P(1,P)<0) THEN 6820
6850 RETURN
6860 REM -------
7000 REM *** formeer ***
7010 VS=-1:K$(0)=""
7020 SR$="Vorm een nieuw klassebestand":GOSUB 150
7030 PRINT:PRINT:PRINT" Vaste gegevens invoeren:"
7040 PRINT:PRINT:NW=0:GOSUB 7230
7050 GOSUB 100:PRINT" namen leerlingen in ";K$(0)
7060 PRINT:PRINT SL$
7070 AL=0:AP=0:P(1,0)=0
7080 FOR VE=11 TO VT-1:HO=0:GOSUB 110:PRINT BL$;:NEXT VE
7090 HO=0:VE=12:GOSUB 110:PRINT"Leerling nummer ";AL+1
7100 ND=0
7110 PRINT:PRINT"* of Voornaam";
7120 LE=10:GOSUB 7520:N$(AL+1)=LE$
7130 IF LEFT$(N$(AL+1),1)="*" THEN ND=1:GOTO 7180
7140 PRINT"  Achternaam ";
7150 LE=13:GOSUB 7520:N$(AL+1)=N$(AL+1)+LE$
7160 PRINT"        Code ";
7170 LE=3:GOSUB 7520:N$(AL+1)=N$(AL+1)+LE$
7180 PRINT:PRINT"RETURN als OK, ELK ANDER als fout";
7190 GOSUB 210:IF IN<>13 THEN 7090
7200 IF ND=0 THEN AL=AL+1:P(AL+1,0)=0:PRINT"OK":GOTO 7080
7210 VS=1:RETURN
7220 REM ** vgeg **
7230 PRINT:PRINT"Naam van de klas/groep";
7240 IF NW=0 THEN PRINT" (b.v. A1C)";
7250 UD$=K$(0):GOSUB 7480:K$(0)=TK$
7260 IF LEN(K$(0))>3 THEN 7230
7270 TK$=K$(0):LE=3:GOSUB 7580:K$(0)=LE$
7280 PRINT:PRINT"Naam docent ";
7290 UD$=K$(3):GOSUB 7480:LE=15:GOSUB 7580:K$(3)=LE$
7300 PRINT"Code docent ";
7310 UD$=K$(2):GOSUB 7480:LE=3:GOSUB 7580:K$(2)=LE$
7320 PRINT:PRINT"Code vak (2 letters)";
7330 UD$=K$(1):GOSUB 7480:K$(1)=TK$
7340 IF LEN(K$(1))<>2 THEN 7320
7350 PRINT:PRINT"Naam klassedocent ";
7360 UD$=K$(4):GOSUB 7480:LE=15:GOSUB 7580:K$(4)=LE$
7370 PRINT:PRINT"Naam adj/conrector ";
7380 UD$=K$(5):GOSUB 7480:LE=15:GOSUB 7580:K$(5)=LE$
7390 PRINT:PRINT 
7400 PRINT"Tot dusverre correct (J/N) ?";
7410 GOSUB 210:IF IN<>74 THEN GOSUB 100:GOTO 7230
7420 PRINT"J":RETURN
7430 REM ** test of leeg **
7440 IF VS>=0 THEN LG=1:RETURN
7450 PRINT:PRINT:PRINT"Er zijn geen gegevens beschikbaar !"
7460 TE$="menukeuze":LG=0:GOTO 2520
7470 REM --------
7480 GOSUB 120:PRINT"  ";UD$:GOSUB 110
7490 INPUT TK$:IF TK$="" THEN TK$=UD$
7500 RETURN
7510 REM ** naaminvoer **
7520 GOSUB 120:PRINT"   ";LEFT$(".............",LE);
7530 HO=HO+2:GOSUB 110:INPUT TK$
7540 IF RIGHT$(TK$,1)<>"." THEN 7560
7550 TK$=LEFT$(TK$,LEN(TK$)-1):GOTO 7540
7560 GOSUB 7580:GOSUB 110:PRINT"    ";TK$;"     ":RETURN
7570 REM ** maak op lengte **
7580 LE$=TK$
7590 IF LE>LEN(LE$) THEN LE$=LE$+" ":GOTO 7590
7600 IF LE<LEN(LE$) THEN LE$=LEFT$(LE$,LE)
7610 RETURN
8000 REM *** stoppen ***
8010 IF VS<1 THEN VS=2:RETURN
8020 PRINT:PRINT:PRINT" Stoppen is niet verstandig !"
8030 PRINT:PRINT"Misschien kun je beter nog iets naar"
8040 PRINT"het bestand wegschrijven !":TE$="menu":GOTO 2520
8500 REM ** naaminvoer nummer L **
8510 GOSUB 100:PRINT TT$:PRINT:PRINT:PRINT 
8520 PRINT"Leerling nummer ";L;:HO=2:VE=10
8530 VN$=LEFT$(N$(L),10):GOSUB 110:PRINT"Voornaam:   ";VN$
8540 HO=0:VE=12:GOSUB 110:AN$=MID$(N$(L),11,13)
8550 PRINT"Achternaam:   ";AN$:HO=6:VE=14:GOSUB 110
8560 CD$=RIGHT$(N$(L),3):PRINT"Code:   ";CD$:PRINT 
8570 PRINT:PRINT"Geef nu de verbeterde namen en code of"
8580 PRINT"druk op RETURN indien het onderdeel"
8590 PRINT"geen verbetering behoeft."
8600 HO=12:VE=10:GOSUB 110:INPUT TK$
8610 IF TK$<>"" THEN LE=10:GOSUB 7580:VN$=LE$
8620 VE=12:GOSUB 110:INPUT TK$
8630 IF TK$<>"" THEN LE=13:GOSUB 7580:AN$=LE$
8640 VE=14:GOSUB 110:INPUT TK$
8650 IF TK$<>"" THEN LE=3:GOSUB 7580:CD$=LE$
8660 IF CD$="-  " THEN CD$="   "
8670 N$(L)=VN$+AN$+CD$:VS=1
8680 PRINT:PRINT 
8690 PRINT"Druk op RETURN als OK, spatie als fout"
8700 PRINT BL$:PRINT BL$
8710 GOSUB 210:IF IN=13 THEN RETURN
8720 IF IN=32 THEN 8510
8730 GOTO 8710
8800 REM ** OPSCHUIVEN **
8810 FOR SL=AL TO NT STEP -1:N$(SL+1)=N$(SL)
8820 FOR SP=0 TO AP-1:P(SL+2,SP)=P(SL+1,SP):NEXT SP
8830 NEXT SL:AL=AL+1:RETURN
8900 REM ** puntin **
8910 VS=1:INPUT GG$:IF GG$="V" THEN GG=-1:GOTO 8960
8920 IF GG$="-" THEN GG=0:GOTO 8960
8930 GG=VAL(GG$):IF (GG>9) AND (GG<101) THEN 8960
8940 GOSUB 250:PRINT:PRINT"V of - of van 10 tot 100"
8950 GOTO 8910
8960 P(L+1,IP)=GG:RETURN
9000 REM ** slotregels **
9010 CT=3:CN=0:SR=AU:GOSUB 9600
9020 SR$=SL$:GOSUB 9700
9030 SR$="Conrector   : "+K$(5):GOSUB 9700
9040 SR$="Klassedocent: "+K$(4):GOSUB 9700
9050 GOSUB 360:SR$="    paraaf docent :":GOSUB 9700
9060 GOTO 9360
9100 REM ** rapportpunt van leerling l **
9110 GOSUB 2470:CN=0:CT=5:SR=GL/10
9120 IF AR=1 THEN 9600
9130 IF AR=2 THEN CN=1:GOTO 9600
9140 IF (GL>=52.5) AND (GL<57.5) THEN SR$="  5.5":GOTO 9700
9150 GOTO 9600
9200 REM ** lijstkop **
9210 DT$="   KLAS : "+KL$:GOSUB 20010:GOSUB 360
9220 GOSUB 360:SR$="Docent : "+K$(3):GOSUB 9700
9230 SR$="   Vak : "+K$(1):GOSUB 9700
9290 DT$="  R A P P O R T"
9300 GOSUB 20010:GOSUB 360
9360 SR$=SL$:GOTO 9700
9500 REM ** printer **
9600 GOSUB 310
9700 GOSUB 350:GOTO 360
9800 GOSUB 310:PRINT SR$;:RETURN
20000 REM --- double width printing ---
20010 SR$=CHR$(14):GOSUB 350:REM select double width
20020 SR$=DT$:GOSUB 350:REM print text
20030 SR$=CHR$(15):GOTO 350:REM deselect double width
20040 REM --- printer aan ---
20050 SR$=CHR$(27)+"3"+CHR$(FD/5):GOSUB 350
20060 REM select linefeed to FD/720 inch/line
20070 IF LT=0 THEN 20090
20080 SR$=CHR$(15):GOTO 350:REM condensed printing
20090 SR$=CHR$(18):GOSUB 350:REM cancel condensed
20100 SR$=CHR$(27)+"P"+CHR$(0):GOTO 350:REM select pica
20110 REM --- PRINTER UIT ---
20120 SR$=CHR$(64):GOSUB 350:REM reset printer
20130 GOSUB 360:GOSUB 360:GOTO 360
32000 REM *************************
32010 REM *                       *
32020 REM *  PUNTENBOEKJE is een  *
32030 REM *  programma van:       *
32040 REM *   J.G.A. Haubrich     *
32050 REM *   Eindhoven           *
32060 REM *                       *
32070 REM *  Copyright (C) 1987   *
32080 REM *                       *
32090 REM ***> 870420 <************
32100 REM TROS-RADIO dd 870826
32110 REM corr 7590,7600,9150

1000 A=1000:GOTO 20:REM BLUEHKALENDER
1010 REM 
1020 REM * H.+ M. FILLINGER *
1030 REM * 11.04.1984       *
1040 REM 
1050 REM 
1100 REM VARIABLENLISTE
1110 REM  A........PFLANZENART
1120 REM  A1,A1$...AUSWAHL MONAT
1130 REM  A2,A2$...AUSWAHL FARBE
1140 REM  A3.......AUSWAHL ART
1150 REM  A4.......AUSWAHL WUCHSHOEHE
1160 REM  AB$......REIHENABSTAND
1170 REM  AW,AW$...AUSWAHL
1180 REM  AZ$......PFLANZZEIT
1190 REM  B,B$.....BLUEHZEIT
1200 REM  B1$......BEDARF PFLANZEN
1210 REM  BZ$......BLUEHZEIT
1220 REM  D........HILFSVAR.DRUCKER
1230 REM  F,F$.....FARBE
1240 REM  G$.......GEMUESENAME
1250 REM  H........WUCHSHOEHE
1260 REM  I,II,J
1270 REM  JJ,K.....LAUFVARIABLE
1280 REM  L........HILFSVAR.LEN$
1290 REM  M$.......MONAT
1300 REM  N,N$,N1,
1310 REM  N1$......NAME
1320 REM  P........HILFSVAR.
1330 REM  RZ$......REIFEZEIT
1340 REM  T$(I)....TITEL
1350 REM  W,W$.....WUCHSHOEHE
1360 REM  X$.......AKTUELLER STRING
1370 REM  Z........ZEILENZAEHLER
1380 REM 
1600 REM BLUMEN-RECORD 
1610 REM NAME,NAME,ART,WUCHSHOEHE,BLUEHZEIT,FARBE
1620 REM 
1700 REM GEMUESE-RECORD
1710 REM NAME,SAMEN/QM,PFLANZEN/QM,PLANZZEIT
1720 REM ABSTAND,REIFEZEIT
1730 REM 
1800 DIM T$(3)
1900 GOSUB 100
1910 GOSUB 9900
2000 REM * MENUE *
2010 GOSUB 100
2020 PRINT "SIE WUENSCHEN ......."
2030 PRINT "-1- AUFSTELLUNG ALLER BLUMEN"
2040 PRINT "-2- AUFSTELLUNG ALLER GEMUESE"
2050 PRINT "-3- AUSKUNFT UEBER EINE BESTIMMTE"
2060 PRINT "    PFLANZE"
2070 PRINT "-4- AUSKUNFT UEBER EINE BLUME MIT"
2080 PRINT "    BESTIMMTER FARBE, BLUEHZEIT USW."
2090 PRINT "-5- EIN GEMUESE MIT BESTIMMTER"
2100 PRINT "    PFLANZZEIT"
2110 PRINT 
2120 INPUT AW
2130 IF (AW<1) OR (AW>6) THEN 2010
2140 GOSUB 100
2150 ON AW GOSUB 3000,4000,5000,6000,8000
2160 PRINT "PROGRAMM BEENDEN ";
2170 INPUT AW$
2180 IF LEFT$(AW$,1)="N" THEN 2010
2190 IF LEFT$(AW$,1)<>"J" THEN 2160
2200 GOTO 950
3000 REM * MENUETEIL -1- *
3010 GOSUB 9800:Z=0
3020 FOR I=1 TO 125
3030 SR$="NAME : "
3040 READ X$:SR$=SR$+X$+", "
3050 READ X$:SR$=SR$+X$
3060 GOSUB 9600
3070 READ A
3080 SR$="ART : ":PRINT SR$;:IF D=1 THEN GOSUB 350
3090 GOSUB 9400
3100 SR$="WUCHSHOEHE IN CM :"
3110 PRINT SR$;:IF D=1 THEN GOSUB 350
3120 READ X$
3130 SR$=X$:GOSUB 9600
3140 SR$="BLUEHZEIT :":GOSUB 9600
3150 READ X$:GOSUB 9200
3160 SR$="FARBE :":GOSUB 9600
3170 READ X$:GOSUB 9000
3180 SR$="":IF Z>1 THEN Z=13:GOSUB 9600
3190 NEXT I
3200 RESTORE
3210 GOSUB 100
3220 RETURN
3230 REM 
4000 REM * MENUETEIL -2- *
4010 FOR I=1 TO 125
4020 READ X$,X$,X,X$,X$,X$
4030 NEXT I
4040 GOSUB 9800
4050 FOR I=1 TO 22
4060 SR$="NAME : "
4070 READ X$
4080 SR$=SR$+X$
4090 GOSUB 9600
4100 SR$="BEDARF AN SAMEN /QM: "
4110 PRINT SR$;:IF D=1 THEN GOSUB 350
4120 READ X$:SR$=X$:GOSUB 9600
4130 SR$="BEDARF AN PFLANZEN /QM: "
4140 PRINT SR$;:IF D=1 THEN GOSUB 350
4150 READ X$:SR$=X$:GOSUB 9600
4160 SR$="PFLANZ-/AUSSAATZEIT :"
4170 GOSUB 9600
4180 READ X$:GOSUB 9200
4190 SR$="REIHENABSTAND IN CM : "
4200 PRINT SR$;:IF D=1 THEN GOSUB 350
4210 READ X$:SR$=X$
4220 GOSUB 9600
4230 SR$="ZEIT V.D. AUSSAAT BIS ZUR"
4240 GOSUB 9600
4250 SR$="ERNTE IN TAGEN          :"
4260 PRINT SR$;:IF D=1 THEN GOSUB 350
4270 READ X$:SR$=X$:GOSUB 9600
4280 SR$="":IF Z>1 THEN Z=13:GOSUB 9600
4290 NEXT I
4300 RESTORE
4310 GOSUB 100
4320 RETURN
4330 REM 
5000 REM * MENUETEIL 3 *
5010 Z=0:GOSUB 9800
5020 PRINT "ES HANDELT SICH UM ....."
5030 PRINT "-1- EINE BLUME"
5040 PRINT "-2- EINE GEMUESEPFLANZE"
5050 AW=0:INPUT AW
5060 IF (AW<1) OR (AW>2) THEN 5020
5070 GOSUB 100
5080 IF AW=2 THEN 5520
5090 AW=0
5100 PRINT "BITTE NAME DER GEWUENSCHTEN"
5110 PRINT "BLUME EINGEBEN :";
5120 INPUT N$
5130 N=0
5140 N=N+1
5150 READ X$:IF X$=N$ THEN P=1:GOTO 5290
5160 READ X$,X,X$,X$,X$
5170 IF NOT (N>=125) THEN 5140
5180 RESTORE
5190 N1=0
5200 N1=N1+1
5210 READ X$,X$
5220 IF X$=N$ THEN P=2:GOTO 5290
5230 READ X,X$,X$,X$
5240 IF NOT (N1>=125) THEN 5200
5250 RESTORE
5260 PRINT "ENDE DER AUSGABE"
5270 SR$="":GOSUB 9600
5280 RETURN
5290 IF P=1 THEN AL=N-1
5300 IF P=2 THEN AL=N1-1
5310 RESTORE
5320 FOR K=1 TO AL
5330 READ X$,X$,X,X$,X$,X$
5340 NEXT K
5350 READ X$
5360 SR$="NAME : "+X$+", "
5370 READ X$:SR$=SR$+X$
5380 GOSUB 9600
5390 READ A
5400 SR$="ART : ":PRINT SR$;:IF D=1 THEN GOSUB 350
5410 GOSUB 9400
5420 SR$="WUCHSHOEHE IN CM : "
5430 PRINT SR$;:IF D=1 THEN GOSUB 350
5440 READ X$:SR$=X$:GOSUB 9600
5450 SR$="BLUEHZEIT :":GOSUB 9600
5460 READ X$:GOSUB 9200
5470 SR$="FARBE :":GOSUB 9600
5480 READ X$:GOSUB 9000
5490 SR$="":IF Z>1 THEN Z=13:GOSUB 9600
5500 IF P=1 GOTO 5170
5510 GOTO 5240
5520 PRINT "BITTE NAMEN DER GESUCHTEN"
5530 PRINT "GEMUESEPFLANZE EINGEBEN";
5540 INPUT N$
5550 FOR I=1 TO 125
5560 READ X$,X$,X,X$,X$,X$
5570 NEXT I
5580 N=0
5590 N=N+1
5600 READ X$
5610 IF X$=N$ THEN GOTO 5660
5620 READ X$,X$,X$,X$,X$
5630 IF NOT (N>=22) THEN 5590
5640 PRINT "LEIDER NICHT GESPEICHERT":RESTORE
5650 Z=13:SR$="":GOSUB 9600:RETURN
5660 SR$="NAME : "+X$:GOSUB 9600
5670 SR$="BEDARF AN SAMEN /QM : "
5680 READ X$:SR$=SR$+X$:GOSUB 9600
5690 SR$="BEDARF AN PFLANZEN /QM : "
5700 READ X$:SR$=SR$+X$:GOSUB 9600
5710 SR$="PFLANZ-/AUSSAATZEIT :"
5720 GOSUB 9600:READ X$:GOSUB 9200
5730 SR$="REIHENABSTAND IN CM : "
5740 READ X$:SR$=SR$+X$:GOSUB 9600
5750 SR$="ZEIT V.D. AUSSAAT BIS ZUR"
5760 GOSUB 9600
5770 SR$="ERNTE IN TAGEN          :"
5780 READ X$:SR$=SR$+X$:GOSUB 9600
5790 SR$="":IF Z>1 THEN Z=13:GOSUB 9600
5800 RESTORE
5810 RETURN
5820 REM 
6000 REM * MENUETEIL -4- *
6010 GOSUB 9800
6020 PRINT "BITTE WAEHLEN SIE DIE BLUEHZEIT"
6030 PRINT "-0- KEINE BESTIMMTE"
6040 PRINT "-1- JANUAR"
6050 PRINT "-2- FEBRUAR"
6060 PRINT "-3- MAERZ"
6070 PRINT "-4- APRIL"
6080 PRINT "-5- MAI"
6090 PRINT "-6- JUNI"
6100 PRINT "-7- JULI"
6110 PRINT "-8- AUGUST"
6120 PRINT "-9- SEPTEMBER"
6130 PRINT "-10- OKTOBER"
6140 PRINT "-11- NOVEMBER"
6150 PRINT "-12- DEZEMBER"
6160 INPUT A1
6170 IF (A1<0) OR (A1>12) THEN 6020
6180 GOSUB 100
6190 IF A1=0 THEN A1$="C"
6200 IF A1=11 THEN A1$="A"
6210 IF A1=12 THEN A1$="B"
6220 IF (A1=0) OR (A1=11) OR (A1=12) THEN 6250
6230 A1=A1-1:SR=A1:GOSUB 300
6240 A1$=SR$
6250 PRINT "BITTE FARBE WAEHLEN"
6260 PRINT "-0- KEINE BESTIMMTE"
6270 PRINT "-1- BLAU"
6280 PRINT "-2- ROSA"
6290 PRINT "-3- ROT"
6300 PRINT "-4- WEISS"
6310 PRINT "-5- VERSCHIEDENE"
6320 PRINT "-6- GELB"
6330 PRINT "-7- FLIEDERFARBEN"
6340 PRINT "-8- UNSCHEINBAR"
6350 PRINT "-9- GELB M.ROT U.BRAUN"
6360 PRINT "-10- GESTREIFT"
6370 PRINT "-11- BRAUN"
6380 PRINT "-12- GOLDGELB"
6390 PRINT "-13- ORANGE"
6400 INPUT A2
6410 IF (A2<0) OR (A2>13) THEN 6250
6420 IF A2=0 THEN A2$="D"
6430 IF A2=11 THEN A2$="A"
6440 IF A2=12 THEN A2$="B"
6450 IF A2=13 THEN A2$="C"
6460 IF A2=0 OR A2>10 THEN 6490
6470 A2=A2-1:SR=A2:GOSUB 300
6480 A2$=SR$
6490 GOSUB 100
6500 PRINT "BITTE WAEHLEN SIE DIE PFLANZENART"
6510 PRINT "-0- KEINE BESTIMMTE"
6520 PRINT "-1- EINJAEHRIG OHNE ANZUCHT"
6530 PRINT "-2- EINJAEHRIG MIT ANZUCHT"
6540 PRINT "-3- MEHRJAEHRIGE STAUDE"
6550 INPUT A3
6560 IF (A3<0) OR (A3>3) THEN 6500
6570 GOSUB 100
6580 PRINT "BITTE GEBEN SIE DIE MAXIMALE"
6590 PRINT "WUCHSHOEHE IN CM EIN - FALLS"
6600 PRINT "GLEICHGUELTIG BITTE 0 EINGEBEN"
6610 INPUT A4
6620 GOSUB 100
6630 PRINT "BITTE WARTEN":PRINT 
6640 FOR I=1 TO 125
6650 READ N$,N1$,A,W$,BZ$,F$
6660 IF A3=0 THEN GOTO 6690
6670 IF A3=ATHEN GOTO 6690
6680 GOTO 7030
6690 B=LEN(BZ$)
6700 II=0
6710 II=II+1
6720 B$=MID$(BZ$,II,1)
6730 IF A1$="C" THEN 6770
6740 IF A1$=B$ THEN 6770
6750 IF NOT (II>B) THEN 6710
6760 GOTO 7030
6770 F=LEN(F$)
6780 II=0
6790 II=II+1
6800 B$=MID$(F$,II,1)
6810 IF A2$="D" THEN GOTO 6850
6820 IF A2$=B$ THEN GOTO 6850
6830 IF NOT (II>F) THEN 6790
6840 GOTO 7030
6850 W=LEN(W$)
6860 FOR II=1 TO W
6870 IF MID$(W$,II,1)="-" THEN W$=MID$(W$,II+1,5)
6880 NEXT II
6890 H=VAL(W$):H=ABS(H):A4=ABS(A4)
6900 IF H<=A4 THEN GOTO 6930
6910 IF A4=0 THEN GOTO 6930
6920 GOTO 7030
6930 SR$="NAME : "+N$+", "+N1$
6940 GOSUB 9600
6950 SR$="ART : ":PRINT SR$;:IF D=1 THEN GOSUB 350
6960 GOSUB 9400
6970 SR$="WUCHSHOEHE IN CM : "+W$
6980 GOSUB 9600
6990 SR$="BLUEHZEIT :":GOSUB 9600
7000 X$=BZ$:GOSUB 9200
7010 SR$="FARBE :":GOSUB 9600:X$=F$:GOSUB 9000
7020 SR$="":IF Z>1 THEN Z=13:GOSUB 9600
7030 NEXT I
7040 RESTORE
7050 PRINT "AUSGABE BEENDET"
7060 RETURN
7070 REM 
8000 REM * MENUETEIL 5 *
8010 GOSUB 9800
8020 PRINT "BITTE MONAT WAEHLEN"
8030 INPUT A1
8040 IF (A1<1) OR (A1>12) THEN 8020
8050 GOSUB 100
8060 PRINT "BITTE WARTEN"
8070 IF A1=11 THEN A1$="A":GOTO 8100
8080 IF A1=12 THEN A1$="B":GOTO 8100
8090 A1=A1-1:SR=A1:GOSUB 300:A1$=SR$
8100 FOR I=1 TO 125
8110 READ X$,X$,X,X$,X$,X$
8120 NEXT I
8130 FOR I=1 TO 22
8140 READ G$,B$,B1$,AZ$,AB$,RZ$
8150 L=LEN(AZ$)
8160 II=0
8170 II=II+1
8180 IF MID$(AZ$,II,1)=A1$ THEN 8210
8190 IF NOT (II>L) THEN 8170
8200 GOTO 8300
8210 SR$="NAME : "+G$:GOSUB 9600
8220 SR$="BEDARF AN SAMEN /QM : "+B$:GOSUB 9600
8230 SR$="BEDARF AN PFLANZEN /QM : "+B1$:GOSUB 9600
8240 SR$="PFLANZ-/AUSSAATZEIT :":GOSUB 9600
8250 X$=AZ$:GOSUB 9200
8260 SR$="REIHENABSTAND IN CM : "+AB$:GOSUB 9600
8270 SR$="ZEIT V.D. AUSSAAT BIS":GOSUB 9600
8280 SR$="ZUR ERNTE IN TAGEN  : "+RZ$:GOSUB 9600
8290 SR$="":IF Z>1 THEN Z=13:GOSUB 9600
8300 NEXT I
8310 PRINT "AUSGABE BEENDET"
8320 RESTORE
8330 RETURN
8340 REM 
9000 REM * ENTSCHLUESSELN FARBE *
9010 L=LEN(X$)
9020 FOR II=1 TO L
9030 F$=MID$(X$,II,1)
9040 IF F$="0" THEN SR$="BLAU"
9050 IF F$="1" THEN SR$="ROSA"
9060 IF F$="2" THEN SR$="ROT"
9070 IF F$="3" THEN SR$="WEISS"
9080 IF F$="4" THEN SR$="VERSCHIEDENE"
9090 IF F$="5" THEN SR$="GELB"
9100 IF F$="6" THEN SR$="FLIEDERFARBEN"
9110 IF F$="7" THEN SR$="UNSCHEINBAR"
9120 IF F$="8" THEN SR$="GELB M. ROT U. BRAUN"
9130 IF F$="9" THEN SR$="GESTREIFT"
9140 IF F$="A" THEN SR$="BRAUN"
9150 IF F$="B" THEN SR$="GOLDGELB"
9160 IF F$="C" THEN SR$="ORANGE"
9170 GOSUB 9600
9180 NEXT II
9190 RETURN
9200 REM * ENTSCHLUESSELN BLUEHZEIT *
9210 L=LEN(X$):IF X$="" THEN SR$="":GOSUB 9600
9220 FOR II=1 TO L
9230 M$=MID$(X$,II,1)
9240 IF M$="0" THEN SR$="JANUAR"
9250 IF M$="1" THEN SR$="FEBRUAR"
9260 IF M$="2" THEN SR$="MAERZ"
9270 IF M$="3" THEN SR$="APRIL"
9280 IF M$="4" THEN SR$="MAI"
9290 IF M$="5" THEN SR$="JUNI"
9300 IF M$="6" THEN SR$="JULI"
9310 IF M$="7" THEN SR$="AUGUST"
9320 IF M$="8" THEN SR$="SEPTEMBER"
9330 IF M$="9" THEN SR$="OKTOBER"
9340 IF M$="A" THEN SR$="NOVEMBER"
9350 IF M$="B" THEN SR$="DEZEMBER"
9360 GOSUB 9600
9370 NEXT II
9380 RETURN
9390 REM 
9400 REM * ENTSCHLUESSELN ART *
9410 ON A GOTO 9420,9470,9540
9420 SR$="EINJAEHRIG OHNE ANZUCHT"
9430 GOSUB 9600
9440 SR$="AUSSAAT : APRIL-MAI"
9450 GOSUB 9600
9460 RETURN
9470 SR$="EINJAEHRIG MIT ANZUCHT"
9480 GOSUB 9600
9490 SR$="AUSSAAT : MAERZ-APRIL"
9500 GOSUB 9600
9510 SR$="FREILAND : MITTE MAI "
9520 GOSUB 9600
9530 RETURN
9540 SR$="STAUDE":GOSUB 9600
9550 SR$="PFLANZZEIT : MAERZ-MAI,AUG.-OKT."
9560 GOSUB 9600
9570 RETURN
9580 GOSUB 9600
9590 RETURN
9600 REM * AUSGABE/ZEILENZAEHLER *
9610 Z=Z+1
9620 PRINT SR$
9630 IF D=1 THEN 9700
9640 J=Z/14:J1=INT(Z/14)
9650 IF J<>J1 THEN RETURN
9660 PRINT "WENN WEITER, BITTE TASTE DRUECKEN !";
9670 GOSUB 210:GOSUB 120:HO=0:GOSUB 110
9680 FOR J=0 TO 36:PRINT " ";:NEXT J
9690 PRINT :Z=0:RETURN
9700 GOSUB 350
9710 GOSUB 360
9720 RETURN
9730 REM 
9800 REM * DRUCKER/BILDSCHIRM *
9810 PRINT "-1- AUSGABE DRUCKER "
9820 PRINT "-2- AUSGABE MONITOR "
9830 AW=0:INPUT AW
9840 IF (AW<1) OR (AW>2) THEN 9810
9850 D=0:IF AW=1 THEN D=1
9860 AW=0
9870 GOSUB 100
9880 RETURN
9890 REM 
9900 REM * TITEL *
9910 GOSUB 100:PRINT :PRINT :PRINT :PRINT 
9920 PRINT "      BLUEH- UND"
9930 PRINT "    PFLANZKALENDER"
9940 PRINT :PRINT :PRINT :PRINT 
9950 PRINT " CO. H.& M.FILLINGER"
9960 PRINT "    MCMLXXXIV"
9970 PRINT :PRINT 
9980 FOR I=1 TO 2000:NEXT I
9990 GOSUB 100:RETURN
25000 REM * PFLANZENDATEI *
25010 REM * BLUMEN *
25020 DATA "LEBERBALSAM","AGERATUM",2,"10-20","58","0"
25030 DATA "MALVE","ALCEA",2,"200-250","78","123"
25040 DATA "STOCKROSE","ALCEA",2,"200-250","78","123"
25050 DATA "LOEWENMAEULCHEN","ANTIRRHINUM",2,"20-70"
25060 DATA "56789","4"
25070 DATA "SOMMERASTER","CALLISTEPHUS",2,"20-90","6789","4"
25080 DATA "HAHNENKAMM","CELOSIA",2,"30-40","6789","25"
25090 DATA "SPINNENPFLANZE","CLEOME",2,"120-150","6789","136"
25100 DATA "SCHMUCKKOERBCHEN","COSMOS",2,"-130","56789"
25110 DATA "1236"
25120 DATA "CHINESER-NELKE","DIANTHUS CHINENSIS",2,"20-40"
25130 DATA "78"
25140 DATA "4","BUNTE WOLFSMILCH","EUPHORBIA MARGINATA",2
25150 DATA "60-80","67","7","GAZANIE","GAZANIA",2,"10-30"
25160 DATA "6789","4","BALSAMINE","IMPATIENS BALSAMINA",2
25170 DATA "20-60","567","4","MAENNERTREU","LOBELIA",2
25180 DATA "10-25"
25190 DATA "45678","03","SOMMERLEVKOJE","MATTHIOLA"
25200 DATA 2,"20-60"
25210 DATA "567","4","GAUKLERBLUME","MIMULUS",2,"40-60"
25220 DATA "56789"
25230 DATA "8","WUNDERBLUME","MIRABILIS",2,"40-70","678"
25240 DATA "2359"
25250 DATA "ZIERTABAK","NICOTIANA",2,"60-80","56789","23"
25260 DATA "BARTFADEN","PENTSTEMON",2,"30-40","6789","4"
25270 DATA "PHLOX","PHLOX DRUMMONDII",2,"30-50","5678","4"
25280 DATA "WUNDERBAUM","RICINUS",2,"200-300","789","7"
25290 DATA "SONNENHUT","RUDBECKIA",2,"70-80","6789","5A"
25300 DATA "BROKATBLUME","SALPIGLOSIS",2,"60-80","5678","4"
25310 DATA "SANVITALIE","SANVITALIA",2,"10-15","56789","B"
25320 DATA "ASCHENPFLANZE","SENECIO",2,"30-40","","7"
25330 DATA "STUDENTENBLUME","TAGETES",2,"50-80","6789","5C"
25340 DATA "EISENKRAUT","VERBENA",2,"30-40","56789","4"
25350 DATA "ZINNIE","ZINNIA",2,"80-100","6789","4"
25360 DATA "SCHAFGARBE","ACHILLEA",3,"60-100","45","B"
25370 DATA "EISENHUT","ACONITUM",3,"120-150","4567","0"
25380 DATA "MALVE","ALCEA",3,"200","56","4","JAPAN-ANEMONE"
25390 DATA "ANEMONE-JAPANICA-HYBRIDEN",3,"40-70","678","123"
25400 DATA "AKELEI","AQUILEGIA",3,"40-70","45678","4"
25410 DATA "HERBSTASTER","",3,"60-80","6789A","02"
25420 DATA "ASTILBE","",3,"50-80","345","123"
25430 DATA "GLOCKENBLUME","CAMPANULA",3,"40-100","4567","036"
25440 DATA "KORNBLUME","CENTAUREA",3,"40-80","4567","02"
25450 DATA "WINTERASTER","CHRYSANTHEMUM",3,"60-80","89A","4"
25460 DATA "MARGERITE","CHRYSANTHEMUM MAXIMUM",3,"70-100"
25470 DATA "678"
25480 DATA "3","SILBERKERZE","CIMICIFUGA",3,"80-100","3456"
25490 DATA "3"
25500 DATA "MAEDCHENAUGE","COREOPSIS",3,"60-100","45678","5"
25510 DATA "RITTERSPORN","DELPHINIUM",3,"80-150","45678","0"
25520 DATA "TRAENENDES HERZ","DICENTRA",3,"60","23","1"
25530 DATA "FINGERHUT","DIGITALIS",3,"60-100","456","12"
25540 DATA "GEMSWURZ","DORONICUM",3,"40","23","5"
25550 DATA "FEINSTRAHLASTER","ERIGERON",3,"60-70","4567","0"
25560 DATA "KOKARDENBLUME","GAILLARDIA",3,"70","45678","C"
25570 DATA "NELKENWURZ","GEUM",3,"40","5678","C"
25580 DATA "SCHLEIERKRAUT","GYPSOPHILA",3,"70-120","4567","3"
25590 DATA "SONNENBRAUT","HELENIUM",3,"50-100","5678","A"
25600 DATA "SONNENBLUME","HELIANTHUS",3,"120-200","45678","5"
25610 DATA "SONNENAUGE","HELIOPSIS",3,"100","45678","B"
25620 DATA "TAGLILIE","HEMEROCALLIS",3,"50-70","456","5A"
25630 DATA "PURPURGLOECKCHEN","HEUCHERA",3,"50","3456","2"
25640 DATA "HERZLILIE","HOSTA",3,"60","34","06"
25650 DATA "SCHWERTLILIE"
25660 DATA "IRIS",3,"50-90","45","4","FACKELLILIE"
25670 DATA "KNIPHOFIA"
25680 DATA 3,"50-80","4567","C","PRACHTSCHARTE","LIATRIS",3
25690 DATA "80","678","6","LUPINE","LUPINUS",3,"70-100"
25700 DATA "45678"
25710 DATA "4","PFINGSTROSE","PAEONIA",3,"60","45","123"
25720 DATA "TUERKENMOHN","PAPAVER",3,"50-80","4567","2"
25730 DATA "FLAMMENBLUME","PHLOX PANICULATA HYBRIDEN"
25740 DATA 3,"40-80"
25750 DATA "5678","4","BUNTE MARGERITE","PYRETHRUM",3,"50-80"
25760 DATA "45678","4","SONNENHUT","RUDBECKIA",3,"80-200"
25770 DATA "56789","B","GOLDRAUTE","SOLIDAGO",3,"50-60"
25780 DATA "56789"
25790 DATA "5","TROLLBLUME","TROLLIUS",3,"50","234","5C"
25800 DATA "KOENIGSKERZE","VERBASCUM",3,"50-200","5678","5"
25810 DATA "EHRENPREIS","VERONICA",3,"60","567","0"
25820 DATA "PALMLILIE"
25830 DATA "YUCCA",3,"150","45","3","ZWERGSCHAFGARBE"
25840 DATA "ACHILLEA"
25850 DATA 3,"20","567","3B","GUENSEL","AJUGA",3,"10","45"
25860 DATA "0A"
25870 DATA "STEINKRAUT","ALYSSUM",3,"20","23","B"
25880 DATA "KATZENPFOETCHEN","ANTENNARIA",3,"15","45","3"
25890 DATA "GAENSEKRESSE","ARABIS",3,"20","234","3"
25900 DATA "GRASNELKE","ARMERIA",3,"15","345","1"
25910 DATA "BLAUKISSEN"
25920 DATA "AUBRIETIA",3,"10","234","0126","ALPENASTER"
25930 DATA "ASTER ALPINUS",3,"20","456","0","KISSENASTER"
25940 DATA "ASTER DUMOSUS",3,"25-50","89","013"
25950 DATA "GLOCKENBLUME"
25960 DATA "CAMPANULA",3,"20","4567","036","HORNKRAUT"
25970 DATA "CERASTIUM",3,"20","456","3","HEIDENELKE"
25980 DATA "DIANTHUS DELTOIDES",3,"30","456","1"
25990 DATA "FEDERNELKE"
26000 DATA "DIANTHUS CAESIUS",3,"30","567","4","WOLFSMILCH"
26010 DATA "EUPHORBIA",3,"20","456","B","ENZIAN","GENTIANA"
26020 DATA 3
26030 DATA "20","34","0","ZWERGSCHLEIERKRAUT","GYPSOPHILA",3
26040 DATA "20","456","13","SONNENROESCHEN","HELIANTHEMUM",3
26050 DATA "25","4567","235","CHRISTROSE","HELLEBORUS",3
26060 DATA "30"
26070 DATA "B012","3","LEBERBLUEMCHEN","HEPATICA",3,"10"
26080 DATA "34"
26090 DATA "0","SCHLEIFENBLUME","IBERIS",3,"20","34","3"
26100 DATA "ZWERGSCHWERTLILIE","IRIS-PUMILA-HYBRIDEN",3
26110 DATA "10-20"
26120 DATA "23","035","KATZENMINZE","NEPETA",3,"30","4567"
26130 DATA "6"
26140 DATA "NACHTKERZE","OENOTHERA",3,"25","56","5"
26150 DATA "ISLANDMOHN","PAPAVER NUDICAULE",3,"15","456"
26160 DATA "35C"
26170 DATA "POLSTERPHLOX","PHLOX SUBULATA",3,"10","234","4"
26180 DATA "PRIMELN","",3,"15-25","234","4","STERNMOOS"
26190 DATA "SAGINA",3,"5","345","3","STEINBRECH"
26200 DATA "SAXIFRAGA",3
26210 DATA "10","2345","135","FETTHENNE","SEDUM",3,"5-15"
26220 DATA "45"
26230 DATA "4","HAUSWURZ","SEMPERVIVUM",3,"10","45","25"
26240 DATA "EHRENPREIS","VERONICA",3,"20","34","0"
26250 DATA "VEILCHEN"
26260 DATA "VIOLA",3,"15","234","04","ADONISROESCHEN"
26270 DATA "ADONIS AESTIVALIS",1,"30-40","23456","2"
26280 DATA "RINGELBLUME","CALENDULA",1,"40-60","567","5C"
26290 DATA "KORNBLUME","CENTAUREA",1,"60-80","678","01235"
26300 DATA "EINJAEHR.MARGERITE","CHRYSANTHEMUM CARINATUM",1
26310 DATA "50-60","6789","34","SOMMERFUCHSIE","CLARKIA",1
26320 DATA "40-60","678","123","EINJAEHR.RITTERSPORN"
26330 DATA "DELPHINIUM AJACIS",1,"80-100","678","013"
26340 DATA "KAPRINGELBLUME","DIMORPHOTECA",1,"15-30"
26350 DATA "45678"
26360 DATA "3C","GOLDMOHN","ESCHSCHOLTZIA",1,"50-60"
26370 DATA "456789"
26380 DATA "5C","SOMMERZYPRESSE","KOCHIA",1,"60-80","","7"
26390 DATA "EINJAHRS-KOKARDENBLUME","GAILLARDIA",1,"70-100"
26400 DATA "6789","25A","SOMMERAZALEE","GODETIA",1,"60"
26410 DATA "5678"
26420 DATA "235","EINJAEHR.SCHLEIERKRAUT","GYPSOPHILA",1
26430 DATA "30-40","567","3","SONNENBLUME","HELIANTHUS",1
26440 DATA "50-250","678","5","EINJAEHR.SCHLEIFENBLUME"
26450 DATA "IBERIS",1,"15-25","56","312","ROTER LEIN"
26460 DATA "LINUM",1
26470 DATA "30-40","567","2","EINJAEHR.LUPINE","LUPINUS",1
26480 DATA "60-70","5678","4","TRICHTERMALVE","MALOPE",1
26490 DATA "60-100","678","123","JUNGFER IM GRUENEN"
26500 DATA "NIGELLA",1,"35-40","5678","013","EINJAEHR.MOHN"
26510 DATA "PAPAVER",1,"50-60","56","2","RESEDA","",1
26520 DATA "20-30","5678","235","WITWENBLUME","SCABIOSA"
26530 DATA 1,"35-40","6789","0123","ZWERG-KAPUZINERKRESSE"
26540 DATA "TROPAEOLUM",1,"15-20","56789","2BC","DAHLIE"
26550 DATA "GEORGINE",3,"30-140","456789","4","SCHIEFBLATT"
26560 DATA "BEGONIE",3,"15-50","456789","4","GERANIE"
26570 DATA "PELARGONIE",3,"20-50","3456789","4"
27000 REM * GEMUESE *
27010 DATA "BUSCHBOHNEN","15-30 G","","456","40","70-90"
27020 DATA "CHINAKOHL","1 PORTION","5-6","6","50"
27030 DATA "60-70"
27040 DATA "ERBSEN","20 G","","234","40","50-60"
27050 DATA "GURKEN"
27060 DATA "1 G","","45","80 X 40","50-100","KARTOFFELN"
27070 DATA "","5-6 KNOLLEN","34","60 X 30","60-70"
27080 DATA "KOHLRABI","","12-16","3456","25 X 25"
27090 DATA "70-100"
27100 DATA "KOPFSALAT","","15-20","45","20 X 20"
27110 DATA "60-100"
27120 DATA "MELONEN","","2-4","4","30 X 80","140"
27130 DATA "MOEHREN"
27140 DATA "1 PORTION","","23456","","150"
27150 DATA "NEUSEEL.SPINAT"
27160 DATA "1 PORTION","2","45","50 X 50","80-90"
27170 DATA "PORREE"
27180 DATA "1 PORTION","20-30","34","30 X 15","180"
27190 DATA "DICKE BOHNEN","20-30 G","","2","30 X 50"
27200 DATA "100-130"
27210 DATA "RADIESCHEN","1-2 PORTIONEN"," ","234567",""
27220 DATA "30-40"
27230 DATA "RETTICHE","2 G","","34567","20 X 15","45-90"
27240 DATA "ROTE BEETE","1 PORTION","","3456","30"
27250 DATA "100-120"
27260 DATA "SCHNITT-UND PFLUECKSALAT","1 PORTION",""
27270 DATA "23","20"
27280 DATA "30-40","SPINAT","3-5 G","","12678","","50"
27290 DATA "STANGENBOHNEN","10-15 G","","4","50 X 100"
27300 DATA "80-100"
27310 DATA "TOMATEN","","3 PFLANZEN","45","35 X 100"
27320 DATA "30"
27330 DATA "ZUCKERMAIS","1 PORTION","","4","40 X 40"
27340 DATA "120-150"
27350 DATA "SAEHZWIEBELN","1 PORTION","","23","10 X 15"
27360 DATA "150"
27370 DATA "STECKZWIEBELN","2-3 G","","3","15","100-120"
27380 REM 
30000 REM  LITERATURHINWEIS 
30010 REM  DER GARTEN IM JAHRESLAUF
30020 REM  VON KARLHEINZ JACOBI UND ELISABETH SCHMITT
30030 REM  HUMBOLDT-TASCHENBUCH 202
30040 REM  ISBN 3-581-66202-7
30050 REM 
30060 REM IN DEN DATA-ZEILEN SIND 125 BLUMEN
30070 REM UND 22 GEMUESEPFLANZEN GESPEICHERT.
30080 REM DARAUS LAESST SICH ABLEITEN, DASS
30090 REM DIESES PROGRAMM KEINEN ANSPRUCH
30100 REM AUF VOLLSTAENDIGE SPEICHERUNG
30110 REM ALLER INFORMATIONEN UEBER ALLE
30120 REM BLUEH- BZW. GEMUESEPFLANZEN
30130 REM ERHEBT. ES SOLL EINE KLEINE HILFE
30140 REM ZUR GESTALTUNG EINES GARTENS
30150 REM ODER BALKONS GEBEN. INTERRESSANT
30160 REM SIND DIE VERSCHLUESSELTEN ABLAGEN
30170 REM DER BLUEHZEIT UND FARBEN IN DEN
30180 REM DATA-ZEILEN. DADURCH KANN DAS
30190 REM PROGRAMM JEDERZEIT ERGAENZT WERDEN.
30200 REM FOLGENDE PROGRAMMZEILEN MUESSEN
30210 REM DAZU GEAENDERT WERDEN 
30220 REM  3020,4010,4050,5170,5240
30230 REM  5550,5630,6640,8100,8130
30240 REM DIE EINGABESCHLUESSEL FINDEN SIE IN DEN
30250 REM UNTERPROGRAMMEN JEWEILS AB ZEILE
30260 REM   9000,9200,9400
30270 REM 

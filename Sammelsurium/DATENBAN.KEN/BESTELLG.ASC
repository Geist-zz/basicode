1000 A=5000:GOTO 20:REM ***BESTELLUNG***
1010 REM 
1020 REM (C) HEIKE UND MANFRED FILLINGER , 20.2.83
1030 REM 
1040 REM INITIALISIERUNG
1050 REM 
1080 DIM AD$(60),S(60),BN$(60),EZ(60),ZS(60)
1090 REM 
1100 REM 
1110 REM       KONSTANTEN
1120 LETRL=10:REM LINKERPAPIERRAND
1150 LETK1$="POS ARTIKEL                  STK."
1155 LETK2$="BESTELLNR.EINZEL(DM) SUMME(DM)"
1160 REM 
1170 REM       VARIABLEN
1180 REM       A1$...ABSENDER NAME
1190 REM       A2$...ABSENDER STRASSE BZW. POSTFACH
1200 REM       A3$...ABSENDER PLZ , ORT
1210 REM       A4$...ABSENDER TEL.NR.
1220 REM 
1230 REM       D$....DATUM
1240 REM 
1250 REM       E1$...EMPFAENGER NAME
1260 REM       E2$...EMPFAENGER STRASSE BZW. POSTFACH
1270 REM       E3$...EMPFAENGER PLZ , ORT
1275 REM       KN$...KUNDENNUMMER
1280 REM 
1290 REM       PS....AKTUELLE POSITION
1300 REM       PM....MAXIMALE POSITION
1310 REM       AD$...ARTIKEL
1320 REM       S.....STUECKZAHL
1330 REM       BN$...BESTELLNUMMER
1340 REM       EZ....EINZELPREIS
1350 REM       ZS....ZEILENSUMME
1360 REM       ES....ENDSUMME
1370 REM 
1375 REM       TB....TABULATOR
1380 REM       C$....DIALOGPUFFER
1390 REM       AW....AUSWAHL
1400 REM       CU$...UEBERGABEPUFFER
1410 REM       SA....AKTUELLE SCHREIBLAENGE
1420 REM       SM....MAXIMALE SCHREIBLAENGE
1430 REM       X.....HILFSVAR.FUER WARTESCHLEIFE
1435 REM       ZL....ZEILENLAENGE
1440 REM 
1450 REM 
1460 REM 
2000 REM ---AUSWAHL-MODUL---
2010 REM 
2020 GOSUB 100
2030 PRINT "B E S T E L L U N G"
2040 PRINT "-------------------"
2050 PRINT "EINGABE ABSENDER (BRIEFKOPF)........1"
2060 PRINT "EINGABE EMPFAENGER..................2"
2070 PRINT 
2080 PRINT "BERECHNUNG/BILDSCHIRMAUSGABE........3"
2090 PRINT "BERECHNUNG/HARDCOPY.................4"
2100 PRINT 
2110 PRINT "BESTELLUNG EINGEBEN.................5"
2140 PRINT "BESTELLUNG AENDERN..................6"
2150 PRINT 
2160 PRINT "PROGRAMM BEENDEN....................7"
2170 PRINT :PRINT ":";
2180 INPUT AW
2190 PRINT 
2200 ON AW GOSUB 3000,4000,5000,6000,7000,8000,9999
2210 PS=PM
2220 GOTO 2000
2230 REM 
2240 REM 
2250 REM 
3000 REM ---EINGABE-MODUL ABSENDER (BRIEFKOPF)---
3010 REM 
3020 PRINT "ABSENDER"
3030 PRINT "--------"
3040 SM=21
3050 CU$=A1$:PRINT "NAME:":GOSUB 15000:A1$=CU$
3060 PRINT 
3070 CU$=A2$:PRINT "STRASSE BZW.POSTFACH:":GOSUB 15000
3080 A2$=CU$
3090 PRINT 
3100 CU$=A3$:PRINT "PLZ , ORT:":GOSUB 15000:A3$=CU$
3110 PRINT 
3120 CU$=A4$:PRINT "TELEFONNUMMER:":GOSUB 15000:A4$=CU$
3130 PRINT 
3140 SM=10
3150 CU$=D$:PRINT "DATUM:":GOSUB 15000:D$=CU$
3160 PRINT 
3170 GOSUB 15500
3180 RETURN
3190 REM 
3200 REM 
3210 REM 
4000 REM ---EINGABE-MODUL EMPFAENGER---
4010 REM 
4020 PRINT "EMPFAENGER"
4030 PRINT "----------"
4040 SM=21
4050 CU$=E1$:PRINT "NAME:":GOSUB 15000:E1$=CU$
4060 PRINT 
4070 CU$=E2$:PRINT "STRASSE BZW. POSTFACH:":GOSUB 15000
4080 E2$=CU$
4090 PRINT 
4100 CU$=E3$:PRINT "PLZ , ORT:":GOSUB 15000:E3$=CU$
4110 PRINT 
4120 CU$=KN$:PRINT "KUNDENNR.:":GOSUB 15000:KN$=CU$:PRINT 
4130 GOSUB 15500
4140 RETURN
4150 REM 
4160 REM 
4170 REM 
5000 REM ---MODUL BERECHNUNG/BILDSCHIRMAUSGABE---
5010 REM 
5020 GOSUB 16000
5030 REM ABSENDER
5040 PRINT A1$;:PRINT TAB(22);A2$
5050 PRINT TAB(22);A3$
5060 PRINT TAB(22);"TEL.";A4$
5070 PRINT TAB(22);"DATUM ";D$
5080 REM EMPFAENGER
5090 PRINT 
5100 PRINT "FIRMA"
5110 PRINT E1$
5120 PRINT E2$
5130 PRINT E3$
5140 REM BESTELLUNG
5150 PRINT :PRINT "KUNDENNR.:";KN$:PRINT 
5160 PRINT "BESTELLUNUG"
5170 PRINT "-----------"
5180 GOSUB 15500
5190 PRINT K1$:PRINT "   ";K2$:PRINT 
5200 FOR PS=1 TO PM
5210 IF AD$(PS)="" THEN 5240
5220 GOSUB 16500
5230 PRINT 
5240 IF (PS/4)=INT(PS/4) THEN GOSUB 15500:PRINT K1$
5250 IF (PS/4)=INT(PS/4) THEN PRINT "   ";K2$
5260 NEXT PS
5270 PRINT :PRINT TAB(28);"-----------"
5280 SR=ES:CT=9:CN=2:GOSUB 310:PRINT TAB(28);SR$;"DM"
5290 GOSUB 15500
5300 RETURN
5310 REM 
5320 REM 
5330 REM 
6000 REM ---MODUL BERECHNUNG/HARDCOPY---
6010 REM 
6020 GOSUB 16000
6030 REM ABSENDER
6040 GOSUB 360
6050 TB=RL:SR$=A1$:GOSUB 17000
6060 TB=RL+37-LEN(SR$):SR$=A2$:GOSUB 17000:GOSUB 360
6070 TB=RL+37:SR$=A3$:GOSUB 17000:GOSUB 360
6080 TB=RL+37:SR$="TEL."+A4$:GOSUB 17000:GOSUB 360
6090 TB=RL+37:SR$="DATUM "+D$:GOSUB 17000:GOSUB 360
6100 GOSUB 360:GOSUB 360
6110 REM EMPFAENGER
6120 GOSUB 360:GOSUB 360:GOSUB 360
6130 TB=RL:SR$="FIRMA":GOSUB 17000:GOSUB 360
6140 TB=RL:SR$=E1$:GOSUB 17000:GOSUB 360
6150 TB=RL:SR$=E2$:GOSUB 17000:GOSUB 360
6160 TB=RL:SR$=E3$:GOSUB 17000:GOSUB 360
6170 GOSUB 360:GOSUB 360:GOSUB 360
6180 REM BESTELLUNG
6190 IF KN$<>"" THEN SR$="":TB=RL:GOSUB 17000
6200 IF KN$<>"" THEN SR$=SR$+"KUNDEN-NR.: "+KN$
6210 GOSUB 350:GOSUB 360
6220 TB=RL:SR$="B E S T E L L U N G":GOSUB 17000:GOSUB 360
6230 TB=RL:SR$="-------------------":GOSUB 17000:GOSUB 360
6240 GOSUB 360:GOSUB 360
6250 TB=RL:SR$=K1$+K2$:GOSUB 17000:GOSUB 360
6260 GOSUB 360
6270 FOR PS=1 TO PM
6280 IF AD$(PS)="" THEN GOTO 6400
6290 SR=PS:CT=3:CN=0:GOSUB 310
6300 TB=RL:GOSUB 17000:ZL=LEN(SR$)
6310 TB=RL+4-ZL:SR$=AD$(PS):GOSUB 17000:ZL=ZL+LEN(SR$)
6320 SR=S(PS):CT=4:CN=0:GOSUB 310
6330 TB=RL+28-ZL:GOSUB 17000:ZL=ZL+LEN(SR$)
6340 TB=RL+33-ZL:SR$=BN$(PS):GOSUB 17000:ZL=ZL+LEN(SR$)
6350 SR=EZ(PS):CT=8:CN=2:GOSUB 310
6360 TB=RL+45-ZL:GOSUB 17000:ZL=ZL+LEN(SR$)
6370 SR=ZS(PS):CT=8:CN=2:GOSUB 310
6380 TB=RL+55-ZL:GOSUB 17000
6390 GOSUB 360
6400 NEXT PS
6410 GOSUB 360:TB=RL+53:SR$="----------":GOSUB 17000
6420 GOSUB 360
6430 SR=ES:CT=8:CN=2:GOSUB 310
6440 TB=RL+55:GOSUB 17000
6450 GOSUB 360:GOSUB 360:GOSUB 360
6460 TB=RL+2:SR$="("+A1$+")":GOSUB 17000:GOSUB 360
6470 RETURN
6480 REM 
6490 REM 
6500 REM 
7000 REM ---MODUL BESTELLUNG EINGEBEN---
7010 PS=PM+1
7020 REM 
7030 SM=23
7040 CU$="":PRINT "ARTIKEL:":GOSUB 15000:AD$(PS)=CU$
7050 IF AD$(PS)="" THEN PM=PS-1:RETURN
7060 SM=4
7070 PRINT 
7080 CU$="":PRINT "STUECKZAHL:":GOSUB 15000:S(PS)=VAL(CU$)
7090 SM=10
7100 PRINT 
7110 CU$="":PRINT "BESTELLNUMMER:":GOSUB 15000:BN$(PS)=CU$
7120 PRINT 
7130 PRINT "EINZELPREIS:";:INPUT EZ(PS)
7140 PS=PS+1
7150 PRINT "....................."
7160 GOTO 7030
7170 REM 
7180 REM 
7190 REM 
8000 REM ---MODUL BESTELLUNG AENDERN---
8010 REM 
8020 PRINT 
8030 PRINT "WELCHE POS. SOLL GEAENDERT WERDEN :";:INPUT PS
8040 IF PS>PM THEN PRINT "POS. ZU GROSS !":GOTO 8290
8050 PRINT 
8060 PRINT K1$:PRINT K2$
8070 PRINT 
8080 GOSUB 16500
8090 PRINT 
8100 PRINT "BESTELLZEILE LOESCHEN (D) ODER AENDERN (C)";
8110 INPUT C$
8120 IF C$="D" THEN AD$(PS)="":S(PS)=0:BN$(PS)="":EZ(PS)=0
8130 IF C$="D" THEN 8290
8140 SM=23
8150 CU$=AD$(PS):PRINT "ARTIKEL:":GOSUB 15000:AD$(PS)=CU$
8160 SM=4
8170 SR=S(PS):GOSUB 300:CU$=SR$
8180 PRINT 
8190 PRINT "STUECKZAHL:":GOSUB 15000:S(PS)=VAL(CU$)
8200 SM=10
8210 PRINT 
8220 CU$=BN$(PS):PRINT "BESTELLNUMMER:":GOSUB 15000
8230 BN$(PS)=CU$
8240 SM=8
8250 SR=EZ(PS):GOSUB 300:CU$=SR$
8260 PRINT 
8270 PRINT "EINZELPREIS:":GOSUB 15000:EZ(PS)=VAL(CU$)
8280 PRINT 
8290 PRINT "WEITERE AENDERUNGEN (J/N):";:INPUT C$
8300 IF C$="J" THEN 8000
8310 RETURN
8320 REM 
8330 REM 
8340 REM 
9999 GOTO 950
15000 REM ---UNTERPROGRAMM ZEILEN-EDITOR---
15010 REM 
15020 REM EINGABEPARAMETER - SM (ZUL. TEXT-LAENGE)
15030 REM EINGABEPARAMETER - CU$ (ALT-TEXT)
15040 REM AUSGABEPARAMETER - CU$ (NEU-TEXT)
15050 REM 
15060 PRINT ">";:GOSUB 120:HO=HO+SM:GOSUB 110:PRINT "<";
15070 HO=HO-SM:GOSUB 110:PRINT CU$;:PRINT "<";
15080 GOSUB 120:GOSUB 210
15090 IF (IN$="<") AND (HO>2) THEN GOSUB 15170
15100 IF IN$=CHR$(13) THEN RETURN
15110 IF IN$="<" THEN 15080
15120 IF HO>=(SM+2) THEN 15080
15130 CU$=CU$+IN$
15140 GOSUB 120:HO=HO-1:GOSUB 110
15150 PRINT IN$;:PRINT "<";
15160 GOTO 15080
15170 GOSUB 120:HO=HO-1:GOSUB 110
15180 IF HO<>(SM+1) THEN PRINT " ";
15190 HO=HO-1:GOSUB 110:PRINT "<";
15200 IF HO<=1 THEN CU$="":RETURN
15210 CU$=LEFT$(CU$,HO-1)
15220 RETURN
15500 REM ---UNTERPROGRAMM WARTESCHLEIFE---
15510 REM 
15520 PRINT "FUER WEITER: EINE TASTE DRUECKEN !"
15530 GOSUB 210:RETURN
15540 REM 
15550 REM 
15560 REM 
16000 REM ---UNTERPROGRAMM BERECHNUNG---
16010 REM 
16020 ES=0
16030 FOR PS=1 TO PM
16040 ZS(PS)=S(PS)*EZ(PS)
16050 ES=ES+ZS(PS)
16060 NEXT PS
16070 RETURN
16080 REM 
16090 REM 
16100 REM 
16500 REM ---UNTERPROGRAMM AUSGABE EINER BESTELL-ZEILE---
16510 REM 
16520 SR=PS:CT=3:CN=0:GOSUB 310:PRINT SR$;
16530 PRINT TAB(4)AD$(PS);
16540 SR=S(PS):CT=4:CN=0:GOSUB 310:PRINT TAB(28);SR$
16550 PRINT TAB(3);BN$(PS);
16560 SR=EZ(PS):CT=8:CN=2:GOSUB 310:PRINT TAB(13);SR$;"DM";
16570 SR=ZS(PS):CT=8:CN=2:GOSUB 310:PRINT TAB(23);SR$;"DM"
16580 RETURN
16590 REM 
16600 REM 
16610 REM 
17000 REM ---UNTERPROGRAMM TAB FUER DRUCKER---
17010 REM 
17020 FOR I=1 TO TB
17030 SR$=" "+SR$
17040 NEXT I
17050 GOSUB 350
17060 RETURN
17070 REM 
17080 REM 
17090 REM 

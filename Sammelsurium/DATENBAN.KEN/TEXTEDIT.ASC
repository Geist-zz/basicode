1000 A=7500:GOTO 20:REM ***BASIC-TEXT-EDITOR***
1010 REM 
1020 REM (C) HEIKE UND MANFRED FILLINGER , 8.4.1983
1030 REM UEBERARBEITET FUER BASICODE-2 12.5.84
1040 REM 
1050 REM       KONSTANTEN
1060 GZ=200:REM MAXIMALETEXTZEILENZAHL
1070 ZM=1:REM KLEINSTEZEILENZAHL
1080 TA=8:REM TAB-WEITE
1090 GOSUB 20000:REM STEUERZEICHEN
1100 DIM T$(200)
1110 REM 
1120 REM       VARIABLEN
1130 REM       Z....AKTUELLE ZEILENNUMMER
1140 REM       ZM...ERSTE FREIE ZEILENNUMMER
1150 REM       ZV...ZEILENNUMMER IM SCHIEBEBEREICH
1160 REM 
1170 REM       T$().TEXTSPEICHER
1180 REM 
1190 REM       N1...ZEILENZEIGER UNTEN
1200 REM       N2...ZEILENZEIGER OBEN
1210 REM       N3...ZEILENZEIGER NACH
1220 REM       P1...ERSTE POSITION
1230 REM       P2...LETZTE POSITION
1240 REM       N1$..URTEXT
1250 REM       N2$..NEUTEXT
1260 REM 
1270 REM       B$,BB$,C$,CC$,KY$..TEXTPUFFER
1280 REM       I,II,J,K,P,..LAUFVARIABLEN
1290 REM       TL...TEXTLAENGE
1300 REM 
1310 REM       RA$..KENNZEICHEN F. RANDAUSGLEICH
1320 REM       B....KENNZEICHEN F. BLANK
1330 REM       BD...BLANK-DIFFERENZ B. RANDAUSGLEICH
1340 REM       L,Z$,ZZ$..ZEICHENVERVIELFACHER
1350 REM 
1360 GOSUB 100
1370 PRINT "*** BASIC-TEXT-EDITOR ***"
1380 REM 
2000 REM ---CONTROL-MODUL---
2010 REM 
2020 GOSUB 250
2030 PRINT "C";ZM;TAB(5)">>";
2040 C$="":INPUT C$
2050 IF C$="" THEN N1=ZM:GOTO 3040
2060 IF LEFT$(C$,1)="I" THEN Z=N1:GOTO 3000
2070 IF C$="LIST" THEN GOSUB 15050:GOTO 4000
2080 IF C$="LLIST" THEN GOSUB 15050:GOTO 4070
2090 SJ=1
2100 IF C$="PRINT" THEN GOSUB 15050:GOTO 4140
2110 IF C$="LPRINT" THEN GOSUB 15050:GOTO 4190
2120 IF C$="DELETE" THEN GOSUB 15050:GOTO 5000
2130 IF C$="CSAVE" THEN 6000
2140 IF C$="CLOAD" THEN 6160
2150 IF C$="CLOAD?" THEN 6290
2160 IF C$="HELP" THEN 7000
2170 IF (C$="CHANGE") OR (C$="C") THEN GOSUB 15110:GOTO 8000
2180 IF C$="FORMAT" THEN 9000
2190 IF C$="COPY" THEN 10000
2200 IF C$="MOVE" THEN 11000
2210 IF LEFT$(C$,2)="EX" THEN GOTO 950
2220 PRINT "UNZULAESSIGES KOMMANDO !"
2230 PRINT "BEFEHLE IN GROSSBUCHSTABEN SCHREIBEN !"
2240 GOTO 2000
2250 PRINT "DER";T;".";M;".";J;"IST UNZULAESSIG"
2260 PRINT 
2270 GOTO 2000
2280 REM 
3000 REM ---EINGABE-MODUL---
3010 REM 
3020 GOSUB 15010
3030 REM 
3040 Z=N1
3050 GOSUB 15250
3060 PRINT "I";Z;TAB(5)">>";
3070 GOSUB 15400
3080 IF C$="" THEN ZM=Z:GOSUB 15330:GOTO 2000
3090 T$(Z)=C$:Z=Z+1
3100 IF Z=ZV THEN PRINT "TEXTSPEICHER VOLL !"
3110 GOTO 3060
4000 REM ---AUSGABE-MODUL---
4010 REM 
4020 REM LIST
4030 FOR I=N1 TO N2
4040 PRINT I;TAB(4)":";T$(I)
4050 NEXT I
4060 GOTO 2000
4070 REM LLIST
4080 FOR I=N1 TO N2
4090 SR=I:CT=4:CN=0:GOSUB 310
4100 SR$=SR$+":"+T$(I)
4110 GOSUB 350:GOSUB 360
4120 NEXT I
4130 GOTO 2000
4140 REM PRINT
4150 FOR I=N1 TO N2
4160 PRINT T$(I)
4170 NEXT I
4180 GOTO 2000
4190 REM LPRINT
4200 FOR I=N1 TO N2
4210 SR$=T$(I):GOSUB 350:GOSUB 360
4220 NEXT I
4230 GOTO 2000
5000 REM ---DELETE-MODUL---
5010 REM 
5020 IF N2=NM THEN ZM=ZM-1:GOTO 2000
5030 ZV=N1
5040 FOR I=N2+1 TO ZM
5050 T$(ZV)=T$(I)
5060 ZV=ZV+1
5070 NEXT I
5080 ZM=ZM-(N2-N1+1):GOTO 2000
6000 REM ---KASSETTEN-MODUL---
6010 REM 
6020 REM CSAVE
6030 SR=ZM:GOSUB 300
6040 PRINT "DATEINAME :";:INPUT DN$
6050 GOSUB 21000
6060 S1$=SR$:S2$=""
6070 GOSUB 21300
6080 FOR I=1 TO ZM STEP 2
6090 S1$=T$(I)
6100 IF I<ZM THEN S2$=T$(I+1)
6110 GOSUB 21300
6120 S2$=""
6130 NEXT I
6140 GOSUB 21700
6150 GOTO 2000
6160 REM CLOAD
6170 PRINT "DATEINAME :";:INPUT DN$
6180 GOSUB 21200
6190 GOSUB 21500
6200 ZV=VAL(S1$)
6210 FOR I=ZM TO ZM+ZV-1 STEP 2
6220 GOSUB 21500
6230 T$(I)=S1$
6240 T$(I+1)=S2$
6250 NEXT I
6260 GOSUB 21800
6270 ZM=ZM+ZV-1
6280 GOTO 2000
6290 REM CLOAD?
6300 PRINT "DATEINAME :";:INPUT DN$
6310 GOSUB 21200
6320 GOSUB 21500
6330 IF VAL(S1$)<>ZM THEN 6430
6340 I=-1
6350 I=I+2
6360 GOSUB 21500
6370 IF T$(I)<>S1$ THEN 6430
6380 IF I+1>ZM THEN 6400
6390 IF T$(I+1)<>S2$ THEN 6430
6400 IF NOT (I>=ZM-1) THEN 6350
6410 GOSUB 21800
6420 GOTO 2000
6430 PRINT "LESEFEHLER !"
6440 GOSUB 21800
6450 GOTO 2000
7000 REM ---KURZANLEITUNGS-MODUL 'HELP'---
7010 REM 
7020 GOSUB 100
7030 PRINT "LIST    : SCHREIBT TEXT MIT ZEILENNR."
7040 PRINT "LLIST   : DRUCKT TEXT MIT ZEILENNR."
7050 PRINT "PRINT   : SCHREIBT TEXT OHNE ZEILENNR."
7060 PRINT "LPRINT  : DRUCKT TEXT OHNE ZEILENNR."
7070 PRINT "DELETE  : LOESCHT TEXTZEILEN"
7080 PRINT "(C)HANGE: AUSTAUSCH VON ZEICHEN BZW ZK."
7090 PRINT "FORMAT  : TEXTFORMATIERUNG"
7100 PRINT "COPY    : KOPIERT TEXTZEILEN IM TEXT"
7110 PRINT "MOVE    : VERSCHIEBT TEXTZEILEN IM TEXT"
7120 PRINT "CSAVE   : SCHREIBT TEXT AUF KASSETTE"
7130 PRINT "CLOAD   : LIEST TEXT VON KASSETTE"
7140 PRINT "CLOAD?  : KONTROLL-LESEN"
7150 PRINT "I...    : EINGABE-MODUS AB TEXTZEILE :..."
7160 PRINT "RETURN  : WECHSEL ZWISCHEN C-/I-MODUS"
7170 PRINT 
7180 GOTO 2000
8000 REM ---CHANGE-MODUL---
8010 REM 
8020 FOR I=N1 TO N2
8030 PRINT :PRINT "<";I;">";
8040 J=1:C$="":P=1
8050 GOSUB 8150
8060 IF J=0 THEN 8110
8070 IF J<>0 THEN PRINT "*";
8080 C$=C$+MID$(T$(I),P,J-P)+N2$
8090 J=J+LEN(N1$):P=J
8100 GOTO 8050
8110 T$(I)=C$+MID$(T$(I),P,LEN(T$(I))-P+1)
8120 NEXT I
8130 PRINT 
8140 GOTO 2000
8150 REM INSTRING
8160 FOR J=J TO LEN(T$(I))-LEN(N1$)+1
8170 IF N1$=MID$(T$(I),J,LEN(N1$)) THEN RETURN
8180 NEXT J
8190 J=0:RETURN
9000 REM ---FORMAT-MODUL---
9010 REM 
9020 GOSUB 15050:PRINT "ERSTE POS.";:INPUT P1
9030 PRINT "LETZTE POS.";:INPUT P2
9040 IF P2>132 THEN 9360
9050 IF (P1+10)>P2 THEN 9360
9060 IF (P1<1) OR (P2<1) THEN 9360
9070 PRINT "MIT RECHTEN RANDAUSGLEICH (J/N)";:INPUT RA$
9080 GOSUB 15250:L=P1-1:Z$=" ":GOSUB 15580
9090 T$(N1)=ZZ$
9100 N2=ZV+N2-N1:Z=N1
9110 FOR I=ZV TO N2
9120 CC$="":PRINT "<";Z;">":Z=Z+1
9130 FOR P=1 TO LEN(T$(I))
9140 C$=MID$(T$(I),P,1)
9150 IF C$<>" " THEN CC$=CC$+C$
9160 IF (C$=" ") OR (P=LEN(T$(I))) THEN GOSUB 9240
9170 NEXT P
9180 NEXT I
9190 ZV=I
9200 IF LEN(T$(N1))<P2 THEN GOSUB 9390
9210 ZM=N1+1
9220 GOSUB 15330
9230 GOTO 2000
9240 REM ABLAGE AB N1
9250 IF CC$="" THEN RETURN
9260 TL=LEN(T$(N1))
9270 IF (TL>P1-1) AND (TL+LEN(CC$)+1<=P2) THEN 9340
9280 IF (TL=P1-1) AND (TL+LEN(CC$)<=P2) THEN 9350
9290 GOSUB 9390
9300 N1=N1+1:L=P1-1:Z$=" ":GOSUB 15580
9310 T$(N1)=ZZ$
9320 T$(N1)=T$(N1)+CC$:CC$=""
9330 RETURN
9340 T$(N1)=T$(N1)+" "+CC$:CC$="":RETURN
9350 T$(N1)=T$(N1)+CC$:CC$="":RETURN
9360 REM PARAM.-FEHLER
9370 PRINT "PARAMETER UNZULAESSIG"
9380 GOTO 2000
9390 REM RECHTER RANDAUSGLEICH
9400 IF RA$<>"J" THEN RETURN
9410 IF (RIGHT$(T$(N1),1)=" ") AND (LEN(T$(N1))>1) THEN 9560
9420 B=0:BB$=""
9430 BD=P2-LEN(T$(N1))
9440 IF BD=0 THEN RETURN
9450 FOR K=1 TO LEN(T$(N1))
9460 B$=MID$(T$(N1),K,1)
9470 BB$=BB$+B$
9480 IF (B$=" ") AND (K>P1) THEN B=1
9490 IF (B$=" ") AND (BD>0) AND (K>P1) THEN BB$=BB$+" "
9500 IF (B$=" ") AND (BD>0) AND (K>P1) THEN BD=BD-1
9510 NEXT K
9520 IF B=0 THEN RETURN
9530 T$(N1)=BB$
9540 IF BD>0 THEN BB$="":GOTO 9450
9550 RETURN
9560 T$(N1)=LEFT$(T$(N1),LEN(T$(N1))-1):GOTO 9410
10000 REM ---COPY-MODUL---
10010 REM 
10020 GOSUB 15170
10030 GOSUB 15250:J=N3
10040 FOR I=N1 TO N1+N2-N3+1
10050 T$(I)=T$(J)
10060 J=J+1
10070 NEXT I
10080 ZM=I-1
10090 GOSUB 15330
10100 GOTO 2000
11000 REM ---MOVE-MODUL---
11010 REM 
11020 GOSUB 15170
11030 GOSUB 15250:J=N3
11040 FOR I=N1 TO N1+N2-N3+1
11050 T$(I)=T$(J)
11060 J=J+1
11070 NEXT I
11080 ZM=I-1
11090 GOSUB 15330
11100 IF N1<N2 THEN N2=N2+ZM-GZ:N3=N3+ZM-GZ
11110 N1=N3
11120 ZV=N1
11130 FOR I=N2+1 TO ZM
11140 T$(ZV)=T$(I)
11150 ZV=ZV+1
11160 NEXT I
11170 ZM=ZM-(N2-N1+1)
11180 GOTO 2000
15000 REM ---ZENTRALES UNTERPROGRAMM-MODUL---
15010 REM I-PARAMETER
15020 N1=VAL(RIGHT$(C$,LEN(C$)-1))
15030 IF N1>ZM THEN N1=ZM
15040 RETURN
15050 REM 2-PARAMETER
15060 PRINT "VON ZEILE";:INPUT N1
15065 PRINT "BIS ZEILE";:INPUT N2
15070 IF (N2>(ZM-1)) OR (N2<1) THEN N2=ZM-1
15080 IF N1<1 THEN N1=1
15090 IF N1>N2 THEN N1=N2
15100 RETURN
15110 REM 2+2 -PARAMETER
15120 GOSUB 15050
15130 PRINT "URTEXT : ";:GOSUB 15400:N1$=C$
15140 PRINT 
15150 PRINT "NEUTEXT: ";:GOSUB 15400:N2$=C$
15160 RETURN
15170 REM 3 PARAMETER
15180 GOSUB 15050
15190 N3=N1
15200 PRINT "NACH ZEILE";:INPUT N1
15210 IF (N1>N3) AND (N1<=2) THEN 15240
15220 IF N1<=N3 THEN N2=GZ-(ZM-N2):N3=GZ-(ZM-N3)
15230 RETURN
15240 PRINT "PARAMETERFEHLER":GOTO 2000
15250 REM LUECKE AUFBAUEN
15260 ZV=GZ
15270 FOR I=ZM TO N1 STEP -1
15280 T$(ZV)=T$(I)
15290 ZV=ZV-1
15300 NEXT I
15310 ZV=ZV+1
15320 RETURN
15330 REM ANFUEGEN
15340 FOR I=ZV TO GZ
15350 T$(ZM)=T$(I)
15360 ZM=ZM+1
15370 NEXT I
15380 ZM=ZM-1
15390 RETURN
15400 REM TASTATUR ABFRAGEN
15410 C$=""
15420 REM 
15430 GOSUB 210:KY$=IN$
15440 IF (LEN(C$)<=0) AND (KY$=BS$) THEN 15430
15450 IF KY$=BS$ THEN GOSUB 15510
15460 IF KY$=TB$ THEN GOSUB 15530
15470 IF KY$>=" " THEN C$=C$+KY$:PRINT KY$;:GOTO 15430
15480 IF KY$<>WR$ THEN 15430
15490 PRINT KY$;
15500 RETURN
15510 IF LEN(C$)>1 THEN C$=LEFT$(C$,LEN(C$)-1):GOTO 15570
15520 C$="":PRINT KY$;:RETURN
15530 REM TAB
15535 JJ=INT(LEN(C$)/TA)
15540 IF (JJ=LEN(C$)/TAAND (KY$<>TB$) THEN RETURN
15550 C$=C$+" ":PRINT " ";:KY$=""
15560 GOTO 15540
15570 PRINT KY$;:RETURN
15580 REM ZEICHENVERVIELFACHER
15590 ZZ$="":IF L<1 THEN RETURN
15600 FOR II=1 TO L
15610 ZZ$=ZZ$+Z$
15620 NEXT II
15630 RETURN
20000 REM STEUERZEICHEN F. CPC 464
20010 BS$=CHR$(8):REM BACK-SPACE
20020 TB$=CHR$(9):REM TABULATOR
20030 WR$=CHR$(13):REM WAGENRUECKLAUF
20040 RETURN
20050 REM DIE IN DEN ZEILEN 20010-20030 VERWENDETEN
20060 REM STEUERZEICHEN GELTEN FUER DEN CPC 464
20070 REM FUER DEN TRS-80 Z.B. LAUTEN DIESE (8),(9),(13).
20080 REM 
20090 REM 
20100 REM 
21000 REM DATEIVERWALTUNG
21010 REM  GILT FUER CPC 464
21100 REM  DATEI OEFFNEN ZUM SCHREIBEN
21110 OPENOUT DN$:RETURN
21200 REM  DATEI OEFFNEN ZUM LESEN
21210 OPENIN DN$:RETURN
21300 QP$=S1$:GOSUB 21400:S1$=QZ$:QP$=S2$:GOSUB 21400
21310 S2$=QZ$
21320 REM DATEN (S1$,S2$) AUF KASSETTE SCHREIBEN
21330 PRINT#9,S1$:PRINT#9,S2$:RETURN
21400 REM UMWANDELN VON DOPPELPUNKT,KOMMA
21410 REM ANFUEHRUNGSZEICHEN USW.
21420 QZ$="":IF QP$="" THEN QP$=" "
21430 FOR QP=1 TO LEN(QP$)
21440 QQ$=MID$(QP$,QP,1):IF QQ$=" " THEN QQ$=CHR$(252)
21450 IF QQ$="," THEN QQ$=CHR$(250)
21460 IF QQ$=CHR$(34) THEN QQ$=CHR$(253)
21470 IF QQ$=":" THEN QQ$=CHR$(251)
21480 QZ$=QZ$+QQ$:NEXT QP:RETURN
21500 REM  DATEN (S1$,S2$) VON KASSETTE LESEN
21510 INPUT#9,S1$:INPUT#9,S2$
21520 QP$=S1$:GOSUB 21600:S1$=QZ$:QP$=S2$
21530 GOSUB 21600:S2$=QZ$:RETURN
21600 REM UMWANDELN VON DOPPELPUNKT,KOMMA
21610 REM ANFUEHRUNGSZEICHEN USW.
21620 QZ$="":IF OP$="" THEN OP$=" "
21630 FOR QP=1 TO LEN(QP$)
21640 QQ$=MID$(QP$,QP,1):IF QQ$=CHR$(252) THEN QQ$=" "
21650 IF QQ$=CHR$(250) THEN QQ$=","
21660 IF QQ$=CHR$(251) THEN QQ$=":"
21670 IF QQ$=CHR$(253) THEN QQ$=CHR$(34)
21680 QZ$=QZ$+QQ$
21690 NEXT QP:RETURN
21700 REM  DATEI ZUM SCHREIBEN SCHLIESSEN
21710 CLOSEOUT:RETURN
21800 REM  DATEI ZUM LESEN SCHLIESSEN
21810 CLOSEIN:RETURN
30000 REM HINWEIS
30010 REM IM I-MODUS IST DER CURSOR AUSGESCHALTET.
30020 REM DIESER KANN DURCH EINE ENTSPRECHENDE
30030 REM ANWEISUNG IN ZEILE 15420 WIEDER
30040 REM EINGESCHALTET WERDEN.
30050 REM (BEI CPC 464 :CALL &BB7B).
30060 REM

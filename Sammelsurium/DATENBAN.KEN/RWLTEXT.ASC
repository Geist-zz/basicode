1000 A=10000:GOTO 20:REM **R-W-L TEXT**
1010 DIM R$(250):R$(0)="250":LF=1
1020 HX=HO:VX=VE:CR$="#":CM$="@"
1040 Y=1:X=0:MX=1:NF$="       ":TB=0
1050 SP$="":FOR F=0 TO HX:SP$=SP$+" ":NEXT F:MN$=""
1070 MN$=MN$+"----:----I":IF LEN(MN$)<HX+2 THEN 1070
1080 MN$=LEFT$(MN$,HX+1)
1230 GOSUB 100
1240 S1$="   RWL-TEXT 2.1   ":S2$=LEFT$(SP$,LEN(S1$))
1250 SR$=S2$:GOSUB 1310
1260 SR$=S1$:GOSUB 1310
1270 SR$=S2$:GOSUB 1310
1280 PRINT
1290 RESTORE:FOR F=1 TO 17:READ SR$:GOSUB 1320:NEXT F
1300 PRINT LEFT$(SP$,HX/2);"Funktion: ";:GOSUB 210:GOTO 1330
1310 PRINT LEFT$(SP$,HX/2-11);:GOSUB 150:PRINT:RETURN
1320 PRINT LEFT$(SP$,(HX-LEN(SR$))/2);SR$:RETURN
1330 IF IN=83 THEN 1800
1340 IF (IN<48) OR (IN>57) THEN GOSUB 250:GOTO 1230
1350 IF IN=48 THEN GOSUB 4030:GOTO 1230
1360 IF IN<>50 THEN 1420
1370 GOSUB 1780
1380 GOSUB 360:FOR F=1 TO MX
1390 SR$=LEFT$(SP$,20)+R$(F):GOSUB 350
1400 GOSUB 360:NEXT F
1410 GOSUB 360:GOSUB 360:GOTO 1230
1420 IF IN<>49 THEN 1490
1430 GOSUB 1780
1440 GOSUB 360:FOR F=1 TO MX/2
1450 SR$=R$(F):G=F+INT(MX/2)
1460 IF G<=MX THEN SR$=SR$+LEFT$(SP$,40-LEN(R$(F)))+R$(G)
1470 GOSUB 350:IF LEN(R$(G))<40 THEN GOSUB 360
1480 NEXT F:GOSUB 360:GOSUB 360:GOTO 1230
1490 IF IN<>51 THEN 1550
1500 GOSUB 1780:GOSUB 360:FOR F=1 TO MX
1510 SR$=R$(F):GOSUB 350
1520 IF F/2=INT(F/2) THEN GOSUB 360
1530 NEXT F
1540 GOSUB 360:GOSUB 360:GOTO 1230
1550 NF=57-IN
1560 GOSUB 120:HO=HX/2:GOSUB 110
1570 IF NF<2 THEN GOSUB 1780:GOTO 1610
1580 PRINT"Name: ";:IP$=NF$
1590 GOSUB 120:PRINT IP$:T1$=CM$:T2$=CR$:CR$="*":CM$=""
1600 PL=1:IN=0:GOSUB 3050:NF$=IP$:CM$=T1$:CR$=T2$
1610 GOSUB 500
1620 IF NF/2=INT(NF/2) THEN GOSUB 1670:GOTO 1640
1630 GOSUB 1730
1640 GOSUB 580:GOTO 1230
1670 GOSUB 540:MX=VAL(IN$)
1680 FOR F=1 TO MX
1690 GOSUB 540:IF IN$=" " THEN IN$=""
1700 R$(F)=IN$:NEXT F
1710 RETURN
1730 SR=MX:GOSUB 300:GOSUB 560
1740 FOR F=1 TO MX
1750 SR$=R$(F):IF SR$="" THEN SR$=" "
1760 GOSUB 560:NEXT F
1770 RETURN
1780 GOSUB 120:HO=HX/2:GOSUB 110:PRINT">TASTE< ";:GOSUB 210
1790 RETURN
1800 GOSUB 120:HO=HX/2:GOSUB 110:PRINT"wirklich? (J/N)";
1810 GOSUB 210:IF IN=74 THEN 950
1820 IF IN=78 THEN 1230
1830 GOSUB 250:GOTO 1800
2000 REM
2010 REM ***** command routine  ****
2020 REM
2030 IN=0:T1=HO:T2=VE:HO=0:VE=VX:GOSUB 110
2040 PRINT"Kommando ?: ";LEFT$(SP$,HX-20);
2050 SD=100:GOSUB 450
2060 HO=11:VE=VX:GOSUB 110:SR$=IN$:GOSUB 330
2070 IF IN>31 THEN PRINT SR$;
2080 IF IN>0 THEN 2150
2090 GOSUB 110:PRINT" ";
2100 HO=0:VE=VX:GOSUB 110
2110 PRINT"nach";CM$;" folgt Kommando  (H=Hilfe)";
2120 PRINT LEFT$(SP$,HX-32);
2130 HO=T1:VE=T2:IF IN<128 THEN IN=0
2140 GOTO 4060
2150 IF IN=76 THEN R$(Y)=RIGHT$(R$(Y),LEN(R$(Y))-1):GOTO 2870
2160 IF IN=82 THEN R$(Y)=" "+LEFT$(R$(Y),HX):GOTO 2870
2170 IF IN=84 THEN 2100
2180 IF IN=77 THEN RETURN
2190 IF IN<>86 THEN 2240
2200 IF Y=MX THEN GOSUB 250:GOTO 2100
2210 MX=MX-1:FOR F=Y TO MX:R$(F)=R$(F+1):NEXT F
2220 IF (BV+VX-3>MX) AND (BV>1) THEN BV=BV-1:T2=T2+1
2230 GOTO 2900
2240 IF IN<>73 THEN 2280
2250 IF Y=MX THEN GOSUB 250:GOTO 2100
2260 FOR F=MX TO Y STEP -1:R$(F+1)=R$(F):NEXT F:MX=MX+1:R$(Y)=""
2270 IN=128:IN$=CHR$(128):GOTO 2900
2280 IF IN=66 THEN T2=1:Y=1:BV=1:GOTO 2900
2290 IF IN<>69 THEN 2330
2300 Y=MX:BV=MX-VX+3
2310 IF BV<1 THEN BV=1
2320 T2=Y-BV+1:GOTO 2900
2330 IF IN<>83 THEN 2430
2340 IF MX<2 THEN GOSUB 250:GOTO 2100
2350 IF MX>5 THEN VE=VX:HO=0:GOSUB 110:PRINT"-ich sortiere-";
2360 FOR F=MX-1 TO 1 STEP -1
2370 FOR G=1 TO F
2380 IF R$(G)<R$(G+1) THEN 2400
2390 T$=R$(G):R$(G)=R$(G+1):R$(G+1)=T$
2400 NEXT G
2410 NEXT F
2420 GOTO 2900
2430 IF (IN<>85) OR (T1>=LEN(R$(Y))) THEN 2480
2440 T$=RIGHT$(R$(Y),LEN(R$(Y))-T1)
2450 IF LEN(R$(Y))=40 THEN T$=LEFT$(T$,LEN(T$)-1)
2460 IF T1>0 THEN R$(Y)=LEFT$(R$(Y),T1)+" "+T$:GOTO 2870
2470 R$(Y)=" "+T$:GOTO 2870
2480 IF IN=80 THEN TB=T1:GOTO 2100
2490 IF IN<>67 THEN 2540
2500 IF LEFT$(R$(Y),1)<>" " THEN 2520
2510 R$(Y)=RIGHT$(R$(Y),LEN(R$(Y))-1):GOTO 2500
2520 R$(Y)=LEFT$(SP$,INT((HX+1-LEN(R$(Y)))/2))+R$(Y)
2530 GOTO 2870
2540 IF IN<>43 THEN 2590
2550 Y=Y-BV:BV=BV+VX-2
2560 IF BV+VX-3>MX THEN BV=MX-VX+3:GOSUB 250
2570 IF BV<1 THEN BV=1
2580 Y=Y+BV:GOTO 2900
2590 IF IN<>45 THEN 2630
2600 Y=Y-BV:BV=BV-VX+2
2610 IF BV<1 THEN BV=1:GOSUB 250
2620 GOTO 2580
2630 IF IN=78 THEN LF=1-LF:GOTO 2100
2640 IF IN<>72 THEN IN=0:GOSUB 250:GOTO 2050
2650 HO=0:VE=1:GOSUB 110
2660 FOR VE=1 TO VX-2:GOSUB 110:PRINT SP$;:NEXT VE
2670 VE=1:GOSUB 110:PRINT"R  Zeile nach rechts"
2680 VE=2:GOSUB 110:PRINT"L  Zeile nach links"
2690 VE=3:GOSUB 110:PRINT"M  Hauptmenue anzeigen"
2700 VE=4:GOSUB 110:PRINT"T  Text anzeigen"
2710 VE=5:GOSUB 110:PRINT"H  Hilfe"
2720 VE=6:GOSUB 110:PRINT"V  Zeile loeschen"
2730 VE=7:GOSUB 110:PRINT"I  Zeile einfuegen"
2740 VE=8:GOSUB 110:PRINT"B  Cursor Textanfang"
2750 VE=9:GOSUB 110:PRINT"E  Cursor Textende"
2760 VE=10:GOSUB 110:PRINT"S  Zeilen sortieren"
2770 VE=11:GOSUB 110:PRINT"U  ab Cursor Zeile nach rechts"
2780 VE=12:GOSUB 110:PRINT"P  Tabulator setzen"
2790 VE=13:GOSUB 110:PRINT"C  Zeile zentrieren"
2800 VE=14:GOSUB 110:PRINT"+  eine Seite vorwaerts"
2810 VE=15:GOSUB 110:PRINT"-  eine Seite zurueck"
2820 VE=16:GOSUB 110:PRINT"N  Zeilennummer an/aus"
2830 SD=200:GOSUB 450
2840 FOR VE=VX-2 TO 1 STEP -1:GOSUB 110:PRINT SP$;:NEXT VE
2850 GOSUB 2920:GOTO 2060
2870 HO=0:VE=T2:GOSUB 110
2880 PRINT R$(Y);LEFT$(SP$,HX+1-LEN(R$(Y)));:GOTO 2100
2900 GOSUB 2920:GOTO 2100
2920 T=0:HO=0:VE=1:GOSUB 110
2930 PRINT R$(BV+T);:IF HX=LEN(R$(BV+T)) THEN 2950
2940 PRINT LEFT$(SP$,HX+1-LEN(R$(BV+T)));
2950 T=T+1:IF (T>VX-3) OR (T>MX) THEN VE=1:RETURN
2960 VE=VE+1:GOSUB 110:GOTO 2930
3030 PL=1:GOSUB 110:PRINT IP$
3040 IN=0
3050 SC$=MID$(IP$,PL,1)
3060 IF IN<>0 THEN 3110
3070 GOSUB 110:PRINT CR$;:GOSUB 110
3080 SD=1:GOSUB 450:IF IN<>0 THEN 3110
3090 GOSUB 110:PRINT SC$;:GOSUB 110
3100 SD=1:GOSUB 450:GOTO 3060
3110 GOSUB 110:PRINT SC$;:GOSUB 110
3120 IF IN=28 THEN GOSUB 3280:GOTO 3040
3130 IF IN=29 THEN GOSUB 3330:GOTO 3050
3140 IF IN=127 THEN 3210
3150 IF (IN<32) OR (IN>126) OR (IN$=CM$) THEN 3370
3160 IF PL<2 THEN IP$=IN$+RIGHT$(IP$,LEN(IP$)-PL):GOTO 3180
3170 IP$=LEFT$(IP$,PL-1)+IN$+RIGHT$(IP$,LEN(IP$)-PL)
3180 GOSUB 110:PRINT IN$;
3190 GOSUB 3330:GOTO 3050
3210 IF PL=1 THEN GOSUB 250:GOTO 3040
3220 IF PL=2 THEN IP$=RIGHT$(IP$,LEN(IP$)-PL+1)+" ":GOTO 3240
3230 IP$=LEFT$(IP$,PL-2)+RIGHT$(IP$,LEN(IP$)-PL+1)+" "
3240 GOSUB 3280
3250 HO=HO-PL+1:GOSUB 110:HO=HO+PL-1
3260 PRINT IP$:GOTO 3040
3280 PL=PL-1:HO=HO-1
3290 IF PL>0 THEN RETURN
3300 PL=PL+LEN(IP$):HO=HO+LEN(IP$)
3310 RETURN
3330 PL=PL+1:HO=HO+1
3340 IF PL<=LEN(IP$) THEN IN=0:RETURN
3350 PL=PL-LEN(IP$):HO=HO-LEN(IP$)
3360 IN=30:RETURN
3370 IF (IN=13) OR (IN=30) OR (IN=31) OR (IN$=CM$) THEN RETURN
3380 GOTO 3040
4030 GOSUB 100:HO=0:VE=0:GOSUB 110:BV=1:Y=1
4040 PRINT MN$:HO=0:VE=VX-1:GOSUB 110:PRINT MN$;:VE=1
4050 T1=HO:T2=VE:GOSUB 2920:IN=0:GOTO 2060
4060 IN=0:SC$=" ":IF HO<LEN(R$(Y)) THEN SC$=MID$(R$(Y),HO+1,1)
4070 IF LF=1 THEN 4100
4080 T1=HO:T2=VE:HO=HX-7:VE=VX:SR=Y:GOSUB 110:GOSUB 300
4090 PRINT"RG=";SR$;" ";:HO=T1:VE=T2
4100 IF IN<>0 THEN 4160
4110 GOSUB 110:PRINT CR$;:GOSUB 110
4120 SD=1:GOSUB 450:IF IN<>0 THEN 4150
4130 GOSUB 110:PRINT SC$;:GOSUB 110
4140 SD=1:GOSUB 450:GOTO 4100
4150 GOSUB 110:PRINT SC$;:GOSUB 110
4160 IF IN<>28 THEN 4190
4170 HO=HO-1:IF HO<0 THEN HO=HX
4180 GOTO 4060
4190 IF IN<>29 THEN 4220
4200 HO=HO+1:IF HO>HX THEN HO=0
4210 GOTO 4060
4220 IF IN<>31 THEN 4270
4230 IF Y=1 THEN GOSUB 250:GOTO 4060
4240 Y=Y-1:VE=VE-1:IF VE>0 THEN 4060
4250 BV=BV-8:VE=VE+8:IF BV>0 THEN 4050
4260 BV=1:VE=Y:GOTO 4050
4270 IF IN<>30 THEN 4320
4280 IF Y=MX THEN GOSUB 250:GOTO 4060
4290 Y=Y+1:VE=VE+1:IF VE<VX-1 THEN 4060
4300 BV=BV+8:VE=VE-8:IF BV+VX-3<=MX THEN 4050
4310 BV=BV-9:VE=VE+9:GOTO 4300
4320 IF IN<>13 THEN 4350
4330 HO=TB:IF (Y=MX) AND (MX<250) THEN MX=MX+1:R$(MX)=""
4340 GOTO 4280
4350 IF IN$=CM$ THEN 2030
4360 IP$=R$(Y)+RIGHT$(SP$,HX+1-LEN(R$(Y)))
4370 PL=HO+1:GOSUB 3050
4380 IF (Y=MX) AND (MX<250) THEN MX=MX+1:R$(MX)=""
4390 T=HX+1
4400 IF MID$(IP$,T,1)<>" " THEN 4420
4410 T=T-1:IF T>0 THEN 4400
4420 IF T=0 THEN R$(Y)="":GOTO 4100
4430 R$(Y)=LEFT$(IP$,T):GOTO 4100
25000 DATA "0) Text zeigen",""
25010 DATA "1) Druck 2-spaltig"
25020 DATA "2) Druck 1-spaltig"
25030 DATA "3) doppelte Breite",""
25040 DATA "4) SAVE Text auf Disk"
25050 DATA "5) LOAD Text von Disk",""
25060 DATA "6) SAVE Text auf Kassette"
25070 DATA "7) LOAD Text von Kassette",""
25080 DATA "8) SAVE Text als Basicode-3 File"
25090 DATA "9) LOAD Text als Basicode-3 File",""
25100 DATA "S) Programmende - BASIC",""
30000 REM ************************
30010 REM *                      *
30020 REM *     RWLTEKST 2.1     *
30030 REM *                      *
30040 REM * is een programma van *
30050 REM *                      *
30060 REM *    Robbert Wethmar   *
30070 REM *     uit Lelystad     *
30080 REM *                      *
30090 REM * in BASICODE 3 op een *
30100 REM *  SONY HB75p MSX 1.0  *
30110 REM *  vanaf januari 1987  *
30120 REM *     Met dank aan:    *
30130 REM *     J.  Haubrich     *
30140 REM * -------------------- *
30150 REM *   Deutsch angepasst  *
30160 REM *  auf dem  KC85/4 von *
30170 REM *  U. Zierott,  Lehnin *
30180 REM ************************
30190 REM Erstausstrahlung:
32000 REM Computernacht 15/16.9.89
32010 REM   ACORN-Master

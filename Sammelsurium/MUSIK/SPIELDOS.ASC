1000 A=2000:GOTO 20:REM SPEELDOOS/BC3
1010 DIM P(255),M$(60):MP=0:VT=VE
1020 SV=7:SD=2:KM=0:AA=0:H=0:L$=""
1030 GOSUB 2150:REM vul tonen-array
1040 GOSUB 100:PRINT:PRINT"    ";
1050 SR$="S P E E L D O O S":GOSUB 150:PRINT:PRINT
1060 PRINT" MENU":PRINT" ====":PRINT
1070 PRINT" 1 = UITLEG":PRINT
1080 PRINT" 2 = MUZIEK UIT DATA-REGELS":PRINT
1090 PRINT" 3 = MUZIEK LADEN VAN CASSETTE":PRINT
1100 PRINT" 4 = MUZIEK LADEN VAN DISKETTE":PRINT
1110 PRINT" 5 = ZELF MUZIEK MAKEN":PRINT
1120 PRINT" 6 = MUZIEK SAVEN OP CASSETTE":PRINT
1130 PRINT" 7 = MUZIEK SAVEN OP DISKETTE":PRINT
1140 PRINT" 8 = STOPPEN":PRINT
1150 PRINT" MAAK JE KEUZE (1...8) ";:GOSUB 210
1160 IF (IN<49) OR (IN>56) THEN 1040
1170 IF IN=56 THEN 950
1180 IN=IN-48:ON IN GOSUB 3030,2030,6040,6030,4030,5040,5030
1190 GOTO 1040
2000 REM 
2010 REM === LUISTER EN KIJK ===
2020 REM 
2030 RESTORE 
2040 READ L$:IF L$="*" THEN RETURN
2050 GOSUB 100:PRINT:PRINT:PRINT
2060 PRINT" Deze regel wordt nu gespeeld:":PRINT
2070 REM -- ingang 'Afspelen' --
2080 PRINT L$:PRINT
2090 GOSUB 2250:REM speel de melodie
2100 IF H=2 THEN RETURN
2110 GOTO 2040
2120 REM 
2130 REM === VUL TONEN-ARRAY ===
2140 REM 
2150 A=ASC("A"):P(A)=57:P(A+1)=59
2160 P(A+2)=60:P(A+3)=62:P(A+4)=64
2170 P(A+5)=65:P(A+6)=67
2180 A=ASC("a"):P(A)=69:P(A+1)=71
2190 P(A+2)=72:P(A+3)=74:P(A+4)=76
2200 P(A+5)=77:P(A+6)=79
2210 RETURN
2220 REM 
2230 REM === SPEEL DE MELODIE ===
2240 REM 
2250 KM=0:FOR N=1 TO LEN(L$)
2260 T$=MID$(L$,N,1)
2270 IF (T$<"0") OR (T$>"9") THEN AA=0:GOTO 2290
2280 SD=AA+ASC(T$)-48:AA=10*SD:GOTO 2430
2290 IF T$="." THEN KM=KM+12:GOTO 2430
2300 IF T$="=" THEN KM=KM-12:GOTO 2430
2310 IF T$="+" THEN KM=KM+1:GOTO 2430
2320 IF T$="-" THEN KM=KM-1:GOTO 2430
2330 IF T$="R" THEN SV=0:GOSUB 400:SV=7:GOTO 2430
2340 IF T$="H" THEN SV=15:GOTO 2430
2350 IF T$="M" THEN SV=7:GOTO 2430
2360 IF T$="Z" THEN SV=3:GOTO 2430
2370 IF (T$=">") AND (SV=1) THEN 2430
2380 IF T$=">" THEN SV=SV-1:GOTO 2430
2390 IF (T$="<") AND (SV=15) THEN 2430
2400 IF T$="<" THEN SV=SV+1:GOTO 2430
2410 IF T$="" THEN 2430
2420 SP=P(ASC(T$))+KM:GOSUB 400:KM=0
2430 NEXT N
2440 RETURN
3000 REM 
3010 REM === UITLEG ===
3020 REM 
3030 GOSUB 100
3040 PRINT:PRINT"    ";:SR$="S P E E L D O O S":GOSUB 150
3050 PRINT:PRINT
3060 PRINT" Dit programma laat de  computer":PRINT
3070 PRINT" muziek spelen, die je zelf kunt":PRINT
3080 PRINT" schrijven. In de DATA-regels in":PRINT
3090 PRINT" het programma staan voorbeelden":PRINT
3100 PRINT" Je kunt zelf nieuwe DATA-regels":PRINT
3110 PRINT" maken, die de computer dan  ook":PRINT
3120 PRINT" zal spelen.":PRINT
3130 PRINT" Je kunt ook een muziek -bestand":PRINT
3140 PRINT" maken, dat je kunt SAVEn en dan":PRINT
3150 PRINT" later weer kunt inlezen":PRINT
3160 GOSUB 3490:PRINT
3170 PRINT" Dat kan als je in het MENU naar":PRINT
3180 SR$="Zelf Muziek Maken":GOSUB 150:PRINT"gaat.":PRINT
3190 PRINT" Je noteert de muziek als volgt:":PRINT
3200 PRINT" Je mag voor de tonen de letters":PRINT
3210 PRINT" ABCDEFGabcdefg gebruiken.":PRINT
3220 PRINT" Een R geeft een  rust  (stilte)":PRINT
3230 PRINT" Na een  HOOFDLETTER H klinkt al":PRINT
3240 PRINT" wat er na komt hard, na M matig":PRINT
3250 PRINT" en na Z zacht. Na de < gaat het":PRINT
3260 PRINT" iets harder, na > iets zachter.":PRINT
3270 GOSUB 3490:PRINT
3280 PRINT" Je mag ook getallen gebruiken:":PRINT
3290 PRINT" Een getal bepaalt de lengte van":PRINT
3300 PRINT" ALLE noten die er na komen, TOT":PRINT
3310 PRINT" er een ander getal komt.":PRINT
3320 PRINT" + voor een noot is 1/2 toon hoger":PRINT
3330 PRINT" - voor een noot is 1/2 toon lager":PRINT
3340 PRINT" . maakt de noot een  octaaf hoger":PRINT
3350 PRINT" = maakt de noot een  octaaf lager":PRINT
3360 PRINT" Je kunt melodietjes maken door ze":PRINT
3370 PRINT" in te tikken  op het  scherm,  ze":PRINT
3380 PRINT" worden dan meteen afgespeeld.":PRINT
3390 GOSUB 3490:PRINT
3400 PRINT" Je kunt ze veranderen tot je er":PRINT
3410 PRINT" tevreden over bent en dan op de":PRINT
3420 PRINT" V tikken. Ze komen daardoor  in":PRINT
3430 PRINT" het bestand, dat 60 regels  kan":PRINT
3440 PRINT" bevatten. Met E kun je het hele":PRINT
3450 PRINT" bestand bereiken, voor achteraf":PRINT
3460 PRINT" verbeteren van muziek-regels.":PRINT
3470 PRINT" Na RETURN komt het hulpmenu met":PRINT
3480 PRINT" alle commando's die nodig zijn.":PRINT
3490 HO=0:VE=VT:GOSUB 110
3500 PRINT"--> Tik op een toets (S=STOP)";
3510 GOSUB 210:IF IN=83 THEN 950:REM S
3520 GOSUB 100:RETURN
4000 REM 
4010 REM === ZELF TIKKEN ===
4020 REM 
4030 GOSUB 100:PRINT:PRINT L$
4040 PRINT:PRINT" TIK EEN MELODIE"
4050 PRINT" (Alleen RETURN = hulpmenu)":PRINT
4060 IF H>0 THEN H=0:PRINT L$:GOTO 4080
4070 INPUT L$:IF L$="" THEN 4090
4080 GOSUB 2250:PRINT
4090 PRINT" Tik 'n toets ( H=herhalen,":PRINT
4100 PRINT" M=MENU, V=Volgende regel,":PRINT
4110 PRINT" A=Afspelen, E=Edit, S=STOP)";:GOSUB 210
4120 IF IN=77 THEN RETURN:REM M
4130 IF IN=83 THEN 950:REM S
4140 IF IN=72 THEN H=1:GOTO 4030:REM H
4150 IF IN=69 THEN GOSUB 4350:GOTO 4030:REM E
4160 IF IN<>65 THEN 4190:REM <>A
4170 GOSUB 100:H=2:FOR M=0 TO MP-1:L$=M$(M):GOSUB 2080
4180 NEXT M:H=0:GOTO 4030
4190 IF IN=85 THEN RETURN:REM U
4200 IF IN<>80 THEN 4220:REM <>P
4210 SR$=L$:GOSUB 350:GOSUB 360
4220 IF IN=86 THEN GOSUB 4270:GOTO 4160:REM V
4230 GOTO 4030
4240 REM 
4250 REM === VOLGENDE ===
4260 REM 
4270 IF MP=60 THEN GOSUB 250:RETURN
4280 M$(MP)=L$:H=1:MP=MP+1:GOSUB 4300:RETURN
4290 REM === HERH.OF AFSP? ===
4300 IF MP=0 THEN H=1:RETURN
4310 IN=65:RETURN
4320 REM 
4330 REM === EDIT ===
4340 REM 
4350 GOSUB 100:PRINT" Alle opgeslagen regels worden een"
4360 PRINT" voor een geprint en gespeeld"
4370 PRINT" Elke regel kan na afspelen nog"
4380 PRINT" veranderd worden.":PRINT
4390 PRINT" Na de laatste regel geeft J of N"
4400 PRINT" het intikscherm weer terug.":PRINT
4410 EP=0:GOTO 4430
4430 L$=M$(EP):PRINT L$
4440 GOSUB 2250:PRINT"O.K.?(J/N) ":GOSUB 210
4450 IF EP=MP THEN RETURN
4460 IF IN=74 THEN M$(EP)=L$:EP=EP+1:GOTO 4430
4470 PRINT"TIK DE VERBETERDE REGEL":INPUT L$:GOTO 4440
5000 REM 
5010 REM === MUZIEK SAVEN ===
5020 REM 
5030 NF=5:GOTO 5050
5040 NF=1
5050 IF MP=0 THEN GOSUB 250:RETURN
5060 NF$="MUZDATA":GOSUB 500
5070 FOR N=0 TO MP
5080 SR$=M$(N):GOSUB 560
5090 NEXT N:GOSUB 580
5100 RETURN
6000 REM 
6010 REM === MUZIEK LADEN ===
6020 REM 
6030 NF=4:GOTO 6050
6040 NF=0
6050 NF$="MUZDATA":GOSUB 500:MP=0
6060 GOSUB 540:M$(MP)=IN$:MP=MP+1
6070 IF IN=0 THEN 6060
6080 GOSUB 580:MP=MP-1
6090 RETURN
25000 REM "TOONLADDERS"
25010 DATA "M8=C=D=E=F=GABCDEFGabcdefg.a.b.cR"
25020 DATA "M4C+CD+DEF+FG+Ga+abc24R"
25030 DATA "Z8C<D<E<F<G<a<b<cc>b>a>G>F>E>D>C8R"
25040 DATA "M2CDEGFFaGGcbcGECDEFGaGFEDECBCD=GBDFED"
25050 DATA ">>ECDEGFFaGGcbcGECDE<AGFEDC<=GCB12CR"
25060 DATA "2CRDRERFRGRaRbRc8R"
25070 DATA "M1cdcbcbabaGaGFGFEFEDED8CZcR"
25080 REM "KORTJAKJE"
25090 DATA "M4CCGGaaGR2FFFF4EEDDCR>>GGFFEEDDGGFFEEDD"
25100 DATA "MCCGGaaGRFFEEDDCR"
25110 DATA "M1CDEFGcbcacbcGcbcFdcdEcbcDccb4c"
25120 DATA "Z1CDEFGcbcacbcGcbcFdcdEcbcDccb4c"
25130 DATA "M1GECEFDBFEC=GC2DDZ1GECEFDBFEC=GC2DD"
25140 DATA "M1CDEFGcbcacbcGcbcZFdcdEcbc<<Dccb4<<CR"
25150 DATA "4AAEEFF8E4DDCCBB8A4EEDDCCBBEEDDCCBB"
25160 DATA "AAEEFF8E4DDCCBB8AR"
25170 DATA "M8C4C8G4G8a4FGcGFdFEcEDcb12c"
25180 DATA ">>8C4C8G4G8a4FGcGFdFEcEDcb12c"
25190 DATA "M8G4G8F4F8E4E8D4D>>8G4G8F4FEFG12D"
25200 DATA "M8C4C8G4G8a4a12G>>4FdF<EcE<D<c<b>>12cR"
25210 REM "DER VOGELFAENGER"
25220 DATA "M2ED4CC2DCBC4DED"
25230 DATA "2DB4=G=G6G2G4EDC"
25240 DATA "4E2DCBC4DEDB6G"
25250 DATA "2D6G2G6D2D4BA=G"
25260 DATA "2=GB4DD2EDCD4EDC"
25270 DATA "4GBBC2BC4DED"
25280 DATA "4BC1CDEF4GBC1CDEF4G"
25290 DATA "2CD4EE2FEDE4FGa"
25300 DATA "2FD4BB2CBABCEDB4C"
25310 DATA "4DE1CDEF4GB12C"
25320 DATA "*"
30000 REM ==============================
30010 REM LAATSTE DATA MOET "*" ZIJN
30020 REM ==============================
30030 REM De in dit programma gebruikte
30040 REM notatie van de muziek sluit
30050 REM zo dicht mogelijk aan bij het
30060 REM gebruikelijke notenschrift.
30070 REM Dit vergemakkelijkt het lezen
30080 REM en schrijven van de voor de
30090 REM computer bestemde DATA zeer.
30100 REM Door de notatie met EEN letter
30110 REM per noot in het groot- en
30120 REM klein octaaf wordt een grote
30130 REM besparing op geheugen bereikt.
30140 REM De lengtewaarden worden zoveel
30150 REM mogelijk per serie noten
30160 REM gegeven.
30170 REM Subroutine regel 2130 zorgt er
30180 REM voor dat ook computers met een
30190 REM afwijkende interne letter-code
30200 REM goed werken. Bij computers met
30210 REM alleen hoofdletters moet wat
30220 REM vaker de . als octaafverhoger
30230 REM worden gebruikt.
30240 REM ==============================
32000 REM GEMAAKT OP EEN BBC B COMPUTER
32010 REM 20 FEBRUARI 1987
32020 REM DOOR
32030 REM Ch.W.Brederode
32040 REM Von Bonninghausenlaan 33
32050 REM 2161 ES  LISSE
32100 REM TROS-RADIO dd 870527

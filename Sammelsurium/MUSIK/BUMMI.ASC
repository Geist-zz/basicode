1000 A=100:GOTO 20:REM ### B U M M I ###
1010 W=200:HH=HO:VV=VE:Z=3
1020 DIM M(W),N(W),Q(13)
1030 SV=15:GOSUB 1200:F$="C-DUR"
1090 GOTO 9020
1100 PRINT:PRINT"              >Taste<";
1110 GOSUB 210:GOSUB 100:PRINT:PRINT:RETURN
1120 PRINT:PRINT" 1 = BASICODE":PRINT
1130 PRINT" 2 = BASIC":PRINT
1140 PRINT" 3 = DISK1":PRINT
1150 PRINT" 4 = DISK2":PRINT:PRINT
1160 PRINT" AUSWAHL:";:GOSUB 210:X=IN-48
1170 IF IN=77 THEN X=1.5
1180 IF (X<1) OR (X>4) THEN X=0
1190 RETURN
1200 RESTORE:FOR I=0 TO 32:READ X:NEXT I
1210 FOR I=1 TO 13:READ Q(I):NEXT I:RETURN
1220 GOSUB 600:CN=0
1230 FOR I=10 TO 30 STEP 5
1240 VE=I/99:HO=.01:GOSUB 620
1250 HO=.99:GOSUB 630
1260 NEXT I:RETURN
1270 A$="C":RETURN
1280 A$="G":RETURN
1290 A$="D":RETURN
1300 A$="A":RETURN
1310 A$="F":RETURN
1320 A$="B":RETURN
1330 VE=.9:SR$="* (A/E/K/CUL/CUR) ":GOSUB 650
1340 GOSUB 210:IF IN=77 THEN X=1:RETURN
1350 IF IN=29 THEN SR$=SR$+"Liedende":GOTO 1410
1360 IF IN=28 THEN SR$=SR$+"Liedanfang":GOTO 1470
1370 IF IN=65 THEN SR$=SR$+"Anfangsmarke":KA=I:GOTO 1480
1380 IF IN=69 THEN SR$=SR$+"Endemarke":KE=I:GOTO 1490
1390 IF IN=75 THEN SR$=SR$+"Kopieren":GOTO 1500
1400 C$="falsche Taste!":GOTO 1570
1410 GOSUB 650:FOR J=1 TO W
1420 IF J=W THEN X=1:I=W
1430 IF M(J)+N(J)=0 THEN I=J-1:J=W
1440 NEXT J:IF X=1 THEN 1550
1450 IF I=0 THEN I=1
1460 GOTO 1580
1470 GOSUB 650:I=1:GOTO 1580
1480 GOSUB 650:KA=I:GOTO 1580
1490 GOSUB 650:KE=I:GOTO 1580
1500 K=KE-KA:IF K<0 THEN 1560
1510 GOSUB 650:FOR J=0 TO K
1520 M(I+J)=M(KA+J):N(I+J)=N(KA+J)
1530 IF I+J=W THEN J=K:X=1
1540 NEXT J:IF X=0 THEN 1580
1550 C$="Speicherende":GOTO 1570
1560 C$="Fehler! (Ende vor Anfang)"
1570 CN=1:GOSUB 650:SR$=C$:CN=0:GOSUB 650:GOSUB 1620
1580 SD=10:GOSUB 450:CN=1:GOSUB 650:VE=.8:CN=0
1590 X=0:RETURN
1600 CN=1:B$="":SR$=B$:GOSUB 650:CN=0
1610 B$=T$+P$+" "+D$:SR$=B$:GOSUB 650:RETURN
1620 RESTORE:FOR H=0 TO 10
1630 READ SP,SD,SV:SD=SD/Z:GOSUB 400
1640 NEXT H:RETURN
2000 REM INPUT/EDIT
2010 GOSUB 1220:SR$="### EINGABE/AENDERN ###"
2020 HO=.02:VE=.02:GOSUB 650
2030 RESTORE:FOR I=0 TO 45:READ A:NEXT I:X=0
2040 FOR I=35 TO 5 STEP -2.5
2050 H=1-(I+.5)/38:X=X+1
2060 FOR J=-1 TO 1 STEP .2
2070 VE=(I+J*1.7)/99:HO=H-COS(J)*2/99:GOSUB 620
2080 IF (ABS(J)>.1) OR (X<7) THEN 2100
2090 VE=VE+.15:GOSUB 630:VE=VE-.15:GOSUB 620:GOTO 2100
2100 HO=H+COS(J)*2/99:GOSUB 630
2110 IF ABS(J)>.1 THEN 2140
2120 IF X<7 THEN VE=VE-.15
2130 GOSUB 630
2140 NEXT J
2150 READ SR$:VE=.4:HO=H-.01:GOSUB 650
2160 IF (X>7) OR (INT((X+1)/2)<>(X+1)/2) THEN 2280
2170 V=60:FOR J=-1 TO 1 STEP .2
2180 VE=(V+J*1.7)/99:HO=H-COS(J)*2/99:GOSUB 620
2190 HO=H+COS(J)*2/99
2200 IF (ABS(J)>.9) OR (X<5) THEN GOSUB 630:GOTO 2220
2210 GOSUB 620
2220 IF (ABS(J)>.1) OR (X>5) THEN 2260
2230 VE=VE-15/99:GOSUB 630:IF X>2 THEN 2260
2240 VE=VE+4/99:HO=HO+2/99:GOSUB 630
2250 VE=VE+5/99:GOSUB 630
2260 NEXT J
2270 VE=.65:READ SR$:HO=H-.01:GOSUB 650
2280 NEXT I
2290 SR$="@=Pause":VE=.5:HO=.55:GOSUB 650
2300 SR$=".=Laenge*1.5":VE=.6:GOSUB 650
2310 SR$="*=Sonderfkt.":VE=.7:GOSUB 650
2320 SR$="M=MENU":VE=.8:GOSUB 650
2330 T$="":P$="":D$="":HO=.02:VE=.8
2340 FOR I=1 TO W:X=0
2350 SR=I:GOSUB 300:T$=SR$+". Ton: ":IF X=1 THEN 9010
2360 IF M(I)+N(I)<>0 THEN Y=0:GOTO 2560
2370 P$="":D$="":GOSUB 1600
2380 Y=1:V=0:GOSUB 210:P=IN
2390 IF P=29 THEN 2650
2400 IF (P=28) AND (I>1) THEN I=I-1:GOTO 2350
2410 IF P=42 THEN X=0:GOSUB 1330:GOTO 2350
2420 IF P=77 THEN 9010
2430 IF P=64 THEN P=.5:GOTO 2480
2440 IF (P>57) AND (P<65) THEN 2470
2450 IF P>64 THEN P=P-7
2460 P=P-48:IF P=-48 THEN 2380
2470 IF (P<1) OR (P>13) THEN GOSUB 1620:GOTO 2380
2480 M(I)=P
2490 P$=IN$:GOSUB 1600
2500 GOSUB 210:D=IN-48:IF IN=77 THEN M(I)=0:GOTO 9010
2510 IF IN<>46 THEN 2530
2520 V=10:D$=" .":GOSUB 1600:GOTO 2500
2530 IF (D=1) OR (D=2) OR (D=4) OR (D=8) THEN 2550
2540 GOSUB 1620:GOTO 2500
2550 N(I)=D+V
2560 SR=N(I):IF SR>10 THEN SR=N(I)-10
2570 GOSUB 300:IF N(I)>10 THEN SR$=SR$+"."
2580 D$=SR$:P=M(I):IF P>9 THEN P=P+7
2590 IF P=.5 THEN P=64:GOTO 2610
2600 P=P+48
2610 P$=CHR$(P):GOSUB 1600
2620 SD=N(I):IF N(I)>10 THEN SD=(N(I)-10)*.75
2630 SD=10/SD:IF P=64 THEN SV=0
2640 SP=Q(M(I)):GOSUB 400:SV=15
2650 IF Y=0 THEN Y=1:GOTO 2380
2660 IF I=W THEN VE=.9:GOSUB 1550:I=I-1
2670 NEXT I
3000 REM PLAY
3010 GOSUB 100:PRINT" ### Abspielen ###"
3020 PRINT:PRINT:PRINT" Tonlage (0...9) : ";
3030 GOSUB 210:L=IN-48:IF IN=77 THEN 9010
3040 IF (L<0) OR (L>9) THEN GOSUB 1620:GOTO 3030
3050 L=(L-5)*7:PRINT IN$
3060 PRINT:PRINT" Tempo   (0...9) : ";
3070 GOSUB 210:T=IN-48:IF IN=77 THEN 9010
3080 IF (T<0) OR (T>9) THEN GOSUB 1620:GOTO 3070
3090 PRINT IN$:PRINT:PRINT:T=T*4+1
3100 SV=15:FOR I=1 TO W
3110 GOSUB 200:IF IN=77 THEN X=0:I=W:GOTO 3160
3120 IF (M(I)+N(I)=0) OR (IN=77) THEN X=I:I=W:GOTO 3160
3130 SD=N(I):IF SD>10 THEN SD=(SD-10)*.75
3140 SD=T/SD:IF M(I)=.5 THEN SV=0
3150 SP=Q(M(I))+L:GOSUB 400:SV=15
3160 NEXT I:IF X=0 THEN 9010
3170 IF X>1 THEN 3200
3180 PRINT"kein Lied im Speicher!"
3190 GOSUB 1620:SD=10:GOSUB 450:GOTO 9010
3200 HO=0:VE=15:GOSUB 110:PRINT"wiederholen? J/(N)";
3210 GOSUB 210:IF IN<>74 THEN 9010
3220 GOSUB 110:PRINT"                      "
3230 GOTO 3100
4000 REM DUR
4010 GOSUB 1220:SR$="### T O N A R T ###"
4020 HO=.02:VE=.02:GOSUB 650:SR$=""
4030 X=1:FOR I=0 TO 1 STEP .15
4040 VE=.07:HO=I:GOSUB 620
4050 VE=.5:GOSUB 630:IF X>6 THEN 4090
4060 SR=X:GOSUB 300:HO=HO+.07:VE=.45:GOSUB 650
4070 ON X GOSUB 1270,1280,1290,1300,1310,1320
4080 VE=.35:HO=HO-.05:SR$=A$+"-Dur":GOSUB 650
4090 X=X+1:NEXT I:SR$="#"
4100 VE=.086:HO=.22:GOSUB 650:HO=.35:GOSUB 650
4110 HO=.49:GOSUB 650:VE=.14:HO=.56:GOSUB 650
4120 VE=.16:HO=.4:GOSUB 650:HO=.52:GOSUB 650
4130 SR$="b":VE=.116:HO=.85:GOSUB 650
4140 VE=.186:HO=.67:GOSUB 650:HO=.8:GOSUB 650
4150 HO=.02:VE=.7:SR$="(Eingestellt: "+F$+")":GOSUB 650
4160 VE=.6:SR$="     AUSWAHL:?":HO=.02:VE=.6:GOSUB 650
4170 GOSUB 210:F=IN-48:IF IN=77 THEN 9010
4180 IF (F<1) OR (F>6) THEN GOSUB 1620:GOTO 4170
4190 GOSUB 1200:ON F GOSUB 1270,1280,1290,1300,1310,1320
4200 F$=A$+"-Dur":SR$=SR$+F$:GOSUB 650:SD=20:GOSUB 450
4210 ON F GOTO 9010,4240,4230,4220,4260,4250
4220 Q(2)=Q(2)+1:Q(9)=Q(9)+1
4230 Q(1)=Q(1)+1:Q(8)=Q(8)+1
4240 Q(4)=Q(4)+1:Q(11)=Q(11)+1:GOTO 4270
4250 Q(3)=Q(3)-1:Q(10)=Q(10)-1
4260 Q(7)=Q(7)-1
4270 GOTO 9010
5000 REM LOAD
5010 GOSUB 100:PRINT:PRINT" ### Laden ###"
5020 GOSUB 1120:IF X=0 THEN GOSUB 1620:GOTO 5010
5030 IF X=1.5 THEN 9010
5040 NF=INT((X-1)*2+.5):PRINT IN$:PRINT:PRINT
5050 PRINT" wirklich? J/(N)";:GOSUB 1620
5060 GOSUB 210:IF IN<>74 THEN 9010
5070 INPUT" Name:";A$:IF (A$="m") OR (A$="M") THEN 9010
5080 NF$=A$:GOSUB 100:GOSUB 500
5090 FOR I=0 TO W
5100 GOSUB 540:M(I)=VAL(IN$):IF I=0 THEN A$=IN$
5110 GOSUB 540:N(I)=VAL(IN$):IF I=0 THEN M$=IN$
5120 NEXT I:GOSUB 580:PRINT:PRINT
5130 PRINT"Name: >";A$;"<"
5140 PRINT:PRINT"Tonart: > ";M$;" <"
5150 GOSUB 1100:GOTO 9010
6000 REM SAVE
6010 GOSUB 100:PRINT:PRINT" ### Retten ###"
6020 GOSUB 1120:IF X=0 THEN GOSUB 1620:GOTO 6010
6030 IF X=1.5 THEN 9010
6040 NF=INT(X*2-.5):PRINT IN$:PRINT:PRINT
6050 INPUT" Name:";A$:IF (A$="m") OR (A$="M") THEN 9010
6060 NF$=A$:GOSUB 100:GOSUB 500
6070 FOR I=0 TO W
6080 SR=M(I):GOSUB 300:IF I=0 THEN SR$=A$
6090 GOSUB 560:SR=N(I):GOSUB 300:IF I=0 THEN SR$=F$
6100 GOSUB 560:NEXT I
6110 GOSUB 580:GOTO 9010
7000 REM HELP
7010 GOSUB 100:PRINT:PRINT" ### H I L F E ###"
7020 PRINT:PRINT
7030 PRINT" Wer  notenunkundig  ist, aber"
7040 PRINT"seinem Kind  z.B.  die  Lieder"
7050 PRINT"aus dem >BUMMI<-Heft vorsingen"
7060 PRINT"will, kann das jetzt mit Hilfe"
7070 PRINT"dieses Programms tun."
7080 PRINT"Man braucht nur  das Notenbild"
7090 PRINT"mit der  Grafik zu vergleichen"
7100 PRINT"und  die richtigen Nummern an-"
7110 PRINT"hand  der  Symbole  der  Noten"
7120 PRINT"und der Tonart einzugeben."
7130 PRINT"Es ist guenstig die Noten vor-"
7140 PRINT"her auf dem  Blatt  zu  numme-"
7150 PRINT"rieren,  um  bei  der  Eingabe"
7160 PRINT"nicht durcheinander zu kommen.":GOSUB 1100
7170 PRINT"1.Eingabe/Aendern"
7180 PRINT"-----------------":PRINT
7190 PRINT"M = Sprung ins MENU (gilt im"
7200 PRINT"ganzen Prog. ausser >Hilfe<)"
7210 PRINT"1.Ziffer = Tonhoehe  (1...D)"
7220 PRINT"2.Ziffer = Tonlaenge (1,2,4,8)"
7230 PRINT"@ als 1.Ziffer = kein Ton/Pause"
7240 PRINT". vor 2.Ziffer = Laenge * 1.5"
7250 PRINT"  (wenn hinter der Note ein  .)"
7260 PRINT"CUR      = naechster Ton"
7270 PRINT"CUL      = 1 Ton zurueck"
7280 PRINT"*        = Sonderfunktion"
7290 PRINT" * CUR = Liedende"
7300 PRINT" * CUL = Liedanfang"
7310 PRINT" *   A = Anfangsmarke"
7320 PRINT" *   E = Endemarke"
7330 PRINT" *   K = Kopieren des markier-"
7340 PRINT"ten Teils ab aktuelle Position":GOSUB 1100
7350 PRINT" Mit den Kursortasten kann ein"
7360 PRINT"schneller  Vor-/Ruecklauf  er-"
7370 PRINT"erzeugt werden. Der angezeigte"
7380 PRINT"Ton  wird  durch  Eingabe  der"
7390 PRINT"beiden  Ziffern ueberschrieben"
7400 PRINT"oder bleibt bei Kursorbetaeti-"
7410 PRINT"gung erhalten."
7420 PRINT"Befindet  sich  auf dem Noten-"
7430 PRINT"blatt in einem Takt ein > oder"
7440 PRINT"aehnl., dann ist der  Takt  an"
7450 PRINT"dieser Stelle mit @ aufzufuel-"
7460 PRINT"len."
7470 PRINT"Das Kopieren dient zum  Zusam-"
7480 PRINT"menstellen  ganzer  Lieder mit"
7490 PRINT"mehreren Strophen und Refrain,"
7500 PRINT"kann aber  auch  zum  Loeschen"
7510 PRINT"benutzt werden.  Bei   *   CUR"
7520 PRINT"wird  der  erste  nichtbelegte"
7530 PRINT"Ton als Liedende gewertet.":GOSUB 1100
7540 PRINT"2.Abspielen"
7550 PRINT"-----------":PRINT
7560 PRINT"Tonlage 0=Tief    9=HOCH"
7570 PRINT"Tempo   0=Schnell 9=Langsam":PRINT
7580 PRINT"Normale Tonlage = 5"
7590 PRINT"Normales Tempo  = 3...5":PRINT
7600 PRINT" Die Tonlage  wird  jeweils um"
7610 PRINT"eine Oktave verstellt.  In ex-"
7620 PRINT"tremen  Tonlagen   ( <2 / >8 )"
7630 PRINT"kommt  es evtl. zum Missklang.":GOSUB 1100
7640 PRINT"3. Tonart":PRINT"---------":PRINT
7650 PRINT"Es werden die Symbole des  No-"
7660 PRINT"tenblattes (vor  dem  1. Takt)"
7670 PRINT"mit den  Symbolen  der  Grafik"
7680 PRINT"verglichen  und  die  entspre-"
7690 PRINT"chende Ziffer (1-6) gedrueckt."
7700 PRINT" Je nach  eingegebener  Tonart"
7710 PRINT"werden die  Toene  umgestimmt."
7720 PRINT"Im Grundzustand ist C-Dur ein-"
7730 PRINT"gestellt.  Das  Einstellen der"
7740 PRINT"Tonart  ist  erforderlich,  um"
7750 PRINT"ein tonrichtiges Abspielen des"
7760 PRINT"Liedes zu erreichen.  Man kann"
7770 PRINT"dies bei einer  einfachen Ton-"
7780 PRINT"leiter (C-Dur) mit verschiede-"
7790 PRINT"Tonarten ausprobieren.":GOSUB 1100
7800 PRINT"4.Laden":PRINT"-------":PRINT
7810 PRINT" Beim Laden wird ein evtl. ge-"
7820 PRINT"speichertes  Lied ueberschrie-"
7830 PRINT"ben.  Deshalb  erfolgt  vorher"
7840 PRINT"eine  Sicherheitsabfrage.  Die"
7850 PRINT"angezeigte Tonart muss vor dem"
7860 PRINT"Abspielen  eingestellt werden.":GOSUB 1100
7870 PRINT"5.Retten":PRINT"--------":PRINT
7880 PRINT" Das Lied wird unabhaengig von"
7890 PRINT"der eingestellten  Tonart  ge-"
7900 PRINT"savt. Die zuletzt eingestellte"
7910 PRINT"Tonart wird beim  erneuten La-"
7920 PRINT"als Empfehlung angezeigt.":GOSUB 1100
7930 PRINT"7.Ende":PRINT"------":PRINT
7940 PRINT" Diese Funktion kann  auch zum"
7950 PRINT"schnellen Loeschen eins Liedes"
7960 PRINT"genutzt werden.":GOSUB 1100:GOTO 9020
8000 REM ENDE
8010 PRINT"  Wirklich? J/(N)";:GOSUB 1620:GOSUB 210
8020 IF IN<>74 THEN 9010
8030 GOTO 950
9000 REM MENU
9010 GOSUB 100
9020 PRINT:PRINT" Programm ### B U M M I ###"
9030 PRINT:PRINT" M E N U"
9040 PRINT" -------":PRINT
9050 PRINT" 1 = Eingabe/Aendern":PRINT
9060 PRINT" 2 = Abspielen":PRINT
9070 PRINT" 3 = Tonart":PRINT
9080 PRINT" 4 = Laden":PRINT
9090 PRINT" 5 = Retten":PRINT
9100 PRINT" 6 = Hilfe":PRINT
9110 PRINT" 7 = Ende":PRINT
9120 PRINT" AUSWAHL:";
9130 GOSUB 210:X=IN-48
9140 IF (X<1) OR (X>7) THEN GOSUB 1620:GOTO 9010
9150 ON X GOTO 2010,3010,4010,5010,6010,7010,8010
25000 REM DATA 
25010 DATA 55,1,15,52,1,15,0,3,0,52,1,15,53,1,15
25020 DATA 55,1,15,64,1,15,0,1,0,64,1,15,0,1,0,60,3,15
25030 DATA 48,50,52,53,55,57,59,60,62,64,65,67,69
25040 DATA 1,8,2,3,4,4,5,2,6,7,1,8,9,"A","B","C","D"
30000 REM W=ANZAHL DER TOENE
30010 REM Z=TEMPO FEHLERMELUNG
30020 REM (ANSTELLE GOSUB 250)
30030 REM BEIDES KANN IN ZEILE
30040 REM 1010 GEAENDERT WERDEN
32000 REM 
32010 REM **********************
32020 REM *                    *
32030 REM *   ERARBEITET AUF   *
32040 REM *  KC85/3 UND C 116  *
32050 REM *                    *
32060 REM *    C 25.05.1990    *
32070 REM *                    *
32080 REM *  STEFFEN HANTZSCH  *
32090 REM *  KRENKELSTR.   15  *
32100 REM *  DRESDEN     8019  *
32110 REM *                    *
32120 REM **********************
32130 REM
32140 REM Erstausstrahlung: Deutschlandsender Kultur
32150 REM -spezial 910306

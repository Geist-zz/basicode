1000 A=2000:GOTO 20:REM NOTENBALK
1010 DIM TN$(14),NO$(96),TE$(96)
1020 GOSUB 9480:REM --- tonentabel ---
1030 NT$=" ":FG$=" ":KM$=" ":W$=" "
1040 PT$=" ":MP=0:NO$(0)="X"
1050 P$="":D$="":V$="4":VG$="4":M$="4"
1060 FG=0:T=0:U=0:VM=0:VA=0
1070 GOSUB 8010:REM --- titel ---
1080 GOSUB 600:CN=0
1090 GOSUB 5540:REM --- muziekscherm ---
1100 REM
1110 REM === HOOFDPROGRAMMA ===
1120 IF NO$(0)="X" THEN NO$(0)="Y":GOSUB 9260
1130 IF IN$="#" THEN GOSUB 9290:GOTO 1170
1140 IF IN=66 THEN IN$="b":GOSUB 9290:GOTO 1170
1150 GOTO 1210
1160 REM --- 1e toets kiest subr.---
1170 GOSUB 210
1180 IF IN=72 THEN GOSUB 8120:IN=84
1190 REM -- Cursor,spatiebalk,delete --
1200 IF IN=32 THEN IN=29
1210 IF (IN>27) AND (IN<32) THEN GOSUB 1530:GOTO 1170
1220 IF IN=127 THEN IN=28:GOSUB 1530:GOSUB 3250
1230 REM -- verlengnoot --
1240 IF IN=43 THEN T=2:GOSUB 1740:GOTO 1500
1250 REM --- Noot ---
1260 IF (IN$>="A") AND (IN$<"H") THEN FG=0:GOSUB 1740:GOTO 1500
1270 IF (IN$>="a") AND (IN$<"h") THEN FG=1:GOSUB 1740:GOTO 1500
1280 REM --- Maataanduiding ---
1290 IF IN=47 THEN GOSUB 9400:GOTO 1170
1300 REM --- Invoegen ---
1310 IF IN=73 THEN GOSUB 7010:GOTO 1170
1320 REM --- Maatstreep ---
1330 IF IN=77 THEN IN$="M":GOSUB 2660:GOSUB 1590:GOTO 1170
1340 REM --- Rust ---
1350 IF IN=82 THEN IN$="R":GOSUB 2730:GOSUB 1590:GOTO 1200
1360 REM --- Speel ---
1370 IF IN=83 THEN GOSUB 5010:GOTO 1170
1380 REM --- Teken opnieuw ---
1390 IF IN=84 THEN GOSUB 600:GOSUB 5540:GOSUB 5820:GOTO 1480
1400 REM --- Uithalen ---
1410 IF IN=85 THEN GOSUB 7010:GOTO 1170
1420 REM --- Volume ---
1430 IF IN=86 THEN GOSUB 3200:GOTO 1170
1440 REM --- Wis op cursor ---
1450 IF IN=87 THEN GOSUB 3250:GOTO 1170
1460 REM --- Load/Save ---
1470 IF IN$="*" THEN GOSUB 5210:IN=84:GOTO 1390
1480 IF NO$(0)="X" THEN 1120
1490 GOTO 1170
1500 GOSUB 1590:GOTO 1170
1510 REM
1520 REM === CURSOR ===
1530 CU=IN-27:ON CU GOTO 1540,1590,1640,1680
1540 IF NO=1 THEN RETURN
1550 CN=1:GOSUB 1710:REM wis
1560 NO=NO-1:HC=HC-.04
1570 IF (HC<.05) AND (VC>.2) THEN HC=.98:VC=VC-.24
1580 GOTO 1710:REM print opnieuw
1590 IF NO=96 THEN RETURN
1600 CN=1:GOSUB 1710:REM wis
1610 NO=NO+1:HC=HC+.04
1620 IF (HC>.99) AND (VC<.9) THEN HC=.06:VC=VC+.24
1630 GOTO 1710:REM print opnieuw
1640 IF NO>72 THEN RETURN
1650 CN=1:GOSUB 1710:REM wis
1660 NO=NO+24:VC=VC+.24
1670 GOTO 1710:REM print opnieuw
1680 IF NO<25 THEN RETURN
1690 CN=1:GOSUB 1710:REM wis
1700 NO=NO-24:VC=VC-.24
1710 HO=HC-.02:VE=VC-.01:SR$=".":GOSUB 650:CN=0
1720 RETURN
1730 REM === NOOT ===
1740 P$="":IF T<2 THEN 1770
1750 IF VA=0 THEN T=0:RETURN
1760 VN=VA:GOTO 1870
1770 SR=FG:GOSUB 300:FG$=SR$
1780 SR=IN-65:GOSUB 300:NT$=SR$
1790 REM --- hulplijntjes --
1800 IF (FG=1) OR (IN>67) THEN 1850
1810 HO=HC-.03:VE=VC-.03:GOSUB 620
1820 HO=HC:GOSUB 630
1830 IF IN>65 THEN 1850
1840 HO=HC-.03:VE=VC:GOSUB 620:HO=HC:GOSUB 630
1850 P=IN-65:VN=VC-(FG*.105)-(P*.015)
1860 P$=TN$(1+P+7*FG):VA=VN
1870 HN=HC:IF T<2 THEN 1950
1880 REM --- verlengboogje ---
1890 HN=HC:HO=HN:VE=VN+.02:GOSUB 620
1900 HO=HO-.02:VE=VE+.02:GOSUB 630
1910 HO=HO-.02:GOSUB 630
1920 HO=HO-.02:VE=VE-.02:GOSUB 630
1930 IF D$="+" THEN RETURN
1940 REM --- teken noot ---
1950 HO=HN:VE=VN-.01:GOSUB 620
1960 HO=HN-.01:GOSUB 630
1970 HO=HO-.01:VE=VE+.01:GOSUB 630
1980 VE=VE+.01:GOSUB 630
1990 HO=HO+.01:GOSUB 630
2000 HO=HO+.01:VE=VE-.01:GOSUB 630
2010 VE=VE-.01:GOSUB 630
2020 IF T=1 THEN RETURN
2030 KM$=" "
2040 GOSUB 210
2050 IF IN$="#" THEN KM$=IN$:GOSUB 2540:GOTO 2040
2060 IF IN=66 THEN KM$="b":GOSUB 2550:GOTO 2040
2070 IF IN$="1" THEN W$=IN$:D$="16":GOTO 2340
2080 IF IN$="2" THEN D$="08":GOTO 2140
2090 IF IN$="4" THEN D$="04":GOTO 2140
2100 IF IN$="8" THEN D$="02":GOTO 2140
2110 IF IN$="6" THEN D$="01":GOTO 2140
2120 GOSUB 250:GOTO 2040
2130 REM ---stokje---
2140 W$=IN$:HO=HN:VE=VN-.06:GOSUB 620
2150 VE=VE+.06:GOSUB 630
2160 IF IN$="2" THEN 2340
2170 REM ---vullen---
2180 HZ=HN-.01:VE=VN-.01
2190 HO=HN:VE=VE+1/VG:GOSUB 620
2200 HZ=HZ-1/VG:HO=HZ:GOSUB 630
2210 IF VE<VN THEN 2190
2220 HZ=HN-.01:VE=VN+.01
2230 HO=HN-.02:VE=VE-1/VG:GOSUB 620
2240 HZ=HZ+1/VG:HO=HZ:GOSUB 630
2250 IF VE>VN THEN 2230
2260 IF IN$="4" THEN 2340
2270 REM 1e vlag
2280 HO=HN:VE=VN-.06:GOSUB 620
2290 HO=HO+.01:VE=VE+.02:GOSUB 630
2300 IF IN$="8" THEN 2340
2310 REM 2e vlag
2320 HO=HO-.01:GOSUB 620
2330 HO=HO+.01:VE=VE+.02:GOSUB 630
2340 IF T=1 THEN RETURN
2350 GOSUB 210
2360 REM --- EVT punt of maatstreep ---
2370 IF IN$="." THEN 2410
2380 IF IN=77 THEN 2490:REM M
2390 PT$=" ":GOTO 2510
2400 REM --- verleng-punt ---
2410 HO=HN+3/HG:VE=VN-2/VG:GOSUB 620
2420 HO=HO+1/HG:GOSUB 620
2430 IF D$="02" THEN D$="03"
2440 IF D$="04" THEN D$="06"
2450 IF D$="08" THEN D$="12"
2460 IF D$="16" THEN D$="24"
2470 PT$=".":GOTO 2510
2480 REM --- compacte maatstreep ---
2490 VM=.015:GOSUB 2660:VM=0
2500 PT$="M"
2510 IF T=1 THEN RETURN
2520 GOSUB 3060:RETURN
2530 REM === kruis of mol ===
2540 IF IN$="#" THEN SR=VAL(P$)+1:GOSUB 300:P$=SR$:GOTO 2560
2550 IF IN=66 THEN IN$="b":SR=VAL(P$)-1:GOSUB 300:P$=SR$
2560 HO=HN-.05:VE=VN-.02:SR$=IN$
2570 IF IN$="#" THEN GOSUB 650
2580 IF IN$="b" THEN GOSUB 2610
2590 HO=HN:VE=VN-.01:RETURN
2600 REM === teken  mol ===
2610 GOSUB 620:VE=VE+.04:GOSUB 630
2620 HO=HO+.015:VE=VE-.01:GOSUB 630
2630 VE=VE-.01:GOSUB 630
2640 HO=HO-.015:GOSUB 630:RETURN
2650 REM === MAATSTREEP ===
2660 HO=HC+VM
2670 VE=VC-.06:GOSUB 620:VE=VE-.12:GOSUB 630
2680 IF VM>0 THEN RETURN
2690 IF T=1 THEN RETURN
2700 NO$(NO)="":TE$(NO)="M    ":IF NO>MP THEN MP=NO
2710 RETURN
2720 REM === RUST ===
2730 NT$=IN$:M$=V$
2740 GOSUB 210:IF (IN<49) OR (IN>56) THEN GOSUB 250:GOTO 2730
2750 IF (IN=51) OR (IN=53) OR (IN=55) THEN GOSUB 250:GOTO 2730
2760 V$="0":W$=IN$
2770 IF IN>49 THEN 2830:REM 1/1
2780 VR=VC-.12:HR=HC-.02
2790 VR=VR+1/VG:HO=HC:VE=VR:GOSUB 620
2800 HO=HR:GOSUB 630
2810 IF VE<VC-.11 THEN 2790
2820 D$="16":GOTO 3020
2830 IF IN>50 THEN 2890:REM 1/2
2840 VR=VC-.12:HR=HC-.02
2850 VR=VR-1/VG:HO=HC:VE=VR:GOSUB 620
2860 HO=HR:GOSUB 630
2870 IF VE>VC-.13 THEN 2850
2880 D$="08":GOTO 3020
2890 IF IN>52 THEN 2960:REM 1/4
2900 VE=VC-.16:HO=HC-.02:GOSUB 620
2910 VE=VE+.02:HO=HO+.02:GOSUB 630
2920 VE=VE+.01:HO=HO-.01:GOSUB 630
2930 VE=VE+.02:GOSUB 630
2940 VE=VE+.01:HO=HO-.01:GOSUB 630
2950 D$="04":GOTO 3020
2960 HO=HC-.01:VE=VC-.1:REM 1/8
2970 GOSUB 620:HO=HO+.02:VE=VE-.04:GOSUB 630
2980 HO=HO-.02:GOSUB 630
2990 IF IN>54 THEN D$="02":GOTO 3020
3000 VE=VE+.03:GOSUB 620:REM 1/16
3010 HO=HO+.015:GOSUB 630:D$="01"
3020 IF T=1 THEN RETURN
3030 GOSUB 3060:RETURN
3040 REM === OPSLAG ===
3050 REM --- muziektoon ---
3060 IF T<2 THEN 3090
3070 X=VAL(NO$(LA))+10*VAL(D$)
3080 SR=X:GOSUB 300:NO$(LA)=SR$:GOTO 3140
3090 LA=NO:NX$=NT$:FX$=FG$
3100 NO$(NO)=P$+D$+V$:IF V$="0" THEN VA=0
3110 V$=M$
3120 N=NO:GOSUB 5110
3130 REM --- tekendata ---
3140 IF T=2 THEN NT$=NX$:FG$=FX$:KM$="+"
3150 TE$(NO)=NT$+FG$+KM$+W$+PT$
3160 NT$=" ":KM$=" ":W$=" ":PT$=" ":T=0
3170 IF NO>MP THEN MP=NO
3180 RETURN
3190 REM === VOLUME ===
3200 GOSUB 210:IF (IN<49) OR (IN>55) THEN GOSUB 250:GOTO 3200
3210 CN=1:HO=0:VE=0:SR$=VG$:GOSUB 650
3220 CN=0:VG$=IN$:SR$=IN$:GOSUB 650
3230 V$=IN$:M$=IN$:RETURN
3240 REM === WIS OP CURSORPOSITIE ===
3250 TE$(NO)="":NO$(NO)=""
3260 HW=HC-.03:VW=VC+.01:TW=VC-.26
3270 CN=1:HW=HW+1/HG:HO=HW
3280 VE=VW:GOSUB 620:VE=TW:GOSUB 630
3290 CN=0
3300 VE=VC-.18:GOSUB 620
3310 VE=VC-.15:GOSUB 620
3320 VE=VC-.12:GOSUB 620
3330 VE=VC-.09:GOSUB 620
3340 VE=VC-.06:GOSUB 620
3350 IF HW<HC+.02 THEN 3270
3360 RETURN
5000 REM === SPEEL MUZIEK ===
5010 FOR N=0 TO MP
5020 IF NO$(N)="" THEN 5040
5030 GOSUB 5110
5040 NEXT N
5050 RETURN
5100 REM === SPEEL 1 NOOT ===
5110 SP=VAL(MID$(NO$(N),1,2))
5120 SD=VAL(MID$(NO$(N),3,2))
5130 SV=VAL(MID$(NO$(N),5,1))*2
5140 GOSUB 400
5150 RETURN
5200 REM === * MENU ===
5210 GOSUB 100:PRINT
5220 SR$="M E N U":GOSUB 150:PRINT:PRINT
5230 PRINT" 1 = UITLEG":PRINT
5240 PRINT" 2 = TERUG NAAR MUZIEKSCHERM":PRINT
5250 PRINT" 3 = EEN NAAM PLAATSEN":PRINT
5260 PRINT" 4 = SAVEN VAN MUZIEK (CASSETTE)":PRINT
5270 PRINT" 5 = SAVEN VAN MUZIEK (DISK)":PRINT
5280 PRINT" 6 = LADEN VAN MUZIEK (CASSETTE)":PRINT
5290 PRINT" 7 = LADEN VAN MUZIEK (DISK)":PRINT
5300 PRINT" 8 = MUZIEKGEHEUGEN WISSEN":PRINT
5310 PRINT" 9 = STOPPEN MET DIT PROGRAMMA":PRINT
5320 PRINT" Tik een cijfer ";:GOSUB 210
5330 IF (IN<49) OR (IN>57) THEN 5320
5340 GOSUB 100:PRINT:PRINT" MOMENT"
5350 IF IN=49 THEN GOSUB 8120:GOTO 5210
5360 IF IN=50 THEN RETURN
5370 IF IN=56 THEN GOSUB 9480:NO=1:NO$(0)="X":MP=0:RETURN
5380 IF IN=57 THEN 950
5390 ON IN-50 GOSUB 5510,9040,9010,9160,9130
5400 RETURN
5500 REM === NAAM ===
5510 GOSUB 100:PRINT"GEEF DE NAAM":PRINT
5520 INPUT A$:TE$(0)=A$:RETURN
5530 REM === NOTENBALKEN ===
5540 FOR N=.08 TO .81 STEP .24:HO=0
5550 VE=N:GOSUB 620:VE=VE+.12:GOSUB 630
5560 FOR M=0 TO .121 STEP .03
5570 HO=0:VE=M+N:GOSUB 620
5580 HO=.999:GOSUB 630
5590 NEXT M:VE=VE-.12:GOSUB 630:NEXT N
5600 IF U=1 THEN U=0:RETURN
5610 REM --- sleutels ---
5620 FOR N=.23 TO .95 STEP .24
5630 HO=.02:VE=N:GOSUB 620
5640 HO=HO+6E-3:VE=VE-4E-3:GOSUB 630
5650 HO=HO-8E-3:VE=VE-.15:GOSUB 630
5660 HO=HO+8E-3:VE=VE-5E-3:GOSUB 630
5670 HO=HO+8E-3:VE=VE+8E-3:GOSUB 630
5680 HO=HO-.03:VE=VE+.09:GOSUB 630
5690 VE=VE+.02:GOSUB 630
5700 HO=HO+.01:VE=VE+.02:GOSUB 630
5710 HO=HO+.02:GOSUB 630
5720 HO=HO+.01:VE=VE-.02:GOSUB 630
5730 VE=VE-.02:GOSUB 630
5740 HO=HO-.01:VE=VE-.01:GOSUB 630
5750 HO=HO-.01:GOSUB 630
5760 HO=HO-.01:VE=VE+.02:GOSUB 630
5770 NEXT N
5780 REM ---init. cursor ---
5790 HC=.06:VC=.26:NO=1:GOSUB 1710
5800 RETURN
5810 REM === TEKEN MUZIEK UIT TE$() ==
5820 HO=.3:VE=0:SR$=TE$(0):GOSUB 650
5830 T=1:D$=NO$(0):IF LEN(D$)=1 THEN IN$=D$:GOTO 5860
5840 A$=MID$(D$,2,1):B$=MID$(D$,3,1):C$=MID$(D$,1,1)
5850 IN$="/":GOSUB 9400:IN$=C$
5860 IF (IN$="#") OR (IN$="b") THEN GOSUB 9290
5870 IF MP=0 THEN T=0:RETURN
5880 AA=1:BB=MP
5890 FOR X=AA TO BB:A$=TE$(X)
5900 IF TE$(X)="" THEN 5920
5910 GOSUB 6010
5920 GOSUB 1590:NEXT X:T=0
5930 RETURN
6000 REM === NOTEN TEKEN ROUTINE ===
6010 B$=MID$(A$,1,1):C$=MID$(A$,2,1)
6020 D$=MID$(A$,3,1):E$=MID$(A$,4,1):F$=MID$(A$,5,1)
6030 IF B$="M" THEN GOSUB 2660:RETURN
6040 IF B$="R" THEN IN=VAL(E$)+48:GOSUB 2770:RETURN
6050 IF (B$<"0") OR (B$>"7") THEN RETURN
6060 IN=VAL(B$)+65:FG=VAL(C$):GOSUB 1800
6070 IF D$="#" THEN IN$="#":GOSUB 2540
6080 IF D$="b" THEN IN$="b":GOSUB 2550
6090 IF D$="+" THEN GOSUB 1890
6100 IN$=E$:IF E$>"1" THEN GOSUB 2140
6110 IN$=F$:IF IN$="M" THEN IN=77
6120 GOSUB 2370
6130 RETURN
7000 REM === INVOEGEN/UITHALEN ===
7010 IF NO=72 THEN RETURN
7020 BA=INT((NO+23)/24):TP=24*BA:MM=NO:VV=VC:HH=HC
7030 CN=1:VE=VC-.26:REM --wis notenb.--
7040 HO=HC-.03:GOSUB 620
7050 HO=1:GOSUB 630:VE=VE+1/VG
7060 IF VE<VC+.02 THEN 7040
7070 CN=0:U=1:GOSUB 5540:REM teken nb.--
7080 IF IN=85 THEN 7140
7090 FOR X=TP TO MM+1 STEP -1:REM --arrays
7100 TE$(X)=TE$(X-1):NO$(X)=NO$(X-1)
7110 NEXT X:TE$(MM)="":NO$(MM)=""
7120 IF MP<TP THEN MP=MP+1
7130 GOTO 7180
7140 FOR X=MM TO TP-1
7150 TE$(X)=TE$(X+1):NO$(X)=NO$(X+1)
7160 NEXT X:TE$(TP)="":NO$(TP)=""
7170 IF MP<TP+1 THEN MP=MP-1
7180 AA=MM:BB=TP:T=1:GOSUB 5890
7190 CN=1:GOSUB 1710:CN=0
7200 NO=MM:HC=HH:VC=VV:GOSUB 1710:RETURN
8000 REM === TITEL EN UITLEG ===
8010 GOSUB 100:PRINT:PRINT:PRINT
8020 A$="                 "
8030 SR$=A$:GOSUB 150:PRINT
8040 SR$="N O T E N B A L K":GOSUB 150:PRINT
8050 SR$=A$:GOSUB 150:PRINT
8060 PRINT:PRINT:PRINT
8070 PRINT" MUZIEKNOTEN-PROGRAMMA":PRINT:PRINT
8080 PRINT" IN BASICODE-3 FORMAAT"
8090 PRINT:PRINT:PRINT:PRINT:PRINT
8100 PRINT" WIL JE UITLEG?   J/N";
8110 GOSUB 210:IF IN<>74 THEN RETURN
8120 GOSUB 100:PRINT
8130 PRINT"Tik muzieknoten als volgt:  eerst":PRINT
8140 PRINT"een van de letters ABCDEFGabcdefg":PRINT
8150 PRINT"daarna  EVENTUEEL  een # of een b":PRINT
8160 PRINT"daarna de  waarde  met EEN cijfer":PRINT
8170 PRINT"1 2 4 8 of 6 voor resp. een hele,":PRINT
8180 PRINT"een halve, een kwart, een achtste":PRINT
8190 PRINT"of een zestiende noot.":PRINT
8200 PRINT"Daarna kan nog een PUNT ingetikt":PRINT
8210 PRINT"worden (verlenging), of een M (=":PRINT
8220 PRINT"maatstreep),tik anders 'n spatie":PRINT
8230 PRINT"Een + maakt een verlengnoot   >>";
8240 GOSUB 210:GOSUB 100:PRINT
8250 PRINT"De  CURSOR  gaat vanzelf naar de":PRINT
8260 PRINT"volgende positie.  De cursor kan":PRINT
8270 PRINT"ook met de  cursortoetsen worden":PRINT
8280 PRINT"verplaatst.":PRINT
8290 PRINT"Een RUST  wordt geplaatst met de":PRINT
8300 PRINT"letter R en een cijfer 1 2 4 8 6":PRINT
8310 PRINT"De M plaatst een MAATSTREEP":PRINT
8320 PRINT"/ en 2 cijfers plaatst een maat-":PRINT
8330 PRINT"aanduiding voor aan de balk":PRINT
8340 PRINT"De S laat de muziek spelen en de":PRINT
8350 PRINT"T tekent alles netjes opnieuw >>";
8360 GOSUB 210:GOSUB 100:PRINT
8370 PRINT"Aan het begin kun je een # of b":PRINT
8380 PRINT"invoeren als voortekening.":PRINT
8390 PRINT"Het volume (geluidssterkte) van":PRINT
8400 PRINT"de muziek kun je veranderen met":PRINT
8410 PRINT"de V en een cijfer van 1 t/m 7.":PRINT
8420 PRINT"Dat geldt dan  voor  alle noten":PRINT
8430 PRINT"die je daarna intikt.":PRINT
8440 PRINT"DOOR EEN  *  TE TIKKEN KRIJG JE":PRINT
8450 PRINT"ALLE INSTRUCTIES VOOR HET SAVEN":PRINT
8460 PRINT"EN LADEN VAN MUZIEK NAAR EN VAN":PRINT
8470 PRINT"CASSETTE OF DISK             >>";
8480 GOSUB 210:GOSUB 100
8490 PRINT
8500 PRINT"Met de W kun je een noot wissen":PRINT
8510 PRINT"die je met de cursor  aanwijst.":PRINT
8520 PRINT"Met de I kun je boven de cursor":PRINT
8530 PRINT"ruimte maken om  een noot in te":PRINT
8540 PRINT"voegen, en met de  U kun je een":PRINT
8550 PRINT"noot uitnemen en al wat  verder":PRINT
8560 PRINT"op de balk staat naar links aan":PRINT
8570 PRINT"schuiven. Zo kun je achteraf de":PRINT
8580 PRINT"muziek nog gemakkelijk wijzigen":PRINT:PRINT
8590 PRINT"DIT KOMT WEER ALS JE EEN H TIKT"
8600 GOSUB 210:IF IN=72 THEN 8120
8610 RETURN
9000 REM === SAVE ===
9010 NF=5:PRINT:PRINT
9020 PRINT" Geef file naam";:INPUT NF$
9030 GOTO 9050
9040 NF=1:NF$="MUZDAT"
9050 IF MP=0 THEN GOSUB 250:RETURN
9060 GOSUB 500
9070 FOR X=0 TO MP
9080 SR$=TE$(X):GOSUB 560
9090 SR$=NO$(X):GOSUB 560
9100 NEXT X:GOSUB 580
9110 RETURN
9120 REM === LOAD ===
9130 NF=4:PRINT:PRINT:PRINT
9140 PRINT" Geef file naam";:INPUT NF$
9150 GOTO 9170
9160 NF=0:NF$="MUZDAT"
9170 GOSUB 9480
9180 GOSUB 500:MP=0
9190 GOSUB 540:TE$(MP)=IN$
9200 GOSUB 540:NO$(MP)=IN$
9210 MP=MP+1
9220 IF IN=0 THEN 9190
9230 GOSUB 580:MP=MP-1
9240 RETURN
9250 REM === VOORTEKENING ===
9260 GOSUB 9270:GOSUB 210:CN=1
9270 HO=0:VE=0:SR$="# of b":GOSUB 650:CN=0
9280 RETURN
9290 SR$=IN$:VN=.12:HO=.04:VE=VN
9300 IF IN$="b" THEN 9330
9310 IF IN$<>"#" THEN RETURN
9320 VN=VN-.055:VE=VE-.055:TN$(6)="66":TN$(13)="78":GOTO 9340
9330 TN$(2)="58":TN$(9)="70"
9340 IF IN$="b" THEN GOSUB 2610
9350 IF IN$="#" THEN GOSUB 650
9360 VN=VN+.24:VE=VN:IF VN<1 THEN 9340
9370 IF T=1 THEN RETURN
9380 NO$(0)=IN$:RETURN
9390 REM === MAATAANDUIDING ===
9400 HO=.06:VE=.13:SR$=IN$:GOSUB 650:IF T=1 THEN 9420
9410 GOSUB 210:A$=IN$:IF (IN<49) OR (IN>57) THEN 9410
9420 VE=VE-.04:SR$=A$:GOSUB 650:IF T=1 THEN 9440
9430 GOSUB 210:B$=IN$:IF (IN<49) OR (IN>57) THEN 9430
9440 VE=VE+.08:SR$=B$:GOSUB 650:IF T=1 THEN RETURN
9450 NO$(0)=MID$(NO$(0),1,1)+A$+B$
9460 RETURN
9470 REM === VUL TONENTABEL ===
9480 RESTORE 
9490 FOR N=1 TO 14:READ A$:TN$(N)=A$:NEXT N
9500 REM --- en leeg de arrays ---
9510 FOR N=0 TO 96:NO$(N)="":TE$(N)="":NEXT N
9520 RETURN
25000 DATA "57","59","60","62","64"
25010 DATA "65","67","69","71","72"
25020 DATA "74","76","77","79"
25030 REM =============================
32000 REM GEMAAKT OP BBC B COMPUTER
32010 REM SEPTEMBER 1987
32020 REM
32030 REM Ch.W.Brederode
32040 REM Von Bonninghausenlaan 33
32050 REM 2161 ES  LISSE
32060 REM
32070 REM TROS-RADIO dd 871209
32080 REM incl corr. dd 880301

1000 a=500:GOTO 20:REM basicode-3 journal 12
1010 ht=ho:vt=ve:s$="":FOR i=1 TO ht:s$=s$+" ":NEXT i
1020 hl=0:hr=ht:vl=2:vr=vt-2:r$="":READ t$
1030 GOSUB 1410:ho=(ht-LEN(sr$)-6)/2:ve=0:GOSUB 110
1040 GOSUB 150:PRINT:PRINT:PRINT
1050 PRINT"Ausdruck ueber"
1060 PRINT:PRINT"   1  den Bildschirm,"
1070 PRINT"   2  den Drucker"
1080 PRINT:PRINT"Welche Nummer ?";:GOSUB 210
1090 sc=in-48:PRINT sc:IF sc<>2 THEN sc=1:GOTO 1220
1100 PRINT:PRINT"Wieviel Leerzeichen"
1110 INPUT"am Zeilenanfang    ";hl
1120 PRINT:PRINT"Wieviel Text-Zeichen"
1130 INPUT"auf jeder Zeile    ";i:hr=hl+i-1
1140 PRINT:PRINT"Wieviel Text-Zeilen"
1150 INPUT"auf jeder Seite    ";vr:vr=vr-3
1160 PRINT"(E) Endlospapier oder  "
1170 PRINT"(L) Lose Blaetter (E/L) ";
1180 GOSUB 210:IF in=76 THEN vl=-1:GOTO 1220
1190 IF in<>69 THEN  GOSUB 250:GOTO 1160
1200 PRINT:PRINT"Wieviel Leerzeilen"
1210 INPUT"am Seitenende      ";vl:vr=vr+vl
1220 mm$="":IF hl>0 THEN mm$=LEFT$(s$,hl)
1230 GOSUB 1380:m$=mm$:al=vl:IF sc=2 THEN  GOSUB 1360
1240 IF LEN(r$)>hr-hl THEN  GOSUB 1820
1250 GOSUB 1440:IF r$<>"" THEN r$=r$+" "
1260 r$=r$+d$:GOTO 1240
1270 IF sc=2 THEN sr$=m$+sr$:GOSUB 350:GOSUB 360:GOTO 1290
1280 GOSUB 120:PRINT m$;sr$;:ve=ve+1:ho=0:GOSUB 110:hl=0
1290 m$=mm$:al=al+1:IF al<=vr THEN RETURN
1300 IF sc=1 THEN 1380
1310 IF vl>=0 THEN FOR i=1 TO vl:GOSUB 360:NEXT i:GOTO 1350
1320 PRINT"Neues Blatt in Drucker einlegen"
1330 PRINT"druecke dann       RETURN/ENTER"
1340 GOSUB 210:IF in<>13 THEN 1340
1350 al=vl
1360 sr$=LEFT$(s$,(hr-hl-LEN(t$))/2)+t$:GOSUB 1270
1370 sr$="":GOSUB 1270:al=vl:RETURN
1380 ho=3:ve=vt:GOSUB 110
1390 sr$="Weiter: druecke <RETURN/ENTER>":GOSUB 150
1400 GOSUB 210:IF in<>13 THEN 1400
1410 GOSUB 100:al=vl:sr$=t$
1420 PRINT LEFT$(s$,(ht-LEN(sr$))/2-3);:GOSUB 150
1430 PRINT:PRINT:RETURN
1440 READ d$
1450 IF d$="" THEN 1440
1460 IF LEFT$(d$,1)="." THEN 1530
1470 IF LEFT$(d$,1)<>">" THEN 1490
1480 d$=LEFT$(s$,23-LEN(r$))+MID$(d$,2):RETURN
1490 IF LEFT$(d$,1)<>"!" THEN RETURN
1500 IF d$="!" THEN d$="":GOTO 1520
1510 d$=MID$(d$,2)
1520 GOSUB 1760:GOTO 1450
1530 IF d$<>".E" THEN 1580
1540 j=1:GOSUB 1760:t$="E N D E"
1550 sr$="":GOSUB 1270:GOSUB 1270:sr$=t$:GOSUB 1270:t$=""
1560 sr$="":GOSUB 1270:IF al<>vl THEN 1560
1570 GOTO 950
1580 dd$=MID$(d$,2,1):d$=MID$(d$,3)
1590 IF dd$<>"R" THEN 1640
1600 IF sc=2 THEN RETURN
1610 IF LEN(r$)+6+LEN(d$)>=hr-hl THEN sr$=r$:GOSUB 1270:r$=""
1620 PRINT m$;r$;:sr$=d$:GOSUB 150:m$=""
1630 hl=hl+LEN(r$)+LEN(d$)+6:r$="":GOTO 1440
1640 IF dd$<>"M" THEN 1670
1650 i=VAL(d$):mm$="":IF i+hl=0 THEN 1440
1660 mm$=LEFT$(s$,i+hl):GOTO 1440
1670 IF dd$="P" THEN j=VAL(d$):GOSUB 1760:GOSUB 1780:GOTO 1440
1680 IF dd$="T" THEN t$=d$:j=10:GOSUB 1760:GOSUB 1780:GOTO 1600
1690 IF dd$="Q" THEN r$=r$+CHR$(34)+d$:GOTO 1440
1700 IF dd$<>"F" THEN 1730
1710 j=VAL(d$):GOSUB 1760
1720 j=j-1:IF j>-1 THEN sr$="":GOSUB 1270:GOTO 1720
1730 IF dd$<>"C" THEN 1750
1740 sr$=LEFT$(s$,(hr-hl-LEN(d$))/2)+d$:GOSUB 1270
1750 GOTO 1440
1760 GOSUB 1820:sr$=r$:GOSUB 1270:r$="":j=j-1
1770 RETURN
1780 IF (sc=1) OR (al+j>vr) THEN 1800
1790 sr$="":GOSUB 1270:GOSUB 1270:RETURN
1800 IF al<>vl THEN sr$="":GOSUB 1270:GOTO 1800
1810 RETURN
1820 k=hr-hl+1-LEN(m$)
1830 IF LEN(r$)<=k THEN RETURN
1840 IF MID$(r$,k,1)=" " THEN 1870
1850 k=k-1:IF k>0 THEN 1840
1860 sr$="":GOTO 1880
1870 sr$=LEFT$(r$,k-1):r$=MID$(r$,k+1)
1880 GOSUB 1270:GOTO 1820
1890 REM
1910 REM uebersetzte version:
1920 REM
1930 REM computer-bulletin no.32 (7/89)
1940 REM stichting basicode, eindhoven
1950 REM---------------------------------
25000 DATA "Programmieren in BC (1)"
25010 DATA "Grundlagen zum Programmieren in BASICODE sind"
25020 DATA "!!- die Programmiersprache BASIC,"
25030 DATA "!- der BASIC-Dialekt des Computers,"
25040 DATA "!- die unter BASICODE erlaubten Anweisungen,"
25050 DATA "Funktionen, Operatoren und Variablennamen,"
25060 DATA "!- die BASICODE-Subroutinen (Unterprogramme) und"
25070 DATA "!- die Regeln zum Aufbau eines Programms."
25080 DATA ".P0"
25090 DATA "Ausgehend vom 'BASICODE-3-Protokoll' soll auf"
25100 DATA "die Besonderheiten hingewiesen werden."
25110 DATA "!!BASIC bzw. BASICODE schliessen strukturiertes"
25120 DATA "Programmieren nicht aus, erzwingen es aber auch"
25130 DATA "nicht, wie z.B. Pascal. Die Gestaltung eines"
25140 DATA "Programms liegt in der Hand des Programmierers."
25150 DATA "Vorhandene BASICODE-Programme zeigen, dass es"
25160 DATA "moeglich ist, Programme zu strukturieren; sie"
25170 DATA "geben Anregungen und Beispiele."
25180 DATA ".P0"
25190 DATA ".RDer Programmaufbau"
25200 DATA "!!Ein Programm besteht aus den Zeilen"
25210 DATA "!!    0 -   999 - BASICODE-Subroutinen,"
25220 DATA "! 1000 - 32767 - BASICODE-Programm."
25230 DATA "!!Der erste Teil (# 0 - 999) wird im Speicher"
25240 DATA "als BASIC- oder Maschinensprache-Programm"
25250 DATA "(durch Laden des Bascoders) abgelegt."
25260 DATA "!Der zweite Teil (# 1000 - 32767) - das"
25270 DATA "BASICODE-Programm - wird mit dem fuer"
25280 DATA "jeden Rechner spezifischen Befehl nachgeladen"
25290 DATA "oder kann frei programmiert werden."
25300 DATA "!!Die Zeilen # 0 - 999 koennen bzw. duerfen"
25310 DATA "nicht veraendert werden!"
25320 DATA ".P0"
25330 DATA "Der Zeilenbereich # 0 - 999 hat folgenden"
25340 DATA "Aufbau:"
25350 DATA "!!    10 GOTO 1000"
25360 DATA "!    20  -  Ueberfuehren des Computers in"
25370 DATA "!           die Betriebsart 'BASICODE'"
25380 DATA "!    25 GOTO 1010"
25390 DATA "!   100 )"
25400 DATA "!    .  (-  Computerspezifische"
25410 DATA "!    .  (   BASICODE-Subroutinen"
25420 DATA "!   650 )"
25430 DATA "!   950  -  Zuruecksetzen in die"
25440 DATA "!           normale Betriebsart"
25450 DATA "!   955 END"
25460 DATA "!!Ist dieser Teil als Maschinenspracheprogramm"
25470 DATA "abgelegt, so bleibt er 'verdeckt'; er ist nicht"
25480 DATA "listbar."
25490 DATA ".P0"
25500 DATA "Das eigentliche Programm beginnt mit der"
25510 DATA "Zeilenummer # 1000. Der Inhalt dieser Zeile ist"
25520 DATA "verbindlich vorgeschrieben."
25530 DATA "!!  1000 A=wert:GOTO 20:REM programmname"
25540 DATA "!  1010 )"
25550 DATA "!   .   ( Hauptprogramm"
25560 DATA "!   .   ( GOTO 950 (Ende)"
25570 DATA "! 19999 )"
25580 DATA "! 20000 )"
25590 DATA "!   .   ( Computerspezifische"
25600 DATA "!   .   ( Unterprogramme, die in"
25610 DATA "!   .   ( BASICODE nicht erlaubte"
25620 DATA "!   .   ( Anweisungen enthalten"
25630 DATA "! 24999 )"
25640 DATA ".P0"
25650 DATA " 25000 )"
25660 DATA "!   .   ( DATA-Zeilen"
25670 DATA "! 29999 )"
25680 DATA "! 30000 )"
25690 DATA "!   .   ( REM-Zeilen"
25700 DATA "!   .   ( Hinweise, Bemerkungen"
25710 DATA "! 31999 )"
25720 DATA "! 32000 )"
25730 DATA "!   .   ( REM-Zeilen"
25740 DATA "!   .   ( Autor, Computer, Datum"
25750 DATA "! 32767 )"
25760 DATA ".P0"
25770 DATA ".RDie Programmzeile"
25780 DATA "!!Die Laenge einer Programmzeile ist auf"
25790 DATA "60 Zeichen beschraenkt - einschliesslich"
25800 DATA "!! - Zeilennummer,"
25810 DATA "! - Zwischenraeume und"
25820 DATA "! - Abschlusszeichen"
25830 DATA "!   (Carriage RETURN, CHR$(13))."
25840 DATA "!!In einer Programmzeile koennem mehrere"
25850 DATA "Anweisungen - getrennt durch ':' (Doppelpunkt) -"
25860 DATA "aufgenommen werden."
25870 DATA "!Es ist eine gute Gewohnheit,  die Zeilen in"
25880 DATA "Schritten von '10' zu numerieren (z.B. ... 2000,"
25890 DATA "2010, 2020, ...); es bleibt Raum fuer"
25900 DATA "Aenderungen und Ergaenzungen."
25910 DATA ".P0"
25920 DATA ".RDie Programmzeile # 1000"
25930 DATA "!!Der Inhalt der Zeile # 1000 ist festgelegt:"
25940 DATA "!! A=wert : GOTO 20 : REM programmname"
25950 DATA "!!Mit dem Sprung zur Zeile # 20 wird der"
25960 DATA "Computer in die Betriebsart 'BASICODE' versetzt."
25970 DATA "Dazu gehoeren:"
25980 DATA "!! - Loeschen des Bildschirms (CLS),"
25990 DATA "! - Loeschen der Variablen (CLR),"
26000 DATA "! - Wahl des Text-Modus,"
26010 DATA "! - Setzen der Bildschirmfarben"
26020 DATA "!   (Zeichen - weiss,"
26030 DATA "!   Hintergrund - dunkel),"
26040 DATA "! - Deklarieren und Initialisieren der"
26050 DATA "!   Variablen HO, VE, HG, VG und SV."
26060 DATA ".P0"
26070 DATA "Wichtig ist der Wert der Variablen A, der vom"
26080 DATA "Programmierer festzulegen ist. Fuer Computer,"
26090 DATA "die mit einer Z-80-aehnlichen CPU arbeiten,"
26100 DATA "muss der String-Speicherraum festgelegt werden"
26110 DATA "(z.B. CLEAR 500). Reicht der Speicherplatz nicht"
26120 DATA "aus, wird ein 'Out of string space'"
26130 DATA "   (OS) - Fehler   angezeigt."
26140 DATA "!Bei Rechnern mit dynamischer Speicherverwaltung"
26150 DATA "(z.B. Commodore) spielt das keine Rolle,ist aber"
26160 DATA "zu beachten, um die Portabilitaet der Programme"
26170 DATA "zu gewaehrleisten. Fuer eine Abschaetzung der"
26180 DATA "Wertzuweisung bietet sich an, die Subroutine"
26190 DATA "# 270 zu nutzen, die in der Variablen FR den"
26200 DATA "freien Speicherplatz (in Byte) zurueckgibt:"
26210 DATA ".P0"
26220 DATA " - Aufstellen des Programms mit A=500,"
26230 DATA "! - Abspeichern des Programms,"
26240 DATA "! - Aus/Einschalten bzw. Reset,"
26250 DATA "! - Laden des Programms,"
26260 DATA "! - GOSUB 270 : PRINT FR (RETURN),"
26270 DATA "! - Lauf des Programms,"
26280 DATA "! - GOSUB 270 : PRINT FR (RETURN)."
26290 DATA "!!Die Differenz beider Werte ergibt annaehernd"
26300 DATA "den Wert der aufgerundet der Variablen A"
26310 DATA "zuzuweisen ist."
26320 DATA ".P0"
26330 DATA ".RBeenden des Programms"
26340 DATA "!!Das Programm ist ordnungsgemaess zu beenden;"
26350 DATA "die Verwendung der Anweisungen END oder STOP"
26360 DATA "ist verboten. Mit dem Sprung 'GOTO 950' wird"
26370 DATA "das Programm beendet, der Bildschirm wird"
26380 DATA "geloescht, BASICODE-Programm und"
26390 DATA "-Subroutinen bleiben erhalten."
26400 DATA ".P0"
26410 DATA ".REin BASICODE-Programm"
26420 DATA "!!Das folgende Programm ('programm') laeuft"
26430 DATA "schnell ab, hat keinen Effekt, ist aber ein"
26440 DATA "BASICODE-Programm:"
26450 DATA "!! 1000 A=100:GOTO 20:REM programm"
26460 DATA "! 1010 REM hauptprogramm"
26470 DATA "! 1020 GOTO 950"
26490 DATA ".P10","Das war das","!!"
26500 DATA ".C**************************"
26510 DATA ".C*                        *"
26520 DATA ".C*  BASICODE-3   Journal  *"
26530 DATA ".C*        Nummer 2        *"
26540 DATA ".C*     21 Februar 1990    *"
26550 DATA ".C*                        *"
26560 DATA ".C**************************",".E"
30000 REM
30010 REM grundlagen von basicode
30020 REM     -- prog-1 --
30030 REM
32000 REM ---------------------
32010 REM friedrich dormeier
32020 REM
32030 REM d-1000 berlin 39
32040 REM
32050 REM 11/1989 - c64/c128
32060 REM ---------------------
32070 REM
32080 REM erstausstrahlung: radio ddr
32090 REM 900221

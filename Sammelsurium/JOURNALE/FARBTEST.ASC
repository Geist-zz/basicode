1000 A=100:GOTO 20:REM *** KLEURENDEMO ***
1010 DIM KC$(7),KT$(7)
1020 FOR I=0 TO 7:READ KC$(I):READ KT$(I):NEXT I
1030 CC(0)=7:CC(1)=0:GOSUB 100
1040 PRINT:PRINT" M E N U "
1050 PRINT:PRINT"1 Weisser Text auf farbigem Hintergrund"
1060 PRINT:PRINT"2 Farbiger Text auf farbigem Hintergrund"
1070 PRINT:PRINT"3 Farbe auf Farbe mit GOSUB 150"
1080 PRINT:PRINT"4 Bloecke mit GOSUB 150"
1090 PRINT:PRINT"5 Farbe auf Farbe im Grafik-Modus"
1100 PRINT:PRINT"6 Test auf Farbaenderung mit GOSUB 110"
1110 PRINT:PRINT"0 ENDE"
1120 PRINT:PRINT"Kennzahl: ";
1130 GOSUB 210:IF (IN<48) OR (IN>54) THEN GOSUB 250:GOTO 1130
1140 IF IN=48 THEN 950
1150 ON IN-48 GOSUB 1180,1240,1260,1290,1720,1890
1160 GOTO 1030
1170 REM *** Witte tekst op kleur achtergrond ***
1180 FOR I=1 TO 7:CC(1)=I:GOSUB 100:PRINT"TEXTBEISPIEL"
1190 PRINT:PRINT"Weisser Text auf ";KT$(CC(1));"em Grund"
1200 PRINT:PRINT"CC(1)=";KC$(CC(1));
1210 SD=20:GOSUB 450:NEXT I
1220 RETURN
1230 REM *** Kleurtekst op kleur achtergrond **
1240 FF=0:GOSUB 2180
1250 RETURN
1260 GOSUB 100:FF=1:GOSUB 2180:REM ** idem met GOSUB150 **
1270 RETURN
1280 REM *** blokken met GOSUB150 ***
1290 CC(0)=7:CC(1)=0:GOSUB 100
1300 CC(0)=2:HO=0:FOR VE=1 TO 12
1310 GOSUB 110:SR$="              ":GOSUB 150
1320 NEXT VE
1330 CC(0)=4:HO=0:FOR VE=10 TO 20
1340 GOSUB 110:SR$="              ":GOSUB 150
1350 NEXT VE
1360 CC(0)=5:HO=20:FOR VE=1 TO 12
1370 GOSUB 110:SR$="              ":GOSUB 150
1380 NEXT VE
1390 CC(0)=6:HO=20:FOR VE=10 TO 20
1400 GOSUB 110:SR$="              ":GOSUB 150
1410 NEXT VE
1420 CC(0)=0:HO=12:FOR VE=8 TO 14
1430 GOSUB 110:SR$="           ":GOSUB 150
1440 NEXT VE
1450 CC(0)=3:HO=15:FOR VE=9 TO 13
1460 GOSUB 110:SR$="   ":GOSUB 150
1470 NEXT VE
1480 SD=20:GOSUB 450
1490 REM *** tweede scherm ***
1500 CC(0)=7:CC(1)=0:GOSUB 100
1510 CC(0)=2:HO=4:FOR VE=20 TO 10 STEP -1
1520 GOSUB 110:SR$="":GOSUB 150
1530 NEXT VE
1540 CC(0)=3:HO=8:FOR VE=20 TO 8 STEP -1
1550 GOSUB 110:SR$="":GOSUB 150
1560 NEXT VE
1570 CC(0)=4:HO=12:FOR VE=20 TO 15 STEP -1
1580 GOSUB 110:SR$="":GOSUB 150
1590 NEXT VE
1600 CC(0)=5:HO=16:FOR VE=20 TO 10 STEP -1
1610 GOSUB 110:SR$="":GOSUB 150
1620 NEXT VE
1630 CC(0)=6:HO=20:FOR VE=20 TO 5 STEP -1
1640 GOSUB 110:SR$="":GOSUB 150
1650 NEXT VE
1660 CC(0)=7:HO=24:FOR VE=20 TO 11 STEP -1
1670 GOSUB 110:SR$="":GOSUB 150
1680 NEXT VE
1690 SD=30:GOSUB 450
1700 RETURN
1710 REM *** GRAFISCHE TESTS ***
1720 CN=0
1730 FOR I=0 TO 7:CC(1)=I
1740 FOR J=0 TO 7:CC(0)=J
1750 GOSUB 600
1760 VE=.1:HO=.2:GOSUB 620
1770 HO=.5:GOSUB 630
1780 VE=.6:GOSUB 630
1790 HO=.2:GOSUB 630
1800 VE=.1:GOSUB 630
1810 VE=.7:HO=.35:SR$="CC(0)="+KC$(CC(0)):GOSUB 650
1820 VE=.8:HO=.35:SR$="CC(1)="+KC$(CC(1)):GOSUB 650
1830 VE=.9:HO=.2:SR$=KT$(CC(0))+" auf "+KT$(CC(1))
1840 GOSUB 650
1850 SD=20:GOSUB 450
1860 NEXT J:NEXT I
1870 SD=20:GOSUB 450
1880 RETURN
1890 REM --- Test auf Farbeinstellung mit GOSUB 110
1900 GOSUB 100
1910 PRINT"Mit dieser Routine wird getestet, ob sich"
1920 PRINT"durch die Nutzung von GOSUB 110 eine Farb-"
1930 PRINT"einstellung durchfuehren laesst."
1940 PRINT"Diese Moeglichkeit geht ueber die Forderungen"
1950 PRINT"des BASICODE-3C-Protokolls hinaus, ist also"
1960 PRINT"nur eine Zusatzfunktion, die auf Ihrem Rech-"
1970 PRINT"ner nicht funktionieren muss.":PRINT
1980 PRINT"Im Moment ist weisse Schrift auf schwarzem"
1990 PRINT"Grund eingestellt."
2000 PRINT:PRINT:PRINT"                           >TASTE<"
2010 GOSUB 210
2020 GOSUB 100
2030 FOR X=0 TO 7
2040  HO=X*3:VE=X*2:CC(0)=X
2050  GOSUB 110
2060  PRINT"Schriftfarbe: ";KT$(CC(0))
2070 NEXT X
2080 GOSUB 210
2090 CC(0)=0:CC(1)=(1):GOSUB 100
2100 FOR X=0 TO 7
2110  HO=X*3:VE=X*2:CC(1)=X
2120  GOSUB 110
2130  PRINT"Hintergrundfarbe: ";KT$(CC(1))
2140 NEXT X
2150 GOSUB 210
2160 RETURN
2170 REM *** tekst-subroutine ***
2180 FOR C1=0 TO 7:CC(1)=C1
2190 FOR C0=0 TO 7:CC(0)=C0
2200 BO$=" auf ":IF FF=1 THEN BO$="  unter  ":GOTO 2320
2210 GOSUB 100::PRINT:PRINT:PRINT:PRINT
2220 PRINT"    *    * *     ****** *    * ******"
2230 PRINT"    *   *  *     *      *    * *    *"
2240 PRINT"    *  *   *     *      *    * *    *"
2250 PRINT"    * *    *     *      *    * *    *"
2260 PRINT"    **     *     ****   *    * ******"
2270 PRINT"    **     *     *      *    * * *"
2280 PRINT"    * *    *     *      *    * *  *"
2290 PRINT"    *  *   *     *      *    * *   *"
2300 PRINT"    *   *  ***** ****** ****** *    *";
2310 GOTO 2360
2320 SR$="                   ":HO=2
2330 VE=11:GOSUB 110:GOSUB 150:VE=13:GOSUB 110:GOSUB 150
2340 SR$="  TEST GOSUB 150   "
2350 VE=12:GOSUB 110:GOSUB 150
2360 VE=18:HO=5:GOSUB 110:PRINT"CC(0)=";KC$(CC(0))
2370 HO=20:GOSUB 110:PRINT"CC(1)=";KC$(CC(1))
2380 VE=20:HO=5:GOSUB 110
2390 PRINT KT$(CC(0));BO$;KT$(CC(1));"   ";
2400 SD=20:GOSUB 450
2410 NEXT C0:NEXT C1:RETURN
25000 DATA "0","schwarz"
25010 DATA "1","blau"
25020 DATA "2","rot"
25030 DATA "3","violett"
25040 DATA "4","gruen"
25050 DATA "5","hellblau"
25060 DATA "6","gelb"
25070 DATA "7","weiss"
32000 REM ** DIT IS PUBLIC DOMAIN SOFTWARE **
32010 REM 
32020 REM ************************
32030 REM *                      *
32040 REM *      RZ-Software     *
32050 REM *                      *
32060 REM *    Rob van der Zee   *
32070 REM *                      *
32080 REM *     Braamkamp 40     *
32090 REM *                      *
32100 REM *    7206 HB Zutphen   *
32110 REM *                      *
32120 REM *    TEL:05750-21736   *
32130 REM * -------------------- *
32140 REM * Deutsche Fassung und *
32150 REM *    Funktion 6 von    *
32160 REM *                      *
32170 REM *   Andreas Garten     *
32180 REM *   O-8514 Pulsnitz    *
32190 REM *                      *
32200 REM * 5.Sept.1992          *
32210 REM *                      *
32220 REM ************************
32230 REM
32240 REM TROS-RADIO dd 910828 + 910904
32250 REM DS-KULTUR
32260 REM -spezial 911030/920318

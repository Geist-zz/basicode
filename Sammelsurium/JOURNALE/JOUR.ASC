1000 A=500:GOTO 20:REM BASICODE-3C COMPUTERBULLETIN
1010 HT=HO:VT=VE:S$="":FOR I=1 TO HT:S$=S$+" ":NEXT I
1020 AN=1:CC(0)=1:CC(1)=7:GOSUB 2160
1030 R$="":RESTORE :READ T$:GOSUB 1590
1040 PRINT" Ausgabe des Bulletins:":PRINT
1050 PRINT"  1  auf dem Bildschirm"
1060 PRINT"  2  auf den Drucker"
1070 PRINT"  3  Ausdruckmuster auf dem Schirm"
1080 PRINT"     (Um jedes Blatt einzeln zu be-"
1090 PRINT"     trachten und um evtl. DATA zu"
1100 PRINT"     aendern oder zu loeschen.)":PRINT
1110 PRINT" Welche Nummer waehlen Sie: ";:GOSUB 210
1120 PN=1:SC=IN-48:PRINT SC
1130 IF (SC<2) OR (SC>3) THEN SC=1:GOSUB 2160:GOTO 1290
1140 IF AN=0 THEN AN=1:GOTO 1290
1150 PRINT:PRINT" Wieviel Zeichen stellt der Drucker"
1160 INPUT " maximal auf einer Zeile dar:   ";HR
1170 PRINT:PRINT" Wieviel Leerzeichen sollen rechts"
1180 INPUT " links gelassen werden (>0):    ";HL
1190 IF HL<1 THEN HL=1
1200 HR=HR-HL
1210 PRINT:PRINT" Wieviel Zeilen Text sind maximal"
1220 INPUT " je Blatt vorgesehen (z.B. 72): ";VR:VR=VR-3
1230 PRINT:PRINT" Gebrauchen Sie (E)ndlospapier oder"
1240 PRINT"         Einzel-(B)laetter (E/B)  ";
1250 LK=1:GOSUB 210:IF IN=66 THEN LK=0:VL=0:GOTO 1290
1260 IF IN<>69 THEN GOSUB 250:GOTO 1230
1270 PRINT:PRINT
1275 PRINT" Wieviel Zeilen sollen am Blattende"
1280 INPUT "          leer gelassen werden: ";VL:PRINT
1290 MM$="":IF HL>0 THEN MM$=LEFT$(S$,HL)
1300 GOSUB 1530:M$=MM$:AL=VL:IF SC=2 THEN GOSUB 1510
1310 IF LEN(R$)>HR-HL THEN GOSUB 2090
1320 GOSUB 1620:IF R$<>"" THEN R$=R$+" "
1330 R$=R$+D$:GOTO 1310
1340 IF SC=1 THEN 1370
1350 SR$=M$+SR$:PRINT SR$:IF SC=2 THEN GOSUB 350:GOSUB 360
1360 GOTO 1380
1370 GOSUB 120:PRINT M$;SR$;:VE=VE+1:HO=0:GOSUB 110:HL=0
1380 M$=MM$:AL=AL+1:IF AL<=VR THEN RETURN
1390 IF SC=1 THEN 1530
1400 IF VL<1 THEN 1430
1410 FOR I=1 TO VL:PRINT:IF SC=2 THEN GOSUB 360
1420 NEXT I
1430 PRINT:PRINT" Ende der Seite ";PN;".":PRINT
1440 PN=PN+1
1450 IF D$=".E" THEN 1750
1460 ON LK*SC+1 GOTO 1470,1470,1500,1480
1470 IF SC=2 THEN PRINT" Neues Blatt einlegen!":PRINT
1480 PRINT" Druecke RETURN/ENTER ";
1490 GOSUB 210:IF IN<>13 THEN 1490
1500 AL=VL
1510 SR$=LEFT$(S$,(HR-HL-LEN(T$))/2)+T$:GOSUB 1340
1520 SR$="":GOSUB 1340:AL=VL:RETURN
1530 HO=0:VE=VT:GOSUB 110
1540 PRINT" <RETURN> = weiter, <S> = Screendump ";
1550 GOSUB 210:IF IN=13 THEN 1590
1560 IF IN<>83 THEN 1550
1570 CN=0:FOR VE=0 TO VT:SR$="":FOR HO=0 TO HT:GOSUB 220
1580 SR$=SR$+CHR$(IN+CN):NEXT HO:GOSUB 350:GOSUB 360:NEXT VE
1590 GOSUB 100:AL=VL:SR$=T$:I=INT((HT-LEN(SR$))/2-3)
1600 IF I>0 THEN PRINT LEFT$(S$,I);
1610 GOSUB 150:PRINT:PRINT:RETURN
1620 READ D$
1630 IF D$="" THEN 1620
1640 IF LEFT$(D$,1)="." THEN 1710
1650 IF LEFT$(D$,1)="=" THEN J=3:GOSUB 2040:GOTO 1680
1660 IF LEFT$(D$,1)<>"!" THEN RETURN
1670 J=1:IF D$="!" THEN D$="":GOSUB 2040:GOTO 1630
1680 D$=MID$(D$,2)
1690 GOSUB 2040:IF AL+J>VR THEN GOSUB 2050
1700 GOTO 1630
1710 IF D$<>".E" THEN 1850
1720 GOSUB 2040:T$=""
1730 SR$="":GOSUB 1340:IF (AL<>VL) AND (SC=2) THEN 1730
1740 IF SC>1 THEN GOSUB 1430
1750 PRINT"   -----------------------"
1760 PRINT"   Ende dieses Bulletins"
1770 PRINT:PRINT"   Noch einmal mit:"
1780 PRINT"    1 den gleichen Einstellungen"
1790 PRINT"    2 neuen Einstellungen":PRINT
1800 PRINT"    3 STOPPEN":PRINT
1810 PRINT"   Ihre Wahl (1/2/3) ";:GOSUB 210
1820 IF (IN=49) AND (SC>1) THEN AN=0:GOTO 1030
1830 IF IN<51 THEN 1000
1840 GOTO 950
1850 DD$=MID$(D$,2,1):D$=MID$(D$,3)
1860 IF DD$<>"R" THEN 1920
1870 IF SC>1 THEN RETURN
1880 IF LEN(R$)+7+LEN(D$)>HR-HL THEN SR$=R$:GOSUB 1340:R$=""
1890 PRINT M$;R$;:SR$=D$:GOSUB 150:M$="":SR$=""
1900 HL=HL+LEN(R$)+LEN(D$)+6:R$="":IF HL>HR THEN GOSUB 1340
1910 GOTO 1620
1920 IF DD$<>"M" THEN 1950
1930 I=VAL(D$):MM$="":IF I+HL=0 THEN 1620
1940 MM$=LEFT$(S$,I+HL):GOTO 1620
1950 IF DD$="P" THEN J=VAL(D$):GOSUB 2040:GOSUB 2050:GOTO 1620
1960 IF DD$="T" THEN T$=D$:J=10:GOSUB 2040:GOSUB 2050:GOTO 1870
1970 IF DD$="Q" THEN R$=R$+CHR$(34)+D$:GOTO 1620
1980 IF DD$<>"F" THEN 2010
1990 J=VAL(D$):GOSUB 2040
2000 J=J-1:IF J>-1 THEN SR$="":GOSUB 1340:GOTO 2000
2010 IF DD$<>"C" THEN 2030
2020 SR$=LEFT$(S$,(HR-HL-LEN(D$))/2)+D$:GOSUB 1340
2030 GOTO 1620
2040 GOSUB 2090:SR$=R$:GOSUB 1340:R$="":RETURN
2050 IF (SC=1) OR (AL+J>VR) THEN 2070
2060 SR$="":GOSUB 1340:GOSUB 1340:RETURN
2070 IF AL<>VL THEN SR$="":GOSUB 1340:GOTO 2070
2080 RETURN
2090 K=HR-HL+1-LEN(M$)
2100 IF LEN(R$)<=K THEN RETURN
2110 IF MID$(R$,K,1)=" " THEN 2140
2120 K=K-1:IF K>0 THEN 2110
2130 SR$="":GOTO 2150
2140 SR$=LEFT$(R$,K-1):R$=MID$(R$,K+1)
2150 GOSUB 1340:GOTO 2090
2160 HL=0:HR=HT:VL=2:VR=VT-2:RETURN
2170 REM -------------------------------------------------
25000 DATA "BASICODE-3C auf PC/XT/AT"
25010 DATA ".M1"," Die vorliegende Farbversion des"
25020 DATA "Bascoders fuer PC/AT mit EGA- oder VGA-Karte"
25030 DATA "stellt eine Arbeitsversion dar, die noch mit"
25040 DATA "einigen kleinen Maengeln behaftet ist. Diese"
25050 DATA "Maengel liegen jedoch nicht im Farbbereich, "
25060 DATA "sondern in der Reaktion auf einige Steuerzeichen"
25070 DATA "(z.B. DEL). Sie wurden von der sw-Version"
25080 DATA "unveraendert uebernommen."
25090 DATA "!Eine systembedingte Besonderheit ist die"
25100 DATA "Anpassung des Bascoders an den Rechnertakt in"
25110 DATA "Zeile 26. Der Wert fuer OTIME ist fuer den"
25120 DATA "konkreten Rechner experimentell festzulegen"
25130 DATA "und in den Quelltext einzutragen."
25140 DATA "!!In diesem Zusammenhang ein wichtiger Hinweis:"
25150 DATA "!!Einige Speicherverwaltungs- und Cacheprogramme"
25160 DATA "veraendern das Zeitregime beim Lesen und"
25170 DATA "Schreiben im Basicode-Format. Wird mit einer"
25180 DATA "veraenderten oder wechselnder Konfiguration"
25190 DATA "des Rechners gearbeitet, ist die Zeitkonstante"
25200 DATA "der BASICODE.EXE zu pruefen und ggf. neu"
25210 DATA "einzustellen. Zu diesen Programmen gehoeren"
25220 DATA "z.B. SMARTDRV.SYS und EMM386.EXE"
25230 DATA ".P",".REinlesen des Farb-Bascoders"
25240 DATA "!!Das uebertragene Basicode-File wird nach dem"
25250 DATA "Starten von BASICODE.EXE mit der Funktion 1"
25260 DATA "(Programm einlesen) in den Rechner"
25270 DATA "uebernommen. Er liegt dann als ASCII-Datei"
25280 DATA "im Programmpuffer vor und muss in das"
25290 DATA "interne Basic-Format uebersetzt werden."
25300 DATA "Dazu wird die entsprechende Frage nach"
25310 DATA "dem Einlesen mit J beantwortet. Nach diesem"
25320 DATA "Vorgang liegt der Bascoder als ASCII-File"
25330 DATA "auf der Diskette/Festplatte vor und kann"
25340 DATA "wie ein normales BASIC-File geladen werden."
25350 DATA ".P5",".RAllgemeine Hinweise"
25360 DATA "!!Der Bascoder wurde auf einem 386-er AT"
25370 DATA "mit 25 MHz Taktfrequenz unter MS-DOS"
25380 DATA "und GW-BASIC (Version 3.23) getestet."
25390 DATA "Da es in der Praxis eine Vielzahl von"
25400 DATA "Grafik-Karten gibt, kann keine Gewaehr fuer"
25410 DATA "eine einwandfreie Funktion uebernommen werden."
25420 DATA "!!Folgende Veraenderungen waren notwendig:"
25430 DATA "!!Die Farbencodes unter Basicode stimmen"
25440 DATA "nicht mit denen unter GW-BASIC ueberein."
25450 DATA "Die entsprechende Korrektur wurde fuer"
25460 DATA "den Textmodus mit Hilfe der Anweisung"
25470 DATA "PALETTE realisiert. Im Grafikbetrieb"
25480 DATA "(Routinen 600, 620, 630 und 650) ist diese"
25490 DATA "Loesung nicht immer anwendbar, da im SCREEN"
25500 DATA "9 die Anweisung COLOR 0,.. zu einer"
25510 DATA "Fehlermeldung fuehrte. Weiterhin wirken die"
25520 DATA "Paletten-Veraenderungen nicht auf die Farben"
25530 DATA "im Hintergrund. Die gefundene Loesung ist"
25540 DATA "sicher etwas umstaendlich und koennte von"
25550 DATA "Ihnen ja noch verbessert werden..."
25560 DATA ".P","!Eine ueber das Basicode-Protokoll"
25570 DATA "hinausgehende Moeglichkeit zum Einstellen"
25580 DATA "der Farben wurde in der Routine 110"
25590 DATA "realisiert. Mit einer Neupositionierung"
25600 DATA "des Cursors ist die Aenderung der Farben"
25610 DATA "durch Angabe der neuen Werte von CC(0)"
25620 DATA "und CC(1) moeglich. Somit sind"
25630 DATA "Farbveraenderungen mit den Routinen 100"
25640 DATA "und 110 moeglich."
25650 DATA "!!Ein weiterer Fehler tritt oft bei der"
25660 DATA "Nutzung der Datei-Routinen 500-580 auf."
25670 DATA "Der Interpreter meldet einen fehlerhaften"
25680 DATA "Dateinamen, obwohl er den Regeln entspricht."
25690 DATA "Die Ursache liegt darin, dass GW-BASIC"
25700 DATA "in der Standardeinstellung nur 3 Dateien"
25710 DATA "gleichzeitig oeffnen kann. Wird der"
25720 DATA "Wert der Dateinummer (entspricht dem"
25730 DATA "Wert der Variablen NF+1) groesser als 3, so"
25740 DATA "kommt es zu dieser Fehlermeldung."
25750 DATA "!Abhilfe ist moeglich, indem GW-BASIC"
25760 DATA "mit folgendem Kommando aus der DOS-Ebene"
25770 DATA "aufgerufen wird:"
25780 DATA "!",".CGWBASIC /F:8"
25790 DATA "!Die Verringerung des zur Verfuegung"
25800 DATA "stehenden Arbeitsspeichers ist unter"
25810 DATA "Basicode nicht von Bedeutung."
25820 DATA ".P","Fuer Hinweise und Verbesserungen des"
25830 DATA "Farb-Bascoders bin ich jederzeit dankbar."
25840 DATA "!Viel Spass mit diesem Programm!"
25850 DATA ".M10"
25860 DATA "!!Andreas Garten"
25870 DATA "!Friedersdorfer Siedlung 2"
25880 DATA "!O-8514 Pulsnitz (Sachsen)"
25890 DATA ".M0",".F2"
25900 DATA ".E"
32000 REM ==============================
32010 REM -                            -
32020 REM - Farbbascoder fuer PC/XT/AT -
32030 REM -                            -
32040 REM - Ueberarbeitung der mono-   -
32050 REM - chromen Version:           -
32060 REM -                            -
32070 REM - Andreas Garten             -
32080 REM - Friedersd. Siedlung 2      -
32090 REM - O-8514 PULSNITZ (Sachsen)  -
32100 REM -                            -
32110 REM - Rechner: 386DX - 25        -
32120 REM -                            -
32130 REM -  6.September 1992          -
32140 REM -                            -
32150 REM ==============================

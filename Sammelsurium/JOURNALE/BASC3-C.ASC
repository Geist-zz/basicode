1000 A=500:GOTO 20:REM BASICODE-3 COMPUTERBULLETIN
1010 HT=HO:VT=VE:S$="":FOR I=1 TO HT:S$=S$+" ":NEXT I
1020 AN=1:DIM CC(2):CC(0)=1:CC(1)=6:GOSUB 2160
1030 R$="":RESTORE:READ T$:GOSUB 1590
1040 PRINT"Anzeige des Bulletins:":PRINT
1050 PRINT" 1  Bildschirm"
1060 PRINT" 2  Drucker"
1070 PRINT" 3  Ausdruckmuster auf dem Schirm"
1080 PRINT"    (Um jedes Blatt einzeln zu be-"
1090 PRINT"    trachten und um evtl. DATA zu"
1100 PRINT"    aendern oder zu loeschen.)"
1110 PRINT:PRINT"Welche Nummer waehlen Sie: ";:GOSUB 210:PN=1
1120 SC=IN-48:PRINT SC
1130 IF (SC<2) OR (SC>3) THEN SC=1:GOSUB 2160:GOTO 1290
1140 IF AN=0 THEN AN=1:GOTO 1290
1150 PRINT:PRINT"Wieviel Zeichen stellt der Drucker"
1160 INPUT"maximal auf einer Zeile dar:   ";HR
1170 PRINT:PRINT"Wieviel Leerzeichen sollen rechts"
1180 INPUT"links gelassen werden (>0):    ";HL
1190 IF HL<1 THEN HL=1
1200 HR=HR-HL
1210 PRINT:PRINT"Wieviel Zeilen Text sind maximal"
1220 INPUT"je Blatt vorgesehen (z.B. 72): ";VR:VR=VR-3
1230 PRINT:PRINT"Gebrauchen Sie (E)ndlospapier oder"
1240 PRINT"        Einzel-(B)laetter (E/B)  ";
1250 LK=1:GOSUB 210:IF IN=66 THEN LK=0:VL=0:GOTO 1290
1260 IF IN<>69 THEN GOSUB 250:GOTO 1230
1270 PRINT:PRINT:PRINT"Wieviel Zeilen sollen am Blattende"
1280 INPUT"         leer gelassen werden: ";VL:PRINT
1290 MM$="":IF HL>0 THEN MM$=LEFT$(S$,HL)
1300 GOSUB 1530:M$=MM$:AL=VL:IF SC=2 THEN GOSUB 1510
1310 IF LEN(R$)>HR-HL THEN GOSUB 2090
1320 GOSUB 1620:IF R$<>"" THEN R$=R$+" "
1330 R$=R$+D$:GOTO 1310
1340 IF SC=1 THEN 1370
1350 SR$=M$+SR$:PRINT SR$:IF SC=2 THEN GOSUB 350:GOSUB 360
1360 GOTO 1380
1370 GOSUB 120:PRINT M$;SR$;:VE=VE+1:HO=0:GOSUB 110:HL=0
1380 M$=MM$:AL=AL+1:IF AL<=VR THEN RETURN
1390 IF SC=1 THEN 1530
1400 IF VL<1 THEN 1430
1410 FOR I=1 TO VL:PRINT:IF SC=2 THEN GOSUB 360
1420 NEXTI
1430 PRINT:PRINT"Ende der Seite ";PN;".":PRINT
1440 PN=PN+1
1450 IF D$=".E" THEN 1750
1460 ON LK*SC+1 GOTO 1470,1470,1500,1480
1470 IF SC=2 THEN PRINT"Neues Blatt einlegen!":PRINT
1480 PRINT"Druecke RETURN/ENTER ";
1490 GOSUB 210:IFIN<>13 THEN 1490
1500 AL=VL
1510 SR$=LEFT$(S$,(HR-HL-LEN(T$))/2)+T$:GOSUB 1340
1520 SR$="":GOSUB 1340:AL=VL:RETURN
1530 HO=0:VE=VT:GOSUB 110
1540 PRINT"<RETURN> = weiter, <S> = Screendump ";
1550 GOSUB 210:IF IN=13 THEN 1590
1560 IF IN<>83 THEN 1550
1570 CN=0:FOR VE=0 TO VT:SR$="":FOR HO=0 TO HT:GOSUB 220
1580 SR$=SR$+CHR$(IN+CN):NEXT HO:GOSUB 350:GOSUB 360:NEXT VE
1590 GOSUB 100:AL=VL:SR$=T$:I=INT((HT-LEN(SR$))/2-3)
1600 IF I>0 THEN PRINT LEFT$(S$,I);
1610 GOSUB 150:PRINT:PRINT:RETURN
1620 READ D$
1630 IF D$="" THEN 1620
1640 IF LEFT$(D$,1)="." THEN 1710
1650 IF LEFT$(D$,1)="=" THEN J=3:GOSUB 2040:GOTO 1680
1660 IF LEFT$(D$,1)<>"!" THEN RETURN
1670 J=1:IF D$="!" THEN D$="":GOSUB 2040:GOTO 1630
1680 D$=MID$(D$,2)
1690 GOSUB 2040:IF AL+J>VR THEN GOSUB 2050
1700 GOTO 1630
1710 IF D$<>".E" THEN 1850
1720 GOSUB 2040:T$=""
1730 SR$="":GOSUB 1340:IF (AL<>VL) AND (SC=2) THEN 1730
1740 IF SC>1 THEN GOSUB 1430
1750 PRINT"-----------------------"
1760 PRINT"Ende dieses Bulletins"
1770 PRINT:PRINT"Noch einmal mit:"
1780 PRINT" 1 den gleichen Einstellungen"
1790 PRINT" 2 neuen Einstellungen"
1800 PRINT" 3 STOPPEN"
1810 PRINT"Ihre Wahl (1/2/3) ";:GOSUB 210
1820 IF (IN=49) AND (SC>1) THEN AN=0:GOTO 1030
1830 IF IN<51 THEN 1000
1840 GOTO950
1850 DD$=MID$(D$,2,1):D$=MID$(D$,3)
1860 IF DD$<>"R" THEN 1920
1870 IF SC>1 THEN RETURN
1880 IF LEN(R$)+7+LEN(D$)>HR-HL THEN SR$=R$:GOSUB 1340:R$=""
1890 PRINT M$;R$;:SR$=D$:GOSUB 150:M$="":SR$=""
1900 HL=HL+LEN(R$)+LEN(D$)+6:R$="":IF HL>HR THEN GOSUB 1340
1910 GOTO 1620
1920 IF DD$<>"M" THEN 1950
1930 I=VAL(D$):MM$="":IF I+HL=0 THEN 1620
1940 MM$=LEFT$(S$,I+HL):GOTO 1620
1950 IF DD$="P" THEN J=VAL(D$):GOSUB 2040:GOSUB 2050:GOTO 1620
1960 IF DD$="T" THEN T$=D$:J=10:GOSUB 2040:GOSUB 2050:GOTO 1870
1970 IF DD$="Q" THEN R$=R$+CHR$(34)+D$:GOTO 1620
1980 IFDD$<>"F" THEN2010
1990 J=VAL(D$):GOSUB2040
2000 J=J-1:IFJ>-1 THENSR$="":GOSUB1340:GOTO2000
2010 IFDD$<>"C" THEN2030
2020 SR$=LEFT$(S$,(HR-HL-LEN(D$))/2)+D$:GOSUB1340
2030 GOTO1620
2040 GOSUB 2090:SR$=R$:GOSUB 1340:R$="":RETURN
2050 IF (SC=1) OR (AL+J>VR) THEN 2070
2060 SR$="":GOSUB1340:GOSUB1340:RETURN
2070 IF AL<>VL THEN SR$="":GOSUB 1340:GOTO 2070
2080 RETURN
2090 K=HR-HL+1-LEN(M$)
2100 IF LEN(R$)<=K THEN RETURN
2110 IF MID$(R$,K,1)=" " THEN 2140
2120 K=K-1:IF K>0 THEN 2110
2130 SR$="":GOTO 2150
2140 SR$=LEFT$(R$,K-1):R$=MID$(R$,K+1)
2150 GOSUB 1340:GOTO 2090
2160 HL=0:HR=HT:VL=2:VR=VT-2:RETURN
25000 DATA"BASICODE-3C"
25010 DATA"BASICODE - in den Niederlanden geboren und seit"
25020 DATA"1981 zur Programmuebertragung benutzt - wurde"
25030 DATA"1986 zur Version -3 weiterentwickelt. Seit 1989"
25040 DATA"wird dieses System in Deutschland zur"
25050 DATA"Uebertragung von Computerprogrammen in der"
25060 DATA"Rundfunksendung ",".QREM",".Q","des"
25070 DATA"Deutschlandsenders Kultur verwendet."
25080 DATA"!In diesem Jahr (1991) wurde die Version"
25090 DATA".RBASICODE-3C","vorgestellt; moeglich wurde dies"
25100 DATA"wiederum durch das Engagement vieler Hobbyisten"
25110 DATA"und der Stichting BASICODE, Eindhoven."
25120 DATA"=Diese Erweiterung ist durch folgende Merkmale"
25130 DATA"gekennzeichnet:",".M3"
25140 DATA"=1) Darstellung von Text oder Grafik auf dem"
25150 DATA".M6","Bildschirm in wahlweise acht Farben -"
25160 DATA"nur in Verbindung mit einem Farbmonitor/TV !"
25170 DATA".M3","=2) Herstellung eines Screendumps"
25180 DATA"(Textbetrieb)",".M0",".P0","Neue"
25190 DATA"Uebersetzungsprogramme bzw. Erweiterungen"
25200 DATA"liegen z.Zt. fuer folgende Computer vor:"
25210 DATA".M3","=Commodore C-64,","!MSX-1 und MSX-2"
25220 DATA"!Schneider CPC","!Philips P 2000",".M0",".P0"
25230 DATA"Ein fuer die praktische Anwendung wichtiges"
25240 DATA"Merkmal ist die absolute Kompatibilitaet"
25250 DATA"von BASICODE-3 und BASICODE-3C:"
25260 DATA"=    Aufgestellt:  >     Ablauf:"
25270 DATA"!  ---------------------------------"
25280 DATA"!  BC-3       s/w  >  BC-3C     s/w"
25290 DATA"!  BC-3C     Farbe >  BC-3      s/w"
25300 DATA"=(s/w) = schwarz/weisse Bildschirmdarstellung"
25310 DATA"(monochrom)"
25320 DATA"=BASICODE-3C-Programme werden, falls nur ein"
25330 DATA"monochromer Monitor (TV) vorhanden ist, mit"
25340 DATA"schwarz/weissem Text bzw. Grafik dargestellt."
25350 DATA".TProgrammieren mit Farbe"
25360 DATA"=Wie schon in der Version -3 werden die hier"
25370 DATA"notwendigen Anweisungen durch Subroutinen"
25380 DATA"ersetzt.","!Es sind folgende Farben zugelassen:"
25390 DATA".M3","=Code Farbe",
25400 DATA"!----------------------"
25410 DATA"!  0  Schwarz"
25420 DATA"!  1  Blau"
25430 DATA"!  2  Rot"
25440 DATA"!  3  Violett (Magenta)"
25450 DATA"!  4  Gruen"
25460 DATA"!  5  Hellblau (Cyan)"
25470 DATA"!  6  Gelb"
25480 DATA"!  7  Weiss",".M0",".P0"
25490 DATA"!Die Code-Ziffern werden den Variablen CC(0) und"
25500 DATA"CC(1) zugewiesen; als Default-Einstellung gilt:"
25510 DATA".M3","=CC(0)=7  > Zeichenfarbe - Weiss"
25520 DATA"!CC(1)=0  > Hintergrund  - Schwarz",".M0"
25530 DATA"=Bei Start des Programms werden diese Werte mit"
25540 DATA"der Subroutine 100 uebernommen und die Farben"
25550 DATA"entsprechend gesetzt.","!Sie gelten im Ablauf"
25560 DATA"des Programms bis zu einer Aenderung und der"
25570 DATA"naechstfolgenden GOSUB 100 Anweisung. Die"
25580 DATA"Programmzeile",".M3"
25590 DATA"=CC(0)=2:CC(1)=6:GOSUB 100",".M0"
25600 DATA"=liefert nach Loeschen des Schirmes rote Zeichen"
25610 DATA"auf gelbem Hintergrund."
25620 DATA"=Um einen hinreichenden Kontrast der Darstellung"
25630 DATA"zu erreichen, empfiehlt sich ein Unterschied von"
25640 DATA"'4' zwischen den Variablen in CC(0) und CC(1)."
25650 DATA"=Der Variablenname 'CC' gilt im Sinne des"
25660 DATA"BASICODE-Protokolls als verboten!"
25670 DATA".TDie Subroutine 150"
25680 DATA"=Die Subroutine 150 bewirkt eine inverse"
25690 DATA"Bildschirmdarstellung, d.h. dem obigen Beispiel"
25700 DATA"folgend werden schwarze Zeichen auf einem hellen"
25710 DATA"Hintergrund dargestellt. Es bestehen aber noch"
25720 DATA"weitere Moeglichkeiten. Ein Beispiel:",".M3"
25730 DATA"=CC(0)=4:CC(1)=1:SR$='TEST':GOSUB 150",".M0"
25740 DATA"=Damit wird das Wort TEST in Blau auf einem"
25750 DATA"gruenen Untergrund ausgegeben. Fuer den weiteren"
25760 DATA"Ablauf des Programms bleibt es bei den vor der"
25770 DATA"letzten GOSUB 100-Anweisung mit CC(0) und CC(1)"
25780 DATA"eingestellten Farben. Aenderungen haben bis zum"
25790 DATA"naechsten GOSUB 100 keinen Einfluss auf die"
25800 DATA"normale Zeichen-Darstellung."
25810 DATA".TFarbe im grafischen Betrieb"
25820 DATA"=BASICODE-3 kennt drei Subroutinen zur"
25830 DATA"grafischen Darstellung:"
25840 DATA".M3","=- GOSUB 600","!  Loeschen des"
25850 DATA"Schirms","!  Einleiten des grafischen Betriebes"
25860 DATA"=- CN=0:GOSUB 620","!  CN=1:GOSUB 620"
25870 DATA"!  Zeichnen/Loeschen eines Punktes"
25880 DATA"=- CN=0:GOSUB 630","!  CN=1:GOSUB 630"
25890 DATA"!  Zeichnen/Loeschen einer Linie"
25900 DATA"=- CN=0:GOSUB 650","!  CN=1:GOSUB 650"
25910 DATA"!  Schreiben/Loeschen von Text",".M0",".P0"
25920 DATA"!Fuer das Programmieren in BASICODE-3C gilt:"
25930 DATA"=- Subroutine 600 loescht den Schirm und zeigt"
25940 DATA".M2","die Farbe, die in CC(1) codiert ist.",".M0"
25950 DATA"=- Subroutinen 620, 630, 650:",".M2"
25960 DATA"=CN=0 - Grafik/Text wird in der Farbe",".M9"
25970 DATA"dargestellt, die in CC(0) festgelegt ist.",".M2"
25980 DATA"=CN=1 - Grafik/Text wird geloescht, d.h. in der"
25990 DATA".M9","Farbe dargestellt, die vor dem letzten"
26000 DATA"GOSUB 600 in CC(1) codiert war.",".M0"
26010 DATA".TScreendump - Hardcopy"
26020 DATA"=Die schon aus BASICODE-3 bekannte Subroutine"
26030 DATA"220 lieferte beim Auslesen der Variablen IN mit"
26040 DATA"PRINT CHR$(IN) nur Grossbuchstaben ab. Dies"
26050 DATA"wurde jetzt verbessert. Durch Hinzufuegen der"
26060 DATA"Variablen CN (Wert computerabhaengig, in den"
26070 DATA"Subroutinen festgelegt) wird ueber einen Drucker"
26080 DATA"das Zeichen ausgegeben, das auf dem Bildschirm"
26090 DATA"gezeigt wird:"
26100 DATA"=xxxx GOSUB 220:SR$=CHR$(IN+CN):GOSUB 350"
26110 DATA"=Beim Lauf von BASICODE-3C-Programmen mit einem"
26120 DATA"BASICODE-3(!)- Uebersetzungsprogramm koennen"
26130 DATA"allerdings Kompatibilitaetsprobleme auftreten."
26140 DATA".P0"
26150 DATA"Als Beispiel einer Hardcopy- Routine fuer BC-3C-"
26160 DATA"Programme bietet sich an:",".M2"
26170 DATA"= 1010 HT=HO:VT=VE: REM schirmgroesse"
26180 DATA"!  :"
26190 DATA"!21000 CN=0 :           REM wenn bc-3c-prog. mit"
26200 DATA"!21010 FOR VE=0 TO VT : REM bc-3(!) routinen laufen"
26210 DATA"!21020     SR$=",".Q",".Q"
26220 DATA"!21030     FOR HO=0 TO HT"
26230 DATA"!21040         GOSUB 220"
26240 DATA"!21050         SR$=SR$+CHR$(IN+CN)"
26250 DATA"!21060     NEXT HO"
26260 DATA"!21070     GOSUB 350:GOSUB 360"
26270 DATA"!21080 NEXT VE"
26280 DATA"!21090 RETURN",".M0"
26290 DATA".TFunktionstasten"
26300 DATA"=In BASICODE-3(!)- Programmen sind ueber die"
26310 DATA"Routinen 200, 210 und 450 neben den"
26320 DATA"Zeichentasten"
26330 DATA"(ASCII 32 ... 126) lediglich sechs Steuertasten"
26340 DATA"ansprechbar (ASCII 13, 28 ... 31 und 127)."
26350 DATA"!In der Version -3C koennen auch die - sofern"
26360 DATA"vorhandenen - Funktionstasten betaetigt und zur"
26370 DATA"Steuerung des Programmablaufs verwendet werden."
26380 DATA"Ueber die o.a. Routinen wird",".M2","=IN$ = "
26390 DATA".Q",".Q","und fuer",
26400 DATA"=F1 : IN = -1","!F2 : IN = -2","!F3 : IN = -3"
26410 DATA"!usw.         zurueckgegeben.",".M0"
26420 DATA".TBASICODE-3C"
26430 DATA"=Es bleibt zu hoffen, dass dieser knappe"
26440 DATA"Ueberblick ueber die Moeglichkeiten, die die"
26450 DATA"neue Version - BASICODE-3C - eroeffnet, einige"
26460 DATA"Hoerer des 'Computermagazins' neugierig macht"
26470 DATA"und ihnen bald die erweiterten"
26480 DATA"Uebersetzungs- Programme fuer ihre Computer"
26490 DATA"zur Verfuegung stehen."
26500 DATA"=Verwendete Unterlagen:"
26510 DATA"=Stichting BASICODE:"
26520 DATA"=   Definitie BASICODE-3C   (April 1991)"
26530 DATA"!   Toelichting BASICODE-3C (April 1991)"
26540 DATA"=   BASICODE-3 Bulletin no. 74 und 75"
26550 DATA"!                       10. und 17.7.91)"
26560 DATA"==Friedrich Dormeier"
26570 DATA"!Berlin 39 (Sept. 1991)"
26580 DATA".P0",".E"
26590 REM
30000 REM BASICODE-JOURNAL
30010 REM Informationen zu BASICODE-3C
30020 REM
32000 REM -------------------------
32010 REM FRIEDRICH DORMEIER
32020 REM BISMARCKSTR. 11A
32030 REM D-1000 BERLIN 39
32040 REM
32050 REM (030) 803 31 11
32060 REM
32070 REM C-128 - 9/91
32080 REM -------------------------
32090 REM
32100 REM Erstausstrahlung: Deutschlandsender Kultur
32110 REM-spezial 910918

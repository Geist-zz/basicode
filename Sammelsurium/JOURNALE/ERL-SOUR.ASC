1000 a=500:GOTO 20:rem --- source erlaeutrg. ---
1010 ht=ho:vt=ve:s$="":FOR i=1 TO ht:s$=s$+" ":NEXT i
1020 hl=0:hr=ht:vl=2:vr=vt-2:r$="":READ t$
1030 GOSUB 1410:ho=(ht-LEN(sr$)-6)/2:ve=0:GOSUB 110
1040 GOSUB 150:PRINT:PRINT:PRINT
1050 PRINT"Ausdruck ueber"
1060 PRINT:PRINT"   1  den Bildschirm,"
1070 PRINT"   2  den Drucker"
1080 PRINT:PRINT"Welche Nummer ?";:GOSUB 210
1090 sc=in-48:PRINT sc:IF sc<>2 THEN sc=1:GOTO 1220
1100 PRINT:PRINT"Wieviel Leerzeichen"
1110 INPUT"am Zeilenanfang    ";hl
1120 PRINT:PRINT"Wieviel Text-Zeichen"
1130 INPUT"auf jeder Zeile    ";i:hr=hl+i-1
1140 PRINT:PRINT"Wieviel Text-Zeilen"
1150 INPUT"auf jeder Seite    ";vr:vr=vr-3
1160 PRINT"(E) Endlospapier oder  "
1170 PRINT"(L) Lose Blaetter (E/L) ";
1180 GOSUB 210:IF in=76 THEN vl=-1:GOTO 1220
1190 IF in<>69 THEN GOSUB 250:GOTO 1160
1200 PRINT:PRINT"Wieviel Leerzeilen"
1210 INPUT"am Seitenende      ";vl:vr=vr+vl
1220 mm$="":IF hl>0 THEN mm$=LEFT$(s$,hl)
1230 GOSUB 1380:m$=mm$:al=vl:IF sc=2 THEN GOSUB 1360
1240 IF LEN(r$)>hr-hl THEN GOSUB 1820
1250 GOSUB 1440:IF r$<>"" THEN r$=r$+" "
1260 r$=r$+d$:GOTO 1240
1270 IF sc=2 THEN sr$=m$+sr$:GOSUB 350:GOSUB 360:GOTO 1290
1280 GOSUB 120:PRINT m$;sr$;:ve=ve+1:ho=0:GOSUB 110:hl=0
1290 m$=mm$:al=al+1:IF al<=vr THEN RETURN
1300 IF sc=1 THEN 1380
1310 IF vl>=0 THEN FOR i=1 TO vl:GOSUB 360:NEXT i:GOTO 1350
1320 PRINT"Neues Blatt in Drucker einlegen"
1330 PRINT"druecke dann       RETURN/ENTER"
1340 GOSUB 210:IF in<>13 THEN 1340
1350 al=vl
1360 sr$=LEFT$(s$,(hr-hl-LEN(t$))/2)+t$:GOSUB 1270
1370 sr$="":GOSUB 1270:al=vl:RETURN
1380 ho=3:ve=vt:GOSUB 110
1390 sr$="Weiter: druecke <RETURN/ENTER>":GOSUB 150
1400 GOSUB 210:IF in<>13 THEN 1400
1410 GOSUB 100:al=vl:sr$=t$
1420 PRINT LEFT$(s$,(ht-LEN(sr$))/2-3);:GOSUB 150
1430 PRINT:PRINT:RETURN
1440 READ d$
1450 IF d$="" THEN 1440
1460 IF LEFT$(d$,1)="." THEN 1530
1470 IF LEFT$(d$,1)<>">" THEN 1490
1480 d$=LEFT$(s$,23-LEN(r$))+MID$(d$,2):RETURN
1490 IF LEFT$(d$,1)<>"!" THEN RETURN
1500 IF d$="!" THEN d$="":GOTO 1520
1510 d$=MID$(d$,2)
1520 GOSUB 1760:GOTO 1450
1530 IF d$<>".E" THEN 1580
1540 j=1:GOSUB 1760:t$="E N D E"
1550 sr$="":GOSUB 1270:GOSUB 1270:sr$=t$:GOSUB 1270:t$=""
1560 sr$="":GOSUB 1270:IF al<>vl THEN 1560
1570 GOTO 950
1580 dd$=MID$(d$,2,1):d$=MID$(d$,3)
1590 IF dd$<>"R" THEN 1640
1600 IF sc=2 THEN RETURN
1610 IF LEN(r$)+6+LEN(d$)>=hr-hl THEN sr$=r$:GOSUB 1270:r$=""
1620 PRINT m$;r$;:sr$=d$:GOSUB 150:m$=""
1630 hl=hl+LEN(r$)+LEN(d$)+6:r$="":GOTO 1440
1640 IF dd$<>"M" THEN 1670
1650 i=VAL(d$):mm$="":IF i+hl=0 THEN 1440
1660 mm$=LEFT$(s$,i+hl):GOTO 1440
1670 IF dd$="P" THEN j=VAL(d$):GOSUB 1760:GOSUB 1780:GOTO 1440
1680 IF dd$="T" THEN t$=d$:j=10:GOSUB 1760:GOSUB 1780:GOTO 1600
1690 IF dd$="Q" THEN r$=r$+CHR$(34)+d$:GOTO 1440
1700 IF dd$<>"F" THEN 1730
1710 j=VAL(d$):GOSUB 1760
1720 j=j-1:IF j>-1 THEN sr$="":GOSUB 1270:GOTO 1720
1730 IF dd$<>"C" THEN 1750
1740 sr$=LEFT$(s$,(hr-hl-LEN(d$))/2)+d$:GOSUB 1270
1750 GOTO 1440
1760 GOSUB 1820:sr$=r$:GOSUB 1270:r$="":j=j-1
1770 RETURN
1780 IF (sc=1) OR (al+j>vr)THEN 1800
1790 sr$="":GOSUB 1270:GOSUB 1270:RETURN
1800 IF al<>vl THEN sr$="":GOSUB 1270:GOTO 1800
1810 RETURN
1820 k=hr-hl+1-LEN(m$)
1830 IF LEN(r$)<=k THEN RETURN
1840 IF MID$(r$,k,1)=" " THEN 1870
1850 k=k-1:IF k>0 THEN 1840
1860 sr$="":GOTO 1880
1870 sr$=LEFT$(r$,k-1):r$=MID$(r$,k+1)
1880 GOSUB 1270:GOTO 1820
1890 rem
25000 DATA" - SOURCE V 4.1 - "
25010 DATA"Das BASICODE-3-Programm 'SOURCE V 4.1' hat"
25020 DATA"seinen Ursprung in der Absicht des"
25030 DATA"'Computermagazins REM' (DS/Kultur) 1990"
25040 DATA"mit einem Einfuehrungskurs in die"
25050 DATA"Programmiersprache 'Pascal' zu beginnen"
25060 DATA"und Programme zur Verwendung mit den"
25070 DATA"Pascal-Compilern fuer die KC 85/2,3,4"
25080 DATA"und Z 1013 auszustrahlen."
25090 DATA"!!Besitzer anderer Computer und Pascal-"
25100 DATA"Compiler haetten zwar den Kurs ueber den"
25110 DATA"Rundfunk verfolgen, nicht aber die in"
25120 DATA"diesem Rahmen ausgestrahlten Programme"
25130 DATA"zur Erlaeuterung erhalten koennen."
25140 DATA".P0"
25150 DATA"Mit dem Programm 'SOURCE' - einer vereinfachten"
25160 DATA"Textverarbeitung - koennen Pascal-Quellprogramme"
25170 DATA"!! - aufgestellt und berichtigt,"
25180 DATA"! - als BASICODE-3 Files (sequentiell)"
25190 DATA"!   gespeichert, uebertragen, geladen,"
25200 DATA"! - ausgedruckt werden."
25210 DATA"!!Dieses Programm ersetzt keinen Compiler; es"
25220 DATA"macht nur die Uebertragung von Programmen"
25230 DATA"(Sourcecode) moeglich."
25240 DATA".P0"
25250 DATA".R1. Systembedingte Aenderungen"
25260 DATA"!!Das Programm geht von einer Laenge des Textes,"
25270 DATA"d.h. des uebertragenen Programms, von 200 Zeilen"
25280 DATA"aus. Bei manchem Computer mag dies den"
25290 DATA"verfuegbaren Raum im Arbeitsspeicher"
25300 DATA"uebersteigen. Es wird beim Laden dieses"
25310 DATA"Programms ein",".R'OUT OF MEMORY-Error"
25320 DATA"angezeigt. Dies kann durch Aendern der Zeilen"
25330 DATA"# 1000 und 1010 vermieden werden:"
25340 DATA"!!   1000 GOSUB  270:A=FR-300:GOTO  20"
25350 DATA"!   1010 DIM T$(X)"
25360 DATA"!!Der Wert von X ist so zu waehlen (X < 200),"
25370 DATA"dass es beim Laden des Programms nicht mehr zu"
25380 DATA"einem OM-Error kommt."
25390 DATA".P0"
25400 DATA"Die",".RRoutine 'Drucken'","nimmt die Verwendung"
25410 DATA"von Endlospapier (72 Zeilen/Blatt) an. Es werden"
25420 DATA"65 Zeilen gedruckt und 7 Leerzeilen eingefuegt."
25430 DATA"U.U. ist sie den eigenen Erfordernissen"
25440 DATA"(ab Zeile # 4000) anzupassen."
25450 DATA".P0"
25460 DATA".R2. Eingabe (# 1)"
25470 DATA"!!Mit dem Programmteil 'Eingabe' kann ein"
25480 DATA"Pascal-Programm in der Form eines Quelltextes"
25490 DATA"eingegeben werden. Natuerlich koennen auch"
25500 DATA"beliebige andere Texte (Maschinensprache-"
25510 DATA"Programme solche in anderen Programmiersprachen"
25520 DATA"usw.) eingegeben werden."
25530 DATA"!!Der Text ist zeilenweise zu schreiben. Evtl."
25540 DATA"auftretende Eingabefehler bleiben vorerst"
25550 DATA"unberuecksichtigt. Fuer spaetere Aenderungen"
25560 DATA"oder Ergaenzungen koennen Leerzeilen eingegeben"
25570 DATA"werden. Praktisch wird mit diesem Teil ein"
25580 DATA"'Rohtext' gefertigt, der dann unter dem"
25590 DATA"Programmpunkt # 6 ('Berichtigen') geaendert"
25600 DATA"werden kann."
25610 DATA".P0"
25620 DATA"Als Bestandteil von Texten koennen Buchstaben,"
25630 DATA"Ziffern und Sonderzeichen - auch"
25640 DATA"!! Komma ','     ; Semikolon   ';'"
25650 DATA"! Punkt  '.'     ; Doppelpunkt ':'"
25660 DATA"!!die zum von 'Pascal' verwendeten Zeichensatz"
25670 DATA"gehoeren - eingegeben werden. Von der Verwendung"
25680 DATA"des Anfuehrungszeichens (ASCII 34) ist"
25690 DATA"abzuraten. Es ist auch nicht Teil des fuer"
25700 DATA"'Pascal' geltenden Alphabets."
25710 DATA"!!Steuerzeichen (CURSOR rechts/links, DELETE)"
25720 DATA"koennen nicht eingegeben werden; die Darstellung"
25730 DATA"wird 'unordentlich'. Die Textzeilen sind unter"
25740 DATA"# 6 zu berichtigen."
25750 DATA".P0"
25760 DATA"Die Laenge einer Textzeile entspricht der je"
25770 DATA"nach Computer auf dem Bildschirm darstellbaren"
25780 DATA"Anzahl von Zeichen/Zeile. Sie wird durch ein"
25790 DATA"ueber dem Eingabefeld erscheinenden Laengsstrich"
25800 DATA"angezeigt."
25810 DATA"!!Die Eingabe einer Zeile wird mit ENTER/RETURN"
25820 DATA"abgeschlossen. Nur ENTER/RETURN bildet eine"
25830 DATA"Leerzeile."
25840 DATA"!!Das Textende wird durch Eingabe des Zeichens"
25850 DATA"'@' (kaufmaennisches 'at', 'Klammeraffe')"
25860 DATA"als letztes oder erstes Zeichen in einer Zeile"
25870 DATA"gekennzeichnet. Das Programm kehrt zum Menu"
25880 DATA"zurueck."
25890 DATA".P0"
25900 DATA".R3. Berichtigen (# 6)"
25910 DATA"!!Mit Betaetigung der Leertaste werden die"
25920 DATA"einzelnen Zeilen des Textes angezeigt. Nach"
25930 DATA"Druecken der 'C'-Taste kann die angezeigte"
25940 DATA"Zeile berichtigt (ueberschrieben) werden."
25950 DATA"!! - Mit ENTER/RETURN allein wird die"
25960 DATA"!   Zeile geloescht."
25970 DATA"!! - Nach Druecken der DELETE-Taste"
25980 DATA"!   (ASCII 127) kann die angezeigte"
25990 DATA"!   Zeile zeichenweise von rechts"
26000 DATA"!   geloescht werden."
26010 DATA".P0"
26020 DATA"Die Korrektur wird mit ENTER/RETURN"
26030 DATA"abgeschlossen."
26040 DATA"!!Eine Berichtigung der Zeile ist nicht"
26050 DATA"moeglich, wenn die tatsaechliche Zeilenlaenge"
26060 DATA"groesser als die auf dem jeweiligen Computer"
26070 DATA"darstellbare ist."
26080 DATA".P0"
26090 DATA".R4. Erweitern (# 7)"
26100 DATA"!!Hiermit kann ein bereits abgeschlossener"
26110 DATA"Text erweitert werden. Es wird die lezte"
26120 DATA"Zeile des vorhandenen Textes angezeigt."
26130 DATA"!!Fuer die Erweiterung des Textes gelten"
26140 DATA"die Hinweise zu 'Eingabe'!"
26150 DATA".E"
26160 rem
32000 rem ------------------------
32010 rem (c) friedrich dormeier
32020 rem     bismarckstr. 11a
32030 rem     d-1000 berlin 39
32040 rem     (030) 803 31 11
32050 rem
32060 rem c-128 - 9/1990
32070 rem ------------------------
32080 rem erstausstrahlung: ds-kultur
32090 rem -spezial 900919

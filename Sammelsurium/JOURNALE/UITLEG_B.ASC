1000 a=500:GOTO 20:REM tips voor programmeurs
1010 GOTO 2500:REM scherm opmeten
1020 READ re$
1030 IF re$<>"***" THEN 1070
1040 GOSUB 1240
1050 IF ar<>0 THEN 1040
1060 GOTO 950
1070 bk=1
1080 IF bk>LEN(re$) THEN 1020
1090 ak=0
1100 ak=ak+1
1110 IF bk+ak>LEN(re$) THEN 1130
1120 IF MID$(re$,bk+ak,1)<>" " THEN 1100
1130 wo$=MID$(re$,bk,ak)
1140 bk=bk+LEN(wo$)+1
1150 IF pl+LEN(wo$)>=rl THEN GOSUB 1240
1160 IF wo$="&" THEN wo$="":GOSUB 1240:GOTO 1200
1170 IF wo$<>"#" THEN 1200
1180 GOSUB 1240:IF ar<>0 THEN 1180
1190 GOTO 1080
1200 PRINT wo$;
1210 pl=pl+LEN(wo$)+1
1220 IF pl<rl-1 THEN PRINT" ";
1230 GOTO 1080
1240 PRINT 
1250 ar=ar+1
1260 IF ar<sh-2 THEN 1320
1270 PRINT 
1280 PRINT"  VERVOLG: <return>";
1290 GOSUB 210:IF in$<>chr$(13) THEN 1290
1300 ar=0
1310 GOSUB 100
1320 pl=0
1330 RETURN
1340 REM
2500 GOSUB 100
2510 PRINT"Een ogenblikje,"
2520 sh=14:ve=14:rl=16
2530 ho=rl:GOSUB 110:GOSUB 120
2540 IF ho=rl THEN rl=rl+1:GOTO 2530
2550 ve=sh:GOSUB 110:GOSUB 120
2560 IF ve=sh THEN sh=sh+1:GOTO 2550
2580 GOSUB 100
2590 pl=0:ar=0
2600 GOTO 1020
2610 REM
25000 DATA "Hallo HOBBYSCOOPVRIENDEN ! & &"
25010 DATA "& De redactie van HOBBYSCOOP wordt gewoon"
25020 DATA "overstroomd met brieven en programma's"
25030 DATA "en we komen er niet aan toe om alles binnen"
25040 DATA "redelijk korte tijd te beantwoorden."
25050 DATA "& We hopen dus op enig begrip bij U allen !"
25060 DATA "& & Daarom nu deze bijzondere vorm van"
25070 DATA "algemene kennisgeving. &"
25080 DATA "& BASICODE-2 wordt nu al weer meer"
25090 DATA "dan een jaar gebruikt en we hebben er"
25100 DATA "inmiddels de nodige ervaring mee opgedaan."
25110 DATA "# Alle programma's"
25120 DATA "die worden ingestuurd worden eerst bekeken"
25130 DATA "door enkele mensen van ons team om te zien"
25140 DATA "of ze wel werken, wat ze doen, of ze wel"
25150 DATA "of niet geschikt zijn voor uitzending en ..."
25160 DATA "& & OF ZE VOLDOEN AAN HET & BASICODE-PROTOCOL"
25170 DATA "& & Dat protocol vindt U in het BASICODE-boek"
25180 DATA "dat thuis naast Uw computer ligt en dat U"
25190 DATA "anders spoedig moet bestellen."
25200 DATA "& In hoofdstuk 3 vindt U het protocol."
25210 DATA "Daarin staat"
25220 DATA "precies alles wat mag."
25230 DATA "# Het probleem is nu, dat niet iedereen alles"
25240 DATA "even goed leest zodat ingezonden"
25250 DATA "programma's soms dingen bevatten die volgens"
25260 DATA "het protocol NIET mogen. &"
25270 DATA "& Als het er niet te veel zijn dan wordt het"
25280 DATA "nog door onze mensen veranderd, maar als er"
25290 DATA "erg veel fouten gemaakt zijn dan moeten we"
25300 DATA "U wel het programma terugsturen met het"
25310 DATA "vriendelijke verzoek, de fouten te verbeteren."
25320 DATA "# Het protocol is er natuurlijk niet voor"
25330 DATA "niets. & Sommige dingen mogen in Uw computer"
25340 DATA "wel en in die van een ander niet, en zo gaat"
25350 DATA "het ook omgekeerd. BASICODE is bedoeld om"
25360 DATA "programma's te kunnen uitwisselen en dan is"
25370 DATA "het wel fijn als elk programma ook op elke"
25380 DATA "computer meteen werkt. #"
25390 DATA "Daarom nu een bericht over een aantal veel"
25400 DATA "gemaakte fouten tegen de regels. & Probeert"
25410 DATA "U het een en ander te onthouden of aan te"
25420 DATA "tekenen ?"
25430 DATA "# INPUT''Hoe heet je'';NA$"
25440 DATA "& & Zoiets mag niet. Gebruik alleen de volgende"
25450 DATA "opbouw: & &"
25460 DATA "PRINT''Hoe heet je'';:INPUT NA$"
25470 DATA "# IF A=>B THEN GOTO 1200"
25480 DATA "& & Hierin zitten twee fouten: & &"
25490 DATA "=> mag zo niet maar MOET perse in de"
25500 DATA "volgorde >= & &"
25510 DATA "& THEN GOTO  is een onjuiste combinatie."
25520 DATA "& & De regel zou moeten luiden:"
25530 DATA "& IF A>=B THEN 1200"
25540 DATA "& & (en dus ook niet :  GOTO 1200)"
25550 DATA "# IF A>B AND C>D THEN 2300 &"
25560 DATA "& Sommige computers, en dus ook ons protocol,"
25570 DATA "willen perse extra haakjes. & Doe het dus"
25580 DATA "voortaan als volgt: &"
25590 DATA "& IF (A>B) AND (C>D) THEN 2300"
25600 DATA "# DATA  Jan,Piet,Kees,23 & &"
25610 DATA "Net als bij een PRINT opdracht moet ook"
25620 DATA "in een DATA -regel de tekst tussen"
25630 DATA "dubbele aanhalingstekens. Doe het dus zo: &"
25640 DATA "& DATA  ''Jan'',''Piet'',''Kees'',23"
25650 DATA "& & Dus alles wat wordt ingelezen met"
25660 DATA "& READ ..$  moet tussen quotes."
25670 DATA "# Nu we het toch over aanhalingstekens hebben,"
25680 DATA "& volgens het protocol MOET elke regel een"
25690 DATA "EVEN aantal quotes bevatten."
25700 DATA "& & Dus als u een PRINT of DATA  regel"
25710 DATA "iets met tekst laat doen, sluit dan die"
25720 DATA "regel netjes af met weer een quote."
25730 DATA "& &"
25740 DATA "Dus niet: & PRINT''Jan is gek"
25750 DATA "& &"
25760 DATA "Maar wel: & PRINT''Jan is gek''"
25770 DATA "# U mag er niet van uitgaan dat alle"
25780 DATA "variabelen automatisch de waarde 0 hebben."
25790 DATA "Een variabele MOET dus EERST een bepaalde"
25800 DATA "waarde krijgen voordat u er iets mee doet."
25810 DATA "& & Een DIM-opdracht is verplicht voor alle"
25820 DATA "array's, dus OOK als ze minder dan 10"
25830 DATA "elementen hebben. Zet uw DIM-opdrachten"
25840 DATA "ook altijd voor in het programma, dus op"
25850 DATA "regel 1010 of kort daarna."
25860 DATA "# PRINT TAB(10)X & &"
25870 DATA " Juist, daar had een ; tussen moeten staan"
25880 DATA "& & Dus:"
25890 DATA "& & PRINT TAB(10);X & & & &"
25900 DATA "INPUT A,B"
25910 DATA "& & mag ook niet, doe het zo:"
25920 DATA "& & INPUT A : INPUT B"
25930 DATA "# Houdt er ook rekening mee dat de"
25940 DATA "regellengte in BASICODE is gebonden aan"
25950 DATA "een maximum van 60 karakters."
25960 DATA "& Diverse computers kunnen geen langere regels"
25970 DATA "verwerken."
25980 DATA "# Dat was het voor deze keer. Wees niet boos"
25990 DATA "dat we uw aandacht vragen voor deze dingen"
26000 DATA "want het is voor iedereen plezieriger als"
26010 DATA "alle programma's echt uitwisselbaar zijn."
26020 DATA "& & Natuurlijk maakte u uw programma's"
26030 DATA "altijd al erg overzichtelijk van opbouw,"
26040 DATA "dus daar hoeven we nu geen aandacht aan"
26050 DATA "te besteden."
26060 DATA "# Met vriendelijke groeten van het hele"
26070 DATA "& & NOS-HOBBYSCOOP team."
26080 DATA "# En blijf uw programma's insturen naar:"
26090 DATA "& & NOS-HOBBYSCOOP"
26100 DATA "& & Postbus 1200"
26110 DATA "& & 1200 BE Hilversum"
26120 DATA "***"
30000 REM    nos - hobbyscoop
30010 REM    uitleg basicode-2
30020 REM       (protocol)
30030 REM
30040 REM copyright  nos  hilversum

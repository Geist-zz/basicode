1000 A=10000:GOTO 20:REM SOURCE.V4
1010 DIM T$(200),J(200):REM ZEILEN
1020 JE=0:H=HO:Z=INT((H+1)/5):ZH=5*(Z-1)
1030 REM FR=1:GOSUB  280
1040 REM
1050 GOSUB 9000
1060 SR$="Erlaeuterung ? - J/N - "
1070 HO=INT((H-LEN(SR$))/2):VE=8:GOSUB 110:PRINT SR$;
1080 GOSUB 210:IF IN=74 THEN GOSUB 10000
1090 REM ------ MENUE
1100 FL=0:GOSUB 100:GOSUB 9000
1110 SR$="   M  E  N  U  E   "
1120 GOSUB 9260
1130 HO=0:VE=3:GOSUB 110
1140 PRINT"  1 - Eingabe":PRINT
1150 PRINT"  2 - Anzeige-Bildschirm":PRINT
1160 PRINT"  3 - Drucken":PRINT
1170 PRINT"  4 - Speichern (Write)":PRINT
1180 PRINT"  5 - Laden (Read)":PRINT
1190 PRINT"  6 - Berichtigen":PRINT
1200 PRINT"  7 - Erweitern":PRINT
1210 PRINT"  8 - Loeschen":PRINT
1220 PRINT"  9 - Erlaeuterung":PRINT
1230 PRINT"  X - Ende        ":PRINT
1240 PRINT"  Eingabe: 1...9 oder X ";
1250 GOSUB 210
1260 IF IN=88 THEN 1330
1270 IF IN=55 THEN FL=1
1280 IF (IN<49) OR (IN>57) THEN 1250
1290 IF IN>53 THEN 1310
1300 ON IN-48 GOSUB 2000,3000,4000,5000,6000:GOTO 1100
1310 ON IN-53 GOSUB 7000,2000,8000,10000:GOTO 1100
1320 REM ------ ENDE
1330 GOSUB 100:GOSUB 9000
1340 SR$="ENDE - Sicher ? - J/N - "
1350 HO=INT((H-LEN(SR$))/2):VE=8:GOSUB 110:PRINT SR$;
1360 GOSUB 210:IF IN=74 THEN FR=0:GOSUB 280:GOTO 950
1370 GOTO 1100
1380 REM ------ EINGABE
2000 GOSUB 100:GOSUB 9000
2010 IF FL=1 THEN SR$=" E r w e i t e r n ":GOTO 2030
2020 SR$="   E i n g a b e   "
2030 GOSUB 9260
2040 IF (FL=1) AND (JE=0) THEN GOSUB 9090:RETURN
2050 HO=4:VE=3:GOSUB 110:GOSUB 9120
2060 CT=3:CN=0:V=4:J=1:IF FL=1 THEN J=JE+1:GOSUB 2370
2070 SR=J:T$(J)="":L=0
2080 HO=0:VE=V:GOSUB 110
2090 GOSUB 310:PRINT SR$;":";
2100 GOSUB 210
2110 IF (IN=13) OR (IN=64) THEN IN$=""
2120 IF IN=127 THEN GOSUB 120:HH=HO:HO=HH-1:GOSUB 110:IN$=""
2130 IF HO<4 THEN HO=4:GOSUB 110
2140 IF IN=28 THEN GOSUB 120:HH=HO-1:HO=HH:GOSUB 110:IN$=""
2150 IF IN=29 THEN GOSUB 120:HH=HO+1:HO=HH:GOSUB 110:IN$=""
2160 IF (IN=30) OR (IN=31) THEN IN$="":GOTO 2080
2170 PRINT IN$;:T$(J)=T$(J)+IN$:L=LEN(T$(J))
2180 IF IN=64 THEN 2260
2190 IF L>(5*(Z-1)) THEN GOSUB 2280:GOTO 2070
2200 IF IN<>13 THEN 2100
2210 REM
2220 IF V<=18 THEN J=J+1:V=V+1
2230 GOSUB 120
2240 IF VE=18 THEN GOSUB 100:GOSUB 9000:GOSUB 2370
2250 GOTO 2070
2260 JE=J:IF T$(J)="" THEN JE=J-1
2270 GOSUB 9040:FL=0:RETURN
2280 HO=4:VE=V+3:GOSUB 110
2290 PRINT"Wiederholen ! - Taste ! ";
2300 GOSUB 210
2310 HO=4:VE=V:GOSUB 110
2320 FOR N=0 TO LEN(T$(J)):PRINT" ";:NEXT N
2330 HO=4:VE=V+3:GOSUB 110
2340 PRINT"                        ";
2350 RETURN
2360 REM
2370 IF FL=1 THEN SR$=" E r w e i t e r n ":GOTO 2390
2380 SR$="   E i n g a b e   "
2390 GOSUB 9260
2400 HO=4:VE=3:GOSUB 110:GOSUB 9120
2410 V=4
2420 HO=0:VE=4:GOSUB 110
2430 SR=J-1:GOSUB 310:PRINT SR$;":";T$(J-1)
2440 V=5:RETURN
2450 REM ------ ANZEIGE
3000 GOSUB 100:GOSUB 9000
3010 SR$="   A n z e i g e   "
3020 GOSUB 9260
3030 V=3
3040 IF JE=0 THEN GOSUB 9090:RETURN
3050 FOR J=1 TO JE
3060 HO=0:VE=V:GOSUB 110:PRINT T$(J):V=V+1
3070 IF LEN(T$(J))>=H THEN V=V+1
3080 IF V=17 THEN GOSUB 9040:GOSUB 3120
3090 NEXT J
3100 GOSUB 9040:RETURN
3110 REM
3120 GOSUB 100:GOSUB 9000
3130 SR$="   A n z e i g e   "
3140 GOSUB 9260
3150 V=3
3160 RETURN
3170 REM ------ DRUCKEN
4000 GOSUB 100:GOSUB 9000
4010 SR$="   D r u c k e n   "
4020 GOSUB 9260
4030 IF JE=0 THEN GOSUB 9090:RETURN
4040 HO=0:VE=3:GOSUB 110
4050 PRINT"Drucker bereit  ?"
4060 PRINT"Papier justiert ? - J/N -";
4070 GOSUB 210:IF IN<>74 THEN RETURN
4080 Z=0
4090 FOR J=1 TO JE
4100 SR$="     "+T$(J):GOSUB 350:GOSUB 360:Z=Z+1
4110 IF Z=65 THEN GOSUB 210:GOSUB 4140:Z=1
4120 NEXT J
4130 GOSUB 9040:RETURN
4140 FOR N=1 TO 7:GOSUB 360:NEXT N:RETURN
4150 REM ------ SPEICHERN
5000 GOSUB 100:GOSUB 9000
5010 SR$=" S p e i c h e r n "
5020 GOSUB 9260
5030 IF JE=0 THEN GOSUB 9090:RETURN
5040 HO=0:VE=5:GOSUB 110
5050 PRINT" 1 - BASICODE-3   (Cass.)":PRINT
5070 PRINT" 2 - Ihr Computer (Cass.)":PRINT
5090 PRINT" 3 - Ihr Computer (Disk.)":PRINT
5110 PRINT" X - Abbruch"
5120 HO=0:VE=12:GOSUB 110
5130 FOR N=0 TO H:PRINT"-";:NEXT N:PRINT
5140 HO=1:VE=13:GOSUB 110:GOSUB 210
5150 IF IN=88 THEN GOSUB 9040:RETURN
5160 IF (IN<49) OR (IN>51) THEN 5120
5170 REM
5180 NF=1+2*(IN-49)
5190 REM
5200 HO=0:VE=11:GOSUB 110
5210 SR=IN-48:GOSUB 300
5220 PRINT"(";SR$;"):";
5230 HO=5:VE=11:GOSUB 110
5240 PRINT"Name (max. 7 Zch.): ";
5250 F$="":INPUT F$
5260 IF F$="" THEN GOSUB 9040:RETURN
5270 NF$=F$:GOSUB 500
5280 SR=JE:GOSUB 300:GOSUB 560
5290 REM
5300 FOR J=1 TO JE
5310 SR$=T$(J)
5320 GOSUB 560
5330 IF IN=-1 THEN PRINT"Fehler !":J=JE
5340 NEXT J
5350 GOSUB 580
5360 GOSUB 9040:RETURN
5370 REM ------ LADEN
6000 GOSUB 100:GOSUB 9000
6010 SR$="     L a d e n     "
6020 GOSUB 9260
6030 REM
6040 HO=0:VE=5:GOSUB 110
6050 PRINT" 1 - BASICODE-3    (Cass.)":PRINT
6070 PRINT" 2 - Ihr Computer  (Cass.)":PRINT
6090 PRINT" 3 - Ihr Computer  (Disk.)":PRINT
6110 PRINT" X - Abbruch"
6120 HO=0:VE=12:GOSUB 110
6130 FOR N=1 TO H:PRINT"-";:NEXT N:PRINT
6140 HO=1:VE=13:GOSUB 110:GOSUB 210
6150 IF IN=88 THEN GOSUB 9040:RETURN
6160 IF (IN<49) OR (IN>51) THEN 6120
6170 REM
6180 NF=2*(IN-49)
6190 REM
6200 HO=0:VE=11:GOSUB 110
6210 SR=IN-48:GOSUB 300
6220 PRINT"(";SR$;"):";
6230 PRINT" N a m e : ";
6240 F$="":INPUT F$
6250 IF F$="" THEN GOSUB 9040:RETURN
6260 NF$=F$
6270 GOSUB 500:GOSUB 540
6280 JE$=IN$:JE=VAL(JE$)
6290 FOR J=1 TO JE
6300 GOSUB 540
6310 T$(J)=IN$
6320 IF IN=-1 THEN PRINT"FEHLER !":J=JE
6330 NEXT J
6340 GOSUB 580
6350 GOSUB 9040:RETURN
6360 REM ------ BERICHTIGEN
7000 GOSUB 100:GOSUB 9000
7010 SR$="    Berichtigen    "
7020 GOSUB 9260
7030 IF JE=0 THEN GOSUB 9090:RETURN
7040 HO=0:VE=3:GOSUB 110
7050 PRINT"Der Text umfasst";JE;"Zeilen."
7060 PRINT"Start / Weiter => SPACE !"
7070 PRINT"Ueberschreiben =>  'C'  !"
7080 PRINT"Abbruch        =>  'X'  !"
7090 FOR N=1 TO H:PRINT"-";:NEXT N
7100 J=1:CT=3:CN=0
7110 SR=J:GOSUB 310
7120 HO=0:VE=8:GOSUB 110
7130 PRINT SR$;":";T$(J);:L=LEN(T$(J))
7140 HO=0:VE=8:GOSUB 110:GOSUB 210
7150 IF (IN=67) AND (L>ZH) THEN GOSUB 7460:GOTO 7200
7160 IF IN=67 THEN GOSUB 7260:GOTO 7240
7170 IF IN=88 THEN GOSUB 9040:RETURN
7180 IF IN<>32 THEN 7140
7190 IF (J=JE) OR (JE=0) THEN GOSUB 9040:RETURN
7200 FOR VE=8 TO 14
7210 HO=0:GOSUB 110
7220 FOR N=1 TO H:PRINT" ";:NEXT N
7230 NEXT VE
7240 J=J+1:IF J=JE+1 THEN GOSUB 9040:RETURN
7250 GOTO 7110
7260 HO=4:VE=12:GOSUB 110:GOSUB 9120
7270 T$="":HO=0:VE=13:GOSUB 110
7280 PRINT SR$;":";T$(J);
7290 REM
7300 HO=4:VE=13:GOSUB 110
7310 GOSUB 210
7320 IF IN=13 THEN IN$=""
7330 IF (IN>27) AND (IN<32) THEN IN$="":GOTO 7310
7340 IF IN=127 THEN IN$="":GOSUB 9180
7350 IF IN=127 THEN HO=HO-1:VE=13:GOSUB 110:GOTO 7310
7360 PRINT IN$;:T$=T$+IN$
7370 IF IN<>13 THEN 7310
7380 T$(J)=T$
7390 IF (IN=13) AND (T$="") THEN T$(J)="":GOSUB 7550
7400 FOR VE=8 TO 15
7410 HO=0:GOSUB 110
7420 FOR N=1 TO H:PRINT" ";:NEXT N:PRINT
7430 NEXT VE
7440 GOSUB 7520:RETURN
7450 IF IN=13 THEN IN$=""
7460 HO=4:VE=12:GOSUB 110
7470 PRINT"Keine Berichtigung moeglich !"
7480 HO=25:VE=14:GOSUB 110
7490 PRINT"Taste =>";:GOSUB 210
7500 RETURN
7510 REM
7520 IF T$<>"" THEN T$=LEFT$(T$,LEN(T$)-1)
7530 RETURN
7540 REM
7550 FOR N=J TO JE
7560 T$(N)=T$(N+1)
7570 NEXT N
7580 J=J-1:JE=JE-1:RETURN
7600 REM ------ LOESCHEN
8000 GOSUB 100:GOSUB 9000
8010 SR$="  L o e s c h e n  "
8020 GOSUB 9260
8030 IF JE=0 THEN GOSUB 9090:RETURN
8040 SR$="LOESCHEN - Sicher ? - J/N - "
8050 HO=INT((H-LEN(SR$))/2):VE=8:GOSUB 110:PRINT SR$;
8060 H1=HO:GOSUB 210
8070 IF IN=74 THEN 8090
8080 RETURN
8090 FOR J=1 TO JE:T$(J)="":NEXT J:JE=0
8100 HO=H1:VE=10:GOSUB 110
8110 PRINT"Quelltext geloescht !"
8120 GOSUB 9040:RETURN
8130 RETURN
8140 REM ------ SUBROUTINEN
9000 SR$="Pascal - Quelltexte"
9010 HO=INT((H-LEN(SR$)-6)/2):VE=0:GOSUB 110
9020 GOSUB 150:RETURN
9030 REM
9040 SR$="Taste =>":GOSUB 250
9050 HO=INT((H-LEN(SR$)-6)/2):VE=23:GOSUB 110:GOSUB 150
9060 GOSUB 120:HO=HO+1:VE=23:GOSUB 110:GOSUB 210:IN=58
9070 RETURN
9080 REM
9090 HO=9:VE=8:GOSUB 110
9100 PRINT"Kein Quellcode !":GOSUB 9040:RETURN
9110 REM
9120 FOR N=1 TO Z-1
9130 PRINT"----+";
9140 NEXT N
9150 PRINT:RETURN
9160 REM
9170 REM ------ ASCII 127
9180 LX=LEN(T$(J)):IF LX<2 THEN T$(J)=" ":LX=1
9190 LZ=LX+4
9200 HO=LZ:VE=13:GOSUB 110
9210 IF IN<>127 THEN 9200
9220 HO=LZ:VE=13:GOSUB 110
9230 PRINT" ";:T$(J)=LEFT$(T$(J),LEN(T$(J))-1):T$=T$(J)
9240 RETURN
9250 REM ------ ZENTRIEREN
9260 HO=INT((H-LEN(SR$)-6)/2):VE=1:GOSUB 110
9270 GOSUB 150:RETURN
9280 REM
9290 REM ------ ERLAEUTERUNGEN
10000 GOSUB 100:GOSUB 9000:GOSUB 10820
10010 PRINT"Mit diesem Programm kann der Quelltext"
10020 PRINT"eines Pascal-Programms aufgestellt"
10030 PRINT"und als sequentielles File (Diskette"
10040 PRINT"oder Cassette) gespeichert werden.":PRINT
10050 PRINT"Ob und inwieweit dieses File Grundlage"
10060 PRINT"einer Kompilation sein kann, haengt"
10070 PRINT"von dem benutzten Pascal-Compiler ab.":PRINT
10080 PRINT"Ggf. ist eine Konvertierung oder An-"
10090 PRINT"passung erforderlich. In jedem Fall"
10100 PRINT"ist es moeglich, den Quelltext ueber"
10110 PRINT"den Bildschirm oder den Drucker aus-"
10120 PRINT"zugeben, ihn zu speichern oder als"
10130 PRINT"BASICODE-File zu uebertragen.":PRINT
10140 PRINT"Entsprechendes gilt fuer Programme,"
10150 PRINT"die in Assembler oder einer anderen"
10160 PRINT"Programmiersprache aufgestellt sind."
10170 GOSUB 9040:GOSUB 100:GOSUB 9000:GOSUB 10820
10180 PRINT"Eingabehinweise (1):":PRINT
10190 PRINT"- Es koennen alle ASCII-Zeichen ein-"
10200 PRINT"  gegeben werden - auch Sonderzeichen,"
10210 PRINT"  wie (,) (.) (:) (;).":PRINT
10220 PRINT"- Steuerzeichen koennen nicht ver-"
10230 PRINT"  wendet werden. Es ist nur eine"
10240 PRINT"  Berichtigung (vergl. # 6) moeglich.":PRINT
10250 REM
10260 PRINT"- Die Laenge einer Programmzeile ent-"
10270 PRINT"  spricht der - je nach Computer - auf"
10280 PRINT"  dem Bildschirm moeglichen Anzahl"
10290 PRINT"  von Zch./Zeile (32, 40, 42, 64, 80).":PRINT
10300 PRINT"- Die Eingabe einer Zeile wird mit"
10310 PRINT"  ENTER / RETURN abgeschlossen."
10320 GOSUB 9040:GOSUB 100:GOSUB 9000:GOSUB 10820
10330 PRINT"Eingabehinweise (2):":PRINT
10340 PRINT"- Nur ENTER / RETURN bildet eine Leer-"
10350 PRINT"  zeile.":PRINT
10360 PRINT"- Die Eingabe eines Textes wird mit"
10370 PRINT"  der letzten Zeile abgeschlossen."
10380 PRINT"  Mit der Eingabe des Zeichens '@'"
10390 PRINT"  (Kaufmaennisches 'zu', Klammeraffe)"
10400 PRINT"  als letztes oder erstes Zeichen in"
10410 PRINT"  einer Zeile, kehrt das Programm in"
10420 PRINT"  das Haupt-Menu zurueck.":PRINT
10430 GOSUB 9040:GOSUB 100:GOSUB 9000:GOSUB 10820
10440 PRINT"Speicherplatz:":PRINT
10450 PRINT"Sollte nach Laden/RUN ein OUT OF"
10460 PRINT"MEMORY-Error angezeigt werden, ist in"
10470 PRINT"Zeile # 1010 die Dimensionierung zu"
10480 PRINT"ueberpruefen."
10490 GOSUB 9040:GOSUB 100:GOSUB 9000:GOSUB 10820
10500 PRINT"Berichtigen:":PRINT
10510 PRINT"Mit Betaetigung der Leertaste werden"
10520 PRINT"die einzelnen Zeilen des Quelltextes"
10530 PRINT"angezeigt. Nach Druecken der 'C'-Taste"
10540 PRINT"kann die angezeigte Zeile berichtigt"
10550 PRINT"(ueberschrieben) werden. Die Korrektur"
10560 PRINT"wird mit ENTER/RETURN abgeschlossen.":PRINT
10570 PRINT"Die Berichtigung ist NICHT moeglich,"
10580 PRINT"wenn die tatsaechliche Zeilenlaenge"
10590 PRINT"groesser als die auf dem eigenen"
10600 PRINT"Computer darstellbare ist.":PRINT
10610 PRINT"Erweitern:":PRINT
10620 PRINT"Mit diesem Programmteil kann ein"
10630 PRINT"Quelltext erweitert werden.  Fuer"
10640 PRINT"Eingabe und Abschluss gelten die"
10650 PRINT"Hinweise zu - EINGABE - ."
10660 GOSUB 9040:GOSUB 100:GOSUB 9000:GOSUB 10820
10670 PRINT"Loeschen:":PRINT
10680 PRINT"Im Modus BERICHTIGEN kann mit 'C'"
10690 PRINT"und ENTER/RETURN eine Textzeile ge-"
10700 PRINT"loescht werden.":PRINT
10710 PRINT"Soll zeichenweise von rechts geloescht"
10720 PRINT"werden, so ist DELETE (ASCII 127) zu"
10730 PRINT"druecken.":PRINT
10740 PRINT"Ausdruck:":PRINT
10750 PRINT"Die Routine ist fuer den Druck von"
10760 PRINT"65 Zeilen (80 Zeichen/Zeile) + 7"
10770 PRINT"Leerzeilen je Seite eingestellt."
10780 PRINT"U.U. sind die Zeilen ab # 4000 ff."
10790 PRINT"zu aendern."
10800 GOSUB 9040:RETURN
10810 REM
10820 SR$="   Erlaeuterung    "
10830 GOSUB 9260
10840 HO=0:VE=3:GOSUB 110:RETURN
10850 REM
32000 REM --------------------------
32010 REM FRIEDRICH DORMEIER
32020 REM BISMARCKSTR. 11A
32030 REM D-1000 BERLIN 39
32040 REM (030) 803 31 11
32050 REM
32060 REM C64/C128 - 8/1990
32070 REM --------------------------
32080 REM MIT DANK AN J. HAUBRICH
32090 REM (STICHTING BASICODE)
32100 REM --------------------------
32110 REM Erstausstrahlung: DS-Kultur
32120 REM -spezial 900822

1000 A=100:GOTO 20:REM STRINGFUNKTIONEN
1010 SR$="Stringfunktionen"
1020 HO=(HO-LEN(SR$)-4)/2
1030 VE=0
1040 GOSUB 110:GOSUB 150
1050 REM
1060 HO=0:VE=2:GOSUB 110
1070 PRINT"Mit Zeichenkettenfunktionen koennen"
1080 PRINT"aus einer gegebenen Zeichenkette Teil-"
1090 PRINT"zeichenketten herausgeschnitten werden."
1100 PRINT:PRINT"In BASICODE-Programmen sind erlaubt:"
1110 PRINT:PRINT"  LEFT$, RIGHT$ und"
1120 PRINT"  MID$ in zwei Formen."
1130 PRINT:PRINT"Es soll hier von der Zeichenkette"
1140 PRINT:PRINT"  ";"a$=";CHR$(34);"Basicode";CHR$(34)
1150 PRINT:PRINT"ausgegangen werden."
1160 REM
1170 A$="Basicode"
1180 GOSUB 15000
1190 GOSUB 15100
1200 REM -------------------------LEFT$
1210 HO=0:VE=4:GOSUB 110
1220 PRINT"Die Funktion LEFT$(a$,m) loest aus der"
1230 PRINT"Zeichenkette von links her, soviel Zei-"
1240 PRINT"chen heraus, wie der Parameter 'm' an-"
1250 PRINT"gibt (m <= string-Laenge):"
1260 HO=5:VE=9:GOSUB 110
1270 PRINT"LEFT$(a$,3) => ";LEFT$(A$,3)
1280 HO=0:VE=11:GOSUB 110
1290 PRINT"Bestimmen der Teilzeichenkette ueber"
1300 PRINT"den Parameter 'm':"
1310 HO=0:VE=14:GOSUB 110
1320 M$="":INPUT"Laenge - von links ";M$:M=VAL(M$)
1330 IF LEN(M$)=0 THEN GOSUB 15100:GOSUB 15310:GOTO 1210
1340 IF (ASC(M$)<49) OR (ASC(M$)>57) THEN GOSUB 15310:GOTO 1310
1350 IF M>LEN(A$) THEN GOSUB 15310:GOTO 1310
1360 HO=5:VE=16:GOSUB 110
1370 PRINT"LEFT$(a$,";M$;") => ";LEFT$(A$,M)
1380 HO=0:VE=22:GOSUB 110
1390 PRINT"Noch einmal / Weiter (N/RETURN) =>";:GOSUB 210
1400 IF (IN<>78) THEN GOSUB 100:GOTO 1470
1410 FOR VE=13 TO 16
1420 HO=5:GOSUB 110:PRINT"                                 ";
1430 NEXT VE
1440 GOTO 1310
1450 REM
1460 REM ------------------------RIGHT$
1470 GOSUB 15100
1480 HO=0:VE=4:GOSUB 110
1490 PRINT"Mit der Funktion RIGHT$(a$,n) wird"
1500 PRINT"rechts beginnend eine Teilzeichenkette"
1510 PRINT"von der durch den Parameter 'n' be-"
1520 PRINT"stimmten Laenge gebildet:"
1530 HO=5:VE=9:GOSUB 110
1540 PRINT"RIGHT$(a$,4) => ";RIGHT$(A$,4)
1550 HO=0:VE=11:GOSUB 110
1560 PRINT"Bestimmen der Teilzeichenkette ueber"
1570 PRINT"den Parameter 'n':"
1580 HO=0:VE=14:GOSUB 110
1590 M$="":INPUT"Laenge - von rechts ";M$:M=VAL(M$)
1600 IF LEN(M$)=0 THEN GOSUB 100:GOSUB 15100:GOTO 1480
1610 IF (ASC(M$)<49) OR (ASC(M$)>57) THEN GOSUB 15310:GOTO 1580
1620 IF M>LEN(A$) THEN GOSUB 15310:GOTO 1580
1630 HO=5:VE=16:GOSUB 110
1640 PRINT"RIGHT$(a$,";M$;") => ";RIGHT$(A$,M)
1650 HO=0:VE=22:GOSUB 110
1660 PRINT"Noch einmal / Weiter (N/RETURN) =>";:GOSUB 210
1670 IF (IN<>78) THEN GOSUB 100:GOTO 1740
1680 FOR VE=14 TO 16
1690 HO=5:GOSUB 110:PRINT"                                 ";
1700 NEXT VE
1710 GOTO 1580
1720 REM
1730 REM ------------------------MID$-1
1740 GOSUB 15100
1750 HO=0:VE=4:GOSUB 110
1760 PRINT"Mit der Funktion MID$(a$,m,l) wird auch"
1770 PRINT"eine Zeichenkette aus a$ herausgeloest,"
1780 PRINT"beginnend mit der Position m und der"
1790 PRINT"Laenge l."
1800 HO=5:VE=9:GOSUB 110
1810 PRINT"MID$(a$,3,4) => ";MID$(A$,3,4)
1820 HO=0:VE=11:GOSUB 110
1830 PRINT"Bestimmen der Teilzeichenkette ueber"
1840 PRINT"die Parameter 'm' und 'l':"
1850 HO=0:VE=14:GOSUB 110
1860 M$="":INPUT"ab Position ";M$:M=VAL(M$)
1870 IF LEN(M$)=0 THEN GOSUB 100:GOSUB 15100:GOTO 1750
1880 L$="":INPUT"Laenge      ";L$:L=VAL(L$)
1890 IF LEN(L$)=0 THEN GOSUB 100:GOSUB 15100:GOTO 1750
1900 IF (ASC(M$)<49) OR (ASC(M$)>57) THEN GOSUB 15310:GOTO 1850
1910 IF (ASC(L$)<49) OR (ASC(M$)>57) THEN GOSUB 15310:GOTO 1850
1920 IF M>LEN(A$) THEN GOSUB 15310:GOTO 1850
1930 IF (M+L)>(LEN(A$)+1) THEN GOSUB 15310:GOTO 1850
1940 HO=5:VE=17:GOSUB 110
1950 PRINT"MID$(a$,";M$;",";L$;") => ";MID$(A$,M,L)
1960 HO=0:VE=22:GOSUB 110
1970 PRINT"Noch einmal / Weiter (N/RETURN) =>";:GOSUB 210
1980 IF (IN<>78) THEN GOSUB 100:GOTO 2060
1990 FOR VE=14 TO 17
2000 HO=0:GOSUB 110
2010 PRINT"                                      ";
2020 NEXT VE
2030 GOTO 1850
2040 REM
2050 REM ------------------------MID$-2
2060 GOSUB 15100
2070 HO=0:VE=4:GOSUB 110
2080 PRINT"Die andere - in BASICODE-3 auch erlaub-"
2090 PRINT"te - Version"
2100 HO=5:VE=7:GOSUB 110
2110 PRINT"MID$(a$,3) => ";MID$(A$,3)
2120 HO=0:VE=9:GOSUB 110
2130 PRINT"gibt die Teilzeichenkette von der Posi-"
2140 PRINT"tion 'm' bis zum Ende der Zeichenkette"
2150 PRINT"a$ wieder."
2160 HO=0:VE=12:GOSUB 110
2170 PRINT"Bestimmen der Teilzeichenkette ueber"
2180 PRINT"den Parameter 'm':"
2190 HO=5:VE=15:GOSUB 110
2200 M$="":INPUT"Startposition ";M$:M=VAL(M$)
2210 IF LEN(M$)=0 THEN GOSUB 100:GOSUB 15100:GOTO 2070
2220 IF (ASC(M$)<49) OR (ASC(M$)>57) THEN GOSUB 15310:GOTO 2190
2230 IF M>LEN(A$) THEN GOSUB 15310:GOTO 2190
2240 HO=5:VE=17:GOSUB 110
2250 PRINT"MID$(a$,";M$;") => ";MID$(A$,M)
2260 HO=0:VE=22:GOSUB 110
2270 PRINT"Noch einmal / Weiter (N/RETURN) =>";:GOSUB 210
2280 IF (IN<>78) THEN GOSUB 100:GOTO 2350
2290 FOR VE=15 TO 18
2300 HO=5:GOSUB 110:PRINT"                                 ";
2310 NEXT VE
2320 GOTO 2190
2330 REM
2340 REM -------------------------INSTR
2350 HO=0:VE=0:GOSUB 110
2360 PRINT"Mit der Funktion INSTR(x$,y$) bzw."
2370 PRINT"                 INSTR(n,x$,y$)":PRINT
2380 PRINT"steht in verschiedenen BASIC-Dialekten"
2390 PRINT"eine Zeichenketten-Funktion zur"
2400 PRINT"Verfuegung, die das Suchen nach einem"
2410 PRINT"String in einer vorgegebenen Zeichen-"
2420 PRINT"kette erleichtert.":PRINT
2430 PRINT"Ihre Verwendung ist in BASICODE-3 -"
2440 PRINT"Programmen nicht erlaubt; die Funktion"
2450 PRINT"muss mit einem Unterprogramm nachgebil-"
2460 PRINT"det werden."
2470 GOSUB 15000
2480 GOSUB 100:HO=0:VE=0:GOSUB 110
2490 PRINT"Ueber die Form INSTR(x$,y$) wird die"
2500 PRINT"Position ausgegeben, an der der String"
2510 PRINT"y$ beginnt. Ist y$ in x$ nicht enthal-"
2520 PRINT"ten, ist der Wert '0'."
2530 HO=5:VE=5:GOSUB 110
2540 X$="":INPUT"x$ ";X$
2550 IF (LEN(X$)=0) OR (LEN(X$)>25) THEN GOSUB 100:GOTO 2480
2560 HO=5:VE=7:GOSUB 110
2570 Y$="":INPUT"y$ ";Y$
2580 IF (LEN(Y$)=0) OR (LEN(Y$)>25) THEN GOSUB 100:GOTO 2480
2590 HO=0:VE=9:GOSUB 110
2600 PRINT"Unterprogramm # 20000 ff.:":GOSUB 20010
2610 PRINT:PRINT"Position in String x$:";I:PRINT
2620 PRINT"MID$(x$,";:GOSUB 300:PRINT SR$;",";
2630 SR=LEN(Y$):GOSUB 300:PRINT SR$;") => ";
2640 IF I<>0 THEN PRINT MID$(X$,I,LEN(Y$)):GOTO 2670
2650 HO=0:VE=13:GOSUB 110
2660 SR$="  Nicht enthalten !  ":GOSUB 150
2670 HO=0:VE=22:GOSUB 110
2680 PRINT"Noch einmal / Weiter (N/RETURN) =>";:GOSUB 210
2690 IF (IN<>78) THEN GOSUB 100:GOTO 2720
2700 GOSUB 22010:GOTO 2530
2710 REM
2720 GOSUB 100
2730 HO=0:VE=0:GOSUB 110
2740 PRINT"Die Stringfunktion INSTR(n,x$,y$) gibt"
2750 PRINT"mit n (1...255) die Position an, mit"
2760 PRINT"der in x$ die Suche beginnt; auch hier"
2770 PRINT"ist ein Unterprogramm einzusetzen."
2780 HO=5:VE=5:GOSUB 110
2790 X$="":INPUT"x$   ";X$
2800 IF (LEN(X$)=0) OR (LEN(X$)>25) THEN GOSUB 100:GOTO 2730
2810 HO=5:VE=7:GOSUB 110
2820 Y$="":INPUT"y$   ";Y$
2830 IF (LEN(Y$)=0) OR (LEN(Y$)>25) THEN GOSUB 100:GOTO 2730
2840 HO=5:VE=9:GOSUB 110
2850 N$="":INPUT"Pos. ";N$:N=VAL(N$)
2860 IF LEN(N$)=0 THEN GOSUB 100:GOTO 2730
2870 IF (N<1) OR (N>255) THEN 2840
2880 IF (ASC(N$)<49) OR (ASC(N$)>57) THEN 2840
2890 HO=0:VE=11:GOSUB 110
2900 PRINT"Unterprogramm # 21000 ff.":GOSUB 21010
2910 PRINT:PRINT"Position in String x$:";I
2920 PRINT:PRINT"MID$(x$,";:GOSUB 300:PRINT SR$;",";
2930 SR=LEN(Y$):GOSUB 300:PRINT SR$;") => ";
2940 IF (I<>0) THEN PRINT MID$(X$,I,LEN(Y$)):GOTO 2970
2950 HO=0:VE=15:GOSUB 110
2960 SR$="  Nicht enthalten !  ":GOSUB 150
2970 HO=0:VE=22:GOSUB 110
2980 PRINT"Noch einmal/ Start/ Ende (N/S/RETURN)";:GOSUB 210
2990 IF (IN=78) THEN GOSUB 22010:GOTO 2780
3000 IF (IN=83) THEN 1000
3010 GOSUB 100
3020 HO=13:VE=12:GOSUB 110
3030 SR$=" E N D E "
3040 GOSUB 150:GOSUB 210:GOTO 950
3050 REM
3060 REM
14090 REM ------------------------TASTE
15000 HO=30:VE=22:GOSUB 110
15010 PRINT"Taste =>";:GOSUB 210
15020 RETURN
15090 REM --------------------KOPFZEILE
15100 GOSUB 100:HO=11:VE=0:GOSUB 110
15110 PRINT"a$ = ";CHR$(34);"B a s i c o d e";CHR$(34)
15120 PRINT"Zeichenposition  1 2 3 4 5 6 7 8"
15130 PRINT"--------------------------------------"
15140 RETURN
15300 REM ------------------FEHLEINGABE
15310 FOR VE=15 TO 18
15320 HO=0:GOSUB 110
15330 PRINT"                                      "
15340 NEXT VE
15350 RETURN
20000 REM ----------------------INSTR-1
20010 FOR I=1 TO LEN(X$)-LEN(Y$)+1
20020 IF Y$=MID$(X$,I,LEN(Y$)) THEN SR=I:RETURN
20030 NEXT I
20040 I=0:SR=I:RETURN
21000 REM ----------------------INSTR-2
21010 FOR I=N TO LEN(X$)-LEN(Y$)+1
21020 IF Y$=MID$(X$,I,LEN(Y$)) THEN SR=I:RETURN
21030 NEXT I
21040 I=0:SR=I:RETURN
22000 REM ---------------------LOESCHEN
22010 FOR VE=4 TO 18
22020 HO=0:GOSUB 110
22030 PRINT"                                      "
22040 NEXT VE
22050 RETURN
30000 REM -----------------------------
30010 REM MIT DIESEM PROGRAMM SOLL DER
30020 REM VERSUCH GEMACHT WERDEN, DIE
30030 REM IN BASICODE-3-PROGRAMMEN
30040 REM MOEGLICHEN STRING-FUNKTIONEN
30050 REM DARZUSTELLEN.
30060 REM AUSSERDEM SOLL DURCH IF-
30070 REM ABFRAGEN NACH EINER INPUT-
30080 REM ANWEISUNG AUSGESCHLOSSEN
30090 REM WERDEN, DASS UNZULAESSIGE
30100 REM ZEICHEN (KOMMA, DOPPELPUNKT)
30110 REM ODER STEUERBEFEHLE EINGEGEBEN
30120 REM WERDEN.
30130 REM
30140 REM AUF DIE UNTERPROGRAMME
30150 REM   # 20000 FF. UND
30160 REM   # 21000 FF.
30170 REM ZUR NACHBILDUNG DER INSTR-
30180 REM FUNKTION WIRD HINGEWIESEN.
30190 REM DIE STRINGLAENGE IST HIER AUF
30200 REM 25 ZEICHEN BEGRENZT.
32000 REM -----------------------------
32010 REM FRIEDRICH DORMEIER
32020 REM
32030 REM D-1000 BERLIN 39
32040 REM
32050 REM C64/C128 - 1/90
32060 REM -----------------------------
32070 REM Erstausstrahlung: Radio DDR
32080 REM 900321

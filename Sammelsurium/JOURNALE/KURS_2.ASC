1000 a=500:GOTO 20:REM basicode-3 journal 3
1010 ht=ho:vt=ve:s$="":FOR i=1 TO ht:s$=s$+" ":NEXT i
1020 hl=0:hr=ht:vl=2:vr=vt-2:r$="":READ t$
1030 GOSUB 1410:ho=(ht-LEN(sr$)-6)/2:ve=0:GOSUB 110
1040 GOSUB 150:PRINT:PRINT:PRINT
1050 PRINT"Ausdruck ueber"
1060 PRINT:PRINT"   1  den Bildschirm,"
1070 PRINT"   2  den Drucker"
1080 PRINT:PRINT"Welche Nummer ?";:GOSUB 210
1090 sc=in-48:PRINT sc:IF sc<>2 THEN sc=1:GOTO 1220
1100 PRINT:PRINT"Wieviel Leerzeichen"
1110 INPUT"am Zeilenanfang    ";hl
1120 PRINT:PRINT"Wieviel Text-Zeichen"
1130 INPUT"auf jeder Zeile    ";i:hr=hl+i-1
1140 PRINT:PRINT"Wieviel Text-Zeilen"
1150 INPUT"auf jeder Seite    ";vr:vr=vr-3
1160 PRINT"(E) Endlospapier oder  "
1170 PRINT"(L) Lose Blaetter (E/L) ";
1180 GOSUB 210:IF in=76 THEN vl=-1:GOTO 1220
1190 IF in<>69 THEN GOSUB 250:GOTO 1160
1200 PRINT:PRINT"Wieviel Leerzeilen"
1210 INPUT"am Seitenende      ";vl:vr=vr+vl
1220 mm$="":IF hl>0 THEN mm$=LEFT$(s$,hl)
1230 GOSUB 1380:m$=mm$:al=vl:IF sc=2 THEN GOSUB 1360
1240 IF LEN(r$)>hr-hl THEN GOSUB 1820
1250 GOSUB 1440:IF r$<>"" THEN r$=r$+" "
1260 r$=r$+d$:GOTO 1240
1270 IF sc=2 THEN sr$=m$+sr$:GOSUB 350:GOSUB 360:GOTO 1290
1280 GOSUB 120:PRINT m$;sr$;:ve=ve+1:ho=0:GOSUB 110:hl=0
1290 m$=mm$:al=al+1:IF al<=vr THEN RETURN
1300 IF sc=1 THEN 1380
1310 IF vl>=0 THEN FOR i=1 TO vl:GOSUB 360:NEXT i:GOTO 1350
1320 PRINT"Neues Blatt in Drucker einlegen"
1330 PRINT"druecke dann       RETURN/ENTER"
1340 GOSUB 210:IF in<>13 THEN 1340
1350 al=vl
1360 sr$=LEFT$(s$,(hr-hl-LEN(t$))/2)+t$:GOSUB 1270
1370 sr$="":GOSUB 1270:al=vl:RETURN
1380 ho=3:ve=vt:GOSUB 110
1390 sr$="Weiter: druecke <RETURN/ENTER>":GOSUB 150
1400 GOSUB 210:IF in<>13 THEN 1400
1410 GOSUB 100:al=vl:sr$=t$
1420 PRINT LEFT$(s$,(ht-LEN(sr$))/2-3);:GOSUB 150
1430 PRINT:PRINT:RETURN
1440 READ d$
1450 IF d$="" THEN 1440
1460 IF LEFT$(d$,1)="." THEN 1530
1470 IF LEFT$(d$,1)<>">" THEN 1490
1480 d$=LEFT$(s$,23-LEN(r$))+MID$(d$,2):RETURN
1490 IF LEFT$(d$,1)<>"!" THEN RETURN
1500 IF d$="!" THEN d$="":GOTO 1520
1510 d$=MID$(d$,2)
1520 GOSUB 1760:GOTO 1450
1530 IF d$<>".E" THEN 1580
1540 j=1:GOSUB 1760:t$="E N D E"
1550 sr$="":GOSUB 1270:GOSUB 1270:sr$=t$:GOSUB 1270:t$=""
1560 sr$="":GOSUB 1270:IF al<>vl THEN 1560
1570 GOTO 950
1580 dd$=MID$(d$,2,1):d$=MID$(d$,3)
1590 IF dd$<>"R" THEN 1640
1600 IF sc=2 THEN RETURN
1610 IF LEN(r$)+6+LEN(d$)>=hr-hl THEN sr$=r$:GOSUB 1270:r$=""
1620 PRINT m$;r$;:sr$=d$:GOSUB 150:m$=""
1630 hl=hl+LEN(r$)+LEN(d$)+6:r$="":GOTO 1440
1640 IF dd$<>"M" THEN 1670
1650 i=VAL(d$):mm$="":IF i+hl=0 THEN 1440
1660 mm$=LEFT$(s$,i+hl):GOTO 1440
1670 IF dd$="P" THEN j=VAL(d$):GOSUB 1760:GOSUB 1780:GOTO 1440
1680 IF dd$="T" THEN t$=d$:j=10:GOSUB 1760:GOSUB 1780:GOTO 1600
1690 IF dd$="Q" THEN r$=r$+CHR$(34)+d$:GOTO 1440
1700 IF dd$<>"F" THEN 1730
1710 j=VAL(d$):GOSUB 1760
1720 j=j-1:IF j>-1 THEN sr$="":GOSUB 1270:GOTO 1720
1730 IF dd$<>"C" THEN 1750
1740 sr$=LEFT$(s$,(hr-hl-LEN(d$))/2)+d$:GOSUB 1270
1750 GOTO 1440
1760 GOSUB 1820:sr$=r$:GOSUB 1270:r$="":j=j-1
1770 RETURN
1780 IF (sc=1)or(al+j>vr) THEN 1800
1790 sr$="":GOSUB 1270:GOSUB 1270:RETURN
1800 IF al<>vl THEN sr$="":GOSUB 1270:GOTO 1800
1810 RETURN
1820 k=hr-hl+1-LEN(m$)
1830 IF LEN(r$)<=k THEN RETURN
1840 IF MID$(r$,k,1)=" " THEN 1870
1850 k=k-1:IF k>0 THEN 1840
1860 sr$="":GOTO 1880
1870 sr$=LEFT$(r$,k-1):r$=MID$(r$,k+1)
1880 GOSUB 1270:GOTO 1820
1890 REM
1910 REM uebersetzte version:
1920 REM
1930 REM computer-bulletin no.32 (7/89)
1940 REM stichting basicode, eindhoven
1950 REM
25000 DATA "Programmieren in BASICODE (2)"
25010 DATA ".RVariable"
25020 DATA "!!Wie in allen BASIC-Dialekten sind numerische"
25030 DATA "und String-Variable zugelassen."
25040 DATA "!!Fuer numerische Variablen gilt:"
25050 DATA "! - Typ 'real',"
25060 DATA "! - einfache Genauigkeit"
25070 DATA "!   (6 gueltige Ziffern),"
25080 DATA "! - Integer-Variable sind nicht erlaubt."
25090 DATA "!!Eine Stringvariable wird durch den Zusatz '$'"
25100 DATA "nach dem Namen gekennzeichnet; sie kann 255"
25110 DATA "Zeichen lang sein."
25120 DATA ".P0"
25130 DATA "Logische Werte werden je nach Computer"
25140 DATA "unterschiedlich repraesentiert (z.B."
25150 DATA "'wahr' => '+1' oder '-1'). Eine logische"
25160 DATA "Variable darf deshalb nicht Gegenstand"
25170 DATA "arithmetischer Operationen sein."
25180 DATA ".P0"
25190 DATA "Variablennamen sind maximal zwei Zeichen lang:"
25200 DATA "! - Buchstabe, Buchstabe,"
25210 DATA "! - Buchstabe, Ziffer."
25220 DATA "!!Ausgeschlossen ist der Gebrauch von"
25230 DATA "Variablennamen,"
25240 DATA "! - die mit dem Buchstaben 'O' beginnen;"
25250 DATA "!   diese sind den BASICODE-Standard-"
25260 DATA "!   Routinen vorbehalten,"
25270 DATA "! - die BASIC-SchluesselwoerÛer oder"
25280 DATA "!   Systemvariable darstellen:"
25290 DATA "!   AS, AT, DI, DI$, DO, DO$, DS, DS$,"
25300 DATA "!   EI, EI$, EL, ER, FN, GO, GO$, GR, HC,"
25310 DATA "!   IF, LN, MA, MP, PI, SQ, SQ$, ST, ST$,"
25320 DATA "!   TI, TI$, TO, TO$,"
25330 DATA "! - die Teil von BASICODE-Subroutinen"
25340 DATA "!   sind."
25350 DATA ".P0"
25360 DATA "Folgende Variablen werden ueber die BASICODE-"
25370 DATA "Unterprogramme aufgerufen oder ihnen uebergeben"
25380 DATA "und haben eine festgelegte Bedeutung:"
25390 DATA "!! HO,VE   - Cursor-Positionierung"
25400 DATA "!           Textbetrieb"
25410 DATA "!           (# 110, 120)"
25420 DATA "!         - Auslesen des Textschirms"
25430 DATA "!           (# 220)"
25440 DATA "!         - Cursor-Positionierung"
25450 DATA "!           Grafik-Betrieb"
25460 DATA "!           (# 620 ... 650)"
25470 DATA "!! IN, IN$ - Tastaturabfrage"
25480 DATA "!         - (# 200, 210, 450)"
25490 DATA "!! IN      - Auslesen Textschirm (# 220)"
25500 DATA "!         - Status-Variable externe"
25510 DATA "!           Speicher (# 540, 550, 560)",".P0"
25520 DATA " IN$     - Lesen von externen Speichern"
25530 DATA "!           (# 550)"
25540 DATA "!! SR, SR$ - Bilden einer Stringvariablen"
25550 DATA "!           (# 300, 310)"
25560 DATA "!! SR$     - String-Ausgabe auf"
25570 DATA "!           Bildschirm (# 150, 650)"
25580 DATA "!         - Drucker (# 350)"
25590 DATA "!         - Externe Speicher (# 560)"
25600 DATA "!         - Klein- => Gross-Buchstaben"
25610 DATA "!           (# 330)"
25620 DATA "!! CN      - Formatierung numerischer"
25630 DATA "!           Ausgaben (# 310)"
25640 DATA "!         - Zeichenfarbe bei grafischem"
25650 DATA "!           Betrieb (# 620, 630, 650)"
25660 DATA "!! CT      - Formatierung numerischer"
25670 DATA "!           Ausgaben (# 310)",".P0"
25680 DATA " NF, NF$ - Externes Speichern von"
25690 DATA "!           Daten-Files (# 500 ... 580)"
25700 DATA "!! FR      - Freier Speicherplatz (# 270)"
25710 DATA "!         - Wirksamkeit der Stop-Taste"
25720 DATA "!           (# 280)"
25730 DATA "!! SD      - Warte-Routine (# 450)"
25740 DATA "!         - Musik (# 400)"
25750 DATA "!! SP, SV  - Musik (# 400)"
25760 DATA "!! RV      - Zufallsvariable (# 260)"
25770 DATA "!! HG, VG  - Grafischer Cursor,"
25780 DATA "!           Bildpunkte"
25790 DATA "!           (# 620, 630, 650)"
25800 DATA ".P0"
25810 DATA "Abweichend vom Umgang mit Variablen, der auf"
25820 DATA "manchem Computer moeglich ist, sind in BASICODE"
25830 DATA "Variable vor ihrem Aufruf im Programm zu"
25840 DATA "deklarieren und zu initialisieren, d.h. es ist"
25850 DATA "allen im Programm verwendeten Variablen ein Wert"
25860 DATA "zuzuweisen. Die Variablen HO, VE, HG, VG und SV"
25870 DATA "werden durch den Sprung nach Zeile # 20"
25880 DATA "initialisiert. Man kann nicht davon ausgehen,"
25890 DATA "dass den anderen bei ihrem Aufruf implizit der"
25900 DATA "Wert '0' oder 'leer' zugewiesen wird."
25910 DATA ".P0"
25920 DATA ".RFeldvariable"
25930 DATA "!!Vor Gebrauch in einem Programm sind Felder"
25940 DATA "(arrays) zu dimensionieren. Zugelassen sind ein-"
25950 DATA "oder zwei-dimensionale Felder (Listen oder"
25960 DATA "Tabellen). Mit der Dimensionierung werden"
25970 DATA "gleichzeitig die Elemente auf '0' bzw. 'leer'"
25980 DATA "gesetzt. Es kann nicht davon ausgegangen werden,"
25990 DATA "dass der Aufruf einer Feldvariablen im Programm"
26000 DATA "automatisch ein Feld mit 11 Elementen"
26010 DATA "dimensioniert. Auch Felder mit weniger als 11"
26020 DATA "Elementen sind zu dimensionieren (z.B.DIM A(4))."
26030 DATA "Die Zaehlung der Feldelemente beginnt bei '0'."
26040 DATA ".P0"
26050 DATA ".RBASIC-Sprachumfang"
26060 DATA "!!Im Vyrgleich zu manchem BASIC-Dialekt ist der"
26070 DATA "Sprachumfang reduziert. Um die mit BASICODE"
26080 DATA "angestrebte Portabilitaet der Programme zu"
26090 DATA "erreichen, musste die Anzahl der zugelassenen"
26100 DATA "BASIC-Anweisungen und -Operatoren auf den bei"
26110 DATA "allen beteiligten Computern vorhandenen Umfang"
26120 DATA "beschraenkt werden. Erlaubt ist die Verwendung"
26130 DATA "folgender Anweisungen:"
26140 DATA "!! DATA    DEF FN  DIM    FOR   GOSUB"
26150 DATA "! GOTO    IF      INPUT  LET   NEXT"
26160 DATA "! ON      PRINT   READ   REM   RESTORE"
26170 DATA "! RETURN  STEP    TAB    THEN  TO",
26180 DATA ".P0","Funktionen und Operatoren:"
26190 DATA "!! ABS     ASC     ATN    CHR$  COS"
26200 DATA "! EXP     INT     LEFT$  LEN   LOG"
26210 DATA "! MID$    RIGHT$  SGN    SIN   SQR"
26220 DATA "! TAN     VAL"
26230 DATA "!! AND     OR      NOT"
26240 DATA "!! ^  Exponentation"
26250 DATA "! *  Multiplikation"
26260 DATA "! /  Division"
26270 DATA "! +  - Addition"
26280 DATA "!    - Verkettung (concatenation) von"
26290 DATA "!      Stringvariablen"
26300 DATA "! -  Subtraktion"
26310 DATA "!! =    <>    )"
26320 DATA "! <    >     ( Vergleichsoperatoren"
26330 DATA "! <=   >=    )",".P0"
26340 DATA "Die Folge der Zeichen der Vergleichsoperatoren"
26350 DATA "'<=', '>=' und '<>' ist vorgeschrieben."
26360 DATA ".P0"
26370 DATA ".RDer Zeichensatz"
26380 DATA "!!Die in BASICODE verwendbaren Zeichen werden"
26390 DATA "nach ASCII codiert. Zu beachten ist jedoch, dass"
26400 DATA "fuer bestimmte Computer Abweichungen gelten"
26410 DATA "(z.B.Commodore) und dass die Steuerkodes nicht"
26420 DATA "benutzt werden koennen. Die ASC- und CHR$-"
26430 DATA "Funktionen sind daher mit Vorsicht zu verwenden."
26440 DATA "Bei manchen Computern wird eine exakte Beziehung"
26450 DATA "zwischen 'IN' und 'IN$' nicht hergestellt."
26460 DATA "!Bei Aufruf der BASICODE-Subroutinen # 200,"
26470 DATA "# 210, # 220 und # 450 ist dies zu beachten."
26480 DATA ".P0"
26490 DATA "Der ASCII gilt in folgenden Bereichen:"
26500 DATA "!! ASCII          Zeichen"
26510 DATA "! -------------------------------"
26520 DATA "!  32            Space"
26530 DATA "!  33 ...  47    Sonderzeichen"
26540 DATA "!  48 ...  57    Ziffern"
26550 DATA "!  58 ...  64    Sonderzeichen"
26560 DATA "!  65 ...  90    Gross-Buchstaben"
26570 DATA "!  91 ...  96    Sonderzeichen"
26580 DATA "!  97 ... 122    Klein-Buchstaben"
26590 DATA "! 123 ... 126    Sonderzeichen"
26600 DATA "!!Bei der Darstellung der Sonderzeichen koennen"
26610 DATA "bei einzelnen Computern Unterschiede - je"
26620 DATA "nach implementiertem Zeichensatz - auftreten,"
26630 DATA "(vergl. 'basicode-ascii')",".P0"
26640 DATA "Bei Lauf eines BASICODE-Programmes auf einem"
26650 DATA "Commodore wirkt sich der Unterschied der"
26660 DATA "Codierung zwischen 'echtem' ASCII und CBM-ASCII"
26670 DATA "deutlich aus, wenn es sich um die Kodierung"
26680 DATA "von Gross- und Klein-Buchstaben handelt"
26690 DATA "('cbm-ascii')."
26700 DATA ".P0"
26710 DATA "Ausserdem koennen ueber die Variable 'IN'"
26720 DATA "folgende Steuercodes abgefragt werden:"
26730 DATA "!!  13  Carriage Return / Enter    (CR)"
26740 DATA "!  28  Cursor nach links"
26750 DATA "!  29  Cursor nach rechts"
26760 DATA "!  30  Cursor nach unten"
26770 DATA "!  31  Cursor nach oben"
26780 DATA "! 127  Links ein Zeichen loeschen (DEL)"
26790 DATA "!!Diese Steuerfunktionen koennen aber in"
26800 DATA "BASICODE-Programmen nicht aufgerufen werden."
26810 DATA "Mit den Unterprogrammen # 200, 210 kann"
26820 DATA "nur der Wert von 'IN' abgefragt werden,"
26830 DATA "der zur Steuerung des Programmlaufs"
26840 DATA "verwendet werden kann."
26850 DATA "!Zur Cursor-Positionierung dient allein die"
26860 DATA "Subroutine # 110 mit den Variablen HO und VE."
26870 DATA ".P10","Das war das","!!"
26880 DATA ".C**************************"
26890 DATA ".C*                        *"
26900 DATA ".C*  BASICODE-3   Journal  *"
26910 DATA ".C*        Nummer 3        *"
26920 DATA ".C*     7.  Maerz  1990    *"
26930 DATA ".C*                        *"
26940 DATA ".C**************************",".E"
30000 REM
30010 REM grundlagen von basicode
30020 REM       -- teil 2 --
32000 REM --------------------
32010 REM friedrich dormeier
32020 REM
32030 REM d-1000 berlin 39
32040 REM
32050 REM 11/1989 - c64/c128
32060 REM --------------------
32070 REM
32080 REM erstausstrahlung: radio ddr
32090 REM 900307

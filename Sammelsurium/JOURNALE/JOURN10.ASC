1000 A=500:GOTO 20:REM BASICODE-3 JOURNAL 10
1010 HT=HO:VT=VE:S$="":FOR I=1 TO HT:S$=S$+" ":NEXT I
1020 HL=0:HR=HT:VL=2:VR=VT-2:R$="":READ T$
1030 GOSUB 1340:HO=(HT-LEN(SR$)-6)/2:VE=0:GOSUB 110
1040 GOSUB 150:PRINT:PRINT:PRINT
1050 PRINT"Ausdruck ueber"
1060 PRINT:PRINT"   - 1  den Bildschirm,"
1070 PRINT"   - 2  den Drucker."
1080 PRINT:PRINT"Welche Nummer ?";:GOSUB 210
1090 SC=IN-48:PRINT SC:IF SC<>2 THEN SC=1:GOTO 1180
1100 PRINT:PRINT"Wieviel Leerzeichen  "
1110 PRINT"am Zeilenanfang      ";:INPUT HL
1120 PRINT:PRINT"Wieviel Text-Zeichen "
1130 PRINT"auf jeder Zeile      ";:INPUT I:HR=HL+I-1
1140 PRINT:PRINT"Wieviel Leer-Zeilen  "
1150 PRINT"am Seitenende        ";:INPUT VL
1160 PRINT:PRINT"Wieviel Text-Zeilen  "
1170 PRINT"auf jeder Seite      ";:INPUT VR:VR=VR+VL-3
1180 MM$="":IF HL>0 THEN MM$=LEFT$(S$,HL)
1190 GOSUB 1310:M$=MM$:AL=VL:IF SC=2 THEN GOSUB 1290
1200 IF LEN(R$)>HR-HL THEN GOSUB 1730
1210 GOSUB 1370:IF R$<>"" THEN R$=R$+" "
1220 R$=R$+D$:GOTO 1200
1230 IF SC=2 THEN SR$=M$+SR$:GOSUB 350:GOSUB 360:GOTO 1250
1240 GOSUB 120:PRINT M$;SR$;:VE=VE+1:HO=0:GOSUB 110:HL=0
1250 M$=MM$:AL=AL+1:IF AL<=VR THEN RETURN
1260 IF SC=1 THEN 1310
1270 IF VL>0 THEN FOR I=1 TO VL:GOSUB 360:NEXT I:GOSUB 210
1280 AL=VL
1290 SR$=LEFT$(S$,(HR-HL-LEN(T$))/2)+T$:GOSUB 1230
1300 SR$="":GOSUB 1230:AL=VL:RETURN
1310 HO=0:VE=VT:GOSUB 110
1320 SR$="WEITER: druecke <RETURN/ENTER>":GOSUB 150
1330 GOSUB 210:IF IN<>13 THEN 1330
1340 GOSUB 100:AL=VL:SR$=T$
1350 PRINT LEFT$(S$,(HT-LEN(SR$))/2-3);:GOSUB 150
1360 PRINT:PRINT:RETURN
1370 READ D$
1380 IF D$="" THEN 1370
1390 IF LEFT$(D$,1)="." THEN 1440
1400 IF LEFT$(D$,1)<>"!" THEN RETURN
1410 IF D$="!" THEN D$="":GOTO 1430
1420 D$=RIGHT$(D$,LEN(D$)-1)
1430 GOSUB 1670:GOTO 1380
1440 IF D$<>".E" THEN 1490
1450 J=1:GOSUB 1670:T$="E N D E"
1460 SR$="":GOSUB 1230:GOSUB 1230:SR$=T$:GOSUB 1230:T$=""
1470 SR$="":GOSUB 1230:IF AL<>VL THEN 1470
1480 GOTO 950
1490 DD$=MID$(D$,2,1):D$=RIGHT$(D$,LEN(D$)-2)
1500 IF DD$<>"R" THEN 1550
1510 IF SC=2 THEN RETURN
1520 IF LEN(R$)+6+LEN(D$)>=HR-HL THEN SR$=R$:GOSUB 1230:R$=""
1530 PRINT M$;R$;:SR$=D$:GOSUB 150:M$=""
1540 HL=HL+LEN(R$)+LEN(D$)+6:R$="":GOTO 1370
1550 IF DD$<>"M" THEN 1580
1560 I=VAL(D$):MM$="":IF I+HL=0 THEN 1370
1570 MM$=LEFT$(S$,I+HL):GOTO 1370
1580 IF DD$="P" THEN J=VAL(D$):GOSUB 1670:GOSUB 1690:GOTO 1370
1590 IF DD$="T" THEN T$=D$:J=10:GOSUB 1670:GOSUB 1690:GOTO 1510
1600 IF DD$="Q" THEN R$=R$+CHR$(34)+D$:GOTO 1370
1610 IF DD$<>"F" THEN 1640
1620 J=VAL(D$):GOSUB 1670
1630 J=J-1:IF J>-1 THEN SR$="":GOSUB 1230:GOTO 1630
1640 IF DD$<>"C" THEN 1660
1650 SR$=LEFT$(S$,(HR-HL-LEN(D$))/2)+D$:GOSUB 1230
1660 GOTO 1370
1670 GOSUB 1730:SR$=R$:GOSUB 1230:R$="":J=J-1
1680 RETURN
1690 IF (SC=1) OR (AL+J>VR) THEN 1710
1700 SR$="":GOSUB 1230:GOSUB 1230:RETURN
1710 IF AL<>VL THEN SR$="":GOSUB 1230:GOTO 1710
1720 RETURN
1730 K=HR-HL+1-LEN(M$)
1740 IF LEN(R$)<=K THEN RETURN
1750 IF MID$(R$,K,1)=" " THEN 1780
1760 K=K-1:IF K>0 THEN 1750
1770 SR$="":GOTO 1790
1780 SR$=LEFT$(R$,K-1):R$=RIGHT$(R$,LEN(R$)-K)
1790 GOSUB 1230:GOTO 1730
25000 DATA "JOURNAL-Dokumentation."
25010 DATA "Einfuehrung"
25020 DATA "!!Die Dokumentation ist eine Beschreibung des"
25030 DATA "vorliegenden Textprogrammes"
25040 DATA "(Zeilennummer 1000 - 1790)."
25050 DATA "Das Programm ermoeglicht eine vorteilhafte"
25060 DATA "Gestaltung von Texten auf dem Bildschirm und"
25070 DATA "beim Druck."
25080 DATA "!Anwendung fand das Programm bisher bei den"
25090 DATA "COMPUTER-BULLETINs von Radio Hilversum"
25100 DATA "und den BASICODE-3 Journalen im"
25110 DATA "Computermagazin REM von Radio DDR II und"
25120 DATA "derzeit beim Deutschlandsender Kultur."
25130 DATA "!Die Urheberrechte fuer das Textprogramm"
25140 DATA "liegen bei der STICHTING BASICODE, Eindhoven."
25150 DATA ".P0"
25160 DATA "Um Sendezeit zu sparen und trotzdem interessante"
25170 DATA "Beitraege zu bringen, wird in den hollaendischen"
25180 DATA "BASICODE-Sendungen oft diese Journal-Form"
25190 DATA "genutzt - warum also nicht auch bei uns?"
25200 DATA "Wir wuerden uns freuen, wenn Sie fuer REM"
25210 DATA "solche Beitraege schreiben - Ihrer"
25220 DATA "Ideenvielfalt sind dabei keine Grenzen gesetzt."
25230 DATA "Buch- Hard- Software-Tips, Clubarbeit,"
25240 DATA "Technikberichte (wie z.B. das 'ASTRA-Journal'"
25250 DATA "von Michael Friedrich aus Zoerbig) uvm. sind"
25260 DATA "stets willkommen. Schicken Sie uns Ihre"
25270 DATA "Journale auf Kassette zu (Kassette garantiert"
25280 DATA "zurueck) - kleine Preise als Dankeschoen"
25290 DATA "warten auf Sie!","!Wie man ein solches"
25300 DATA "Journal realisiert, erlaeutert der folgende"
25310 DATA "Text.","!!Danke sagt ** REM **"
25320 DATA ".P0"
25330 DATA "Text"
25340 DATA "!!Der Text wird in den Konstantenlisten"
25350 DATA "von DATA -Anweisungen, ab Zeile"
25360 DATA "25000, abgelegt. Die Listenelemente sind in"
25370 DATA "Anfuehrungszeichen zu setzen und gegebenfalls"
25380 DATA "durch Kommata zu trennen. Worttrenungen"
25390 DATA "ueber Listenelemente hinweg sind nicht erlaubt."
25400 DATA "!!Die Kommandos des Textprogrammes werden mit"
25410 DATA "in die Listenelemente aufgenommen"
25420 DATA "und stehen zu Beginn derselben oder bilden"
25430 DATA "selber eine Konstantenliste."
25440 DATA ".P0"
25450 DATA "Titelzeile"
25460 DATA "!!Die erste DATA -Anweisung (Zeile 25000)"
25470 DATA "enthaelt die Titelzeile."
25480 DATA "Sie wird durch das Programm gesondert behandelt."
25490 DATA "!Bei der Bildschirmanzeige"
25500 DATA "erfolgt ein auffaelliges Anzeigen mittig"
25510 DATA "am oberen Bildschirmrand."
25520 DATA "!Beim Druck erscheint die Titelzeile auf"
25530 DATA "jeder neuen Seite mittig am oberen Blattrand."
25540 DATA "!In dieser Beschreibung heisst die Titelzeile:"
25550 DATA "!.CJOURNAL-Dokumentation."
25560 DATA ".P14"
25570 DATA "Kommandos"
25580 DATA "!!Die Kommandos dienen der Gestaltung der Texte."
25590 DATA "Sie stehen am Anfang der DATA -Listenelemente"
25600 DATA "oder stellen selber eines dar."
25610 DATA ".P0","!!.R  ! "
25620 DATA "!Ein Ausrufezeichen (!) bewirkt die Ausgabe des"
25630 DATA "vorhergehenden Textrestes,"
25640 DATA "der bei der Zeilenformatierung entstand,"
25650 DATA "und einen Zeilenvorschub, wobei die"
25660 DATA "Zeilenanfangsposition eingenommen wird."
25670 DATA "!Ein zweites Ausrufezeichen (!!) erzeugt"
25680 DATA "zusaetzlich noch"
25690 DATA "eine Leerzeile, jedes weitere ebenfalls."
25700 DATA "!!.R .R "
25710 DATA "!Das .R-Kommando bewirkt ein auffaelliges"
25720 DATA "Anzeigen des Textes (hier",".R.R"
25730 DATA ") auf dem Bildschirm.","!Auf den Druck hat es"
25740 DATA "keinen Einfluss."
25750 DATA ".P12"
25760 DATA "!!.R .C "
25770 DATA "!Das .C-Kommando setzt dem Kommando folgenden"
25780 DATA "Text in","!.CZeilenmitte."
25790 DATA "!!.R .Fn "
25800 DATA "!Das .F-Kommando erzeugt n-1 Leerzeilen, wobei"
25810 DATA "n eine natuerliche Zahl ist. Das"
25820 DATA "Kommando ist ein eigenstaendiges Listenelement."
25830 DATA "!Zum Beispiel:.F3",".F3"
25840 DATA "entspricht 2 Leerzeilen."
25850 DATA ".P18"
25860 DATA "!!.R .Q "
25870 DATA "!Das .Q-Kommando setzt vor den folgenden Text"
25880 DATA "ein ",".QAnfuehrungszeichen",".Q."
25890 DATA "Es ist zu beachten, dass nach dem .Q-Kommando"
25900 DATA "wenigstens noch ein Zeichen folgen muss."
25910 DATA "!!.R .Pn "
25920 DATA "!Das .P-Kommando dient zur Kennzeichnung von"
25930 DATA "Textabsaetzen.","!Im Bildschirmmodus"
25940 DATA "wird auf eine neue Bildseite gewechselt."
25950 DATA "!Beim Druck werden 2 Leerzeilen erzeugt."
25960 DATA "!Steht hinter dem Kommando die Anzahl n der"
25970 DATA "Absatzzeilen, so wird beim"
25980 DATA "Druck geprueft, ob der Absatz noch geschlossen"
25990 DATA "auf dem Blatt untergebracht werden kann. Ist"
26000 DATA "dieses nicht der Fall, wird ein Blattwechsel"
26010 DATA "erzeugt.","!Das Kommando ist ein eigenstaendiges"
26020 DATA "DATA -Listenelement."
26030 DATA ".P0","!!.R .T "
26040 DATA "!Das .T-Kommando erzeugt eine neue Titelzeile."
26050 DATA "!Ansonsten entspricht es im wesentlichen"
26060 DATA "dem .P-Kommando, wobei n auf 10 festgelegt ist."
26070 DATA "!Im Bildschirmmodus erfolgt ein auffaelliges"
26080 DATA "Anzeigen des Textes, welcher dem Kommando folgt."
26090 DATA "!!.R .Mn "
26100 DATA "!Das .Mn-Kommando erzeugt am Zeilenanfang"
26110 DATA "n Leerzeichen. Beispiel:",".M5","links stehen"
26120 DATA "5 Leerzeichen.",".M0","Die Option wird mit dem"
26130 DATA "Kommando .M0 aufgehoben."
26140 DATA "!!.R .E "
26150 DATA "!Das .E-Kommando beendet die Arbeit mit dem"
26160 DATA "Textprogramm."
26170 DATA "!Es erfolgt die Ausschrift  E N D E . "
26180 DATA "Der BASICODE-Mode wird beendet."
26190 DATA ".P22"
26200 DATA "!Hinweise zum Drucken"
26210 DATA "!!Bei Einzelblattverarbeitung sollte Zeile"
26220 DATA "1270 mit GOSUB  210 abgeschlossen"
26230 DATA "sein. Es ermoeglicht den Blattwechsel."
26240 DATA "!!Folgende Eingaben vor dem Druck haben sich"
26250 DATA "bewaehrt, fuer:"
26260 DATA "!!Schriftart ELITE  Zeilenabstand 1/8",".Q "
26270 DATA "!Leerzeichen am Zeilenanfang 12"
26280 DATA "!Textzeichen pro Zeile       85"
26290 DATA "!Leerzeilen am Blattende      6"
26300 DATA "!Textzeilen pro Seite        85"
26310 DATA "!!Schriftart PICA   Zeilenabstand 1/6",".Q "
26320 DATA "!Leerzeichen am Zeilenanfang 10"
26330 DATA "!Textzeichen pro Zeile       75"
26340 DATA "!Leerzeilen am Blattende      4"
26350 DATA "!Textzeilen pro Seite        62"
26360 DATA ".P10","Das war das","!!"
26370 DATA ".C**************************"
26380 DATA ".C*                        *"
26390 DATA ".C*  BASICODE-3   Journal  *"
26400 DATA ".C*        Nummer 10       *"
26410 DATA ".C*     13. Juni 1990      *"
26420 DATA ".C*  erweitert / 28.11.90  *"
26430 DATA ".C*                        *"
26440 DATA ".C**************************",".E"
30000 REM
30010 REM Beschreibung - Textprogramm
30020 REM
32000 REM ---------------
32010 REM H. Eckert
32020 REM P.-Warschow-Str. 2
32030 REM 2200 Greiswald
32040 REM 5/1990 - KC85/3
32050 REM ---------------
32060 REM
32070 REM Erstausstrahlung: Radio DDRII
32080 REM -spezial 900608

1000 a=500:GOTO 20:REM basicode-3 journal 6
1010 ht=ho:vt=ve:s$="":FOR i=1 TO ht:s$=s$+" ":NEXT i
1020 hl=0:hr=ht:vl=2:vr=vt-2:r$="":READ t$
1030 GOSUB 1410:ho=(ht-LEN(sr$)-6)/2:ve=0:GOSUB 110
1040 GOSUB 150:PRINT:PRINT:PRINT
1050 PRINT"Ausdruck ueber"
1060 PRINT:PRINT"   1  den Bildschirm,"
1070 PRINT"   2  den Drucker"
1080 PRINT:PRINT"Welche Nummer ?";:GOSUB 210
1090 sc=in-48:PRINT sc:IF sc<>2 THEN sc=1:GOTO 1220
1100 PRINT:PRINT"Wieviel Leerzeichen"
1110 input"am Zeilenanfang    ";hl
1120 PRINT:PRINT"Wieviel Text-Zeichen"
1130 input"auf jeder Zeile    ";i:hr=hl+i-1
1140 PRINT:PRINT"Wieviel Text-Zeilen"
1150 input"auf jeder Seite    ";vr:vr=vr-3
1160 PRINT"(E) Endlospapier oder  "
1170 PRINT"(L) Lose Blaetter (E/L) ";
1180 GOSUB 210:IF in=76 THEN vl=-1:GOTO 1220
1190 IF in<>69 THEN GOSUB 250:GOTO 1160
1200 PRINT:PRINT"Wieviel Leerzeilen"
1210 input"am Seitenende      ";vl:vr=vr+vl
1220 mm$="":IF hl>0 THEN mm$=LEFT$(s$,hl)
1230 GOSUB 1380:m$=mm$:al=vl:IF sc=2 THEN GOSUB 1360
1240 IF LEN(r$)>hr-hl THEN GOSUB 1820
1250 GOSUB 1440:IF r$<>"" THEN r$=r$+" "
1260 r$=r$+d$:GOTO 1240
1270 IF sc=2 THEN sr$=m$+sr$:GOSUB 350:GOSUB 360:GOTO 1290
1280 GOSUB 120:PRINT m$;sr$;:ve=ve+1:ho=0:GOSUB 110:hl=0
1290 m$=mm$:al=al+1:IF al<=vr THEN RETURN
1300 IF sc=1 THEN 1380
1310 IF vl>=0 THEN FOR i=1 TO vl:GOSUB 360:NEXT i:GOTO 1350
1320 PRINT"Neues Blatt in Drucker einlegen"
1330 PRINT"druecke dann       RETURN/ENTER"
1340 GOSUB 210:IF in<>13 THEN 1340
1350 al=vl
1360 sr$=LEFT$(s$,(hr-hl-LEN(t$))/2)+t$:GOSUB 1270
1370 sr$="":GOSUB 1270:al=vl:RETURN
1380 ho=3:ve=vt:GOSUB 110
1390 sr$="Weiter: druecke <RETURN/ENTER>":GOSUB 150
1400 GOSUB 210:IF in<>13 THEN 1400
1410 GOSUB 100:al=vl:sr$=t$
1420 PRINT LEFT$(s$,(ht-LEN(sr$))/2-3);:GOSUB 150
1430 PRINT:PRINT:RETURN
1440 READ d$
1450 IF d$="" THEN 1440
1460 IF LEFT$(d$,1)="." THEN 1530
1470 IF LEFT$(d$,1)<>">" THEN 1490
1480 d$=LEFT$(s$,23-LEN(r$))+MID$(d$,2):RETURN
1490 IF LEFT$(d$,1)<>"!" THEN RETURN
1500 IF d$="!" THEN d$="":GOTO 1520
1510 d$=MID$(d$,2)
1520 GOSUB 1760:GOTO 1450
1530 IF d$<>".E" THEN 1580
1540 j=1:GOSUB 1760:t$="E N D E"
1550 sr$="":GOSUB 1270:GOSUB 1270:sr$=t$:GOSUB 1270:t$=""
1560 sr$="":GOSUB 1270:IF al<>vl THEN 1560
1570 GOTO 950
1580 dd$=MID$(d$,2,1):d$=MID$(d$,3)
1590 IF dd$<>"R" THEN 1640
1600 IF sc=2 THEN RETURN
1610 IF LEN(r$)+6+LEN(d$)>=hr-hl THEN sr$=r$:GOSUB 1270:r$=""
1620 PRINT m$;r$;:sr$=d$:GOSUB 150:m$=""
1630 hl=hl+LEN(r$)+LEN(d$)+6:r$="":GOTO 1440
1640 IF dd$<>"M" THEN 1670
1650 i=VAL(d$):mm$="":IF i+hl=0 THEN 1440
1660 mm$=LEFT$(s$,i+hl):GOTO 1440
1670 IF dd$="P" THEN j=VAL(d$):GOSUB 1760:GOSUB 1780:GOTO 1440
1680 IF dd$="T" THEN t$=d$:j=10:GOSUB 1760:GOSUB 1780:GOTO 1600
1690 IF dd$="Q" THEN r$=r$+chr$(34)+d$:GOTO 1440
1700 IF dd$<>"F" THEN 1730
1710 j=VAL(d$):GOSUB 1760
1720 j=j-1:IF j>-1 THEN sr$="":GOSUB 1270:GOTO 1720
1730 IF dd$<>"C" THEN 1750
1740 sr$=LEFT$(s$,(hr-hl-LEN(d$))/2)+d$:GOSUB 1270
1750 GOTO 1440
1760 GOSUB 1820:sr$=r$:GOSUB 1270:r$="":j=j-1
1770 RETURN
1780 IF (sc=1)or(al+j>vr) THEN 1800
1790 sr$="":GOSUB 1270:GOSUB 1270:RETURN
1800 IF al<>vl THEN sr$="":GOSUB 1270:GOTO 1800
1810 RETURN
1820 k=hr-hl+1-LEN(m$)
1830 IF LEN(r$)<=k THEN RETURN
1840 IF MID$(r$,k,1)=" " THEN 1870
1850 k=k-1:IF k>0 THEN 1840
1860 sr$="":GOTO 1880
1870 sr$=LEFT$(r$,k-1):r$=MID$(r$,k+1)
1880 GOSUB 1270:GOTO 1820
1900 REM -------------------------------
1910 REM uebersetzte version:
1920 REM
1930 REM computer-bulletin no.32 (7/89)
1940 REM stichting basicode, eindhoven
1950 REM
25000 DATA "Progr. in BASICODE (5)"
25010 DATA 
25020 DATA ".RProgrammsteuerungsanweisungen"
25030 DATA "!!Mit diesen Anweisungen ist es moeglich, ein"
25040 DATA "Programm nicht in der Folge der Zeilennummern"
25050 DATA "abzuarbeiten, sondern den Programmlauf in einer"
25060 DATA "bestimmten Zeile fortzusetzen oder zu beenden."
25070 DATA "!Dazu gehoeren:"
25080 DATA "!! - GOTO, ON ... GOTO,"
25090 DATA "! - IF ... THEN,"
25100 DATA "! - FOR ... NEXT,"
25110 DATA "! - Aufruf von Unterprogrammen"
25120 DATA "!   (GOSUB, ON ... GOSUB),"
25130 DATA "! - unterbrechen bzw. beenden des"
25140 DATA "!   Programms (STOP, END)."
25150 DATA ".P0"
25160 DATA "GOTO Zeilennummer"
25170 DATA "!!ON Variable GOTO Zn1,Zn2,..."
25180 DATA "!ON Ausdruck GOTO Zn1,Zn2,..."
25190 DATA "!!Damit wird das Programm mit den Anweisungen"
25200 DATA "der angegebenen Zeilennummer fortgesetzt."
25210 DATA "Das Sprungziel darf nur eine im Programm"
25220 DATA "vorhandene Zeilennummer sein. Verboten sind"
25230 DATA "!! - Spruenge zu BASICODE-Subroutinen,"
25240 DATA "!   Ausnahme: GOTO 20   - Zeile # 1000,"
25250 DATA "!             GOTO 950  - Programmende,"
25260 DATA "!             GOTO 1000 - RUN im Progr."
25270 DATA "! - Sprunganweisungen folgender Form:"
25280 DATA "!   A=2000 : GOTO A"
25290 DATA ".P0"
25300 DATA "Sorgfalt ist geboten, wenn der Sprung aufgrund"
25310 DATA "des Wertes einer Variablen oder eines Ausdrucks"
25320 DATA "ausgefuehrt werden soll (ON ... GOTO ...,"
25330 DATA "computed GOTO)."
25340 DATA "!Der Wert der Variablen bzw. des Ausdrucks muss"
25350 DATA "ganzzahlig sein. Sofern der BASIC-Interpreter"
25360 DATA "dies nicht ueberwacht, ist die INT-Funktion zu"
25370 DATA "verwenden. Ist der ganzzahlige Wert kleiner als"
25380 DATA "1 oder groesser als die Anzahl der Zeilennumern"
25390 DATA "(Sprungziele), so wird die naechstfolgende"
25400 DATA "Anweisung ausgefuehrt."
25410 DATA "!So darf 'K' nur folgende Werte annehmen:"
25420 DATA "!! ON K GOTO 2000,3000,4000"
25430 DATA "! K=1, 2, 3"
25440 DATA "!! ON K-10 GOTO 5000,6000,7000"
25450 DATA "! K=11, 12, 13"
25460 DATA "!!Es ist deshalb sinnvoll, den Wert von 'K' vor"
25470 DATA "einer solchen Anweisung durch das Programm"
25480 DATA "pruefen zu lassen."
25490 DATA ".P0"
25500 DATA "IF log. Ausdruck THEN Zeilennummer"
25510 DATA "!IF log. Ausdruck THEN Anweisung"
25520 DATA "!!Die in BASICODE zugelassene einfache"
25530 DATA "IF-Anweisung bewirkt, dass das Programm mit der"
25540 DATA "folgenden Anweisung (Zeile) fortgesetzt wird,"
25550 DATA "falls der logische Ausdruck 'falsch' ist; ist er"
25560 DATA "'wahr', wird die nach THEN folgende Anweisung"
25570 DATA "ausgefuehrt."
25580 DATA "!!Die zweiseitig bedingte IF-Anweisung der Form"
25590 DATA "!!IF log.Ausdruck THEN Anweisung ELSE Anweisung"
25600 DATA "!!ist in BASICODE nicht zugelassen."
25610 DATA "!!An Stelle des Ausdrucks kann auch eine"
25620 DATA "logische Variable eingesetzt werden, womit"
25630 DATA "folgende Konstruktion moeglich wird:"
25640 DATA "!! A=(B=3)"
25650 DATA "! IF A THEN ..."
25660 DATA "!!Fuer die Lesbarkeit eines Programms ist es"
25670 DATA "jedoch besser, wenn der logische Ausdruck hinter"
25680 DATA "IF explizit angegeben wird."
25690 DATA "!!Soll aufgrund des Wertes eines log. Ausdrucks"
25700 DATA "ein Sprung (GOTO) zu einer anderen Programmzeile"
25710 DATA "ausgefuehrt werden, muss die Anweisung folgende"
25720 DATA "Form haben:"
25730 DATA "!! IF log. Ausdruck THEN Zeilennummer"
25740 DATA "!!Nicht zugelassen sind:"
25750 DATA "!! IF log. Ausdruck THEN GOTO Zeilennummer"
25760 DATA "! IF log. Ausdruck GOTO Zeilennummer"
25770 DATA "!!Der Aufruf eines Unterprogramms mit GOSUB wird"
25780 DATA "wie eine Anweisung behandelt:"
25790 DATA "!! IF log. Ausdruck THEN GOSUB Zeilennummer"
25800 DATA "! IF log. Ausdruck THEN PRINT ..."
25810 DATA "! IF log. Ausdruck THEN Variable=Wert"
25820 DATA ".P0"
25830 DATA "FOR ... TO ... NEXT"
25840 DATA "!!Die aus BASIC bekannte Schleifenkonstruktion"
25850 DATA "!! FOR Llaufvariable = Anfang TO Grenze"
25860 DATA "! (STEP Schrittweite)"
25870 DATA "!!      Schleifeninhalt"
25880 DATA "!! NEXT Laufvariable"
25890 DATA "!!kann in BASICODE uneingeschraenkt verwendet"
25900 DATA "werden.",".P0"
25910 DATA "Zu beachten ist, dass hinter NEXT die"
25920 DATA "zugehoerige Laufvariable angegeben wird; dies"
25930 DATA "gilt vor allem fuer verschachtelte Schleifen."
25940 DATA "Die Angabe von zwei oder mehr Laufvariablen"
25950 DATA "ist unzulaessig:"
25960 DATA "!! FOR A=1 TO 10"
25970 DATA "! FOR B=1 TO 10"
25980 DATA "!!     Schleifeninhalt"
25990 DATA "!! NEXT B"
26000 DATA "! NEXT A"
26010 DATA " (nicht NEXT B,A !)"
26020 DATA ".P0"
26030 DATA "Unterprogramme (Subroutinen)"
26040 DATA "!!Unterprogramme haben in BASICODE-Programmen"
26050 DATA "eine besondere Bedeutung:"
26060 DATA "!! - sie stellen die BASICODE-Routinen"
26070 DATA "!   dar, die die Portabilitaet der"
26080 DATA "!   Programme moeglich machen"
26090 DATA "! - in BASICODE-Programmen sind sie"
26100 DATA "!   die Programm-Module, die fuer ein"
26110 DATA "!   strukturiertes Programmieren not-"
26120 DATA "!   wendig sind.",".P0"
26130 DATA "Der Aufruf eines Unterprogramms erfolgt mit"
26140 DATA "!! GOSUB Zeilennummer"
26150 DATA "!!Zum berechneten GOSUB (ON ... GOSUB ...)"
26160 DATA "s.o. die Ausfuehrungen zu ON ... GOTO ... !"
26170 DATA "!!Jedes Unterprogramm ist mit RETURN"
26180 DATA "abzuschliessen.",".P0"
26190 DATA "Beenden des Programms"
26200 DATA "!!Zum Unterbrechen bzw. zum Beenden des"
26210 DATA "Programms dienen in den BASIC-Dialekten die"
26220 DATA "Anweisungen STOP und END; in BASICODE-Programmen"
26230 DATA "ist deren Verwendung verboten. Das Programm ist"
26240 DATA "mit dem unbedingten Sprung"
26250 DATA "!!   'GOTO 950'     zu beenden."
26260 DATA ".P0"
26270 DATA ".RREM-Anweisungen"
26280 DATA "!!Die Funktion der REM-Anweisung ist bekannt;"
26290 DATA "mit ihr koennen Anmerkungen, Hinweise,"
26300 DATA "Kennzeichnung von Abschnitten in einem Programm"
26310 DATA "untergebracht werden. Sie erscheinen jedoch nur"
26320 DATA "im Listing des Programms. Beim Programmlauf"
26330 DATA "werden sie vom Interpreter ueberlesen."
26340 DATA "!Das BASICODE-System geht in seiner Konzeption"
26350 DATA "von einer Nutzung der Programme durch andere"
26360 DATA "aus, so dass sich der Zweck von Erlaeuterungen"
26370 DATA "allein schon daraus ergibt. Ausserdem wird der"
26380 DATA "Autor des Programms nach einiger Zeit fuer"
26390 DATA "Erlaeuterungen selbst dankbar sein!",".P0"
26400 DATA "!Gegen die Verwendung von REM-Anweisungen wird"
26410 DATA "! - der Bedarf an Speicherplatz und"
26420 DATA "! - eine Verlaengerung der Laufzeit"
26430 DATA "!angefuehrt. Beide Gruende gelten aber nur in"
26440 DATA "besonderen Faellen."
26450 DATA ".P10","Das war das","!!"
26460 DATA ".C**************************"
26470 DATA ".C*                        *"
26480 DATA ".C*  BASICODE-3   Journal  *"
26490 DATA ".C*        Nummer 6        *"
26500 DATA ".C*     11.  Mai   1990    *"
26510 DATA ".C*                        *"
26520 DATA ".C**************************",".E"
30000 REM
30010 REM grundlagen von basicode
30020 REM       -- teil 5 --
32000 REM --------------------
32010 REM friedrich dormeier
32020 REM
32030 REM d-1000 berlin 39
32040 REM
32050 REM 12/1989 - c64/c128
32060 REM --------------------
32070 REM
32080 REM erstausstrahlung: radio ddr
32090 REM -spezial 900511

1000 A=500:GOTO 20:REM  BASICODE-3 JOURNAL
1010 HT=HO:VT=VE:S$="":FOR I=1 TO HT:S$=S$+" ":NEXT I
1020 HL=0:HR=HT:VL=2:VR=VT-2:R$="":READ T$
1030 GOSUB 1340:HO=(HT-LEN(SR$)-6)/2:VE=0:GOSUB 110
1040 GOSUB 150:PRINT:PRINT:PRINT
1050 PRINT"Soll das Journal auf"
1060 PRINT:PRINT"   1  den Bildschirm,"
1070 PRINT"   2  den Drucker."
1080 PRINT:PRINT"Waehle 1 oder 2: ";:GOSUB 210
1090 SC=IN-48:PRINT SC:IF SC<>2 THEN SC=1:GOTO 1180
1100 PRINT:PRINT"Wieviel Leerzeichen sollen an"
1110 INPUT"den Anfang jeder Zeile ";HL
1120 PRINT:PRINT"Wieviel echte Textzeichen"
1130 INPUT"sollen in eine Zeile ";I:HR=HL+I-1
1140 PRINT:PRINT"Anzahl der Leerzeilen"
1150 INPUT"am Seitenende ";VL
1160 PRINT:PRINT"Wieviel echte Textzeilen sollen auf"
1170 INPUT"eine Seite ";VR:VR=VR+VL-3
1180 MM$="":IF HL>0 THEN MM$=LEFT$(S$,HL)
1190 GOSUB 1310:M$=MM$:AL=VL:IF SC=2 THEN GOSUB 1290
1200 IF LEN(R$)>HR-HL THEN GOSUB 1730
1210 GOSUB 1370:IF R$<>"" THEN R$=R$+" "
1220 R$=R$+D$:GOTO 1200
1230 IF SC=2 THEN SR$=M$+SR$:GOSUB 350:GOSUB 360:GOTO 1250
1240 GOSUB 120:PRINT M$;SR$;:VE=VE+1:HO=0:GOSUB 110:HL=0
1250 M$=MM$:AL=AL+1:IF AL<=VR THEN RETURN
1260 IF SC=1 THEN 1310
1270 IF VL>0 THEN FOR I=1 TO VL:GOSUB 360:NEXT I
1280 AL=VL
1290 SR$=LEFT$(S$,(HR-HL-LEN(T$))/2)+T$:GOSUB 1230
1300 SR$="":GOSUB 1230:AL=VL:RETURN
1310 HO=3:VE=VT:GOSUB 110
1320 SR$="Weiter: mit <ENTER>":GOSUB 150
1330 GOSUB 210:IF IN<>13 THEN 1330
1340 GOSUB 100:AL=VL:SR$=T$
1350 PRINT LEFT$(S$,(HT-LEN(SR$))/2-3);:GOSUB 150
1360 PRINT:PRINT:RETURN
1370 READ D$
1380 IF D$="" THEN 1370
1390 IF LEFT$(D$,1)="." THEN 1440
1400 IF LEFT$(D$,1)<>"!" THEN RETURN
1410 IF D$="!" THEN D$="":GOTO 1430
1420 D$=RIGHT$(D$,LEN(D$)-1)
1430 GOSUB 1670:GOTO 1380
1440 IF D$<>".E" THEN 1490
1450 J=1:GOSUB 1670:T$="Ende dieses Journals"
1460 SR$="":GOSUB 1230:GOSUB 1230:SR$=T$:GOSUB 1230:T$=""
1470 SR$="":GOSUB 1230:IF AL<>VL THEN 1470
1480 GOTO 950
1490 DD$=MID$(D$,2,1):D$=RIGHT$(D$,LEN(D$)-2)
1500 IF DD$<>"R" THEN 1550
1510 IF SC=2 THEN RETURN
1520 IF LEN(R$)+6+LEN(D$)>=HR-HL THEN SR$=R$:GOSUB 1230:R$=""
1530 PRINT M$;R$;:SR$=D$:GOSUB 150:M$=""
1540 HL=HL+LEN(R$)+LEN(D$)+6:R$="":GOTO 1370
1550 IF DD$<>"M" THEN 1580
1560 I=VAL(D$):MM$="":IF I+HL=0 THEN 1370
1570 MM$=LEFT$(S$,I+HL):GOTO 1370
1580 IF DD$="P" THEN J=VAL(D$):GOSUB 1670:GOSUB 1690:GOTO 1370
1590 IF DD$="T" THEN T$=D$:J=10:GOSUB 1670:GOSUB 1690:GOTO 1510
1600 IF DD$="Q" THEN R$=R$+CHR$(34)+D$:GOTO 1370
1610 IF DD$<>"F" THEN 1640
1620 J=VAL(D$):GOSUB 1670
1630 J=J-1:IF J>-1 THEN SR$="":GOSUB 1230:GOTO 1630
1640 IF DD$<>"C" THEN 1660
1650 SR$=LEFT$(S$,(HR-HL-LEN(D$))/2)+D$:GOSUB 1230
1660 GOTO 1370
1670 GOSUB 1730:SR$=R$:GOSUB 1230:R$="":J=J-1
1680 RETURN
1690 IF (SC=1) OR (AL+J>VR) THEN 1710
1700 SR$="":GOSUB 1230:GOSUB 1230:RETURN
1710 IF AL<>VL THEN SR$="":GOSUB 1230:GOTO 1710
1720 RETURN
1730 K=HR-HL+1-LEN(M$)
1740 IF LEN(R$)<=K THEN RETURN
1750 IF MID$(R$,K,1)=" " THEN 1780
1760 K=K-1:IF K>0 THEN 1750
1770 SR$="":GOTO 1790
1780 SR$=LEFT$(R$,K-1):R$=RIGHT$(R$,LEN(R$)-K)
1790 GOSUB 1230:GOTO 1730
25000 DATA "BASICODE-3 JOURNAL-1"
25010 DATA "Hallo Computerfreunde!","!!Endlich ist es da:"
25020 DATA ".RBASICODE"," im Rundfunk der DDR. Wir hoffen,"
25030 DATA "dass BASICODE vielen Spass machen wird und"
25040 DATA "einen umfangreichen Softwaretausch ermoeglicht."
25050 DATA "!!Fuer die hilfreiche Unterstuetzung moechten"
25060 DATA "wir allen Mitgliedern der niederlaendischen"
25070 DATA "Stichting BASICODE, insbesondere aber den"
25080 DATA "Herren",".RKlaas Robers und Jacob Haubrich"
25090 DATA "recht herzlich danken.",".P0"
25100 DATA "!!In diesem Computer-Journal geben wir einige"
25110 DATA "Hinweise zur Funktion der Bascoder fuer die"
25120 DATA "KC-Rechner und den Z-1013."
25130 DATA ".TBascoder fuer KC - Serie"
25140 DATA "!!Die Bascoder fuer die KC-Typen sind"
25150 DATA "BASIC-Programme mit eingebauten"
25160 DATA "Maschinenroutinen. Sie werden"
25170 DATA "mit:","!!  CLOAD",".QName",".Q ","!!eingelesen."
25180 DATA "Der Name fuer die verschiedenen Typen"
25190 DATA "ist","!!BAC853 fuer KC 85/2,3","!BAC854 fuer"
25200 DATA "KC 85/4","!BAC87  fuer KC 85/1 und KC 87",".P0"
25210 DATA "Der Start des Bascoders erfolgt mit RUN. Die"
25220 DATA "Auswahl der Funktionen des Bascoders erfolgt"
25230 DATA "durch Eingabe eines * und eines Buchstabens."
25240 DATA "Fuer die KC 85/2-4 koennen aber auch die"
25250 DATA "Funktionstasten benutzt werden.","!Folgende"
25260 DATA "Funktionen werden realisiert:",".P20"
25270 DATA "Eingabe   Funktion"
25280 DATA "!_______________________________"
25290 DATA "!!*         Es erscheint das Hilfsmenu mit allen"
25300 DATA "Funktionen","!!*L  (F1)  So wird ein"
25310 DATA "BASICODE-Programm von Kassette eingelesen,"
25320 DATA "automatisch in das KC-BASIC uebersetzt"
25330 DATA "und gestartet",".F2","*S  (F2)  Speichern eines"
25340 DATA "BASICODE-Programms im KC-Format",".P0","*A  (F3)"
25350 DATA " Einlesen eines Programms oder ASCII-files"
25360 DATA "im BASICODE-Format. Es wird nicht"
25370 DATA "uebersetzt und gestartet und"
25380 DATA "ist daher zum Kopieren von BASISODE-"
25390 DATA "files geeignet.","!!*W  (F4)  Mit dieser"
25400 DATA "Funktion werden BASICODE-Programme auf Kassette"
25410 DATA "gespeichert.","!!*T  (F5)  Ein im"
25420 DATA "Speicher stehendes Programm im BASICODE-Format"
25430 DATA "wird in das KC-BASIC transformiert.",".P0"
25440 DATA "*C  (F6)  Ein im Speicher stehendes"
25450 DATA "BASIC-Programm im KC-Format wird in"
25460 DATA "ein ASCII-file (BASICODE-Format)"
25470 DATA "convertiert.","!!*K  (F7)  Listen eines"
25480 DATA "BASICODE-Programms","!!",".RHinweis:","Der Start"
25490 DATA "eines BASICODE-Programms erfolgt mit  RUN,"
25500 DATA "fortsetzen mit  CONT nach BRK, listen im KC"
25510 DATA "Format mit LIST und BASICODE-Restart nach"
25520 DATA "RESET mit WBASIC bzw. REBASIC und CALL*40D",".P0"
25530 DATA "!!Die Bascoder fuer die KC-Rechner wurden von"
25540 DATA ".RUwe Zierott","(geb.1954) und"
25550 DATA ".R Andreas Zierott","(geb.1969) aus Lehnin"
25560 DATA "entwickelt. Uwe ist Facharbeiter fuer"
25570 DATA "Datenverarbeitung und arbeitet in einer"
25580 DATA "KC-Vertragswerkstatt. Andreas hat den gleichen"
25590 DATA "Beruf und arbeitet im Rechenzentrum des"
25600 DATA "Meteorologischen Dienst / Potsdam. Beide"
25610 DATA "computern seit ca.4 Jahren."
25620 DATA ".TBASCODER fuer Z-1013","!!! Da viele Z-1013"
25630 DATA "Besitzer noch die 16 k Grundversion haben,"
25640 DATA "musste fuer den Bascoder eine spezielle"
25650 DATA "Loesung gefunden werden. Sie besteht darin, dass"
25660 DATA "zur Arbeit mit BASICODE zwei Teilprogramme"
25670 DATA "benoetigt werden:",".RBASCON","und der"
25680 DATA "eigentliche",".RBASCODER","!Beide Programme"
25690 DATA "koennen mit HEADERSAVE, MAINTAPE oder ab"
25700 DATA "2.Vorton eingelesen werden."
25710 DATA ".TBASCON","!!L 100 EFF   Start mit J 100"
25720 DATA "!!BASCON ist ein"
25730 DATA "Convertierungsprogramm und stellt nach seinem"
25740 DATA "Start folgende Funktionen bereit:","!!Einlesen"
25750 DATA "(BC) von BASICODE-Programmen oder ASCII-files"
25760 DATA "im BASICODE-Format","!!Save (KC) bedeutet"
25770 DATA "Abspeichern im Arbeitsformat des Bascoders"
25780 DATA "(KC-Format)",".P0","Load (KC) bedeutet"
25790 DATA "einlesen von Basicode-Programmen im"
25800 DATA "KC-Format","!!Abspeichern (BC) von"
25810 DATA "BASICODE-Programmen im"
25820 DATA "BC-Format","!!Textkorrektur erlaubt in"
25830 DATA "einigen Faellen die Korrektur von"
25840 DATA "Einlesefehlern"
25850 DATA "der BASICODE-Programme","!!Beenden ="
25860 DATA "Rueckkehr in den Monitor",".P0",".RHinweis"
25870 DATA "Die Funktionsauswahl erfolgt durch Eingabe des"
25880 DATA "jeweiligen Anfangsbuchstabens. Bei SAVE (KC) ist"
25890 DATA "VERIFY mit Y zu quittieren. Ein Warmstart von"
25900 DATA "BASCON kann mit J 102 erfolgen."
25910 DATA ".TZ-1013 BASCODER"
25920 DATA "!!L 100 2AFF  Start mit J 300"
25930 DATA "!!Der Bascoder ist ein speziell"
25940 DATA "entwickelter Z-1013-BASICODE-Interpreter. Er"
25950 DATA "benoetigt ein eigenes Kassettenformat, das mit"
25960 DATA "BASCON bereitgestellt werden kann. Das Einlesen"
25970 DATA "von BASICODE-Programmen (KC-Format) erfolgt"
25980 DATA "mit:","!!LOAD#1",".QName",".Q ",".P0","Der"
25990 DATA "Name muss mit dem uebereinstimmen, wie er unter"
26000 DATA "BASCON mit SAVE (KC) abgespeichert wurde. Will"
26010 DATA "man bereits existierende BASIC-Programme an"
26020 DATA "BASICODE anpassen, so sind sie mit dem normalen"
26030 DATA "10k BASIC-Interpreter mit LIST#1",".QName",".Q "
26040 DATA "zu saven und koennen wie beschrieben in den"
26050 DATA "Bascoder eingelesen werden. Das anzupassende"
26060 DATA "Programm kann in Probelaeufen mit RUN auf"
26070 DATA "unzulaessige Befehle ueberprueft werden. Ein"
26080 DATA "Warmstart des Bascoders erfolgt mit J"
26090 DATA "302.",".P0","Der Bascoder fuer den Z-1013 wurde"
26100 DATA "von",".RMartin Duchrow","(geb.1954) aus Berlin"
26110 DATA "entwickelt. Er ist Dipl.-Ing.-Oekonom und"
26120 DATA "beschaeftigt sich seit 1986 mit dem Z"
26130 DATA "1013.",".TBemerkung","!!Die hier"
26140 DATA "beschriebenen Hinweise zur Benutzung der"
26150 DATA "Bascoder stellen nur eine Minimalvariante dar."
26160 DATA "Eine ausfuehrliche Dokumentation wird in einem"
26170 DATA "umfangreichen BASICODE-Handbuch enthalten sein,"
26180 DATA "das voraussichtlich Anfang naechsten Jahres vom"
26190 DATA "Verlag Technik herausgegeben wird."
29000 DATA ".THINWEIS"
29010 DATA "!!Fuer die weitere Gestaltung unserer Sendungen"
29020 DATA "sind wir an Ihren Erfahrungen (Einlesbarkeit"
29030 DATA "der Programme), Hinweisen  und"
29040 DATA "Programmangeboten interessiert.","!Schreiben Sie"
29050 DATA "uns bitte, welche Aufzeichnungstechnik und"
29060 DATA "welchen Computer Sie verwenden."
29070 DATA ".P10"," Unsere Adresse:","!!"
32000 DATA ".C**************************"
32010 DATA ".C*                        *"
32020 DATA ".C*         R E M          *"
32030 DATA ".C*      RADIO DDR II      *"
32040 DATA ".C*      Nalepastr.        *"
32050 DATA ".C*      BERLIN, 1160      *"
32060 DATA ".C*                        *"
32070 DATA ".C**************************"
32074 DATA ".P10","Das war das","!!"
32076 DATA ".C**************************"
32080 DATA ".C*                        *"
32090 DATA ".C*  BASICODE-3   Journal  *"
32100 DATA ".C*        Nummer 1        *"
32110 DATA ".C*    6. September 1989   *"
32120 DATA ".C*      Acorn-Master      *"
32130 DATA ".C*                        *"
32140 DATA ".C**************************",".E"

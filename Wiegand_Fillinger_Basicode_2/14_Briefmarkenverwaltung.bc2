
1000 A=5000:GOTO20:REM * BRIEFMARKENVERWALTUNG *
1010 GOSUB100
1020 REM * CO. H+M FILLINGER *
1030 REM * 2.4.1984          *
1040 REM  VARIABLENLISTE
1050 REM A$(I).............BEZEICHNUNGEN
1060 REM AW,AW$............AUSWAHL
1070 REM B$(I).............BESONDERHEITEN
1080 REM D,D1..............DRUCKERAUSGABE
1090 REM DA,TS.............KONTROLLVARIABLE DATEILESEN
1100 REM DN$...............DATEINAME
1110 REM ET(I).............ANZAHL ETB-SAMMELBLAETTER
1120 REM FD(I).............ANZAHL ERSTTAGSBRIEFE
1130 REM GE(I).............ANZAHL MARKEN GESTEMPELT
1140 REM I,J...............LAUFVARIABLE
1150 REM L$................SAMMELGEBIET
1160 REM J$................JAHRGANG
1170 REM KB$(I)............KURZBEZEICHNUNG
1180 REM MH(I).............ANZAHL MARKENHEFTE
1190 REM MK(I).............ANZAHL MAXIMUMKARTEN
1200 REM MN$(I)............KATALOGNUMMER
1210 REM P$,P$(I)..........PREISNOTIERUNG AUS KATALOG
1220 REM S.................GESAMTWERT JAHRGANG
1230 REM S1,S2,S3,S4,S5,S6.GESAMTWERT ART
1240 REM S1(I).....S6(I)...EINZELPREISE
1250 REM
1300 CT=7:CN=2:AW=0:DA=0:TS=0
1310 DIMA$(14),MN$(100),KB$(100),PF(100),GE(100)
1320 DIMET(100),FD(100),MH(100),MK(100),B$(100)
1330 DIMP$(100),S1(100),S2(100),S3(100),S4(100)
1340 DIMS5(100),S6(100)
1350 REM        
1700 REM * DATA EINLESEN *
1710 FORJ=0 TO14
1720 READA$(J)
1730 IFJ>11 THEN1790
1740 L=LEN(A$(J))
1750 FORJJ=L TO15
1760 A$(J)=A$(J)+" "
1770 NEXTJJ
1780 A$(J)=A$(J)+":"
1790 NEXTJ
1800 REM * TITEL *
1810 GOSUB100:VE=5:HO=0:GOSUB110
1820 PRINT
1830 PRINT
1840 PRINT"BRIEFMARKENVERWALTUNG"
1850 PRINT:PRINT
1860 PRINT"CO. H+M FILLINGER"
1870 PRINT"  MCMLXXXIV"
1900 REM * PAUSE *
1910 FORJ=0 TO2000:NEXTJ
1920 I=-1:ID=0:GOSUB100
1930 REM
1940 REM
2000 REM * MENUE *
2010 PRINT"-1- NEUEINGABEN/ERGAENZUNGEN"
2020 PRINT"-2- AENDERUNGEN"
2030 PRINT"-3- AUSGABE BESTANDSLISTE"
2040 PRINT"-4- KATALOGWERTERMITTLUNG"
2050 PRINT"-0- PROGRAMM BEENDEN"
2060 INPUTAW
2070 IF(AW=0) AND(TS=1) THENGOSUB6500
2080 IFAW=0 THENEND
2090 IF(AW<1) OR(AW>4) THEN2010
2100 IFID=0 THENGOSUB7000
2110 IFDA=0 THENGOSUB6000
2120 ONAW GOSUB2500,3000,4000,5000
2130 GOSUB100
2140 AW=0
2150 GOTO2010
2160 REM
2170 REM
2500 REM * NEUEINGABEN/ERGAENZUNGEN *
2510 GOSUB100
2520 TS=1:IFP$="" THENGOSUB7060
2530 I=I+1
2540 PRINT"RUECKKEHR ZUM MENUE DURCH EINGABE"
2550 PRINT"VON 0 ALS MICHEL-NR."
2560 PRINTDN$
2570 PRINTA$(2);:INPUTMN$(I)
2580 IFMN$(I)="0" THENI=I-1:RETURN
2590 PRINTA$(3);:INPUTKB$(I)
2600 PRINTA$(12)+" "+A$(4);:INPUTPF(I)
2610 PRINTA$(13)+" "+A$(4);:INPUTS1(I)
2620 PRINTA$(12)+" "+A$(5);:INPUTGE(I)
2630 PRINTA$(13)+" "+A$(5);:INPUTS2(I)
2640 PRINTA$(12)+" "+A$(6);:INPUTET(I)
2650 PRINTA$(13)+" "+A$(6);:INPUTS3(I)
2660 PRINTA$(12)+" "+A$(7);:INPUTFD(I)
2670 PRINTA$(13)+" "+A$(7);:INPUTS4(I)
2680 PRINTA$(12)+" "+A$(8);:INPUTMH(I)
2690 PRINTA$(13)+" "+A$(8);:INPUTS5(I)
2700 PRINTA$(12)+" "+A$(9);:INPUTMK(I)
2710 PRINTA$(13)+" "+A$(9);:INPUTS6(I)
2720 PRINTA$(10);:INPUTB$(I)
2730 P$(I)=P$
2740 GOSUB100
2750 GOTO2530
2760 REM
3000 REM * AENDERUNGEN *
3010 GOSUB100
3020 TS=1
3030 PRINT"RUECKKEHR ZUM MENUE DURCH EINGABE"
3040 PRINT"VON 0 ALS MICHEL-NR."
3050 PRINT:PRINTDN$
3060 PRINTA$(2);:INPUTMN$
3070 IFMN$="0" THENRETURN
3080 J=-1
3090 FORL=0 TOI
3100 IFMN$=MN$(L) THENJ=L
3110 NEXTL
3120 IFJ=-1 THENGOSUB3700
3130 PRINT"-0- WEITER"
3140 PRINT"-1- AENDERN"
3150 PRINTDN$
3160 PRINTA$(2);MN$(J)
3170 GOSUB210:IFIN$="0" THEN3190
3180 PRINTA$(2);:INPUTMN$(J)
3190 PRINTA$(3);KB$(J)
3200 GOSUB210:IFIN$="0" THEN3220
3210 PRINTA$(3);:INPUTKB$(J)
3220 PRINTA$(12)+" "+A$(4);PF(J)
3230 GOSUB210:IFIN$="0" THEN3250
3240 PRINTA$(12)+" "+A$(4);:INPUTPF(J)
3250 PRINTA$(13)+" "+A$(4);S1(J)
3260 GOSUB210:IFIN$="0" THEN3280
3270 PRINTA$(13)+" "+A$(4);:INPUTS1(J)
3280 PRINTA$(12)+" "+A$(5);GE(J)
3290 GOSUB210:IFIN$="0" THEN3310
3300 PRINTA$(12)+" "+A$(5);:INPUTGE(J)
3310 PRINTA$(13)+" "+A$(5);S2(J)
3320 GOSUB210:IFIN$="0" THEN3340
3330 PRINTA$(13)+" "+A$(5);:INPUTS2(J)
3340 PRINTA$(12)+" "+A$(6);ET(J)
3350 GOSUB210:IFIN$="0" THEN3370
3360 PRINTA$(12)+" "+A$(6);:INPUTET(J)
3370 PRINTA$(13)+" "+A$(6);S3(J)
3380 GOSUB210:IFIN$="0" THEN3400
3390 PRINTA$(13)+" "+A$(6);:INPUTS3(J)
3400 PRINTA$(12)+" "+A$(7);FD(J)
3410 GOSUB210:IFIN$="0" THEN3430
3420 PRINTA$(12)+" "+A$(7);:INPUTFD(J)
3430 PRINTA$(13)+" "+A$(7);S4(J)
3440 GOSUB210:IFIN$="0" THEN3460
3450 PRINTA$(13)+" "+A$(7);:INPUTS4(J)
3460 PRINTA$(12)+" "+A$(8);MH(J)
3470 GOSUB210:IFIN$="0" THEN3490
3480 PRINTA$(12)+" "+A$(8);:INPUTMH(J)
3490 PRINTA$(13)+" "+A$(8);S5(J)
3500 GOSUB210:IFIN$="0" THEN3520
3510 PRINTA$(13)+" "+A$(8);:INPUTS5(J)
3520 PRINTA$(12)+" "+A$(9);MK(J)
3530 GOSUB210:IFIN$="0" THEN3550
3540 PRINTA$(12)+" "+A$(9);:INPUTMK(J)
3550 PRINTA$(13)+" "+A$(9);S6(J)
3560 GOSUB210:IFIN$="0" THEN3580
3570 PRINTA$(13)+" "+A$(9);:INPUTS6(J)
3580 PRINTA$(10);B$(J)
3590 GOSUB210:IFIN$="0" THEN3610
3600 PRINTA$(10);:INPUTB$(J)
3610 PRINTA$(11);P$(J)
3620 GOSUB210:IFIN$="0" THEN3640
3630 PRINTA$(11);:INPUTP$(J)
3640 GOSUB100
3650 GOTO3030
3660 REM
3670 REM
3700 REM * HILFE *
3710 GOSUB100
3720 PRINT"LEIDER NICHT GEFUNDEN "
3730 PRINT"HIER EINE AUFLISTUNG DER"
3740 PRINT"GESPEICHERTEN MICHEL-NR."
3750 PRINT:PRINT"-0- WEITER "
3760 PRINT"-1- AENDERN"
3770 PRINT
3780 J=0
3790 PRINTMN$(J)
3800 GOSUB210
3810 IFIN$="1" THENRETURN
3820 J=J+1
3830 IFJ>I THENPRINT"AUSGABE ENDE":MN$="0":GOTO3070
3840 GOTO3790
3850 REM
4000 REM * BESTANDSLISTE *
4010 GOSUB100
4020 GOSUB8000
4030 GOSUB100
4040 J=0
4050 ZE=0
4060 SR$=A$(0):GOSUB8500
4070 ZE=1
4080 SR$=L$:GOSUB8500
4090 ZE=0:SR$=A$(1):GOSUB8500
4100 ZE=1:SR$=J$:GOSUB8500
4110 ZE=0:SR$=A$(2):GOSUB8500
4120 ZE=1:SR$=MN$(J):GOSUB8500
4130 ZE=0:SR$=A$(3):GOSUB8500
4140 ZE=1:SR$=KB$(J):GOSUB8500
4150 ZE=0:SR$="                   ":GOSUB8500
4160 SR$=A$(12)+" ":GOSUB8500
4170 SR$=A$(13)+"  ":GOSUB8500
4180 ZE=1:SR$=A$(14):GOSUB8500
4190 ZE=0:SR$=A$(4):GOSUB8500
4200 SR=PF(J):CN=0:GOSUB310:GOSUB8500
4210 SR=S1(J):CN=2:GOSUB310:GOSUB8500
4220 SR=PF(J)*S1(J):GOSUB310:ZE=1:GOSUB8500
4230 ZE=0:SR$=A$(5):GOSUB8500
4240 SR=GE(J):CN=0:GOSUB310:GOSUB8500
4250 SR=S2(J):CN=2:GOSUB310:GOSUB8500
4260 SR=GE(J)*S2(J):GOSUB310:ZE=1:GOSUB8500
4270 ZE=0:SR$=A$(6):GOSUB8500
4280 SR=ET(J):CN=0:GOSUB310:GOSUB8500
4290 SR=S3(J):CN=2:GOSUB310:GOSUB8500
4300 SR=ET(J)*S3(J):GOSUB310:ZE=1:GOSUB8500
4310 ZE=0:SR$=A$(7):GOSUB8500
4320 SR=FD(J):CN=0:GOSUB310:GOSUB8500
4330 SR=S4(J):CN=2:GOSUB310:GOSUB8500
4340 SR=FD(J)*S4(J):GOSUB310:ZE=1:GOSUB8500
4350 ZE=0:SR$=A$(8):GOSUB8500
4360 SR=MH(J):CN=0:GOSUB310:GOSUB8500
4370 SR=S5(J):CN=2:GOSUB310:GOSUB8500
4380 SR=MH(J)*S5(J):GOSUB310:ZE=1:GOSUB8500
4390 ZE=0:SR$=A$(9):GOSUB8500
4400 SR=MK(J):CN=0:GOSUB310:GOSUB8500
4410 SR=S6(J):CN=2:GOSUB310:GOSUB8500
4420 SR=MK(J)*S6(J):GOSUB310:ZE=1:GOSUB8500
4430 ZE=0:SR$=A$(10):GOSUB8500
4440 ZE=1:SR$=B$(J):GOSUB8500
4450 ZE=0:SR$=A$(11):GOSUB8500
4460 ZE=1:SR$=P$(J):GOSUB8500
4470 IFD=1 THENSR$="":GOSUB8500:GOSUB8500:GOTO4500
4480 PRINT:PRINT"WENN WEITER, BITTE TASTE DRUECKEN"
4490 GOSUB210:GOSUB100
4500 J=J+1
4510 IFJ>I THENRETURN
4520 GOTO4050
4530 REM
5000 REM * KATALOGWERT *
5010 GOSUB100
5020 GOSUB8000
5030 GOSUB100
5040 S1=0:S2=0:S3=0:S4=0:S5=0:S6=0:S=0
5050 ZE=1:SR$=DN$:GOSUB8500
5060 SR$="":GOSUB8500
5070 FORJ=0 TOI
5080 S1=S1+(S1(J)*PF(J))
5090 S2=S2+(S2(J)*GE(J))
5100 S3=S3+(S3(J)*ET(J))
5110 S4=S4+(S4(J)*FD(J))
5120 S5=S5+(S5(J)*MH(J))
5130 S6=S6+(S6(J)*MK(J))
5140 NEXTJ
5150 S=S1+S2+S3+S4+S5+S6
5160 SR$="ZUSAMMENSTELLUNG":GOSUB8500
5170 SR$="":GOSUB8500
5180 CN=2
5190 ZE=0:SR$=A$(4):GOSUB8500
5200 SR=S1:GOSUB310:SR$=SR$+" DM":ZE=1:GOSUB8500
5210 ZE=0:SR$=A$(5):GOSUB8500
5220 SR=S2:GOSUB310:SR$=SR$+" DM":ZE=1:GOSUB8500
5230 ZE=0:SR$=A$(6):GOSUB8500
5240 SR=S3:GOSUB310:SR$=SR$+" DM":ZE=1:GOSUB8500
5250 ZE=0:SR$=A$(7):GOSUB8500
5260 SR=S4:GOSUB310:SR$=SR$+" DM":ZE=1:GOSUB8500
5270 ZE=0:SR$=A$(8):GOSUB8500
5280 SR=S5:GOSUB310:SR$=SR$+" DM":ZE=1:GOSUB8500
5290 ZE=0:SR$=A$(9):GOSUB8500
5300 SR=S6:GOSUB310:SR$=SR$+" DM":ZE=1:GOSUB8500
5310 SR$="":GOSUB8500
5320 ZE=0:SR$="SUMME           :":GOSUB8500
5330 SR=S:GOSUB310:SR$=SR$+" DM":ZE=1:GOSUB8500
5340 IFD=1 THENRETURN
5350 PRINT"WENN WEITER, BITTE TASTE DRUECKEN"
5360 GOSUB210
5370 RETURN
5380 REM
6000 REM * DATEI EINLESEN *
6010 DA=1:IFAW>1 THEN6070
6020 PRINT"WURDEN BEREITS DATEN ZU"
6030 PRINTDN$+" GESPEICHERT";
6040 INPUTAW$
6050 IFLEFT$(AW$,1)="N" THENRETURN
6060 IFLEFT$(AW$,1)<>"J" THEN6020
6070 GOSUB21200
6080 GOSUB21500
6090 IFDN$=S1$ THEN6110
6100 PRINT"FALSCHE DATENKASSETTE !!":GOTO6070
6110 I=VAL(S2$)
6120 FORJ=0 TOI
6130 GOSUB21500:MN$(J)=S1$:KB$(J)=S2$
6140 GOSUB21500:PF(J)=VAL(S1$):S1(J)=VAL(S2$)
6150 GOSUB21500:GE(J)=VAL(S1$):S2(J)=VAL(S2$)
6160 GOSUB21500:ET(J)=VAL(S1$):S3(J)=VAL(S2$)
6170 GOSUB21500:FD(J)=VAL(S1$):S4(J)=VAL(S2$)
6180 GOSUB21500:MH(J)=VAL(S1$):S5(J)=VAL(S2$)
6190 GOSUB21500:MK(J)=VAL(S1$):S6(J)=VAL(S2$)
6200 GOSUB21500:B$(J)=S1$:P$(J)=S2$
6210 NEXTJ
6220 GOSUB21700
6230 RETURN
6240 REM
6500 REM * DATEN SPEICHERN *
6510 TS=0
6520 IFLEFT$(AW$,1)="N" THEN6570
6530 GOSUB21200
6540 GOSUB21500:GOSUB21700
6550 IFDN$=S1$ THENGOTO6570
6560 PRINT"FALSCHE DATENKASSETTE !!!":GOTO6530
6570 PRINT"BITTE ZURUECKSPULEN"
6580 PRINT"WENN FERTIG, BITTE TASTE DRUECKEN"
6590 GOSUB210
6600 GOSUB21100
6610 S1$=DN$:SR=I:GOSUB300:S2$=SR$
6620 GOSUB21300
6630 FORJ=0 TOI
6640 S1$=MN$(J):S2$=KB$(J):GOSUB21300
6650 SR=PF(J):GOSUB300:S1$=SR$
6660 SR=S1(J):GOSUB300:S2$=SR$:GOSUB21300
6670 SR=GE(J):GOSUB300:S1$=SR$
6680 SR=S2(J):GOSUB300:S2$=SR$:GOSUB21300
6690 SR=ET(J):GOSUB300:S1$=SR$
6700 SR=S3(J):GOSUB300:S2$=SR$:GOSUB21300
6710 SR=FD(J):GOSUB300:S1$=SR$
6720 SR=S4(J):GOSUB300:S2$=SR$:GOSUB21300
6730 SR=MH(J):GOSUB300:S1$=SR$
6740 SR=S5(J):GOSUB300:S2$=SR$:GOSUB21300
6750 SR=MK(J):GOSUB300:S1$=SR$
6760 SR=S6(J):GOSUB300:S2$=SR$:GOSUB21300
6770 S1$=B$(J):S2$=P$(J):GOSUB21300
6780 NEXTJ
6790 GOSUB21700
6800 RETURN
6810 REM
7000 REM * IDENTIFIKATION *
7010 GOSUB100:ID=1
7020 PRINTA$(0);:INPUTL$
7030 PRINTA$(1);:INPUTJ$
7040 DN$=L$+" "+J$
7050 IFAW>2 THENRETURN
7060 PRINTA$(11)+" GEMAESS ";:INPUTP$
7070 RETURN
7080 REM
8000 REM * AUSGABEAUSWAHL *
8010 PRINT"-1- AUSGABE DRUCKER"
8020 PRINT"-2- AUSGABE MONITOR"
8030 INPUTD
8040 IF(D<1) OR(D>2) THEN8010
8050 RETURN
8060 REM
8500 REM * AUSGABE *
8510 PRINTSR$;
8520 IFD=1 THENGOSUB350
8530 IFZE=0 THENRETURN
8540 IFD=1 THENGOSUB360
8550 PRINT
8560 RETURN
8570 REM
21000 REM DATEIVERWALTUNG
21010 REM  GILT FUER C64
21100 REM  DATEI OEFFNEN ZUM SCHREIBEN
21110 REM OPEN 1,1,1,DN$:RETURN
21200 REM  DATEI OEFFNEN ZUM LESEN
21210 REM OPEN 1,1,0,DN$:RETURN
21300 QP$=S1$:GOSUB21400:S1$=QZ$:QP$=S2$:GOSUB21400
21310 S2$=QZ$
21320 REM DATEN (S1$,S2$) AUF KASSETTE SCHREIBEN
21330 REM PRINT#1,S1$:REM PRINT#1,S2$:RETURN
21400 REM UMWANDELN VON DOPPELPUNKT,KOMMA
21410 REM ANFUEHRUNGSZEICHEN USW.
21420 QZ$="":IFQP$="" THENQP$=" "
21430 FORQP=1 TOLEN(QP$)
21440 QQ$=MID$(QP$,QP,1):IFQQ$=" " THENQQ$=CHR$(252)
21450 IFQQ$="," THENQQ$=CHR$(250)
21460 IFQQ$=CHR$(34) THENQQ$=CHR$(253)
21470 IFQQ$=":" THENQQ$=CHR$(251)
21480 QZ$=QZ$+QQ$:NEXTQP:RETURN
21500 REM  DATEN (S1$,S2$) VON KASSETTE LESEN
21510 REM INPUT#1,S1$:REM INPUT#1,S2$
21520 QP$=S1$:GOSUB21600:S1$=QZ$:QP$=S2$
21530 GOSUB21600:S2$=QZ$:RETURN
21600 REM UMWANDELN VON DOPPELPUNKT,KOMMA
21610 REM ANFUEHRUNGSZEICHEN USW.
21620 QZ$="":IFOP$="" THENOP$=" "
21630 FORQP=1 TOLEN(QP$)
21640 QQ$=MID$(QP$,QP,1):IFQQ$=CHR$(252) THENQQ$=" "
21650 IFQQ$=CHR$(250) THENQQ$=","
21660 IFQQ$=CHR$(251) THENQQ$=":"
21670 IFQQ$=CHR$(253) THENQQ$=CHR$(34)
21680 QZ$=QZ$+QQ$
21690 NEXTQP:RETURN
21700 REM  DATEI SCHLIESSEN
21710 REM CLOSE 1:RETURN
25000 DATA "LAND","JAHRGANG","MICHEL-NR."
25010 DATA "KURZBEZEICHNUNG","POSTFRISCH"
25020 DATA "GESTEMPELT","ETB/SAMMELBLATT"
25030 DATA "ERSTTAGSBRIEF","MARKENHEFT"
25040 DATA "MAXIMUMKARTE","BESONDERHEITEN"
25050 DATA "PREISNOTIERUNG","ANZAHL"
25060 DATA "PREIS","SUMME"

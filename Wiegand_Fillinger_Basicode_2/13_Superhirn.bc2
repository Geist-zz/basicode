1000 A=200:GOTO20:REM * SUPERHIRN *
1010 REM
1020 REM
1030 REM * H & M FILLINGER  *
1040 REM * 1.2.1984         *
1050 REM
1060 REM
1100 REM * KONSTANTEN *
1110 REM
1120 REM TITEL
1130 REM
1140 LETA$="     S U P E R H I R N    "
1150 LETA1$="   CO. H.& M. FILLINGER"
1160 LETL=LEN(A$)
1170 LETL1=LEN(A1$)
1180 REM ANZAHL DER VERSUCHE
1190 LETV=4
1200 REM
1210 REM
1220 REM
1300 REM * VARIABLENLISTE *
1310 REM
1320 REM  F$     = JA-NEIN ABFRAGE
1330 REM  I,II,IJ= LAUFVARIABLE
1340 REM  K$(I)  = GERATENE KOMBINATION
1350 REM  K1$(II)= POSITIONSBEWERTUNG T$(II)
1360 REM  KL$(I) = ZUSAMMENGEFASSTE BEWERTUNG
1370 REM           DER KOMBINATION K$(I)
1380 REM  R(I)   = ZUFALLSZAHL
1390 REM  R$(I)  = AUS ZUFALLSZAHL GEBILDETE FARBE
1400 REM  R$     = RATESTRING
1410 REM  T$(II) = ZERLEGTE KOMBINATION K$(I)
1420 REM
1430 REM
1440 REM
1500 DIMK$(V),K1$(4),KL$(V),R(4),R$(4),T$(4)
1510 REM
1520 REM
1530 REM
1540 REM
1550 REM * VARIABLEN = 0 SETZEN *
1560 F$="":R$=""
1570 FORI=1 TOV
1580 K$(I)=""
1590 KL$(I)=""
1600 NEXTI
1610 FORI=1 TO4
1620 K1$(I)=""
1630 R(I)=0
1640 R$(I)=""
1650 T$(I)=""
1660 NEXTI
1670 REM
1680 REM
1690 GOSUB100
1700 REM * TITEL *
1710 FORI=1 TOL
1720 VE=2
1730 HO=3+I
1740 GOSUB110
1750 PRINTMID$(A$,I,1);
1760 NEXTI
1770 FORI=1 TOL1
1780 VE=5
1790 HO=3+I
1800 GOSUB110
1810 PRINTMID$(A1$,I,1);
1820 NEXTI
1830 FORI=1 TO1000
1840 NEXTI
1850 FORI=L1 TO1 STEP-1
1860 VE=5
1870 HO=3+I
1880 GOSUB110
1890 PRINT" ";
1900 NEXTI
1910 FORI=L TO1 STEP-1
1920 VE=2
1930 HO=3+I
1940 GOSUB110
1950 PRINT" ";
1960 NEXTI
1970 GOSUB100
1980 GOSUB4000
1990 REM
2000 REM * FESTLEGUNG DER FARBKOMBINATION *
2010 REM
2020 FORI=1 TO4
2030 GOSUB260
2040 R(I)=INT(RV*10)
2050 IF(R(I)=0) OR(R(I)>5) THEN2030
2060 ONR(I) GOTO2070,2080,2090,2100,2110
2070 R$(I)="B":GOTO2120
2080 R$(I)="G":GOTO2120
2090 R$(I)="R":GOTO2120
2100 R$(I)="S":GOTO2120
2110 R$(I)="W"
2120 R$=R$+R$(I)
2130 NEXTI
2140 GOSUB100
2150 I=0:GOSUB3000
2200 REM * EINGABE UND AUSWERTUNG *
2210 REM
2220 FORI=1 TOV
2230 PRINT"KOMBINATION";:INPUTK$(I)
2240 IFNOT(LEN(K$(I))>4) THEN2270
2250 PRINT"EINGABE UNZULAESSIG     "
2260 GOTO2230
2270 II=0
2280 II=II+1
2290 T$(II)=MID$(K$(I),II,1)
2300 IFT$(II)="B" THEN2360
2310 IFT$(II)="G" THEN2360
2320 IFT$(II)="R" THEN2360
2330 IFT$(II)="S" THEN2360
2340 IFT$(II)="W" THEN2360
2350 PRINT"UNZULAESSIGE FARBE":KL$(I)="":GOTO2230
2360 IFT$(II)=R$(II) THENK1$(II)="P"
2370 FORIJ=1 TO4
2380 IF(T$(II)=R$(IJ)) AND(K1$(II)<>"P") THENK1$(II)="F"
2390 NEXTIJ
2400 IFK1$(II)="" THENK1$(II)="-"
2410 KL$(I)=KL$(I)+K1$(II)
2420 K1$(II)=""
2430 IFNOT(II>=4) THEN2280
2440 GOSUB3000
2450 IFKL$(I)="PPPP" THENGOSUB100:GOTO2540
2460 NEXTI
2470 GOSUB100
2480 PRINT"SIE HABEN DIE KOMBINATON"
2490 PRINTR$;" MIT";V;"VERSUCHEN"
2500 PRINT"NICHT GERATEN !!!!"
2510 PRINT:PRINT
2520 GOTO2590
2530 REM
2540 PRINT"SIE HABEN DIE KOMBINATION"
2550 PRINTR$;" MIT";I;"VERSUCHEN"
2560 PRINT"GERATEN !!!!"
2570 PRINT
2580 PRINT
2590 PRINT"NOCH EIN VERSUCH";:INPUTF$
2600 IFLEFT$(F$,1)="J" THEN1550
2610 IFLEFT$(F$,1)<>"N" THEN2590
2620 END
3000 REM * HILFE *
3010 REM
3020 FORIJ=1 TOI
3030 VE=IJ:HO=0
3040 GOSUB110
3050 PRINTK$(IJ);
3060 HO=10
3070 GOSUB110
3080 PRINTKL$(IJ);
3090 NEXTIJ
3100 FORIJ=2 TO6
3110 VE=IJ
3120 HO=30
3130 GOSUB110
3140 IFIJ=2 THENPRINT"B=BLAU";
3150 IFIJ=3 THENPRINT"G=GELB";
3160 IFIJ=4 THENPRINT"R=ROT";
3170 IFIJ=5 THENPRINT"S=SCHWARZ";
3180 IFIJ=6 THENPRINT"W=WEISS";
3190 NEXTIJ
3200 VE=8:HO=0
3210 GOSUB110
3220 PRINT"-=WEDER FARBE NOCH POSITION STIMMEN";
3230 VE=9:HO=0
3240 GOSUB110
3250 PRINT"F=FARBE AN ANDERER STELLE VORHANDEN";
3260 VE=10:HO=0
3270 GOSUB110
3280 PRINT"P=POSITION UND FARBE STIMMEN";
3290 VE=7:HO=0
3300 GOSUB110
3310 PRINT"                             ";
3320 GOSUB110
3330 RETURN
4000 REM * SPIELREGELN *
4010 REM
4020 PRINT"WERDEN SPIELREGELN GEWUENSCHT";:INPUTF$
4030 IFLEFT$(F$,1)="N" THENRETURN
4040 IFLEFT$(F$,1)<>"J" THEN4020
4050 GOSUB100
4060 PRINT
4070 PRINT"ES IST EINE KOMBINATION VON 5 FARBEN IN"
4080 PRINT"4 POSITIONEN MIT";V;"VERSUCHEN ZU RATEN."
4090 PRINT
4100 PRINT"DIE FARBEN BEDEUTEN :"
4110 PRINT"   B=BLAU"
4120 PRINT"   G=GELB"
4130 PRINT"   R=ROT"
4140 PRINT"   S=SCHWARZ"
4150 PRINT"   W=WEISS"
4160 PRINT
4170 PRINT"WENN WEITER BITTE TASTE DRUECKEN"
4180 GOSUB210
4190 GOSUB100
4200 PRINT
4210 PRINT"IN DER KOMBINATION KOENNEN FARBEN GAR"
4220 PRINT"NICHT ODER MEHRFACH VORHANDEN SEIN !"
4230 PRINT"Z.B.:"
4240 PRINT"        SSRW"
4250 PRINT"(SCHWARZ MEHRFACH;BLAU,GELB NICHT)"
4260 PRINT
4270 PRINT"BITTE ACHTEN SIE IMMER DARAUF 4"
4280 PRINT"BUCHSTABEN EINZUGEBEN"
4290 PRINT
4300 PRINT"WENN WEITER BITTE TASTE DRUECKEN"
4310 GOSUB210
4320 GOSUB100
4330 PRINT
4340 PRINT"JEDE EINGABE WIRD BEWERTET."
4350 PRINT"VOR JEDER NEUEN EINGABE ERSCHEINEN AUF"
4360 PRINT"DEM BILDSCHIRM ALLE BISHERIGEN MIT IHRER"
4370 PRINT"BEWERTUNG."
4380 PRINT"DABEI BEDEUTET :"
4390 PRINT" - = WEDER FARBE NOCH POSITION STIMMEN"
4400 PRINT"     UEBEREIN"
4410 PRINT" F = FARBE VORHANDEN, JEDOCH AN ANDERER"
4420 PRINT"     POSITION"
4430 PRINT" P = POSITION UND FARBE STIMMEN UEBEREIN"
4440 PRINT
4450 PRINT"WENN WEITER BITTE TASTE DRUECKEN"
4460 GOSUB210
4470 RETURN


1000 A=50:GOTO20:REM *** WOCHENTAG ***
1010 REM
1020 REM CO. H.UND M.FILLINGER 4.4.84
1030 REM
1040 DIMW$(7)
1050 REM
1060 REM        KONSTANTEN
1070 LETW$(1)="SONNTAG"
1080 LETW$(2)="MONTAG"
1090 LETW$(3)="DIENSTAG"
1100 LETW$(4)="MITTWOCH"
1110 LETW$(5)="DONNERSTAG"
1120 LETW$(6)="FREITAG"
1130 LETW$(7)="SAMSTAG"
1140 REM
1150 REM        VARIABLEN
1160 REM        J....JAHR
1170 REM        SJ...KENNZEICHEN F. SCHALTJAHR
1180 REM        M....MONAT
1190 REM        T....TAG
1200 REM        V,Z,S,R...HILFSVAR.
1210 REM
1220 GOSUB100
1230 REM
2000 REM ---EINGABE-MODUL---
2010 PRINT:PRINTTAB(12)"WOCHENTAG":PRINT
2020 PRINT"BITTE EIN DATUM (TT-MM-JJJJ) EINGEBEN:"
2030 PRINT"TAG";:INPUTT
2040 PRINT"MONAT";:INPUTM
2050 PRINT"JAHR";:INPUTJ
2060 REM EINGABEPRUEFUNG
2070 IF(T<1) OR(T>31) OR(T<>INT(T)) THEN2500
2080 IF(M<1) OR(M>12) OR(M<>INT(M)) THEN2500
2090 IF(J<1583) OR(J>9999) OR(J<>INT(J)) THEN2500
2100 SJ=1
2110 IFJ/4<>INT(J/4) THENSJ=0
2120 IFJ/100=INT(J/100) THENSJ=0
2130 IFJ/400=INT(J/400) THENSJ=1
2140 IF((M=4) AND(T>30)) OR((M=6) AND(T>30)) THEN2500
2150 IF((M=9) AND(T>30)) OR((M=11) AND(T>30)) THEN2500
2160 IF(SJ=0) AND(M=2) AND(T>28) THEN2500
2170 IF(SJ=1) AND(M=2) AND(T>29) THEN2500
2180 GOTO3000
2190 REM
2500 PRINT"DER";T;".";M;".";J;"IST UNZULAESSIG"
2510 PRINT
2520 GOTO2000
2530 REM
3000 REM ---BERECHNUNGS-MODUL---
3010 IFM>=3 THENV=INT((306*M-324)/10)
3020 IFM<3 THENV=(M-1)*31:SJ=0
3030 Z=SJ+(J-1)*365+INT((J-1)/4)
3040 Z=Z-INT((J-1)/100)+INT((J-1)/400)
3050 S=Z+V+T
3060 R=S-INT(S/7)*7+1
3070 REM
4000 REM ---AUSGABE-MODUL---
4010 PRINT
4020 PRINT"DER ";T;".";M;".";J;" IST EIN ";W$(R)
4030 END
30000 REM ES WIRD FUER EIN DATUM ZWISCHEN 1.1.1583
30010 REM UND 31.12.9999 DER WOCHENTAG BERECHNET
30020 REM

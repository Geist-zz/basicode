1000 A=500:GOTO20:REM JAHRESKALENDER
1010 REM
1020 REM ERSTELLT EINEN KALENDER ZWISCHEN 1901 UND 2099
1030 REM
1040 REM (C) HEIKE UND MANFRED FILLINGER , 12.12.83
1050 REM     UEBERARBEITET 6.5.1984
1060 REM      KONSTANTEN,
1070 R=6:REM LINKER RAND
1080 REM
1090 REM        VARIABLEN,
1100 REM        J.... GEWAEHLTES JAHR
1110 REM        BA... BASISABSTAND
1120 REM        FB... TAGESZAHL IM FEB.
1130 REM        WT... ERSTER WOCHENTAG IM JAHR
1140 REM        W.... BETRIEBSART
1150 REM        WM... GEWAEHLTER MONAT
1160 REM        WK$.. WOCHENKOPF
1170 REM        M$(). MONATSNAMEN
1180 REM        M().. TAGE JE MONAT
1190 REM        W().. 1. WOCHENTAG JE MONAT
1200 REM        W$... WAHL (1..12,E)
1220 REM        ES... TAGE BIS OSTERN
1230 REM        SW... TAGE BIS 4. ADVENT
1240 REM        A,B,C,D,E,EM,ET... HILFSVAR. OSTERFORMEL
1250 REM        H1,H2$,H3,H4... HILFSVAR. SORT.
1260 REM        SP$,TB,T1-3,W1-3... HILFSVAR AUSGABE
1270 REM        KT(),N$(),FM(),FT()... FEIERTAGS-REC.
1280 REM        KT,M,T,L,Z$,ZZ$,TB... UEBERGABEVAR.
1290 REM        I,II,J,JJ... LAUFVAR.
1300 REM        CN,CT,HO,VE,SR,SR$,IN$... BASICODE-2
1310 REM
1500 DIMM(12),M$(12),W(12),KT(20),N$(20),FM(20),FT(20)
1510 REM
1520 GOSUB100
2000 REM---EINGABE-MODUL---
2010 REM
2020 PRINT"BITTE EIN JAHR ZWISCHEN 1901 UND 2099":INPUTJ
2030 IF(J<1901) OR(J>2099) OR(INT(J)<>J) THEN2020
2040 PRINT
2050 PRINT"BILDSCHIRMAUSGABE.....:1"
2060 PRINT"DRUCKERAUSGABE........:2"
2070 INPUTW
2080 IF(W<>1) AND(W<>2) THEN2040
2090 IFW=2 THEN2120
2100 PRINT"BITTE EINEN MONAT WAEHLEN";:INPUTWM
2110 IF(INT(WM)<>WM) OR(WM<1) OR(WM>12) THEN2100
2120 PRINT:PRINT"BITTE ETWAS GEDULD"
2130 REM
2500 REM---KALENDER-MODUL---
2510 REM
2520 REM ERSTER WOCHENTAG IM JAHR
2530 REM ALS BASIS DIENT DIENSTAG DER 1.1.1901
2540 BA=INT((J-1901)*365.25)
2550 WT=BA-INT(BA/7)*7+1
2560 IFWT=7 THENWT=0:REM 0=MO...6=SO
2570 REM MENGE DER TAGE IM JAHR
2580 FB=29
2590 IF(J-INT(J/4)*4)<>0 THENFB=28
2600 REM MENGE DER TAGE JE MONAT,MONATSNAMEN
2610 M(1)=31:M$(1)="  JANUAR  "
2620 M(2)=FB:M$(2)=" FEBRUAR  "
2630 M(3)=31:M$(3)="  MAERZ   "
2640 M(4)=30:M$(4)="  APRIL   "
2650 M(5)=31:M$(5)="   MAI    "
2660 M(6)=30:M$(6)="   JUNI   "
2670 M(7)=31:M$(7)="   JULI   "
2680 M(8)=31:M$(8)="  AUGUST  "
2690 M(9)=30:M$(9)="SEPTEMBER "
2700 M(10)=31:M$(10)=" OKTOBER  "
2710 M(11)=30:M$(11)=" NOVEMBER "
2720 M(12)=31:M$(12)=" DEZEMBER "
2730 REM ERSTER WOCHENTAG JE MONAT
2740 W(1)=WT:II=WT
2750 FORI=1 TO11
2760 II=II+M(I)
2770 W(I+1)=II-INT(II/7)*7
2780 NEXTI
2790 REM WOCHENKOPF
2800 WK$=" MO DI MI DO FR SA SO"
2810 REM
3000 REM---FEIERTAGS-MODUL---
3010 REM
3020 REM OSTERREGEL NACH GAUSS
3030 A=J-INT(J/19)*19
3040 B=J-INT(J/4)*4
3050 C=J-INT(J/7)*7
3060 D=(19*A+24)-INT((19*A+24)/30)*30
3070 E=(2*B+4*C+6*D+5)-INT((2*B+4*C+6*D+5)/7)*7
3080 EM=4:ET=D+E-9:ES=FB+62+ET
3090 IF(22+D+E)<=31 THENEM=3:ET=22+D+E:ES=FB+31+ET
3100 REM AUSNAHMEN
3110 IF(EM=4) AND(ET=26) THENET=19:ES=FB+62+ET
3120 IF(EM=4) AND(ET=25) AND(D=28) AND(A>10) THENET=18
3125 ES=FB+62+ET
3130 REM 4. ADVENT (1.SONNTAG VOR WEIHNACHTEN)
3140 SW=(306+FB+25)
3145 SW=SW-(((306+FB-1+WT+25)-INT((306+FB-1+WT+25)/7)*7)+1)
3150 REM FESTE FEIERTAGE
3160 KT(1)=1:FM(1)=1:FT(1)=1:N$(1)="NEUJAHR :"
3170 KT(2)=6:FM(2)=1:FT(2)=6:N$(2)="HL. 3 KOENIGE :"
3180 KT(3)=FB+92+1:FM(3)=5:FT(3)=1:N$(3)="MAIFEIERTAG :"
3190 KT(4)=123+FB+17:FM(4)=6:FT(4)=17:N$(4)="GES.FEIERTAG:"
3200 KT(5)=FB+184+15:FM(5)=8:FT(5)=15
3205 N$(5)="MARIAE HIMMELF.:"
3210 KT(6)=FB+276+1:FM(6)=11:FT(6)=1:N$(6)="ALLERHEILIGEN :"
3220 KT(7)=FB+306+25:FM(7)=12:FT(7)=25
3225 N$(7)="1.WEIHNACHTST. :"
3230 KT(8)=FB+306+26:FM(8)=12:FT(8)=26
3235 N$(8)="2.WEIHNACHTST. :"
3240 REM BEWEGLICHE FEIERTAGE
3250 KT(9)=ES-6*7-4:N$(9)="ASCHERMITTWOCH :"
3260 KT=KT(9):GOSUB16000:FM(9)=M:FT(9)=T
3270 REM
3280 KT(10)=ES-2:N$(10)="KARFREITAG :"
3290 KT=KT(10):GOSUB16000:FM(10)=M:FT(10)=T
3300 REM
3310 KT(11)=ES:N$(11)="OSTERSONNTAG :"
3320 KT=KT(11):GOSUB16000:FM(11)=M:FT(11)=T
3330 REM
3340 KT(12)=ES+1:N$(12)="OSTERMONTAG :"
3350 KT=KT(12):GOSUB16000:FM(12)=M:FT(12)=T
3360 REM
3370 KT(13)=FB+92+14-((WT+FB+92)-INT((WT+FB+92)/7)*7)
3380 N$(13)="MUTTERTAG :"
3390 KT=KT(13):GOSUB16000:FM(13)=M:FT(13)=T
3400 REM
3410 KT(14)=ES+39:N$(14)="CHR. HIMMELF. :"
3420 KT=KT(14):GOSUB16000:FM(14)=M:FT(14)=T
3430 REM
3440 KT(15)=ES+49:N$(15)="PFINGSTSO. :"
3450 KT=KT(15):GOSUB16000:FM(15)=M:FT(15)=T
3460 REM
3470 KT(16)=ES+50:N$(16)="PFINGSTMO. :"
3480 KT=KT(16):GOSUB16000:FM(16)=M:FT(16)=T
3490 REM
3500 KT(17)=ES+60:N$(17)="FRONLEICHN. :"
3510 KT=KT(17):GOSUB16000:FM(17)=M:FT(17)=T
3520 REM
3530 KT(18)=SW-4*7-4:N$(18)="BUSS-TAG :"
3540 KT=KT(18):GOSUB16000:FM(18)=M:FT(18)=T
3550 REM
3560 KT(19)=SW-4*7:N$(19)="TOTENSONNTAG :"
3570 KT=KT(19):GOSUB16000:FM(19)=M:FT(19)=T
3580 REM
3590 KT(20)=SW-3*7:N$(20)="1.ADVENT :"
3600 KT=KT(20):GOSUB16000:FM(20)=M:FT(20)=T
3610 REM
3620 REM FEIERTAGS-RECORD NACH DATUM SORTIEREN
3630 FORI=2 TO20
3640 H1=KT(I)
3650 H2$=N$(I):H3=FM(I):H4=FT(I)
3660 JJ=I-1
3670 IFH1>=KT(JJ) THEN3720
3680 KT(JJ+1)=KT(JJ)
3690 N$(JJ+1)=N$(JJ):FM(JJ+1)=FM(JJ):FT(JJ+1)=FT(JJ)
3700 JJ=JJ-1
3710 IFJJ<>0 THEN3670
3720 KT(JJ+1)=H1
3730 N$(JJ+1)=H2$:FM(JJ+1)=H3:FT(JJ+1)=H4
3740 NEXTI
3750 REM
5000 REM---DISPLAY-MODUL---
5010 REM
5020 GOSUB100
5030 IFW=2 THEN6000:REM DRUCKER
5040 CT=3:CN=0:PRINT" ****** ";J;" ******"
5050 PRINT" -----";M$(WM);"-----"
5060 PRINTWK$
5075 W1=W(WM)
5080 FORI=1 TOM(WM)
5090 SR=I:GOSUB310
5100 PRINTTAB(W1*3+1);SR$;
5110 W1=W1+1
5120 IFW1=7 THENW1=0:PRINT
5130 NEXTI
5140 PRINT:PRINT
5150 PRINT" FEIERTAGE:"
5160 CT=2:CN=0
5170 FORI=1 TO20
5180 IFFM(I)=WM THENPRINT" ";N$(I);FT(I);".";FM(I);"."
5190 NEXTI
5195 HO=26:VE=15
5200 GOSUB110:PRINT"1..12 , E  :";:INPUTW$
5215 IFW$="E" THENEND
5220 WM=VAL(W$)
5240 IF(WM<1) OR(WM>12) OR(WM<>INT(WM)) THEN5200
5260 GOTO5000
6000 REM---KALENDER DRUCKEN---
6010 REM
6020 REM JAHRESKOPF DRUCKEN
6030 GOSUB360
6040 L=R:Z$=" ":GOSUB18000
6050 R$=ZZ$
6060 L=68:Z$="*":GOSUB18000
6070 SR$=R$+ZZ$
6080 GOSUB350:GOSUB360
6090 SR$=R$+"*":TB=67+R:GOSUB17000:SR$=SR$+"*"
6100 GOSUB350:GOSUB360
6110 SR$=R$+"*":TB=32+R:GOSUB17000
6120 SP$=SR$:SR=J:CT=4:CN=0:GOSUB310:SR$=SP$+SR$
6130 TB=67+R:GOSUB17000:SR$=SR$+"*"
6140 GOSUB350:GOSUB360
6150 SR$=R$+"*":TB=67+R:GOSUB17000:SR$=SR$+"*"
6160 GOSUB350:GOSUB360
6170 L=68:Z$="*":GOSUB18000
6180 SR$=R$+ZZ$
6190 GOSUB350:GOSUB360:GOSUB360:GOSUB360
6400 REM MONATE DRUCKEN
6410 CT=3:CN=0
6420 FORI=1 TO12 STEP3
6430 T1=1:T2=1:T3=1
6440 W1=W(I):W2=W(I+1):W3=W(I+2)
6450 L=R:Z$=" ":GOSUB18000
6460 SR$=ZZ$+" -----"+M$(I)+"-----   -----"+M$(I+1)
6470 SR$=SR$+"-----   -----"+M$(I+2)+"-----"
6480 GOSUB350:GOSUB360:GOSUB360
6490 SR$=ZZ$+WK$+"  "+WK$+"  "+WK$
6500 GOSUB350:GOSUB360
6550 GOSUB360:SR$=""
6560 IFT1>M(I) THEN6620
6570 TB=R+W1*3:GOSUB17000
6580 SP$=SR$:SR=T1:GOSUB310:SR$=SP$+SR$
6590 W1=W1+1:T1=T1+1
6600 IFW1=7 THENW1=0:GOTO6620
6610 GOTO6560
6620 IFT2>M(I+1) THEN6680
6630 TB=R+23+W2*3:GOSUB17000
6640 SP$=SR$:SR=T2:GOSUB310:SR$=SP$+SR$
6650 W2=W2+1:T2=T2+1
6660 IFW2=7 THENW2=0:GOTO6680
6670 GOTO6620
6680 IFT3>M(I+2) THEN6735
6690 TB=R+46+W3*3:GOSUB17000
6700 SP$=SR$:SR=T3:GOSUB310:SR$=SP$+SR$
6710 W3=W3+1:T3=T3+1
6720 IFW3=7 THENW3=0:GOTO6735
6730 GOTO6680
6735 GOSUB350
6740 IFNOT((T1>M(I)) AND(T2>M(I+1)) AND(T3>M(I+2))) THEN6550
6750 GOSUB360:GOSUB360
6760 NEXTI
6800 REM FEIERTAGE DRUCKEN
6810 GOSUB360
6820 SR$=R$+"FEIERTAGE :"
6830 GOSUB350:GOSUB360
6840 CT=2:CN=0
6850 FORI=1 TO20 STEP3
6870 SR=FT(I):GOSUB310
6880 SP$=R$+N$(I)+SR$+"."
6890 SR=FM(I):GOSUB310
6900 SP$=SP$+SR$+"."
6910 IF(I+1)>20 THEN6970
6920 SR$=SP$:TB=23+R:GOSUB17000:SP$=SR$
6930 SR=FT(I+1):GOSUB310
6940 SP$=SP$+N$(I+1)+SR$+"."
6950 SR=FM(I+1):GOSUB310
6960 SP$=SP$+SR$+"."
6970 IF(I+2)>20 THEN7030
6980 SR$=SP$:TB=46+R:GOSUB17000:SP$=SR$
6990 SR=FT(I+2):GOSUB310
7000 SP$=SP$+N$(I+2)+SR$+"."
7010 SR=FM(I+2):GOSUB310
7020 SP$=SP$+SR$+"."
7030 SR$=SP$
7040 GOSUB350:GOSUB360
7050 NEXTI
7060 REM
7070 END
16000 REM ERMITTELT AUS KALENDERTAG,MONAT UND TAG
16010 IFKT>(306+FB) THENM=12:T=KT-306-FB:RETURN
16020 IFKT>(276+FB) THENM=11:T=KT-276-FB:RETURN
16030 IFKT>(245+FB) THENM=10:T=KT-245-FB:RETURN
16040 IFKT>(215+FB) THENM=9:T=KT-215-FB:RETURN
16050 IFKT>(184+FB) THENM=8:T=KT-184-FB:RETURN
16060 IFKT>(153+FB) THENM=7:T=KT-153-FB:RETURN
16070 IFKT>(123+FB) THENM=6:T=KT-123-FB:RETURN
16080 IFKT>(92+FB) THENM=5:T=KT-92-FB:RETURN
16090 IFKT>(62+FB) THENM=4:T=KT-62-FB:RETURN
16100 IFKT>(31+FB) THENM=3:T=KT-31-FB:RETURN
16110 IFKT>31 THENM=2:T=KT-31:RETURN
16120 IFKT>0 THENM=1:T=KT:RETURN
16130 RETURN
16140 REM
17000 REM TAB-STEUERUNG
17010 IFTB<=LEN(SR$) THENRETURN
17020 L=TB-LEN(SR$):Z$=" "
17030 GOSUB18000
17040 SR$=SR$+ZZ$
17050 RETURN
18000 REM ZEICHENVERVIELFACHER
18010 ZZ$="":IFL<1 THENRETURN
18020 FORII=1 TOL
18030 ZZ$=ZZ$+Z$
18040 NEXTII
18050 RETURN
30000 REM---LITERATUR---
30010 REM
30020 REM OSTERFORMEL,
30030 REM MATHEMATISCHES HILFSBUCH FUER STUDIERENDE
30040 REM UND FREUNDE DER ASTRONOMIE
30050 REM VON WOLFGANG WEPPNER
30060 REM TREUGESELL-VERLAG
30070 REM SEITE 114-115
30080 REM
30090 REM SORTIEREN,
30100 REM MC-ZEITSCHRIFT
30110 REM MAI/JUNI 1981 SEITE 26
30120 REM
